/*-------------------------------------------------------------------*/
/*                                                                   */
/*  OR690CL   Print Transaction Audit Report.                        */
/*  12 April 2004                                                    */
/*  Max Blacknik                                                     */
/*                                                                   */
/*  Note: This program is called from OR646 and hence the option     */
/*        program is disabled.                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions                                                        */
/*                                                                   */
/*    04/12/04  MLB  4.17                                            */
/*      - Created.                                                   */
/*                                                                   */
/*    01/16/11  MLB  6.40                                            */
/*      - Fix: Revised pgm to add CLOF cmd for &file variable.       */
/*                                                                   */
/*    08/28/19  KDE  7.30                                            */
/*      - Fix: Revised pgm to delete open query override at end      */
/*             and reclaim all resources                             */
/*                                                                   */
/*-------------------------------------------------------------------*/

 OR690CL:    PGM        PARM(&CMD &WHSEA &RTID &RTE &TRN &TRUK &ZONE &DEV +
                          &FORM &RETURN)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(OR690CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(NONE)

  /* Report program and printer file name. */

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(PLTSUM3)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) VALUE(OR695PR)

  /* Informational message. */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Transaction Audit Report')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Printing Transaction Audit Report')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&CMD)        TYPE(*CHAR) LEN(8)
             DCL        VAR(&DEV)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RTE)        TYPE(*CHAR) LEN(5)
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&RTID)       TYPE(*CHAR) LEN(5)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TRN)        TYPE(*CHAR) LEN(7)
             DCL        VAR(&TRUK)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&WHSEA)      TYPE(*CHAR) LEN(3)
             DCL        VAR(&ZONE)       TYPE(*CHAR) LEN(6)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))
/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/


/*  Get report options. */

/*           CALL      PGM(&OPTPGM) PARM(&WHSEA &RTID &RETURN &DEV + */
/*                        &FORM)                                     */

/*  See if user wants to proceed. */

             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)

               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) +
                          MSGTYPE(*INFO)
               RETURN
               ENDDO

/*  Otherwise, run program. */

 REPORT:

   /* Override printer file. */

  /*         OVRPRTF    FILE(&PRTF) DEV(&DEV) OUTQ(*DEV) +
                          FORMTYPE(&FORM)                  */

   /* Create selection criteria string. */

             CHGVAR     VAR(&QRYSLT) VALUE('PSWHSE *EQ ' *CAT &WHSEA)

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PSRTID +
                          *EQ ' *CAT &QUOTE *CAT &RTID *CAT &QUOTE)

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PSTRN# *EQ' +
                           *BCAT &TRN)



   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE((&FILE)) QRYSLT(&QRYSLT) KEYFLD(*FILE) +
                          SEQONLY(*NO)

             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

             CALL       PGM(OR690) PARM(&CMD &WHSEA &RTID &RTE &TRUK +
                          &ZONE &DEV &FORM)
             CLOF       OPNID(&FILE)                              /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */

 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

   /* Clean up before leaving */

             DLTOVR     FILE(&PRTF)
             MONMSG     MSGID(CPF9841)
/* 7.30 A */ DLTOVR     FILE(&FILE)
/* 7.30 A */ MONMSG     MSGID(CPF9841)
/*           CLOF       OPNID(&FILE)       */
             MONMSG     MSGID(CPF4519)
             MONMSG     MSGID(CPF4520)
  /*  7.30 D RCLRSC     LVL(*CALLER)   */
/* 7.30 M */ RCLRSC

             ENDPGM
