/*-------------------------------------------------------------------*/
/*                                                                   */
/*  PO720CL   Export closed receiving P.O's                          */
/*  14 November, 1994                                                */
/*  Dave Sommerville                                                 */
/*  Hemant Kapadia                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions                                                        */
/*                                                                   */
/*    03/18/96  DAS  2.11                                            */
/*      - Added PO number check to OPNQRY statement.                 */
/*    01/27/97  RH   2.18                                            */
/*      - Change to use POHDR instead of POH file.                   */
/*    05/01/97  RH   2.20                                            */
/*      - Add status not open(0) to OPNQRY statement.                */
/*    05/01/97  RH   2.20                                            */
/*      - Change MSG command to MSGBFC.                              */
/*    06/19/97  HNK  2.22                                            */
/*      - Correct query select string. (PHSTAT *NE "0")              */
/*    03/09/99  HNK  4.11                                            */
/*      - Revised to work with FTP export.                           */
/*      - Added &FTP parameter to PO720 call.                        */
/*      - Used parameter to either call PO725 (&FTP=N) or            */
/*        PO726CL (&FTP=Y).                                          */
/*                                                                   */
/*    04/10/03  MLB  4.17                                            */
/*      - Enh: Added support to program to allow PO Exceptions       */
/*        to be exported back to host system using file EXPFPOE.     */
/*                                                                   */
/*    06/01/10  MLB  5.30                                            */
/*      - Enh: Added client custom code support to program.          */
/*                                                                   */
/*    03/03/11  MLB  6.40                                            */
/*      - Enh: Revised program to add support for calling staging    */
/*        export program.                                            */
/*                                                                   */
/*    10/22/20  RTR  7.40a                                           */
/*      - Enh: Added FTP type R to do same as S for Staging.         */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Client Custom Revisions: Coastal Sunbelt Produce                 */
/*                                                                   */
/*    06/01/10  MLB  CSP                                             */
/*      - Add user authorization check.                              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Client Custom Revisions: West Side Foods                         */
/*                                                                   */
/*    04/02/01  DAS  WS1                                             */
/*      - Revised to clear EXP files at end. Westside doesn't use    */
/*        exported PO info to update the host.                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Client Custom Revisions: Dairyland                               */
/*                                                                   */
/*    08/01/13  MLB  DRY                                             */
/*      - Revised program to call Staging Export interface pgm even  */
/*        though FTP Export option is set to Y=Yes. Client in        */
/*        process of migrating to new Host ERP and will use Staging  */
/*        Export interfaces going forward.                           */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Client Custom Revisions: Springfield Grocery                     */
/*                                                                   */
/*    09/19/17  MLB  SGC                                             */
/*      - Enh: Revised pgm to submit exports to jobq BATCH in        */
/*        library PWRUSER. Per Bill H. email.                        */
/*                                                                   */
/*-------------------------------------------------------------------*/

 PO720CL:    PGM        PARM(&WHSE)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

/* Client ID's                                                      (530 A)  */

/*CSP A*/    DCL        VAR(&COASTALSUN) TYPE(*CHAR) LEN(10) +
                          VALUE('COASTALSUN')
/*WS1 A*/    DCL        VAR(&WESTSIDE) TYPE(*CHAR) LEN(10) +
                          VALUE('WESTSIDE')
             DCL        VAR(&DAIRYLAND) TYPE(*CHAR) LEN(10) +
                          VALUE('DAIRYLAND')                      /* (DRY A) */
             DCL        VAR(&SGC)     TYPE(*CHAR) LEN(8) +
                          VALUE('SGC')                            /* (SGC A) */

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(PO720CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(8) +
                          VALUE(PO720)

  /* Main program. */

             DCL        VAR(&MAINPGM) TYPE(*CHAR) LEN(8) +
                          VALUE(PO725)

  /* Main FTP program. */

             DCL        VAR(&FTPPGM) TYPE(*CHAR) LEN(8) +
                          VALUE(PO726CL)
             DCL        VAR(&STGPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(PO726SCL)                         /* (640 A) */

  /* Informational messages */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Export Purchase Orders')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Exporting Purchase Orders.')
/*CSP A*/    DCL        VAR(&NOAUTHRZE) TYPE(*CHAR) LEN(60) +
                          VALUE('   You are not authorized to use +
                          this option.')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&FTP)        TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(10)      /* (SGC A) */
             DCL        VAR(&JOBQLIB)    TYPE(*CHAR) LEN(10)      /* (SGC A) */
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
/*530 A*/    DCL        VAR(&CLIENT)     TYPE(*CHAR) LEN(10)
/*530 A*/    DCL        VAR(&CLIENTLOC)  TYPE(*CHAR) LEN(10)
/*CSP A*/    DCL        VAR(&PGMOPT)     TYPE(*CHAR) LEN(20)
/*CSP A*/    DCL        VAR(&PRTN)       TYPE(*CHAR) LEN(8)
/*CSP A*/    DCL        VAR(&PUSR)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(GETOUT))

             CALL       PGM(GETCLIENT) PARM(&CLIENT &CLIENTLOC)   /* (530 A) */

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

/* Don't get options if being run from JOBQ. */

             RTVJOBA    TYPE(&TYPE)

/*  Check if user is authorized to use this option                   (CSP A) */

/*CSP A*/    IF         COND(&CLIENT *EQ &COASTALSUN)  THEN(DO)
/*CSP A*/    RTVJOBA    USER(&PUSR)
/*CSP A*/    CALL       PGM(CHKLIST) PARM(&SBMPGM &PGMOPT &PUSR &PRTN)
/*CSP A*/    IF         COND(&PRTN *EQ '*NO')  THEN(DO)
/*CSP A*/               SNDPGMMSG  MSG(&NOAUTHRZE)   +
/*CSP A*/               MSGTYPE(*INFO)
/*CSP A*/    RETURN
/*CSP A*/    ENDDO
/*CSP A*/    ENDDO
/*CSP A*/
             IF (&TYPE = '0') THEN(GOTO MAIN)

/*  Get report options. */

             CALL       PGM(&OPTPGM) PARM(&RETURN &WHSE &FTP)

/*  See if user wants to proceed. */

             IF         COND((&RETURN *EQ '*CANCEL') *OR (&RETURN +
                          *EQ '*EXIT')) THEN(DO)
               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) MSGTYPE(*INFO)
               RETURN
             ENDDO

/*  See if user wants to put report on JOBQ. */

     /* 411 A  Use FTP option to call appropriate program */

             IF (&RETURN *EQ '*JOBQ') THEN(DO)
               SELECT                                             /* (640 A) */
/*             IF (&FTP = 'Y') THEN(DO)                              (640 D) */
               when (&FTP = 'Y') THEN(DO)                         /* (640 M) */
                 IF         COND(&CLIENT = &DAIRYLAND) THEN(DO)
                   SBMJOB     CMD(CALL PGM(&STGPGM) PARM(&WHSE)) +
                                JOB(&STGPGM)                      /* (DRY A) */
                 ENDDO                                            /* (DRY A) */
                 ELSE       CMD(DO)                               /* (DRY A) */

                 select                                           /* (SGC A) */
                 when       COND(&CLIENT = &SGC) THEN(DO)         /* (SGC A) */
                   CHGVAR     VAR(&JOBQ) VALUE('BATCH')           /* (SGC A) */
                   CHGVAR     VAR(&JOBQLIB) VALUE('PWRUSER')      /* (SGC A) */
                   SBMJOB  CMD(CALL PGM(&FTPPGM) PARM(&WHSE)) JOB(&FTPPGM) +
                           jobq(&jobqlib/&jobq)                   /* (SGC A) */
                   GOTO    CMDLBL(GETOUT)                         /* (SGC A) */
                 enddo                                            /* (SGC A) */
                 OTHERWISE  CMD(DO)                               /* (SGC A) */

                 SBMJOB  CMD(CALL PGM(&FTPPGM) PARM(&WHSE)) JOB(&FTPPGM)
                 ENDDO                                            /* (SGC A) */
                 ENDSELECT                                        /* (SGC A) */
                 ENDDO                                            /* (DRY A) */
               ENDDO

/*             Staging File Export method in use.                    (640 A) */
          /*   when (&FTP = 'S') THEN(DO)                            (740aD) */
               WHEN  COND(&FTP = 'S' *OR &FTP = 'R') THEN(DO)     /* (740aM) */
                 SBMJOB     CMD(CALL PGM(&STGPGM) PARM(&WHSE)) +
                              JOB(&STGPGM)                        /* (640 A) */
               Enddo                                              /* (640 A) */

/*             IF (&FTP *NE 'Y') THEN(DO)                            (640 D) */
               OTHERWISE  CMD(DO)                                 /* (640 M) */
                 SBMJOB  CMD(CALL PGM(&SBMPGM) PARM(&WHSE)) JOB(&SBMPGM)
               ENDDO
               ENDSELECT                                          /* (640 A) */
               GOTO    CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

     /* 411 A  FTP uses a different program */

             SELECT                                               /* (640 A) */
/*           IF (&FTP = 'Y') THEN(DO)                                (640 D) */
             When (&FTP = 'Y') THEN(DO)                           /* (640 M) */
                 IF         COND(&CLIENT = &DAIRYLAND) THEN(DO)
                   CALL PGM(&STGPGM) PARM(&WHSE)                  /* (DRY A) */
                 ENDDO                                            /* (DRY A) */
                 ELSE       CMD(DO)                               /* (DRY A) */
               CALL PGM(&FTPPGM) PARM(&WHSE)
                 ENDDO                                            /* (DRY A) */
               GOTO GETOUT
             ENDDO

/*             Staging File Export method in use.                    (640 A) */
           /*  WHEN       COND(&FTP = 'S') THEN(DO)               /* (740aD) */
               WHEN  COND(&FTP = 'S' *OR &FTP = 'R') THEN(DO)     /* (740aM) */
                 CALL PGM(&STGPGM) PARM(&WHSE)                    /* (640 A) */
                 GOTO GETOUT                                      /* (640 A) */
               Enddo                                              /* (640 A) */
               ENDSELECT                                          /* (640 A) */

 MAIN:
             CHKOBJ     OBJ(EXPFPOE) OBJTYPE(*FILE) MBR(EXPFPOE)    /* (417)  */

/*           Member not found in file. Add member to proceed.          (417)  */

             MONMSG     MSGID(CPF9801 CPF9815) EXEC(DO)             /* (417)  */
               ADDPFM     FILE(EXPFPOE) MBR(EXPFPOE) TEXT('PO +
                            Receiving Exceptions - Export')         /* (417)  */
             ENDDO                                                  /* (417)  */

/* Use OPNQRYF to select and order the records we need */

             MSGBFC     MESSAGE('Selecting records to export')

             OVRDBF     FILE(POHDR) SHARE(*YES)
             CHGVAR     VAR(&QRYSLT) VALUE('(PHWHSE *EQ' *BCAT &WHSE +
                          *CAT ') *AND (PHUDTE *EQ 0) *AND +
                          (PHSTAT *NE "0") *AND +
                          (%SST(PHPO 8 2) *NE' *BCAT &QUOTE *CAT +
                          'TF' *CAT &QUOTE *CAT ')')
             OPNQRYF    FILE((POHDR)) OPTION(*ALL) QRYSLT(&QRYSLT) +
                          KEYFLD((PHWHSE) (PHPO))

/***** Unconditionally clear EXP files for Westside  WS1  *****/

/*WS1 A*/    IF         COND(&CLIENT *EQ &WESTSIDE)  THEN(DO)
             CLRPFM     FILE(EXPPOH)               /* WS1 A */
             CLRPFM     FILE(EXPPOD)               /* WS1 A */
/*WS1 A*/    ENDDO

             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

             CALL       PGM(&MAINPGM)

/*  Receive any messages and pass them on  */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
        /*   IF         COND(&SEV *LT 09) THEN(GOTO CMDLBL(NEXTMSG)) */
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                          MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

/*  Clean up before leaving */

             DLTOVR     FILE(*ALL)
             RCLRSC     LVL(*CALLER)

             ENDPGM
