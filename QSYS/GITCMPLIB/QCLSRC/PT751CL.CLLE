/*-------------------------------------------------------------------*/
/*                                                                   */
/*  PT751CL   Staging Export - Closed Putaway/Replen/Transfers       */
/*  10 December 2013                                                 */
/*  Rod Roller                                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions:                                                       */
/*                                                                   */
/*    12/10/13  RTR  6.50                                            */
/*      - Created from PT741CL                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Notes:                                                           */
/*                                                                   */
/*    This program is the same as PT731CL except that the file       */
/*    used for export has all zoned decimal fields instead of        */
/*    packed decimal fields in EXPFTRN. Also, the file includes      */
/*    the To Slot license plate for each warehouse activity.         */
/*                                                                   */
/*    Revised to clear export members first. Re: can't clear         */
/*    them at end of program because they still need to be           */
/*    retrieved using ftp by the host ftp script.                    */
/*                                                                   */
/*    &GTRN is used to re-export transactions with the trans group #.*/
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Parameters:                                                      */
/*                                                                   */
/*    &WHSE   -  Warehouse                                           */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Member name notes                                                */
/*                                                                   */
/*   - The first character of the member name must be an alpha       */
/*     character.                                                    */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Notes:                                                           */
/*                                                                   */
/*-------------------------------------------------------------------*/

 PT751CL:    PGM        PARM(&WHSE)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

/* Client ID's  */

             DCL        VAR(&SGC) TYPE(*CHAR) LEN(10) +
                          VALUE('SGC       ')
             DCL        VAR(&DAIRYLAND) TYPE(*CHAR) LEN(10) +
                          VALUE('DAIRYLAND ')

/* Export program.    */

             DCL        VAR(&EXPPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(PT751S)
/* RE-Export program. */

             DCL        VAR(&REXPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(PT751B)

/* Message queues */

             DCL        VAR(&FTPMSGQ) TYPE(*CHAR) LEN(10) +
                          VALUE('EXPTRN')

             DCL        VAR(&FTPERRMSGQ) TYPE(*CHAR) LEN(10) +
                          VALUE('EXPTRNERR')

/* Import files */

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&CLIENT)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&CLIENTLOC)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERRCNT)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&FINISHED)   TYPE(*LGL)
             DCL        VAR(&JOB)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&SAVMBR)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGKEY)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&NXTNUM)     TYPE(*DEC)  LEN(7)
             DCL        VAR(&NXTNUMA)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&GTRN)       TYPE(*CHAR) LEN(7)

             DCL        VAR(&PSTAT) TYPE(*CHAR) LEN(1) VALUE('4')
             DCL        VAR(&PTRTN) TYPE(*CHAR) LEN(6) VALUE('PUTRTN')
             DCL        VAR(&PTEMP) TYPE(*CHAR) LEN(6) VALUE('PUTEMP')
             DCL        VAR(&PREPL) TYPE(*CHAR) LEN(6) VALUE('REPL  ')
             DCL        VAR(&PPLBK) TYPE(*CHAR) LEN(6) VALUE('REPLBK')
             DCL        VAR(&PPLTF) TYPE(*CHAR) LEN(6) VALUE('REPLTF')
             DCL        VAR(&PTFR)  TYPE(*CHAR) LEN(6) VALUE('TFRMOV')


             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(GETOUT))

             CALL       PGM(GETCLIENT) PARM(&CLIENT &CLIENTLOC)

             CHGVAR     VAR(&ERRCNT) VALUE(0)
             CHGVAR     VAR(&FINISHED) VALUE('0')

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/
/* Get job information */

             RTVJOBA    JOB(&JOB) USER(&USER) TYPE(&TYPE)

/* Use OPNQRYF to select and order the records we need */

             MSGBFC     MESSAGE('Selecting records to export')

/* Select for EXPORT                                   */

             IF (&GTRN =' ') DO

   /* Create selection criteria string. */
             IF         COND(&CLIENT *EQ &SGC *OR &CLIENT *EQ +
                          &DAIRYLAND) THEN(DO)

               CHGVAR     VAR(&QRYSLT) VALUE('(PTTASK *EQ ' *CAT &QUOTE +
                            *CAT &PTRTN *CAT &QUOTE)

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTTASK +
                            *EQ ' *BCAT &QUOTE *CAT &PTEMP *CAT &QUOTE)

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTTASK +
                            *EQ ' *BCAT &QUOTE *CAT &PREPL *CAT &QUOTE)

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTTASK +
                            *EQ ' *BCAT &QUOTE *CAT &PPLBK *CAT &QUOTE)

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTTASK +
                            *EQ ' *BCAT &QUOTE *CAT &PPLTF *CAT &QUOTE)

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTTASK +
                            *EQ ' *BCAT &QUOTE *CAT &PTFR *CAT &QUOTE *CAT ')')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')

               CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'PTSTAT  +
                            *EQ ' *BCAT &QUOTE *CAT &PSTAT *CAT &QUOTE)
             ENDDO

             ELSE       CMD(DO)

               CHGVAR     VAR(&QRYSLT) VALUE('PTSTAT *EQ "4"')
             ENDDO

               OVRDBF     FILE(PT751) TOFILE(PIRTRAN) SHARE(*YES)
               OPNQRYF    FILE((PIRTRAN) (FLGTRAN)) +
                            FORMAT(*LIBL/PT751) QRYSLT(&QRYSLT) +
                            KEYFLD((PTTRN#)) JFLD((PIRTRAN/PTWHSE +
                            FLGTRAN/FGWHSE) (PIRTRAN/PTTRN# +
                            FLGTRAN/FGTRN# *EQ)) JDFTVAL(*ONLYDFT)
             ENDDO

/* Select for Re-Export                                */

             IF (&GTRN >' ') DO
               OVRDBF     FILE(PT751) TOFILE(PIRTRAN) SHARE(*YES)
               CHGVAR     VAR(&QRYSLT) VALUE('FGGTRN *EQ ' *CAT &GTRN)
               OPNQRYF    FILE((PIRTRAN) (FLGTRAN)) +
                            FORMAT(*LIBL/PT751) QRYSLT(&QRYSLT) +
                            KEYFLD((PTTRN#)) JFLD((PIRTRAN/PTTRN# +
                            FLGTRAN/FGTRN# *EQ))
             ENDDO

/*  Call program to move records to export files */

             IF (&GTRN = ' ') DO
               CALL       PGM(&EXPPGM) PARM(&WHSE)
             ENDDO

             IF (&GTRN > ' ') DO
               CALL       PGM(&REXPGM) PARM(&WHSE)
             ENDDO

/* We finished without error */

             CHGVAR     VAR(&FINISHED) VALUE('1')

/*  Clean up before leaving */

             DLTOVR     FILE(*ALL)

/*  Send Successful message */

             SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Export successful for EXPSTRN.') +
                          TOMSGQ(&FTPMSGQ) MSGTYPE(*INFO)

             GOTO BOTTOM

/*  Receive any messages and pass them on  */

 GETOUT:

             CHGVAR     VAR(&ERRCNT) VALUE(&ERRCNT + 1)
             IF COND(&ERRCNT = 3) THEN(GOTO CMDLBL(BOTTOM))

             IF COND(&FINISHED = '0') THEN(DO)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Export FAILED, see following +
                          messages.') +
                          TOMSGQ(&FTPMSGQ) MSGTYPE(*INFO)

             SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Export FAILED, see following +
                          messages.') +
                          TOMSGQ(&FTPERRMSGQ) MSGTYPE(*INFO)

             ENDDO

 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))

               SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                     MSGDTA(&MSGDTA) TOMSGQ(&FTPMSGQ) MSGTYPE(*COMP)

             IF COND(&FINISHED = '0') THEN(DO)
               SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                          MSGDTA(&MSGDTA) TOMSGQ(&FTPERRMSGQ) +
                          MSGTYPE(*COMP)
             ENDDO

             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

 BOTTOM:

               RCLRSC     LVL(*CALLER)

        /*  Send '*Escape' message to caller */

               IF COND(&FINISHED = '0') THEN(DO)
                 SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Error Occured Processing EXPSTRN.') +
                          MSGTYPE(*ESCAPE)
               ENDDO

             ENDPGM
