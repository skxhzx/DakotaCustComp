/*-------------------------------------------------------------------*/
/*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved     */
/*   BFC Software, Inc.                                              */
/*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                          */
/*   West Chicago, IL  60185                                         */
/*   (630) 562-0375                                                  */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*  PI260CL   Import FTP P.O.'s                                      */
/*  29 July 2000                                                     */
/*  Kenneth Elder                                                    */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions:                                                       */
/*                                                                   */
/*    Rev 4.14  07/29/00  KDE                                        */
/*      - Copy of PI250CL.  Changed to call PI26* versus PI25*       */
/*                                                                   */
/*  03/18/04  MLB  4.17                                              */
/*    - Add code to override to new exception printer file           */
/*      PI2651PR.                                                    */
/*                                                                   */
/*  06/08/06  MLB  5.10                                              */
/*    - Enh: Add file overrides for new Import P/O Header Misc.      */
/*      file, IMPFPOHM.                                              */
/*                                                                   */
/*  10/30/08  MLB  5.20                                              */
/*    - Fix: Remove prtf override for PI2651PR. Override for except- */
/*      ions prtf, is handled in pgm PI265.                          */
/*  12/11/08  MLB  5.20a                                             */
/*    - Enh: Add file overrides for new Import P/O Detail Misc.      */
/*      file, IMPFPODM.                                              */
/*                                                                   */
/*  05/25/12  MLB  6.40                                              */
/*    - Fix: Revised program to add DLTOVR *ALL and then CLOF cmds   */
/*      for each of the import files.                                */
/*                                                                   */
/*   04/22/19  RTR  7.30a                                            */
/*     - Enh: Save member as 'LAST' for debugging.                   */
/*                                                                   */
/*   08/16/21  MLB  7.50                                             */
/*     - Fix: Added MONMSG to CPYF cmds for when file is empty.      */
/*       SBR was sending header file data to update Delivery Date/   */
/*       time, but no p/o detail data. This is a fix to the v7.30a   */
/*       mod.                                                        */
 /*  09/22/21  MLB  7.50a                                            */
 /*    - Fix: Revise pgm to add ovrdbf for IMPSPOH, IMPSPOD with     */
 /*           FRCRATIO(1) to ensure that rcds are written to files.  */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Parameters:                                                      */
/*                                                                   */
/*    &MBR   -  Member to create. Must start with alpha character.   */
/*    &RUN   -  *INTER  Run interactively                            */
/*              *JOBQ   Run on Jobq                                  */
/*    &OUTQ  -  Printer output queue (Default is *JOB)               */
/*    &FORM  -  Printer form (Default is *STD)                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  FTP command sequence                                             */
/*                                                                   */
/*    PUT localpoh PIRFTPF/IMPFPOH.P123456789                        */
/*    PUT localpod PIRFTPF/IMPFPOD.P123456789                        */
/*    PUT localpohm PIRFTPF/IMPFPOHM.P123456789              (510 A) */
/*    PUT localpodm PIRFTPF/IMPFPODM.P123456789              (520aA) */
/*    QUOTE RCMD PIRFTP/FTPLIBL *PROD                                */
/*    QUOTE RCMD PI250CL (P123456789 *INTER *JOB *STD)               */
/*                                                                   */
/*     where P123456789 is the unique member name                    */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Member name notes                                                */
/*                                                                   */
/*   - Member name must be unique for each PUT operation.  If not,   */
/*     PUT will delete any existing information and replace it       */
/*     with the new information.                                     */
/*                                                                   */
/*   - The first character of the member name must be an alpha       */
/*     character.                                                    */
/*                                                                   */
/*   - Do not use date and/or time to make the member name unique.   */
/*     There is always a possibility that two terminals processing   */
/*     will call orders will generate the same date/time.            */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Notes:                                                           */
/*                                                                   */
/*   A single member should only contain P.O.'s for a single         */
/*   warehouse.                                                      */
/*                                                                   */
/*   FTP orders are sent into a special FTP import file.  Unlike     */
/*   the normal import where everything goes into a single member,   */
/*   the FTP import files contain multiple members.  The member      */
/*   that is used for a particular session is sent in as a           */
/*   parameter.                                                      */
/*                                                                   */
/*   This is designed to be called from the command line or as a     */
/*   remote command using the FTP Quote command. When called from    */
/*   the command line the user will be prompted for the necessary    */
/*   parameters.  When called as a remote command, the necessary     */
/*   parameters are passed in.                                       */
/*                                                                   */
/*   Normally this program is called as a remote FTP command after   */
/*   the data has been sent.  The option to call this from the       */
/*   command line is mainly for testing and the instances when       */
/*   something goes wrong with the import process.                   */
/*                                                                   */
/*   When called as a remote FTP command the job 'Type' is '0'       */
/*   which means it looks like a batch job.                          */
/*                                                                   */
/*-------------------------------------------------------------------*/

 PI260CL:    PGM        PARM(&MBR &RUN &OUTQ &FORM)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(PI260CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(8) +
                          VALUE(PI260)

  /* Report program and printer file names. */

             DCL        VAR(&RPTPGM) TYPE(*CHAR) LEN(8) +
                          VALUE(PI265)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) +
                          VALUE(PI265PR)
             DCL        VAR(&PRTF2) TYPE(*CHAR) LEN(10) +
                          VALUE(PI2651PR) /* (417) */

  /* Informational messages */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Import FTP purchase orders')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Importing FTP purchase orders')

  /* Messag queues */

             DCL        VAR(&FTPMSGQ) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPPO')
             DCL        VAR(&FTPERRMSGQ) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPPOERR')

  /* Import files */

             DCL        VAR(&FILEH) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPFPOH2')
             DCL        VAR(&FILEHM) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPFPOHM')                       /* (510 A) */
             DCL        VAR(&FILED) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPFPOD2')
             DCL        VAR(&FILEDM) TYPE(*CHAR) LEN(10) +
                          VALUE('IMPFPODM')                       /* (520aA) */

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&ERRCNT)     TYPE(*DEC)  LEN(1 0)
             DCL        VAR(&EXIST)      TYPE(*LGL)
             DCL        VAR(&FINISHED)   TYPE(*LGL)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOB)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&OUTQ)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&RUN)        TYPE(*CHAR) LEN(8)
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&WHSE)       TYPE(*DEC)  LEN(15 5)

             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(GETOUT))
             CHGVAR     VAR(&ERRCNT) VALUE(0)
             CHGVAR     VAR(&EXIST) VALUE('0')
             CHGVAR     VAR(&FINISHED) VALUE('0')

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

/* Get job information */

             RTVJOBA    JOB(&JOB) USER(&USER) TYPE(&TYPE)

/* See if user wants to run from Jobq. */

             IF (&RUN = '*JOBQ') THEN(DO)
               CHGVAR VAR(&RETURN) VALUE('*JOBQ')
               GOTO CMDLBL(SKIP1)
             ENDDO

/* Don't get options if being run from JOBQ. */

             IF ((&TYPE = '0') *OR (&MBR *NE ' ')) THEN(GOTO REPORT)

/*  Get report options. */

             CALL       PGM(&OPTPGM) PARM(&RETURN &MBR &OUTQ &FORM)

/*  See if user wants to proceed. */

             IF         COND((&RETURN *EQ '*CANCEL') *OR (&RETURN +
                          *EQ '*EXIT')) THEN(DO)
               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) MSGTYPE(*INFO)
               RETURN
             ENDDO

 SKIP1:

/*  Make sure members exist                                          */

             CHKOBJ     OBJ(&FILEH) OBJTYPE(*FILE) MBR(&MBR)
             CHKOBJ     OBJ(&FILED) OBJTYPE(*FILE) MBR(&MBR)
             CHGVAR     VAR(&EXIST) VALUE('1')

/*  See if user wants to put report on JOBQ. */

             IF (&RETURN *EQ '*JOBQ') THEN(DO)
               SBMJOB  CMD(CALL PGM(&SBMPGM) PARM(&MBR '*JOBQ2' +
                          &OUTQ &FORM)) JOB(&SBMPGM)
               RMVMSG     CLEAR(*ALL)
               CHGVAR  VAR(&FINISHED) VALUE('1')
               GOTO    CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

 REPORT:

/*  Make sure members exist                                          */

             CHKOBJ     OBJ(&FILEH) OBJTYPE(*FILE) MBR(&MBR)
             CHKOBJ     OBJ(&FILED) OBJTYPE(*FILE) MBR(&MBR)
             CHGVAR     VAR(&EXIST) VALUE('1')

/*  Make copy of member                                          730a*/

             CPYF       FROMFILE(&FILEH) TOFILE(&FILEH) FROMMBR(&MBR) +
                          TOMBR(LAST) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2869 CPF2817)                    /* (750 A) */
             CPYF       FROMFILE(&FILED) TOFILE(&FILED) FROMMBR(&MBR) +
                          TOMBR(LAST) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2869 CPF2817)                    /* (750 A) */

/* Make sure &OUTQ and &FORM are not blank */

               IF (&OUTQ = ' ') +
                 THEN(CHGVAR VAR(&OUTQ) VALUE('*JOB'))
               IF (&FORM = ' ') +
                 THEN(CHGVAR VAR(&FORM) VALUE('*STD'))

/* Clear any existing messages and display working message. */

             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

/* Override printer file */

             OVRPRTF    FILE(&PRTF) OUTQ(&OUTQ) FORMTYPE(&FORM)
/*           OVRPRTF    FILE(&PRTF2) OUTQ(&OUTQ) FORMTYPE(&FORM) /* (520 D) */

/* Override import files to correct member */

             OVRDBF     FILE(&FILEH) TOFILE(*FILE) MBR(&MBR)
             OVRDBF     FILE(&FILEHM) TOFILE(*FILE) MBR(&MBR)     /* (510 A) */
             OVRDBF     FILE(&FILED) TOFILE(*FILE) MBR(&MBR)
             OVRDBF     FILE(&FILEDM) TOFILE(*FILE) MBR(&MBR)     /* (520aA) */
             OVRDBF     FILE(IMPSPOH) TOFILE(*FILE) frcratio(1)   /* (750aA) */
             OVRDBF     FILE(IMPSPOD) TOFILE(*FILE) frcratio(1)   /* (750aA) */

/* Import orders */

             CALL       PGM(&RPTPGM)

             CHGVAR     VAR(&FINISHED) VALUE('1')

/*  Clean up before leaving */

             DLTOVR     FILE(*ALL)
                                                                  /* (640 A) */
             CLOF       OPNID(&FILEH)                             /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */
             CLOF       OPNID(&FILEHM)                            /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */
             CLOF       OPNID(&FILED)                             /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */
             CLOF       OPNID(&FILEDM)                            /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */
             RMVM       FILE(&FILEH) MBR(&MBR)
             RMVM       FILE(&FILEHM) MBR(&MBR)                   /* (510 A) */
             MONMSG     MSGID(CPF320A CPF320B CPF3203 CPF3220 +
                          CPF3273 CPF7301 CPF7310)                /* (510 A) */
             RMVM       FILE(&FILED) MBR(&MBR)
             RMVM       FILE(&FILEDM) MBR(&MBR)                   /* (520aA) */
             MONMSG     MSGID(CPF320A CPF320B CPF3203 CPF3220 +
                          CPF3273 CPF7301 CPF7310)                /* (520aA) */
             RMVMSG     CLEAR(*ALL)

/*  Send 'Successful message */

             IF COND(&FINISHED = '1') THEN(DO)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Import successful for' *BCAT &MBR +
                          *TCAT '/' *CAT &RUN *TCAT '/' *CAT &JOB +
                          *TCAT '/' *CAT &USER) TOMSGQ(&FTPMSGQ) +
                          MSGTYPE(*INFO)
             ENDDO

/*  Receive any messages and pass them on  */

 GETOUT:

             CHGVAR     VAR(&ERRCNT) VALUE(&ERRCNT + 1)
             IF COND(&ERRCNT = 3) THEN(GOTO CMDLBL(BOTTOM))

             IF COND(&FINISHED = '0') THEN(DO)
               SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Import FAILED, see following +
                          message(s):' *BCAT &MBR *TCAT '/' *CAT +
                          &RUN *TCAT '/' *CAT &JOB *TCAT '/' *CAT +
                          &USER) TOMSGQ(&FTPMSGQ) MSGTYPE(*INFO)
               SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Import FAILED, see following +
                          message(s):' *BCAT &MBR *TCAT '/' *CAT +
                          &RUN *TCAT '/' *CAT &JOB *TCAT '/' *CAT +
                          &USER) TOMSGQ(&FTPERRMSGQ) MSGTYPE(*INFO)
             ENDDO

 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))

             IF COND(&TYPE = '1') THEN(DO)
               SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                     MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             ENDDO

               SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                     MSGDTA(&MSGDTA) TOMSGQ(&FTPMSGQ) MSGTYPE(*COMP)

             IF COND(&FINISHED = '0') THEN(DO)
               SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                          MSGDTA(&MSGDTA) TOMSGQ(&FTPERRMSGQ) +
                          MSGTYPE(*COMP)
             ENDDO

             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

 BOTTOM:

               RCLRSC     LVL(*CALLER)

/*  Send '*Escape' message to caller */

             IF COND(&FINISHED = '0') THEN(DO)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QSYS/QCPFMSG) +
                          MSGDTA('Error Occured Processing Member' +
                          *BCAT &MBR) MSGTYPE(*ESCAPE)
             ENDDO

             ENDPGM
