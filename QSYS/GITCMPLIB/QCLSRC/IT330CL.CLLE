/*-------------------------------------------------------------------*/
/*                                                                   */
/*  IT330CL   Warehouse book by slot                                 */
/*  26 August, 1994                                                  */
/*  Hemant Kapadia                                                   */
/*                                                                   */
/*  Revisions                                                        */
/*                                                                   */
/*    02/20/95  DAS                                                  */
/*      - Revised OPNQRYF to include the warehouse along with the    */
/*        item as join fields.                                       */
/*                                                                   */
/*    05/01/97  RH   2.20                                            */
/*      - Change MSG command to MSGBFC.                              */
/*                                                                   */
/*    08/20/99  HNK  4.12                                            */
/*      - Slot side option added                                     */
/*                                                                   */
/*    07/20/00  HNK  4.14                                            */
/*      - Slot type option added. (P=Pick only, O=Overflow only)     */
/*                                                                   */
/*    03/31/09  CWM  5.20A                                           */
/*      - Add support for new Report Distributution                  */
/*                                                                   */
/*-------------------------------------------------------------------*/

 IT330CL:    PGM        PARM(&WHSE &WHDP &RETURN &DEV &FORM)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT330CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT330)

  /* Report program and printer file name. */

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(PIRITEM)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) VALUE(IT331PR)

  /* Informational message. */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Warehouse book by slot')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Printing Warehouse book by slot')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&DEV)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHDP)       TYPE(*CHAR) LEN(5)
             DCL        VAR(&SECTN)      TYPE(*CHAR) LEN(1)
     /* 412 Begin */
             DCL        VAR(&SIDE)       TYPE(*CHAR) LEN(1)
     /* 412 End   */
     /* 414 Begin */
             DCL        VAR(&PICK)       TYPE(*CHAR) LEN(1)
     /* 414 End   */
             DCL        VAR(&VP)         TYPE(*CHAR) LEN(1)

/* 520aA */  DCL        VAR(&CANCEL) TYPE(*CHAR) LEN(8)
/* 520aA */  DCL        VAR(&CMD)    TYPE(*CHAR) LEN(1024)
/* 520aA */  DCL        VAR(&LEN)    TYPE(*DEC)  LEN(15 5) VALUE(1024)
/* 520aA */  DCL        VAR(&PARMS)  TYPE(*CHAR) LEN(80)
/* 520aA */  DCL        VAR(&POVR)   TYPE(*CHAR) LEN(1)
/* 520aA */  DCL        VAR(&PRTCTR) TYPE(*DEC)  LEN(15 5) VALUE(1)
/* 520aA */  DCL        VAR(&ONETIM) TYPE(*CHAR) LEN(1)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))
/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

START:

/*  Don't get options, if being run from JOBQ. */

             RTVJOBA    TYPE(&TYPE)
             IF (&TYPE = '0') THEN(GOTO REPORT)

/*  Get report options. */

     /* 412 Begin */ /* 414 Begin */
/* 520AD     CALL       PGM(&OPTPGM) PARM(&WHSE &WHDP &SECTN &PICK +
                          &SIDE &RETURN &VP &JOBQ &DEV &FORM) */
/* 520AA */  CALL       PGM(&OPTPGM) PARM(&WHSE &WHDP &SECTN &PICK +
                          &SIDE &RETURN &VP &JOBQ &POVR)
     /* 412 End   */ /* 414 Begin */

/*  See if user wants to proceed. */

             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)

               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) +
                          MSGTYPE(*INFO)
               RETURN
               ENDDO


/* See if user wants to put report on jobq. */

             IF (&JOBQ = 'Y') THEN(DO)
             SBMJOB     CMD(CALL PGM(&SBMPGM) PARM(&WHSE &WHDP +
                          &RETURN &DEV &FORM)) JOB(&SBMPGM)
             GOTO       CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

 REPORT:

   /* Override printer file. */

 /* 520D     OVRPRTF    FILE(&PRTF) DEV(&DEV) OUTQ(*DEV) +
                          FORMTYPE(&FORM)           */

/*       Process Override Print File Flag     */

 /*520AA*/   IF (&POVR = 'Y') THEN(DO)

 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 1 8)) VALUE('*ADD    ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 9 10)) VALUE(&SBMPGM)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 19 10)) VALUE('IT331PR   ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 29 3)) VALUE('000')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 32 3)) VALUE(&WHSE)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 35 45)) VALUE(' ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 68 1)) VALUE('Y')
 /*520AA*/   CALL       PGM(RP152) PARM(&PARMS)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(%SST(&PARMS 69 8))
 /*520AA*/   IF         COND(&CANCEL *EQ '*CANCEL ') THEN(DO)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(' ')
 /*520AA*/   GOTO       CMDLBL(START)
 /*520AA*/   ENDDO
 /*520AA*/   ENDDO

/*520AA*/    DOUNTIL    COND(&PRTCTR *EQ 0)
/*520AA*/    CALL       PGM(RPT120) PARM(&PRTF &PRTCTR &CMD &ONETIM)
/*520AA*/    IF         COND(&PRTCTR *NE 0) THEN(DO)


   /* Create selection criteria string. */

             CHGVAR     VAR(&QRYSLT) VALUE('ITWHSE *EQ ' *CAT &WHSE)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLDISP *NE +
                          " "')
     /* 414 Begin */

    /* Slot type P=Pick slots only   */
             IF (&PICK *EQ 'P') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLPICK *EQ ' +
                         *CAT &QUOTE *CAT Y *CAT &QUOTE)
             ENDDO

    /* Slot type O=Overflow slots only   */
             IF (&PICK *EQ 'O') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLPICK *EQ ' +
                         *CAT &QUOTE *CAT N *CAT &QUOTE)
             ENDDO
     /* 414 End   */

     /* 412 Begin */
    /* Odd Slots only */
             IF (&SIDE *EQ 'O') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLSIDE *EQ +
                         1')
             ENDDO

    /* Even Slots only */
             IF (&SIDE *EQ 'E') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLSIDE *EQ +
                         0')
             ENDDO
     /* 412 End   */

   /* If viewing the report, select all records for this warehouse */
   /* Skip all other selection. */

             IF         COND(&VP *EQ 'V') THEN(GOTO CMDLBL(VIEW))


             IF (&WHDP *NE '*ALL') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITWHDP *EQ +
                          ' *BCAT &QUOTE *CAT &WHDP *CAT &QUOTE)
             ENDDO

             IF (&RETURN = '*PIR') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'P' *CAT &QUOTE)
             ENDDO

             IF (&RETURN = '*FIXED') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'F' *CAT &QUOTE)
             ENDDO

 VIEW:

   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(IT331) TOFILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE((&FILE) (SLOT)) FORMAT(IT331) +
                          QRYSLT(&QRYSLT) KEYFLD((PIRITEM/ITWHSE) +
                          (PIRITEM/ITWHDP) (PIRITEM/ITSTYP) +
                          (SLOT/SLDISP)) JFLD((PIRITEM/ITWHSE +
                          SLOT/SLWHSE) (PIRITEM/ITITEM +
                          SLOT/SLITEM)) JDFTVAL(*YES) +
                          OPTIMIZE(*FIRSTIO) SEQONLY(*NO)

/*  If user viewed the data then we are done so exit. */

             IF (&VP = 'V') THEN(DO)
             CALL       PGM(IT332) PARM(&WHSE &WHDP &SECTN)
               GOTO       CMDLBL(OUT)
             ENDDO


             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(&PRTCTR + 1)
 /*520AA*/   CALL       PGM(QCMDEXC) PARM(&CMD &LEN)
 /*520AA*/   CALL       PGM(IT331)
 /*520AA*/   IF         COND(&ONETIM *EQ 'Y') THEN(DO)
 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(0)
 /*520AA*/   ENDDO
 /*520AA*/   CLOF       OPNID(&FILE)
 /*520AA*/   DLTOVR     FILE(&PRTF)
 /*520AA*/   MONMSG     MSGID(CPF9841)
 /*520AA*/   ENDDO
 /*520AA*/   ENDDO

 /* 520D     CALL       PGM(IT331)   */

 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 OUT:
 NOMSG:

   /* Clean up before leaving */

             RCLRSC     LVL(*CALLER)
             DLTOVR     file(&FILE)                               /* (640 A) */
             MONMSG     MSGID(CPF9841)                            /* (640 A) */
             CLOF       OPNID(&FILE)                              /* (640 A) */
             MONMSG     MSGID(CPF4519 CPF4520)                    /* (640 A) */

             ENDPGM
