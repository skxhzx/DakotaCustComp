/*-------------------------------------------------------------------*/
/*                                                                   */
/*  IT358CL   Department Inventory Activity report                   */
/*  07 May 2007                                                      */
/*  Retha Davis                                                      */
/*                                                                   */
/*  Revisions                                                        */
/*    05/07/07  RBD  5.10                                            */
/*      - Created                                                    */
/*                                                                   */
/*-------------------------------------------------------------------*/

 IT358CL:    PGM        PARM(&WHSE &RETURN &JOBQ &DEV &FORM +
                             &FMDT &TODT)
/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT358CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT358)

  /* Report program and printer file name. */

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(LABEL)
             DCL        VAR(&FILE2) TYPE(*CHAR) LEN(10) VALUE(RTEHED)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) VALUE(IT359PR)

  /* Informational message. */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Department Inventory Activity report')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Printing Department Inventory +
                                 Activity report')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEV)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&FMDT)       TYPE(*CHAR) LEN(8)
             DCL        VAR(&FMDTC)      TYPE(*CHAR) LEN(8)
             DCL        VAR(&TODT)       TYPE(*CHAR) LEN(8)
             DCL        VAR(&TODTC)      TYPE(*CHAR) LEN(8)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))
/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

/*  Don't get options, if being run from JOBQ. */

             RTVJOBA    TYPE(&TYPE)
             IF (&TYPE = '0') THEN(GOTO REPORT)

/*  Get report options. */

             CALL       PGM(&OPTPGM) PARM(&WHSE &RETURN &JOBQ +
                          &DEV &FORM &FMDT &TODT)

/*  Convert MMDDYY parm dates to YYYYMMDD dates to compare         */
             CVTDAT     DATE(&FMDT) TOVAR(&FMDTC) FROMFMT(*YYMD) +
                          TOFMT(*YYMD) TOSEP(*NONE)
             CVTDAT     DATE(&TODT) TOVAR(&TODTC) FROMFMT(*YYMD) +
                          TOFMT(*YYMD) TOSEP(*NONE)
/*  See if user wants to proceed. */

             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)

               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) +
                          MSGTYPE(*INFO)
               RETURN
               ENDDO


/* See if user wants to put report on jobq. */

             IF (&JOBQ = 'Y') THEN(DO)
             SBMJOB     CMD(CALL PGM(&SBMPGM) PARM(&WHSE +
                          &RETURN &DEV &FORM &FMDT &TODT )) JOB(&SBMPGM)
             GOTO       CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

 REPORT:

/*  Build IT357A Work File                                                    */

/*  Create selection criteria string.                                         */

/*  Select records within date range                                          */
             CHGVAR     VAR(&QRYSLT) VALUE('RHWHSE *EQ ' *CAT &WHSE)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'RHIMPD +
                          *GE ' *CAT &FMDTC)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'RHIMPD +
                          *LE ' *CAT &TODTC)

   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(&FILE2) SHARE(*YES)
             OPNQRYF    FILE((&FILE2)) QRYSLT(&QRYSLT) +
                          KEYFLD((RTEHED/RHRTID)) SEQONLY(*NO) +
                          OPTIMIZE(*FIRSTIO)


             CALL       PGM(GETUNQMEM) PARM(*REUSE IT357A &MEMBER)
             OVRDBF     FILE(IT357A) MBR(&MEMBER) SHARE(*YES)
             CALL       PGM(IT357)

             DLTOVR     FILE(&FILE2)

   /* Override printer file. */

             OVRPRTF    FILE(&PRTF) DEV(&DEV) OUTQ(*DEV) +
                          FORMTYPE(&FORM)

/*  Create selection criteria string.                                         */

/*  Select records within date range                                          */

             CHGVAR     VAR(&QRYSLT) VALUE('LBWHSE *EQ ' *CAT &WHSE)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'LBSDTE +
                          *GE ' *CAT &FMDTC)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'LBSDTE +
                          *LE ' *CAT &TODTC)

 PRINT:

   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE((&FILE)) QRYSLT(&QRYSLT) +
                          KEYFLD((LABEL/LBWHDP)) SEQONLY(*NO) +
                          OPTIMIZE(*FIRSTIO)


             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

             CALL       PGM(IT359)  PARM(&WHSE &FMDT &TODT)
             DLTOVR     FILE(IT357A)
             MONMSG     MSGID(CPF9841)
             CLRPFM     FILE(IT357A) MBR(&MEMBER)

 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

   /* Clean up before leaving */

             RCLRSC     LVL(*CALLER)

             ENDPGM
