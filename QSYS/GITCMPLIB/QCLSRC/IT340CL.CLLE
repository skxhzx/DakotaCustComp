/*-------------------------------------------------------------------*/
/*                                                                   */
/*  IT340CL   Unslotted Items Report                                 */
/*  01 September, 1994                                               */
/*  Hemant Kapadia                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions                                                        */
/*                                                                   */
/*    02/20/95  DAS                                                  */
/*      - Revised OPNQRYF to include the warehouse along with the    */
/*        item as join fields.                                       */
/*                                                                   */
/*    05/01/97  RH   2.20                                            */
/*      - Change MSG command to MSGBFC.                              */
/*    09/14/99  RH   4.12                                            */
/*      - Change QRYSLT to select only Base or Regular items         */
/*    11/26/07  RH   5.10                                            */
/*      - Enh: Added client custom code support to program.          */
/*                                                                   */
/*    03/31/09  CWM  5.20A                                           */
/*      - Add support for new Report Distributution                  */
/*                                                                   */
/*    12/03/12  MLB  6.40                                            */
/*      - Fix: Corrected problem with file PIRITEM not being closed. */
/*        Was causing CPF4174 - OPNID(PIRITEM) already exists.       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Client Custom Revisions: City Wholesale                          */
/*                                                                   */
/*    11/26/07  RH   CW                                              */
/*      - Revised program to NOT select discontinued items           */
/*        ITACTV='DC'                                                */
/*-------------------------------------------------------------------*/

 IT340CL:    PGM        PARM(&WHSE &WHDP &RETURN &DEV &FORM)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Client ID's.    */                                           /* (510 A) */

             DCL        VAR(&CITYWHO) TYPE(*CHAR) LEN(8) +
                          VALUE('CITYWHO ')                       /* (CW  A) */

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT340CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT340)

  /* Report program and printer file name. */

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(PIRITEM)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) VALUE(IT341PR)

  /* Informational message. */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Unslotted Items')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Printing Unslotted Items Report')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&actv)       TYPE(*CHAR) LEN(2) VALUE('DC')
             DCL        VAR(&client)     TYPE(*CHAR) LEN(10)      /* (510 A) */
             DCL        VAR(&clientloc)  TYPE(*CHAR) LEN(10)      /* (510 A) */
             DCL        VAR(&DEV)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&WHDP)       TYPE(*CHAR) LEN(5)
             DCL        VAR(&SECTN)      TYPE(*CHAR) LEN(1)
             DCL        VAR(&VP)         TYPE(*CHAR) LEN(1)

/* 520aA */  DCL        VAR(&CANCEL) TYPE(*CHAR) LEN(8)
/* 520aA */  DCL        VAR(&CMD)    TYPE(*CHAR) LEN(1024)
/* 520aA */  DCL        VAR(&LEN)    TYPE(*DEC)  LEN(15 5) VALUE(1024)
/* 520aA */  DCL        VAR(&PARMS)  TYPE(*CHAR) LEN(80)
/* 520aA */  DCL        VAR(&POVR)   TYPE(*CHAR) LEN(1)
/* 520aA */  DCL        VAR(&PRTCTR) TYPE(*DEC)  LEN(15 5) VALUE(1)
/* 520aA */  DCL        VAR(&ONETIM) TYPE(*CHAR) LEN(1)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))

/* Get Client ID */                                               /* (510 A) */
             CALL       PGM(GETCLIENT) PARM(&CLIENT &CLIENTLOC)   /* (510 A) */

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

START:

/*  Don't get options, if being run from JOBQ. */

             RTVJOBA    TYPE(&TYPE)
             IF (&TYPE = '0') THEN(GOTO REPORT)

/*  Get report options. */

/* 520D      CALL       PGM(&OPTPGM) PARM(&WHSE &WHDP &SECTN &RETURN +
                          &VP &JOBQ &DEV &FORM)        */
/* 520A */   CALL       PGM(&OPTPGM) PARM(&WHSE &WHDP &SECTN &RETURN +
                          &VP &JOBQ &POVR)

/*  See if user wants to proceed. */

             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)

               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) +
                          MSGTYPE(*INFO)
               RETURN
               ENDDO


/* See if user wants to put report on jobq. */

             IF (&JOBQ = 'Y') THEN(DO)
             SBMJOB     CMD(CALL PGM(&SBMPGM) PARM(&WHSE &WHDP +
                          &RETURN &DEV &FORM)) JOB(&SBMPGM)
             GOTO       CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

 REPORT:

   /* Override printer file. */

/* 520D      OVRPRTF    FILE(&PRTF) DEV(&DEV) OUTQ(*DEV) +
                          FORMTYPE(&FORM)       */

/*       Process Override Print File Flag     */

 /*520AA*/   IF (&POVR = 'Y') THEN(DO)

 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 1 8)) VALUE('*ADD    ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 9 10)) VALUE(&SBMPGM)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 19 10)) VALUE('IT341PR   ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 29 3)) VALUE('000')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 32 3)) VALUE(&WHSE)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 35 45)) VALUE(' ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 68 1)) VALUE('Y')
 /*520AA*/   CALL       PGM(RP152) PARM(&PARMS)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(%SST(&PARMS 69 8))
 /*520AA*/   IF         COND(&CANCEL *EQ '*CANCEL ') THEN(DO)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(' ')
 /*520AA*/   GOTO       CMDLBL(START)
 /*520AA*/   ENDDO

 /*520AA*/   ENDDO

/*520AA*/    DOUNTIL    COND(&PRTCTR *EQ 0)
/*520AA*/    CALL       PGM(RPT120) PARM(&PRTF &PRTCTR &CMD &ONETIM)
/*520AA*/    IF         COND(&PRTCTR *NE 0) THEN(DO)

   /* Create selection criteria string. */

             CHGVAR     VAR(&QRYSLT) VALUE('ITWHSE *EQ ' *CAT &WHSE)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '(ITTYPE *EQ +
                          " "')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*OR')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITTYPE *EQ +
                          "B")')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLDISP *EQ +
                          " "')

/*  If City Wholesale, skip discontinued items                       (CW  A) */
             IF (&client *eq &citywho) THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITACTV *NE +
                          ' *BCAT &QUOTE *CAT &ACTV *CAT &QUOTE)
             ENDDO


   /* If viewing the report, select all records for this warehouse */
   /* Skip all other selection. */

             IF         COND(&VP *EQ 'V') THEN(GOTO CMDLBL(VIEW))


             IF (&WHDP *NE '*ALL') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITWHDP *EQ +
                          ' *BCAT &QUOTE *CAT &WHDP *CAT &QUOTE)
             ENDDO

             IF (&RETURN = '*PIR') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'P' *CAT &QUOTE)
             ENDDO

             IF (&RETURN = '*FIXED') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'F' *CAT &QUOTE)
             ENDDO

 VIEW:

   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(IT341) TOFILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE((&FILE) (SLOT)) FORMAT(IT341) +
                          QRYSLT(&QRYSLT) KEYFLD((PIRITEM/ITWHSE) +
                          (PIRITEM/ITWHDP) (PIRITEM/ITSTYP) +
                          (PIRITEM/ITITEM)) JFLD((PIRITEM/ITWHSE +
                          SLOT/SLWHSE) (PIRITEM/ITITEM +
                          SLOT/SLITEM)) JDFTVAL(*YES) +
                          OPTIMIZE(*FIRSTIO) SEQONLY(*NO)

/*  If user viewed the data then we are done so exit. */

             IF (&VP = 'V') THEN(DO)
             CALL       PGM(IT342) PARM(&WHSE &WHDP &SECTN)
               GOTO       CMDLBL(OUT)
             ENDDO


             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(&PRTCTR + 1)
 /*520AA*/   CALL       PGM(QCMDEXC) PARM(&CMD &LEN)
 /*520AA*/   CALL       PGM(IT341)
 /*520AA*/   IF         COND(&ONETIM *EQ 'Y') THEN(DO)
 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(0)
 /*520AA*/   ENDDO
 /*520AA*/   CLOF       OPNID(&FILE)
 /*520AA*/   DLTOVR     FILE(&PRTF)
 /*520AA*/   MONMSG     MSGID(CPF9841)
 /*520AA*/   ENDDO
 /*520AA*/   ENDDO

 /* 520D     CALL       PGM(IT341)     */

 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 OUT:
             DLTOVR     FILE(&FILE)
             MONMSG     MSGID(CPF9841)
             DLTOVR     FILE(IT341)
             MONMSG     MSGID(CPF9841)
             CLOF       OPNID(&FILE)
             MONMSG     MSGID(CPF4519 CPF4520)
             CLOF       OPNID(IT341)
             MONMSG     MSGID(CPF4519 CPF4520)
 NOMSG:

   /* Clean up before leaving */

             RCLRSC     LVL(*CALLER)

             ENDPGM
