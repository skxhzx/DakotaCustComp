/*-------------------------------------------------------------------*/
/*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved     */
/*   BFC Software, Inc.                                              */
/*   24 N. Washington Ave Suite 203                                  */
/*   Batavia, IL 60510                                               */
/*   (630) 562-0375                                                  */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*  RP310CL    Batch Reports - Driver                                */
/*  03 December 2012                                                 */
/*  Rod Roller                                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions:                                                       */
/*                                                                   */
/*    12/03/12  RTR  6.40                                            */
/*      - Created.                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Parameters:                                                      */
/*                                                                   */
/*    None                                                           */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Notes:                                                           */
/*                                                                   */
/*   Driver CL to create and send a Batch Report.                    */
/*                                                                   */
/*-------------------------------------------------------------------*/
             PGM        PARM(&RUNTYP &RPTID &USERIN &OUTQ &PARM +
                          &RETRN &MSGTXT)

             DCL        VAR(&RUNTYP)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&RPTID)      TYPE(*CHAR) LEN(20)
             DCL        VAR(&USERIN)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM)       TYPE(*CHAR) LEN(200)
             DCL        VAR(&RETRN)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGTXT)     TYPE(*CHAR) LEN(100)
             DCL        VAR(&QSHSTR)     TYPE(*CHAR) LEN(200)

             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)

             RTVJOBA    USER(&USER) TYPE(&TYPE)

/*  If user passed is blank or *USER, use current user instead  */
             IF         COND(&USERIN *EQ '*USER' *OR &USERIN *EQ +
                          '          ') THEN(CHGVAR VAR(&USERIN) +
                          VALUE(&USER))

/*  Call program to build scripts in IFS  */
             CALL       PGM(RP310) PARM(&RUNTYP &RPTID &USERIN &OUTQ +
                          &PARM &RETRN &MSGTXT)

/*  If script generated, run it  */
             IF         COND(%SST(&RETRN 1 4) *EQ '*RUN') THEN(DO)

             ADDENVVAR  ENVVAR(JAVA_HOME) +
                          VALUE('/QIBM/ProdData/Java400/jdk6') +
                          LEVEL(*JOB) REPLACE(*YES)
             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT) VALUE('NONE') +
                          LEVEL(*JOB) REPLACE(*YES)
             CHGVAR     VAR(&QSHSTR) +
                          VALUE('/batchscripts/bfcbatchrpt.sh ' +
                          *BCAT &MSGTXT)
             QSH        CMD(&QSHSTR)

             ENDDO

/*  If printing to PCL printer, run Pdf to Outq script  */
             IF         COND(&RETRN *EQ '*RUNPRTPCL') THEN(DO)
             CHGVAR     VAR(&QSHSTR) +
                          VALUE('/batchscripts/pdf2outqBfc1.sh' +
                          *BCAT &MSGTXT *CAT '.pdf' *BCAT &MSGTXT +
                          *CAT '.pcl')
             QSH        CMD(&QSHSTR)
             ENDDO

/*  If printing to PS printer, run Pdf to Outq script  */
             IF         COND(&RETRN *EQ '*RUNPRTPS ') THEN(DO)
             CHGVAR     VAR(&QSHSTR) +
                          VALUE('/batchscripts/pdf2outqBfc1.sh' +
                          *BCAT &MSGTXT *CAT '.pdf' *BCAT &MSGTXT +
                          *CAT '.ps')
             QSH        CMD(&QSHSTR)
             ENDDO

/*  All done, return OK  */
             CHGVAR     VAR(&RETRN) VALUE('*OK')
             CHGVAR     VAR(&MSGTXT) +
                          VALUE('                                     -
                                                               ')
             GOTO       CMDLBL(NOMSG)

/*  Receive any messages and pass them on  */
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             IF         COND(&TYPE = '0') THEN(DO)
                SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                            MSGDTA(&MSGDTA) TOUSR(&USER) MSGTYPE(*COMP)
                CHGVAR     VAR(&MSGTXT) VALUE(&MSGDTA)
                CHGVAR     VAR(&RETRN) VALUE('*ERROR')
                ENDDO
             ELSE       CMD(DO)
                SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                            MSGDTA(&MSGDTA) MSGTYPE(*COMP)
                CHGVAR     VAR(&MSGTXT) VALUE(&MSGDTA)
                CHGVAR     VAR(&RETRN) VALUE('*ERROR')
                ENDDO
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:
             ENDPGM
