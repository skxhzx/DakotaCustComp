/*-------------------------------------------------------------------*/
/*                                                                   */
/*  APTCRTMBR   Create member in APTEXP file.                        */
/*  September 30, 2011                                               */
/*  Rod Roller                                                       */
/*                                                                   */
/*  Revisions                                                        */
/*                                                                   */
/*    09/30/11  RTR  6.40                                            */
/*      - Created.                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/

 CRTAPTMBR:  PGM        PARM(&MEMBER &RETURN &MESSAGE)

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

 /* General variables */

             DCL        VAR(&MEMBER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(60)
             DCL        VAR(&RETURN)  TYPE(*CHAR) LEN(10)

             DCL        VAR(&COUNT)   TYPE(*DEC) LEN(3 0) VALUE(0)
             DCL        VAR(&COUNTA)  TYPE(*CHAR) LEN(3)
             DCL        VAR(&FILE)    TYPE(*CHAR) LEN(10) VALUE('APTEXP')
             DCL        VAR(&EXIST)   TYPE(*LGL)

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/
/* See if member exists (1=EXIST)                               */

TRYAGAIN:
             CHGVAR     VAR(&EXIST) VALUE('1')
             CHKOBJ     OBJ(&FILE) OBJTYPE(*FILE) MBR(&MEMBER)
             MONMSG     MSGID(CPF9815) EXEC(CHGVAR VAR(&EXIST) +
                          VALUE('0'))

/* If new member - use it, otherwise change member and try again */

             IF (&EXIST = '1') DO
             CHGVAR     VAR(&COUNT) VALUE(&COUNT + 1)
             CHGVAR     VAR(&COUNTA) VALUE(&COUNT)
             CHGVAR     VAR(%SST(&MEMBER 8 3)) VALUE(&COUNTA)

             IF         COND(&COUNT = 0) THEN(DO)
             CHGVAR     VAR(&RETURN) VALUE('*ERROR')
             CHGVAR     VAR(&MESSAGE) VALUE('Error occured creating +
                          member')
             GOTO       CMDLBL(ENDPGM)
             ENDDO

             GOTO       CMDLBL(TRYAGAIN)
             ENDDO

             CHGVAR     VAR(&RETURN) VALUE('*OK')
             CHGVAR     VAR(&MESSAGE) VALUE(' ')

             ADDPFM     FILE(APTEXP) MBR(&MEMBER)
             MONMSG     MSGID(CPF3204 CPF7306) EXEC(DO)
             CHGVAR     VAR(&RETURN) VALUE('*ERROR')
             CHGVAR     VAR(&MESSAGE) VALUE('Error occured creating +
                          member')
             ENDDO

ENDPGM:      ENDPGM
