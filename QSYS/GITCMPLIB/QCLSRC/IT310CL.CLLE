/*-------------------------------------------------------------------*/
/*                                                                   */
/*  IT310CL   Inventory register                                     */
/*  28 July,1994                                                     */
/*  Hemant Kapadia                                                   */
/*  Revisions                                                        */
/*                                                                   */
/*    05/01/97  RH   2.20                                            */
/*      - Change MSG command to MSGBFC.                              */
/*                                                                   */
/*    02/18/09  CWM  5.20A                                           */
/*      - Add support for new Report Distributu=ion                  */
/*                                                                   */
/*-------------------------------------------------------------------*/

         IT310CL:    PGM        PARM(&WHSE &RETURN &DEV &FORM)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

  /* Name of this program. */

             DCL        VAR(&SBMPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT310CL)

  /* Option program. */

             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) +
                          VALUE(IT310)

  /* Report program and printer file name. */

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(PIRITEM)
             DCL        VAR(&PRTF) TYPE(*CHAR) LEN(10) VALUE(IT311PR)

  /* Informational message. */

             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Inventory Register')
             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                          VALUE('Printing Inventory Register')

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&DEV)        TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBQ)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)

/* 520aA */  DCL        VAR(&CMD)    TYPE(*CHAR) LEN(1024)
/* 520AA */  DCL        VAR(&CANCEL) TYPE(*CHAR) LEN(8)
/* 520aA */  DCL        VAR(&LEN)    TYPE(*DEC)  LEN(15 5) VALUE(1024)
/* 520aA */  DCL        VAR(&PARMS)  TYPE(*CHAR) LEN(80)
/* 520aA */  DCL        VAR(&POVR)   TYPE(*CHAR) LEN(1)
/* 520aA */  DCL        VAR(&PRTCTR) TYPE(*DEC)  LEN(15 5) VALUE(1)
/* 520aA */  DCL        VAR(&ONETIM) TYPE(*CHAR) LEN(1)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))
/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

 START:

/*  Don't get options, if being run from JOBQ. */

             RTVJOBA    TYPE(&TYPE)
             IF (&TYPE = '0') THEN(GOTO REPORT)

/*  Get report options. */

/* 520AD     CALL       PGM(&OPTPGM) PARM(&WHSE &RETURN &JOBQ &DEV &FORM) */
/* 520AA */  CALL       PGM(&OPTPGM) PARM(&WHSE &RETURN &JOBQ &POVR)

/*  See if user wants to proceed. */

             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)

               SNDPGMMSG  MSGID(PIR9801) MSGF(PIRMSGF) +
                          MSGDTA(&CANCELMSG) +
                          MSGTYPE(*INFO)
               RETURN
               ENDDO


/* See if user wants to put report on jobq. */

             IF (&JOBQ = 'Y') THEN(DO)
             SBMJOB     CMD(CALL PGM(&SBMPGM) PARM(&WHSE &RETURN  +
                          &DEV &FORM)) JOB(&SBMPGM)

             GOTO       CMDLBL(GETOUT)
             ENDDO

/*  Otherwise, run program. */

 REPORT:

   /* Override printer file. */

 /* 520AD    OVRPRTF    FILE(&PRTF) DEV(&DEV) OUTQ(*DEV) +
                          FORMTYPE(&FORM)       */


      /*                n For 1 Time Override Entry */

 /*520AA*/   IF (&POVR = 'Y') THEN(DO)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 1 8)) VALUE('*ADD    ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 9 10)) VALUE(&SBMPGM)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 19 10)) VALUE('IT311PR   ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 29 3)) VALUE('000')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 32 3)) VALUE(&WHSE)
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 35 45)) VALUE(' ')
 /*520AA*/   CHGVAR VAR(%SUBSTRING(&PARMS 68 1)) VALUE('Y')
 /*520AA*/   CALL       PGM(RP152) PARM(&PARMS)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(%SST(&PARMS 69 8))
 /*520AA*/   IF         COND(&CANCEL *EQ '*CANCEL ') THEN(DO)
 /*520AA*/   CHGVAR VAR(&CANCEL)  VALUE(' ')
 /*520AA*/   GOTO       CMDLBL(START)
 /*520AA*/   ENDDO
 /*520AA*/   ENDDO

/*520AA*/    DOUNTIL    COND(&PRTCTR *EQ 0)
/*520AA*/    CALL       PGM(RPT120) PARM(&PRTF &PRTCTR &CMD &ONETIM)
/*520AA*/    IF         COND(&PRTCTR *NE 0) THEN(DO)

   /* Create selection criteria string. */

             CHGVAR     VAR(&QRYSLT) VALUE('ITWHSE *EQ ' *CAT &WHSE)

             IF (&RETURN = '*PIR') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'P' *CAT &QUOTE)
             ENDDO

             IF (&RETURN = '*FIXED') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'ITSTYP +
                          *EQ' *BCAT &QUOTE *CAT 'F' *CAT &QUOTE)
             ENDDO


   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE((&FILE)) QRYSLT(&QRYSLT) +
                          KEYFLD((1/ITSTYP) (1/ITITEM))

             RMVMSG     CLEAR(*ALL)
             MSGBFC     MESSAGE(&WORKINGMSG)

 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(&PRTCTR + 1)
 /*520AA*/   CALL       PGM(QCMDEXC) PARM(&CMD &LEN)
 /*520AA*/   CALL       PGM(IT311)
 /*520AA*/   IF         COND(&ONETIM *EQ 'Y') THEN(DO)
 /*520AA*/   CHGVAR     VAR(&PRTCTR) VALUE(0)
 /*520AA*/   ENDDO
 /*520AA*/   CLOF       OPNID(&FILE)
 /*520AA*/   DLTOVR     FILE(&PRTF)
 /*520AA*/   MONMSG     MSGID(CPF9841)
 /*520AA*/   ENDDO
 /*520AA*/   ENDDO

 /* 520AD    CALL       PGM(IT311)      */

 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 NOMSG:

   /* Clean up before leaving */

   /*        DLTOVR     FILE(&PRTF)   */
             RCLRSC     LVL(*CALLER)

             ENDPGM
