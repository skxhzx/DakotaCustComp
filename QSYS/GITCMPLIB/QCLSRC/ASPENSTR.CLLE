/*-------------------------------------------------------------------*/
/*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved     */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*  ASPENSTR   Aspen Interface - Submit                              */
/*  May 17, 2018                                                     */
/*  Rod Roller                                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*  Revisions                                                        */
/*                                                                   */
/*  05/17/18  RTR  7.20                                              */
/*    - Created.                                                     */
/*                                                                   */
/*-------------------------------------------------------------------*/
 ASPENSTR:   PGM

             DCL        VAR(&JOBD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OIVALT) TYPE(*CHAR) LEN(1)
             DCL        VAR(&OIVALN) TYPE(*DEC) LEN(15 5)
             DCL        VAR(&OIVALA) TYPE(*CHAR) LEN(100)
             DCL        VAR(&OIRETURN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OPTRET) TYPE(*CHAR) LEN(117)
             DCL        VAR(&SBMUSR) TYPE(*CHAR) LEN(10)

             /* Get job description */
             CALL       PGM(GETOPTIONS) PARM('001' '*ASPEN ' '030' +
                          '010' &OPTRET)
             CHGVAR     VAR(&JOBD) VALUE(%SST(&OPTRET 1 10))

             /* Get job name */
             CALL       PGM(GETOPTIONS) PARM('001' '*ASPEN ' '063' +
                          '010' &OPTRET)
             CHGVAR     VAR(&NAME) VALUE(%SST(&OPTRET 1 10))

             /* Get job user */
             CALL       PGM(GETOPTINT) PARM('Interface' 'General +
                          Options' 'Submit Batch User' &OIVALT +
                          &OIVALN &OIVALA &OIRETURN)
             IF         COND(&OIRETURN *EQ '*OK') THEN(CHGVAR +
                          VAR(&SBMUSR) VALUE(%SST(&OIVALA 1 10)))
             ELSE       CMD(CHGVAR VAR(&SBMUSR) VALUE('*CURRENT'))

             /* Submit job */
             SBMJOB     CMD(CALL PGM(ASPENINT)) JOB(&NAME) +
                          JOBD(&JOBD) JOBQ(QS36EVOKE) +
                          OUTQ(BFCIMPORT) USER(&SBMUSR) +
                          INLLIBL(*JOBD) LOGCLPGM(*YES) +
                          LOGOUTPUT(*JOBEND) JOBMSGQFL(*WRAP) +
                          MSGQ(QSYSOPR)

 DONE:       ENDPGM
