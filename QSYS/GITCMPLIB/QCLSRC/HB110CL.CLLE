/*-------------------------------------------------------------------*/
/*                                                                   */
/*  HB110CL   HACCP Question Type File Maintenance                   */
/*  20 February 2012                                                 */
/*  Lynn McMahon                                                     */
/*                                                                   */
/*  8/8/14   LMC     6.50a                                           */
/*    - Enh:  Add user authorization check.                          */
/*-------------------------------------------------------------------*/

 HB110CL:    PGM

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(HACCPQT)
             DCL        VAR(&PGM)  TYPE(*CHAR) LEN(8)  VALUE(HB110)
             DCL        VAR(&NOAUTHRZE) TYPE(*CHAR) LEN(60) +
                          VALUE('   You are not authorized to use +
                          this option.')              /* 6.50a */

/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RETURN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&UNQKEY) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PGMID)  TYPE(*CHAR) LEN(10) /* 6.50a */
             DCL        VAR(&PGMOPT) TYPE(*CHAR) LEN(20) /* 6.50a */
             DCL        VAR(&PRTN)   TYPE(*CHAR) LEN(8)  /* 6.50a */
             DCL        VAR(&PUSR)   TYPE(*CHAR) LEN(10) /* 6.50a */

/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/

/*  Define file to be used for display         */
/*      - Share it with everyone that follows. */
/*      - Open it to allow Input and Output.   */
/*  Check if user is authorized to use this option              6.50a */

               CHGVAR     VAR(&PGMID) VALUE(HB110CL)         /* 6.50a */
               CHGVAR     VAR(&PGMOPT) VALUE('*ALL')         /* 6.50a */
               RTVJOBA    USER(&PUSR)                        /* 6.50a */
               CALL       PGM(CHKLIST) PARM(&PGMID &PGMOPT &PUSR &PRTN)
               IF         COND(&PRTN *EQ '*NO')  THEN(DO)    /* 6.50a */
                 SNDPGMMSG  MSG(&NOAUTHRZE) MSGTYPE(*INFO)   /* 6.50a */
                 RETURN                                      /* 6.50a */
               ENDDO                                         /* 6.50a */

             OVRDBF     FILE(&FILE) SHARE(*YES)
             OPNDBF     FILE(&FILE) OPTION(*ALL)

/*  Define work file to be used for display options.  */
/*    - Get unique member to use.                     */
/*    - Override file to use unique member.           */

             IF (&MEMBER = ' ') DO
                CALL       PGM(GETUNQMEM) PARM(*REUSE WORKOPT &MEMBER)
                ENDDO

             OVRDBF     FILE(WORKOPT) MBR(&MEMBER) SHARE(*YES)

/*  On with the show ... */

             CALL       PGM(&PGM)  PARM(&UNQKEY &RETURN)

/*  Clean up before leaving */

             CLOF       OPNID(&FILE)
             DLTOVR     FILE(&FILE)
             DLTOVR     FILE(WORKOPT)
             RCLRSC     LVL(*CALLER)
             CLRPFM     FILE(WORKOPT) MBR(&MEMBER)

             ENDPGM
