/*   KENNETH ELDER */
/*   10-23-2017    */
/*   CREATE FILE WITH REQUESTED IFS DIRECTORY/FILE PASSED   */

             PGM        PARM(&DRPATH &DRNAME &DRFILE)

             DCL        VAR(&DRPATH) TYPE(*CHAR) LEN(384) VALUE(' ')
             DCL        VAR(&DRNAME) TYPE(*CHAR) LEN(384) VALUE(' ')
             DCL        VAR(&DRFILE) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&cmdStr) TYPE(*CHAR) LEN(100) VALUE(' ')
             DCL        VAR(&QUOTE) TYPE(*CHAR) LEN(1) VALUE('''')

             CHGJOB     LOG(1 30)

             /* TELL QSHELL NOT TO DISPLAY OUTPUT ON SCREEN  */
             RMVENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT)
             MONMSG MSGID(CPFA981) /* ENVVAR DOES NOT EXIST */
             RMVENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG)
             MONMSG MSGID(CPFA981) /* ENVVAR DOES NOT EXIST */
             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG) VALUE(Y)

             CD         DIR(&DRPATH)
             MONMSG     MSGID(CPFA0A9) EXEC(DO)
             /*  If path is invalid simply create work file empty and return */
             CRTPF      FILE(QTEMP/&DRFILE) +
                          SRCFILE(QDDSFILES) +
                          SRCMBR(IFSDIR) OPTION(*NOLIST *NOSRC)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(ENDNOW)
             ENDDO
             /*  string Shell command together   */
             /*  ls is linus list command and -lta says list details  */
             /*  for all files including hidden                       */
             CRTPF      FILE(QTEMP/&DRFILE) +
                          SRCFILE(QDDSFILES) +
                          SRCMBR(IFSDIR) OPTION(*NOLIST *NOSRC)
             MONMSG     MSGID(CPF7302) EXEC(GOTO FEXIST)
   FEXIST:
             GRTOBJAUT  OBJ(QTEMP/&DRFILE) OBJTYPE(*FILE) +
                          USER(*PUBLIC) AUT(*ALL)

             CHGVAR     VAR(&CMDSTR) VALUE('ls -ltsa')

             CLRPFM     FILE(QTEMP/&DRFILE)

             OVRDBF     FILE(STDOUT) TOFILE(QTEMP/&DRFILE)
             STRQSH     CMD(&CMDSTR)
             DLTOVR     FILE(STDOUT)

             MONMSG MSGID(QSH0005 QSH0006 QSH0007) EXEC(DO)
                 SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) MSGDTA('QShell +
                             command failed!')
             ENDDO

   ENDNOW:
        ENDPGM
