/*-------------------------------------------------------------------*/
/*  RC201CL   Put Away support process. Display overstocks locations */
/*            close to the pick slot of the item being put away.     */
/*                                                                   */
/*  June 21, 2010                                                    */
/*  Jose G. Duque                                                    */
/*                                                                   */
/*  Revisions                                                        */
/*                                                                   */
/*-------------------------------------------------------------------*/

 RC201CL:    PGM        PARM(&WHSE &LBWHDP &LBITEM &LBTRN &SLOT &RTNCD)

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(SLOT)


/*-------------------------------------------------------------------*/
/*  Field declarations                                               */
/*-------------------------------------------------------------------*/

             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&LBWHDP)     TYPE(*CHAR) LEN(5)
             DCL        VAR(&LBITEM)     TYPE(*CHAR) LEN(15)
             DCL        VAR(&LBTRN)      TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&W3QTY)      TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&RTNCD)      TYPE(*CHAR) LEN(8)
             DCL        VAR(&SLOT)       TYPE(*CHAR) LEN(12)
             DCL        VAR(&PICK)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&VP)         TYPE(*CHAR) LEN(1)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))
/*-------------------------------------------------------------------*/
/*  Main line                                                        */
/*-------------------------------------------------------------------*/



   /* Create selection criteria string. */

    /* Select specific department only */
             CHGVAR     VAR(&QRYSLT) VALUE('SLWHSE *EQ ' *CAT &WHSE)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLWHDP *EQ +
                          ' *BCAT &QUOTE *CAT &LBWHDP *CAT &QUOTE)

    /* Slot Stat V=Verify Zero slots only */
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLSTAT *EQ ' +
                         *CAT &QUOTE *CAT V *CAT &QUOTE)

    /* Slot type N=OverStock slots only   */
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLPICK *EQ ' +
                         *CAT &QUOTE *CAT N *CAT &QUOTE)



   /* Use OPNQRYF to select and order the records we need */

             OVRDBF     FILE(&FILE) TOFILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE(&FILE)                          +
                        QRYSLT(&QRYSLT) KEYFLD((SLOT/SLWHSE) +
                        (SLOT/SLWHDP) (SLOT/SLDISP))         +
                        OPTIMIZE(*FIRSTIO) SEQONLY(*NO)


             CALL       PGM(GETUNQMEM) +
                        PARM(*REUSE ZEROSLOT &MEMBER)

             CLRPFM     FILE(ZEROSLOT) MBR(&MEMBER)
             MONMSG     MSGID(CPF3130 CPF3133 CPF3141 CPF3156)

             CPYFRMQRYF FROMOPNID(&FILE) TOFILE(ZEROSLOT) +
                          TOMBR(&MEMBER) MBROPT(*ADD) FMTOPT(*MAP +
                          *DROP)


             RMVMSG     CLEAR(*ALL)

             OVRDBF     FILE(ZEROSLOT) TOFILE(ZEROSLOT) +
                          MBR(&MEMBER)
             CALL       PGM(RC201) PARM(&WHSE &LBITEM &LBTRN &SLOT +
                                        &RTNCD)

             RMVM       FILE(ZEROSLOT) MBR(&MEMBER)
             MONMSG     MSGID(CPF320A CPF7301 CPF7310)
             GOTO       NOMSG


 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 OUT:
 NOMSG:

   /* Clean up before leaving */

             CLOF       OPNID(&FILE)

             DLTOVR     *ALL
             RCLRSC     LVL(*CALLER)

             ENDPGM
