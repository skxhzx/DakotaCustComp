/*-------------------------------------------------------------------*/
/*                                                                   */
/*  LT111CL RF - Pick Slot w/< 5 Cases - Control Driver Program.     */
/*  September 21, 2010                                               */
/*  Jose G. Duque                                                    */
/*                                                                   */
/*  Revisions                                                        */
/*                                                                   */
/*  12/13/19  KDE  7.30                                              */
/*    - Add full Display slot to return for everyone other than      */
/*      Cheney                                                       */
/*                                                                   */
/*-------------------------------------------------------------------*/

 LT111CL:    PGM        PARM(&LTWHSE &PCKSLT &LTWHDP &DSPLS5 &AISL1   +
/*  7.30 M   */              &AISL2 &FULLDISP)
/*  7.30 D                   &AISL2)                                 */

/*-------------------------------------------------------------------*/
/*  Constant declarations                                            */
/*-------------------------------------------------------------------*/

             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) VALUE(SLOT)
             DCL        VAR(&OPTPGM) TYPE(*CHAR) LEN(10) VALUE(LT111B)

  /*-------------------------*/
  /* Informational messages. */
  /*-------------------------*/
             DCL        VAR(&CANCELMSG) TYPE(*CHAR) LEN(60) +
                        VALUE('Repl Pick Slots w/< 5 Cases')

             DCL        VAR(&WORKINGMSG) TYPE(*CHAR) LEN(60) +
                        VALUE('Repl Pick Slots w/< 5 Cases Disp')


/*---------------------*/
/*  Field declarations */
/*---------------------*/
             DCL        VAR(&AISL1)      TYPE(*CHAR) LEN(3)
             DCL        VAR(&AISL2)      TYPE(*CHAR) LEN(3)
             DCL        VAR(&LTWHDP)     TYPE(*CHAR) LEN(5)
             DCL        VAR(&DSPLS5)     TYPE(*CHAR) LEN(1)
             DCL        VAR(&LTWHSE)     TYPE(*CHAR) LEN(3)
             DCL        VAR(&MEMBER)     TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGDTA)     TYPE(*CHAR) LEN(256)
             DCL        VAR(&MSGF)       TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
             DCL        VAR(&PCKSLT)     TYPE(*CHAR) LEN(7)
/* 7.30 */   DCL        VAR(&FULLDISP)   TYPE(*CHAR) LEN(12)
             DCL        VAR(&PICK)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRTN)       TYPE(*CHAR) LEN(8)
             DCL        VAR(&QRYSLT)     TYPE(*CHAR) LEN(2000)
             DCL        VAR(&QUOTE)      TYPE(*CHAR) LEN(1) VALUE('"')
             DCL        VAR(&RETURN)     TYPE(*CHAR) LEN(8)
             DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&SLOT)       TYPE(*CHAR) LEN(12)
             DCL        VAR(&TYPE)       TYPE(*CHAR) LEN(1)
             DCL        VAR(&UNQKEY)     TYPE(*CHAR) LEN(50)
             DCL        VAR(&VP)         TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHDP)       TYPE(*CHAR) LEN(5)
             DCL        VAR(&WFRA)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&WTOA)       TYPE(*CHAR) LEN(3)
             DCL        VAR(&W3QTY)      TYPE(*DEC)  LEN(7 0)
             DCL        VAR(&WHSE)       TYPE(*CHAR) LEN(3)

             MONMSG     MSGID(CPF0000)   EXEC(GOTO CMDLBL(GETOUT))


/*-------------*/
/*  Main line. */
/*-------------*/


             CHGVAR VAR(&PRTN) VALUE(' ')

/*-------------------------------------------------*/
/*  If first time thru display aisle range screen. */
/*-------------------------------------------------*/
             IF         (&DSPLS5 = '0') THEN(DO)
/*  7.30 */  CHGVAR     VAR(&WHDP) VALUE(&LTWHDP)
             CALL       PGM(&OPTPGM) PARM(&WHSE &WHDP &WFRA &WTOA +
                                          &PRTN &DSPLS5)
             CHGVAR     VAR(&LTWHSE) VALUE(&WHSE)
             CHGVAR     VAR(&LTWHDP) VALUE(&WHDP)
             CHGVAR     VAR(&AISL1)  VALUE(&WFRA)
             CHGVAR     VAR(&AISL2)  VALUE(&WTOA)
             ENDDO
             ELSE       (DO)
             CHGVAR     VAR(&WHSE)   VALUE(&LTWHSE)
             CHGVAR     VAR(&WHDP)   VALUE(&LTWHDP)
             CHGVAR     VAR(&WFRA)   VALUE(&AISL1)
             CHGVAR     VAR(&WTOA)   VALUE(&AISL2)
             ENDDO


/*--------------------------------*/
/*  See if user wants to proceed. */
/*--------------------------------*/
             IF (&RETURN = '*EXIT' *OR &RETURN = '*CANCEL') THEN(DO)
                RETURN
                ENDDO


/*--------------------------------------------------------------*/
/* Create selection criteria to find pick slots with qty < 5 CS */
/*--------------------------------------------------------------*/

    /*---------------------------------*/
    /* Select specific department only */
    /*---------------------------------*/
             CHGVAR     VAR(&QRYSLT) VALUE('SLWHSE *EQ ' *CAT &WHSE)

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLWHDP *EQ +
                          ' *BCAT &QUOTE *CAT &WHDP *CAT &QUOTE)

    /*--------------------------*/
    /* Select active slots only */
    /*--------------------------*/
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLSTAT *EQ ' +
                         *CAT &QUOTE *CAT A *CAT &QUOTE)


    /*------------------------*/
    /* Select specific aisles */
    /*------------------------*/
             IF         COND(&WFRA *NE ' ') THEN(DO)
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')

             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT +
                        '(SLAISL *EQ %RANGE("' *CAT +
                        &WFRA *CAT '" "' *CAT &WTOA +
                        *CAT '"))')
                        ENDDO

    /*-----------------------*/
    /* Select Pick Slot only */
    /*-----------------------*/
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT '*AND')
             CHGVAR     VAR(&QRYSLT) VALUE(&QRYSLT *BCAT 'SLPICK *EQ ' +
                         *CAT &QUOTE *CAT Y *CAT &QUOTE)


/*-----------------------------------------------------*/
/* Use OPNQRYF to select and order the records we need */
/*-----------------------------------------------------*/
             OVRDBF     FILE(&FILE) TOFILE(&FILE) SHARE(*YES)
             OPNQRYF    FILE(&FILE)                          +
                        QRYSLT(&QRYSLT) KEYFLD((SLOT/SLWHSE) +
                        (SLOT/SLWHDP) (SLOT/SLDISP))         +
                        OPTIMIZE(*FIRSTIO) SEQONLY(*NO)


             CALL       PGM(GETUNQMEM) +
                        PARM(*REUSE PICKLESS5 &MEMBER)

             CPYFRMQRYF FROMOPNID(&FILE) TOFILE(PICKLESS5) +
                        TOMBR(&MEMBER) MBROPT(*ADD) FMTOPT(*MAP +
                        *DROP)

             CLOF       OPNID(&FILE)

             RMVMSG     CLEAR(*ALL)

    /*---------------------------------------*/
    /* Display Pick Slots with qty < 5 cases */
    /*---------------------------------------*/
             OVRDBF     FILE(PICKLESS5) TOFILE(PICKLESS5) MBR(&MEMBER)
             CALL       PGM(LT111) PARM(&PCKSLT &LTWHDP &DSPLS5 +
/* 7.30  */               &FULLDISP)

             RMVM       FILE(PICKLESS5) MBR(&MEMBER)
             MONMSG     MSGID(CPF320A CPF320B CPF3203 CPF3220 +
                              CPF3273 CPF7301 CPF7310)
             GOTO       NOMSG


 /* Receive any messages and pass them on */

 GETOUT:
 NEXTMSG:    DO
             RCVMSG     PGMQ(*SAME *) MSGQ(*PGMQ) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID) SEV(&SEV) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             IF         COND(&MSGID *EQ ' ') THEN(GOTO CMDLBL(NOMSG))
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                        MSGDTA(&MSGDTA) MSGTYPE(*COMP)
             GOTO       CMDLBL(NEXTMSG)
             ENDDO
 OUT:
 NOMSG:

   /* Clean up before leaving */

             DLTOVR     *ALL
             RCLRSC     LVL(*CALLER)

             ENDPGM
