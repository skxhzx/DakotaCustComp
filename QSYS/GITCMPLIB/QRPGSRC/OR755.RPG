     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR755     Remove exported customer orders - Post
     F*  11 November, 1994
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
213 AF*    05/31/96  DAS
     F*      - Added logic to remove records from files ORDC and ORDT.
409 AF*    04/21/98  RH
     F*      - Recompiled, file ORDC changed.
412 AF*    08/23/99  RH
     F*      - Recompiled, file ORDC changed.
417aAF*    07/08/03  RH   4.17a
    AA*      - ORDT file changed, Re-compile only.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  79        - EOF for ORRTN
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FORDH    UP  E           K        DISK
     FORDD    UF  E           K        DISK
213 AFORDC    UF  E           K        DISK
213 AFORDT    UF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    DESC    1   1 50
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   30ERRECS
     I                                        4  48 ERDESC
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PPRG   - Program to send message back to.
     C*      &PWHSE  - Warehouse
     C*      &PDATE  - Export date
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PPRG  10
     C                     PARM           $PWHSE  3
     C                     PARM           $PDATE  8
     C                     MOVE $PWHSE    $KWHSE  30
     C                     MOVE $PDATE    $KDATE  80
     C*----------------------------------------------------------------
     C* Main line
     C*
     C*  Although the following conditions are tested by open query
     C*  in the preceding CL program, this is tested here again for
     C*  additional safety.
     C*
     C           OHWHSE    IFNE $KWHSE
     C           OHUDTE    ORGE $KDATE
     C                     GOTO BOTTOM
     C                     ENDIF
     C*
     C*  Delete exported customer order detail records.
     C*
     C           KEYOD     SETLLORDD                 79
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           KEYOD     READEORDD                     79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     DELETODREC                  78
     C                     ENDIF
     C                     ENDDO
213 AC*
213 AC*  Delete exported customer order detail catch weight records.
213 AC*
213 AC           KEYOD     SETLLORDC                 79
213 AC                     MOVE *IN79     EOF     1
213 AC           EOF       DOWEQ*OFF
213 AC           KEYOD     READEORDC                     79
213 AC                     MOVE *IN79     EOF
213 AC           *IN79     IFEQ *OFF
213 AC                     DELETOCREC                  78
213 AC                     ENDIF
213 AC                     ENDDO
213 AC*
213 AC*  Delete exported customer order detail transaction records.
213 AC*
213 AC           KEYOD     SETLLORDT                 79
213 AC                     MOVE *IN79     EOF     1
213 AC           EOF       DOWEQ*OFF
213 AC           KEYOD     READEORDT                     79
213 AC                     MOVE *IN79     EOF
213 AC           *IN79     IFEQ *OFF
213 AC                     DELETOTREC                  78
213 AC                     ENDIF
213 AC                     ENDDO
     C*
     C*  Delete exported customer order header record.
     C*
     C                     ADD  1         ERRECS
     C                     DELETOHREC                  79
     C*
     C           BOTTOM    TAG
     C*
     CLR                   EXSR ZM0750
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   Z-ADD0         ERRECS
     CSR                   MOVELDESC,1    ERDESC
     C*
     C*   Define partial key for ORDD file.
     C*
     CSR         KEYOD     KLIST
     CSR                   KFLD           OHWHSE
     CSR                   KFLD           OHORD
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*     PIR0750  Records removed.
     C*
     CSR         ZM0750    BEGSR
     CSR                   MOVE 'PIR0750' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
**
exported customer orders
