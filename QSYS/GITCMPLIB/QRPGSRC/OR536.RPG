     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR536     Reprint - Replenishment.
     F*  06 April, 1998
     F*  Roxanne Harrison
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
415 AF*    06/06/01  HNK 4.15
     F*      - Modified to work with new PFMT label printing program.
     F*
     F*
     F*----------------------------------------------------------------
415 AF*  Notes
     F*   - A special label printing program has been created to work
     F*     with Zebra type printers.  The name of the new program is
     F*     "PFMT".  To use this program the user would enter the
     F*     following for the label program within zone maintenance.
     F*
     F*                      PFMTxxxxxx
     F*
     F*     where 'xxxxxx' is the name of the label format to be used.
     F*     Menu PIRLF contains the options to work with and define
     F*     the label formats.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
409 AFOPTIONZ IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I* Data structure
     I*
409 MIOPZDTA      DS
     I I            0                         1   30OPCLVL
     I                                        4   4 OPCWRA
     I                                        5   5 OPCWRB
     I                                        6   6 OPUWRA
     I                                        7   7 OPUWRB
     I                                        8  50 OPTEXT
     I I            0                        51  520OPERR
     I I            0                        53  540OPCWGT
     I                                       55  64 OPLPGM
409 AI                                       65  74 OPPDEV
409 AI                                       75  84 OPPFRM
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHS
     I                                       22  29 $PTYPE
     I                                    P  30  330$PBAT#
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'ADDSTDTRN'           C         @ADDTR
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Error message parameter
     I*
214 AI$AERM       DS
214 AI                                        1  60 XAERM
214 AI                                        1   4 $AMSG
     I$CERM       DS
     I                                        1  60 XCERM
     I                                        1   4 $CMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD
     C*      $PWHSE  Warehouse number.
     C*      $PTYPE  Label type.
     C*      $PBAT#  Batch #
     C*
     C*    Returned Parameters
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*                    PARM           $PWH15 155
     C*                    PARM           $PBATN  70
     C*                    PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Get replenishment label print program name.
     C*
409 MC                     MOVE '*PICKLBL'KYCODE
409 AC                     MOVEL'REPLEN'  KYTRUK
409 AC                     MOVE 'ISH '    KYTRUK
409 AC                     MOVEL'REPL  '  KYZONE
409 MC           KEYOPZ    CHAINOPTIONZ              79
     C*
     C           *IN79     IFEQ *OFF
     C           OPLPGM    ANDNE*BLANKS
415 AC                     MOVELOPLPGM    @PGM    4
415 AC                     MOVE OPLPGM    @SET    6
     C*
415 AC                     SELEC
415 AC           @PGM      WHEQ 'PFMT'
415 AC                     CALL 'PFMT'
415 AC                     PARM '*REPLEN '$PCMD
415 AC                     PARM           @SET
415 AC                     PARM           @FMT    8
415 AC                     PARM           $PWHSE
415 AC                     PARM 'REPL '   $PRTID
415 AC                     PARM *BLANKS   $PCOMP
415 AC                     PARM KYZONE    $PTZON
415 AC                     PARM 0         $PGEN
415 AC                     PARM KYZONE    $PSZON
415 AC                     PARM KYTRUK    $PTRUK
415 AC                     PARM 'REPL '   $PRTE
415 AC                     PARM           OPZDTA
415 AC                     PARM           $PBAT#
     C*
     C*  Use original line printer label program
     C*
415 AC                     OTHER
     C*
409 AC                     CALL OPLPGM
409 AC                     PARM '*REPLEN '$PCMD   8
409 AC                     PARM           $PWHSE
409 AC                     PARM 'REPL '   $PRTID  5
409 AC                     PARM *BLANKS   $PCOMP  1
409 AC                     PARM KYZONE    $PTZON  6
409 AC                     PARM 0         $PGEN   10
409 AC                     PARM KYZONE    $PSZON  6
409 AC                     PARM KYTRUK    $PTRUK 10
409 AC                     PARM 'REPL '   $PRTE   5
409 AC                     PARM           OPZDTA
409 AC                     PARM           $PBAT#
415 AC                     ENDSL
     C                     ENDIF
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   Z-ADD$PWHS     $PWHSE  30
     C*R                   Z-ADD$PBATN    $PBAT#  70
     CSR                   MOVE *OFF      ERROR   1
     C*
     C*
409 AC*
409 AC*  Define partial key for options file.
409 AC*
409 ACSR         *LIKE     DEFN OPZCOD    KYCODE
409 ACSR         *LIKE     DEFN OPZTRK    KYTRUK
409 ACSR         *LIKE     DEFN OPZZON    KYZONE
409 ACSR         KEYOPZ    KLIST
409 ACSR                   KFLD           KYCODE
409 ACSR                   KFLD           $PWHSE
409 ACSR                   KFLD           KYTRUK
409 ACSR                   KFLD           KYZONE
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   ENDSR
