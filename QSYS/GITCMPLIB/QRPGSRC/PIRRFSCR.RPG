     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PIRRFSCR    Get/Set RF screen size
     F*  15 June 2001
     F*  Dave Sommerville
     F*
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CRTRFSCRCL'          C         @CRTDA
     C*----------------------------------------------------------------
     C*
     C*  Subprogram parameters
     C*
     C*    Input Parameters
     C*
     C*      $PCMD  -  *GET  Get screen size
     C*                *SET  Set screen size
     C*      $PLINE -  Number of lines on screen (for *SET).
     C*      $PCOL  -  Number of columns on screen (for *SET).
     C*
     C*    Returned Parameters
     C*
     C*      $PLINE -  Number of lines on screen (for *GET).
     C*      $PCOL  -  Number of columns on screen (for *GET).
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PLINE  2
     C                     PARM           $PCOL   3
     C*----------------------------------------------------------------
     C* Main Line
     C*
     C* Retreive data area
     C*
     C                     MOVE *ON       FIRST   1
     C           *NAMVAR   DEFN PIRRFSCR  #DATA   5
     C*
     C           FOREVR    DOWEQFOREVR
     C           $PCMD     IFEQ '*SET'
     C           *LOCK     IN   #DATA                  79
     C                     ELSE
     C                     IN   #DATA                  79
     C                     ENDIF
     C*
     C*   If no error or second time through, move on.
     C*
     C           *IN79     IFEQ *OFF
     C           FIRST     OREQ *OFF
     C                     LEAVE
     C                     ENDIF
     C*
     C*   Otherwise, create data area and try again.
     C*
     C                     CALL @CRTDA
     C                     MOVE *OFF      FIRST
     C*
     C                     ENDDO
     C*
     C* Now do the actual request ...
     C*
     C                     SELEC
     C*
     C*   Write out the data area for *SET with no retreival error.
     C*
     C           $PCMD     WHEQ '*SET'
     C           *IN79     ANDEQ*OFF
     C                     MOVEL$PLINE    #DATA
     C                     MOVE $PCOL     #DATA
     C                     OUT  #DATA
     C*
     C*   Get the values for *GET and no retreival error.
     C*
     C           $PCMD     WHEQ '*GET'
     C           *IN79     ANDEQ*OFF
     C                     MOVEL#DATA     $PLINE
     C                     MOVE #DATA     $PCOL
     C*
     C*   Set values to zeros for *GET with retreival error.
     C*
     C           $PCMD     WHEQ '*GET'
     C           *IN79     ANDEQ*ON
     C                     MOVE *ZEROS    $PLINE
     C                     MOVE *ZEROS    $PCOL
     C*
     C                     ENDSL
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     CSR                   ENDSR
