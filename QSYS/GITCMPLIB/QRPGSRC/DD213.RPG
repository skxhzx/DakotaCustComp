     F*----------------------------------------------------------------
     F*
     F*  DD213     Populate TIEHIGH file using occupied slot types
     F*  21 January, 1998
     F*  Roxanne Harrison
     F*    CREATED        4.08
     F*
     F*
     F*----------------------------------------------------------------
     F*  Revisions
410 AF*    12/16/98  RH   4.10
     F*      - Change to round length and width to next .5
     F*      - If high of tiehigh is >1, z-add 0 to high.
     F*
500 AF*    09/01/04  MLB  5.00
     F*      - Enh: Revise program to include aisle is slot def
     F*        selection. If passed in aisle isn't ALL h.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IP  E           K        DISK
     FSLOTDEF IF  E           K        DISK
     FPACKAGE IF  E           K        DISK
     FPALLET  IF  E           K        DISK
     FITEMDEF IF  E           K        DISK
     FSLOT3   IF  E           K        DISK
     FTIEHIGH UF  E           K        DISK                      A
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE  155
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
500 AC                     PARM           $PAISL  4
     C                     PARM           $PITEM 15
     C                     PARM           $HANG  155
     C                     PARM           $PCLRF  1
     C                     PARM           $NEWD   1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C           $NEWD     IFEQ 'Y'
     C           IDKEY     SETLLITEMDEF                  77
     C*   Match found
     C           *IN77     CABEQ*ON       SKIPIT
     C                     ENDIF
     C*
     C*
     C*   Round length and width.
     C*
     C                     Z-ADDITULEN    DECMAL  22
     C                     SUB  DECMAL    ITULEN
410 DC*          DECMAL    IFGE .5
410 DC*                    ADD  .5        ITULEN
410 DC*                    ENDIF
410 AC*
410 AC                     SELEC
410 AC           DECMAL    WHGT .5
410 AC                     ADD  1         ITULEN
410 AC           DECMAL    WHGT 0
410 AC                     ADD  .5        ITULEN
410 AC                     ENDSL
     C*
     C                     Z-ADDITUWDT    DECMAL  22
     C                     SUB  DECMAL    ITUWDT
410 DC*          DECMAL    IFGE .5
410 DC*                    ADD  .5        ITUWDT
410 DC*                    ENDIF
410 AC*
410 AC                     SELEC
410 AC           DECMAL    WHGT .5
410 AC                     ADD  1         ITUWDT
410 AC           DECMAL    WHGT 0
410 AC                     ADD  .5        ITUWDT
410 AC                     ENDSL
     C*
     C*   Loop through slots occupied by item.
     C           SLKEY     SETLLSLOT3                79
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           SLKEY     READESLOT3                    79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C           SLWHDP    IFNE ITWHDP
     C                     ITER
     C                     ENDIF
     C           SLSTAT    IFEQ 'V'
     C           SLSTAT    OREQ 'Z'
     C                     ITER
     C                     ENDIF
500 AC*
500 AC*    Only create definitions for selected aisle.
500 AC           $PAISL    IFNE '*ALL'
500 AC           SLAISL    ANDNEWKAISL
500 AC                     ITER
500 AC                     ENDIF
     C*
     C           SDKEY     CHAINSLOTDEF              76
     C           *IN76     IFEQ *OFF
     C                     EXSR ZZGET
     C                     EXSR ZZADD
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C           SKIPIT    TAG
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for PACKAGE file.
     C*
     CSR         PKKEY     KLIST
     CSR                   KFLD           SDPAL
     CSR                   KFLD           $PHANG
     CSR                   KFLD           ITULEN
     CSR                   KFLD           ITUWDT
     C*
     C*  Define key for SLOT3 file.
     C*
     CSR         SLKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           ITITEM
     C*
     C*  Define key for SLOTDEF file.
     C*
     CSR         SDKEY     KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLSDEF
     C*
     C*  Define key for TIEHIGH file.
     C*
     CSR         THKEY     KLIST
     CSR                   KFLD           THWHSE
     CSR                   KFLD           THITEM
     CSR                   KFLD           THWHDP
     CSR                   KFLD           THSTYP
     CSR                   KFLD           THSDEF
     CSR                   KFLD           THPAL
     CSR                   KFLD           THHANG
     C*
     C*  Define PARTIAL key for TIEHIGH file.
     C*
     CSR         THKEY1    KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*  Define key for PALLET file.
     C*
     CSR         PLKEY     KLIST
     CSR                   KFLD           SDPAL
     C*
     C*  Define key for ITEMDEF file.
     C*
     CSR         IDKEY     KLIST
     CSR                   KFLD           ITWHSE
     CSR                   KFLD           ITITEM
     C*
     CSR                   EXSR ZZINZ2
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZADD   Add records to TIEHIGH file.
     C*
     CSR         ZZADD     BEGSR
     CSR         THKEY     CHAINTIEHIGH              77
     CSR         *IN77     IFEQ *ON
     CSR         THTIE     ANDGT0
     CSR         THHIGH    ANDGT0
     CSR                   WRITETHREC
     CSR                   ENDIF
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZDEL   Delete records first that match selection criteria.
     C*
     CSR         ZZDEL     BEGSR
     CSR         THKEY1    SETLLTIEHIGH              79
     CSR                   MOVE *IN79     EOF     1
     CSR         EOF       DOWEQ*OFF
     CSR         THKEY1    READETIEHIGH                  79
     CSR                   MOVE *IN79     EOF
     CSR         *IN79     IFEQ *OFF
     C*
     CSR         $PWHDP    IFNE '*ALL '
     CSR         THWHDP    ANDNE$PWHDP
     CSR                   ITER
     CSR                   ENDIF
     C*
     CSR         $PSTYP    IFNE 'B'
     CSR         THSTYP    ANDNE$PSTYP
     CSR                   ITER
     CSR                   ENDIF
     C*
     CSR         $PITEM    IFNE '*ALL '
     CSR         THITEM    ANDNE$PITEM
     CSR                   ITER
     CSR                   ENDIF
     C*
     CSR                   DELETTHREC
     CSR                   ENDIF
     CSR                   ENDDO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZGET   Get all info needed for TIEHIGH record.
     C*
     CSR         ZZGET     BEGSR
     CSR                   Z-ADDITWHSE    THWHSE
     CSR                   MOVE ITWHDP    THWHDP
     CSR                   MOVE ITSTYP    THSTYP
     CSR                   MOVE ITITEM    THITEM
     CSR                   MOVE SDCODE    THSDEF
     CSR                   MOVE SDPAL     THPAL
     CSR                   Z-ADD$PHANG    THHANG
     CSR         PKKEY     SETLLPACKAGE              78
     CSR         *IN78     IFEQ *ON
     CSR                   Z-ADD0         THTIE
     CSR                   ELSE
     CSR         PKKEY     READEPACKAGE                  78
     CSR         *IN78     IFEQ *OFF
     CSR                   Z-ADDPKTIE     THTIE
     CSR                   ELSE
     CSR                   Z-ADD0         THTIE
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         PLKEY     CHAINPALLET               78
     CSR         *IN78     IFEQ *OFF
     CSR                   Z-ADDPLHGT     PALHGT  30
     CSR                   ELSE
     CSR                   Z-ADD0         PALHGT
     CSR                   ENDIF
     C*
     CSR         SDHGT     SUB  PALHGT    T#HGT   30
     CSR         ITUHGT    IFNE 0
     CSR         T#HGT     DIV  ITUHGT    THHIGH
     CSR                   ELSE
     CSR                   Z-ADD0         THHIGH
     CSR                   ENDIF
     C*
     CSR         THHIGH    IFLT 1
410 DC*R                   Z-ADD1         THHIGH
410 MCSR                   Z-ADD0         THHIGH
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZINZ2  Program specific initialization.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*  Delete existing records from tie/high file.
     C*
     CSR                   Z-ADD$WHSE     $PWHSE  30
     CSR                   Z-ADD$HANG     $PHANG  32
500 AC*
500 AC*    Move passed in Aisle to work field same size as aisle in slot.
500 AC           $PAISL    IFNE '*ALL'
500 AC                     MOVE $PAISL    WKAISL
500 AC           *LIKE     DEFN SLAISL    WKAISL
500 AC                     ENDIF
500 AC*
     CSR         $PCLRF    IFEQ 'Y'
     CSR                   EXSR ZZDEL
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
