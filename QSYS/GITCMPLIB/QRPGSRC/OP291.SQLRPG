     F*----------------------------------------------------------------
     F*
     F* ***** NOT USED ANYMORE *******
     F* ***** REPLACED WITH OP295 AS OF VER 5.00 ***********
     F*
     F*----------------------------------------------------------------
     F*----------------------------------------------------------------
     F*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  OP291     PIR Options - Update Item License Plate Tracking
     F*  Max Blacknik
     F*  25 November 2002
     F*
     F*----------------------------------------------------------------
     F*  Revisions:
     F*
417 AF*    11/25/02  MLB  4.17
     F*      - Created.
     F*
500 AF*    11/18/04  DAS  5.00
     F*      - Replaced by OP295.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions:
     F*
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     I*----------------------------------------------------------------
     ISQ1REC    E DSITEMMSC                   1
     ISQ2REC    E DSPIRITEM                   1
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   *CHANGE  - Update records
     C*      $PWHS   Warehouse
     C*      $PLPTR  Allow License Plate Tracking. (Y/N)
     C*      $PLPAL  Allow License Plate Tracking - All items. (Y/N)
     C*      $PLPDS  Allow L/P Tracking - Date sensitive items. (Y/N)
     C*      $PLPLC  Allow License Plate Tracking - Lot code items. (Y/N)
     C*      $PLPCW  Allow License Plate Tracking - Catchwgt items. (Y/N)
     C*
     C*    Returned Parameters
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PWHS   30
     C                     PARM           $PLPTR  1
     C                     PARM           $PLPAL  1
     C                     PARM           $PLPDS  1
     C                     PARM           $PLPLC  1
     C                     PARM           $PLPCW  1
     C                     PARM           $PRTN   8
     C*
     C           *LIKE     DEFN $PWHS     $PWHSE
     C                     Z-ADD$PWHS     $PWHSE
     C*
     C                     SELEC
     C*
     C*----------------------------------------------------------------
     C*  Update records - License plate tracking - No Items.
     C           $PLPTR    WHEQ 'N'
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'N'
     C+  WHERE  (IMWHSE = :$PWHSE  AND  IMLTRK <> 'N')
     C/END-EXEC
     C*
     C*----------------------------------------------------------------
     C*  Update records - License plate tracking - All Items.
     C           $PLPTR    WHEQ 'Y'
     C           $PLPAL    ANDEQ'Y'
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'Y'
     C+  WHERE  (IMWHSE = :$PWHSE  AND  IMLTRK <> 'Y')
     C/END-EXEC
     C*
     C*----------------------------------------------------------------
     C*  Update records - License plate tracking - Specific groups.
     C           $PLPTR    WHEQ 'Y'
     C           $PLPAL    ANDEQ'N'
     C*
     C* Reset all items that are currently flagged for tracking.
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'N'
     C+  WHERE  (IMWHSE = :$PWHSE  AND  IMLTRK <> 'N')
     C/END-EXEC
     C*
     C*  Update records - License plate tracking - Date sensitive items.
     C           $PLPDS    IFEQ 'Y'
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'Y'
     C+  WHERE EXISTS
     C+  (SELECT ITFLGD FROM PIRITEM
     C+  WHERE IMWHSE = :$PWHSE AND IMWHSE = ITWHSE AND IMITEM = ITITEM
     C+  AND ITFLGD = 'Y')
     C/END-EXEC
     C                     ENDIF
     C*
     C*  Update records - License plate tracking - Lot code items.
     C           $PLPLC    IFEQ 'Y'
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'Y'
     C+  WHERE  IMWHSE = :$PWHSE  AND IMLOT = 'Y'
     C/END-EXEC
     C                     ENDIF
     C*
     C*  Update records - License plate tracking - Catchweight items.
     C           $PLPCW    IFEQ 'Y'
     C/EXEC SQL
     C+  UPDATE ITEMMSC
     C+  SET IMLTRK = 'Y'
     C+  WHERE EXISTS
     C+  (SELECT ITCWGT FROM PIRITEM
     C+  WHERE IMWHSE = :$PWHSE AND IMWHSE = ITWHSE AND IMITEM = ITITEM
     C+  AND ITCWGT = 'Y')
     C/END-EXEC
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C                     ENDSL
     C*
     C                     RETRN
