     F*----------------------------------------------------------------
     F*
     F*  TH122     Export item tie/high to item file
     F*  30 April, 1996
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
213 AF*    06/14/96  DAS
     F*      - Changed default for IDPREF to ' ' from 'N'.in ZZGET.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FTIEHIGH IP  E           K        DISK
     FITEMDEF UF  E           K        DISK                      A
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE  155
     C                     PARM           $PWHDP  5
     C                     PARM           $PITEM 15
     C                     PARM           $PREPL  1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C           *LIKE     DEFN THITEM    SVITEM
     C*
     C*  - If replace option = Yes, then delete existing records
     C*    from ITEMDEF file for an item.
     C*  - Otherwise, count the existing records in ITEMDEF file for
     C*    an item.
     C*
     C                     Z-ADD$WHSE     $PWHSE  30
     C           THITEM    IFNE SVITEM
     C                     Z-ADD1         N       30
     C           $PREPL    IFEQ 'Y'
     C                     EXSR ZZDEL
     C                     ELSE
     C                     EXSR ZZINZ
     C                     ENDIF
     C                     MOVE THITEM    SVITEM
     C                     ENDIF
     C*
     C*  - An item can only have a maximum of 5 definitions.
     C*
     C           N         IFLE 5
     C                     EXSR ZZGET
     C                     EXSR ZZADD
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for ITEMDEF file.
     C*
     CSR         IDKEY     KLIST
     CSR                   KFLD           THWHSE
     CSR                   KFLD           THITEM
     CSR                   KFLD           THSDEF
     C*
     C*  Define PARTIAL key for ITEMDEF file.
     C*
     CSR         IDKEY1    KLIST
     CSR                   KFLD           THWHSE
     CSR                   KFLD           THITEM
     C*
     C*  Define PARTIAL key for TIEHIGH file.
     C*
     CSR         THKEY1    KLIST
     CSR                   KFLD           $PWHSE
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZGET   Get all info needed for ITEMDEF record.
     C*
     CSR         ZZGET     BEGSR
     CSR                   Z-ADDTHWHSE    IDWHSE
     CSR                   MOVE THITEM    IDITEM
     CSR                   MOVE THSDEF    IDSDEF
     CSR                   Z-ADDTHTIE     IDTIE
     CSR                   Z-ADDTHHIGH    IDHIGH
     CSR                   MOVE 'N'       IDUM
213 DC*                    MOVE 'N'       IDPREF
213 MCSR                   MOVE ' '       IDPREF
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZADD   Add records to ITEMDEF file.
     C*
     CSR         ZZADD     BEGSR
     CSR         IDKEY     CHAINITEMDEF              77
     CSR         *IN77     IFEQ *ON
     CSR                   WRITEIDREC
     CSR                   ADD  1         N
     CSR                   ENDIF
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZDEL   Delete records from ITEMDEF file, if replace option= Y
     C*
     CSR         ZZDEL     BEGSR
     CSR         IDKEY1    SETLLITEMDEF              79
     CSR                   MOVE *IN79     EOF     1
     CSR         EOF       DOWEQ*OFF
     CSR         IDKEY1    READEITEMDEF                  79
     CSR                   MOVE *IN79     EOF
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETIDREC
     CSR                   ENDIF
     CSR                   ENDDO
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZINZ   Count existing number of records.
     C*
     CSR         ZZINZ     BEGSR
     CSR         IDKEY1    SETLLITEMDEF              79
     CSR                   MOVE *IN79     EOF     1
     CSR         EOF       DOWEQ*OFF
     CSR         IDKEY1    READEITEMDEF                  79
     CSR                   MOVE *IN79     EOF
     CSR         *IN79     IFEQ *OFF
     CSR                   ADD  1         N
     CSR                   ENDIF
     CSR                   ENDDO
     CSR                   ENDSR
     C*----------------------------------------------------------------
