     F*----------------------------------------------------------------
     F*
     F*  UTNF001   Reset DRY picking sequences - NorthFarm custom mod
     F*  10 October 2000
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT1   UF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Hard code warehouse and department.
     C*
     C                     Z-ADD1         KYWHSE
     C                     MOVE 'DRY  '   KYWHDP    P
     C*
     C*  Resequence each aisle in department.
     C*
     C*    - Using Z-SUB 2000 will reverse the picking sequence
     C*      order verses the actual slot location (SLLOC).
     C*
     C*    - Up/Back is from front of warehouse.
     C*
     C*
     C*    Aisle DE - Up even, Back odd
     C*
     C                     MOVE ' DE'     KYAISL
     C                     Z-SUB2000      INCODD  50       155-105
     C                     Z-ADD0         INCEVN  50       118-146
     C                     EXSR RESEQ
     C*
     C*    Aisle DF - Up odd, Back even
     C*
     C                     MOVE ' DF'     KYAISL
     C                     Z-SUB2000      INCODD           555-505
     C                     Z-ADD2000      INCEVN           506-556
     C                     EXSR RESEQ
     C*
     C*    Aisle DG - Up even, Back odd
     C*
     C                     MOVE ' DG'     KYAISL
     C                     Z-SUB2000      INCODD           155-105
     C                     Z-ADD0         INCEVN           106-156
     C                     EXSR RESEQ
     C*
     C*    Aisle DH - Up odd, Back even
     C*
     C                     MOVE ' DH'     KYAISL
     C                     Z-SUB2000      INCODD           555-505
     C                     Z-ADD2000      INCEVN           506-556
     C                     EXSR RESEQ
     C*
     C*    Aisle DJ - Up even, Back odd
     C*
     C                     MOVE ' DJ'     KYAISL
     C                     Z-SUB2000      INCODD           151-103
     C                     Z-ADD0         INCEVN           106-156
     C                     EXSR RESEQ
     C*
     C*    Aisle DK - Up odd, Back even
     C*
     C                     MOVE ' DK'     KYAISL
     C                     Z-SUB2000      INCODD           153-105
     C                     Z-ADD2000      INCEVN           102-152
     C                     EXSR RESEQ
     C*
     C*    Aisle DV - Up odd, Back even
     C*
     C                     MOVE ' DV'     KYAISL
     C                     Z-ADD0         INCODD           101-183
     C                     Z-SUB2000      INCEVN           184-102
     C                     EXSR RESEQ
     C*
     C*    Aisle DW - Up odd, Back even
     C*
     C                     MOVE ' DW'     KYAISL
     C                     Z-ADD0         INCODD           101-183
     C                     Z-SUB2000      INCEVN           184-102
     C                     EXSR RESEQ
     C*
     C*    Aisle DX - Up odd, Back even
     C*
     C                     MOVE ' DX'     KYAISL
     C                     Z-ADD0         INCODD           101-183
     C                     Z-SUB2000      INCEVN           184-102
     C                     EXSR RESEQ
     C*
     C*    Aisle DY - Up odd, Back even
     C*
     C                     MOVE ' DY'     KYAISL
     C                     Z-ADD0         INCODD           101-141
     C                     Z-SUB2000      INCEVN           142-102
     C                     EXSR RESEQ
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C                     MOVE *ON       FOREVR  1
     C*
     C*  Define partial key for SLOT1.
     C*
     C           *LIKE     DEFN SLWHSE    KYWHSE
     C           *LIKE     DEFN SLWHDP    KYWHDP
     C           *LIKE     DEFN SLAISL    KYAISL
     C           SLOTKY    KLIST
     C                     KFLD           KYWHSE
     C                     KFLD           KYWHDP
     C                     KFLD           KYAISL
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  RESEQ   Initialization subrotine
     C*
     CSR         RESEQ     BEGSR
     C*
     C*  Loop through all the records for the aisle.
     C*
     CSR         SLOTKY    SETLLSLOT1                7979
     CSR         FOREVR    DOWEQFOREVR
     CSR         SLOTKY    READESLOT1                    79
     CSR         *IN79     IFEQ *ON
     CSR                   LEAVE
     CSR                   ENDIF
     C*
     C*     Change picking sequence to slot location.
     C*
     CSR         SLSIDE    IFEQ 1                          Odd
     CSR         SLLOC     ADD  INCODD    SLPSEQ
     CSR                   ELSE                            Even
     CSR         SLLOC     ADD  INCEVN    SLPSEQ
     CSR                   ENDIF
     C*
     CSR         SLPSEQ    IFLT 0
     CSR                   Z-SUBSLPSEQ    SLPSEQ
     CSR                   ENDIF
     C*
     C*     Update slot record with new sequence.
     C*
     CSR                   UPDATSLREC
     C*
     CSR                   ENDDO
     CSR                   ENDSR
