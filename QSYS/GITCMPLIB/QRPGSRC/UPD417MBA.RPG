     F*----------------------------------------------------------------
     F*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  UPD417MBA  Initialize fields in *RCVLBL by warehouse.
     F*  11 April 2003
     F*  Max Blacknik
     F*
     F*  Revisions
417 AF*    04/11/03  MLB  4.17
     F*      - Initialize OPRUPC, OPRUPS and OPRUPF.
417aAF*    02/01/04  MLB  4.17a
     F*      - Initialize OPMSPO.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FWAREHS  IF  E           K        DISK
     FOPTIONS UF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*----------------------------------------------------------------
     I*  *RCVLBL  -  Receiving options
     I*----------------------------------------------------------------
     I*
217 AI*    11/ 6/96  RH
     I*      - Added OPPASL & OPPFRC.
221 AI*    05/14/97  RH
     I*      - Added OPPLCN
409 AI*    04/09/98  HNK
     I*      - Added OPPCHM
     I*      - Added OPPMIX
412 AI*    08/19/99  MLB
     I*      - Added OPAPCK
414 AI*    10/09/00  DAS
     I*      - Added OPFRCM
416 AI*    09/24/01  DAS
     I*      - Added OPEXCK, OPCWEN, OPCWTP, OPCWFR
416gAI*    12/12/01  DAS
     I*      - Added OPIPCK
     I*
417 AI*    04/11/03  MLB
     I*      - Added OPRUPC, OPRUPS, OPRUPF.
417aAI*    02/01/04  MLB
     I*      - Added OPMSPO.
     I*
     I* Fields
     I*
     I*    OPLPGM  -  Receiving label program.
     I*    OPUPCT  -  Ideal slot usage percentage.
     I*    OPPDEF  -  PIR minimum number of slots priority.
     I*    OPPBLD  -  PIR same build code priority.
     I*    OPPACT  -  PIR same activity zone priority.
     I*    OPFDEF  -  Fixed minimum number or slots priority.
     I*    OPFASL  -  Fixed same aisle priority.
     I*    OPFSID  -  Fixed same side priority.
     I*    OPFLVL  -  Fixed highest level priority.
217 AI*    OPPASL  -  PIR same aisle priority.
217 AI*    OPPFRC  -  Force PIR same aisle priority.
221 AI*    OPPLCN  -  Use generic license plates.
409 AI*    OPPCHM  -  Check chimney (Y,N)
409 AI*    OPPMIX  -  Allow mixed expiration dates in fixed home slot(Y,N)
412 AI*    OPAPCK  -  Assign pick slots automatically for fixed items(Y,N)
414 AI*    OPFRCM  -  Force marking of all P.O. detail lines (Y,N)
416 AI*    OPEXCK  -  Expiration date check (1,2,3)
416 AI*                What to do when exp date being rcvd comes
416 AI*                earlier than existing overflow exp date ...
416 AI*                  1 = Don't allow it.
416 AI*                  2 = Warn user & log when overriden
416 AI*                  3 = Let it go through.
416 AI*    OPCWEN  -  Allow catch weight entry (1,2,3)
416 AI*                  1 = Every receipt
416 AI*                  2 = First receipt only
416 AI*                  3 = Never
416 AI*    OPCWTP  -  Catch weight value entered (1,2,3)
416 AI*                  1 = User choice
416 AI*                  2 = Average
416 AI*                  3 = Total
416 AI*    OPCWFR  -  Force catch weight entry (Y,N)
416gAI*    OPIPCK  -  Ignore existing pick slots (Y,N)
417 AI*    OPRUPC  -  Prompt user for Retail UPC during receiving? (Y,N)
417 AI*    OPRUPS  -  Allow user to skip Retail UPC scan? (Y,N)
417 AI*    OPRUPF  -  Require Retail UPC on file before receipt? (Y,N)
417aAI*    OPMSPO  -  Allow single po attached to multiple Super P/Os? (Y/N)
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I                                        1  10 OPLPGM
     I                                       11  130OPUPCT
     I                                       14  140OPPDEF
     I                                       15  150OPPBLD
     I                                       16  160OPPACT
     I                                       17  170OPFDEF
     I                                       18  180OPFASL
     I                                       19  190OPFSID
     I                                       20  200OPFLVL
217 AI                                       21  210OPPASL
217 AI                                       22  22 OPPFRC
221 AI                                       23  23 OPPLCN
409 AI                                       24  24 OPPCHM
409 AI                                       25  25 OPPMIX
412 AI                                       26  26 OPAPCK
414 AI                                       27  27 OPFRCM
416 AI                                       28  28 OPEXCK
416 AI                                       29  29 OPCWEN
416 AI                                       30  30 OPCWTP
416 AI                                       31  31 OPCWFR
416gAI                                       32  32 OPIPCK
417 AI                                       33  33 OPRUPC
417 AI                                       34  34 OPRUPS
417 AI                                       35  35 OPRUPF
417aAI                                       36  36 OPMSPO
     I                                      117 117 OPTEND
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *OFF      *IN79
     C           *LIKE     DEFN OPCODE    KYCODE
     C           OPKEY     KLIST
     C                     KFLD           WHCODE
     C                     KFLD           KYCODE
     C                     MOVE '*RCVLBL 'KYCODE
     C*
     C           *LOVAL    SETLLWAREHS
     C           *IN79     DOWEQ*OFF
     C                     READ WAREHS                   79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE                           EXIT LOOP
     C*
     C                     OTHER
     C*
     C           OPKEY     CHAINOPTIONS              78
     C                     SELEC
     C           *IN78     WHEQ *OFF
     C           OPRUPC    IFEQ ' '
     C                     MOVE 'N'       OPRUPC
     C                     ENDIF
     C           OPRUPS    IFEQ ' '
     C                     MOVE 'N'       OPRUPS
     C                     ENDIF
     C           OPRUPF    IFEQ ' '
     C                     MOVE 'N'       OPRUPF
     C                     ENDIF
417aAC           OPMSPO    IFEQ ' '
417aAC                     MOVE 'N'       OPMSPO
417aAC                     ENDIF
     C                     UPDATOPREC
     C*
     C                     ENDSL
     C                     ENDSL
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*
