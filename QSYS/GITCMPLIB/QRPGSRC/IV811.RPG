     F*----------------------------------------------------------------
     F*
     F*  IV811     Cleanup slot, label and Trans file for Full Inv
     F*  31 August, 1999
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*
     F*  Revisions
412 AF*    08/31/99  HNK  4.12
     F*      - Created.
412aAF*    09/10/99  HNK  4.12a
     F*      - Changed to allow cleanup by dept.
640aAF*    06/07/12  LMC  6.40a
     F*      - Enh: Added call to WRTTRAN1 for PIRTRAND.
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  78        Chain indicator
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT2   UF  E           K        DISK
     FLABEL   UF  E           K        DISK
     FPIRTRAN UF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     I*----------------------------------------------------------------
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I                                    P   1   20ERWHSE
     I                                        3   7 ERWHDP
     I                                        8  12 ERSTYP
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse
     C*      $PJOBQ  Running from JOBQ (Y,N)
     C*
     C*    Returned Parameters
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
412aAC                     PARM           $PWHDP  5
     C*                    PARM           $PJOBQ  1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*   Cleanup slot qty's
     C*
     C           SLKEY     SETLLSLOT2
     C           FOREVR    DOUNEFOREVR
     C*
     C           SLKEY     READESLOT2                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
412aAC           $PWHDP    IFNE '*ALL '
412aAC           SLWHDP    ANDNE$PWHDP
412aAC                     ITER
412aAC                     ENDIF
     C*
     C*  Zero out all qty's except stock quantity.
     C*
     C                     Z-ADD0         SLALC1
     C                     Z-ADD0         SLALC2
     C                     Z-ADD0         SLALC3
     C                     Z-ADD0         SLPCK1
     C                     Z-ADD0         SLPCK2
     C                     Z-ADD0         SLPCK3
     C                     Z-ADD0         SLRCV1
     C                     Z-ADD0         SLRCV2
     C                     Z-ADD0         SLRCV3
     C                     Z-ADD0         SLTFR1
     C                     Z-ADD0         SLTFR2
     C                     Z-ADD0         SLTFR3
     C                     UPDATSLREC
     C*
     C                     ENDDO
     C*
     C*   Cleanup label records. Close all label records.
     C*
     C           *LOVAL    SETLLLABEL
     C           FOREVR    DOUNEFOREVR
     C*
     C                     READ LABEL                    78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
412aAC           $PWHDP    IFNE '*ALL '
412aAC           LBWHDP    ANDNE$PWHDP
412aAC                     ITER
412aAC                     ENDIF
     C*
     C*  Update open label records.
     C*
     C           LBWHSE    IFEQ $PWHSE
     C           LBSTAT    ANDNE'C'
     C                     MOVE 'C'       LBSTAT
     C                     Z-ADDTODAY     LBSDTE
     C                     Z-ADDCURTIM    LBSTIM
     C                     UPDATLBREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*   Cleanup transaction records. Close all trans records.
     C*
     C           *LOVAL    SETLLPIRTRAN
     C           FOREVR    DOUNEFOREVR
     C*
     C                     READ PIRTRAN                  78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
412aAC           $PWHDP    IFNE '*ALL '
412aAC           PTWHDP    ANDNE$PWHDP
412aAC                     ITER
412aAC                     ENDIF
     C*
     C*  Update open label records.
     C*
     C           PTWHSE    IFEQ $PWHSE
     C           PTSTAT    ANDNE'4'
     C                     MOVE '4'       PTSTAT
     C                     Z-ADDTODAY     PTEDTE
     C                     Z-ADDCURTIM    PTETIM
     C                     Z-ADDTODAY     PTCDTE
     C                     Z-ADDCURTIM    PTCTIM
     C                     UPDATPTREC
640aAC                     EXSR ZZWRTT
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     CSR                   MOVE #PROG     #PGMQ
     C*
     C*  Define keys.
     C*
     C*    Slot file (Partial key)
     C*
     CSR         SLKEY     KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*    Label file
     C*
     CSR         LBKEY     KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*    Transaction file (Partial key)
     C*
     CSR         PTKEY     KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     CSR                   TIME           CURTIM  60
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCPMQ  Clear program message queue.
     C*
     CSR         ZMCPMQ    BEGSR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     C*R         $PJOBQ    IFNE 'Y'
     C*R                   MOVE '1'       *IN97
     C*R                   WRITEMSGCTL
     C*R                   ENDIF
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ   4
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
640aAC*----------------------------------------------------------------*********
640aAC*
640aAC*  ZZWRTT     Execute new WRTTRAN1 program to add/update PIRTRANDn
640aAC*
640aAC           ZZWRTT    BEGSR
640aAC                     MOVEL'*ONETRAN'WPCMD  10
640aAC                     Z-ADD0         WPWAIT  20
640aAC                     MOVELPTWHSE    WPWHSE  30
640aAC                     MOVELPTTRN#    WPTRN#  70
640aAC                     Z-ADD0         WPFRDT  80
640aAC                     Z-ADD0         WPTODT  80
640aAC                     Z-ADD0         WPEMP#  50
640aAC                     CALL 'WRTTRAN1'
640aAC                     PARM           WPCMD
640aAC                     PARM           WPWAIT
640aAC                     PARM           WPWHSE
640aAC                     PARM           WPTRN#
640aAC                     PARM           WPFRDT
640aAC                     PARM           WPTODT
640aAC                     PARM           WPEMP#
640aAC                     ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
