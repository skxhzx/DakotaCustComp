     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  ER230     Thru-put and number of cases selected
     F*  January 15, 1996
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
     F*    03/24/97  RH   2.19
     F*      - Change ER23001 to ER21001.                              d
     F*
409 AF*    06/05/98  HNK  4.09
     F*      - Recompiled.                                             d
     F*
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*       The format for this program needs to be compiled with
     F*       DFRWRT=*NO so that status messages can be displayed.
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FER230FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
    DF*                                       MSGK  KSFILE MSGREC
     FER231   O   E                    DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    WKB        99  8 0
     E                    WKE        99  8 0
     E                    $MD       128  1
     E                    DESC    1  11 60
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   6 T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  24 $LCAT
     I*
     I                                       19  210$LWHSE
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   40ERDATE
     I                                        5  14 ERDAY
     I*
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     ISQ1REC    E DSPIRTRAN                   1
     ISQ2REC    E DSTASKDET                   1
     ISQ3REC    E DSPIRUSER                   1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $PFMDT  From date (YYYYMMDD)
     C*      $PTODT  To date (YYYYMMDD)
     C*      $PWEEK  Number of weeks
     C*      $PWHSE  Warehouse
     C*      $PJOBQ  Put on jobq (Y,N)
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PFMDT  8
     C                     PARM           $PTODT  8
     C                     PARM           $PWEEK  2
     C                     PARM           $PWHSE  3
     C                     PARM           $PJOBQ  1
     C                     PARM           $PRTN   8
     C                     PARM           $PDEV  10
     C                     PARM           $PFORM 10
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     EXSR ZMCPMQ
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
    MCSR                   EXSR ZMCPMQ
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     C*  Put out info message.
     CSR                   EXSR ZM2102
     CSR                   EXSR ZMDMSG
     CSR                   EXSR ZMCPMQ
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C                     Z-ADD0         GTDATE  80
     C                     MOVE *BLANKS   USNAME 40
     C*
     C*  Initialize message handling
     C*
    DC**                   EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*
     C*    ZMCPMQ  Clear program message queue.
     C*
     CSR         ZMCPMQ    BEGSR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR                   WRITEMSGCTL
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*
     C*    PIR9901  From date greater than To date.
     C*
     CSR         ZM9901    BEGSR
     CSR                   MOVE 'PIR9901' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR0110  Number must be >0.
     C*
     CSR         ZM0110    BEGSR
     CSR                   MOVE 'PIR0110' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ER21001  Start date must be a sunday.
     C*
     CSR         ZM2101    BEGSR
219 DC*R                   MOVE 'ER23001' #MSGID
219 MCSR                   MOVE 'ER21001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ER21002  Records are being selected. (Info message)
     C*
     CSR         ZM2102    BEGSR
     CSR                   MOVE 'ER21002' #MSGID
     CSR                   MOVE '*INFO  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         GTOJ$     BEGSR
     CSR                   MOVE G$DATE    G$DATE  60
     C* SPLIT DATE INTO MONTH, DAY, YEAR FIELDS
     CSR         DTFRMT    IFEQ 1                          BLVL-01
     CSR                   MOVELG$DATE    G$YY    20
     CSR                   MOVE G$DATE    WORK4   4
     CSR                   MOVELWORK4     G$MM    20
     CSR                   MOVE WORK4     G$DD    20
     CSR                   ELSE                            EELS-01
     CSR                   MOVELG$DATE    G$MM
     CSR                   MOVE G$DATE    WORK4
     CSR                   MOVELWORK4     G$DD
     CSR                   MOVE WORK4     G$YY
     CSR                   END                             EEND-01
     C* CREATE 4 DIGIT YEAR
     CSR                   MOVE G$YY      G$YYYY  40
     CSR         G$YY      IFGE 60                         BLVL-01
     CSR                   MOVEL'19'      G$YYYY
     CSR                   ELSE                            EELS-01
     CSR                   MOVEL'20'      G$YYYY
     CSR                   END                             EEND-01
     CSR                   Z-ADD0         G$JD
     CSR         G$MM      SUB  3         G$MMWK  20
     CSR                   Z-ADDG$MMWK    GMWKSV  20
     CSR         G$MMWK    IFLT 0                           LVL-01
     CSR                   ADD  12        G$MMWK
     CSR                   END                              END-01
     CSR         GMWKSV    IFNE 0                           LVL-01
     CSR         G$MMWK    MULT 30.6      G$JD      H
     CSR                   END                              END-01
     CSR                   ADD  G$DD      G$JD    50
     CSR         G$YYYY    SUB  1900      G$YYWK  30
     CSR         G$YYWK    IFNE 0                           LVL-01
     CSR         GMWKSV    IFLT 0                           LVL-02
     CSR                   SUB  1         G$YYWK
     CSR                   END                              END-02
     CSR                   END                              END-01
     CSR         G$YYWK    MULT 365.25    G$JYD   70
     CSR                   ADD  G$JYD     G$JD
     CSR         G$JD      DIV  7         G$WK7   70
     CSR                   MVR            G$DW    10
     C* MAKE SUNDAY = 1ST DAY OF THE WEEK
     CSR         G$DW      IFLE 3
     CSR                   ADD  4         G$DW
     CSR                   ELSE
     CSR                   SUB  3         G$DW
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  JTOJ$    Julian to Julian date conversion
     C*
     CSR         JTOG$     BEGSR
     CSR         J$JD      DIV  365.25    J$YYWK  30
     CSR         J$JD      DIV  365.25    J$TST   99
     CSR         J$TST     IFEQ 0                           LVL-01
     CSR                   SUB  1         J$YYWK
     CSR                   END                              END-01
     CSR         J$YYWK    MULT 365.25    J$YD    70
     CSR         J$JD      SUB  J$YD      J$YD
     CSR         J$YD      IFGT 306                         LVL-01
     CSR                   ADD  1         J$YYWK
     CSR                   END                              END-01
     CSR                   Z-ADD0         J$X     20
     CSR         J$YD      DOULEJ$MD                        LVL-01
     CSR                   ADD  1         J$X
     CSR         J$X       MULT 30.6      J$MD    30H
     CSR                   END                              END-01
     CSR                   SUB  1         J$X
     CSR         J$X       MULT 30.6      J$MD      H
     CSR         J$YD      SUB  J$MD      J$DD    20
     CSR         J$X       ADD  3         J$MM    20
     CSR         J$MM      IFGT 12                          LVL-01
     CSR                   SUB  12        J$MM
     CSR                   END                              END-01
     CSR         J$YYWK    ADD  1900      J$YYYY  40
     CSR         DTFRMT    IFEQ 1                          BLVL-01
     CSR                   MOVE J$YYYY    WORK2   2
     CSR                   MOVELJ$MM      WORK4   4
     CSR                   MOVE J$DD      WORK4
     CSR                   MOVELWORK2     J$DATE  60
     CSR                   MOVE WORK4     J$DATE
     CSR                   ELSE                            EELS-01
     CSR                   MOVE J$YYYY    WORK2
     CSR                   MOVELJ$MM      WORK4
     CSR                   MOVE J$DD      WORK4
     CSR                   MOVE WORK2     J$DATE
     CSR                   MOVELWORK4     J$DATE
     CSR                   END                             EEND-01
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify From Date.
     C*
     CSR         W1FMDT    IFNE 0
     CSR                   MOVE '*MDYEDT '$CVCMD
     CSR                   MOVE W1FMDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $CVMSG    #MSGK
    DC*                    EXSR ZMQMSG
     CSR                   ELSE
     CSR                   MOVE $CVD6O    W1FMDT
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Start day must be a sunday
     C*
     CSR                   MOVE W1FMDT    G$DATE
     CSR                   Z-ADD2         DTFRMT  10
     CSR                   EXSR GTOJ$
     CSR         G$DW      IFNE 1
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   Z-ADDW1FMDT    ERDATE
     C* Monday
     CSR         G$DW      IFEQ 2
     CSR                   MOVELDESC,5    ERDAY
     CSR                   ENDIF
     C* Tuesday
     CSR         G$DW      IFEQ 3
     CSR                   MOVELDESC,6    ERDAY
     CSR                   ENDIF
     C* Wednesday
     CSR         G$DW      IFEQ 4
     CSR                   MOVELDESC,7    ERDAY
     CSR                   ENDIF
     C* Thursday
     CSR         G$DW      IFEQ 5
     CSR                   MOVELDESC,8    ERDAY
     CSR                   ENDIF
     C* Friday
     CSR         G$DW      IFEQ 6
     CSR                   MOVELDESC,9    ERDAY
     CSR                   ENDIF
     C* Saturday
     CSR         G$DW      IFEQ 7
     CSR                   MOVELDESC,10   ERDAY
     CSR                   ENDIF
     C*
     CSR                   EXSR ZM2101
     CSR                   ENDIF
     C*
     C*  Number of weeks must be > 0.
     C*
     CSR         W1WEEK    IFLE 0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,11   ERRMSG
     CSR                   EXSR ZM0110
     CSR                   ENDIF
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $LMSG     #MSGK
    DC*                    EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN25
     CSR                   MOVE *ON       *IN05
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,1    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default device
     C*
     CSR                   MOVELW1DEV     WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1DEV     ANDNE'*SYSVAL'
     CSR         W1DEV     ANDNE'*JOB'
     CSR         W1DEV     OREQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN26
     CSR                   MOVE *ON       *IN06
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,2    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default form type
     C*
     CSR                   MOVELW1FORM    WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1FORM    ANDNE'*STD'
     CSR         W1FORM    OREQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN27
     CSR                   MOVE *ON       *IN07
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Warehouse code.
     C*
     CSR         ROW#      WHEQ 10
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   END
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   GOTO ENDCM1
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     CSR                   MOVE *BLANKS   W1FMDT
     CSR                   MOVE *BLANKS   W1WEEK
     CSR                   MOVE 'N'       W1JOBQ
     C*   GET REPORT TITLE
     CSR                   CALL 'GETRPTE'
     CSR                   PARM '*DESC  ' $CMD    8
     CSR                   PARM 'ER231PR' $LRPT  10
     CSR                   PARM 0         $WHSE   30
     CSR                   PARM ' '       $STYP   1
     CSR                   PARM ' '       $WHDP   5
     CSR                   PARM           $DESC  50
     CSR                   PARM           $DEV   10
     CSR                   PARM           $FORM  10
     CSR                   MOVEL$DESC     RPTTTL
     C*
     C*   Center report title.
     C*
     CSR                   MOVELRPTTTL    $PSTR  60
     CSR                   Z-ADD50        $PLEN   30
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*   GET DEVICE AND FORM
     CSR                   CALL 'GETRPTE'
     CSR                   PARM '*DEVFORM'$CMD    8
     CSR                   PARM 'ER231PR' $LRPT  10
     CSR                   PARM 0         $WHSE   30
     CSR                   PARM ' '       $STYP   1
     CSR                   PARM ' '       $WHDP   5
     CSR                   PARM           $DESC  50
     CSR                   PARM           $DEV   10
     CSR                   PARM           $FORM  10
     CSR                   MOVEL$DEV      W1DEV
     CSR                   MOVEL$FORM     W1FORM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZZFILL    BEGSR
     C*
     CSR                   MOVE '*LFM    '$CMD    8
     CSR                   MOVE *BLANKS   $NAME  40
     CSR                   CALL 'FRMTNM'
     CSR                   PARM           $CMD
     CSR                   PARM           USFNAM
     CSR                   PARM           USINIT
     CSR                   PARM           USLNAM
     CSR                   PARM           $NAME
     CSR                   MOVEL$NAME     USNAME
     C*
     CSR                   Z-ADD0         STDPPM  95
     CSR                   Z-ADD0         ACTPPM  95
     C*   Std pcs per hr / 60 =  Std pcs per min
     CSR         TDSTDP    DIV  60        STDPPM    H
     C*   Pcs picked / Min =  Pcs picked per min
     CSR         PTDMIN    IFGT 0
     CSR         PTPCS     DIV  PTDMIN    ACTPPM    H
     CSR                   ENDIF
     CSR         PTIMIN    IFGT 0
     CSR         PTPCS     DIV  PTIMIN    ACTPPM    H
     CSR                   ENDIF
     C*   Pcs picked per min / Std pcs per min = % of standard
     CSR         STDPPM    IFGT 0
     CSR         ACTPPM    DIV  STDPPM    T#PCNT  95H
     CSR         T#PCNT    MULT 100       PCNT    52
     CSR                   ELSE
     CSR                   Z-ADD0         PCNT
     CSR                   ENDIF
     C*
     CSR                   Z-ADDPTWHSE    WKWHSE
     CSR                   Z-ADDPTEMP#    WKEMP#
     CSR                   MOVELUSNAME    WKNAME
     CSR                   Z-ADDGTDATE    WKGDTE
     CSR                   Z-ADDPTPCS     WKPCS
     CSR                   MOVELTDCODE    WKCODE
     CSR                   Z-ADDTDSTDP    WKSTDP
     CSR                   Z-ADDPTDMIN    WKDMIN
     CSR                   Z-ADDPTIMIN    WKIMIN
     C* Find which week the date belongs to.
     CSR                   DO   W1WEEK    N
     CSR         GTDATE    IFGE WKB,N
     CSR         GTDATE    ANDLEWKE,N
     CSR                   Z-ADDN         WKWNUM
     CSR                   LEAVE
     CSR                   ENDIF
     CSR                   ENDDO
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE #PROG     #PGMQ
     CSR                   MOVE #PROG     $CVPRG
     CSR                   MOVE #PROG     $LPRG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1FMDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    FROMDT  80
     CSR                   MOVE $CVD8O    $PFMDT
     CSR                   MOVE W1WEEK    $PWEEK
     CSR                   MOVE W1WHSE    $PWHSE
     CSR                   MOVE W1JOBQ    $PJOBQ
     CSR                   MOVE W1DEV     $PDEV
     CSR                   MOVE W1FORM    $PFORM
     CSR                   MOVE '*OK     '$PRTN
     C*
     CSR                   EXSR ZZWARY
     CSR                   EXSR ZZWORK
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZWARY  Fill week array
     C*
     CSR         ZZWARY    BEGSR
     CSR                   MOVE W1FMDT    G$DATE
     CSR                   Z-ADD2         DTFRMT  10
     CSR                   EXSR GTOJ$
     CSR                   Z-ADDG$JD      BEGJUL  50
     C*
     C*  Find end date of the last week.
     C*
     CSR                   Z-ADD0         NDAYS   50
     CSR         W1WEEK    SUB  1         NWEEK   20
     CSR         NWEEK     MULT 7         NDAYS
     CSR                   ADD  6         NDAYS
     CSR         BEGJUL    ADD  NDAYS     ENDJUL
     CSR                   Z-ADDENDJUL    J$JD    50
     CSR                   EXSR JTOG$
     CSR                   Z-ADDJ$MM      MONTH
     CSR                   Z-ADDJ$DD      DAY
     CSR                   MOVE J$YYYY    YEAR
     CSR                   MOVELT#DATE    ENDGRG  6
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE ENDGRG    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    TODT    80
     CSR                   MOVE $CVD8O    $PTODT
     C*
     C*  Fill array with individual week's beg. and end date.
     C*
     CSR         BEGJUL    ADD  6         ENDJUL  50
     C*
     CSR                   DO   W1WEEK    N       30
     C*  Begining of the week
     CSR                   Z-ADDBEGJUL    J$JD    50
     CSR                   EXSR JTOG$
     CSR                   Z-ADDJ$MM      MONTH
     CSR                   Z-ADDJ$DD      DAY
     CSR                   MOVE J$YYYY    YEAR
     CSR                   MOVELT#DATE    BEGGRG  6
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE BEGGRG    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    BEGCMD  80
     C*  End of the week
     CSR                   Z-ADDENDJUL    J$JD    50
     CSR                   EXSR JTOG$
     CSR                   Z-ADDJ$MM      MONTH
     CSR                   Z-ADDJ$DD      DAY
     CSR                   MOVE J$YYYY    YEAR
     CSR                   MOVELT#DATE    ENDGRG  6
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE ENDGRG    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    ENDCMD  80
     C*
     CSR                   Z-ADDBEGCMD    WKB,N
     CSR                   Z-ADDENDCMD    WKE,N
     CSR         ENDJUL    ADD  1         BEGJUL
     CSR         BEGJUL    ADD  6         ENDJUL
     CSR                   ENDDO
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZWORK   Populate work file.
     C*
     CSR         ZZWORK    BEGSR
     C*
     C*
     C*  Select records
     C*
     C/EXEC SQL DECLARE C1 CURSOR FOR
     C+  SELECT PTWHSE, PTGDTE, PTEMP#, USFNAM, USINIT, USLNAM,
     C+         SUM(PTPCS), TDCODE, TDSTDP, SUM(PTDMIN), SUM(PTIMIN)
     C+  FROM   PIRTRAN, PIRUSER, TASKDET
     C+  WHERE  (PIRTRAN.PTEMP# = PIRUSER.USEMP# AND PTWHSE = :W1WHSE
     C+         AND PTCAT = 'SELECT')
     C+         AND ((PTTASK = TDCODE) AND (PTWHSE = TDWHSE)
     C+         AND (PTWHDP = TDWHDP) AND (PTSTYP = TDSTYP))
     C+         AND (PTGDTE >= :FROMDT) AND (PTGDTE <= :TODT)
     C+  GROUP BY PTWHSE, PTGDTE, PTEMP#, USFNAM, USINIT, USLNAM,
     C+           TDCODE, TDSTDP
     C+  ORDER BY 1 ASC, 2 ASC, 3 ASC
     C/END-EXEC
     C*
     C/EXEC SQL
     C+     OPEN C1
     C/END-EXEC
     C*
     C           SQLCOD    DOWEQ0
     C*
     C/EXEC SQL
     C+ FETCH C1
     C+         INTO :PTWHSE, :GTDATE, :PTEMP#, :USFNAM,
     C+              :USINIT, :USLNAM, :PTPCS,  :TDCODE, :TDSTDP,
     C+              :PTDMIN, :PTIMIN
     C/END-EXEC
     C           SQLCOD    IFNE 0
     C                     LEAVE
     C                     ENDIF
     C                     EXSR ZZFILL
     C                     WRITEWKREC
     C                     ENDDO
     C*
     C/EXEC SQL
     C+     CLOSE C1
     C/END-EXEC
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Job queue option
Printer device
Form type
Sunday
Monday
Tuesday
Wednesday
Thursday
Friday
Saturday
Number of weeks
