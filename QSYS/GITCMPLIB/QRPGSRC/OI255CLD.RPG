     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OI255     Import FTP orders - Post
     F*  26 February 1999
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
411 AF*    03/14/99  DAS 4.11
     F*      - Changed key of IMPFORDH file to include route. This will
     F*        allow for multiple routes to be included in one FTP file.
     F*      - Revised to print heading if warehouse changes.
     F*
414 AF*    05/11/00  DAS 4.14
     F*      - Revised FILLRH for willcalls. If route is blank it will
     F*        be set to the route id. If truck is blank it will be set
     F*        to WILLCALL.
     F*
414aAF*    05/19/00  DAS  4.14a
     F*      - Revised to send route id back to calling program.
     F*
416 AF*    10/05/01  MLB  4.16
     F*      - Added exception print file to program.
416aAF*    10/05/01  MLB  4.16a
     F*      - Revised program to merge Add-on orders to
     F*        Open Route.
     F*      - Added files: OPTIONS,ORDD1,ORDH4,RTEHED1.
416bAF*    10/11/01  MLB  4.16b
     F*      - Added cross dock support to this program.
     F*
416cAF*    06/05/02  DAS  4.16c
     F*      - Added 'S' order type.
     F*
416dAF*    06/13/02  DAS  4.16d
     F*      - Revised to capture and send back up to 10 route id's.
     F*
416eAF*    06/20/02  MLB  4.16e
     F*      - Use OIHTRK field instead of RHTRUK
     F*
417 AF*    07/15/03  MLB  4.17
     F*      - Fix: Use OIHRTE field instead of RHRTE
     F*
417aAF*    05/03/04  DAS  4.17a
     F*      - Enh: Created routine UPDRTE to automatically assign
     F*        a truck code when the code is blank.
     F*      - Revised main line to call UPDRTE for each order header.
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*    This program assumes that there is only one route for one
     F*    warehouse OR only one order in the member.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  61        - Special order (Customer # > 0)
     F*  78        - EOF for IMPFORDD
     F*  79        - EOF for IMPFORDH
     F*  91        - Overflow indicator
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FIMPFORDHUF  E           K        DISK
     FIMPFORDDUF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
416bAFSLOT1   UF  E           K        DISK                      A
416bAFSLOT4   UF  E           K        DISK
416bAF            SLREC                             KRENAMESLREC4
     FRTESUM  UF  E           K        DISK                      A
416aAFORDH4   IF  E           K        DISK
416aAF            OHREC                             KRENAMEOHREC4
416aAFORDD1   UF  E           K        DISK
416aAF            ODREC                             KRENAMEODREC1
416aAFOPTIONS IF  E           K        DISK
416bAFPODTL1  IF  E           K        DISK
416aAFRTEHED1 UF  E           K        DISK
416aAF            RHREC                             KRENAMERHREC1
     FRTEHED  O   E           K        DISK                      A
416aDF*ORDH    O   E           K        DISK                      A
416aMFORDH    UF  E           K        DISK                      A
416bAFCROSSDK O   E           K        DISK                      A
     FORDD    O   E           K        DISK                      A
     FOI255PR O   E             91     PRINTER
416 AFOI2551PRO   E             92     PRINTER                        UC
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
416bDE*                   DESC    1   3 20
416bME                    DESC    1   6 20
     E                    RDSC    1   1 50
416 AE                    A80        80  1
416 AE                    OVRPRT  1   1 80
416aAI*----------------------------------------------------------------
416aAI*  *GENPICK -  General picking options.
416aAI*----------------------------------------------------------------
416aAI*
416aAI* Fields
416aAI*
416aAI*    OPERPT  -  Print exception (short) error report (Y,N).
416aAI*    OPELBL  -  Print exception (short) labels (Y,N)
416aAI*    OPRFWC  -  Using RF will call (Y,N).
416aAI*    OPNRSC  -  Number of pallet sections (1-9).
416aAI*    OPHPWC  -  Host pick Will Call (Y/N).
416aAI*    OPMRGA  -  Merge Add-on order(s) with Open Routes.
416aAI*    OPPLSQ  -  Pallet number sequence. 1=Unload seq, 2=Load seq
416aAI*
416aAI* Data structure
416aAI*
416aAIOPDATA      DS
416aAI                                        1   1 OPERPT
416aAI                                        2   2 OPELBL
416aAI                                        3   3 OPRFWC
416aAI                                        4   40OPNRSC
416aAI                                        5   5 OPHPWC
416aAI                                        6   6 OPMRGA
416aAI                                        7   7 OPPLSQ
416aAI                                      117 117 OPTEND
416aAI*----------------------------------------------------------------
416aAI*  Re-definition field(s)
416aAI*
416aAI            DS
416aAI                                       13  15 OIHMEC
416aAI                                        1  20 OIHMSC
416aAI            DS
416aAI                                       13  15 OHMEMC
416aAI                                        1  20 OHMISC
416 AI*----------------------------------------------------------------
416 AI*  Constants
416 AI*
416 AI              '- Exceptions'        C         EXCHDR
416aAI              '** Added to existing-C         STS01
416aAI              ' Open Order. '
416aAI              '** New Order Added t-C         STS02
416aAI              'o Open Route.'
416aAI              '** Upd''d on existin-C         STS03
416aAI              'g Open Order.'
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PTYPE  Order types to process.
     C*              ' ' - Normal
     C*              'T' - Transfer
     C*              'W' - Will Call
416 AC*              'S' - Single
     C*
     C*    Returned Parameters
     C*      $PWHS   Warehouse to process.
     C*      $PTRUK  Truck code.
416dDC*****  $PRTID  Route id assigned to will call order.
416dAC*      $PRT01  Route id assigned to will call order.
416dAC*      $PRT02  Route id assigned to will call order.
416dAC*      $PRT03  Route id assigned to will call order.
416dAC*      $PRT04  Route id assigned to will call order.
416dAC*      $PRT05  Route id assigned to will call order.
416dAC*      $PRT06  Route id assigned to will call order.
416dAC*      $PRT07  Route id assigned to will call order.
416dAC*      $PRT08  Route id assigned to will call order.
416dAC*      $PRT09  Route id assigned to will call order.
416dAC*      $PRT10  Route id assigned to will call order.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PTYPE  1
     C                     PARM           $PWHS  155
     C                     PARM           $PTRUK 10
416dDC**                   PARM           $PRTID  5
416dAC                     PARM           $PRT01  5
416dAC                     PARM           $PRT02  5
416dAC                     PARM           $PRT03  5
416dAC                     PARM           $PRT04  5
416dAC                     PARM           $PRT05  5
416dAC                     PARM           $PRT06  5
416dAC                     PARM           $PRT07  5
416dAC                     PARM           $PRT08  5
416dAC                     PARM           $PRT09  5
416dAC                     PARM           $PRT10  5
     C*----------------------------------------------------------------
     C* Key definitions
     C*
     C* Download order detail file - Partial key
     C*
     C           OIDKEY    KLIST
     C                     KFLD           OIHWHS
     C                     KFLD           OIHORD
     C*
     C* Item file
     C*
     C           ITKEY     KLIST
     C                     KFLD           OIDWHS
     C                     KFLD           OIDITM
     C*
     C* Route summary file
     C*
416aAC           *LIKE     DEFN RHRTID    KYRTID
     C           KEYRS     KLIST
     C                     KFLD           RHWHSE
416aDC*                    KFLD           RHRTID
416aMC                     KFLD           KYRTID
     C                     KFLD           ITWHDP
     C*----------------------------------------------------------------
     C* Main line
416dAC*
416dAC* Initialize route id's
416dAC*
416dAC                     MOVE *BLANKS   $PRT01
416dAC                     MOVE *BLANKS   $PRT02
416dAC                     MOVE *BLANKS   $PRT03
416dAC                     MOVE *BLANKS   $PRT04
416dAC                     MOVE *BLANKS   $PRT05
416dAC                     MOVE *BLANKS   $PRT06
416dAC                     MOVE *BLANKS   $PRT07
416dAC                     MOVE *BLANKS   $PRT08
416dAC                     MOVE *BLANKS   $PRT09
416dAC                     MOVE *BLANKS   $PRT10
416dAC                     Z-ADD0         RTCNT   30
     C*
     C* Get import date/time.
     C*
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    IMPDTE  80
     C                     TIME           IMPTIM  60
     C*
     C* Get report heading.
     C*
     C                     MOVE *BLANKS   W1HEAD
     C                     MOVELRDSC,1    $PSTR
     C                     Z-ADD50        $PLEN
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     W1HEAD
416 AC*
416 AC*    Create heading for exceptions report.
416 AC*
416 AC           RDSC,1    CAT  EXCHDR:1  $PSTR     P
416 AC                     CALL 'CENTER'
416 AC                     PARM           $PSTR
416 AC                     PARM           $PLEN
416 AC                     MOVEL$PSTR     W1HDR1
416 AC                     MOVE *ON       *IN92
416 AC                     MOVE *OFF      PRHDR1  1
416 AC                     MOVE *OFF      EXCPRT  1
416 AC                     MOVE *OFF      EXCPR1  1
     C*
     C* Initialize grand total fields
     C*
     C                     Z-ADD0         GRDORD
     C                     Z-ADD0         GRDLIN
416 AC                     MOVE *ZEROS    GRORD1
416 AC                     MOVE *ZEROS    GRLIN1
     C                     Z-ADD0         PREOH
     C                     Z-ADD0         PREOD
     C                     Z-ADD0         NOITEM
     C                     Z-ADD0         BADBRK
     C*
     C* Whip through all the download header records
     C*
     C           *LIKE     DEFN OIHRTE    LSTRTE
417aAC           *LIKE     DEFN OIHRTE    LSTRID
     C                     MOVE '@@@@@'   LSTRTE
417aAC                     MOVE '@@@@@'   LSTRID
411 DC*                    MOVE *OFF      WFLAG   1
     C                     MOVE *OFF      PFLAG   1
     C                     MOVE *OFF      EOFH    1
     C                     Z-ADD55        LINPPG  30       MAX LINES PER PAGE
     C                     Z-ADD0         COUNT   30       LINE COUNTER
411 AC                     Z-SUB1         LSTWHS  30
     C*
     C           EOFH      DOUEQ*ON
417aAC                     EXSR UPDRTE
     C*
     C                     READ IMPFORDH                 79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       EOFH
     C                     GOTO ENDDOH
     C                     ENDIF
     C*
     C*  Create warehouse heading if necessary.
     C*
411 DC*          WFLAG     IFEQ *OFF
411 MC           LSTWHS    IFNE OIHWHS
     C*
     C                     Z-ADDOIHWHS    WHCODE
416aAC                     Z-ADDOIHWHS    $PWHSE  30
     C           WHCODE    CHAINWAREHS               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   WHDESC
     C                     END
416aAC*
416aAC*    Retrieve General Pick options for warehouse.
416aAC*    This applies to only Regular orders.
416aAC*
416cAC                     SELEC
416cMC           $PTYPE    WHEQ ' '
416aAC                     Z-ADDOIHWHS    OPWHSE
416aAC                     MOVE '*GENPICK'OPCODE
416aAC           OPKEY     CHAINOPTIONS              7978
416aAC           *IN79     IFEQ *ON
416aAC                     MOVE 'N'       OPMRGA
416aAC                     ENDIF
416cAC*
416cAC*    Force Allow Merge orders to Yes for 'S' orders.
416cAC*
416cMC           $PTYPE    WHEQ 'S'
416aAC                     MOVE 'Y'       OPMRGA
416aAC*
416aAC*    All other order types, force Allow Merge orders to No
416aAC*
416cMC                     OTHER
416aAC                     MOVE 'N'       OPMRGA
416cMC                     ENDSL
     C*
     C                     Z-ADDWHCODE    $PWHS
     C                     MOVE OIHWHS    WORK3A  3
     C           ' 0'      CHECKWORK3A    POS     10
     C           4         SUB  POS       LEN     10
     C           LEN       SUBSTWORK3A:POSWHHEAD
     C           WHHEAD    CAT  '-':1     WHHEAD
     C           WHHEAD    CAT  WHDESC:1  WHHEAD
     C                     CALL 'CENTER'
     C                     PARM WHHEAD    $CSTR  60
     C                     PARM 40        $CLEN   30
     C                     MOVEL$CSTR     WHHEAD
411 DC*                    MOVE *OFF      WFLAG
411 AC                     Z-ADDOIHWHS    LSTWHS
411 AC                     MOVE *OFF      PFLAG
416 AC*    Get exceptions report override if any.
416 AC                     EXSR OPNPRT
     C                     ENDIF
     C*
     C*  Print report heading if necessary.
     C*
416 AC                     MOVE *OFF      PRHDR1
     C           PFLAG     IFEQ *OFF
     C           COUNT     ORGE LINPPG
     C                     Z-ADD0         COUNT
     C                     WRITEHEAD1
     C                     ADD  3         COUNT
     C                     MOVE *OFF      PFLAG
     C                     ENDIF
     C*
     C*  Add record to route header file when route changes.
416cAC*     or when processing 'S' orders.
     C*
     C           OIHRTE    IFNE LSTRTE
416cAC           $PTYPE    OREQ 'S'
416aAC                     MOVE *OFF      MRGRTE
416aAC                     MOVE *BLANKS   NEWRTE
416aAC                     MOVE *BLANKS   NEWRTC
416aAC*
416aAC*  Merge order(s) into Open routes allowed.
416aAC*  Check if Route still at Open status.
416aAC*
416aAC           OPMRGA    IFEQ 'Y'
416aAC           $PTYPE    ANDEQ' '
416aAC                     EXSR ZZCKR1
416aAC                     EXSR ZZCKRT
416aAC                     ENDIF
416aAC*
     C                     EXSR FILLRH
     C                     WRITERHREC                  49
     C                     MOVE OIHRTE    LSTRTE
     C                     ENDIF
     C*
     C*  Add record to order header file. If an error occurs, assume
     C*    it was caused by a duplicate record.
     C*
     C                     MOVE *BLANKS   HEDMSG
416aAC*
416aAC*    Route allows Add-on orders to be merged with existing Open orders.
416aAC*    Check if order header still at Open status.
416aAC*
416aAC                     MOVE *OFF      MRGORD
416aAC                     MOVE *OFF      WRGSTS
416aAC                     MOVE *ZEROS    OPNORD
416aAC                     MOVE *ZEROS    OPNORC
416aAC           MRGRTE    IFEQ *ON
416aAC                     EXSR ZZCKO1
416aAC                     EXSR ZZCKOR
416aAC                     ENDIF
416aAC*
     C                     EXSR FILLOH
     C                     WRITEOHREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    HEDMSG
     C                     ADD  1         PREOH
     C                     ENDIF
     C*
     C*    Print order heading
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD2
     C                     ADD  2         COUNT
     C                     ENDIF
     C*
     C                     WRITEHEAD3
     C                     ADD  2         COUNT
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD4
     C                     ADD  2         COUNT
     C                     MOVE *ON       PFLAG
     C                     ENDIF
416 AC*
416 AC*    Exception occurred, print on report.
416 AC*
416 AC                     SELEC
416 AC           HEDMSG    WHGT *BLANKS
416 AC*
416 AC*    Print exception report page headings.
416 AC           *IN92     IFEQ *ON
416 AC                     WRITEHEAD11                 92
416 AC                     WRITEHEAD21
416 AC                     ENDIF
416 AC                     WRITEHEAD31
416 AC*    Set on flag that customer heading printed already.
416 AC                     MOVE *ON       PRHDR1
416 AC*
416 AC                     ENDSL
     C*
     C*    Initialize order subtotals
     C*
     C                     Z-ADD0         SUBLIN  30
     C                     Z-ADD0         SUBQ1   70
     C                     Z-ADD0         SUBQ2   70
     C                     Z-ADD0         SUBQ3   70
416 AC                     MOVE *ZEROS    SUBQ11
416 AC                     MOVE *ZEROS    SUBQ21
416 AC                     MOVE *ZEROS    SUBQ31
     C*
     C*    Whip through all the download detail records
     C*
     C                     MOVE *OFF      EOFD    1
416 AC                     MOVE *OFF      EXCPR1
     C           OIDKEY    SETLLIMPFORDD
     C           EOFD      DOUEQ*ON
     C*
     C           OIDKEY    READEIMPFORDD                 78
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       EOFD
     C                     GOTO ENDDOD
     C                     ENDIF
     C                     MOVE *BLANKS   DETMSG
     C                     MOVE 'N'       GOOD1
416bAC                     MOVE 'N'       GOOD2
416aAC                     MOVE *OFF      MRGITM
416aAC                     MOVE *BLANKS   STSMSG
     C*
     C*       If item doesn't exist write error message.
     C*
     C           ITKEY     CHAINPIRITEM              76
     C           *IN76     IFEQ *ON
     C                     MOVE DESC,2    DETMSG
     C                     ADD  1         NOITEM
     C                     MOVE *BLANKS   ITDESC
     C                     MOVEL'*****'   ITDESC
     C                     ELSE
     C*       Check if breakdown qty is allowed using piritem file.
     C*       Add rec if qty1, brk qty2 or brk qty3 has good qty.
     C                     EXSR ZZBKDN
     C*
     C*       If qty1, brkdown qty2 or brkdown qty3 has good qty,
     C*        add record to order detail file. If an error
     C*        occurs, assume was caused by a duplicate record.
     C*        Update route summary with detail information.
     C*
     C           GOOD1     IFEQ 'Y'
416bAC                     EXSR ZZXDCK
416bAC           GOOD2     IFEQ 'Y'
     C                     EXSR FILLOD
416aAC*
416aAC*    Record was updated in FILLOD.
416aAC           SKPWRT    IFEQ *OFF
     C                     WRITEODREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    DETMSG
     C                     ADD  1         PREOD
     C                     ENDIF
416aAC                     ENDIF
     C                     EXSR UPDSUM
416bAC                     ENDIF
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C*       Print order detail and update subtotals
     C*
     C                     EXSR ZRHEAD
     C                     WRITEDETAIL
     C                     ADD  1         COUNT
416aAC*
416aAC*       Print order detail item merge info line.
416aAC*
416aAC           MRGRTE    IFEQ *ON
416aAC           MRGITM    ANDEQ*ON
416aAC                     EXSR ZRHEAD
416aAC                     WRITEDETAIL2
416aAC                     ADD  1         COUNT
416aAC                     ENDIF
416aAC*
     C                     ADD  1         SUBLIN
     C                     ADD  OIDQ1     SUBQ1
     C                     ADD  OIDQ2     SUBQ2
     C                     ADD  OIDQ3     SUBQ3
     C*
     C*       Delete detail record from download file
     C*
     C                     DELETOIDREC
416 AC*
416 AC*       Exception occurred, print on report.
416 AC                     SELEC
416 AC           DETMSG    WHGT *BLANKS
416 AC*
416 AC*    Turn on flag to indicate at least one exception printed.
416 AC*    Print grand totals when complete.
416 AC                     MOVE *ON       EXCPRT
416 AC*    Turn on flag to indicate at least one exception printed.
416 AC*    Print sub totals when complete.
416 AC                     MOVE *ON       EXCPR1
416 AC*
416 AC                     ADD  OIDQ1     SUBQ11
416 AC                     ADD  OIDQ2     SUBQ21
416 AC                     ADD  OIDQ3     SUBQ31
416 AC*
416 AC*    Print exception report page headings.
416 AC           *IN92     IFEQ *ON
416 AC                     WRITEHEAD11                 92
416 AC                     WRITEHEAD21
416 AC                     WRITEHEAD31
416 AC                     WRITEHEAD41
416 AC                     MOVE *ON       PRHDR1
416 AC                     ENDIF
416 AC*
416 AC*    Print exception report Customer info and headings.
416 AC           PRHDR1    IFEQ *OFF
416 AC                     WRITEHEAD31                 92
416 AC                     MOVE *ON       PRHDR1
416 AC                     ENDIF
416 AC*
416 AC*    Print exception report page headings.
416 AC           *IN92     IFEQ *ON
416 AC                     WRITEHEAD11                 92
416 AC                     WRITEHEAD21
416 AC                     WRITEHEAD31
416 AC                     WRITEHEAD41
416 AC                     ENDIF
416 AC*
416 AC                     WRITEDETAIL1                92
416 AC                     ADD  1         GRLIN1
416 AC*
416 AC                     ENDSL
     C*
     C           ENDDOD    TAG
     C                     ENDDO
     C*
     C*    Print order subtotals and update grand totals
     C*
     C**                   EXSR ZRHEAD
     C                     WRITESUBTTL
     C                     ADD  3         COUNT
     C                     ADD  1         GRDORD
     C                     ADD  SUBLIN    GRDLIN
416 AC*
416 AC*    Print Order exception subtotals.
416 AC           EXCPR1    IFEQ *ON
416 AC                     ADD  1         GRORD1
416 AC           *IN92     IFEQ *ON
416 AC                     WRITEHEAD11                 92
416 AC                     WRITEHEAD21
416 AC                     WRITEHEAD31
416 AC                     WRITEHEAD41
416 AC                     ENDIF
416 AC                     WRITESUBTTL1                92
416 AC                     ENDIF
     C*
     C*  Delete header record from download file
     C*
     C                     DELETOIHREC
     C*
     C           ENDDOH    TAG
     C                     ENDDO
     C*
     C*  Print order grand totals
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     IFGE LINPPG
     C                     WRITEHEAD1
     C                     ENDIF
     C                     WRITEGRNDTTL
     C                     ENDIF
416aAC*
416aAC*    Remove NEW Order Header if no items attached.
416aAC           MRGORD    IFEQ *ON
416aAC                     EXSR ZZCKO1
416aAC                     ENDIF
416aAC*
416aAC*    Remove NEW Route Header if no orders attached.
416aAC           MRGRTE    IFEQ *ON
416aAC                     EXSR ZZCKR1
416aAC                     ENDIF
416 AC*
416 AC*    Print exception grand totals.
416 AC           EXCPRT    IFEQ *ON
416 AC           *IN92     IFEQ *ON
416 AC                     WRITEHEAD11                 92
416 AC                     ENDIF
416 AC                     WRITEGRNDTTL1
416 AC                     ENDIF
416 AC*
     C*
     C*  We are finished so get out
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
416aAC*
416aAC*  *INZSR  Initialization subroutine
416aAC*
416aACSR         *INZSR    BEGSR
416bAC*
416bAC* IMPFORDH Download order header file - Partial key
416bAC           OIHKEY    KLIST
416bAC                     KFLD           $PWHSE
416bAC                     KFLD           $PTYPE
416aAC*
416aAC* ORDH Order header file - Partial key
416aAC*
416aACSR         *LIKE     DEFN OHORD     NEWORD
416aACSR         OHKEY     KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           NEWORD
416aAC*
416aAC* ORDH4 Order header file - Full key  (NEW Route)
416aAC*
416aACSR         *LIKE     DEFN RHRTID    NEWRTE
416aACSR         OH3KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           NEWRTE
416aAC*
416aAC* ORDH4 Order header file - Full key  (Open Route)
416aAC*
416aACSR         OH5KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           OPNRTE
416aACSR                   KFLD           OIHORD
416aAC*
416aAC* ORDD Order detail file - Partial key
416aAC*
416aACSR         ODKEY     KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           NEWORD
416aAC*
416aAC* ORDD1 Order detail file - Full key
416aAC*
416aACSR         *LIKE     DEFN ODSTAT    KYSTAT
416aACSR                   MOVE 'O'       KYSTAT
416aACSR         OD1KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           OPNORD
416aACSR                   KFLD           KYSTAT
416aACSR                   KFLD           OIDSEQ
416aACSR                   KFLD           OIDITM
416aACSR         OD2KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           NEWORD
416aAC*
416aAC* OPTIONS Options file - Full key
416aAC*
416aACSR         OPKEY     KLIST
416aACSR                   KFLD           OPWHSE
416aACSR                   KFLD           OPCODE
416bAC*
416bAC* PODTL1 PO DETAIL file
416bAC*
416bAC           KEYPD1    KLIST
416bAC                     KFLD           $PWHSE
416bAC                     KFLD           OIDPO
416bAC                     KFLD           OIDITM
416aAC*
416aAC* RTEHED1 - Route header file - Partial key
416aAC*
416aACSR         RH1KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           OIHRTE
416aAC*
416aAC* RTEHED1 - Route header file - Full key
416aACSR         *LIKE     DEFN RHRTE     NEWRTC
416aACSR         RH2KEY    KLIST
416aACSR                   KFLD           $PWHSE
416aACSR                   KFLD           NEWRTC
416aACSR                   KFLD           NEWRTE
417aAC*
417aAC* RTEHED1 - Route header file - Full key
417aACSR         RH3KEY    KLIST
417aACSR                   KFLD           $PWHSE
417aACSR                   KFLD           LSTRTE
417aACSR                   KFLD           LSTRID
416bAC*
416bAC* SLOT1 file
416bACSR         *LIKE     DEFN SLAISL    KYAISL
416bACSR         KEYSL1    KLIST
416bACSR                   KFLD           $PWHSE
416bACSR                   KFLD           ITWHDP
416bAC                     KFLD           KYAISL
416bAC*
416bAC* SLOT4 file
416bAC           *LIKE     DEFN SLDISP    KYDISP
416bAC           KEYSL4    KLIST
416bAC                     KFLD           $PWHSE
416bAC                     KFLD           ITWHDP
416bAC                     KFLD           OIDITM
416bAC                     KFLD           KYDISP
416aAC*
416aAC*  Variables and constants.
416aAC*
416aACSR                   MOVE *OFF      FOREVR  1
416aACSR                   MOVE *OFF      MRGRTE  1
416aACSR                   MOVE *OFF      MRGORD  1
416aACSR                   MOVE *OFF      MRGITM  1
416aACSR                   MOVE *OFF      WRGSTS  1
416aACSR                   MOVE *OFF      RCDLCK  1
416aACSR                   MOVE *OFF      SKPWRT  1
416aACSR         *LIKE     DEFN RHRTID    OPNRTE
416aACSR         *LIKE     DEFN RHRTE     OPNRTC
416aACSR         *LIKE     DEFN OHORD     OPNORD
416aACSR         *LIKE     DEFN OHORD     OPNORC
416aACSR         *LIKE     DEFN ODQOR1    DIFQY1
416aACSR         *LIKE     DEFN ODQOR2    DIFQY2
416aACSR         *LIKE     DEFN ODQOR3    DIFQY3
416aAC*
416bAC*  Convert today's date into century format.
416bAC*
416bACSR                   MOVE '*CURCMD '$CVCMD
416bACSR                   CALL @CVTDT
416bACSR                   PARM           $CVTDT
416bACSR                   Z-ADD$CVD8O    TODAY   80
416bAC*
416aACSR                   ENDSR
416aAC*----------------------------------------------------------------
     C*
     C*  FILLOD  Fill order detail fields.
     C*
     CSR         FILLOD    BEGSR
416aACSR                   MOVE *OFF      SKPWRT
416aAC*
416aAC*    Route & order at Open status. Check if item already on order.
416aAC*    If yes, then compute difference between import qty's and order
416aAC*    qty's. Then update order with import qty.
416aACSR                   SELEC
416aACSR         MRGRTE    WHEQ *ON
416aACSR         MRGORD    ANDEQ*ON
416aACSR         WRGSTS    ANDEQ*OFF
416aAC*
416aACSR                   MOVE *ON       RCDLCK
416aACSR                   EXSR ZZGTOD
416aACSR                   MOVE *OFF      RCDLCK
416aAC*
416aACSR                   SELEC
416aACSR         *IN75     WHEQ *OFF
416aACSR         OIDITM    ANDEQODITEM
416aAC*
416aAC*    Calculate change in quantity to update RTESUM file.
416aACSR         OIDQ1     SUB  ODQOR1    DIFQY1
416aACSR         OIDQ2     SUB  OIDQ2     DIFQY2
416aACSR         OIDQ3     SUB  OIDQ3     DIFQY3
416aAC*
416aACSR                   Z-ADDOIDQ1     ODQOR1
416aACSR         ITFLG1    IFEQ 'Y'
416aACSR                   Z-ADDOIDQ2     ODQOR2
416aACSR                   ENDIF
416aACSR         ITFLG2    IFEQ 'Y'
416aACSR                   Z-ADDOIDQ3     ODQOR3
416aACSR                   ENDIF
416aACSR                   UPDATODREC1
416aAC*    Load print field for report.
416aACSR                   MOVE ODORD     PRTORD
416aACSR                   MOVE STS03     STSMSG
416aACSR                   MOVE *ON       MRGITM
416aACSR                   MOVE *ON       SKPWRT
416aACSR                   GOTO ENDLOD
416aACSR                   ENDSL
416aAC*
416aACSR                   ENDSL
     C*
     C*  Initialize record
     C*
     CSR                   CLEARODREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIDWHS    ODWHSE
416aAC*
416aAC*    Open route found, Open order not found or at wrong status.
416aAC*    Create order under current Open route.
416aACSR                   SELEC
416aACSR         MRGRTE    WHEQ *ON
416aACSR         MRGORD    ANDEQ*OFF
416aACSR         WRGSTS    ANDEQ*OFF
416aAC*
416aACSR         MRGRTE    OREQ *ON
416aACSR         MRGORD    ANDEQ*OFF
416aACSR         WRGSTS    ANDEQ*ON
416aACSR                   Z-ADDNEWORD    ODORD
416aACSR                   MOVE STS02     STSMSG
416aAC*
416aAC*    Open route found, Open order found.
416aAC*    Add item to current Open order.
416aACSR         MRGRTE    WHEQ *ON
416aACSR         MRGORD    ANDEQ*ON
416aACSR         WRGSTS    ANDEQ*OFF
416aACSR                   Z-ADDOPNORD    ODORD
416aACSR                   MOVE STS01     STSMSG
416aAC*
416aACSR                   OTHER
     CSR                   Z-ADDOHORD     ODORD
416aACSR                   ENDSL
416aAC*    Load print field for report.
416aACSR                   MOVE ODORD     PRTORD
416aAC*
     CSR                   Z-ADDOIDSEQ    ODSEQ
     CSR                   MOVE OIDITM    ODITEM
     CSR                   Z-ADDOIDQ1     ODQOR1
416aACSR                   Z-ADDOIDQ1     DIFQY1
     CSR         ITFLG1    IFEQ 'Y'
     CSR                   Z-ADDOIDQ2     ODQOR2
416aACSR                   Z-ADDOIDQ2     DIFQY2
     CSR                   ELSE
     CSR                   Z-ADD0         ODQOR2
416aACSR                   Z-ADD0         DIFQY2
     CSR                   ENDIF
     CSR         ITFLG2    IFEQ 'Y'
     CSR                   Z-ADDOIDQ3     ODQOR3
416aACSR                   Z-ADDOIDQ3     DIFQY3
     CSR                   ELSE
     CSR                   Z-ADD0         ODQOR3
416aACSR                   Z-ADD0         DIFQY3
     CSR                   ENDIF
     CSR                   Z-ADDOIDWH2    ODWH2
     CSR                   Z-ADDOIDOR2    ODOR2
     CSR                   MOVE OIDOIT    ODOITM
     CSR                   MOVE OIDSPC    ODSPCL
     CSR                   MOVE 'O'       ODSTAT
416aACSR                   MOVE *ON       MRGITM
416aAC*
416aACSR         ENDLOD    TAG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLOH   Fill order header fields.
     C*
     CSR         FILLOH    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEAROHREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIHWHS    OHWHSE
     CSR                   CALL 'PIRORD#'
     CSR                   PARM OHWHSE    $OWHSE  30
     CSR         OHORD     PARM 0         $OORD   70
416aACSR                   Z-ADDOHORD     NEWORD
     CSR                   MOVE OIHTYP    OHTYPE
     CSR                   Z-ADDOIHORD    OHCORD
     CSR                   MOVE OIHCPO    OHCPO
     CSR                   Z-ADDOIHCUS    OHCUST
     CSR                   MOVE OIHCNM    OHCNAM
     CSR                   MOVE OIHCA1    OHCAD1
     CSR                   MOVE OIHCA2    OHCAD2
     CSR                   MOVE OIHCCT    OHCCTY
     CSR                   MOVE OIHCST    OHCST
     CSR                   MOVE OIHCZP    OHCZIP
416aAC*
416aAC*    Open route found, Open order NOT found.
416aAC*    Add Order to current Open Route.
416aACSR                   SELEC
416aACSR         MRGRTE    WHEQ *ON
416aACSR         MRGORD    ANDEQ*OFF
416aACSR         WRGSTS    ANDEQ*OFF
416aACSR                   MOVE OPNRTE    OHRTE
416aAC*
416aACSR                   OTHER
     CSR                   MOVE RHRTID    OHRTE
416aACSR                   ENDSL
416aAC*    Load print field for report.
416aACSR                   MOVE OHRTE     PRTRTE
     CSR                   Z-ADDOIHSTP    OHSTOP
     C**                   MOVE OIHTRK    OHTRUK
     CSR                   MOVE $PTRUK    OHTRUK
     CSR                   MOVE OIHMSC    OHMISC
     CSR                   MOVE 'O'       OHSTAT
     CSR                   Z-ADDIMPDTE    OHSDTE
     CSR                   Z-ADDIMPTIM    OHSTIM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLRH   Fill route header fields.
     C*
     CSR         FILLRH    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEARRHREC
     C*
     C*  Update fields.
     C*
     CSR                   Z-ADDOIHWHS    RHWHSE
     CSR                   CALL 'PIRRTE#'
     CSR                   PARM RHWHSE    $RWHSE  30
     CSR                   PARM ' '       RHRTID
417aACSR                   MOVE RHRTID    LSTRID
416dDC**                   MOVE RHRTID    $PRTID
416dACSR                   ADD  1         RTCNT
416dACSR                   SELEC
416dACSR         RTCNT     WHEQ 1
416dACSR                   MOVE RHRTID    $PRT01
416dACSR         RTCNT     WHEQ 2
416dACSR                   MOVE RHRTID    $PRT02
416dACSR         RTCNT     WHEQ 3
416dACSR                   MOVE RHRTID    $PRT03
416dACSR         RTCNT     WHEQ 4
416dACSR                   MOVE RHRTID    $PRT04
416dACSR         RTCNT     WHEQ 5
416dACSR                   MOVE RHRTID    $PRT05
416dACSR         RTCNT     WHEQ 6
416dACSR                   MOVE RHRTID    $PRT06
416dACSR         RTCNT     WHEQ 7
416dACSR                   MOVE RHRTID    $PRT07
416dACSR         RTCNT     WHEQ 8
416dACSR                   MOVE RHRTID    $PRT08
416dACSR         RTCNT     WHEQ 9
416dACSR                   MOVE RHRTID    $PRT09
416dACSR         RTCNT     WHEQ 10
416dACSR                   MOVE RHRTID    $PRT10
416dACSR                   ENDSL
416aACSR                   MOVE RHRTID    NEWRTE
414 ACSR         $PTYPE    IFEQ 'W'
417 DC*R         RHRTE     ANDEQ*BLANKS
417 MCSR         OIHRTE    ANDEQ*BLANKS
414 ACSR                   MOVE RHRTID    RHRTE
414 ACSR                   ELSE
     CSR                   MOVE OIHRTE    RHRTE
414 ACSR                   ENDIF
416aACSR                   MOVE OIHRTE    NEWRTC
     CSR                   MOVE OIHTYP    RHTYPE
414 ACSR         $PTYPE    IFEQ 'W'
416eMCSR         OIHTRK    ANDEQ*BLANKS
416eDC**         RHTRUK    ANDEQ*BLANKS
414 ACSR                   MOVE *BLANKS   RHTRUK
414 ACSR                   MOVEL'WILLCALL'RHTRUK
414 ACSR                   ELSE
     CSR                   MOVE OIHTRK    RHTRUK
414 ACSR                   ENDIF
414 DC**                   MOVE OIHTRK    $PTRUK
414 MCSR                   MOVE RHTRUK    $PTRUK
     CSR                   MOVE '1'       RHSTAT
     CSR                   Z-ADDIMPDTE    RHSTSD
     CSR                   Z-ADDIMPTIM    RHSTST
     CSR                   Z-ADDIMPDTE    RHIMPD
     CSR                   Z-ADDIMPTIM    RHIMPT
     CSR                   Z-ADDIMPDTE    RHRPTD
     C*
     CSR                   ENDSR
416 AC*----------------------------------------------------------------
416 AC*
416 AC*  OPNPRT  Open printer file.
416 AC*
416 ACSR         OPNPRT    BEGSR
416 AC*
416 AC*   Get device and form type for department.
416 AC*
416 ACSR                   CALL 'GETRPT'
416 ACSR                   PARM '*DEVFORM'$CMD    8
416 ACSR                   PARM 'OI2551PR'$LRPT  10
416 ACSR                   PARM LSTWHS    $WHSE   30
416 ACSR                   PARM *BLANKS   $STYP   1
416 ACSR                   PARM *BLANKS   $WHDP   5
416 ACSR                   PARM           $DESC  50
416 ACSR                   PARM           $DEV   10
416 ACSR                   PARM           $FORM  10
416 ACSR         $DEV      IFEQ *BLANKS
416 ACSR                   MOVEL'*JOB'    $DEV
416 ACSR                   ENDIF
416 ACSR         $FORM     IFEQ *BLANKS
416 ACSR                   MOVEL'*STD'    $FORM
416 ACSR                   ENDIF
416 AC*
416 AC*   Open printer file for department.
416 AC*
416 ACSR         OPENED    IFNE *ON
416 AC*
416 AC*      If printer already open then close it.
416 AC*
416 ACSR         OPENED    IFEQ *ON
416 ACSR                   CLOSEOI2551PR
416 ACSR                   ENDIF
416 AC*
416 AC*      Override printer file.
416 AC*
416 AC*          OVRPRTF FILE(OI2551PR) OUTQ(*DEV) DEV($dev)
416 AC*                  FORMTYPE($form)
416 AC*        NOTE: Ending array positions for $DEV 39, $FORM 60 are one
416 AC*              greater than standard code. This is due to the
416 AC*              extra character in the printer file name.
416 AC*
416 ACSR                   MOVEAOVRPRT,1  A80
416 ACSR                   MOVEA$DEV      A80,39
416 ACSR                   MOVEA$FORM     A80,60
416 ACSR                   MOVEAA80       QCMD
416 AC*
416 ACSR                   CALL 'QCMDEXC'
416 ACSR                   PARM           QCMD   80
416 ACSR                   PARM 80        QLEN   155
416 AC*
416 AC*      Open printer file.
416 AC*
416 ACSR                   OPEN OI2551PR
416 ACSR                   MOVE *ON       OPENED  1
416 AC*
416 ACSR                   ENDIF
416 AC*
416 ACSR                   ENDSR
417aAC*----------------------------------------------------------------
417aAC*
417aAC*  UPDRTE   Update route with automatically assigned truck.
417aAC*
417aACSR         UPDRTE    BEGSR
     C*
     C*  If first time throught, then get out.
     C*
     CSR         LSTRTE    CABEQ'@@@@@'   ENDUPR
     C*
     C*  Get route header record.
     C*
     CSR         RH3KEY    CHAINRTEHED1              73
     CSR         *IN73     IFEQ *ON
     CSR                   GOTO ENDUPR
     CSR                   ENDIF
     C*
     C*  If truck already defined, get out.
     C*
     CSR         RHTRUK    IFNE *BLANKS
     CSR                   UNLCKRTEHED1
     CSR                   GOTO ENDUPR
     CSR                   ENDIF
     C*
     C*  Call program to automatically assign truck.
     C*
     CSR                   CALL 'OI260'
     CSR                   PARM           RHWHSE
     CSR                   PARM           RHRTID
     CSR                   PARM           RHTRUK
     CSR                   PARM           $XRTN   8
     CSR         $XRTN     IFEQ '*OK'
     CSR                   UPDATRHREC1
     CSR                   ELSE
     CSR                   UNLCKRTEHED1
     CSR                   ENDIF
     C*
     CSR         ENDUPR    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  UPDSUM   Update route summary file with detail record.
     C*
     CSR         UPDSUM    BEGSR
     C*
     C*  Define work fields
     C*
     CSR         *LIKE     DEFN ITCUBE    TMPCUB
     CSR         *LIKE     DEFN ITSWGT    TMPWGT
     C*
     C*  Try to get existing summary records for department.
     C*
416aACSR                   SELEC
416aAC*    Update route summary for Open route when Open Order
416aAC*    found and at correct status.
416aACSR         MRGRTE    WHEQ *ON
416aACSR         MRGORD    ANDEQ*ON
416aACSR         WRGSTS    ANDEQ*OFF
416aACSR         MRGITM    ANDEQ*ON
416aAC*
416aAC*    Update route summary for Open route when Open Order
416aAC*    NOT found and at correct status.
416aACSR         MRGRTE    OREQ *ON
416aACSR         MRGORD    ANDEQ*OFF
416aACSR         WRGSTS    ANDEQ*OFF
416aACSR         MRGITM    ANDEQ*ON
416aACSR                   MOVE OPNRTE    KYRTID
416aAC*
416aACSR                   OTHER
416aACSR                   MOVE NEWRTE    KYRTID
416aACSR                   ENDSL
416aAC*
     CSR         KEYRS     CHAINRTESUM               77
     C*
     C*     Record does not exist - Initialize fields.
     C*
     CSR         *IN77     IFEQ *ON
     CSR                   Z-ADDRHWHSE    RSWHSE
416aDC*R                   MOVE RHRTID    RSRTID
416aMCSR                   MOVE KYRTID    RSRTID
     CSR                   MOVE ITWHDP    RSWHDP
     CSR                   Z-ADD0         RSORDC
     CSR                   Z-ADD0         RSORDW
     CSR                   Z-ADD0         RSORDP
     CSR                   Z-ADD0         RSPCKC
     CSR                   Z-ADD0         RSPCKW
     CSR                   Z-ADD0         RSPCKP
     CSR                   ENDIF
     C*
     C*     Update department totals.
     C*
416aDC*R                   ADD  ODQOR1    RSORDP
416aMCSR                   ADD  DIFQY1    RSORDP
416aDC*R         ITCUBE    MULT ODQOR1    TMPCUB
416aMCSR         ITCUBE    MULT DIFQY1    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
416aDC*R         ITSWGT    MULT ODQOR1    TMPWGT
416aMCSR         ITSWGT    MULT DIFQY1    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     C*
416aDC*R                   ADD  ODQOR2    RSORDP
416aMCSR                   ADD  DIFQY2    RSORDP
416aDC*R         ODQOR2    IFNE 0
416aMCSR         DIFQY2    IFNE 0
     CSR         ITUMQ2    ANDGT0
     CSR         ITCUBE    DIV  ITUMQ2    TMPCUB    H
416aDC*R         TMPCUB    MULT ODQOR2    TMPCUB
416aMCSR         TMPCUB    MULT DIFQY2    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
     CSR         ITSWGT    DIV  ITUMQ2    TMPWGT    H
416aDC*R         TMPWGT    MULT ODQOR2    TMPWGT
416aMCSR         TMPWGT    MULT DIFQY2    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     CSR                   ENDIF
     C*
416aDC*R                   ADD  ODQOR3    RSORDP
416aMCSR                   ADD  DIFQY3    RSORDP
416aDC*R         ODQOR3    IFNE 0
416aMCSR         DIFQY3    IFNE 0
     CSR         ITUMQ3    ANDGT0
     CSR         ITCUBE    DIV  ITUMQ3    TMPCUB    H
416aDC*R         TMPCUB    MULT ODQOR3    TMPCUB
416aMCSR         TMPCUB    MULT DIFQY3    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
     CSR         ITSWGT    DIV  ITUMQ3    TMPWGT    H
416aDC*R         TMPWGT    MULT ODQOR3    TMPWGT
416aMCSR         TMPWGT    MULT DIFQY3    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     CSR                   ENDIF
     C*
     C*     Add/Update summary record.
     C*
     CSR         *IN77     IFEQ *ON
     CSR                   WRITERSREC
     CSR                   ELSE
     CSR                   UPDATRSREC
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZBKDN  Check breakdown quanties.
     C*
     CSR         ZZBKDN    BEGSR
     CSR         OIDQ2     IFNE 0
     CSR         ITFLG1    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   ENDIF
     C*
     CSR         OIDQ3     IFNE 0
     CSR         ITFLG2    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   ENDIF
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR         OIDQ1     IFNE 0
     CSR                   MOVE 'Y'       GOOD1   1
     CSR                   ELSE
     CSR         OIDQ2     IFNE 0
     CSR         ITFLG1    ANDEQ'Y'
     CSR                   MOVE 'Y'       GOOD1
     CSR                   ELSE
     CSR         OIDQ3     IFNE 0
     CSR         ITFLG2    ANDEQ'Y'
     CSR                   MOVE 'Y'       GOOD1
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR                   ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZCKRT  Check if route still at Open Status.
416aAC*
416aACSR         ZZCKRT    BEGSR
416aAC*
416aAC*    Position past last route, then read backward to get last route.
416aAC*    If Regular route and status is Open, then allow orders to be
416aAC*    merged to existing route.
416aAC*
416aAC*    NOTE: If we find the route that is being merged in,
416aAC*          and the route is at Open status, we will leave the
416aAC*          route header record locked till the merge is com-
416aAC*          plete or we process a new route.  Leaving the record
416aAC*          locked will prevent anyone from Allocating the route
416aAC*          while we are importing new orders.
416aAC*
416aACSR         RH1KEY    SETGTRTEHED1
416aACSR         RH1KEY    REDPERTEHED1                  73
416aACSR                   SELEC
416aACSR         *IN73     WHEQ *OFF
416aACSR         RHTYPE    ANDEQ' '
416aACSR         RHSTAT    ANDEQ'1'
416aACSR                   MOVE *ON       MRGRTE
416aACSR                   MOVE RHRTID    OPNRTE
416aACSR                   MOVE RHRTE     OPNRTC
416aAC*
416aACSR                   OTHER
416aACSR                   MOVE *OFF      MRGRTE
416aACSR                   UNLCKRTEHED1
416aAC*
416aACSR                   ENDSL
416aAC*
416aACSR                   ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZCKR1  Check if NEW route has any orders attached.
416aAC*
416aACSR         ZZCKR1    BEGSR
416aAC*
416aAC*    Check to see if any orders attached to NEW Route Header.
416aAC*    If none found, then Delete NEW Route Header record.
416aACSR         OH3KEY    SETLLORDH4                    77
416aACSR         *IN77     IFEQ *OFF
416aACSR         RH2KEY    CHAINRTEHED1              73
416aACSR         *IN73     IFEQ *OFF
416aACSR                   DELETRHREC1
416aACSR                   ENDIF
416aACSR                   ENDIF
416aAC*
416aACSR                   ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZCKOR  Check if order header still at Open status.
416aAC*
416aACSR         ZZCKOR    BEGSR
416aAC*
416aACSR         OH5KEY    SETLLORDH4
416aACSR         FOREVR    DOWEQFOREVR
416aACSR         OH5KEY    READEORDH4                    77
416aACSR                   SELEC
416aACSR         *IN77     WHEQ *ON
416aAC*
416aACSR                   LEAVE
416aAC*
416aAC*    In order to merge an order being imported, the following have
416aAC*    to match: Must be the same type, existing order must be open,
416aAC*    same customer and for GFG ONLY: Memo codes must match.
416aACSR         *IN77     WHEQ *OFF
416aACSR         OIHTYP    ANDEQ' '
416aACSR         OHTYPE    ANDEQ' '
416aACSR         OHSTAT    ANDEQ'O'
416aAC*    Next line is for GFG ONLY: Memo codes must match.
416aACSR         OIHMEC    ANDEQOHMEMC
416aACSR                   MOVE *ON       MRGORD
416aACSR                   Z-ADDOHORD     OPNORD
416aACSR                   Z-ADDOHCORD    OPNORC
416aACSR                   LEAVE
416aAC*
416aACSR                   OTHER
416aACSR                   MOVE *ON       WRGSTS
416aACSR                   LEAVE
416aAC*
416aACSR                   ENDSL
416aACSR                   ENDDO
416aAC*
416aACSR                   ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZCKO1  Check if NEW order header has items attached.
416aAC*
416aACSR         ZZCKO1    BEGSR
416aAC*
416aAC*    Check to see if any Items attached to NEW Order Header.
416aAC*    If none found, then Delete NEW Order Header record.
416aACSR         ODKEY     SETLLORDD1                    75
416aACSR         *IN75     IFEQ *OFF
416aACSR         OHKEY     CHAINORDH                 74
416aACSR         *IN74     IFEQ *OFF
416aACSR                   DELETOHREC
416aACSR                   ENDIF
416aACSR                   ENDIF
416aAC*
416aACSR                   ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZGTOD  Get order detail record. (with/without) record lock.
416aAC*
416aACSR         ZZGTOD    BEGSR
416aAC*
416aACSR                   SELEC
416aACSR         RCDLCK    WHEQ *OFF
416aACSR         OD1KEY    CHAINORDD1               N75
416aAC*
416aACSR         RCDLCK    WHEQ *ON
416aACSR         OD1KEY    CHAINORDD1                75
416aAC*
416aACSR                   ENDSL
416aAC*
416aAC*    Order detail line item not found. Clear qty fields.
416aACSR         *IN75     IFEQ *ON
416aACSR                   MOVE *ZEROS    ODQOR1
416aACSR                   MOVE *ZEROS    ODQOR2
416aACSR                   MOVE *ZEROS    ODQOR3
416aACSR                   ENDIF
416aAC*
416aACSR                   ENDSR
416bAC*----------------------------------------------------------------
416bAC*
416bAC*  ZZXDCK  Attach cross dock item to PO detail line.
416bAC*
416bACSR         ZZXDCK    BEGSR
416aAC*
416aAC*    Retrieve Open item order detail line
416aACSR         MRGRTE    IFEQ *ON
416aACSR         MRGORD    ANDEQ*ON
416aACSR                   MOVE *OFF      RCDLCK
416aACSR                   EXSR ZZGTOD
416aACSR                   ENDIF
416aAC*
416aAC*    If we find this item on an existing Open Order and the codes
416aAC*    don't match or this is a cross dock item, force item to New
416aAC*    Route, New Order.
416aACSR         MRGRTE    IFEQ *ON
416aACSR         MRGORD    ANDEQ*ON
416aACSR         *IN75     ANDEQ*ON
416aACSR         OIDSPC    ANDNEODSPCL
416aAC*
416aACSR         OIDSPC    OREQ 'XD'
416aACSR                   MOVE *ON       WRGSTS
416aACSR                   ENDIF
416aAC*
416bAC* Only work with cross dock items.
416bACSR         OIDSPC    IFNE 'XD'
416bACSR                   MOVE 'Y'       GOOD2
416bACSR                   GOTO ENDXD
416bACSR                   ENDIF
416bAC* Loop through P.O. detail records looking for item.
416bACSR         KEYPD1    SETLLPODTL1
416bACSR         FOREVR    DOWEQFOREVR
416bACSR         KEYPD1    READEPODTL1                   79
416bAC* Error if none found.
416bACSR         *IN79     IFEQ *ON
416bACSR                   Z-ADD0         PDSEQ
416bACSR                   MOVE OIDPO     PDPO
416bDC*R                   MOVE DESC,4    DETMSG
416bDC*R                   ADD  1         BADPO
416bDC*R                   LEAVE
416bACSR                   ENDIF
416bAC* If cross dock slot record exists, update
416bACSR                   MOVEL'XDK'     KYDISP
416bACSR         KEYSL4    SETLLSLOT4
416bACSR         FOREVR    DOWEQFOREVR                                     DO2
416bACSR                   READ SLOT4                    79
416bACSR         *IN79     IFEQ *OFF
416bMCSR         SLSTAT    ANDEQ'XD'
416bACSR         SLWHSE    ANDEQ$PWHSE
416bACSR         SLWHDP    ANDEQITWHDP
416bACSR         SLITEM    ANDEQOIDITM
416bACSR         SLAISL    ANDEQ'XDK'
416bAC* Pre-allocate product in case receiving is done before picking.
416bACSR                   ADD  OIDQ1     SLALC1
416bACSR         ITFLG1    IFEQ 'Y'
416bACSR                   ADD  OIDQ2     SLALC2
416bACSR                   ENDIF
416bACSR         ITFLG2    IFEQ 'Y'
416bACSR                   ADD  OIDQ3     SLALC3
416bACSR                   ENDIF
416bACSR                   UPDATSLREC4
416bACSR                   GOTO ADDXDK
416bACSR                   ELSE
416bACSR                   UNLCKSLOT4
416bACSR                   LEAVE
416bACSR                   ENDIF
416bACSR                   ENDDO                                          END2
416bAC* Create cross dock slot record.
416bACSR                   Z-ADD1         TMPLOC  30
416bACSR                   MOVE 'XDK'     KYAISL
416bACSR         KEYSL1    SETLLSLOT1
416bACSR         FOREVR    DOWEQFOREVR
416bACSR         KEYSL1    READESLOT1               N    79
416bACSR         *IN79     IFEQ *ON
416bACSR         SLLOC     ORGT TMPLOC
416bACSR                   LEAVE
416bACSR                   ENDIF
416bACSR         SLLOC     ADD  1         TMPLOC
416bACSR                   ENDDO
416bAC*
416bACSR                   CLEARSLREC
416bACSR                   Z-ADDOIDWHS    SLWHSE
416bACSR                   MOVE ITWHDP    SLWHDP
416bACSR                   MOVE 'XDK'     SLAISL
416bACSR                   Z-ADDTMPLOC    SLLOC
416bACSR                   Z-ADD1         SLRLVL
416bACSR                   MOVE *BLANKS   SLHAND
416bACSR                   MOVE 'X'       SLSTYP
416bACSR                   Z-ADDTMPLOC    SLPSEQ
416bACSR                   MOVE OIDITM    SLITEM
406 DC*                    MOVE 'A '      SLSTAT
416bACSR                   MOVE OIDSPC    SLSTAT
416bACSR                   Z-ADDTODAY     SLSDTE
416bACSR                   TIME           SLSTIM
416bACSR                   MOVE 'N'       SLPICK
416bACSR                   MOVE 'N'       SLRSRV
416bAC* Format display slot.
416bACSR                   CALL 'FRMTSL'
416bACSR                   PARM           SLWHSE
416bACSR                   PARM           SLWHDP
416bACSR                   PARM           SLAISL
416bACSR                   PARM           SLLOC
416bACSR                   PARM           SLRLVL
416bACSR                   PARM           SLHAND
416bACSR                   PARM           SLSTYP
416bACSR                   PARM           SLPSEQ
416bACSR                   PARM           SLDISP
416bAC* Pre-allocate product in case receiving is done before picking.
416bACSR                   Z-ADDOIDQ1     SLALC1
416bACSR         ITFLG1    IFEQ 'Y'
416bACSR                   Z-ADDOIDQ2     SLALC2
416bACSR                   ENDIF
416bACSR         ITFLG2    IFEQ 'Y'
416bACSR                   Z-ADDOIDQ3     SLALC3
416bACSR                   ENDIF
416bAC* Add record
416bACSR         SLLOC     DIV  2         DUMMY   30
416bACSR                   MVR            SLSIDE
416bACSR         SLSIDE    IFLT 0
416bACSR                   Z-SUBSLSIDE    SLSIDE
416bACSR                   ENDIF
416bACSR                   WRITESLREC                  77
416bAC* Error if slot not created.
416bACSR         *IN77     IFEQ *ON
404 ACSR                   MOVE DESC,5    DETMSG
416bACSR                   ADD  1         BADPO
416bACSR                   LEAVE
416bACSR                   ENDIF
416bAC* Create cross dock record.
416bACSR         ADDXDK    TAG
416bACSR                   Z-ADDSLWHSE    CDWHSE
416bACSR                   MOVE SLWHDP    CDWHDP
416bACSR                   MOVE SLDISP    CDSLOT
416bACSR                   Z-ADDOHORD     CDORD
416bACSR                   Z-ADDOIDSEQ    CDORSQ
416bACSR                   Z-ADD0         CDORT#
416bACSR                   Z-ADD0         CDORL#
416bACSR                   MOVE PDPO      CDPO
416bACSR                   Z-ADDPDSEQ     CDPOSQ
416bACSR                   Z-ADD0         CDPOT#
416bACSR                   Z-ADD0         CDPOL#
416bACSR                   WRITECDREC                  79
416bACSR         *IN79     IFEQ *ON
416bACSR                   MOVE DESC,6    DETMSG
416bACSR                   ADD  1         BADPO
416bACSR                   ELSE
416bACSR                   MOVE 'Y'       GOOD2   1
416bACSR                   ENDIF
416bACSR                   LEAVE
416bACSR                   ENDDO
416bAC*
416bACSR         ENDXD     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZRHEAD   Print report headings.
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         COUNT     IFGE LINPPG
     CSR                   SETON                     81
     CSR                   Z-ADD0         COUNT
     CSR                   WRITEHEAD1
     CSR                   ADD  3         COUNT
     CSR                   WRITEHEAD2
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD3
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD4
     CSR                   ADD  2         COUNT
     CSR                   SETOF                     81
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  COMPILE TIME TABLES
     O*
**
*EXIST*
*INVALID ITEM*
*BREAKDOWN INVALID*
*INVALID X-DOCK PO*
*XDK SLOT ERROR*
*NO XDK REC CREATED*
**
Import FTP Orders
**   OVRPRTF statment
OVRPRTF FILE(OI2551PR) OUTQ(*DEV) DEV(XXXXXXXXXX) FORMTYPE(XXXXXXXXXX)
