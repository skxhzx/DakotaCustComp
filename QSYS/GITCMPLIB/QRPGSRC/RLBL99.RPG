     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  RLBL01    Receiving label - Standard (1 across)
     F*  29 October 1994
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*    11/07/94  DAS
     F*      - Added unit of measure code to parameters.
     F*      - Added code to get short unit of measure description.
     F*
     F*    11/15/94  DAS
     F*      - Added picking slot to parameters and label. For PIR
     F*        items the picking slot and putaway slot will be the
     F*        same.
     F*
SPL AF*    04/26/96  DAS
SPL AF*      - Revised to call CL program when printer file is closed.
SPL AF*        The spool number will be sent as a parameter.
SPL AF*        Changes are marked with SPL in column 1.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  90        Overflow indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FREPORTS IF  E           K        DISK
     FRPTOVR1 IF  E           K        DISK
     FUNMESR  IF  E           K        DISK
     FRLBL01PRO   E             90     PRINTER                        UC
SPL AF                                              KINFDS PRTDS
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A80        80  1
     E                    OVRPRT  1   2 80
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
SPL AI*----------------------------------------------------------------
SPL AI*  Printer file info data structure
SPL AI*
SPL AIPRTDS       DS
SPL AI                                    B 123 1240SPLNUM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   *LABEL   - Label being sent to print.
     C*              *FLUSH   - Use FEOD to release the printer thus
     C*                         printing any labels that have
     C*                         accumulated.
     C*              *CLOSE   - Close up shop.
     C*
     C*      $PTRN#  Transaction number
     C*      $PWHSE  Warehouse
     C*      $PWHDP  Warehouse department
     C*      $PSTYP  Department section
     C*      $PDISP  Slot display
     C*      $PPSLT  Picking slot display
     C*      $PQTY   Quantity
     C*      $PUM    Unit of measure code
     C*      $PTIE   Tie value
     C*      $PHIGH  High value
     C*      $PITEM  Item number
     C*      $PDESC  Item description
     C*      $PPDSC  Item pack description
     C*      $PUPC   Item UPC code
     C*      $PVEN   Vendor number
     C*      $PVITM  Vendor item number
     C*      $PPO    P.O. number
     C*      $PRCVD  Receive date (MMDDYY)
     C*      $PEXPD  Expiration date (MMDDYY)
     C*      $PDEV   Printer device
     C*      $PFORM  Printer form type
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PTRN#  70
     C                     PARM           $PWHSE  30
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
     C                     PARM           $PDISP 12
     C                     PARM           $PPSLT 12
     C                     PARM           $PQTY   50
     C                     PARM           $PUM    2
     C                     PARM           $PTIE   40
     C                     PARM           $PHIGH  20
     C                     PARM           $PITEM 15
     C                     PARM           $PDESC 30
     C                     PARM           $PPDSC 15
     C                     PARM           $PUPC  15
     C                     PARM           $PVEN  110
     C                     PARM           $PVITM 15
     C                     PARM           $PPO    9
     C                     PARM           $PRCVD  60
     C                     PARM           $PEXPD  60
     C                     PARM           $PDEV  10
     C                     PARM           $PFORM 10
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Do one of the following ...
     C*
     C                     SELEC
     C*
     C*     Print label.
     C*
     C           $PCMD     WHEQ '*LABEL  '
     C                     EXSR OPNPRT
     C           $PUM      CHAINUNMESR               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   W1SHRT
     C                     ELSE
     C                     MOVE UMSHRT    W1SHRT
     C                     ENDIF
     C                     WRITELABEL
     C*
     C*     Flush spool file of labels waiting to print.
     C*
     C           $PCMD     WHEQ '*FLUSH  '
     C           OPENED    IFEQ *ON
SPL DC*                    FEOD RLBL01PR
SPL AC                     CLOSERLBL01PR
SPL AC                     CALL 'RLBL00CL'
SPL AC                     PARM SPLNUM    $SPLNM  40
SPL AC                     OPEN RLBL01PR
     C                     ENDIF
     C*
     C*     Close up shop.
     C*
     C           $PCMD     WHEQ '*CLOSE  '
     C           OPENED    IFEQ *ON
     C                     CLOSERLBL01PR
SPL AC                     CALL 'RLBL00CL'
SPL AC                     PARM SPLNUM    $SPLNM  40
     C                     ENDIF
     C                     SETON                     LR
     C*
     C                     ENDSL
     C*
     C*  Bye, Bye.
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for REPORTS file.
     C*
     CSR         *LIKE     DEFN RPPRTF    KYPRTF
     CSR         KEYRP     KLIST
     CSR                   KFLD           KYPRTF
     C*
     C*  Define key for RPTOVR1 file.
     C*
     CSR         KEYRO     KLIST
     CSR                   KFLD           KYPRTF
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PSTYP
     CSR                   KFLD           $PWHDP
     C*
     C*  Define variables
     C*
     CSR         *LIKE     DEFN $PSTYP    LSTSTP
     CSR         *LIKE     DEFN $PWHDP    LSTWDP
     CSR         *LIKE     DEFN $PDEV     LSTDEV
     CSR         *LIKE     DEFN $PFORM    LSTFRM
     C*
     C*  Initialize variables
     C*
     CSR                   MOVE *OFF      OPENED
     CSR                   MOVE '@'       LSTSTP
     CSR                   MOVE 'NOMATCH' LSTWDP
     CSR                   MOVE 'NOMATCH' LSTDEV
     CSR                   MOVE 'NOMATCH' LSTFRM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  OPNPRT  Open printer file.
     C*
     CSR         OPNPRT    BEGSR
     C*
     C*   Get default device and form type for department/section.
     C*
     CSR         $PWHDP    IFNE LSTWDP
     CSR         $PSTYP    ORNE LSTSTP
     C*
     CSR                   MOVEL'RCVLBL  'KYPRTF
     CSR         KEYRO     CHAINRPTOVR1              76
     CSR         *IN76     IFEQ *ON
     C*
     CSR         KEYRP     CHAINREPORTS              77
     CSR         *IN77     IFEQ *OFF
     CSR                   MOVE RPDEV     RODEV
     CSR                   MOVE RPFORM    ROFORM
     CSR                   ELSE
     CSR                   MOVE *BLANKS   RODEV
     CSR                   MOVEL'*JOB'    RODEV
     CSR                   MOVE *BLANKS   ROFORM
     CSR                   MOVEL'*STD'    ROFORM
     CSR                   ENDIF
     C*
     CSR                   ENDIF
     C*
     CSR                   ENDIF
     C*
     C*   Override defaults if necessary.
     C*
     CSR         $PDEV     IFNE '*DEFAULT'
     CSR                   MOVE $PDEV     RODEV
     CSR                   ENDIF
     CSR         $PFORM    IFNE '*DEFAULT'
     CSR                   MOVE $PFORM    ROFORM
     CSR                   ENDIF
     C*
     C*   Open printer file for labels.
     C*
     CSR         OPENED    IFNE *ON
     CSR         RODEV     ORNE LSTDEV
     CSR         ROFORM    ORNE LSTFRM
     C*
     C*      If printer already open then close it.
     C*
     CSR         OPENED    IFEQ *ON
     CSR                   CLOSERLBL01PR
SPL ACSR                   CALL 'RLBL00CL'
SPL ACSR                   PARM SPLNUM    $SPLNM  40
     CSR                   ENDIF
     C*
     C*      Override printer file.
     C*
     C*          OVRPRTF FILE(RLBL__PR) OUTQ(*DEV) DEV(rodev)
     C*                  FORMTYPE(roform)
     C*
     CSR                   MOVEAOVRPRT,1  A80
     CSR                   MOVEARODEV     A80,39
     CSR                   MOVEAROFORM    A80,60
     CSR                   MOVEAA80       QCMD
     C*
     CSR                   CALL 'QCMDEXC'
     CSR                   PARM           QCMD   80
     CSR                   PARM 80        QLEN   155
     C*
     C*      Open printer file.
     C*
     CSR                   OPEN RLBL01PR
     CSR                   MOVE *ON       OPENED  1
     CSR                   MOVE RODEV     LSTDEV
     CSR                   MOVE ROFORM    LSTFRM
     C*
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
**   OVRPRTF statment
OVRPRTF FILE(RLBL01PR) OUTQ(*DEV) DEV(XXXXXXXXXX) FORMTYPE(XXXXXXXXXX)
) FORMTYPE(
