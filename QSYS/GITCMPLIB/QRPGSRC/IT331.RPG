     F*----------------------------------------------------------------
     F*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  IT331     Warehouse book by slot
     F*  26 August, 1994
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*  Revisions:
     F*
412 AF*  10/01/99  HNK  4.12
     F*   - Revised to print available quantity instead of stock qty
     F*
417 AF*  03/22/04  MLB  4.17
     F*   - Enh: Added item type to report.
500 AA* 03/21/05  RH   5.00
     A*   - ENH: 417 Change missed in update, add as 5.00
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT331   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FIT331PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              ITWHDPL1
     I                                              ITSTYPL1
     I*----------------------------------------------------------------
     I*  General Data structure.
     I*
     IDS0         DS
     I                                        1  12 C0SLOT
     I                                       13  27 C0ITEM
     I                                       28  37 C0QTY
417 AI                                       38  38 C0TYPE
     I*  Data structure for column 1.
     I*
     IDS1         DS                         50
     I                                        1  12 C1SLOT
     I                                       13  27 C1ITEM
     I                                       28  37 C1QTY
417 AI                                       38  38 C1TYPE
     I*
     I*  Data structure for column 2.
     I*
     IDS2         DS                         50
     I                                        1  12 C2SLOT
     I                                       13  27 C2ITEM
     I                                       28  37 C2QTY
417 AI                                       38  38 C2TYPE
     I*
     I*  Data structure for column 3.
     I*
     IDS3         DS                         50
     I                                        1  12 C3SLOT
     I                                       13  27 C3ITEM
     I                                       28  37 C3QTY
417 AI                                       38  38 C3TYPE
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *INL1     IFEQ *ON
     C           COUNT     OREQ RECPAG
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
412 AC*
412 AC*     Call program to calculate availabe slot quantities.
412 AC*
412 AC                     CALL 'SLOTQTY'
412 AC                     PARM           SLSTK1
412 AC                     PARM           SLSTK2
412 AC                     PARM           SLSTK3
412 AC*
412 AC                     PARM           SLALC1
412 AC                     PARM           SLALC2
412 AC                     PARM           SLALC3
412 AC*
412 AC                     PARM           SLTFR1
412 AC                     PARM           SLTFR2
412 AC                     PARM           SLTFR3
412 AC*
412 AC                     PARM           SLPCK1
412 AC                     PARM           SLPCK2
412 AC                     PARM           SLPCK3
412 AC*
412 AC                     PARM           SLRCV1
412 AC                     PARM           SLRCV2
412 AC                     PARM           SLRCV3
412 AC*
412 AC                     PARM           STOCK1  50
412 AC                     PARM           STOCK2  30
412 AC                     PARM           STOCK3  30
412 AC*
412 AC                     PARM           AVAIL1  50
412 AC                     PARM           AVAIL2  30
412 AC                     PARM           AVAIL3  30
     C*
     C*    Create a quantity string to print.
     C*
     C                     MOVE *OFF      FIRST
     C                     MOVE '*QTY2STR'$PCMD
412 DC*                    Z-ADDSLSTK1    $PQTY1
412 DC*                    Z-ADDSLSTK2    $PQTY2
412 DC*                    Z-ADDSLSTK3    $PQTY3
412 MC                     Z-ADDAVAIL1    $PQTY1
412 MC                     Z-ADDAVAIL2    $PQTY2
412 MC                     Z-ADDAVAIL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C*
     C                     MOVE ITITEM    C0ITEM
     C                     MOVE SLDISP    C0SLOT
     C                     MOVE $PQSTR    C0QTY
417 AC                     MOVE ITTYPE    C0TYPE
     C*
     C*  Store info. from first 50 records into data structure DS1.
     C*  Next 50 records into DS2 and then into DS3.
     C*
     C                     SELEC
     C           C1        WHLE RECCOL
     C           C1        OCUR DS1
     C                     MOVE DS0       DS1
     C                     ADD  1         C1
     C*
     C           C2        WHLE RECCOL
     C           C2        OCUR DS2
     C                     MOVE DS0       DS2
     C                     ADD  1         C2
     C*
     C           C3        WHLE RECCOL
     C           C3        OCUR DS3
     C                     MOVE DS0       DS3
     C                     ADD  1         C3
     C                     ENDSL
     C*
     C                     ADD  1         COUNT
     C*
     C*
     C*  Accumulate totals
     C*                    ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     C**                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRDETL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD0         COUNT   30
     C                     Z-ADD3         L       10
     C                     Z-ADD1         X       30
     C                     Z-ADD1         C1      30
     C                     Z-ADD1         C2      30
     C                     Z-ADD1         C3      30
     C                     Z-ADD50        RECCOL  30
     C                     Z-ADD150       RECPAG  30
     C                     EXSR ZRCLER
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           ITWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*  Format department/section.
     C*
     C                     MOVE *BLANKS   TYPE
     C           ITWHDP    CAT  '/':0     TYPE
     C           ITSTYP    IFEQ 'P'
     C           TYPE      CAT  'PIR':0   TYPE
     C                     ELSE
     C           TYPE      CAT  'FIXED':0 TYPE
     C                     ENDIF
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*  Print previous page detail lines.
     C*
     C           RECK,L    IFGT 0
     C                     EXSR ZRDETL
     C                     Z-ADD0         COUNT
     C                     ENDIF
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRCLER    BEGSR
     C*
     C*   Initialize all data structures.
     C*
     C                     CLEARC0ITEM
     C                     CLEARC0SLOT
     C                     CLEARC0QTY
     C           1         DO   RECCOL    X
     C           X         OCUR DS1
     C                     MOVE DS0       DS1
     C           X         OCUR DS2
     C                     MOVE DS0       DS2
     C           X         OCUR DS3
     C                     MOVE DS0       DS3
     C                     ENDDO
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRDETL    BEGSR
     C*  Print detail line.
     C                     CLEARC0ITEM
     C                     CLEARC0SLOT
     C                     CLEARC0QTY
     C           1         DO   RECCOL    X
     C           X         OCUR DS1
417 AC           C1ITEM    IFEQ *BLANKS
417 AC                     MOVE *BLANKS   C1TYPE
417 AC                     ENDIF
     C           X         OCUR DS2
417 AC           C2ITEM    IFEQ *BLANKS
417 AC                     MOVE *BLANKS   C2TYPE
417 AC                     ENDIF
     C           X         OCUR DS3
417 AC           C3ITEM    IFEQ *BLANKS
417 AC                     MOVE *BLANKS   C3TYPE
417 AC                     ENDIF
     C                     WRITEDETAIL1
     C                     MOVE DS0       DS1
     C                     MOVE DS0       DS2
     C                     MOVE DS0       DS3
     C                     ENDDO
     C*
     C*  Initialize record counters for each columns.
     C*
     C                     Z-ADD1         C1
     C                     Z-ADD1         C2
     C                     Z-ADD1         C3
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C*                    ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C*                    ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C*                    Z-ADDCLS,L     T#CLS
     C*                    MOVE TYPE      T#DESC
     C*
     C*                    MOVE RPD,L     $RPD
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C           L         IFEQ 2
     C*                    WRITETOTAL2
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C*                    Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C*                    Z-ADD0         CLS,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
