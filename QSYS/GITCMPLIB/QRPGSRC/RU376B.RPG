     F*----------------------------------------------------------------
     F*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*----------------------------------------------------------------
     F*
     F*  RU376B    Route Loading - Calc time taken from an item
     F*                            start and end flag process, it
     F*                            borrows field (RHSHPT) to hold
     F*                            the diff between flag Start and
     F*                            Close times, and (RHEXPT) to hold
     F*                            the diff between route End and
     F*                            Close times.
     F*  April 9, 2008
     F*
     F*  Jose Duque   (Copy of RU371B)
     F*
     F*  Revisions:
     F*
520 AF*    06/17/09  RBD  5.20
     F*      - Removed hardcoding of start and end times, will use
     F*        new pgm GETLOARNG instead to retrieve *LOADER options.
     F*      - Added call to GETWHSE.
     F*      - Enh: Added client custom code support to program.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: Cheney
     F*
CBI AF*    06/17/09  RBD  CBI
     F*      - Coded special produce items logic as specific to Cheney.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FLDRHHDR3IF  E           K        DISK
     FRU376B2 IF  E           K        DISK
     FRU376   UF  E           K        DISK                      A
     F*
     I*----------------------------------------------------------------
520 AI*  Client ID's
     I*
CBI A /COPY QCOPYSRC,IL#CHENEY
     I*
520 AI              'GETCLIENT'           C         @GETCL
     I*----------------------------------------------------------------
     I*
     I*  Data structures for time calculation
     I*
     I            DS
     I                                        1   60CLSTIM
     I                                        1   20PHH
     I                                        3   40PMM
     I                                        5   60PSS
     I            DS
     I                                        1   60STRTIM
     I                                        1   20IHH
     I                                        3   40IMM
     I                                        5   60ISS
     I            DS
     I                                        1   60TDIFF
     I                                        1   20TDHR
     I                                        3   40TDMIN
     I                                        5   60TDSEC
     I            DS
     I                                        1   52HLDMM2
     I                                        1   30MM
     I                                        4   50MINPER
     I*
     I            DS
     I                                        1   6 SAVTIM
     I                                        1   2 SAVHR
     I                                        3   4 SAVMM
     I                                        5   6 SAVSS
     I*
      *----------------------------------------------------------------
     I*  Constants
     I*
520 AI              'GETLOARNG'           C         GETLOA
      *----------------------------------------------------------------
520 AI*  Program info data structure
520 AI*
520 AI           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
      *----------------------------------------------------------------
     C           *ENTRY    PLIST
     C                     PARM           $PDATE  8
     C                     PARM           $SDATE  8
     C                     PARM           $FSEC   6
     C                     PARM           $RSEC   6
      *
520 AC*      Get warehouse for user.
     C*
520 AC                     CALL 'GETWHSE'
520 AC                     PARM           #USER
520 AC                     PARM 0         #WHSE   30
      *
520 AC*      Get loader start and end times.
     C*
520 AC                     CALL GETLOA
520 AC                     PARM           #WHSE
520 AC                     PARM 0         STIME   60
520 AC                     PARM 0         ETIME   60
      *
     C*----------------------------------------------------------------
     C*
     C*  Define partial key for RU376 file.
     C*
     C           RU76KY    KLIST
     C                     KFLD           RURTE
     C*
     C*
     C*  Define partial key for LDRHHDR3 file.
     C*
     C           HDRKEY    KLIST
     C                     KFLD           RUWHSE
     C                     KFLD           RHRTE
     C                     KFLD           RHRTID
     C*
     C                     MOVEL$PDATE    PDATE   80
     C                     MOVEL$SDATE    SDATE   80
     C                     Z-ADD*ZEROS    FLGSEC  60
     C                     Z-ADD*ZEROS    RTESEC  60
     C                     Z-ADD*ZEROS    HFMIN   60
     C                     Z-ADD*ZEROS    HRMIN   60
     C*
     C*----------------------------------------------------------------
     C*  File RU376B2 below holds Route and Route Id of all
     C*  closed routes, it is kept by LOADR1.
      *
     C           PDATE     SETLLRU376B2
     C                     READ RU376B2                  LR
     C           *INLR     DOWEQ*OFF
      *
     C           RUSTRD    IFEQ PDATE                      PrevDate
520 MC           RUSTRT    ANDGESTIME                      FromOptScrn
     C           RUSTRD    OREQ SDATE                      ShipDate
520 MC           RUSTRT    ANDLEETIME
      *
     C                     Z-ADD*ZEROS    CNTR    30
      *
     C           RURTE     SETLLRU376
     C           RURTE     READERU376                    78
     C           *IN78     DOWEQ*OFF
     C                     ADD  1         CNTR
      *
     축*                    RU376B2/RURTID=OrigRteId
     C           RHRTID    IFNE RURTID                     If route is
     C                     MOVE RURTID    RHRTID           already in
     C                     UPDATRUREC                      RU376 file
     C                     ENDIF                           Make sure it
     C           RURTE     READERU376                    78has the orig
     C                     ENDDO                           route id.
      *
     C           CNTR      IFEQ *ZEROS                     If not then
     C                     CLEARRUREC                      write it
     C                     Z-ADD1         RUWHSE           into RU376
     C                     MOVELRURTE     RHRTE            file as a
     C                     MOVELRURTID    RHRTID           non flagged
     C                     WRITERUREC                      route.
      *
     C                     ENDIF
     C                     ENDIF
     C*
     C                     READ RU376B2                  LR
     C                     ENDDO
     C*
     C*----------------------------------------------------------------
     C*  CALC the time difference between 'Start Time' and
     C*  'Close Time' of flag process  for a given item and
     C*   store the result in the borrowed field 'RHSHPT'.
     C*
     C           *LOVAL    SETLLRUREC
     C                     READ RUREC                    LR
     C                     MOVELRHRTE     SAVRTE  5
     C           *INLR     DOWNE*ON
     C*
     C           RUODTT    IFNE *BLANKS                    Flagged rte
     C                     MOVE 'F'       RTN     1
     C                     MOVE RUODTT    STRTIM
     C*                                                    Find diff
     C           2         SUBSTRUCDTS:12 SAVHR            between
     C           2         SUBSTRUCDTS:15 SAVMM            flag Start
     C           2         SUBSTRUCDTS:18 SAVSS            and Close
     C                     MOVE SAVTIM    CLSTIM           times
     C                     EXSR ZZCALC                     (flaggedRtes
     C                     Z-ADDTDIFF     RHSHPT            only).
     C                     ELSE
     C                     Z-ADD*ZEROS    RHSHPT
     C                     ENDIF
      *
      *
      *  The piece below finds the route START and CLOSE times,
      *  if this information is not found in the header history
      *  file, it means that the route in 'RU376' file is either
      *  BREAKERS (other than the first one which is the one in
      *  the header history) or a regular route ADD-ONS which in
      *  both cases the ROUTE ID is different than the original.
      *
      *  This cause a not found record condition when chaining to
      *  LDRHHDR below. So, in this situation we recurr to chain
      *  RU376B2 file to get the orig ROUTE ID in order to get the
      *  route START and CLOSE times.
      *
     C                     Z-ADD*ZEROS    RHEXPT
     C           HDRKEY    CHAINLDRHHDR3             79
     C           *IN79     IFEQ *OFF
     C                     MOVE 'R'       RTN
     C                     Z-ADDLHHENT    STRTIM           Find diff
     C                     Z-ADDLHHCLT    CLSTIM           between
     C                     EXSR ZZCALC                     route End
     C                     Z-ADDTDIFF     RHEXPT           and Close
     C                     ENDIF
      *
CBI M *  Identify Produce item within CLR and DRY depts.
      *
CBI AC           CLIENT    IFEQ CHENEY
CBI MC           ITITEM    ANDGE'405000'
CBI MC           ITITEM    ANDLE'492999'
CBI MC                     MOVEL'PRD  '   RUWHDP
CBI MC                     ENDIF
      *
     C                     UPDATRUREC
     C*
     C*
     C           REDAGN    TAG
     C                     READ RUREC                    LR
     C           *INLR     IFNE *ON
     C*
     C           RHRTE     IFEQ SAVRTE                     Skip dup
     C                     DELETRUREC                      route recs
     C                     GOTO REDAGN                     leave the
     C                     ENDIF                           one with the
     C*                                                    latest
     C                     MOVELRHRTE     SAVRTE           closed flag
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*
     C                     MOVE FLGSEC    $FSEC            parm value
      *
     C                     MOVE RTESEC    $RSEC            parm value
     C*
     C*----------------------------------------------------------------
     C*
520 AC*  *INZSR  Initialization subrotine
     C*
520 AC           *INZSR    BEGSR
     축*
520 A축*  Get Client ID
     축*
520 AC                     CALL @GETCL
520 AC                     PARM           CLIENT 10
     C*
520 AC                     ENDSR
     C*
     C*----------------------------------------------------------------
     C           ZZCALC    BEGSR
     C*----------------------------------------------------------------
      *  The code below compare to time values (start and close
      *  times) and calculate the time difference between the two.
      *
      *  *IN89 drives part of the code based on which date is greater.
      *  If 'close time' is greater then both time are converted to
      *  seconds, 'start time' is subtracted from 'close time' and
      *  the result in seconds is converted back to HR:MM:SS to be
      *  displayed as the time difference between the two.
      *
      *  If on the other hand 'close time' is lower then 'start time'
      *  is converted to seconds along with 24:00:00 to calc the
      *  elapsed time from 'start time' to the end of the day.
      *  The result in seconds is converted back to HH:MM:NN and
      *  added to 'close date'
      *
     C           CLSTIM    COMP STRTIM               89
      *
      *  Convert 'import time' to seconds
      *
     C           IHH       MULT 60        HLDMM   60       ConvHrtoMin
     C           HLDMM     MULT 60        HLDSS1  60       ConvMintoSec
     C           IMM       MULT 60        HLDSS2  60       CnovMintoSec
     C           HLDSS2    ADD  ISS       HLDSS2           AddupAllSecs
     C           HLDSS1    ADD  HLDSS2    TOTIMS  60       TotalImpSecs
      *
      *
      *  Print time is greater, convert to seconds
      *
     C           *IN89     IFEQ *ON
     C           PHH       MULT 60        HLDMM   60       ConvHrtoMin
     C           HLDMM     MULT 60        HLDSS1  60       ConvMintoSec
     C           PMM       MULT 60        HLDSS2  60       CnovMintoSec
     C           HLDSS2    ADD  PSS       HLDSS2           AddupAllSecs
     C           HLDSS1    ADD  HLDSS2    TOTPRS  60       TotalPrtSecs
     C                     ENDIF
      *
      *  Close time is lower, subtract the calc time between start
      *  time and the end of the day from 24:00:00 represented in
      *  seconds.
     C  N89      86400     SUB  TOTIMS    SECDIF  60
      *
     C           RTN       IFEQ 'F'
     C           *IN89     ANDEQ*OFF
     C                     ADD  SECDIF    FLGSEC           Accumulate
     C                     ENDIF                           flag seconds
      *                                                    difference
     C           RTN       IFEQ 'R'
     C           *IN89     ANDEQ*OFF                       Accumulate
     C                     ADD  SECDIF    RTESEC           rte seconds
     C                     ENDIF                           difference
      *
      *  Close time is greater, subtract start time from it
     C   89      TOTPRS    SUB  TOTIMS    SECDIF  60
      *
     C           RTN       IFEQ 'F'
     C           *IN89     ANDEQ*ON                        Accumulate
     C                     ADD  SECDIF    FLGSEC           flag seconds
     C                     ENDIF                           difference
      *
     C           RTN       IFEQ 'R'                        Accumulate
     C           *IN89     ANDEQ*ON                        rte seconds
     C                     ADD  SECDIF    RTESEC           difference
     C                     ENDIF
      *
     C           SECDIF    DIV  60        HLDMM2           ConvSectoMin
     C           MINPER    DIV  100       HLDPER  32       SetSecPercent
     C           HLDPER    MULT 60        HLDSS1    H      CalcSeconds
     C                     Z-ADDHLDSS1    TDSEC
      *
      *
      *  Take care of minutes overflow
     C           MM        IFGT 59
     C           MM        DIV  60        HLDHR   20       Loading
     C                     MVR            TDMIN            Tdiff
     C                     Z-ADDHLDHR     TDHR
     C                     ELSE
     C                     Z-ADD*ZEROS    TDHR
     C                     Z-ADDMM        TDMIN
     C                     ENDIF
      *
      *  Move result to borowed field 'RHSHPT' to hold time diff.
     C           *IN89     IFEQ *ON
     C                     GOTO ENDCAL
     C                     ENDIF
      *
      *
      *
      *  Close time is lower, add calc result of start time
      *  to end of day to close time
     C           TDIFF     ADD  CLSTIM    TDIFF
      *
      *
      *  Take care of seconds overflow
     C           TDSEC     IFGT 59
     C           TDSEC     DIV  60        HLDMM
     C                     MVR            HLDSS1
     C                     ADD  HLDMM     TDMIN
     C                     Z-ADDHLDSS1    TDSEC
     C                     ENDIF
      *
      *  Take care of minutes overflow
      *
     C           TDMIN     IFGT 59
     C           TDMIN     DIV  60        HLDHR   20
     C                     MVR            TDMIN
     C                     ADD  HLDHR     TDHR
     C                     ENDIF
      *
      *
     C           ENDCAL    TAG
     C                     ENDSR
