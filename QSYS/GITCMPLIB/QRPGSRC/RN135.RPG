     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  RN135     Import batch customer returns - Post
     F*  Dave Sommerville/Roxanne Harrison
     F*
     F*  Revisions
     F*
     F*
     F*  Notes -
     F*      - If breakdown qty being imported, verify if allowed.
     F*      - If breakdown qty in not allowed, do not import brk qty.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  61        - Special order (Customer # > 0)
     F*  79        - EOF for IMPRTN
     F*  78        - EOF for ORTNBAT
     F*  91        - Overflow indicator
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FIMPRTN  UF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FORTNBAT UF  E           K        DISK                      A
     FRN135PR O   E             91     PRINTER
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    DESC    1   6 20
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  36 $LITEM
     I*
     I                                       19  20 $LCODE
     I*
     I                                       19  250$LTRN#
     I*
     I                                       19  28 $LUSER
     I*
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHS   Warehouse to process.
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHS  155
     C*----------------------------------------------------------------
     C                     Z-ADD$PWHS     $PWHSE  30
     C*----------------------------------------------------------------
     C* Main line
     C*
     C* Get report heading.
     C*
     C                     MOVE *BLANKS   W1HEAD
     C                     MOVEL'RN135PR' RPPRTF
     C           RPPRTF    CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD50        $PLEN
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     W1HEAD
     C*
     C*   Get warehouse description.
     C*
     C           $PWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   WHDESC
     C                     END
     C*
     C*   Create warehouse heading.
     C*
     C                     MOVE $PWHSE    WORK3A  3
     C           ' 0'      CHECKWORK3A    POS     10
     C           4         SUB  POS       LEN     10
     C           LEN       SUBSTWORK3A:POSWHHEAD
     C           WHHEAD    CAT  '-':1     WHHEAD
     C           WHHEAD    CAT  WHDESC:1  WHHEAD
     C                     CALL 'CENTER'
     C                     PARM WHHEAD    $CSTR  60
     C                     PARM 40        $CLEN   30
     C                     MOVEL$CSTR     WHHEAD
     C*
     C* Initialize grand total fields
     C*
     C                     Z-ADD0         GRDLIN
     C                     Z-ADD0         NOITEM
     C                     Z-ADD0         BADDTE
     C                     Z-ADD0         BADBRK
     C                     Z-ADD0         BADRCD
     C                     Z-ADD0         BADTRN
     C*
     C*    Initialize order subtotals
     C*
     C                     Z-ADD0         SUBLIN  30
     C                     Z-ADD0         SUBQ1   70
     C                     Z-ADD0         SUBQ2   70
     C                     Z-ADD0         SUBQ3   70
     C*
     C*
     C* Whip through all the download records
     C*
     C                     MOVE *OFF      PFLAG   1
     C                     MOVE *OFF      EOFH    1
     C                     Z-ADD55        LINPPG  30       MAX LINES PER PAGE
     C                     Z-ADD0         COUNT   30       LINE COUNTER
     C           OIRKEY    SETLLIMPRTN
     C           EOFH      DOUEQ*ON
     C*
     C           OIRKEY    READEIMPRTN                   79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       EOFH
     C                     GOTO ENDDOH
     C                     ENDIF
     C*
     C*  Print report heading if necessary.
     C*
     C           PFLAG     IFEQ *OFF
     C           COUNT     ORGE LINPPG
     C                     Z-ADD0         COUNT
     C                     WRITEHEAD1
     C                     ADD  3         COUNT
     C                     MOVE *OFF      PFLAG
     C                     ENDIF
     C*
     C*
     C*    Print order heading
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD2
     C                     ADD  2         COUNT
     C                     MOVE *ON       PFLAG
     C                     ENDIF
     C*
     C                     MOVE *BLANKS   DETMSG
     C                     MOVE 'N'       BAD1    1
     C*
     C*       Print return detail and update subtotals
     C*
     C*                    EXSR ZRHEAD
     C                     MOVELOIRMEM    RMEMO
     C                     WRITEDETAIL
     C                     ADD  1         COUNT
     C                     ADD  1         GRDLIN
     C*
     C*       If item doesn't exist write error message,
     C*          don't write to ORTNBAT file.
     C*
     C           ITKEY     CHAINPIRITEM              76
     C           *IN76     IFEQ *ON
     C                     MOVE DESC,2    DETMSG
     C                     ADD  1         NOITEM
     C                     MOVE *BLANKS   ITDESC
     C                     MOVEL'*****'   ITDESC
     C                     MOVE 'Y'       BAD1
     C                     EXSR ZZDET2
     C                     ENDIF
     C*
     C*       If invalid date, write error message,
     C*          don't write to ORTNBAT file.
     C*
     C                     EXSR ZZEXDT
     C*
     C*       Check if breakdown qty is allowed using piritem file.
     C*       If invalid qty1, brk qty2 or brk qty3 , write error msg.
     C*          don't write to ORTNBAT file.
     C                     EXSR ZZBKDN
     C*
     C*       Verify Transaction # and return code, write error msg.
     C*
     C                     EXSR ZZCHK1
     C*
     C*        add record to ORTNBAT file. If an error
     C*        occurs, assume was caused by a duplicate record.
     C*
     C           BAD1      IFEQ 'N'
     C                     EXSR FILLOR
     C                     WRITEOBRREC                 49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    DETMSG
     C                     EXSR ZZDET2
     C                     ENDIF
     C                     ENDIF
     C*
     C*       Delete return record from download file
     C*
     C                     DELETOIRREC
     C*
     C           ENDDOH    TAG
     C                     ENDDO
     C*
     C*  Print order grand totals
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     IFGE LINPPG
     C                     WRITEHEAD1
     C                     ENDIF
     C                     WRITEGRNDTTL
     C                     ENDIF
     C*
     C*  We are finished so get out
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FIRST   1
     C*
     C* Key definitions
     C*
     C* Download return file - Partial key
     C*
     C           OIRKEY    KLIST
     C                     KFLD           $PWHSE
     C*
     C* Item file
     C*
     C           ITKEY     KLIST
     C                     KFLD           OIRWHS
     C                     KFLD           OIRITM
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   MOVE #PROG     $CVPRG
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   EXSR CLRRTN
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  CLRRTN  Clear previously sucessfully completed records.
     C*
     CSR         CLRRTN    BEGSR
     C*
     C*  Process ORTNBAT file.
     C*
     CSR                   MOVE *OFF      EOFR    1
     CSR         *LOVAL    SETLLORTNBAT
     CSR         EOFR      DOUEQ*ON
     C*
     CSR                   READ ORTNBAT                  78
     CSR         *IN78     IFEQ *ON
     CSR                   MOVE *ON       EOFR
     CSR                   GOTO ENDCLR
     CSR                   ENDIF
     CSR         OBRSTS    IFEQ '*OK     '
     CSR                   DELETOBRREC
     CSR                   ENDIF
     C*
     CSR         ENDCLR    TAG
     CSR                   ENDDO
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLOR  Fill batch return fields.
     C*
     CSR         FILLOR    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEAROBRREC
     C*
     C*  Move fields from download file to batch return fields
     C*
     CSR                   Z-ADDOIRWHS    OBRWHS
     CSR                   Z-ADDOIRORD    OBRORD
     CSR                   MOVE OIRITM    OBRITM
     CSR                   MOVE OIRTYP    OBRTYP
     CSR                   Z-ADDOIRQT1    OBRQT1
     CSR         ITFLG1    IFEQ 'Y'
     CSR                   Z-ADDOIRQT2    OBRQT2
     CSR                   ELSE
     CSR                   Z-ADD0         OBRQT2
     CSR                   ENDIF
     CSR         ITFLG2    IFEQ 'Y'
     CSR                   Z-ADDOIRQT3    OBRQT3
     CSR                   ELSE
     CSR                   Z-ADD0         OBRQT3
     CSR                   ENDIF
     CSR                   Z-ADDOIRCWT    OBRCWT
     CSR                   MOVE OIRTRN    OBRTRN
     CSR                   MOVE OIRCIT    OBRCIT
     CSR                   MOVE OIRRCD    OBRRCD
     CSR                   MOVE OIRMEM    OBRMEM
     CSR                   MOVE 'N'       OBRPUT
     CSR         OIRPUT    IFEQ 'Y'
     CSR                   MOVE OIRPUT    OBRPUT
     CSR                   ENDIF
     CSR                   Z-ADDTODAY     OBRUDT
     CSR                   TIME           OBRUTI
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZBKDN  Check breakdown quanties.
     C*
     CSR         ZZBKDN    BEGSR
     CSR         OIRQT2    IFNE 0
     CSR         ITFLG1    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   MOVE 'Y'       BAD1
     CSR                   EXSR ZZDET2
     CSR                   ENDIF
     C*
     CSR         OIRQT3    IFNE 0
     CSR         ITFLG2    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   MOVE 'Y'       BAD1
     CSR                   EXSR ZZDET2
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1  Verify return code and transaction #
     C*
     CSR         ZZCHK1    BEGSR
     C*
     C*
     C*  Verify return code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE OIRRCD    $LCODE
     CSR                   MOVE #PROG     $LPRG
     CSR                   CALL 'RN900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   ADD  1         BADRCD
     CSR                   MOVE DESC,5    DETMSG
     CSR                   EXSR ZZDET2
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   ADD  1         BADRCD
     CSR                   MOVE DESC,5    DETMSG
     CSR                   EXSR ZZDET2
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify transaction number.
     C*
     CSR         OIRTRN    IFNE 0
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDOIRTRN    $LTRN#
     CSR                   MOVE #PROG     $LPRG
     CSR                   CALL 'PT900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   ADD  1         BADTRN
     CSR                   MOVE DESC,6    DETMSG
     CSR                   EXSR ZZDET2
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDET2  Print error message lines.
     C*
     CSR         ZZDET2    BEGSR
     C*
     C                     EXSR ZRHEAD
     C                     WRITEDETL2
     C                     ADD  1         COUNT
     C                     MOVE *BLANKS   DETMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZEXDT  Check expiration date.
     C*
     CSR         ZZEXDT    BEGSR
     C*
     C*  Verify expiration date.
     C*
     C           OIREDT    IFNE 0
     C                     MOVE '*YMDEDT '$CVCMD
     C                     MOVE OIREDT    $CVD6I
     CSR                   MOVE #PROG     $CVPRG
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C           $CVRTN    IFEQ '*PGMQ   '
     C                     MOVE DESC,4    DETMSG
     CSR                   EXSR ZZDET2
     C                     ADD  1         BADDTE
     C                     ELSE
     C*
     C*  Convert expiration date into century format.
     C*
     C                     MOVE '*YMDCMD '$CVCMD
     CSR                   MOVE #PROG     $CVPRG
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    EXPD    80
     C*
     C*  Expiration date must be greater than today.
     C*
     C           EXPD      IFLE TODAY
     C                     MOVE DESC,4    DETMSG
     CSR                   EXSR ZZDET2
     C                     ADD  1         BADDTE
     C                     ELSE
     C                     Z-ADDEXPD      OBREDT
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZRHEAD   Print report headings.
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         COUNT     IFGE LINPPG
     CSR                   SETON                     81
     CSR                   Z-ADD0         COUNT
     CSR                   WRITEHEAD1
     CSR                   ADD  3         COUNT
     CSR                   WRITEHEAD2
     CSR                   ADD  2         COUNT
     CSR                   SETOF                     81
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
*EXIST*
*INVALID ITEM*
*BREAKDOWN INVALID*
*INVALID DATE*
*INVALID RETURN CODE*
*INVALID TRANS# *
