     F*----------------------------------------------------------------
     F*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SL830     Change slot utility
     F*  23 May 2000
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
414 AF*    05/23/00  DAS  4.14
     F*      - Created                                                 d
     F*
415 AF*    04/18/01  DAS  4.15
     F*      - Moved initialization of WOTYPE field from ZZDFT1        d
     F*        to ZZINZ2.                                              d
     F*      - Added error message routine ZM83.                       d
     F*      - Added data structures to save record info.              d
     F*      - Added logic to see if record was changed by another userd
     F*
415aAF*    04/24/01  DAS  4.15a
     F*      - Added PASWRD window to screen format.
     F*      - Revised to ask user for password if *CHANGE or *VIEW
     F*        are not passed in.
     F*      - Revised to get "open" quantities for slot.
     F*
416 AF*    01/24/02  HNK  4.16
     F*      - Move screen fields to $P fields in ZZUPD routine.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  90        View mode (protect all fields)
     F*  91        Change mode (protect key fields)
     F*  97        Alawys on (used for SFLEND keyword)
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSL830FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FSLOT2   UF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FUNMESR  IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   3 30
415 AI*----------------------------------------------------------------
415 AI*  Data structures for comparing original and current records.
415 AI*
415 AICURREC    E DSSLOT
415 AIORGREC      DS
415 AI                                     10001000 DS1000
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
415aAI              'OPNSQTYCL '          C         @OPNSQ
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I                                       27  41 $LITEM
     I                                       42  53 $LDISP
     I                                       54  58 $LDESG
     I                                       59  60 $LSTAT
     I                                       61  62 $LSDEF
     I                                       63  64 $LACTV
     I*
     I                                       19  23 $LDES2
     I*
     I                                       19  20 $LACT2
     I*
     I                                       19  20 $LBC2
     I*
     I                                       19  20 $LSTA2
     I*
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   Command: *CHANGE, *VIEW
     C*      $PWHSE  Warehouse code.
     C*      $PWHDP  Department.
     C*      $PDISP  Slot.
     C*
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PWHSE  30
     C                     PARM           $PWHDP  5
     C                     PARM           $PDISP 12
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C           NXTSCR    CASEQ'02 '     SC2
     C           NXTSCR    CASEQ'03 '     SC3
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     CSR         NXTSCR    IFEQ '02 '
     CSR                   EXFMTSCREEN2                50
     CSR                   END
     CSR         NXTSCR    IFEQ '03 '
     CSR                   EXFMTSCREEN3                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   EXSR ZZDFT1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  Screen 02 initialization
     C*
     CSR         SCR02I    BEGSR
     CSR                   MOVE '02 '     NXTSCR  3
     C*
     C*  Get existing record(s).
     C*
     CSR                   EXSR ZZFIL2
     CSR         END02I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  Screen 03 initialization
     C*
     CSR         SCR03I    BEGSR
     CSR                   MOVE '03 '     NXTSCR  3
     C*
     C*  Get existing record(s).
     C*
     CSR                   EXSR ZZFIL3
     CSR         END03I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   EXSR ZZUPD
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  SC2  -  Screen 2
     C*
     CSR         SC2       BEGSR
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR02I
     CSR                   GOTO ENDSC2
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC2
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD2
     CSR         CMDTKN    CABEQ*ON       ENDSC2
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK2
     CSR         ERROR     CABEQ*ON       ENDSC2
     CSR                   EXSR ZZUPD2
     CSR         ERROR     CABEQ*ON       ENDSC2
     CSR         ENDSC2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  SC3  -  Screen 3
     C*
     CSR         SC3       BEGSR
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR03I
     CSR                   GOTO ENDSC3
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE '02 '     NXTSCR
     CSR                   GOTO ENDSC3
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD3
     CSR         CMDTKN    CABEQ*ON       ENDSC3
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK3
     CSR         ERROR     CABEQ*ON       ENDSC3
     C***                  EXSR ZZUPD3
     C***        ERROR     CABEQ*ON       ENDSC3
     CSR         ENDSC3    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0112  Invalid range entered.
     C*
     CSR         ZM0112    BEGSR
     CSR                   MOVE 'PIR0112' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
415 AC*----------------------------------------------------------------
415 AC*   PIR7983  Record changed by another user.
415 AC*
415 ACSR         ZM83      BEGSR
     CSR                   MOVE 'PIR7983' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     IT12210  Can't lock item record.
     C*
     CSR         ZM10      BEGSR
     CSR                   MOVE 'IT12210' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     IT12273  Item does not exist.
     C*
     CSR         ZM73I     BEGSR
     CSR                   MOVE 'IT12273' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify warehouse department.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify slot number.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   MOVE *BLANKS   $LITEM
     CSR                   MOVE W1DISP    $LDISP
     CSR                   MOVE *BLANKS   $LDESG
     CSR                   MOVE *BLANKS   $LSTAT
     CSR                   MOVE *BLANKS   $LSDEF
     CSR                   MOVE *BLANKS   $LACTV
     CSR                   CALL 'SL900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK2   Screen 2 error checking.
     C*
     CSR         ZZCHK2    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR         ENDCK2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK3   Screen 3 error checking.
     C*
     CSR         ZZCHK3    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR         ENDCK3    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ '1'
     CSR                   MOVE '1'       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Warehouse.
     C*
     CSR         ROW#      WHEQ 8
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
     C*     Department.
     C*
     CSR         ROW#      WHEQ 9
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LWHDP    W1WHDP
     CSR                   END
     C*
     C*     Slot.
     C*
     CSR         ROW#      WHEQ 10
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE '*GENERAL'$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   MOVE *BLANKS   $LITEM
     CSR                   MOVE W1DISP    $LDISP
     CSR                   MOVE *BLANKS   $LDESG
     CSR                   MOVE *BLANKS   $LSTAT
     CSR                   MOVE *BLANKS   $LSDEF
     CSR                   MOVE *BLANKS   $LACTV
     CSR                   CALL 'SL900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LDISP    W1DISP
     CSR                   END
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE '1'       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD2   User defined command keys for screen 2.
     C*
     CSR         ZZCMD2    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ '1'
     CSR                   MOVE '1'       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Designation area.
     C*
     CSR         ROW#      WHEQ 6
     CSR         COL#      ANDGE29
     CSR         COL#      ANDLE33
     C***                  MOVE *ON       *IN05
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE W2DESG    $LDES2
     CSR                   CALL 'DS900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LDES2    W2DESG
     CSR                   ENDIF
     C*
     C*     Activity zone.
     C*
     CSR         ROW#      WHEQ 6
     CSR         COL#      ANDGE42
     CSR         COL#      ANDLE43
     C****                 MOVE *ON       *IN06
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE W2ACTV    $LACT2
     CSR                   CALL 'AC900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LACT2    W2ACTV
     CSR                   ENDIF
     C*
     C*     Build code.
     C*
     CSR         ROW#      WHEQ 6
     CSR         COL#      ANDGE52
     CSR         COL#      ANDLE53
     C****                 MOVE *ON       *IN09
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE W2BLD     $LBC2
     CSR                   CALL 'BC900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LBC2     W2BLD
     CSR                   END
     C*
     C*     Status code.
     C*
     CSR         ROW#      WHEQ 5
     CSR         COL#      ANDGE30
     CSR         COL#      ANDLE31
     C****                 MOVE *ON       *IN12
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE W2STAT    $LSTA2
     CSR                   CALL 'SS900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LSTA2    W2STAT
     CSR                   END
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE '1'       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     CSR         ENDCM2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD3   User defined command keys for screen 2.
     C*
     CSR         ZZCMD3    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F10 - Update
     C*
     CSR         *INKJ     IFEQ '1'
     CSR                   MOVE '1'       CMDTKN
     CSR                   EXSR ZZUPD3
     CSR                   ENDIF
     CSR         ENDCM3    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDFT1   Get default values for add.
     C*
     CSR         ZZDFT1    BEGSR
     CSR                   MOVE *OFF      *IN90
415 DC**         $PCMD     IFEQ '*CHANGE'
415 DC**                   MOVE 'CHANGE'  WOTYPE
415 DC**                   ELSE
415 DC**                   MOVE 'VIEW  '  WOTYPE
415 DC**                   ENDIF
     CSR         $PDISP    IFNE *BLANKS
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   END
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL2   Fill screen 2 fields with info from file.
     C*
     CSR         ZZFIL2    BEGSR
     C*
     CSR         $PCMD     IFEQ '*CHANGE'
     CSR                   MOVE *OFF      *IN90
     CSR                   ELSE
     CSR                   MOVE *ON       *IN90
     CSR                   ENDIF
415aAC*
415aAC*   Get "open" slot quantities.
415aAC*
415aACSR                   CALL @OPNSQ
415aACSR                   PARM $PWHSE    $OWHSE  30
415aACSR                   PARM $PWHDP    $OWHDP  5
415aACSR                   PARM $PDISP    $ODISP 12
415aACSR                   PARM           $OALC1  50
415aACSR                   PARM           $OALC2  30
415aACSR                   PARM           $OALC3  30
415aACSR                   PARM           $OTFR1  50
415aACSR                   PARM           $OTFR2  30
415aACSR                   PARM           $OTFR3  30
415aACSR                   PARM           $OPCK1  50
415aACSR                   PARM           $OPCK2  30
415aACSR                   PARM           $OPCK3  30
415aACSR                   PARM           $ORCV1  50
415aACSR                   PARM           $ORCV2  30
415aACSR                   PARM           $ORCV3  30
415aACSR                   PARM           $OUNK1  50
415aACSR                   PARM           $OUNK2  30
415aACSR                   PARM           $OUNK3  30
     C*
415aACSR                   Z-ADD$OALC1    WOALC1
415aACSR                   Z-ADD$OALC2    WOALC1
415aACSR                   Z-ADD$OALC3    WOALC1
415aACSR                   Z-ADD$OTFR1    WOTFR1
415aACSR                   Z-ADD$OTFR2    WOTFR2
415aACSR                   Z-ADD$OTFR3    WOTFR3
415aACSR                   Z-ADD$OPCK1    WOPCK1
415aACSR                   Z-ADD$OPCK2    WOPCK2
415aACSR                   Z-ADD$OPCK3    WOPCK3
415aACSR                   Z-ADD$ORCV1    WORCV1
415aACSR                   Z-ADD$ORCV2    WORCV2
415aACSR                   Z-ADD$ORCV3    WORCV3
415aACSR                   Z-ADD$OUNK1    WOUNK1
415aACSR                   Z-ADD$OUNK2    WOUNK2
415aACSR                   Z-ADD$OUNK3    WOUNK3
     C*
     C*   Get slot and item quantity records.
     C*
     CSR         SLKEY     CHAINSLOT2               N79
     CSR         *IN79     IFEQ *ON
     CSR                   CLEARSLREC
     CSR                   ENDIF
415 AC*
415 AC*   Save current record values for later comparison.
415 AC*
415 ACSR                   MOVELCURREC    ORGREC
     C*
     C*   Format slot display
     C*
     CSR                   CALL 'FRMTSL2'
     CSR                   PARM           SLWHSE
     CSR                   PARM           SLWHDP
     CSR                   PARM           SLAISL
     CSR                   PARM           SLLOC
     CSR                   PARM           SLRLVL
     CSR                   PARM           SLHAND
     CSR                   PARM           SLDISP
     CSR                   PARM           SLSIDE
     CSR                   PARM           WOSTR
     C*
     C*   Move file fields into screen fields
     C*
     CSR                   MOVE SLDISP    W2DISP
     CSR                   MOVE SLITEM    W2ITEM
     CSR                   MOVE SLSTYP    W2STYP
     CSR                   MOVE SLSTAT    W2STAT
     CSR                   Z-ADDSLSDTE    W2SDTE
     CSR                   Z-ADDSLSTIM    W2STIM
     CSR                   MOVE SLSDEF    W2SDEF
     CSR                   MOVE SLDESG    W2DESG
     CSR                   MOVE SLACTV    W2ACTV
     CSR                   MOVE SLBLD     W2BLD
     CSR                   MOVE SLPICK    W2PICK
     CSR                   MOVE SLRSRV    W2RSRV
     CSR                   Z-ADDSLPRTY    W2PRTY
     CSR                   Z-ADDSLEXPD    W2EXPD
     CSR                   Z-ADDSLENTD    W2ENTD
     CSR                   Z-ADDSLSLFD    W2SLFD
     C*
     CSR                   Z-ADDSLSTK1    W2STK1
     CSR                   Z-ADDSLSTK2    W2STK2
     CSR                   Z-ADDSLSTK3    W2STK3
     CSR                   Z-ADDSLALC1    W2ALC1
     CSR                   Z-ADDSLALC2    W2ALC2
     CSR                   Z-ADDSLALC3    W2ALC3
     CSR                   Z-ADDSLPCK1    W2PCK1
     CSR                   Z-ADDSLPCK2    W2PCK2
     CSR                   Z-ADDSLPCK3    W2PCK3
     CSR                   Z-ADDSLTFR1    W2TFR1
     CSR                   Z-ADDSLTFR2    W2TFR2
     CSR                   Z-ADDSLTFR3    W2TFR3
     CSR                   Z-ADDSLRCV1    W2RCV1
     CSR                   Z-ADDSLRCV2    W2RCV2
     CSR                   Z-ADDSLRCV3    W2RCV3
     C*
     C*   Format item description
     C*
     CSR         SLITEM    IFEQ *BLANKS
     CSR                   MOVE *BLANKS   WODESC
     CSR                   ELSE
     CSR         ITKEY     CHAINPIRITEM              78
     CSR         *IN78     IFEQ *ON
     CSR                   MOVELDESC,3    ITDESC
     CSR                   ENDIF
     CSR         ITDESC    CAT  '-':1     WODESC
     CSR         WODESC    CAT  ITPDSC:1  WODESC
     CSR                   ENDIF
     C*
     C*   Get short descriptions for units of measure.
     C*
     CSR                   MOVE *BLANKS   W2UM1
     CSR                   MOVE *BLANKS   W2UM2
     CSR                   MOVE *BLANKS   W2UM3
     C*
     CSR         ITUM1     IFNE *BLANKS
     CSR         ITUM1     CHAINUNMESR               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE UMSHRT    W2UM1
     CSR                   ELSE
     CSR                   MOVELITUM1     W2UM1
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         ITUM2     IFNE *BLANKS
     CSR         ITUM2     CHAINUNMESR               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE UMSHRT    W2UM2
     CSR                   ELSE
     CSR                   MOVELITUM2     W2UM2
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         ITUM3     IFNE *BLANKS
     CSR         ITUM3     CHAINUNMESR               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE UMSHRT    W2UM3
     CSR                   ELSE
     CSR                   MOVELITUM3     W2UM3
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR                   Z-ADD0         W2AVL1
     CSR                   Z-ADD0         W2AVL2
     CSR                   Z-ADD0         W2AVL3
     C*
     C*   Calculate available quantities.
     C*
     CSR                   CALL 'SLOTQTY'
     CSR                   PARM           SLSTK1
     CSR                   PARM           SLSTK2
     CSR                   PARM           SLSTK3
     C*
     CSR                   PARM           SLALC1
     CSR                   PARM           SLALC2
     CSR                   PARM           SLALC3
     C*
     CSR                   PARM           SLTFR1
     CSR                   PARM           SLTFR2
     CSR                   PARM           SLTFR3
     C*
     CSR                   PARM           SLPCK1
     CSR                   PARM           SLPCK2
     CSR                   PARM           SLPCK3
     C*
     CSR                   PARM           SLRCV1
     CSR                   PARM           SLRCV2
     CSR                   PARM           SLRCV3
     C*
     CSR                   PARM           STOCK1  50
     CSR                   PARM           STOCK2  30
     CSR                   PARM           STOCK3  30
     C*
     CSR                   PARM           AVAIL1  50
     CSR                   PARM           AVAIL2  30
     CSR                   PARM           AVAIL3  30
     C*
     CSR                   Z-ADDAVAIL1    W2AVL1
     CSR                   Z-ADDAVAIL2    W2AVL2
     CSR                   Z-ADDAVAIL3    W2AVL3
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL3   Fill screen 3 fields with info from file.
     C*
     CSR         ZZFIL3    BEGSR
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define key for item file.
     C*
     CSR         ITKEY     KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLITEM
     C*
     C*  Define key for slot file.
     C*
     CSR         SLKEY     KLIST
     CSR                   KFLD           W1WHSE
     CSR                   KFLD           W1WHDP
     CSR                   KFLD           W1DISP
     C*
     CSR                   MOVE #PROG     $LPRG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
415aACSR         $PCMD     IFNE '*CHANGE'
415aACSR         $PCMD     ANDNE'*VIEW  '
415aACSR                   EXSR ZZPWRD
415aACSR                   ENDIF
     C*
415 ACSR         $PCMD     IFEQ '*CHANGE'
415 ACSR                   MOVE 'CHANGE'  WOTYPE
415 ACSR                   ELSE
415 ACSR                   MOVE 'VIEW  '  WOTYPE
415 ACSR                   ENDIF
     C*
     C*   Select first screen to display.
     C*
     CSR                   SELEC
     CSR         $PDISP    WHEQ *BLANKS
     C*
     CSR                   CALL 'GETWHSE'
     CSR                   PARM           #USER
     CSR         W1WHSE    PARM 0         #WHSE   30
     C*
     CSR                   EXSR SCR01I
     C*
     CSR                   OTHER
     CSR                   Z-ADD$PWHSE    W1WHSE
     CSR                   MOVE $PWHDP    W1WHDP
     CSR                   MOVE $PDISP    W1DISP
     CSR                   EXSR ZZCHK1
     CSR         ERROR     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   ELSE
     CSR                   EXSR SCR02I
     CSR                   ENDIF
     CSR                   ENDSL
     C*
     CSR                   ENDSR
415aAC*----------------------------------------------------------------
415aAC*
415aAC*  ZZPWRD   Get password
415aAC*
415aAC*    Note: Password becomes value for $CMD. Therefore, the
415aAC*          password to allow changes is *CHANGE.
415aAC*
415aACSR         ZZPWRD    BEGSR
     CSR                   MOVE *BLANKS   W1PWRD
     CSR                   EXFMTPASWRD
     CSR                   MOVE W1PWRD    $PCMD
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE *OFF      ERROR
416 ACSR                   Z-ADDW1WHSE    $PWHSE
416 ACSR                   MOVE W1WHDP    $PWHDP
416 ACSR                   MOVE W1DISP    $PDISP
     CSR                   EXSR SCR02I
     CSR         ENDUPD    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD2   Update record.
     C*
     CSR         ZZUPD2    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR         $PCMD     IFEQ '*CHANGE'
     CSR                   EXSR SCR03I
     CSR                   ELSE
     CSR                   EXSR SCR01I
     CSR                   ENDIF
     CSR         ENDUP2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD3   Update for screen 3.
     C*
     CSR         ZZUPD3    BEGSR
     C*
     C*   Get slot and update
     C*
     CSR         SLKEY     CHAINSLOT2                79
415 DC**         *IN79     IFEQ *OFF
415 AC*
415 AC*   Check for error updating record.
415 AC*
415 ACSR         *IN79     IFEQ *ON
415 ACSR                   MOVE *ON       ERROR
415 ACSR                   EXSR ZM72
415 ACSR                   GOTO ENDUP3
415 ACSR                   ENDIF
415 AC*
415 AC*   Check to see if record was changed by someone else.
415 AC*
415 ACSR         CURREC    IFNE ORGREC
415 ACSR                   MOVE *ON       ERROR
415 ACSR                   EXSR ZM83
415 ACSR                   MOVE '02 '     NXTSCR
415 ACSR                   GOTO ENDUP3
415 ACSR                   ENDIF
415 AC*
415 AC*   Update record.
415 AC*
     CSR                   MOVE W2DISP    SLDISP
     CSR                   MOVE W2ITEM    SLITEM
     CSR                   MOVE W2STYP    SLSTYP
     CSR                   MOVE W2STAT    SLSTAT
     CSR                   Z-ADDW2SDTE    SLSDTE
     CSR                   Z-ADDW2STIM    SLSTIM
     CSR                   MOVE W2SDEF    SLSDEF
     CSR                   MOVE W2DESG    SLDESG
     CSR                   MOVE W2ACTV    SLACTV
     CSR                   MOVE W2BLD     SLBLD
     CSR                   MOVE W2PICK    SLPICK
     CSR                   MOVE W2RSRV    SLRSRV
     CSR                   Z-ADDW2PRTY    SLPRTY
     CSR                   Z-ADDW2EXPD    SLEXPD
     CSR                   Z-ADDW2ENTD    SLENTD
     CSR                   Z-ADDW2SLFD    SLSLFD
     C*
     CSR                   Z-ADDW2STK1    SLSTK1
     CSR                   Z-ADDW2STK2    SLSTK2
     CSR                   Z-ADDW2STK3    SLSTK3
     CSR                   Z-ADDW2ALC1    SLALC1
     CSR                   Z-ADDW2ALC2    SLALC2
     CSR                   Z-ADDW2ALC3    SLALC3
     CSR                   Z-ADDW2PCK1    SLPCK1
     CSR                   Z-ADDW2PCK2    SLPCK2
     CSR                   Z-ADDW2PCK3    SLPCK3
     CSR                   Z-ADDW2TFR1    SLTFR1
     CSR                   Z-ADDW2TFR2    SLTFR2
     CSR                   Z-ADDW2TFR3    SLTFR3
     CSR                   Z-ADDW2RCV1    SLRCV1
     CSR                   Z-ADDW2RCV2    SLRCV2
     CSR                   Z-ADDW2RCV3    SLRCV3
     CSR                   UPDATSLREC
415 DC**                   ENDIF
     C*
     CSR                   MOVE *OFF      ERROR
     CSR                   EXSR SCR01I
     CSR         ENDUP3    ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
* * Invalid Warehouse * *
* * Invalid Slot * *
* * No item in slot * *
