     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FRMTTIHI  Format Tie/High
     F*  09 March 2001
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
415 AF*    03/09/01  DAS  4.15
     F*      - Created.
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A7          7  1
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PTI    Tie value
     C*      $PHI    High value
     C*
     C*    Returned Parameters
     C*      $PSTR   Tie/High string
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PTI    40
     C                     PARM           $PHI    20
     C                     PARM           $PSTR   7
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $PSTR
     C                     MOVE *BLANKS   A7
     C                     Z-ADD1         P       10
     C*
     C*  Move Tie value into string
     C*
     C                     SELEC
     C           $PTI      WHGT 999
     C                     MOVE $PTI      TEMP4A  4
     C                     MOVEATEMP4A    A7,P
     C                     ADD  4         P
     C*
     C           $PTI      WHGT 99
     C                     MOVE $PTI      TEMP3A  3
     C                     MOVEATEMP3A    A7,P
     C                     ADD  3         P
     C*
     C           $PTI      WHGT 9
     C                     MOVE $PTI      TEMP2A  2
     C                     MOVEATEMP2A    A7,P
     C                     ADD  2         P
     C*
     C                     OTHER
     C                     MOVE $PTI      TEMP1A  1
     C                     MOVE TEMP1A    A7,P
     C                     ADD  1         P
     C*
     C                     ENDSL
     C*
     C*  Add '/'
     C*
     C                     MOVE '/'       A7,P
     C                     ADD  1         P
     C*
     C*  Move High value into string
     C*
     C                     SELEC
     C           $PHI      WHGT 9
     C                     MOVE $PHI      TEMP2A  2
     C                     MOVEATEMP2A    A7,P
     C*
     C                     OTHER
     C                     MOVE $PHI      TEMP1A  1
     C                     MOVE TEMP1A    A7,P
     C*
     C                     ENDSL
     C*
     C*  Move into return string.
     C*
     C                     MOVEAA7        $PSTR
     C*
     C                     RETRN
