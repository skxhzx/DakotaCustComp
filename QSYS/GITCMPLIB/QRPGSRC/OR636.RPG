     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR636     Truck build - Get replenishment pallet #.
     F*  09 October 1997
     F*  Roxanne Harrison
     F*
     F*  Revisions
     F*
411 AF*    06/21/99  DAS  4.11
     F*      - Modified to work with new item types.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FWORKLBL8IF  E           K        DISK
411 AFITEMLNK1IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse number.
     C*      $PRTID  Route Id.
     C*      $PITEM  Item number.
     C*      $PPAL#  Corresponding pick pallet number.
     C*
     C*    Returned Parameters
     C*      $PPAL#  Pick pallet number.
     C*      $PRTN   *OK      - Replenishment completed.
     C*              *ERROR   - Replenishment not done.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PRTID  5
     C                     PARM           $PITEM 15
     C                     PARM           $PPAL#  70
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       FOREVR  1
     C                     MOVE '*ERROR  '$PRTN
     C*
411 AC           KEYIL1    SETLLITEMLNK1
411 AC                     MOVE $PITEM    KYITEM
411 AC           FOREVR    DOUNEFOREVR
     C*
     C*  Get Pallet # for route/item.
     C*
     C           KEYWL8    SETLLWORKLBL8             79
     C           *IN79     CABEQ*ON       OUT
     C           FOREVR    DOUNEFOREVR
     C           KEYWL8    READEWORKLBL8            N    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*     Skip if pallet # = 0.
     C*
     C           LWPAL#    IFEQ 0
     C                     ITER
     C                     ENDIF
     C*
     C                     Z-ADDLWPAL#    $PPAL#
     C                     MOVE '*OK     '$PRTN
     C                     LEAVE
     C*
     C                     ENDDO
     C*
411 AC           $PRTN     IFEQ '*OK'
411 AC                     LEAVE
411 AC                     ENDIF
     C*
411 AC           KEYIL1    READEITEMLNK1                 78
411 AC           *IN78     IFEQ *ON
411 AC                     LEAVE
411 AC                     ENDIF
411 AC                     MOVE ILITEM    KYITEM
     C*
411 AC                     ENDDO
     C*
     C*
     C           OUT       TAG
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for WORKLBL8 file.
     C*
411 ACSR         *LIKE     DEFN $PITEM    KYITEM
     CSR         KEYWL8    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PRTID
411 MCSR                   KFLD           KYITEM
411 DC*R                   KFLD           $PITEM
     C*
411 AC*  Define key for ITEMLNK1 file.
     C*
     CSR         KEYIL1    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   ENDSR
