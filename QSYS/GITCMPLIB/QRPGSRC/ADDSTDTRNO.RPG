     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  ADDSTDTRN   Add standard transaction record (EPM version)
     F*  05 January 1996
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
213 AF*    05/22/96  DAS
     F*      - Added new fields to PIRTRAN (PTRDTE, PTGRP1, PTGRP2).
     F*      - Added GRP1 and GRP2 to parameter list.
     F*      - Moved values into new PIRTRAN fields.
     F*
218 AF*    01/23/97  HNK
     F*      - Added one more parameter $PCMD. Logic added to create
     F*        and start transaction. Call ADJTSKDET program with
     F*        with "*START" command to start the task.
     F*
415 AF*    06/14/01  HNK  4.15
     F*      - Get current date just before updating the record instead
     F*        of in *INZSR routine.
     F*
416 AF*    12/06/02  MLB  4.16
     F*      - No code changes.  Just added note below.
     F*
417xAF*    05/13/03  DAS  4.17x
     F*      **** CONVERTED TO ILE.
     F*      **** LOGIC INCORPORATED INTO ADJTRAN2.
     F*      **** NEW VERSION OF ADDSTDTRN JUST CALLS ADJTRAN2.
     F*
416 AF*----------------------------------------------------------------
416 AF*  Notes:
416 AF*
416 AF*    Program ADDSTDTRN2 is an exact duplicate of this program.
416 AF*    Please make sure than any changes made to this program
416 AF*    are duplicated in ADDSTDTRN2.
416 aF*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FTASK    IF  E           K        DISK
     FPIRTRAN O   E           K        DISK                      A
218 AFPIRUSER IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  PIRTRAN record layout for use as parameters
     I*
     IPTPARM    E DSPIRTRAN
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'ADJTSKDET'           C         @ADJTD
     I              'CVTDTE'              C         @CVTDT
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I                                        1  10 ERQTY
     I                                    P  11  120ERPAL
     I                                       13  27 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
218 AC*      $PCMD   *CREATE = Create transaction
218 AC*              *CRTSTRT= Create and start transaction
     C*      $PPRG   Program to send messages back to.
     C*      $PTRN#  Transaction number
     C*      $PTASK  Task code
     C*      $PWHSE  Warehouse
     C*      $PWHDP  Department
     C*      $PSTYP  Section
     C*      $PCUBE  Cube
     C*      $PSWGT  Weight
     C*      $PPCS   Pieces
     C*      $PAISL  Aisles
     C*      $PQTY1  User quantity 1
     C*      $PQTY2  User quantity 2
     C*      $PQTY3  User quantity 3
     C*      $PRTE   Route
     C*      $PPO    P.O.
213 AC*      $PGRP1  Group 1 code
213 AC*      $PGRP2  Group 2 code
     C*
     C*    Returned Parameters
     C*      $PRTN   *OK      - No problem encountered.
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*                         (for use with $PTYPE = *INTER).
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
218 AC                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PTRN#  70
     C                     PARM           $PTASK  6
     C                     PARM           $PWHSE  30
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
     C                     PARM           $PCUBE  93
     C                     PARM           $PSWGT  92
     C                     PARM           $PPCS   50
     C                     PARM           $PAISL  50
     C                     PARM           $PQTY1  50
     C                     PARM           $PQTY2  50
     C                     PARM           $PQTY3  50
     C                     PARM           $PRTE   5
     C                     PARM           $PPO    9
213 AC                     PARM           $PGRP1  1
213 AC                     PARM           $PGRP2  1
     C                     PARM           $PRTN   8
     C                     PARM           $PMSG   4
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $PRTN
     C                     MOVE *BLANKS   $PMSG
     C*
     C*  Get task information.
     C*
     C           $PTASK    CHAINTASK                 79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   TKCAT
     C                     MOVE *BLANKS   TKSUB
     C                     ENDIF
     C*
     C*  Create transaction record.
     C*
     C*    Note: The task tracking level determines whether or not the
     C*          department and the section are written to the transaction
     C*          record.  This was done in order to make it possible to
     C*          use a simple join between the transaction records and
     C*          the task detail file which contains the standards.
     C*
     C                     CLEARPTREC
     C                     Z-ADD$PTRN#    PTTRN#
     C                     Z-ADD$PWHSE    PTWHSE
     C                     MOVE $PTASK    PTTASK
     C                     MOVE TKCAT     PTCAT
     C                     MOVE TKSUB     PTSUB
     C                     SELEC
     C           TKTLVL    WHEQ 'D'
     C                     MOVE $PWHDP    PTWHDP
     C           TKTLVL    WHEQ 'S'
     C                     MOVE $PWHDP    PTWHDP
     C                     MOVE $PSTYP    PTSTYP
     C                     ENDSL
     C                     Z-ADD$PCUBE    PTCUBE
     C                     Z-ADD$PSWGT    PTSWGT
     C                     Z-ADD$PPCS     PTPCS
     C                     Z-ADD$PAISL    PTAISL
     C                     Z-ADD$PQTY1    PTQTY1
     C                     Z-ADD$PQTY2    PTQTY2
     C                     Z-ADD$PQTY3    PTQTY3
415 AC                     MOVE '*CURCMD '$CVCMD
415 AC                     CALL @CVTDT
415 AC                     PARM           $CVTDT
415 AC                     Z-ADD$CVD8O    PTGDTE
415 DC**                   Z-ADDTODAY     PTGDTE
     C                     TIME           PTGTIM
     C           $PPO      IFNE *BLANKS
     C                     MOVEL$PPO      PTRTPO
     C                     ELSE
     C                     MOVEL$PRTE     PTRTPO
     C                     ENDIF
415 AC                     MOVE '*CURCMD '$CVCMD
415 AC                     CALL @CVTDT
415 AC                     PARM           $CVTDT
415 AC                     Z-ADD$CVD8O    PTRDTE
415 DC**                   Z-ADDTODAY     PTRDTE
213 AC                     MOVE $PGRP1    PTGRP1
213 AC                     MOVE $PGRP2    PTGRP2
218 AC*
218 AC                     SELEC
218 AC           $PCMD     WHEQ '*CREATE '
     C                     MOVE '1'       PTSTAT
218 AC*
218 AC           $PCMD     WHEQ '*CRTSTRT'
218 AC                     MOVE '2'       PTSTAT
218 AC           #USER     CHAINPIRUSER              79
218 AC           *IN79     IFEQ *OFF
218 AC                     MOVE USSHFT    PTSHFT
218 AC                     MOVE USCREW    PTCREW
218 AC                     Z-ADDUSEMP#    PTEMP#
218 AC                     ELSE
218 AC                     MOVE *BLANKS   PTSHFT
218 AC                     MOVE *BLANKS   PTCREW
218 AC                     Z-ADD0         PTEMP#
218 AC                     ENDIF
415 AC                     MOVE '*CURCMD '$CVCMD
415 AC                     CALL @CVTDT
415 AC                     PARM           $CVTDT
415 AC                     Z-ADD$CVD8O    PTSDTE
415 DC**                   Z-ADDTODAY     PTSDTE
218 AC                     TIME           PTSTIM
218 AC                     ENDSL
     C                     WRITEPTREC
218 AC*
218 AC                     SELEC
218 AC           $PCMD     WHEQ '*CREATE '
     C*
     C*  Update transaction detail record.
     C*
     C                     CALL @ADJTD
     C                     PARM '*GEN    '@PCMD   8
     C                     PARM '*INTER  '@PTYPE  8
     C                     PARM           PTPARM
     C                     PARM ' '       @PRTN   8
     C                     PARM ' '       @PMSG   4
218 AC*
218 AC           $PCMD     WHEQ '*CRTSTRT'
218 AC*
218 AC*  Update transaction detail record.
218 AC*
218 AC                     CALL @ADJTD
218 AC                     PARM '*START  '@PCMD   8
218 AC                     PARM '*INTER  '@PTYPE  8
218 AC                     PARM           PTPARM
218 AC                     PARM ' '       @PRTN   8
218 AC                     PARM ' '       @PMSG   4
218 AC                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Convert today's date into century format.
     C*
415 DC**                   MOVE '*CURCMD '$CVCMD
415 DC**                   CALL @CVTDT
415 DC**                   PARM           $CVTDT
415 DC**                   Z-ADD$CVD8O    TODAY   80
     C                     ENDSR
