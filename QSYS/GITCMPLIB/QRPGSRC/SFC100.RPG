     F*----------------------------------------------------------------
     F*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SFC100    Route detail by stop
     F*  14 September 2000
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*  Revisions:
     F*
414 AF*    09/14/00  DAS  4.14
     F*      - Created
     F*
520 AF*    02/05/09  CWM/MLB 5.20  01/26  02/04
     F*      - Enh: Added support for new extended stop number.
     F*        Retrieve stop # in subroutine ZZFGTS and pgm GETSTOP
     F*
650aAF*    06/17/13  RBD  6.50a
     F*      - Enh: Added parms to GETSTOP.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSFC100  IP  E           K        DISK
     FSFC100PRO   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    TTL     1   1 40
     E                    RPD     1   5 25
     E                    RECK        5  7 0
     E                    LVLK        5  7 0
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              OHSTOPL1
     I                                              OHCUSTL1
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL1     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*  Level 1 break - Stop/Cust#
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
520 AC                     EXSR ZZFGTS
520 AC           $SSTP     IFGT 0
650AD ***                  Z-ADD$SSTP     WOSTOP
650aMC                     MOVE $PSD      WOSTOP
520 AC                     ELSE
650AD ***                  Z-ADDOHSTOP    WOSTOP
650aMC                     MOVE OHSTOP    WOSTOP
520 AC                     ENDIF
520 AC*
     C                     Z-ADDOHCUST    WOCUST
     C                     MOVELOHCNAM    WOCNAM
     C*
     C*                    MOVE *BLANKS   $CSTR
     C*                    MOVE *BLANKS   CUST#
     C*                    MOVE OHCUST    $CSTR
     C*                    CALL 'ADJUST'
     C*                    PARM '*LEFT   '$CCMD   8
     C*                    PARM           $CSTR  30
     C*                    MOVEL$CSTR     CUST#  11
     C*                    MOVELOHCNAM    CSNAME
     C*
     C*  Print headings.
     C*
     C           *IN91     IFEQ *ON
     C           *INL1     OREQ *ON
     C*                    WRITESKIP1
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C*    Create a quantity string for qty ordered
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDODQOR1    $PQTY1
     C                     Z-ADDODQOR2    $PQTY2
     C                     Z-ADDODQOR3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM  60
     C*
     C                     MOVEL$PQSTR    WOQORD
     C*
     C*    Create a quantity string for qty picked
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDODQPK1    $PQTY1
     C                     Z-ADDODQPK2    $PQTY2
     C                     Z-ADDODQPK3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C*
     C                     MOVEL$PQSTR    WOQPCK
     C*
     C*    Flag detail line if ordered and picked don't match.
     C*
     C           ODQOR1    IFNE ODQPK1
     C           ODQOR2    ORNE ODQPK2
     C           ODQOR3    ORNE ODQPK3
     C                     MOVE '*'       WOFLG
     C                     ELSE
     C                     MOVE ' '       WOFLG
     C                     ENDIF
     C*
     C*    Write detail line.
     C*
     C                     MOVELODITEM    WOITEM
     C                     WRITEDETAIL1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Stop/Cust# totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1         *IN90     IFEQ *ON
     CL1                   EXSR ZRHEAD
     CL1                   ENDIF
     CL1                   EXSR ZRTOTL
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Batch totals
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     C**         *IN90     IFEQ *ON
     C**                   EXSR ZRHEAD
     C**                   ENDIF
     C**                   EXSR ZRTOTL
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C                     MOVE TTL,1     TITLE
     C                     MOVELTITLE     $PSTR  60
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     TITLE
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C                     MOVELWHDESC    $PSTR  60
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*   Format Route number.
     C*
     C                     MOVE *BLANKS   BATCH
     C                     MOVE *BLANKS   $PSTR
     C           BATCH     CAT  'Route:':1BATCH
     C           BATCH     CAT  RHRTE:1   BATCH
     C                     MOVELBATCH     $PSTR  60
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center batch/route description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     BATCH
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     MOVE RPD,L     $RPD   20
     C*
     C           L         IFEQ 1
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
520 AC*  ZZFGTS   Get Host Stop Number
520 AC*
520 AC           ZZFGTS    BEGSR
520 AC*
520 AC*    Get Stop From RTESTP
520 AC*
520 AC                     CALL 'GETSTOP'
520 MC                     PARM '*ORDSTP '$SCMD   8
520 MC                     PARM OHWHSE    $SWHSE  30
520 MC                     PARM OHRTE     $SRTID  5
520 MC                     PARM OHORD     $SORD   70
520 MC                     PARM 0         $SDSTP  20
520 MC                     PARM           $SSTP   50
650aAC                     PARM           $PDROP  3
650aAC                     PARM           $PEXTD 30
650aAC                     PARM           $PSD    9
520 AC*
520 AC                     ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      TTL   Report title descriptions
Route Detail by Stop Report
**      RPD   Report level descriptions

 * TOTALS      *



