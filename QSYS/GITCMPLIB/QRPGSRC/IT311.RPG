     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT311     Inventory register
     F*  28 July, 1994
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*
     F*  Program Modification History
     F*  Revisions:
     F*
     F*    01/28/00  RH  4.13
     A*      - Remove Reorder and Replenish levels, add quantity avail.
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
413 AFITEMQTY IF  E           K        DISK
     FIT311PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
     I*----------------------------------------------------------------
     IITREC
     I                                              ITSTYPL1
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C*
     C           ITSTYP    IFEQ 'P'
     C                     MOVE ' PIR '   TYPE
     C                     ELSE
     C           ITSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type
     C*
     C           *INL1     IFEQ '1'
     C                     Z-ADD1         L
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
413 AC*
413 AC*
413 AC*    Create a quantity string to print.
413 AC*
413 AC           IQKEY     CHAINITEMQTY              77
413 AC           *IN77     IFEQ *OFF
413 AC*
413 AC*   Calculate available quantities.
413 AC*
413 AC           IQSTK1    SUB  IQALC1    AVAIL1
413 AC                     SUB  IQHLD1    AVAIL1
413 AC*
413 AC           IQSTK2    SUB  IQALC2    AVAIL2
413 AC                     SUB  IQHLD2    AVAIL2
413 AC*
413 AC           IQSTK3    SUB  IQALC3    AVAIL3
413 AC                     SUB  IQHLD3    AVAIL3
413 AC                     ELSE
413 AC                     Z-ADD0         AVAIL1  50
413 AC                     Z-ADD0         AVAIL2  30
413 AC                     Z-ADD0         AVAIL3  30
413 AC                     ENDIF
413 AC*
413 AC*
413 AC                     MOVE '*QTY2STR'$PCMD
413 AC                     Z-ADDAVAIL1    $PQTY1
413 AC                     Z-ADDAVAIL2    $PQTY2
413 AC                     Z-ADDAVAIL3    $PQTY3
413 AC                     MOVE *BLANKS   $PQSTR
413 AC*
413 AC                     CALL 'CVTQTY'
413 AC                     PARM           $PCMD   8
413 AC                     PARM           $PPRG  10
413 AC                     PARM           $PQTY1  70
413 AC                     PARM           $PQTY2  70
413 AC                     PARM           $PQTY3  70
413 AC                     PARM           $PUM1   2
413 AC                     PARM           $PUM2   2
413 AC                     PARM           $PUM3   2
413 AC                     PARM           $PQSTR 10
413 AC                     PARM           $PUSTR 17
413 AC                     PARM           $PRTN   8
413 AC                     PARM           $PERM  60
413 AC*
413 AC                     MOVE $PQSTR    W1QTY
413 AC*
413 AC*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN92
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   END
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
413 AC*
413 AC*     ITEMQTY KEY
413 AC*
413 AC           IQKEY     KLIST
413 AC                     KFLD           ITWHSE
413 AC                     KFLD           ITITEM
413 AC*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'IT311PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD3         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           ITWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDCLS,L     T#CLS
     C                     MOVE TYPE      T#DESC
     C*
     C*                    MOVE RPD,L     $RPD
     C           L         IFEQ 1
     C                     WRITETOTAL1
     C                     END
     C*
     C           L         IFEQ 2
     C                     WRITETOTAL2
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         CLS,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
