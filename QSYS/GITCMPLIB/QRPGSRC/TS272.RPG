     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  TS272     Employee management report for an employee(Summary)
     F*  19 February, 1996
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FTS271   IP  E           K        DISK
     FREPORTE IF  E           K        DISK
     FTS272PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   2 25
     E                    RPD     1   2  8
     E                    RECK        2  7 0
     E                    LVLK        2  7 0
     E*
     E                    DTOT        2  7 2
     E                    PTOT        2  7 0
     E                    CTOT        2 11 3
     E                    WTOT        2 11 2
     E*
     E                    DTE     1   4 22
     E                    A22        22  1
     E*
     I*----------------------------------------------------------------
     IWKREC
     I                                              WKGDTEL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Date redefinition data structure
     I*
     I$PFMDT      DS
     I                                        1   2 $PFMCC
     I                                        3   4 $PFMYY
     I                                        5   6 $PFMMM
     I                                        7   8 $PFMDD
     I$PTODT      DS
     I                                        1   2 $PTOCC
     I                                        3   4 $PTOYY
     I                                        5   6 $PTOMM
     I                                        7   8 $PTODD
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PFMDT  From date (YYYYMMDD)
     C*      $PTODT  To date (YYYYMMDD)
     C*      $PWHSE  Warehouse number
     C*      $PEMP#  Employee number
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PFMDT
     C                     PARM           $PTODT
     C                     PARM           $PWHSE  3
     C                     PARM           $PEMP#  5
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*  Level 1 break - Date
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*  Print headings.
     C*
     C*
     C*          *IN91     IFEQ *ON
     C*          *INL1     OREQ *ON
     C*                    ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C* Transaction date
     C           WKGDTE    IFNE 0
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    GTDATE
     C                     ELSE
     C                     Z-ADD0         GTDATE
     C                     ENDIF
     C           WKDTOT    DIV  60        DIRTOT    H
     C           DIRTOT    IFGT 0
     C           WKPTOT    DIV  DIRTOT    CASEPH    H
     C                     ELSE
     C                     Z-ADD0         CASEPH
     C                     ENDIF
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  DIRTOT    DTOT,1
     C                     ADD  WKPTOT    PTOT,1
     C                     ADD  WKCTOT    CTOT,1
     C                     ADD  WKWTOT    WTOT,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Transaction date break
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     C*                    WRITESKIP1
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Batch totals
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C*
     C                     MOVEL'TS272PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTE              79
     C           *IN79     IFEQ *OFF
     C                     MOVELERDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD60        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C*  Format date range
     C*
     C                     SELEC
     C           $PFMDT    WHNE *BLANKS
     C           $PTODT    ANDNE*BLANKS
     C                     MOVEADTE,1     A22
     C                     MOVEA$PFMMM    A22,1
     C                     MOVEA$PFMDD    A22,4
     C                     MOVEA$PFMYY    A22,7
     C                     MOVEA$PTOMM    A22,15
     C                     MOVEA$PTODD    A22,18
     C                     MOVEA$PTOYY    A22,21
     C                     MOVEAA22       DTERNG
     C*
     C           $PFMDT    WHNE *BLANKS
     C                     MOVEADTE,2     A22
     C                     MOVEA$PFMMM    A22,11
     C                     MOVEA$PFMDD    A22,14
     C                     MOVEA$PFMYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C           $PTODT    WHNE *BLANKS
     C                     MOVEADTE,3     A22
     C                     MOVEA$PTOMM    A22,11
     C                     MOVEA$PTODD    A22,14
     C                     MOVEA$PTOYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C                     OTHER
     C                     MOVEADTE,4     DTERNG
     C*
     C                     ENDSL
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C*
     C*  Get employee name
     C*
     C                     WRITEEPHEAD
     C*  Columns
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  DTOT,L    DTOT,M
     C                     ADD  PTOT,L    PTOT,M
     C                     ADD  CTOT,L    CTOT,M
     C                     ADD  WTOT,L    WTOT,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDDTOT,L    T#DTOT
     C                     Z-ADDPTOT,L    T#PTOT
     C                     Z-ADDCTOT,L    T#CTOT
     C                     Z-ADDWTOT,L    T#WTOT
     C           T#DTOT    IFGT 0
     C           T#PTOT    DIV  T#DTOT    T#CSPH    H
     C                     ELSE
     C                     Z-ADD0         T#CSPH
     C                     ENDIF
     C*                    Z-ADDRECK,L    T#RECK
     C*
     C                     MOVE RPD,L     $RPD    8
     C*
     C                     WRITETOTAL1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         DTOT,L
     C                     Z-ADD0         PTOT,L
     C                     Z-ADD0         CTOT,L
     C                     Z-ADD0         WTOT,L
     C*
     C                     ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions

 Totals
**      DTE   Date headings
99/99/99 thru 99/99/99
     From 99/99/99
     Thru 99/99/99

