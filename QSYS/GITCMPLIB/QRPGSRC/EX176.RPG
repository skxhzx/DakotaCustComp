     F*----------------------------------------------------------------
     F*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*----------------------------------------------------------------
     F*
     F*  EX176   Runner exception report.
     F*  24 May 2007
     F*  Dave Sommerville
     F*
     F*  Revisions:
     F*
510 AF*    05/24/07  DAS  5.10
     F*      - Created.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FITMEXCP IP  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FEX176PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
     E                    DESC    1   3 50
     I*----------------------------------------------------------------
     IEXREC
     I                                              EXESTAL2
     I                                              EXWHDPL1
     I                                              EXSTYPL1
     I*----------------------------------------------------------------
     I*  Redefinition data structures
     I*
     I            DS
     I                                        1  12 EXFDIS
     I                                       13  24 EXTDIS
     I*
     I                                        1   70DSRTRN
     I                                        8  140DSPTRN
     I                                       15  210DSPLBL
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C*
     C*  Format Dept/Section/Desig
     C*
     C           EXSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           EXSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*Both'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Dept:'   CAT  EXWHDP:1  P#DESC
     C           P#DESC    CAT  'Type:':2 P#DESC
     C           P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Dept/Section
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     ENDIF
     C*
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Convert Generated date
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE EXGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    GENDTE  60
     C*
     C*  Retrieve item description.
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   ITDESC
     C                     ENDIF
     C*
     C*  Setup error description for printing.
     C*
     C                     SELEC
     C           EXERID    WHEQ 'OVRZRO'
     C                     MOVELDESC,3    ERRMSG
     C                     Z-ADDDSRTRN    W1RTRN
     C                     Z-ADDDSPTRN    W1PTRN
     C                     Z-ADDDSPLBL    W1PLBL
     C                     OTHER
     C                     MOVELEXERID    ERRMSG    P
     C                     ENDSL
     C*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C           ITKEY     KLIST
     C                     KFLD           EXWHSE
     C                     KFLD           EXITEM
     C*
     CSR         EXKEY     KLIST
     CSR                   KFLD           EXWHSE
     CSR                   KFLD           EXWHDP
     CSR                   KFLD           EXSTYP
     CSR                   KFLD           EXITEM
     CSR                   KFLD           EXGDTE
     CSR                   KFLD           EXGTIM
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'EX176PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     C*
     C*   Convert dates to MMDDYY format.
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $FROM     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    FMDT    60
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $TO       $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    TODT    60
     C*
     C*
     CSR                   MOVE #PROG     $CVPRG
     C*
     C                     Z-ADD3         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           EXWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*  Format Dept/Section/Desig
     C*
     C           EXSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           EXSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*Both'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Dept:'   CAT  EXWHDP:1  P#DESC
     C           P#DESC    CAT  'Type:':2 P#DESC
     C           P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C                     EXSR ZRFRMT
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*                    WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDCLS,L     T#CLS
     C                     MOVE *BLANKS   T#DESC
     C           EXWHDP    CAT  '/':1     T#DESC
     C           T#DESC    CAT  TYPE:1    T#DESC
     C*
     C           L         IFEQ 1
     C                     WRITETOTAL1
     C                     ENDIF
     C*
     C           L         IFEQ 2
     C                     WRITETOTAL2
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         CLS,L
     C*
     C*    Setup heading for either open/closed exceptions.
     CSR                   SELEC
     CSR         EXESTA    WHEQ '1 '                       Open excpt
     CSR                   MOVELDESC,1    P#STAT
     C*
     CSR         EXESTA    WHEQ '4 '                       Closed excpt
     CSR                   MOVELDESC,2    P#STAT
     CSR                   ENDSL
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
** Descriptions
            Open Exceptions
            Closed Exceptions
Zero w/Avail Qty Override
