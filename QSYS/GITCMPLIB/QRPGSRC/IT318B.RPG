     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT318B    Item activity report Item/Date
     F*  Roxanne Harrison
     F*  07 December, 2000
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT318A  IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FIT318BPRO   E             90     PRINTER
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
415aME                    TABT    1   7  1
415aME                    TABTYP  1   7 10
     E                    RPD     1   3 25
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    ATL1        3  7 0
     E                    ATL2        3  7 0
     E                    ATL3        3  7 0
     E                    ITL1        3  7 0
     E                    ITL2        3  7 0
     E                    ITL3        3  7 0
     E                    OTL1        3  7 0
     E                    OTL2        3  7 0
     E                    OTL3        3  7 0
     E                    PTL1        3  7 0
     E                    PTL2        3  7 0
     E                    PTL3        3  7 0
     E                    RTL1        3  7 0
     E                    RTL2        3  7 0
     E                    RTL3        3  7 0
     E                    TTL1        3  7 0
     E                    TTL2        3  7 0
     E                    TTL3        3  7 0
     E                    NTL1        3  7 0
     E                    NTL2        3  7 0
     E                    NTL3        3  7 0
     I*****************************************************************
     IWKREC
     I                                              WKITEML2
     I                                              WKDATEL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $FROM  - From date - returned as unpacked numeric
     C*      $TO    - To date - returned as unpacked numeric
     C*      $ITEM  - Item # or *all
     C*      $DISP  - Slot or *all
     C*
     C*    Returned Parameters
     C*      None
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C                     PARM           $ITEM  15
     C*                    PARM           $DISP  12
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C           *IN90     IFEQ '1'
     C           FIRST     OREQ '1'
     C                     MOVE '1'       *IN95
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*  L2 break - Item change.
     C*
     C           *INL2     IFEQ '1'
     C                     MOVE '1'       *IN92
415 AC                     Z-ADD2         L
415 AC                     EXSR ZRZERO
     C                     END
     C*
     C*  L1 break - Date change.
     C*
     C           *INL1     IFEQ '1'
     C                     MOVE '1'       *IN91
415 AC                     Z-ADD1         L
415 AC                     EXSR ZRZERO
     C                     END
     C*
     C* Detail Calcs
     C*
     C*
     C* Lookup Type
     C           WKTYPE    LOKUPTABT      TABTYP         09
     C           *IN09     IFEQ '1'
     C                     MOVELTABTYP    W1TYPE 10
     C                     ELSE
415aMC                     MOVELWKTYPE    W1TYPE
     C                     ENDIF
     C* Convert date
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKDATE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    W1DATE  60
     C*
     C* Convert quantity
     C                     Z-ADDWKQTY     W1QTY   50
415  C*  Accumulate totals
     C*
     C                     SELEC
     C*
     C           WKTYPE    WHEQ 'A'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     ATL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     ATL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     ATL3,1
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'B'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     OTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     OTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     OTL3,1
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'I'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     ITL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     ITL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     ITL3,1
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'O'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     OTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     OTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     OTL3,1
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'P'
     C           WKGRP1    IFEQ 'R'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     NTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     NTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     NTL3,1
     C                     ENDIF
     C                     ELSE
     C*
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     PTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     PTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     PTL3,1
     C                     ENDIF
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'R'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     RTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     RTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     RTL3,1
     C                     ENDIF
     C*
     C           WKTYPE    WHEQ 'T'
     C           WKUTYP    IFEQ 'N'
     C                     ADD  WKQTY     TTL1,1
     C                     ENDIF
     C           WKUTYP    IFEQ '1'
     C                     ADD  WKQTY     TTL2,1
     C                     ENDIF
     C           WKUTYP    IFEQ '2'
     C                     ADD  WKQTY     TTL3,1
     C                     ENDIF
     C                     ENDSL
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       FIRST
     C*                    MOVE '0'       *IN91
     C*                    MOVE '0'       *IN92
     C*
     C*
     C*----------------------------------------------------------------
     C*
     C* DATE
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   EXSR ZRTOTL
     CL1         WKITEM    IFEQ SVITEM
     CL1                   MOVE *OFF      *IN92
     CL1                   ENDIF
     CL1                   WRITEDETAIL1
     CL1                   WRITESKIP1
     CL1                   MOVE '0'       *IN91
     CL1                   MOVE WKITEM    SVITEM
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* ITEM
     C*
     CL2                   Z-ADD2         L
     CL2                   EXSR ZRROLL
     C*
     CL2         RECK,L    IFGT 0
     C**                   EXSR ZRTOTL
     C**                   WRITEDETAIL1
     C**                   WRITESKIP1
     C**                   WRITESKIP1
     CL2                   MOVE '0'       *IN92
     CL2                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Batch totals
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITETOTAL1
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C           *LIKE     DEFN WKITEM    SVITEM
     CSR                   MOVE '1'       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     MOVE *OFF      BEGIN   1
     C                     EXSR ZRZERO
     C*
     C*   Convert dates to MMDDYY format.
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $FROM     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    W1FROM  60
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $TO       $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    W1TO    60
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           WKWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*
     C*   Move item # and slot description.
     C*
     C                     MOVE *BLANKS   ITEM
     C                     MOVEL$ITEM     ITEM
     C                     ENDSR
     C*****************************************************************
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         *INL1     IFEQ '0'
     CSR                   MOVE '1'       *IN91
     CSR                   END
     C*
     CSR         *INL2     IFEQ '0'
     CSR                   MOVE '1'       *IN92
     CSR                   END
     C*
     CSR                   MOVE '0'       *IN90
     CSR                   WRITEHEAD1
     CSR                   WRITECOLUMNS
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  ATL1,L    ATL1,M
     C                     ADD  ATL2,L    ATL2,M
     C                     ADD  ATL3,L    ATL3,M
     C                     ADD  ITL1,L    ITL1,M
     C                     ADD  ITL2,L    ITL2,M
     C                     ADD  ITL3,L    ITL3,M
     C                     ADD  OTL1,L    OTL1,M
     C                     ADD  OTL2,L    OTL2,M
     C                     ADD  OTL3,L    OTL3,M
     C                     ADD  PTL1,L    PTL1,M
     C                     ADD  PTL2,L    PTL2,M
     C                     ADD  PTL3,L    PTL3,M
     C                     ADD  RTL1,L    RTL1,M
     C                     ADD  RTL2,L    RTL2,M
     C                     ADD  RTL3,L    RTL3,M
     C                     ADD  TTL1,L    TTL1,M
     C                     ADD  TTL2,L    TTL2,M
     C                     ADD  TTL3,L    TTL3,M
     C                     ADD  NTL1,L    NTL1,M
     C                     ADD  NTL2,L    NTL2,M
     C                     ADD  NTL3,L    NTL3,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDATL1,L    T#ATL1  50
     C                     Z-ADDATL2,L    T#ATL2  50
     C                     Z-ADDATL3,L    T#ATL3  50
     C                     Z-ADDITL1,L    T#ITL1  50
     C                     Z-ADDITL2,L    T#ITL2  50
     C                     Z-ADDITL3,L    T#ITL3  50
     C                     Z-ADDOTL1,L    T#OTL1  50
     C                     Z-ADDOTL2,L    T#OTL2  50
     C                     Z-ADDOTL3,L    T#OTL3  50
     C                     Z-ADDPTL1,L    T#PTL1  50
     C                     Z-ADDPTL2,L    T#PTL2  50
     C                     Z-ADDPTL3,L    T#PTL3  50
     C                     Z-ADDRTL1,L    T#RTL1  50
     C                     Z-ADDRTL2,L    T#RTL2  50
     C                     Z-ADDRTL3,L    T#RTL3  50
     C                     Z-ADDTTL1,L    T#TTL1  50
     C                     Z-ADDTTL2,L    T#TTL2  50
     C                     Z-ADDTTL3,L    T#TTL3  50
     C                     Z-ADDNTL1,L    T#NTL1  50
     C                     Z-ADDNTL2,L    T#NTL2  50
     C                     Z-ADDNTL3,L    T#NTL3  50
     C*
     C*    Create a quantity string to print. (ADJ)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#ATL1    $PQTY1
     C                     Z-ADDT#ATL2    $PQTY2
     C                     Z-ADDT#ATL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    AQTY
     C*
     C*    Create a quantity string to print. (INV)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#ITL1    $PQTY1
     C                     Z-ADDT#ITL2    $PQTY2
     C                     Z-ADDT#ITL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    IQTY
     C*
     C*    Create a quantity string to print. (PCK)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#OTL1    $PQTY1
     C                     Z-ADDT#OTL2    $PQTY2
     C                     Z-ADDT#OTL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    OQTY
     C*
     C*    Create a quantity string to print. (RCV)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#PTL1    $PQTY1
     C                     Z-ADDT#PTL2    $PQTY2
     C                     Z-ADDT#PTL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    PQTY
     C*
     C*    Create a quantity string to print. (RPL)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#RTL1    $PQTY1
     C                     Z-ADDT#RTL2    $PQTY2
     C                     Z-ADDT#RTL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    RQTY
     C*
     C*    Create a quantity string to print. (TFR)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#TTL1    $PQTY1
     C                     Z-ADDT#TTL2    $PQTY2
     C                     Z-ADDT#TTL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    TQTY
     C*
     C*    Create a quantity string to print. (RTN)
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDT#NTL1    $PQTY1
     C                     Z-ADDT#NTL2    $PQTY2
     C                     Z-ADDT#NTL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD
     C                     PARM           $PPRG
     C                     PARM           $PQTY1
     C                     PARM           $PQTY2
     C                     PARM           $PQTY3
     C                     PARM           $PUM1
     C                     PARM           $PUM2
     C                     PARM           $PUM3
     C                     PARM           $PQSTR
     C                     PARM           $PUSTR
     C                     PARM           $PRTN
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    NQTY
     C*
     C                     MOVE *BLANKS   $RPD
     C                     MOVELRPD,L     $RPD   20
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         ATL1,L
     C                     Z-ADD0         ATL2,L
     C                     Z-ADD0         ATL3,L
     C                     Z-ADD0         ITL1,L
     C                     Z-ADD0         ITL2,L
     C                     Z-ADD0         ITL3,L
     C                     Z-ADD0         OTL1,L
     C                     Z-ADD0         OTL2,L
     C                     Z-ADD0         OTL3,L
     C                     Z-ADD0         PTL1,L
     C                     Z-ADD0         PTL2,L
     C                     Z-ADD0         PTL3,L
     C                     Z-ADD0         RTL1,L
     C                     Z-ADD0         RTL2,L
     C                     Z-ADD0         RTL3,L
     C                     Z-ADD0         TTL1,L
     C                     Z-ADD0         TTL2,L
     C                     Z-ADD0         TTL3,L
     C                     Z-ADD0         NTL1,L
     C                     Z-ADD0         NTL2,L
     C                     Z-ADD0         NTL3,L
     C*
     C                     ENDSR
      *----------------------------------------------------------------
     O*
     O*  COMPILE TIME TABLES
     O*
**
B
O
R
P
I
A
T
**
Bulk pick
Pick
Replenish
Put away
Inventory
Adjustment
Transfer
**      RPD   Report level descriptions
 * Item *
 ** Date **
 *** Totals
