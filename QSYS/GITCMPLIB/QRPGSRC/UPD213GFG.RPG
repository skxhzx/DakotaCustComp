     F*----------------------------------------------------------------
     F*
     F*  UPD213GFG Convert old PIRTRAN to new PIRTRAN file.
     F*  20 August, 1996
     F*  Hemant Kapadia
     F*
     F*  Note:  This program is a combination of CV112 and UPD213C.
     F*
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FINPUT   IP  E           K        DISK
     F            PTREC                             KRENAMEINPREC
     FTASK    IF  E           K        DISK
     FLABEL7  IF  E           K        DISK
     FPIRTRAN O   E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  30 ERFLD
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*
     I            DS
     I                                        1   60DSHMS
     I                                        1   40DSHM
     I                                        1   20DSHH
     I                                        3   40DSMM
     I                                        5   60DSSS
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Convert old transaction type to new task code.
     C*
     C                     SELEC
     C*
     C           PTTYPE    WHEQ 'BF'
     C                     MOVE 'BULKF '  PTTASK
     C                     MOVE 'S'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'BP'
     C                     MOVE 'BULKP '  PTTASK
     C                     MOVE 'S'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'BR'
     C                     MOVE 'BULKR '  PTTASK
     C                     MOVE 'S'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'FP'
     C                     MOVE 'FIXED '  PTTASK
     C                     MOVE 'S'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'PP'
     C                     MOVE 'PIR   '  PTTASK
     C                     MOVE 'S'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'PA'
     C                     MOVE 'PUTEMP'  PTTASK
     C                     MOVE 'P'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'RN'
     C                     MOVE 'PUTRTN'  PTTASK
     C                     MOVE 'R'       PTGRP1
     C*
     C           PTTYPE    WHEQ 'RP'
     C                     MOVE 'REPL  '  PTTASK
     C                     MOVE 'L'       PTGRP1
     C*
     C                     ENDSL
     C*
     C*  Get task category and subcategory.
     C*
     C           PTTASK    CHAINTASK                 79
     C           *IN79     IFEQ *OFF
     C                     MOVE TKCAT     PTCAT
     C                     MOVE TKSUB     PTSUB
     C                     ELSE
     C                     MOVE *BLANKS   PTCAT
     C                     MOVE *BLANKS   PTSUB
     C                     ENDIF
     C*
     C*  Set genereated date/time to old started date/time.
     C*
     C                     Z-ADDPTSDTE    PTGDTE
     C                     Z-ADDPTSTIM    PTGTIM
     C                     Z-ADDPTSDTE    PTRDTE
     C*
     C*  If old transaction ended then set new transaction to closed.
     C*
     C           PTEDTE    IFGT 0
     C                     Z-ADDPTEDTE    PTCDTE
     C                     Z-ADDPTETIM    PTCTIM
     C                     MOVE '4'       PTSTAT
     C                     MOVE 'N'       PTETYP
     C                     Z-ADDPTSTIM    STRTIM
     C                     Z-ADDPTETIM    FINTIM
     C                     EXSR ZZTIME
     C                     Z-ADDTIMLEN    PTDMIN
     C                     ELSE
     C                     MOVE '2'       PTSTAT
     C                     ENDIF
     C*
     C*  Move route or po to new field.
     C*
     C           PTPO      IFNE *BLANKS
     C                     MOVELPTPO      PTRTPO
     C                     ELSE
     C                     MOVELPTRTE     PTRTPO
     C                     ENDIF
     C*
     C*  Get warehouse, department and section from label file.
     C*
     C           KEYLB     SETLLLABEL7
     C           KEYLB     READELABEL7                   79
     C           *IN79     IFEQ *OFF
     C                     SELEC
     C           TKTLVL    WHEQ 'S'
     C                     Z-ADDLBWHSE    PTWHSE
     C                     MOVE LBWHDP    PTWHDP
     C                     MOVE LBSTYP    PTSTYP
     C           TKTLVL    WHEQ 'D'
     C                     Z-ADDLBWHSE    PTWHSE
     C                     MOVE LBWHDP    PTWHDP
     C                     MOVE *BLANKS   PTSTYP
     C                     OTHER
     C                     Z-ADDLBWHSE    PTWHSE
     C                     MOVE *BLANKS   PTWHDP
     C                     MOVE *BLANKS   PTSTYP
     C                     ENDSL
     C                     ENDIF
     C*
     C*  Clear all other fields.
     C*
     C                     MOVE *BLANKS   PTSHFT
     C                     MOVE *BLANKS   PTCREW
     C                     Z-ADD0         PTEMP#
     C                     Z-ADD0         PTAISL
     C                     Z-ADD0         PTQTY1
     C                     Z-ADD0         PTQTY2
     C                     Z-ADD0         PTQTY3
     C                     Z-ADD0         PTIMIN
     C*
     C*  Add record to new transaction file.
     C*
     C                     WRITEPTREC                  79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       ERROR
     C                     EXSR ZM71
     C                     ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE '0'       ERROR   1
     C*
     C*  Define key for LABEL file.
     C*
     CSR         KEYLB     KLIST
     CSR                   KFLD           PTTRN#
     C*
     C*  Initialize message handling
     C*
     CSR                   EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     CSR                   EXSR ZZINZ
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     C**                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C**                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR         *LIKE     DEFN PTSTIM    STRTIM
     CSR         *LIKE     DEFN PTETIM    FINTIM
     CSR                   MOVE #PROG     $PPRG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZTIME   Update for screen 1.
     C*
     CSR         ZZTIME    BEGSR
     C*
     C*      Calculate length of time in minutes for output.
     C*
     CSR                   Z-ADDSTRTIM    DSHMS
     C*   Round up minute, if the seconds is >= 30
     CSR         DSSS      IFGE 30
     CSR                   ADD  1         DSMM
     CSR                   ENDIF
     CSR         DSHH      MULT 60        TMPBEG  40
     CSR                   ADD  DSMM      TMPBEG
     C*
     CSR                   Z-ADDFINTIM    DSHMS
     C*   Round up minute, if the seconds is >= 30
     CSR         DSSS      IFGE 30
     CSR                   ADD  1         DSMM
     CSR                   ENDIF
     C*
     C*   If the ending time is < starting time, than
     C*   we have gone past the midnight.
     C*
     CSR         FINTIM    IFLT STRTIM
     CSR                   ADD  24        DSHH
     CSR                   ENDIF
     CSR         DSHH      MULT 60        TMPEND  40
     CSR                   ADD  DSMM      TMPEND
     C*
     CSR         *LIKE     DEFN PTDMIN    TIMLEN
     CSR         TMPEND    SUB  TMPBEG    TIMLEN
     CSR         TIMLEN    IFLT 0
     CSR                   Z-ADD0         TIMLEN
     CSR                   ENDIF
     C*
     CSR         ENDTIM    ENDSR
