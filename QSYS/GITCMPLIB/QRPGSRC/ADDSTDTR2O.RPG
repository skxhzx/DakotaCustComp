     F*----------------------------------------------------------------
     F*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (708) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  ADDSTDTRN2  Add standard transaction record (EPM version #2)
     F*  06 December 2002
     F*  Max Blacknik
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
416 AF*    12/06/02  MLB  4.16
     F*      - Created.
     F*
417xAF*    05/13/03  DAS  4.17x
     F*      **** CONVERTED TO ILE.
     F*      **** LOGIC INCORPORATED INTO ADJTRAN2.
     F*      **** NEW VERSION OF ADDSTDTRN2 JUST CALLS ADJTRAN2.
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*    - This program is identical to ADDSTDTRN except that instead
     F*      of retrieving the system date/time to start the trans-
     F*      action it uses the date/time passed into the program.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FTASK    IF  E           K        DISK
     FPIRTRAN O   E           K        DISK                      A
     FPIRUSER IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  PIRTRAN record layout for use as parameters
     I*
     IPTPARM    E DSPIRTRAN
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'ADJTSKDET'           C         @ADJTD
     I              'CVTDTE'              C         @CVTDT
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I                                        1  10 ERQTY
     I                                    P  11  120ERPAL
     I                                       13  27 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   *CREATE = Create transaction
     C*              *CRTSTRT= Create and start transaction
     C*      $PPRG   Program to send messages back to.
     C*      $PTRN#  Transaction number
     C*      $PTASK  Task code
     C*      $PWHSE  Warehouse
     C*      $PWHDP  Department
     C*      $PSTYP  Section
     C*      $PCUBE  Cube
     C*      $PSWGT  Weight
     C*      $PPCS   Pieces
     C*      $PAISL  Aisles
     C*      $PQTY1  User quantity 1
     C*      $PQTY2  User quantity 2
     C*      $PQTY3  User quantity 3
     C*      $PRTE   Route
     C*      $PPO    P.O.
     C*      $PGRP1  Group 1 code
     C*      $PGRP2  Group 2 code
     C*      $PDATE  Transaction date  (use if greater than zero)
     C*      $PTIME  Transaction time
     C*
     C*    Returned Parameters
     C*      $PRTN   *OK      - No problem encountered.
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*                         (for use with $PTYPE = *INTER).
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PTRN#  70
     C                     PARM           $PTASK  6
     C                     PARM           $PWHSE  30
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
     C                     PARM           $PCUBE  93
     C                     PARM           $PSWGT  92
     C                     PARM           $PPCS   50
     C                     PARM           $PAISL  50
     C                     PARM           $PQTY1  50
     C                     PARM           $PQTY2  50
     C                     PARM           $PQTY3  50
     C                     PARM           $PRTE   5
     C                     PARM           $PPO    9
     C                     PARM           $PGRP1  1
     C                     PARM           $PGRP2  1
     C                     PARM           $PDATE  80
     C                     PARM           $PTIME  60
     C                     PARM           $PRTN   8
     C                     PARM           $PMSG   4
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $PRTN
     C                     MOVE *BLANKS   $PMSG
     C*
     C*  Get task information.
     C*
     C           $PTASK    CHAINTASK                 79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   TKCAT
     C                     MOVE *BLANKS   TKSUB
     C                     ENDIF
     C*
     C*  Create transaction record.
     C*
     C*    Note: The task tracking level determines whether or not the
     C*          department and the section are written to the transaction
     C*          record.  This was done in order to make it possible to
     C*          use a simple join between the transaction records and
     C*          the task detail file which contains the standards.
     C*
     C                     CLEARPTREC
     C                     Z-ADD$PTRN#    PTTRN#
     C                     Z-ADD$PWHSE    PTWHSE
     C                     MOVE $PTASK    PTTASK
     C                     MOVE TKCAT     PTCAT
     C                     MOVE TKSUB     PTSUB
     C                     SELEC
     C           TKTLVL    WHEQ 'D'
     C                     MOVE $PWHDP    PTWHDP
     C           TKTLVL    WHEQ 'S'
     C                     MOVE $PWHDP    PTWHDP
     C                     MOVE $PSTYP    PTSTYP
     C                     ENDSL
     C                     Z-ADD$PCUBE    PTCUBE
     C                     Z-ADD$PSWGT    PTSWGT
     C                     Z-ADD$PPCS     PTPCS
     C                     Z-ADD$PAISL    PTAISL
     C                     Z-ADD$PQTY1    PTQTY1
     C                     Z-ADD$PQTY2    PTQTY2
     C                     Z-ADD$PQTY3    PTQTY3
     C*
     C                     SELEC
     C*    Use date/time passed in for Generated date/time.
     C           $PDATE    WHGT *ZEROS
     C                     Z-ADD$PDATE    PTGDTE
     C                     Z-ADD$PTIME    PTGTIM
     C*
     C                     OTHER
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    PTGDTE
     C                     TIME           PTGTIM
     C                     ENDSL
     C           $PPO      IFNE *BLANKS
     C                     MOVEL$PPO      PTRTPO
     C                     ELSE
     C                     MOVEL$PRTE     PTRTPO
     C                     ENDIF
     C*
     C                     SELEC
     C*    Use date/time passed in for Report date/time.
     C           $PDATE    WHGT *ZEROS
     C                     Z-ADD$PDATE    PTRDTE
     C*
     C                     OTHER
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    PTRDTE
     C                     ENDSL
     C*
     C                     MOVE $PGRP1    PTGRP1
     C                     MOVE $PGRP2    PTGRP2
     C*
     C                     SELEC
     C           $PCMD     WHEQ '*CREATE '
     C                     MOVE '1'       PTSTAT
     C*
     C           $PCMD     WHEQ '*CRTSTRT'
     C                     MOVE '2'       PTSTAT
     C           #USER     CHAINPIRUSER              79
     C           *IN79     IFEQ *OFF
     C                     MOVE USSHFT    PTSHFT
     C                     MOVE USCREW    PTCREW
     C                     Z-ADDUSEMP#    PTEMP#
     C                     ELSE
     C                     MOVE *BLANKS   PTSHFT
     C                     MOVE *BLANKS   PTCREW
     C                     Z-ADD0         PTEMP#
     C                     ENDIF
     C*
     C                     SELEC
     C*    Use date/time passed in for Transaction start date/time.
     C           $PDATE    WHGT *ZEROS
     C                     Z-ADD$PDATE    PTSDTE
     C                     Z-ADD$PTIME    PTSTIM
     C*
     C                     OTHER
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    PTSDTE
     C                     TIME           PTSTIM
     C*
     C                     ENDSL
     C                     ENDSL
     C*
     C                     WRITEPTREC
     C*
     C                     SELEC
     C           $PCMD     WHEQ '*CREATE '
     C*
     C*  Update transaction detail record.
     C*
     C                     CALL @ADJTD
     C                     PARM '*GEN    '@PCMD   8
     C                     PARM '*INTER  '@PTYPE  8
     C                     PARM           PTPARM
     C                     PARM ' '       @PRTN   8
     C                     PARM ' '       @PMSG   4
     C*
     C           $PCMD     WHEQ '*CRTSTRT'
     C*
     C*  Update transaction detail record.
     C*
     C                     CALL @ADJTD
     C                     PARM '*START  '@PCMD   8
     C                     PARM '*INTER  '@PTYPE  8
     C                     PARM           PTPARM
     C                     PARM ' '       @PRTN   8
     C                     PARM ' '       @PMSG   4
     C                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*
     CSR                   ENDSR
