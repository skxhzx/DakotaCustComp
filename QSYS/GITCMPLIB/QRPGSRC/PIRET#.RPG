     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PIRET#    Get next PIR equipment trans number
     F*  09 January 2003
     F*  Roxanne Harrison
     F*
     F*----------------------------------------------------------------
     F*
     F*  File specs
     F*
     FPIRET#  UF  E           K        DISK                      A
     FEQUIPT1 IF  E           K        DISK
     C*----------------------------------------------------------------
     C*
     C*  Subprogram parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse.
     C*
     C*    Returned Parameters
     C*      $NXTNM  Next PIR equipment transaction number
     C*
     C*----------------------------------------------------------------
     C* Main Line
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $NXTNM  70
     C*
     C*  Get record with next number.  Create it if necessary.
     C*
     C           $PWHSE    CHAINPIRET#               79
     C           *IN79     IFEQ *ON
     C                     Z-ADD$PWHSE    NXEWHS
     C                     Z-ADD0         NXENUM
     C                     WRITENXEREC
     C           $PWHSE    CHAINPIRET#               79
     C                     ENDIF
     C                     Z-ADDNXENUM    NEXT#   70
     C*
     C*  File contains the next sequence number to use.  If it
     C*  has already been used then keep adding 1 until we find one
     C*  that hasn't been used (we will take the chance that all
     C*  9999999 have not been used).
     C*
     C                     MOVE *OFF      STOP    1
     C           STOP      DOUEQ*ON
     C           NEXT#     IFEQ 0
     C                     Z-ADD1         NEXT#
     C                     ENDIF
     C                     Z-ADDNEXT#     ETTRN#
     C           FILEKY    SETLLEQUIPT1                  50
     C           *IN50     IFEQ *OFF
     C                     MOVE *ON       STOP
     C                     ELSE
     C                     ADD  1         NEXT#
     C                     ENDIF
     C                     ENDDO
     C*
     C*  Return the unique sequence number.
     C*
     C                     Z-ADDNEXT#     $NXTNM
     C*
     C*  Update the file with the next number to be used.
     C*
     C           NEXT#     ADD  1         NXENUM
     C                     UPDATNXEREC
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for EQUIPT1.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           ETTRN#
     CSR                   ENDSR
