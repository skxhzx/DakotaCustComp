     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT312     Item/Slot activity - Options
     F*  Hemant Kapadia
     F*  Roxanne Harrison
     F*
     F*  Revisions
     F*
     F*    01/12/96  DAS  2.10
     F*      - Took out PIRUSER file and corresponding code and replaced
     F*        with call to GETWHSE.
     F*
     F*    06/05/96  RH   2.13
     F*      - Add options to select order of report, item or All &
     F*        slot or All.
     F*      - Remove RPTTTL from screen, code as a constant.
416a F*    01/02/02  RH   4.16a
     F*      - Write base item and alias items to work file if base
     F*        item # was entered. If alias entered, write only alias.
416b F*    01/07/02  RH   4.16b
     F*      - Set flag if alias item exists, pass parm.
416c F*    12/17/02  RH   4.16c
     F*      - Must enter valid base slot or *all
     F*      - Add $DISP1 to contain base slot with *
416dAF*    01/31/03  MLB  4.16d
     F*      - Enh: Revised ZZFIL1 to load blanks for W1ITEM
     F*        instead of defaulting to *ALL.
     F*        Revised ZZCHK1 to not allow item number to be blank.
417aAF*    05/21/03  RH   4.17a
     F*      - FIX: Replace ZZITEM with call to IT312A.
510a F*    06/28/06  HNK  5.10a
     F*      - Enh: Default dates to today's date and Print or view
     F*             option to View.
510bAF*    07/07/06  MLB  5.10b
     F*      - Fix: Add option 4 to list of valid sort by options.
530aAF*    11/01/10  RH   5.30a
     F*      - Enh: Added client custom code support to program.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT312FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
210 DF*IRUSER IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FRPTOVR1 IF  E           K        DISK
416a FPIRITEM IF  E           K        DISK
416c FSLOT2   IF  E           K        DISK
416a FITEMLNK1IF  E           K        DISK
416b FITEMLNK IF  E           K        DISK
416b F            ILREC                             KRENAMERECORD
416a FIT312   O   E           K        DISK                      A
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   6 60
     I*----------------------------------------------------------------
530aAI*  Client ID's
     I*
JOR A /COPY QCOPYSRC,IL#JORDANO
     I*
530aAI              'GETCLIENT'           C         @GETCL
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
213 AI                                       22  26 $LWHDP
213 AI                                       27  41 $LITEM
213 AI                                       42  53 $LDISP
213 AI*
213 AI                                       22  36 $LITM1
213 AI*
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1   60ERDATE
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*              *PIR     - Select only PIR items
     C*              *FIXED   - Select only Fixed items
     C*              *BOTH    - Select all items
     C*      $PLNK   'Y'      - Linked item exists
     C*              'N'      - No linked items.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE   3
     C                     PARM           $PRTN   8
     C                     PARM           $JOBQ   1
     C                     PARM           $DEV   10
     C                     PARM           $FORM  10
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C                     PARM           $VP     1
213 AC                     PARM           $WHDP   5
213 AC                     PARM           $SORT   1
213 AC                     PARM           $ITEM  15
213 AC                     PARM           $DISP  12
416bAC                     PARM           $PLNK   1
416cAC                     PARM           $DISP1 12
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE '1'       *IN97
     C                     MOVE '0'       *IN90
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ '1'
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE '1'       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ '1'
     CSR                   MOVE '1'       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     3031
     CSR                   SETOF                     98
     CSR                   MOVE '0'       WARN    1
     CSR                   MOVE '0'       ERROR   1
     CSR                   MOVE '0'       CMDTKN  1
416a CSR                   MOVE '0'       STOP    1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ '1'
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     *
530aA*  Get Client ID
     *
530aAC                     CALL @GETCL
530aAC                     PARM           CLIENT 10
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*
     C*    PIR9901  Invalid date entered.
     C*
     CSR         ZM9901    BEGSR
     CSR                   MOVE 'PIR9901' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    PIR9906  Invalid date entered.
     C*
     CSR         ZM9906    BEGSR
     CSR                   MOVE 'PIR9906' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
213 AC*    PIR0203  Value cannot be blank.
213 AC*
213 ACSR         ZM0203    BEGSR
213 ACSR                   MOVE 'PIR0203' #MSGID
213 ACSR                   MOVE '*DIAG  ' #MSGTP
213 ACSR                   MOVEADESC,6    ERRMSG
213 ACSR                   MOVEAERRMSG    $MD,1
213 ACSR                   EXSR ZMPMSG
213 ACSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1006  Invalid jobq option for view report option.
     C*
     CSR         ZM1006    BEGSR
     CSR                   MOVE 'PIR1006' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
416cAC*    IT31201  Can't enter virtual slot, enter base.
416cAC*
416cACSR         ZM1201    BEGSR
416cACSR                   MOVE 'IT31201' #MSGID
416cACSR                   MOVE '*DIAG  ' #MSGTP
416cACSR                   MOVEAERRMSG    $MD
416cACSR                   EXSR ZMPMSG
416cACSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
213 AC*
213 AC*  Verify warehouse department.
213 AC*
213 ACSR         W1WHDP    IFNE *BLANKS
213 ACSR                   MOVE '*VERIFY '$LCMD
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1WHDP    $LWHDP
213 ACSR                   CALL 'WD900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*ERROR  '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN31
213 ACSR                   MOVE *ON       *IN11
213 ACSR                   MOVEL$LERM     ERRMSG
213 ACSR                   EXSR ZM0105
213 ACSR                   ELSE
213 ACSR         $LRTN     IFEQ '*PGMQ   '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN31
213 ACSR                   MOVE *ON       *IN11
213 ACSR                   MOVE $LMSG     #MSGK
213 ACSR                   EXSR ZMQMSG
213 ACSR                   ENDIF
213 ACSR                   ENDIF
213 ACSR                   ENDIF
     C*
     C*  Verify dates.
     C*
     C*
     C*     Verify FROM date
     C*
     CSR                   MOVE '*MDYEDT '$CVCMD
     CSR                   MOVE W1FMDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVEL$CVERM    ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $CVMSG    #MSGK
213 DC*R                   EXSR ZMCMSG
     CSR                   Z-ADDW1FMDT    ERDATE
     CSR                   EXSR ZM9906
     CSR                   ELSE
     CSR                   MOVE $CVD6O    W1FMDT
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*     Verify TO date
     C*
     CSR                   MOVE '*MDYEDT '$CVCMD
     CSR                   MOVE W1TODT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVEL$CVERM    ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $CVMSG    #MSGK
213 DC*R                   EXSR ZMCMSG
     CSR                   Z-ADDW1TODT    ERDATE
     CSR                   EXSR ZM9906
     CSR                   ELSE
     CSR                   MOVE $CVD6O    W1TODT
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*     FROM date can not be greater than TO date.
     C*
     CSR         ERROR     IFEQ *OFF
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1FMDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    W1FRM8  80
     C*
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1TODT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    W1TO8   80
     C*
     CSR         W1FRM8    IFGT W1TO8
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN23
     CSR                   EXSR ZM9901
     CSR                   ENDIF
     CSR                   ENDIF
213 AC*
213 AC*  Verify order by option.
213 AC*
213 ACSR         W1SORT    IFNE 1
213 ACSR         W1SORT    ANDNE2
213 ACSR         W1SORT    ANDNE3
510bAC           W1SORT    ANDNE4
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN28
213 ACSR                   MOVE *ON       *IN08
213 ACSR                   MOVE *BLANKS   ERRMSG
213 ACSR                   MOVELDESC,5    ERRMSG
213 ACSR                   EXSR ZM1001
213 ACSR                   ENDIF
213 AC*
213 AC*  Verify item number if entered.
213 AC*
213 ACSR         W1ITEM    IFNE '*ALL'
416dDC*R         W1ITEM    ANDNE*BLANKS
213 ACSR                   MOVE '*VERIFY '$LCMD
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1ITEM    $LITM1
213 ACSR                   CALL 'IT900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*ERROR  '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN29
213 ACSR                   MOVE *ON       *IN09
213 ACSR                   MOVEL$LERM     ERRMSG
213 ACSR                   EXSR ZM0105
213 ACSR                   ELSE
213 ACSR         $LRTN     IFEQ '*PGMQ   '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN29
213 ACSR                   MOVE *ON       *IN09
213 ACSR                   MOVE $LMSG     #MSGK
213 ACSR                   EXSR ZMQMSG
213 ACSR                   ENDIF
213 ACSR                   ENDIF
213 ACSR                   ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,2    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default device
     C*
     CSR                   MOVELW1DEV     WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1DEV     ANDNE'*SYSVAL'
     CSR         W1DEV     ANDNE'*JOB'
     CSR         W1DEV     OREQ *BLANKS
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE '1'       *IN25
     CSR                   MOVE '1'       *IN05
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default form type
     C*
     CSR                   MOVELW1FORM    WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1FORM    ANDNE'*STD'
     CSR         W1FORM    OREQ *BLANKS
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE '1'       *IN26
     CSR                   MOVE '1'       *IN06
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,4    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify view/print.
     C*
     CSR         W1VP      IFNE 'V'
     CSR         W1VP      ANDNE'P'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN27
     CSR                   MOVE *ON       *IN07
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,1    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
213 AC*
213 AC*  Cannot run on jobq, if we want to view the report.
213 AC*
213 ACSR         W1VP      IFEQ 'V'
213 ACSR         W1JOBQ    ANDEQ'Y'
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN24
213 ACSR                   MOVE *ON       *IN04
213 ACSR                   MOVE *BLANKS   ERRMSG
213 ACSR                   EXSR ZM1006
213 ACSR                   ENDIF
213 AC*
213 AC*  Verify slot.
213 AC*
213 ACSR         W1DISP    IFNE '*ALL'
213 ACSR                   MOVE '*VERIFY '$LCMD
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1WHDP    $LWHDP
213 ACSR                   MOVE *BLANKS   $LITEM
213 ACSR                   MOVE W1DISP    $LDISP
213 ACSR                   CALL 'SL900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*ERROR  '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN30
213 ACSR                   MOVE *ON       *IN10
213 ACSR                   MOVEL$LERM     ERRMSG
213 ACSR                   EXSR ZM0105
213 ACSR                   ELSE
213 ACSR         $LRTN     IFEQ '*PGMQ   '
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN30
213 ACSR                   MOVE *ON       *IN10
213 ACSR                   MOVE $LMSG     #MSGK
213 ACSR                   EXSR ZMQMSG
213 ACSR                   ENDIF
213 ACSR                   ENDIF
213 ACSR                   ENDIF
213 AC*
213 AC*  If slot was entered, Whse department can't be blank.
213 AC*
213 ACSR         W1DISP    IFNE *BLANKS
213 ACSR         W1DISP    ANDNE'*ALL'
213 ACSR         W1WHDP    ANDEQ*BLANKS
213 ACSR                   MOVE *ON       ERROR
213 ACSR                   MOVE *ON       *IN31
213 ACSR                   MOVE *ON       *IN11
213 ACSR                   MOVE *BLANKS   ERRMSG
213 ACSR                   EXSR ZM0203
213 ACSR                   ENDIF
213 AC*
416cAC*  Entered slot can not be virtual slot.
416cAC*
416cACSR         ERROR     IFEQ *OFF
416cACSR         W1DISP    ANDNE'*ALL'
416cACSR         SLKEY     CHAINSLOT2                79
416cACSR         *IN79     IFEQ *OFF
416cACSR         SLRSRV    ANDEQ'V'
416cACSR                   MOVE *ON       ERROR
416cACSR                   MOVE *ON       *IN30
416cACSR                   MOVE *ON       *IN10
416cACSR                   MOVE *BLANKS   ERRMSG
416cACSR                   EXSR ZM1201
416cACSR                   ENDIF
416cACSR                   ENDIF
416cAC*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE '0'       CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Warehouse.
     C*
     CSR         ROW#      WHEQ 08
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE #PROG     $LPRG
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
213 AC*
213 AC*     Warehouse department.
213 AC*
213 ACSR         ROW#      WHEQ 9
213 ACSR                   MOVE *ON       *IN11
213 ACSR                   MOVE '*LOOKUP '$LCMD
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1WHDP    $LWHDP
213 ACSR                   CALL 'WD900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*SELECT '
213 ACSR                   MOVE $LWHDP    W1WHDP
213 ACSR                   ENDIF
213 AC*
213 AC*     Item.
213 AC*
213 ACSR         ROW#      WHEQ 14
213 ACSR                   MOVE *ON       *IN09
213 ACSR                   MOVE '*LOOKUP '$LCMD
213 ACSR                   MOVE #PROG     $LPRG
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1ITEM    $LITM1
213 ACSR                   CALL 'IT900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*SELECT '
213 ACSR                   MOVE $LITM1    W1ITEM
213 ACSR                   ENDIF
213 AC*
213 AC*
213 AC*     SLOT.
213 AC*
213 ACSR         ROW#      WHEQ 15
213 ACSR                   MOVE *ON       *IN10
213 ACSR                   MOVE '*GENERAL'$LCMD
213 ACSR                   Z-ADDW1WHSE    $LWHSE
213 ACSR                   MOVE W1WHDP    $LWHDP
213 ACSR                   MOVE *BLANKS   $LITEM
213 ACSR                   MOVE W1DISP    $LDISP
213 ACSR                   CALL 'SL900'
213 ACSR                   PARM           $LPARM
213 ACSR         $LRTN     IFEQ '*SELECT '
213 ACSR                   MOVE $LDISP    W1DISP
213 ACSR                   ENDIF
     C*
     CSR                   OTHER
     C*
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
510aDC**                   MOVE 'P'       W1VP
510a CSR                   MOVE 'V'       W1VP
     C*
     C*   Initialize printer device/form
     C*
     C*      Get warehouse for user.
     C*
210 DC*R         #USER     CHAINPIRUSER              79
210 DC*R         *IN79     IFEQ *ON
210 DC*R                   Z-ADD1         WKWHSE
210 DC*R                   Z-ADD1         W1WHSE
210 DC*R                   ELSE
210 DC*R                   Z-ADDUSWHSE    WKWHSE
210 DC*R                   Z-ADDUSWHSE    W1WHSE
210 DC*R                   ENDIF
210 AC*
210 ACSR                   CALL 'GETWHSE'
210 ACSR                   PARM           #USER
210 ACSR         W1WHSE    PARM 0         #WHSE   30
210 ACSR                   Z-ADDW1WHSE    WKWHSE
210 AC*
     C*
510a C*  Convert today's date into MMDDYY format.
     C*
510a CSR                   MOVE '*CURMDY '$CVCMD
510a CSR                   CALL @CVTDT
510a CSR                   PARM           $CVTDT
510a CSR                   Z-ADD$CVD6O    W1FMDT
510a CSR                   Z-ADD$CVD6O    W1TODT
     C*
     CSR                   MOVE *BLANKS   WKSTYP
     CSR                   MOVE *BLANKS   WKWHDP
     CSR                   MOVE 'N'       W1JOBQ
213 ACSR                   MOVE *BLANKS   W1WHDP
213 ACSR                   Z-ADD1         W1SORT
416dDC*R                   MOVEL'*ALL'    W1ITEM
416dMCSR                   MOVE *BLANKS   W1ITEM
213 ACSR                   MOVEL'*ALL'    W1DISP
     C*
     CSR         ROKEY     CHAINRPTOVR1              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE RODEV     W1DEV
     CSR                   MOVE ROFORM    W1FORM
     CSR                   ENDIF
     C*
     CSR         *IN79     IFEQ *ON
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE RPDEV     W1DEV
     CSR                   MOVE RPFORM    W1FORM
     CSR                   ELSE
     CSR                   MOVEL'*JOB    'W1DEV
     CSR                   MOVEL'*STD    'W1FORM
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
416a CSR         *LIKE     DEFN W1ITEM    KYITEM
416a CSR         ITKEY     KLIST
416a CSR                   KFLD           KYWHSE  30
416a CSR                   KFLD           KYITEM
     C*
416c CSR         SLKEY     KLIST
416c CSR                   KFLD           W1WHSE
416c CSR                   KFLD           W1WHDP
416c CSR                   KFLD           W1DISP
     C*
     CSR         ROKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     CSR                   KFLD           WKWHSE  30
     CSR                   KFLD           WKSTYP  1
     CSR                   KFLD           WKWHDP  5
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF
213 DC*
213 DC*R                   MOVEL'IT316PR 'WKPRTF
213 DC*R                   MOVE *BLANKS   RPTTTL
213 DC*R         RPKEY     CHAINREPORTS              79
213 DC*R         *IN79     IFEQ *OFF
213 DC*R                   MOVELRPDESC    $PSTR  60
213 DC*R                   ELSE
213 DC*R                   MOVE *BLANKS   $PSTR
213 DC*R                   ENDIF
213 DC*R                   Z-ADD40        $PLEN   30
     C*
213 DC*R                   CALL 'CENTER'
213 DC*R                   PARM           $PSTR
213 DC*R                   PARM           $PLEN
213 DC*R                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   MOVE #PROG     $CVPRG
     CSR                   MOVE #PROG     $LPRG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
416a C*----------------------------------------------------------------
416a C*
416a C*  ZZITEM   Get linked items for work file.
416a C*
416a CSR         ZZITEM    BEGSR
416a C*  Write entered item to work file
416a CSR                   Z-ADDW1WHSE    WKWHSE
416a CSR                   MOVE W1ITEM    WKITEM
416a CSR                   WRITEWKREC
416a C*  Write alias item to work file
416a CSR                   Z-ADDW1WHSE    KYWHSE
416a CSR                   MOVE W1ITEM    KYITEM
416a CSR         ITKEY     CHAINPIRITEM              79
416a CSR         *IN79     IFEQ *OFF
416a CSR         ITTYPE    ANDEQ' '
416a CSR         ITKEY     SETLLITEMLNK1             79
416a CSR         STOP      DOUEQ'1'
416a CSR         ITKEY     READEITEMLNK1                 79
416a CSR         *IN79     IFEQ '1'
416a CSR                   MOVE '1'       STOP
416a CSR                   ELSE
416a CSR                   Z-ADDILWHSE    WKWHSE
416a CSR                   MOVE ILITEM    WKITEM
416a CSR                   WRITEWKREC
416a CSR                   ITER
416a CSR                   ENDIF
416a CSR                   ENDDO
416a CSR                   ENDIF
416a C*
416a C*  Check if linked item exists.
416b C*  Check if alias item was entered.
416b CSR                   MOVE 'N'       $PLNK
416b CSR         ITKEY     CHAINITEMLNK              78
416b CSR         *IN78     IFEQ *OFF
416b CSR                   MOVE 'Y'       $PLNK
416b CSR                   ELSE
416b C*  Check if base item was entered.
416b CSR         ITKEY     CHAINITEMLNK1             78
416b CSR         *IN78     IFEQ *OFF
416b CSR                   MOVE 'Y'       $PLNK
416b CSR                   ENDIF
416b CSR                   ENDIF
416a C*
416a CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
416a CSR         W1ITEM    IFNE '*ALL'
416a CSR         W1ITEM    ANDNE*BLANKS
417a CSR                   MOVE W1WHSE    $WHSEA  3
417aACSR                   MOVE W1ITEM    $ITEM
417aACSR                   MOVE *BLANKS   $LINK   1
417aACSR                   MOVE *BLANKS   $RTNCD  8
417a CSR                   CALL 'IT312A'
417a CSR                   PARM           $WHSEA
417a CSR                   PARM           $ITEM
417a CSR                   PARM           $LINK
417a CSR                   PARM           $RTNCD
417aDC**                   EXSR ZZITEM
416a CSR                   ENDIF
     C*
417a CSR                   MOVE $LINK     $PLNK
     C*
     CSR                   MOVE W1WHSE    $WHSE
     C*
     CSR                   MOVE W1JOBQ    $JOBQ
     C*
     CSR                   MOVE W1DEV     $DEV
     C*
     CSR                   MOVE W1FORM    $FORM
     C*
     CSR                   MOVE W1FRM8    $FROM
     CSR                   MOVE W1TO8     $TO
     CSR                   MOVE W1VP      $VP
213 ACSR                   MOVE W1WHDP    $WHDP
213 ACSR                   MOVE W1SORT    $SORT
213 ACSR                   MOVE W1ITEM    $ITEM
     CSR                   MOVE W1DISP    $DISP
416CACSR                   MOVE *BLANKS   $DISP1
416cACSR         W1DISP    IFNE '*ALL'
416cACSR         W1DISP    CAT  '*':0     $DISP1
416cACSR                   ENDIF
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Print option
Job queue option
Printer device
Form type
Order by option
Department
