     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  ER231     Thru-put and number of cases selected.
     F*  16 January, 1996
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
     F*
409 AF*  06/05/98  HNK  4.09
     F*    - Control break added on date.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FER231   IP  E           K        DISK
     FREPORTE IF  E           K        DISK
     FER231PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
409 ME                    RPD     1   4 25
409 ME                    RECK        4  7 0
409 ME                    LVLK        4  7 0
409 DE*                   RPD     1   3 25
409 DE*                   RECK        3  7 0
409 DE*                   LVLK        3  7 0
     E*
     E                    DTE     1   4 22
     E                    A22        22  1
     E*
409 ME                    SUNT        4  7 0
409 ME                    MONT        4  7 0
409 ME                    TUET        4  7 0
409 ME                    WEDT        4  7 0
409 ME                    THUT        4  7 0
409 ME                    FRIT        4  7 0
409 ME                    SATT        4  7 0
409 ME                    WIKT        4  9 0
409 ME                    PCPH        4  7 2
     E*
409 ME                    SUNM        4  7 0
409 ME                    MONM        4  7 0
409 ME                    TUEM        4  7 0
409 ME                    WEDM        4  7 0
409 ME                    THUM        4  7 0
409 ME                    FRIM        4  7 0
409 ME                    SATM        4  7 0
409 ME                    WIKM        4  9 0
     E*
409 DE*                   SUNT        3  7 0
409 DE*                   MONT        3  7 0
409 DE*                   TUET        3  7 0
409 DE*                   WEDT        3  7 0
409 DE*                   THUT        3  7 0
409 DE*                   FRIT        3  7 0
409 DE*                   SATT        3  7 0
409 DE*                   WIKT        3  9 0
409 DE*                   PCPH        3  7 2
     E*
409 DE*                   SUNM        3  7 0
409 DE*                   MONM        3  7 0
409 DE*                   TUEM        3  7 0
409 DE*                   WEDM        3  7 0
409 DE*                   THUM        3  7 0
409 DE*                   FRIM        3  7 0
409 DE*                   SATM        3  7 0
409 DE*                   WIKM        3  9 0
     E*
     E                    WKB        99  8 0
     E                    WKE        99  8 0
     E*
     I*----------------------------------------------------------------
     IWKREC
409 MI                                              WKWNUML3
409 MI                                              WKEMP#L2
409 AI                                              WKGDTEL1
     I*
409 DI*                                             WKWNUML2
409 DI*                                             WKEMP#L1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   60T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     I*  Date redefinition data structure
     I*
     I$PFMDT      DS
     I                                        1   2 $PFMCC
     I                                        3   4 $PFMYY
     I                                        5   6 $PFMMM
     I                                        7   8 $PFMDD
     I$PTODT      DS
     I                                        1   2 $PTOCC
     I                                        3   4 $PTOYY
     I                                        5   6 $PTOMM
     I                                        7   8 $PTODD
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PFMDT  From date (YYYYMMDD)
     C*      $PTODT  To date (YYYYMMDD)
     C*      $PWEEK  Number of weeks
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PFMDT
     C                     PARM           $PTODT
     C                     PARM           $PWEEK  2
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
409 MC*  Level 3 break - Week
     C*
409 MC           *INL3     IFEQ *ON
409 MC                     Z-ADD3         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
409 MC*  Level 2 break - Employee number
     C*
     C           *INL2     IFEQ *ON
     C                     Z-ADD2         L
     C                     EXSR ZRZERO
     C                     ENDIF
409 AC*
409 AC*  Level 1 break - Date
409 AC*
409 AC           *INL1     IFEQ *ON
409 AC                     Z-ADD1         L
409 AC                     EXSR ZRZERO
409 AC                     ENDIF
     C*
     C*  Print headings.
     C*
409 MC           *IN93     IFEQ *ON
409 MC           *INL3     OREQ *ON
409 DC***        *IN92     IFEQ *ON
409 DC***        *INL2     OREQ *ON
     C                     EXSR ZRDATE
     C                     WRITEDTHEAD
     C                     WRITECOLUMNS
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C* Convert date
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    T#DATE  60
     C*  Find day of the week
     C                     ADD  0         DTFRMT  10
     C                     MOVE T#DATE    G$DATE
     C                     Z-ADD2         DTFRMT
     C                     EXSR GTOJ$
     C*
     C                     SELEC
     C* Sunday
     C           G$DW      WHEQ 1
     C                     Z-ADDWKPCS     SUNPCS  50
     C           WKDMIN    ADD  WKIMIN    SUNMIN  50
     C* Monday
     C           G$DW      WHEQ 2
     C                     Z-ADDWKPCS     MONPCS  50
     C           WKDMIN    ADD  WKIMIN    MONMIN  50
     C* Tuesday
     C           G$DW      WHEQ 3
     C                     Z-ADDWKPCS     TUEPCS  50
     C           WKDMIN    ADD  WKIMIN    TUEMIN  50
     C* Wednesday
     C           G$DW      WHEQ 4
     C                     Z-ADDWKPCS     WEDPCS  50
     C           WKDMIN    ADD  WKIMIN    WEDMIN  50
     C* Thursday
     C           G$DW      WHEQ 5
     C                     Z-ADDWKPCS     THUPCS  50
     C           WKDMIN    ADD  WKIMIN    THUMIN  50
     C* Friday
     C           G$DW      WHEQ 6
     C                     Z-ADDWKPCS     FRIPCS  50
     C           WKDMIN    ADD  WKIMIN    FRIMIN  50
     C* Saturday
     C           G$DW      WHEQ 7
     C                     Z-ADDWKPCS     SATPCS  50
     C           WKDMIN    ADD  WKIMIN    SATMIN  50
     C*
     C                     ENDSL
     C*  Accumulate totals
     C*
     C                     ADD  SUNPCS    SUNT,1
     C                     ADD  MONPCS    MONT,1
     C                     ADD  TUEPCS    TUET,1
     C                     ADD  WEDPCS    WEDT,1
     C                     ADD  THUPCS    THUT,1
     C                     ADD  FRIPCS    FRIT,1
     C                     ADD  SATPCS    SATT,1
     C*
     C                     ADD  SUNMIN    SUNM,1
     C                     ADD  MONMIN    MONM,1
     C                     ADD  TUEMIN    TUEM,1
     C                     ADD  WEDMIN    WEDM,1
     C                     ADD  THUMIN    THUM,1
     C                     ADD  FRIMIN    FRIM,1
     C                     ADD  SATMIN    SATM,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
409 AC                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
409 AC*----------------------------------------------------------------
409 AC*
409 AC* Date
409 AC*
409 ACL1                   Z-ADD1         L
409 ACL1                   EXSR ZRROLL
     C*----------------------------------------------------------------
     C*
     C* Employee number
     C*
409 ACL2                   Z-ADD2         L
409 ACL2                   MOVELWKNAME    EMNAME
409 ACL2                   ADD  SUNT,L    WIKT,L
409 ACL2                   ADD  MONT,L    WIKT,L
409 ACL2                   ADD  TUET,L    WIKT,L
409 ACL2                   ADD  WEDT,L    WIKT,L
409 ACL2                   ADD  THUT,L    WIKT,L
409 ACL2                   ADD  FRIT,L    WIKT,L
409 ACL2                   ADD  SATT,L    WIKT,L
     C*
409 ACL2                   ADD  SUNM,L    WIKM,L
409 ACL2                   ADD  MONM,L    WIKM,L
409 ACL2                   ADD  TUEM,L    WIKM,L
409 ACL2                   ADD  WEDM,L    WIKM,L
409 ACL2                   ADD  THUM,L    WIKM,L
409 ACL2                   ADD  FRIM,L    WIKM,L
409 ACL2                   ADD  SATM,L    WIKM,L
409 ACL2         WIKM,L    DIV  60        HOURS   72H
409 ACL2         HOURS     IFGT 0
409 ACL2         WIKT,L    DIV  HOURS     PCPH,L    H
409 ACL2                   ELSE
409 ACL2                   Z-ADD0         PCPH,L
409 ACL2                   ENDIF
409 ACL2                   EXSR ZRROLL
     C*
409 ACL2         RECK,L    IFGT 0
409 ACL2                   EXSR ZRTOTL
409 ACL2                   ENDIF
     C*
409 DC*1                   MOVELWKNAME    EMNAME
409 DC*1                   ADD  SUNT,1    WIKT,1
409 DC*1                   ADD  MONT,1    WIKT,1
409 DC*1                   ADD  TUET,1    WIKT,1
409 DC*1                   ADD  WEDT,1    WIKT,1
409 DC*1                   ADD  THUT,1    WIKT,1
409 DC*1                   ADD  FRIT,1    WIKT,1
409 DC*1                   ADD  SATT,1    WIKT,1
     C*
409 DC*1                   ADD  SUNM,1    WIKM,1
409 DC*1                   ADD  MONM,1    WIKM,1
409 DC*1                   ADD  TUEM,1    WIKM,1
409 DC*1                   ADD  WEDM,1    WIKM,1
409 DC*1                   ADD  THUM,1    WIKM,1
409 DC*1                   ADD  FRIM,1    WIKM,1
409 DC*1                   ADD  SATM,1    WIKM,1
409 DC*1         WIKM,1    DIV  60        HOURS   72H
409 DC*1         HOURS     IFGT 0
409 DC*1         WIKT,1    DIV  HOURS     PCPH,1    H
409 DC*1                   ELSE
409 DC*1                   Z-ADD0         PCPH,1
409 DC*1                   ENDIF
409 DC*1                   Z-ADD1         L
409 DC*1                   EXSR ZRROLL
     C*
409 DC*1         RECK,L    IFGT 0
409 DC*1                   EXSR ZRTOTL
409 DC*1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Week totals
     C*
409 MCL3                   Z-ADD3         L
409 MCL3                   EXSR ZRROLL
     C*
409 MCL3         RECK,L    IFGT 0
409 MCL3                   EXSR ZRTOTL
409 MCL3                   WRITESKIP1
409 MCL3                   WRITESKIP1
409 MCL3                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Grand totals
     C*
409 MCLR                   Z-ADD4         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C                     Z-ADD0         J$JD    50
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     MOVE $PWEEK    WEEK    20
     C                     MOVE #PROG     $CVPRG
     C                     EXSR ZRZERO
     C                     EXSR ZZWARY
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           GTOJ$     BEGSR
     C                     MOVE G$DATE    G$DATE  60
     C* SPLIT DATE INTO MONTH, DAY, YEAR FIELDS
     C           DTFRMT    IFEQ 1                          BLVL-01
     C                     MOVELG$DATE    G$YY    20
     C                     MOVE G$DATE    WORK4   4
     C                     MOVELWORK4     G$MM    20
     C                     MOVE WORK4     G$DD    20
     C                     ELSE                            EELS-01
     C                     MOVELG$DATE    G$MM
     C                     MOVE G$DATE    WORK4
     C                     MOVELWORK4     G$DD
     C                     MOVE WORK4     G$YY
     C                     END                             EEND-01
     C* CREATE 4 DIGIT YEAR
     C                     MOVE G$YY      G$YYYY  40
     C           G$YY      IFGE 60                         BLVL-01
     C                     MOVEL'19'      G$YYYY
     C                     ELSE                            EELS-01
     C                     MOVEL'20'      G$YYYY
     C                     END                             EEND-01
     C                     Z-ADD0         G$JD
     C           G$MM      SUB  3         G$MMWK  20
     C                     Z-ADDG$MMWK    GMWKSV  20
     C           G$MMWK    IFLT 0                           LVL-01
     C                     ADD  12        G$MMWK
     C                     END                              END-01
     C           GMWKSV    IFNE 0                           LVL-01
     C           G$MMWK    MULT 30.6      G$JD      H
     C                     END                              END-01
     C                     ADD  G$DD      G$JD    50
     C           G$YYYY    SUB  1900      G$YYWK  30
     C           G$YYWK    IFNE 0                           LVL-01
     C           GMWKSV    IFLT 0                           LVL-02
     C                     SUB  1         G$YYWK
     C                     END                              END-02
     C                     END                              END-01
     C           G$YYWK    MULT 365.25    G$JYD   70
     C                     ADD  G$JYD     G$JD
     C           G$JD      DIV  7         G$WK7   70
     C                     MVR            G$DW    10
     C* MAKE SUNDAY = 1ST DAY OF THE WEEK
     C           G$DW      IFLE 3
     C                     ADD  4         G$DW
     C                     ELSE
     C                     SUB  3         G$DW
     C                     ENDIF
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  JTOJ$    Julian to Julian date conversion
     C*
     C           JTOG$     BEGSR
     C           J$JD      DIV  365.25    J$YYWK  30
     C           J$JD      DIV  365.25    J$TST   99
     C           J$TST     IFEQ 0                           LVL-01
     C                     SUB  1         J$YYWK
     C                     END                              END-01
     C           J$YYWK    MULT 365.25    J$YD    70
     C           J$JD      SUB  J$YD      J$YD
     C           J$YD      IFGT 306                         LVL-01
     C                     ADD  1         J$YYWK
     C                     END                              END-01
     C                     Z-ADD0         J$X     20
     C           J$YD      DOULEJ$MD                        LVL-01
     C                     ADD  1         J$X
     C           J$X       MULT 30.6      J$MD    30H
     C                     END                              END-01
     C                     SUB  1         J$X
     C           J$X       MULT 30.6      J$MD      H
     C           J$YD      SUB  J$MD      J$DD    20
     C           J$X       ADD  3         J$MM    20
     C           J$MM      IFGT 12                          LVL-01
     C                     SUB  12        J$MM
     C                     END                              END-01
     C           J$YYWK    ADD  1900      J$YYYY  40
     C           DTFRMT    IFEQ 1                          BLVL-01
     C                     MOVE J$YYYY    WORK2   2
     C                     MOVELJ$MM      WORK4   4
     C                     MOVE J$DD      WORK4
     C                     MOVELWORK2     J$DATE  60
     C                     MOVE WORK4     J$DATE
     C                     ELSE                            EELS-01
     C                     MOVE J$YYYY    WORK2
     C                     MOVELJ$MM      WORK4
     C                     MOVE J$DD      WORK4
     C                     MOVE WORK2     J$DATE
     C                     MOVELWORK4     J$DATE
     C                     END                             EEND-01
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRDATE    BEGSR
     C                     Z-ADDWKWNUM    N
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKB,N     $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    BGDATE
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKE,N     $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    ENDATE
     C*
     C                     ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C*
     C                     MOVEL'ER231PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTE              79
     C           *IN79     IFEQ *OFF
     C                     MOVELERDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C*  Format date range
     C*
     C                     SELEC
     C           $PFMDT    WHNE *BLANKS
     C           $PTODT    ANDNE*BLANKS
     C                     MOVEADTE,1     A22
     C                     MOVEA$PFMMM    A22,1
     C                     MOVEA$PFMDD    A22,4
     C                     MOVEA$PFMYY    A22,7
     C                     MOVEA$PTOMM    A22,15
     C                     MOVEA$PTODD    A22,18
     C                     MOVEA$PTOYY    A22,21
     C                     MOVEAA22       DTERNG
     C*
     C           $PFMDT    WHNE *BLANKS
     C                     MOVEADTE,2     A22
     C                     MOVEA$PFMMM    A22,11
     C                     MOVEA$PFMDD    A22,14
     C                     MOVEA$PFMYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C*          $PTODT    WHNE *BLANKS
     C*                    MOVEADTE,3     A22
     C                     MOVEA$PTOMM    A22,11
     C                     MOVEA$PTODD    A22,14
     C                     MOVEA$PTOYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C                     OTHER
     C                     MOVEADTE,4     DTERNG
     C*
     C                     ENDSL
     C*
     C                     MOVE *BLANKS   SUBTTL
     C*
     C*   Center report sub-title.
     C*
     C                     MOVELSUBTTL    $PSTR
     C                     Z-ADD40        $PLEN
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     SUBTTL
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
409 AC           *INL3     IFEQ *OFF
409 AC                     MOVE *ON       *IN93
409 AC                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  SUNT,L    SUNT,M
     C                     ADD  MONT,L    MONT,M
     C                     ADD  TUET,L    TUET,M
     C                     ADD  WEDT,L    WEDT,M
     C                     ADD  THUT,L    THUT,M
     C                     ADD  FRIT,L    FRIT,M
     C                     ADD  SATT,L    SATT,M
     C                     ADD  WIKT,L    WIKT,M
     C                     ADD  PCPH,L    PCPH,M
     C*
     C                     ADD  SUNM,L    SUNM,M
     C                     ADD  MONM,L    MONM,M
     C                     ADD  TUEM,L    TUEM,M
     C                     ADD  WEDM,L    WEDM,M
     C                     ADD  THUM,L    THUM,M
     C                     ADD  FRIM,L    FRIM,M
     C                     ADD  SATM,L    SATM,M
     C                     ADD  WIKM,L    WIKM,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDSUNT,L    T#SUNT
     C                     Z-ADDMONT,L    T#MONT
     C                     Z-ADDTUET,L    T#TUET
     C                     Z-ADDWEDT,L    T#WEDT
     C                     Z-ADDTHUT,L    T#THUT
     C                     Z-ADDFRIT,L    T#FRIT
     C                     Z-ADDSATT,L    T#SATT
     C                     Z-ADDWIKT,L    T#WIKT
     C                     Z-ADDPCPH,L    T#PCPH
     C                     Z-ADDRECK,L    T#RECK  30
     C*
     C                     MOVE RPD,L     $RPD   25
     C*
409 MC           L         IFEQ 2
     C                     WRITEDETAIL1
     C                     ENDIF
409 MC           L         IFEQ 3
     C           SUNM,L    IFGT 0
     C           SUNM,L    DIV  60        HOURS     H
     C           T#SUNT    DIV  HOURS     T#SUPH    H
     C                     ELSE
     C                     Z-ADD0         T#SUPH
     C                     ENDIF
     C           MONM,L    IFGT 0
     C           MONM,L    DIV  60        HOURS     H
     C           T#MONT    DIV  HOURS     T#MOPH    H
     C                     ELSE
     C                     Z-ADD0         T#MOPH
     C                     ENDIF
     C           TUEM,L    IFGT 0
     C           TUEM,L    DIV  60        HOURS     H
     C           T#TUET    DIV  HOURS     T#TUPH    H
     C                     ELSE
     C                     Z-ADD0         T#TUPH
     C                     ENDIF
     C           WEDM,L    IFGT 0
     C           WEDM,L    DIV  60        HOURS     H
     C           T#WEDT    DIV  HOURS     T#WEPH    H
     C                     ELSE
     C                     Z-ADD0         T#WEPH
     C                     ENDIF
     C           THUM,L    IFGT 0
     C           THUM,L    DIV  60        HOURS     H
     C           T#THUT    DIV  HOURS     T#THPH    H
     C                     ELSE
     C                     Z-ADD0         T#THPH
     C                     ENDIF
     C           FRIM,L    IFGT 0
     C           FRIM,L    DIV  60        HOURS     H
     C           T#FRIT    DIV  HOURS     T#FRPH    H
     C                     ELSE
     C                     Z-ADD0         T#FRPH
     C                     ENDIF
     C           SATM,L    IFGT 0
     C           SATM,L    DIV  60        HOURS     H
     C           T#SATT    DIV  HOURS     T#SAPH    H
     C                     ELSE
     C                     Z-ADD0         T#SAPH
     C                     ENDIF
     C                     WRITETOTAL1
     C                     WRITETOTAL2
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         SUNT,L
     C                     Z-ADD0         MONT,L
     C                     Z-ADD0         TUET,L
     C                     Z-ADD0         WEDT,L
     C                     Z-ADD0         THUT,L
     C                     Z-ADD0         FRIT,L
     C                     Z-ADD0         SATT,L
     C                     Z-ADD0         WIKT,L
     C                     Z-ADD0         PCPH,L
     C*
     C                     Z-ADD0         SUNM,L
     C                     Z-ADD0         MONM,L
     C                     Z-ADD0         TUEM,L
     C                     Z-ADD0         WEDM,L
     C                     Z-ADD0         THUM,L
     C                     Z-ADD0         FRIM,L
     C                     Z-ADD0         SATM,L
     C                     Z-ADD0         WIKM,L
     C*
     C                     Z-ADD0         SUNPCS
     C                     Z-ADD0         MONPCS
     C                     Z-ADD0         TUEPCS
     C                     Z-ADD0         WEDPCS
     C                     Z-ADD0         THUPCS
     C                     Z-ADD0         FRIPCS
     C                     Z-ADD0         SATPCS
     C*
     C                     Z-ADD0         SUNMIN
     C                     Z-ADD0         MONMIN
     C                     Z-ADD0         TUEMIN
     C                     Z-ADD0         WEDMIN
     C                     Z-ADD0         THUMIN
     C                     Z-ADD0         FRIMIN
     C                     Z-ADD0         SATMIN
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZWARY  Fill week array
     C*
     CSR         ZZWARY    BEGSR
     C*  CONVERT DATE FROM CMD TO MDY
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE $PFMDT    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    T#DATE  60
     C*  CONVERT DATE FROM MDY TO JULIAN
     C                     MOVE T#DATE    G$DATE
     C                     Z-ADD2         DTFRMT  10
     C                     EXSR GTOJ$
     C                     Z-ADDG$JD      BEGJUL  50
     C*
     C*  Find end date of the last week.
     C*
     C                     Z-ADD0         NDAYS   50
     C           WEEK      SUB  1         NWEEK   20
     C           NWEEK     MULT 7         NDAYS
     C                     ADD  6         NDAYS
     C           BEGJUL    ADD  NDAYS     ENDJUL
     C                     Z-ADDENDJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    ENDGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE ENDGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    TODT    80
     C                     MOVE $CVD8O    $PTODT
     C*
     C*  Fill array with individual week's beg. and end date.
     C*
     C           BEGJUL    ADD  6         ENDJUL  50
     C*
     C                     DO   WEEK      N       30
     C*  Begining of the week
     C                     Z-ADDBEGJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    BEGGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE BEGGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    BEGCMD  80
     C*  End of the week
     C                     Z-ADDENDJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    ENDGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE ENDGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    ENDCMD  80
     C*
     C                     Z-ADDBEGCMD    WKB,N
     C                     Z-ADDENDCMD    WKE,N
     C           ENDJUL    ADD  1         BEGJUL
     C           BEGJUL    ADD  6         ENDJUL
     C                     ENDDO
     C*
     CSR                   ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions
 * Not used *
 * Not used *
 Pieces per hour:
 *** Grand totals ***
**      DTE   Date headings
99/99/99 thru 99/99/99
     From 99/99/99
     Thru 99/99/99
 * Not used *
