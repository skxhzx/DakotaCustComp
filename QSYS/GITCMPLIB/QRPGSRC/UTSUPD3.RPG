     F*----------------------------------------------------------------
     F*
     F*  UTSUPD3   Reset PIR picking sequences custom for SFC, MA.
     F*  24 April, 2000
     F*  Roxanne Harrison
     F*  NOTES:
     F*  Hard coded for BFC Aisle C level 1-6 (NDS-aisle F)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT1   UF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Define partial key for SLOT1.
     C*
     C           *LIKE     DEFN SLWHSE    $PWHSE
     C           *LIKE     DEFN SLWHDP    $PWHDP
     C           *LIKE     DEFN SLAISL    $PAISL
     C           SLOTKY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           $PWHDP
     C                     KFLD           $PAISL
     C*
     C*  Hard code aisle being resequenced.
     C*
     C                     Z-ADD1         $PWHSE
     C                     MOVE 'DRY  '   $PWHDP    P
     C                     MOVE '  C'     $PAISL
     C*
     C*  Loop through all the records for the aisle.
     C*
     C           SLOTKY    SETLLSLOT1                7979
     C           *IN79     DOWEQ*OFF
     C           SLOTKY    READESLOT1                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*     Change picking sequence to slot location.
     C*
     C                     Z-ADD0         TMPSEQ  50
     C                     MOVELSLDISP    TMPDSP  1
     C           SLRLVL    IFLT 7
     C           SLPICK    ANDEQ'Y'
     C           TMPDSP    ANDEQ'F'
     C           SLLOC     SUB  2000      TMPSEQ
     C           TMPSEQ    MULT -1        SLPSEQ
     C*
     C*     Update slot record with new sequence.
     C*
     C                     UPDATSLREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
