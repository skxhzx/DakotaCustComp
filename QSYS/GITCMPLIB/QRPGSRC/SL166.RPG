     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SL166     Verify zero slot exception report.
     F*  02 April, 1997
     F*  Roxanne Harrison
     F*
     F*  Revisions:
     A*  08/31/00  RH  4.14a
     A*   - Add detail2 to list all slots associated with exception
     A*     slot.
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSL166   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
414aAFSLOT2   IF  E           K        DISK
414aAFVSLOT2  IF  E           K        DISK
414aAFVSLOT1  IF  E           K        DISK
     F            VSREC                             KRENAMEVSPREC
414aAFPIRITEM IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FVSLEXCP UF  E           K        DISK
     FSL166PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
     I*----------------------------------------------------------------
     IOQREC
     I                                              VXWHDPL1
     I                                              VXSTYPL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PR     1
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C*
     C*  Format Dept/Section/Desig
     C*
     C           VXSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           VXSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Dept:'   CAT  VXWHDP:1  P#DESC
     C           P#DESC    CAT  'Type:':2 P#DESC
     C           P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Dept/Section
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     ENDIF
     C*
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Convert Generated date
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE VXGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    GENDTE  60
414a C*
414a C*  Get entered date.
414aAC           EXKEY     CHAINSLOT2                77
414aAC           *IN77     IFEQ *OFF
414aAC*  Convert Entered date
414aAC                     MOVE '*CMDMDY '$CVCMD
414aAC                     MOVE SLENTD    $CVD8I
414aAC                     CALL @CVTDT
414aAC                     PARM           $CVTDT
414aAC                     Z-ADD$CVD6O    WOENTD  60
414aAC                     MOVE SLDISP    WODISP
414aAC                     MOVE SLITEM    WOITEM
414aAC                     MOVE SLITEM    KYITEM
414aAC           ITKEY     CHAINPIRITEM              76
414aAC*
414aAC           *IN76     IFEQ *OFF
414aAC                     MOVELITDESC    WODESC
414aAC                     ELSE
414aAC                     MOVE *BLANKS   WODESC
414aAC                     ENDIF
414aAC*
414aAC                     ELSE
414aAC                     MOVE *BLANKS   WOITEM
414aAC                     Z-ADD0         WOENTD
414aAC                     MOVE *BLANKS   WODISP
414aAC                     MOVE *BLANKS   WODESC
414aAC                     ENDIF
     C*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Update printed date.
     C*
     C           $PR       IFEQ 'P'
     C           EXKEY     CHAINVSLEXCP              79
     C           *IN79     IFEQ *OFF
     C                     Z-ADDTODAY     VXPDTE
     C                     TIME           VXPTIM
     C                     UPDATVXREC
     C                     ENDIF
     C                     ENDIF
414a C*
414aAC*  Check if slot is virtual.
414a C*
414aAC           EXKEY     CHAINSLOT2                77
414aAC           *IN77     IFEQ *OFF
414aAC           SLRSRV    IFEQ 'V'
414aAC                     EXSR ZZBSLT
414aAC                     EXSR ZZVSLT
414aAC                     ELSE
414aAC                     MOVE VXDISP    KYPDSP
414aAC                     EXSR ZZVSLT
414aAC                     ENDIF
414aAC                     ENDIF
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
414aAC*  Define key for PIRITEM file.
414aAC*
414aACSR         *LIKE     DEFN SLITEM    KYITEM
414aACSR         ITKEY     KLIST
414aACSR                   KFLD           VXWHSE
414aACSR                   KFLD           KYITEM
     C*
414aAC*  Define key for VSLOT1 file.
414aAC*
414aACSR         *LIKE     DEFN VXDISP    KYVDSP
414aACSR         VSKEY1    KLIST
414aACSR                   KFLD           VXWHSE
414aACSR                   KFLD           VXWHDP
414aACSR                   KFLD           KYVDSP
     C*
414aAC*  Define key for VSLOT2 file.
414aAC*
414aACSR         *LIKE     DEFN VXDISP    KYPDSP
414aACSR         VSKEY2    KLIST
414aACSR                   KFLD           VXWHSE
414aACSR                   KFLD           VXWHDP
414aACSR                   KFLD           KYPDSP
     C*
414aAC*  Define key for SLOT2 file.
414aAC*
414aACSR         *LIKE     DEFN VXDISP    KYDISP
414aACSR         SLKEY2    KLIST
414aACSR                   KFLD           VXWHSE
414aACSR                   KFLD           VXWHDP
414aACSR                   KFLD           KYDISP
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     CSR         EXKEY     KLIST
     CSR                   KFLD           VXWHSE
     CSR                   KFLD           VXWHDP
     CSR                   KFLD           VXDISP
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'SL166PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   MOVE #PROG     $CVPRG
     C*
     C                     Z-ADD3         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           VXWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*  Format Dept/Section/Desig
     C*
     C           VXSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           VXSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Dept:'   CAT  VXWHDP:1  P#DESC
     C           P#DESC    CAT  'Type:':2 P#DESC
     C           P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C                     EXSR ZRFRMT
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*                    WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDCLS,L     T#CLS
     C                     MOVE TYPE      T#DESC
     C*
     C*                    MOVE RPD,L     $RPD
     C           L         IFEQ 1
     C                     WRITETOTAL1
     C                     ENDIF
     C*
     C           L         IFEQ 2
     C                     WRITETOTAL2
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         CLS,L
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
414a C*
414aAC*  Get Base slot for virtual exception slot.
414aACSR         ZZBSLT    BEGSR
414a C*
414aAC                     MOVE *BLANKS   WOITEM
414aAC                     Z-ADD0         WOENTD
414aAC                     MOVE *BLANKS   WODISP
414aAC                     MOVE *BLANKS   WODESC
414aACSR                   MOVE VXDISP    KYVDSP
414aAC*  Get physical slot for virtual slot.
414aACSR         VSKEY1    CHAINVSLOT1               79
414aACSR         *IN79     IFEQ *OFF                             01
414aACSR                   MOVE VSPDIS    KYPDSP
414a C*
414a C*  Get entered date for physical slot.
414aAC           VSKEY2    CHAINSLOT2                77
414aAC           *IN77     IFEQ *OFF
414aAC*  Convert Entered date
414aAC                     MOVE '*CMDMDY '$CVCMD
414aAC                     MOVE SLENTD    $CVD8I
414aAC                     CALL @CVTDT
414aAC                     PARM           $CVTDT
414aAC                     Z-ADD$CVD6O    WOENTD  60
414aAC                     MOVE SLDISP    WODISP
414aAC                     MOVE SLITEM    WOITEM
414aAC                     MOVE SLITEM    KYITEM
414aAC           ITKEY     CHAINPIRITEM              76
414aAC*
414aAC           *IN76     IFEQ *OFF
414aAC                     MOVELITDESC    WODESC
414aAC                     ELSE
414aAC                     MOVE *BLANKS   WODESC
414aAC                     ENDIF
414aAC*
414aAC                     ENDIF
414aACSR                   WRITEDETAIL2
414aAC                     ENDIF
414aAC*
414aAC                     ENDSR
     C*----------------------------------------------------------------
414a C*
414aAC*  Print all virtual slots for BASE exception slot.
414aACSR         ZZVSLT    BEGSR
414a C*
414aACSR         VSKEY2    SETLLVSLOT2
414aACSR                   MOVE *OFF      DONE    1
414aACSR                   MOVE *OFF      STOP    1
414aACSR         STOP      DOUEQ*ON
414aACSR         VSKEY2    READEVSLOT2                   79
414a C*
414aAC*  Skip if virtual slot is same as exception slot.
414aACSR         *IN79     IFEQ *OFF                             01
414aACSR         VSDISP    ANDEQVXDISP
414aACSR                   ITER
414aACSR                   ENDIF
414a C*
414aACSR         *IN79     IFEQ *ON                              01
414aACSR                   MOVE *ON       STOP
414aACSR                   ELSE
414aACSR                   MOVE VSDISP    KYDISP
414aACSR         SLKEY2    CHAINSLOT2                77
414aAC*
414aACSR         *IN77     IFEQ *OFF                             02
414aAC*
414aAC*  Convert Entered date
414aAC                     MOVE '*CMDMDY '$CVCMD
414aAC                     MOVE SLENTD    $CVD8I
414aAC                     CALL @CVTDT
414aAC                     PARM           $CVTDT
414aAC                     Z-ADD$CVD6O    WOENTD  60
414aACSR                   MOVE SLDISP    WODISP
414aACSR                   MOVE SLITEM    WOITEM
414aACSR                   MOVE SLITEM    KYITEM
414aACSR         ITKEY     CHAINPIRITEM              76
414aAC*
414aACSR         *IN76     IFEQ *OFF                             03
414aACSR                   MOVELITDESC    WODESC
414aACSR                   ELSE
414aACSR                   MOVE *BLANKS   WODESC
414aACSR                   ENDIF                                 E3
414aAC*
414aACSR                   ELSE
414aACSR                   MOVE *BLANKS   WOITEM
414aACSR                   Z-ADD0         WOENTD
414aACSR                   MOVE *BLANKS   WODISP
414aACSR                   MOVE *BLANKS   WODESC
414aACSR                   ENDIF                                 E2
414aACSR                   WRITEDETAIL2
414aAC                     ENDIF                                 E1
414aAC                     ENDDO
414aAC*
414aAC                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
