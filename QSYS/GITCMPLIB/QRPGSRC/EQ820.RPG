     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  EQ820    Equipment 8 X 20 menu
     F*  27 March, 2003
     F*  Roxanne Harrison
     F*
     F*  Notes
     F*
     F*    8 X 20 Screens for hand held scanners
     F*    Used as default menu for equipment user to force equipment
     F*    check out before using equipment.
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*    This program uses a revised method to display error messages.
     F*    Instead of the program writting each message to the subfile
     F*    using the message key the subfile has been revised to display
     F*    all messages from the program message queue automatically.
     F*
     F*    All changes have been marked with MSG in position 1.
     F*
     F*----------------------------------------------------------------
     F*  Menu Notes
     F*
     F*    The following needs to be done to add a new screen of       .
     F*    menu options:
     F*                                                                y
     F*      1) Add 5 to the number of table entries for the MNU
     F*         E-Spec.
     F*      2) Add 5 lines to the actual table at the end of the
     F*         program.
     F*      3) Add 1 to the value being loaded into field MAXMNU
     F*         in routine ZZINZ2.
     F*
     F*    The following needs to be done to utilize a new menu        .
     F*    option:
     F*                                                                y
     F*      1) Increase the table entries for TABOPT/TABPGM for
     F*         the number of option(s) being added.
     F*      2) Add the option(s) and corresponding program name to
     F*         the TABOPT/TABPGM table at the end of the program.
     F*
     F*    The following needs to be done to to remove an option       .
     F*    from being used:
     F*                                                                y
     F*      1) Decrease the table entries for TABOPT/TABPGM for
     F*         the number of option(s) being removed.
     F*      2) Remove the option(s) and corresponding program name
     F*         in the TABOPT/TABPGM table at the end of the program.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  81        Rollup
     F*  82        Rolldown
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FEQ820FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
MSG DF*                                       MSGK  KSFILE MSGREC
     FEQUIPT3 IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    TABOPT  1  21  3   TABPGM 10
     E                    MNU     1  30 20
     E*
     I*----------------------------------------------------------------
     I*  Data structure for menu display
     I*
     IDSMENU      DS
     I                                        1  20 WOLIN1
     I                                       21  40 WOLIN2
     I                                       41  60 WOLIN3
     I                                       61  80 WOLIN4
     I                                       81 100 WOLIN5
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1   2 EROPT
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*
     C*      $POPT     Selected option (0 = Exit)
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $POPT   2
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $POPT
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C*
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
MSG DC**                   EXSR ZMCMSG
MSG MCSR                   EXSR ZMCPMQ
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     CSR                   SELEC
     C**         STATUS    WHEQ ROLLUP
     CSR         *IN81     WHEQ *ON
414aACSR         MENU      IFLT MAXMNU
414aACSR                   ADD  1         MENU
414aACSR                   ADD  5         MP
414aACSR                   EXSR ZZFIL1
414aACSR                   ENDIF
416 ACSR                   EXSR ZZWHSE
     C*
     C*
     C**         STATUS    WHEQ ROLLDN
     CSR         *IN82     WHEQ *ON
     CSR         MENU      IFGT 1
     CSR                   SUB  1         MENU
     CSR                   SUB  5         MP
     CSR                   EXSR ZZFIL1
     CSR                   ENDIF
     CSR                   EXSR ZZWHSE
     C*
     C*
     C**         STATUS    WHEQ COMAND
     CSR         *INKC     WHEQ *ON
     CSR                   MOVE 'F3'      $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR         *INKL     WHEQ *ON
     CSR                   MOVE 'F2'      $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR                   OTHER
     CSR                   EXSR ZZCHK1
     CSR         ERROR     IFEQ *OFF
     CSR                   EXSR ZZUPD1
     CSR                   ENDIF
     CSR                   ENDSL
     C*
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
MSG DC**                   EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
MSG AC*
MSG AC*    ZMCPMQ  Clear program message queue.
MSG AC*
MSG ACSR         ZMCPMQ    BEGSR
MSG ACSR                   MOVE '*CLEAR ' #MSGID  7
MSG ACSR                   MOVE '*NULL  ' #MSGTP  7
MSG ACSR                   EXSR ZMPMSG
MSG ACSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR                   WRITEMSGCTL
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   EQ82001  No equipment checked out.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'EQ82001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0105  Generic message.
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0108  Invalid option entered.
     C*
     CSR         ZM0108    BEGSR
     CSR                   MOVE 'PIR0108' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify option.
     C*
     C*
     CSR                   MOVELW1OPT     W1OPTA  3
     CSR         W1OPTA    LOKUPTABOPT    TABPGM         50
     CSR         *IN50     IFEQ *OFF
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE W1OPT     EROPT
     CSR                   EXSR ZM0108
     CSR                   ENDIF
     C*
     C*  Verify user has active equipment to allow access to RFSCRN
     C*
     CSR         W1OPT     IFEQ 3
     CSR                   MOVE '1'       KYACTV
     CSR         CHKKEY    SETLLEQUIPT3
     CSR         CHKKEY    READEEQUIPT3                  79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
    DC**                   MOVEAMNU1      DSMENU
    MCSR                   MOVEAMNU,MP    DSMENU
     CSR                   Z-ADD0         W1OPT
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR         CHKKEY    KLIST
     CSR                   KFLD           #WHSE
     CSR                   KFLD           #USER
     CSR                   KFLD           KYACTV  1
     C*
MSG ACSR                   MOVE #PROG     #PGMQ
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     CSR                   Z-ADD6         MAXMNU  30
     CSR                   Z-ADD1         MENU    30
     CSR                   Z-ADD1         MP      30
     C*
     C                     EXSR ZSCLR
     CSR                   EXSR SCR01I
     CSR                   EXSR ZZWHSE
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD1   Update for screen 1.
     C*
     CSR         ZZUPD1    BEGSR
     C*
     C*
     CSR                   SELEC
     CSR         W1OPT     WHEQ 90
     CSR                   MOVE W1OPT     $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR         W1OPT     WHEQ 3
     CSR                   CALL 'RFSCRNCL'
     CSR                   PARM           $MENU  10
     CSR                   PARM           $LIB   10
     CSR                   PARM           $RETRN  2
     CSR                   OTHER
     CSR                   CALL TABPGM                 49
     CSR         *IN49     IFEQ *OFF
     CSR                   Z-ADD0         W1OPT
     CSR                   ENDIF
     CSR                   ENDSL
     C*
     CSR         ENDUP1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZWHSE   Display user's default warehouse in message line.
     C*
     CSR         ZZWHSE    BEGSR
     C*
     CSR                   CALL 'GETWHSE2'
     CSR                   PARM           #USER
     CSR                   PARM 0         #WHSE   30
     CSR                   PARM ' '       #DESC  30
     CSR                   PARM ' '       #FRMT  40
     CSR                   MOVEL#FRMT     ERRMSG    P
     CSR                   EXSR ZM0105
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  COMPILE TIME TABLES
**  TABOPT/TABPGM
01 TE112CL
02 EQ410CL
03 RFSCRN
04
05
06
07
08
09
10
11
12
13
14
15
16
17
18
19
88
90 SIGNOFF
**  MNU
1. Task Entry
 2. Eq.Check Out/In
 3. RFSCRN
 4.

5.
 6.
 7.
 8.

9.
10.
11.
12.

13.
 14.
 15.
 16.

17.
 18.
 19.
 88.




 90. Sign-off

