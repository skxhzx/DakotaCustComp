     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV210     Inventory sheet maintenance - Options
     F*  14 February, 1996
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*  Roxanne Harrison
     F*
     F*  Revisions
     F*
     F*    11/11/94  DAS
     F*      - If *OK is sent back only the message is shown at the
     F*        bottom. The field is not highlighted and there is no
     F*        beep. We don't want the user thinking it is an error.
     F*
409 AF*    07/21/97  RH    4.09
     F*      - Re-compiled, INVPAGE file changed.
510aAF*    07/26/06  RH    5.10a
     F*      - Enh: Add dept and slot, enter in place of page.
510bAF*    12/06/06  RH    5.10b
     F*      - Enh: Remove BFCADR, no long used.
510cAF*    06/08/07  RH    5.10c
     F*      - Enh: Add F7 call to Inv Sheet/item lookup
510dAF*    08/28/07  RH    5.10d
     F*      - Enh: Add slot to $LPARMS, pass to IV212 for position to
510eAF*    08/28/07  RH    5.10e
     F*      - Enh: Blank dept and slot on screen 1
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  90        View mode (protect all fields)
     F*  91        Change mode (protect key fields)
     F*  97        Alawys on (used for SFLEND keyword)
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIV210FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     F*IRUSER IF  E           K        DISK
     FINVPAGE IF  E           K        DISK
510a FINVSLOT4IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
510aME                    DESC    1   2 60
510aDE*                   DESC    1   1 60
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHSE
     I                                       22  26 $PWHDP
     I                                       27  38 $PDISP
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  260$LPAGE
     I                                       27  31 $LWHDP
     I                                       32  34 $LAISL
510d I                                       35  46 $LDISP
     I*                                      54  58 $LDESG
     I*                                      59  60 $LSTAT
     I*                                      61  62 $LSDEF
     I*                                      63  64 $LACTV
     I*
510a I                                       22  26 $LWHDE
     I*
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     MOVE *BLANKS   $PRTN
     C                     MOVE *BLANKS   $PERM
     C*
     C*                    CALL 'BFCADR'
     C*                    PARM           $RTN    8
     C*          $RTN      IFEQ '*EXPIRED'
     C*                    MOVE 'EOJ'     NXTSCR
     C*                    ELSE
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*                    END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   EXSR ZZUPD
     CSR         ERROR     CABEQ*ON       ENDSC1
     C*
     C*  The after update redisplay screen. The only way out is F3.
     C*
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IV33002  Slot not in inventory scope.
     C*
     CSR         ZM3002    BEGSR
     CSR                   MOVE 'IV33002' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN02
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
510a C*
510a C*  Verify warehouse department.
510a C*
510a CSR         W1WHDP    IFNE *BLANKS
510a CSR                   MOVE '*VERIFY '$LCMD
510a CSR                   Z-ADDW1WHSE    $LWHSE
510a CSR                   MOVE W1WHDP    $LWHDE
510a CSR                   CALL 'WD900'
510a CSR                   PARM           $LPARM
510a CSR         $LRTN     IFEQ '*ERROR  '
510a CSR                   MOVE *ON       ERROR
510a CSR                   MOVE *ON       *IN25
510a CSR                   MOVE *ON       *IN05
510a CSR                   MOVEL$LERM     ERRMSG
510a CSR                   EXSR ZM0105
510a CSR                   ELSE
510a CSR         $LRTN     IFEQ '*PGMQ   '
510a CSR                   MOVE *ON       ERROR
510a CSR                   MOVE *ON       *IN25
510a CSR                   MOVE *ON       *IN05
510a CSR                   MOVE $LMSG     #MSGK
510a CSR                   EXSR ZMQMSG
510a CSR                   ENDIF
510a CSR                   ENDIF
510a CSR                   ENDIF
510a C*
510a C*  Error if only one of dept or slot is blank
510a CSR         W1DISP    IFNE *BLANKS
510a CSR         W1WHDP    ANDEQ*BLANKS
510a CSR         W1DISP    OREQ *BLANKS
510a CSR         W1WHDP    ANDNE*BLANKS
510a CSR                   MOVE *ON       ERROR
510a CSR                   MOVE *ON       *IN23
510a CSR                   MOVE *ON       *IN24
510a CSR                   MOVE *ON       *IN03
510a CSR                   MOVE *BLANKS   ERRMSG
510a CSR                   MOVELDESC,2    ERRMSG
510a CSR                   EXSR ZM1001
510a CSR                   ENDIF
510a C*
510a C*  Verify Slot and get page.
510a C*
510a CSR         W1DISP    IFNE *BLANKS
510a CSR         SLKEY     CHAININVSLOT4             79
510a CSR         *IN79     IFEQ *ON
510a CSR                   MOVE *ON       ERROR
510a CSR                   MOVE *ON       *IN23
510a CSR                   MOVE *ON       *IN03
510a CSR                   MOVE *BLANKS   ERRMSG
510a CSR         '"'       CAT  W1DISP    ERRMSG
510a CSR         ERRMSG    CAT  '"':0     ERRMSG
510a CSR                   EXSR ZM3002
510a CSR                   GOTO ENDCK1
510aACSR                   ELSE
510a CSR                   Z-ADDIVSPAG    W1PAGE
510aACSR                   ENDIF
510aACSR                   ENDIF
     C*
     C*  Verify sheet # for warehouse.
     C*
     CSR                   Z-ADDW1WHSE    IKYWHS
     CSR                   Z-ADDW1PAGE    IKYPAG
     CSR         IPKEY     CHAININVPAGE              79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,1    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ELSE
     CSR                   MOVE IVPWHD    $LWHDP
     CSR                   MOVE IVPASL    $LAISL
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ '1'
     CSR                   MOVE '1'       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Warehouse.
     C*
     CSR         ROW#      WHEQ 7
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
510a C*     Department
     C*
510a CSR         ROW#      WHEQ 13
510a CSR                   MOVE *ON       *IN04
510a CSR                   MOVE '*LOOKUP '$LCMD
510a CSR                   Z-ADDW1WHSE    $LWHSE
510a CSR                   MOVE W1WHDP    $LWHDE
510a CSR                   CALL 'WD900'
510a CSR                   PARM           $LPARM
510a CSR         $LRTN     IFEQ '*SELECT '
510a CSR                   MOVE $LWHDE    W1WHDP
510a CSR                   ENDIF
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   GOTO ENDCM1
     CSR                   ENDIF
510c C*
510c C*  TEST FOR CMD7 - Inv Sheet/item lookup
510c C*
510c CSR         *INKG     IFEQ '1'
510c CSR                   CALL 'IV220CL'
510c CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     C*      Get warehouse for user.
     C*
     CSR                   CALL 'GETWHSE'
     CSR                   PARM           #USER
     CSR         W1WHSE    PARM 0         #WHSE   30
     C*
510e CSR                   MOVE *BLANKS   W1WHDP
510e CSR                   MOVE *BLANKS   W1DISP
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE #PROG     $PPRG
     CSR                   MOVE #PROG     $LPRG
     C*
     C*
     C*  Define key for INVPAGE file.
     C*
     C*R         *LIKE     DEFN $PSWHS    IKYWHS  30
     C*R         *LIKE     DEFN $PSPAG    IKYPAG  50
     CSR         IPKEY     KLIST
     CSR                   KFLD           IKYWHS  30
     CSR                   KFLD           IKYPAG  50
510a C*
510a C*  Define key for INVSLOT4 file.
510a C*
510a CSR         SLKEY     KLIST
510a CSR                   KFLD           W1WHSE
510a CSR                   KFLD           W1WHDP
510a CSR                   KFLD           W1DISP
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*R         #USER     CHAINPIRUSER              79
     C*R         *IN79     IFEQ *OFF
     C*R                   Z-ADDUSWHSE    W1WHSE
     C*R                   ELSE
     C*R                   Z-ADD1         W1WHSE
     C*R                   ENDIF
     C*
     C*   Select first screen to display.
     C*
     CSR                   MOVE *ON       *IN01
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Move screen fields to parameter fields.
     C*
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   Z-ADDW1PAGE    $LPAGE
510d CSR                   MOVE W1DISP    $LDISP
     C*
     C*  Call program to do the entry/change.
     C*
     CSR                   MOVE '*CHANGE '$LCMD
     CSR                   CALL 'IV212'
     C*R                   PARM           $PARMS
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*CANCEL '
     CSR                   MOVE *ON       CANCEL  1
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE *ON       ERROR
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*OK     '
510e C*                    MOVE $LMSG     #MSGK
510e C*                    EXSR ZMQMSG
     CSR                   EXSR SCR01I
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         ENDUPD    ENDSR
     C*
     O*----------------------------------------------------------------     ****
     O*
     O*  COMPILE TIME TABLES
     O*
**
Sheet number
Dept / Slot
