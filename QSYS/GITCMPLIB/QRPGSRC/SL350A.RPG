     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SL350A    Unverified Zero Slot with related slot info -Process
     F*  Roxanne Harrison
     F*  01 December, 2004
     F*
     F*  Revisions
500  F*    12/01/04  RH  5.00 - Created
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSL351   IP  E           K        DISK
     FVSLOT1  IF  E           K        DISK
     FVSLOT2  IF  E           K        DISK
     F            VSREC                             KRENAMEVSREC2
     FSL351A  O   E           K        DISK                      A
     I*****************************************************************
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I*----------------------------------------------------------------
     I*
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  36 $LITEM
     I                                       37  51 $LITMF
     I                                       52  83 $LDESC
     I*
     I*----------------------------------------------------------------
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      NONE
     C*
     C*    Returned Parameters
     C*      $RTNCD
     C*
     C           *ENTRY    PLIST
     C                     PARM           $RTNCD  8
     C*----------------------------------------------------------------
     C*
     C*  Main line
     C*
     C                     MOVE *OFF      WTFLAG  1
     C*  If virtual slot, get base and write record for base.
     C           SLRSRV    IFEQ 'V'
     C           KEYVS1    CHAINVSLOT1               79
     C           *IN79     IFEQ *OFF
     C                     MOVE VSPDIS    KYDISP
     C                     ELSE
     C                     MOVE *BLANKS   KYDISP
     C                     ENDIF
     C                     Z-ADDSLWHSE    WKWHSE
     C                     MOVE SLWHDP    WKWHDP
     C                     MOVE SLSTYP    WKSTYP
     C                     MOVE SLSTAT    WKSTAT
     C                     MOVE SLRSRV    WKRSRV
     C                     MOVE SLDISP    WKDISP
     C                     MOVE KYDISP    WKRDSP
     C                     WRITEWKREC                  78
     C                     MOVE *ON       WTFLAG
     C           *IN78     IFEQ *ON
     C                     GOTO ENDIT
     C                     ENDIF
     C                     ELSE
     C                     MOVE SLDISP    KYDISP
     C                     ENDIF
     C*
     C* Get all related slots for base slot and write records.
     C*
     C           KEYVS2    SETLLVSLOT2
     C           FOREVR    DOWEQFOREVR
     C           KEYVS2    READEVSLOT2                   79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C           SLDISP    IFNE VSDISP
     C                     Z-ADDSLWHSE    WKWHSE
     C                     MOVE SLWHDP    WKWHDP
     C                     MOVE SLSTYP    WKSTYP
     C                     MOVE SLSTAT    WKSTAT
     C                     MOVE SLRSRV    WKRSRV
     C                     MOVE SLDISP    WKDISP
     C                     MOVE VSDISP    WKRDSP
     C                     WRITEWKREC                  78
     C                     MOVE *ON       WTFLAG
     C           *IN78     IFEQ *ON
     C                     GOTO ENDIT
     C                     ENDIF
     C                     ENDIF
     C                     ITER
     C*
     C                     ENDDO
     C*
     C* Write record for zero base slot with no related slots.
     C           WTFLAG    IFNE *ON
     C                     Z-ADDSLWHSE    WKWHSE
     C                     MOVE SLWHDP    WKWHDP
     C                     MOVE SLSTYP    WKSTYP
     C                     MOVE SLSTAT    WKSTAT
     C                     MOVE SLRSRV    WKRSRV
     C                     MOVE SLDISP    WKDISP
     C                     MOVE *BLANKS   WKRDSP
     C                     WRITEWKREC                  78
     C                     MOVE *ON       WTFLAG
     C           *IN78     IFEQ *ON
     C                     GOTO ENDIT
     C                     ENDIF
     C                     ENDIF
     C           ENDIT     TAG
     CLR                   MOVE '*OK     '$RTNCD
     CLR                   RETRN
     C*
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   MOVE '1'       FIRST   1
     CSR                   MOVE *ON       FOREVR  1
     C           *LIKE     DEFN SLDISP    KYDISP
     C*
     C*
     C*  Define keys.
     C*
     C*    VSLOT1 file
     C*
     CSR         KEYVS1    KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLWHDP
     CSR                   KFLD           SLDISP
     C*
     C*    VSLOT2 file
     CSR         KEYVS2    KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLWHDP
     CSR                   KFLD           KYDISP
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*****************************************************************
     C*
