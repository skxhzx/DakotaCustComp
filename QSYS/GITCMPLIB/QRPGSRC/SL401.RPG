     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SL401     Used and Available Cube - Print
     F*  13 Aug, 2001
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*
     F*  Program Modification History
     F*  Revisions:
     F*
     F*    08/13/01  HNK 4.16
     A*      - Created
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSL401   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FSL401PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RPD     1   4 30
     E                    RECK        4  7 0
     E                    LVLK        4  7 0
     E                    PCKU        4  7 0
     E                    OVRU        4  7 0
     E                    PCKA        4  7 0
     E                    OVRA        4  7 0
     E*
     E                    DST        99  8
     E                    WHD        99  5
     E                    SEC        99  1
     E                    TYP        99  2
     E                    UPCK       99  7 0
     E                    UOVR       99  7 0
     E                    APCK       99  7 0
     E                    AOVR       99  7 0
     I*----------------------------------------------------------------
     IOQREC
     I                                              SLSDEFL1
     I                                              SLWHDPL2
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PSTYP  5
     C*
     C                     MOVE $PSTYP    TYPE
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           FIRST     OREQ *ON
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     END
     C*
     C*  Level 1 break - Department
     C*
     C           *INL2     IFEQ *ON
     C                     Z-ADD2         L
     C                     EXSR ZRZERO
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           FIRST     OREQ *ON
     C                     MOVE *OFF      FIRST
     C**         *INL1     OREQ '1'
     C**         *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C**                   END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C*  Accumulate totals
     C*
     C                     EXSR ZRFILL
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN92
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   EXSR ZRFIND
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* Department totals
     C*
     CL2                   Z-ADD2         L
     CL2                   EXSR ZRROLL
     C*
     CL2         RECK,L    IFGT 0
     CL2                   WRITESKIP1
     CL2                   EXSR ZRTOTL
     CL2                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     CLR                   EXSR ZRROLL
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   END
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         ITKEY     KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLITEM
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'SL401PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   MOVE *ON       FIRST   1
     CSR                   TIME           TIME
     CSR                   Z-ADD3         L       10
     CSR                   Z-ADD0         N       30
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFILL    BEGSR
     C*
     C*  Call program to calculate available slot quantities.
     C*
     C                     CALL 'SLOTQTY'
     C                     PARM           SLSTK1
     C                     PARM           SLSTK2
     C                     PARM           SLSTK3
     C*
     C                     PARM           SLALC1
     C                     PARM           SLALC2
     C                     PARM           SLALC3
     C*
     C                     PARM           SLTFR1
     C                     PARM           SLTFR2
     C                     PARM           SLTFR3
     C*
     C                     PARM           SLPCK1
     C                     PARM           SLPCK2
     C                     PARM           SLPCK3
     C*
     C                     PARM           SLRCV1
     C                     PARM           SLRCV2
     C                     PARM           SLRCV3
     C*
     C                     PARM           STOCK1  50
     C                     PARM           STOCK2  30
     C                     PARM           STOCK3  30
     C*
     C                     PARM           AVAIL1  50
     C                     PARM           AVAIL2  30
     C                     PARM           AVAIL3  30
     C*
     C*   Fill arrays for summary report.
     C*
     C                     MOVE *BLANKS   W1DST   8
     C           SLWHDP    CAT  SLSTYP:0  W1DST
     C           W1DST     CAT  SLSDEF:0  W1DST
     C                     Z-ADD1         N
     C           W1DST     LOKUPDST,N                    81
     C*  Found
     C                     SELEC
     C           *IN81     WHEQ *ON
     C           SLPICK    IFEQ 'Y'
     C           AVAIL1    ANDGT0
     C                     ADD  1         UPCK,N
     C                     ENDIF
     C           SLPICK    IFEQ 'N'
     C           AVAIL1    ANDGT0
     C                     ADD  1         UOVR,N
     C                     ENDIF
     C           SLPICK    IFEQ 'Y'
     C           AVAIL1    ANDEQ0
     C                     ADD  1         APCK,N
     C                     ENDIF
     C           SLPICK    IFEQ 'N'
     C           AVAIL1    ANDEQ0
     C                     ADD  1         AOVR,N
     C                     ENDIF
     C*
     C                     OTHER
     C*  Not found
     C           *BLANKS   LOKUPDST,N                    82
     C           *IN82     IFEQ *ON
     C                     MOVE W1DST     DST,N
     C                     MOVE SLWHDP    WHD,N
     C                     MOVE SLSTYP    SEC,N
     C                     MOVE SLSDEF    TYP,N
     C           SLPICK    IFEQ 'Y'
     C           AVAIL1    ANDGT0
     C                     Z-ADD1         UPCK,N
     C                     ENDIF
     C           SLPICK    IFEQ 'N'
     C           AVAIL1    ANDGT0
     C                     Z-ADD1         UOVR,N
     C                     ENDIF
     C           SLPICK    IFEQ 'Y'
     C           AVAIL1    ANDEQ0
     C                     Z-ADD1         APCK,N
     C                     ENDIF
     C           SLPICK    IFEQ 'N'
     C           AVAIL1    ANDEQ0
     C                     Z-ADD1         AOVR,N
     C                     ENDIF
     C*
     C                     ENDIF
     C                     ENDSL
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFIND    BEGSR
     C                     Z-ADD1         N
     C           W1DST     LOKUPDST,N                    81
     C*  Found
     C                     SELEC
     C           *IN81     WHEQ *ON
     C                     ADD  UPCK,N    PCKU,1
     C                     ADD  UOVR,N    OVRU,1
     C                     ADD  APCK,N    PCKA,1
     C                     ADD  AOVR,N    OVRA,1
     C                     ENDSL
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           SLWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  PCKU,L    PCKU,M
     C                     ADD  OVRU,L    OVRU,M
     C                     ADD  PCKA,L    PCKA,M
     C                     ADD  OVRA,L    OVRA,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDPCKU,L    T#UPCK
     C                     Z-ADDOVRU,L    T#UOVR
     C                     Z-ADDPCKA,L    T#APCK
     C                     Z-ADDOVRA,L    T#AOVR
     C*
     C                     MOVE RPD,L     $RPD
     C           L         IFEQ 1
     C                     WRITETOTAL1
     C                     ELSE
     C                     WRITETOTAL2
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         PCKU,L
     C                     Z-ADD0         OVRU,L
     C                     Z-ADD0         PCKA,L
     C                     Z-ADD0         OVRA,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
**
Slot Type Total:
Department Total:
Grand Total:
XXXXXXXXXXXXXXXX
