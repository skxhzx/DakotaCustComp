     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT371     Warehouse Short/Out Report
     F*  Hemant Kapadia
     F*  28 October, 1994
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT371   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FIT371PR O   E             90     PRINTER
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           FIRST     OREQ '1'
     C                     EXSR ZRHEAD
     C                     END
     C*
     C* Detail Calcs
     C*
     C           *LIKE     DEFN IQSTK1    AVAIL1
     C           *LIKE     DEFN IQSTK2    AVAIL2
     C           *LIKE     DEFN IQSTK3    AVAIL3
     C*
     C*   Calculate available quantities.
     C*
     C                     MOVE '*AVAIL  '$PCMD
     C                     MOVE '*BATCH  '$PTYPE
     C                     Z-ADDIQWHSE    $PWHSE
     C                     MOVE IQITEM    $PITEM
     C                     Z-ADDIQSTK1    $PQTY1
     C                     Z-ADDIQSTK2    $PQTY2
     C                     Z-ADDIQSTK3    $PQTY3
     C                     CALL 'ADJQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PTYPE  8
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PRTQ1  70
     C                     PARM           $PRTQ2  70
     C                     PARM           $PRTQ3  70
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C                     Z-ADD$PRTQ1    AVAIL1
     C                     Z-ADD$PRTQ2    AVAIL2
     C                     Z-ADD$PRTQ3    AVAIL3
     C*
     C*    Do not print, if the qty available is > reorder qty.
     C*
     C           AVAIL1    CABGTITREOR    SKPREC
     C*
     C*    Create a quantity string to print.
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDAVAIL1    $PQTY1
     C                     Z-ADDAVAIL2    $PQTY2
     C                     Z-ADDAVAIL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    QTAVL
     C*
     C                     WRITEDETAIL1
     C*
     C           SKPREC    TAG
     C                     MOVE '0'       FIRST
     C*
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   TIME           TIME
     CSR                   MOVE '1'       FIRST   1
     CSR                   MOVE #PROG     $PPRG
     CSR                   ENDSR
     C*****************************************************************
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR                   MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     CSR                   MOVE *BLANKS   WHSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR         IQWHSE    CHAINWAREHS               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELWHDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     WHSE
     C*
     CSR                   WRITEHEAD1
     CSR                   WRITECOLUMNS
     C*
     CSR                   ENDSR
