     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SE112     RF shorts Detail
     F*  19 April, 2004
     F*  Hemant Kapadia
     F*
     F*  Notes
     F*       8 X 20 Screens for hand held scanners
     F*
     F*
     F*  Revisions
     F*    04/19/04  HNK 4.17
     F*      - Created
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  01 - 20   FIELD POSITION TO
     F*  21 - 40   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT KEY)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSE112FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FLABEL   UF  E           K        DISK
     FORDD    UF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FOPTIONZ IF  E           K        DISK
     FWORKLBL IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A2          2  1
     E                    A2N         2  1
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   3 60
     E                    HND       702  2
     I*----------------------------------------------------------------
     I*  *PICKLBL  -  Picking label options
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPCLVL  -  Qty level for combined labels.
     I*    OPCWRB  -  Print combined label warning before.
     I*    OPCWRA  -  Print combined label warning after.
     I*    OPUWRB  -  Print unit of measure warning before.
     I*    OPUWRA  -  Print unit of measure warning after.
     I*    OPTEXT  -  Text for last line of label.
     I*    OPERR   -  Error label ratio.
     I*    OPCWGT  -  Catch weight label ratio.
     I*    OPLPGM  -  Picking label program.
     I*    OPPDEV  -  Printer device.
     I*    OPPFRM  -  Printer form.
     I*
     I* Data structure
     I*
     IOPZDTA      DS
     I                                        1   30OPCLVL
     I                                        4   4 OPCWRA
     I                                        5   5 OPCWRB
     I                                        6   6 OPUWRA
     I                                        7   7 OPUWRB
     I                                        8  50 OPTEXT
     I                                       51  520OPERR
     I                                       53  540OPCWGT
     I                                       55  64 OPLPGM
     I                                       65  74 OPPDEV
     I                                       75  84 OPPFRM
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Redefinition data structures
     I*
     I$CERM       DS
     I                                        1  60 XCERM
     I                                        1   4 $CMSG
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I              'FRMTIT'              C         @FMTIT
     I              'ADDSTDTRN'           C         @ADDTR
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I                                       81  88 $LTYPE
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I                                       27  41 $LITEM
     I                                       42  53 $LDISP
     I*
     I                                       22  36 $LITM
     I*
     I*
     I*----------------------------------------------------------------
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*
     I            DS
     I                                        1   60DSHMS
     I                                        1   40DSHM
     I                                        1   20DSHH
     I                                        3   40DSMM
     I                                        5   60DSSS
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I*                                   P   1   20ERWHSE
     I*
     I                                        1  30 ERFLD
     I                                       31  40 ERFROM
     I                                       41  50 ERTO
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PWHDP  5
     C                     PARM           $PEMP#  5
     C                     PARM           $PSHFT  6
     C                     PARM           $PCREW  6
     C                     PARM           $PTRN#  7
     C                     PARM           $PRTID  5
     C                     PARM           $PRTE   5
     C                     PARM           $PPLID  7
     C                     PARM           $PPAL#  7
     C                     PARM           $PRTN   8
     C*
     C                     MOVE *OFF      *IN99
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     ENDIF
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C           NXTSCR    CASEQ'02 '     SC2
     C                     ENDCS
     C                     ENDDO
     C*
     C*   End current task.
     C*
     C                     EXSR ZZEND
     C*
     C*   Create idle task.
     C*
     C                     EXSR ZZIDLE
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   ENDIF
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   ENDIF
     CSR         NXTSCR    IFEQ '02 '
     CSR                   EXFMTSCREEN2                50
     CSR                   ENDIF
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     303132
     CSR                   SETOF                     333435
     CSR                   SETOF                     363738
     CSR                   SETOF                     3940
     CSR                   SETOF                     9298
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   MOVE *OFF      REDSPL  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   EXSR ZZFIL1
     CSR                   MOVE *OFF      REFRSH  1
     CSR                   MOVE *OFF      OVRRID  1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  Screen 02 initialization
     C*
     CSR         SCR02I    BEGSR
     CSR                   MOVE *OFF      EXIST   1
     CSR                   MOVE *OFF      NEWVS   1
     CSR                   MOVE '02 '     NXTSCR  3
     CSR                   EXSR ZZFIL2
     CSR                   MOVE *OFF      REFRSH
     CSR         END02I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   MOVE *ON       REFRSH
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys BEFORE verification and update.
     C*
     CSR                   MOVE '*BEFORE 'CMDCHK  8
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR         REDSPL    CABEQ*ON       ENDSC1
     CSR                   EXSR ZZUPD1
     CSR         ERROR     CABEQ*ON       ENDSC1
     C*
     C*  Test for other command keys AFTER verification and update.
     C*
     CSR                   MOVE '*AFTER  'CMDCHK
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  SC2  -  Screen 2
     C*
     CSR         SC2       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE '01 '     NXTSCR
     CSR                   GOTO ENDSC2
     CSR                   ENDIF
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   MOVE *ON       REFRSH
     CSR                   EXSR SCR02I
     CSR                   GOTO ENDSC2
     CSR                   ENDIF
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE *OFF      REFRSH
     CSR                   MOVE '01 '     NXTSCR
     CSR                   GOTO ENDSC2
     CSR                   ENDIF
     C*
     C*  Test for other command keys BEFORE verification and update.
     C*
     CSR                   MOVE '*BEFORE 'CMDCHK  8
     CSR                   EXSR ZZCMD2
     CSR         CMDTKN    CABEQ*ON       ENDSC2
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK2
     CSR         ERROR     CABEQ*ON       ENDSC2
     CSR                   EXSR ZZUPD2
     CSR         ERROR     CABEQ*ON       ENDSC2
     C*
     C*  Test for other command keys AFTER verification and update.
     C*
     CSR                   MOVE '*AFTER  'CMDCHK
     CSR                   EXSR ZZCMD2
     CSR         CMDTKN    CABEQ*ON       ENDSC2
     C*
     CSR         ENDSC2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0105  Invalid slot entered.
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0112  Invalid range entered.
     C*
     CSR         ZM0112    BEGSR
     CSR                   MOVE 'PIR0112' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0203  Value cannot be blank
     C*
     CSR         ZM0203    BEGSR
     CSR                   MOVE 'PIR0203' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT28001  Invalid slot entered.
     C*
     CSR         ZM2801    BEGSR
     CSR                   MOVE 'IT28001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0110  Number must be > 0
     C*
     CSR         ZM0110    BEGSR
     CSR                   MOVE 'PIR0110' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid value entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR1005  Value must be 'Y' or 'N'
     C*
     CSR         ZM1005    BEGSR
     CSR                   MOVE 'PIR1005' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT11213  Invalid route for item.
     C*
     CSR         ZM1213    BEGSR
     CSR                   MOVE 'IT11213' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT11214  Invalid pallet for item.
     C*
     CSR         ZM1214    BEGSR
     CSR                   MOVE 'IT11214' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT11215  Item scan not allowed.
     C*
     CSR         ZM1215    BEGSR
     CSR                   MOVE 'IT11215' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT11217  Item already scanned.
     C*
     CSR         ZM1217    BEGSR
     CSR                   MOVE 'IT11217' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   IT11218  Invalid label number.
     C*
     CSR         ZM1218    BEGSR
     CSR                   MOVE 'IT11218' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   SL16001 No more zero slots.
     C*
     CSR         ZM1601    BEGSR
     CSR                   MOVE 'SL16001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0202  Code already exists.
     C*
     CSR         ZM02      BEGSR
     CSR                   MOVE 'PIR0202' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR                   MOVE W1LBLA    W1LBL#
     CSR                   Z-ADDW1LBL#    SVLBL#  70
     C*  Use the scanned label # to save LBL# and QTY
     CSR         LBKEY     CHAINLABEL               N79
     CSR         *IN79     IFEQ *OFF
     CSR                   Z-ADDLBQALC    SVQPCK  50
     CSR                   ELSE
     CSR                   Z-ADD0         SVQPCK
     CSR                   ENDIF
     C*
     C*  If there is a merge label, use merge label.
     C*
     CSR                   CALL 'GETTPLT'
     CSR                   PARM W1LBL#    $GLBL#  70
     CSR                   PARM           $GTPLT  70
     CSR                   PARM           $GMLBL  70
     CSR                   PARM           $GMPID  70
     CSR                   PARM           $GSPLT  1
     CSR                   PARM           $GLOW   1
     CSR                   PARM           $GRTN   8
     CSR         $GRTN     IFEQ '*OK     '
     CSR                   Z-ADD$GMLBL    W1LBL#
     C**                   Z-ADD$GMPID    W1PLID
     CSR                   ENDIF
     C*
     C*  Invalid item
     C*
     CSR         LBKEY     CHAINLABEL               N79
     CSR         *IN79     IFEQ *OFF
     CSR         LBRTE     ANDNE$PRTID
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   EXSR ZM1213
     CSR                   ENDIF
     C*
     CSR         *IN79     IFEQ *OFF
     CSR         LBPAL#    ANDNEW1PLID
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   EXSR ZM1214
     CSR                   ENDIF
     C*  Must be selection record
     CSR         *IN79     IFEQ *OFF
     CSR         LBGRP1    ANDNE'S'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   EXSR ZM1218
     CSR                   ENDIF
     C*
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVEADESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK2   Screen 2 error checking.
     C*
     CSR         ZZCHK2    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     CSR         W2QTY     IFGT LBQALC
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVEADESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     CSR         ENDCK2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     C*
     C*  Command keys to check BEFORE verification and update.
     C*
     CSR         CMDCHK    IFEQ '*BEFORE '
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     CSR                   SELEC
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   GOTO ENDCM1
     CSR                   ENDIF
     C*
     CSR         *INKU     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     C***                  EXSR SCR03I
     CSR                   GOTO ENDCM1
     CSR                   ENDIF
     C*
     CSR         *INKQ     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     C**                   CALL 'CK120CL'
     C**                   PARM           $PWHSE
     C**                   PARM           $PRTID
     C**                   PARM           $PPLID
     C**                   PARM           $PRTN
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Command keys to check AFTER verification and update.
     C*
     CSR         CMDCHK    IFEQ '*AFTER  '
     C*
     CSR                   ENDIF
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD2   User defined command keys for screen 2.
     C*
     CSR         ZZCMD2    BEGSR
     C*
     C*  Command keys to check BEFORE verification and update.
     C*
     CSR         CMDCHK    IFEQ '*BEFORE '
     C*
     C*      F10 - Accept Warning.
     C*
     CSR         *INKJ     IFEQ *ON
     CSR                   MOVE *ON       OVRRID
     CSR                   ELSE
     CSR                   MOVE *OFF      OVRRID
     CSR                   ENDIF
     C*
     CSR                   ENDIF
     C*
     C*  Command keys to check AFTER verification and update.
     C*
     CSR         CMDCHK    IFEQ '*AFTER  '
     CSR                   ENDIF
     CSR         ENDCM2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     CSR                   MOVEL$PRTE     W1RTE
     CSR                   MOVEL$PRTID    W1RTID
     CSR                   MOVE $PPAL#    W1PLT1
     CSR                   Z-ADD0         W1PLT2
     C*
     CSR                   MOVE *BLANKS   W1LBLA
     CSR                   Z-ADD0         W1LBL#  70
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL2   Fill screen 2 fields with info from file.
     C*
     CSR         ZZFIL2    BEGSR
     C*
     C*  Total pieces on pallet
     C*
     CSR                   MOVELW1RTE     W2RTE
     CSR                   MOVELW1RTID    W2RTID
     CSR                   MOVE W1PLT1    W2PLT1
     CSR                   MOVE W1PLT2    W2PLT2
     CSR                   Z-ADD0         W2QTY
     CSR         LBKEY     CHAINLABEL               N79
     CSR         *IN79     IFEQ *OFF
     CSR                   Z-ADDLBQALC    W2OQTY
     CSR                   Z-ADDLBQPCK    W2PQTY
     CSR                   ELSE
     CSR                   Z-ADD0         W2OQTY
     CSR                   Z-ADD0         W2PQTY
     CSR                   ENDIF
     C*
     CSR         KEYIT     CHAINPIRITEM              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELITDESC    $IDESC
     CSR                   MOVELITPDSC    $IPDSC
     CSR                   MOVE *BLANKS   $ISTR
     CSR                   CALL @FMTIT
     CSR                   PARM ITITEM    $IITEM 15
     CSR                   PARM           $IDESC 30
     CSR                   PARM           $IPDSC 15
     CSR                   PARM           $ISTR  70
     CSR                   MOVE *BLANKS   W2ITM
     CSR                   MOVEL$ISTR     W2ITM
     CSR                   ELSE
     CSR                   MOVE *BLANKS   W2ITM
     CSR                   ENDIF
     C*
     CSR         ENDFL2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     CSR                   MOVE #PROG     $LPRG
     CSR                   MOVE #PROG     $CVPRG
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for LABEL
     C*
     CSR         LBKEY     KLIST
     CSR                   KFLD           W1LBL#
     C*
     C*  Define key for PIRITEM.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           LBWHSE
     CSR                   KFLD           LBITEM
     C*
     C*  Define key for PLTSUM4.
     C*
     CSR         PLKEY     KLIST
     CSR                   KFLD           WKTRN#
     C*
     C*  Define partial key for options file.
     C*
     CSR         *LIKE     DEFN OPZCOD    KYCODE
     CSR         KEYOPZ    KLIST
     CSR                   KFLD           KYCODE
     CSR                   KFLD           LBWHSE
     CSR                   KFLD           LWTRUK
     CSR                   KFLD           LWSZON
     C*
     C*  Define key for ORDD
     C*
     CSR         KEYOD     KLIST
     CSR                   KFLD           LBWHSE
     CSR                   KFLD           LBORD
     CSR                   KFLD           LBSEQ
     CSR                   KFLD           LBITEM
     C*
     CSR         KEYWL     KLIST
     CSR                   KFLD           LBLBL#
     C*
     CSR                   MOVE $PWHSE    W1WHSE  30
     CSR                   MOVE $PEMP#    W1EMP#  50
     CSR                   MOVE $PPLID    W1PLID  70
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     CSR                   MOVE $PTRN#    WKTRN#  70
     C*
     C*  End current task.
     C*
     CSR                   EXSR ZZEND
     C*
     C*  Create Loader transaction.  PIRTRAN File.
     C*
     CSR                   CALL 'PIRTRN#'
     CSR                   PARM           PTTRN#  70
     CSR                   Z-ADDPTTRN#    W1TRN#  70
     C*
     CSR                   CALL @ADDTR
     CSR                   PARM '*CRTSTRT'@PCMD   8
     CSR                   PARM           #JOB
     CSR                   PARM PTTRN#    @PTRN#  70
     CSR                   PARM 'LOADTRK' @PTASK  6
     CSR                   PARM W1WHSE    @PWHSE  30
     CSR                   PARM $PWHDP    @PWHDP  5
     CSR                   PARM ' '       @PSTYP  1
     CSR                   PARM 0         @PCUBE  93
     CSR                   PARM 0         @PSWGT  92
     CSR                   PARM 0         @PPCS   50
     CSR                   PARM 0         @PAISL  50
     CSR                   PARM 0         @PQTY1  50
     CSR                   PARM 0         @PQTY2  50
     CSR                   PARM 0         @PQTY3  50
     CSR                   PARM           @PRTE   5
     CSR                   PARM ' '       @PPO    9
     CSR                   PARM 'C'       @PGRP1  1
     CSR                   PARM ' '       @PGRP2  1
     CSR                   PARM ' '       @PRTN   8
     CSR                   PARM ' '       @PMSG   4
     C*
     CSR                   MOVE *ON       REFRSH
     CSR                   MOVE *OFF      MORE    1
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZEND  End current task.
     C*
     CSR         ZZEND     BEGSR
     C*
     C*  End current task.
     C*
     CSR                   MOVE '*ENDCUR '$KCMD   8
     CSR                   MOVEL'SE112   '$KPRG  10
     CSR                   Z-ADDW1EMP#    $KEMP#  50
     CSR                   MOVE $PSHFT    $KSHFT  6
     CSR                   MOVE $PCREW    $KCREW  6
     CSR                   Z-ADDW1WHSE    $KWHSE  30
     CSR                   MOVE $PWHDP    $KWHDP  5
     CSR                   MOVE 'F'       $KSTYP  1
     CSR                   Z-ADD0         $KQPCK  50
     CSR                   MOVE *BLANKS   $KRTN   8
     CSR                   MOVE *BLANKS   $KMSG   4
     C*
     CSR                   CALL 'EPMTSK'
     CSR                   PARM           $KCMD
     CSR                   PARM           $KPRG
     CSR                   PARM           $KEMP#
     CSR                   PARM           $KSHFT
     CSR                   PARM           $KCREW
     CSR                   PARM           $KWHSE
     CSR                   PARM           $KWHDP
     CSR                   PARM           $KSTYP
     CSR                   PARM           $KQPCK
     CSR                   PARM           $KRTN
     CSR                   PARM           $KMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZIDLE Create idle task.
     C*
     CSR         ZZIDLE    BEGSR
     C*  Create Idle task
     CSR                   CALL 'EPMTSK'
     CSR                   PARM '*STRIDL '$KCMD
     CSR                   PARM 'SE112   '$KPRG
     CSR                   PARM W1EMP#    $KEMP#
     CSR                   PARM $PSHFT    $KSHFT
     CSR                   PARM $PCREW    $KCREW
     CSR                   PARM W1WHSE    $KWHSE
     CSR                   PARM $PWHDP    $KWHDP
     CSR                   PARM 'F'       $KSTYP
     CSR                   PARM 0         $KQPCK
     CSR                   PARM *BLANKS   $KRTN
     CSR                   PARM *BLANKS   $KMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD1   Update for screen 1.
     C*
     CSR         ZZUPD1    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR                   MOVE *ON       REFRSH
     C*
     CSR         KEYWL     CHAINWORKLBL              79
     CSR         *IN79     IFEQ *ON
     C*  Treat as combo label
     CSR                   Z-ADD1         OPCLVL
     CSR                   ELSE
     C*
     C*   Get general picking options from options file.
     C*
     CSR                   MOVE '*PICKLBL'KYCODE
     CSR         KEYOPZ    CHAINOPTIONZ              79
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADD999       OPCLVL
     CSR                   ENDIF
     C*
     CSR                   ENDIF
     CSR                   EXSR SCR02I
     C*
     CSR         ENDUP1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD2   Update for screen 2.
     C*
     CSR         ZZUPD2    BEGSR
     CSR                   MOVE *OFF      ERROR
     CSR                   MOVE *ON       REFRSH
     C*
     CSR         LBKEY     CHAINLABEL                79
     CSR         *IN79     IFEQ *OFF
     CSR         LBQALC    SUB  W2QTY     LBQPCK
     CSR         LBUCUB    MULT LBQPCK    LBCUBE
     CSR         LBUWGT    MULT LBQPCK    LBSWGT
     CSR                   UPDATLBREC
     CSR                   ENDIF
     C*
     CSR         KEYOD     CHAINORDD                 79
     CSR         *IN79     IFEQ *OFF
     C*
     CSR         LBUTYP    IFEQ 'N'
     CSR         LBQALC    SUB  W2QTY     ODQPK1
     CSR                   ENDIF
     C*
     CSR         LBUTYP    IFEQ '1'
     CSR         LBQALC    SUB  W2QTY     ODQPK2
     CSR                   ENDIF
     C*
     CSR         LBUTYP    IFEQ '2'
     CSR         LBQALC    SUB  W2QTY     ODQPK3
     CSR                   ENDIF
     C*
     CSR                   UPDATODREC
     CSR                   ENDIF
     C*
     CSR                   EXSR SCR01I
     CSR         ENDUP2    ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Route
Pallet
Item
