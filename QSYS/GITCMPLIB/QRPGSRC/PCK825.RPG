     F*----------------------------------------------------------------
     F*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PCK825    Create IMPFITMO member from PIRITEM - Create
     F*  22 August 2000
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
414 AF*    08/22/00  DAS  4.14
     F*      - Created
     F*
     F*----------------------------------------------------------------
     F*
     F*  Notes
     F*
     F*    - The screen for this program must be compiled with
     F*      defer write equal to *NO.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  90        View mode (protect all fields)
     F*  91        Change mode (protect key fields)
     F*  97        Alawys on (used for SFLEND keyword)
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPCK825FMCF  E                    WORKSTN                        UC
     F                                              KINFDS INFDS
     FPIRITEM IF  E           K        DISK
     FITEMQTY IF  E           K        DISK
     FIMPFITMOO   E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  15 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PJTYP  0 - Batch
     C*              1 - Interactive
     C*      $PFROM  From quantity
     C*      $PTO    To quantity
     C*
     C*    Returned Parameters
     C*      None
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PJTYP  1
     C                     PARM           $PFROM 155
     C                     PARM           $PTO   155
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Only work with warehouse 900.
     C*
     C                     Z-ADD900       KYWHSE
     C*
     C*  Whip through items.
     C*
     C                     Z-ADD9         COUNT   30
     C           KEYIT     SETLLPIRITEM
     C           FOREVR    DOWEQFOREVR
     C           KEYIT     READEPIRITEM                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*     Keep user informed.
     C*
     C                     ADD  1         COUNT
     C           $PJTYP    IFEQ *ON
     C           COUNT     ANDGE10
     C                     MOVE ITITEM    W1ITEM
     C                     WRITESCREEN1
     C                     Z-ADD0         COUNT
     C                     ENDIF
     C*
     C*     Get warehouse quantity.
     C*
     C           KEYIQ     CHAINITEMQTY              79
     C           *IN79     IFEQ *ON
     C                     Z-ADD0         IQSTK1
     C                     ENDIF
     C*
     C*     Write import record.
     C*
     C                     Z-ADDITWHSE    IJWHSE
     C                     MOVE ITITEM    IJITEM
     C*
     C                     CALL 'RANDOM2'
     C                     PARM           $RSEED  99
     C                     PARM $PFROM    $RMIN   60
     C                     PARM $PTO      $RMAX   60
     C                     PARM           $RNUM   60
     C                     Z-ADD$RNUM     IJTQO
     C*
     C                     Z-ADDIQSTK1    IJQYOH
     C                     WRITEIJREC
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize variables
     C*
     CSR                   MOVE *ON       FOREVR  1
     CSR                   MOVE *OFF      ERROR   1
     C*
     C*   Define keys
     C*
     CSR         *LIKE     DEFN ITWHSE    KYWHSE
     CSR         KEYIT     KLIST
     CSR                   KFLD           KYWHSE
     C*
     CSR         KEYIQ     KLIST
     CSR                   KFLD           ITWHSE
     CSR                   KFLD           ITITEM
     C*
     C*   Open display file if this program is being run interactively.
     C*
     CSR         $PJTYP    IFEQ *ON
     CSR                   OPEN PCK825FM
     CSR                   ENDIF
     C*
     C*   Fool the compiler.
     C*
     CSR                   MOVE *BLANKS   DUMMY   1
     CSR         DUMMY     IFNE *BLANKS
     CSR                   READ PCK825FM                 50
     CSR                   ENDIF
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
