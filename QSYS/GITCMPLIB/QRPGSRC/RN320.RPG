     F*----------------------------------------------------------------
     F*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  RN320     Print Customer Returns Analysis - Options
     F*  August 28, 2002
     F*  Max Blacknik
     F*
     F*  Revisions
     F*
416 AA*  08/28/02  MLB  4.16
     A*    - Created
     F*
500bAA*  10/31/05  RLP  5.00b
     A*    - ENH: Re-enable/modify w1detl line, also adding *ALL or
     A*           specific return code to options.
     A*    - ENH: adding new report RN326 to print when (S)ummary is
     A*           entered for w1detl field, will default to 'S' and
     A*           will print current report RN325 when (D)etail is
     A*           entered for W1DETL.
     A*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FRN320FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FPIRUSER1IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
500bDE*                   DESC    1   6 60
500bME                    DESC    1   7 60
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  24 $LTASK
     I*
     I                                       19  210$LWHSE
     I                                       22  27 $LCREW
     I*
500bAI                                       19  22 $LRCOD
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   40ERDATE
     I                                        5  14 ERDAY
     I*
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $PWHSE  Warehouse code
     C*      $PFMDT  From date (YYYYMMDD)
     C*      $PENDT  End date (YYYYMMDD)
     C*      $PCREW  Crew name
     C*      $PEMP#  Employee number
500 AC*      $PRCOD  Return code (*ALL, return code)
500 DC**     $PDETL  Report type (B=Both, D=Detail, S=Summary) *not used now*
500 MC*      $PDETL  Report type (D=Detail, S=Summary)
     C*      $PJOBQ  Put on jobq (Y,N)
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*      $PDEV   Printer device
     C*      $PFORM  Form type
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PFMDT  8
     C                     PARM           $PTODT  8
     C                     PARM           $PCREW  6
     C                     PARM           $PEMP#  5
500bAC                     PARM           $PRCOD  4
     C                     PARM           $PDETL  1
     C                     PARM           $PJOBQ  1
     C                     PARM           $PRTN   8
     C                     PARM           $PDEV  10
     C                     PARM           $PFORM 10
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     ENDIF
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     ENDCS
     C                     ENDDO
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   ENDIF
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     C                     SELEC
     CSR         NXTSCR    WHEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   ENDSL
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     C*
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
     CSR                   EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     CSR                   EXSR ZZINZ
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*
     C*    ZMCMSG  Clear message record subfile.
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   ENDIF
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   ENDIF
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*
     C*    PIR9901  From date greater than To date.
     C*
     CSR         ZM9901    BEGSR
     CSR                   MOVE 'PIR9901' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0204  Enter only one value.
     C*
     CSR         ZM0204    BEGSR
     CSR                   MOVE 'PIR0204' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0205  Must enter one value.
     C*
     CSR         ZM0205    BEGSR
     CSR                   MOVE 'PIR0205' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR0110  Number must be >0.
     C*
     CSR         ZM0110    BEGSR
     CSR                   MOVE 'PIR0110' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify From Date.
     C*
     CSR                   MOVE *BLANKS   $PFMDT
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1FMDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $CVMSG    #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ELSE
     CSR                   MOVE $CVD8O    $PFMDT
     CSR                   ENDIF
     C*
     C*  Verify To Date.
     C*
     CSR                   MOVE *BLANKS   $PTODT
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1TODT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $CVMSG    #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ELSE
     CSR                   MOVE $CVD8O    $PTODT
     CSR                   ENDIF
     C*
     C*  Verify From date <= To date
     C*
     CSR         W1FMDT    IFNE 0
     CSR         W1TODT    ANDNE0
     CSR         $PFMDT    ANDGT$PTODT
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   EXSR ZM9901
     CSR                   ENDIF
     C*
     C*  Crew code or employee number must be entered.
     C*
     CSR         W1CREW    IFEQ *BLANKS
     CSR         W1EMP#    ANDEQ0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE *ON       *IN29
     CSR                   MOVE *ON       *IN09
     CSR                   EXSR ZM0205
     CSR                   ENDIF
     C*
     C*  Crew code and employee number cannot both be entered.
     C*
     CSR         W1CREW    IFNE *BLANKS
     CSR         W1EMP#    ANDNE0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE *ON       *IN29
     CSR                   MOVE *ON       *IN09
     CSR                   EXSR ZM0204
     CSR                   ENDIF
     C*
     C*  Verify Crew Code, if entered.
     C*
     CSR         W1CREW    IFNE *BLANKS
     CSR         W1CREW    ANDNE'*ALL  '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1CREW    $LCREW
     CSR                   CALL 'CR900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE $LMSG     #MSGK
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Employee number, if entered.
     C*
     CSR         W1EMP#    IFNE 0
     CSR         W1EMP#    CHAINPIRUSER1             79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN29
     CSR                   MOVE *ON       *IN09
     CSR                   MOVE DESC,5    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Return Code cannot be blank.
     C*
500bACSR         W1RCOD    IFEQ *BLANKS
500bACSR                   MOVE *ON       ERROR
500bACSR                   MOVE *ON       *IN30
500bACSR                   MOVE *ON       *IN10
500bACSR                   MOVE *BLANKS   ERRMSG
500bACSR                   MOVELDESC,7    ERRMSG
500bACSR                   EXSR ZM1001
500bACSR                   ENDIF
500bAC*
500bAC*  Verify Return Code, if not equal to '*ALL'.
500bAC*
500bACSR         W1RCOD    IFNE *BLANKS
500bACSR         W1RCOD    ANDNE'*ALL  '
500bAC*
500bAC*   Left justify Return Code field.
500bAC*
500bACSR                   MOVEL*BLANKS   $CSTR
500bACSR                   MOVELW1RCOD    $CSTR
500bACSR                   CALL 'ADJUST'
500bACSR                   PARM '*LEFTNOZ'$CCMD   8
500bACSR                   PARM           $CSTR  30
500bACSR                   MOVEL$CSTR     W1RCOD
500bAC*
500bACSR                   MOVE '*VERIFY '$LCMD
500bACSR                   MOVE W1RCOD    $LRCOD
500bACSR                   CALL 'RN900'
500bACSR                   PARM           $LPARM
500bACSR         $LRTN     IFEQ '*ERROR  '
500bACSR                   MOVE *ON       ERROR
500bACSR                   MOVE *ON       *IN30
500bACSR                   MOVE *ON       *IN10
500bACSR                   MOVE *BLANKS   ERRMSG
500bACSR                   MOVELDESC,7    ERRMSG
500bACSR                   EXSR ZM1001
500bACSR                   ELSE
500bACSR         $LRTN     IFEQ '*PGMQ   '
500bACSR                   MOVE *ON       ERROR
500bACSR                   MOVE *ON       *IN30
500bACSR                   MOVE *ON       *IN10
500bACSR                   MOVE *BLANKS   ERRMSG
500bACSR                   MOVELDESC,7    ERRMSG
500bACSR                   EXSR ZM1001
500bACSR                   ENDIF
500bACSR                   ENDIF
500bACSR                   ENDIF
     C*
     C*  Verify detail report option
     C*
500bMCSR         W1DETL    IFNE 'B'
500bMCSR         W1DETL    ANDNE'D'
     CSR         W1DETL    ANDNE'S'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN25
     CSR                   MOVE *ON       *IN05
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,6    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN26
     CSR                   MOVE *ON       *IN06
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,1    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default device
     C*
     CSR                   MOVELW1DEV     WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1DEV     ANDNE'*SYSVAL'
     CSR         W1DEV     ANDNE'*JOB'
     CSR         W1DEV     OREQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN27
     CSR                   MOVE *ON       *IN07
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,2    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify default form type
     C*
     CSR                   MOVELW1FORM    WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1FORM    ANDNE'*STD'
     CSR         W1FORM    OREQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN28
     CSR                   MOVE *ON       *IN08
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     CSR                   SELEC
     C*
     C*     Warehouse code.
     C*
     CSR         ROW#      WHEQ 7
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
     C*     Crew code.
     C*
     CSR         ROW#      WHEQ 10
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1CREW    $LCREW
     CSR                   CALL 'CR900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LCREW    W1CREW
     CSR                   ENDIF
     C*
     CSR                   OTHER
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     C*    Get default values for the user.
     C*
     CSR                   CALL 'GETUSER'
     CSR                   PARM           #USER
     CSR                   PARM 0         #WHSE   30
     CSR                   PARM 0         #EMP#   50
     CSR                   PARM           #FNAM  15
     CSR                   PARM           #INIT   1
     CSR                   PARM           #LNAM  20
     CSR                   PARM           #WHDP   5
     CSR                   PARM           #STYP   1
     CSR                   PARM           #SHFT   6
     CSR                   PARM           #CREW   6
     CSR                   PARM 0         #NHRS   31
     CSR                   PARM           #RTN    8
     C*
     CSR                   Z-ADD#WHSE     W1WHSE
     CSR                   MOVE *BLANKS   W1FMDT
     C*    Default cursor to position on from date.
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *BLANKS   W1TODT
     CSR                   MOVE *BLANKS   W1CREW
     CSR                   MOVE *ZEROS    W1EMP#
500bACSR                   MOVE '*ALL'    W1RCOD
500bDC*R                   MOVE 'D'       W1DETL
500bMCSR                   MOVE 'S'       W1DETL
     CSR                   MOVE 'Y'       W1JOBQ
     C*    Get Report Title
     CSR                   CALL 'GETRPT'
     CSR                   PARM '*DESC  ' $CMD    8
     CSR                   PARM 'RN325PR' $LRPT  10
     CSR                   PARM 0         $WHSE   30
     CSR                   PARM ' '       $STYP   1
     CSR                   PARM ' '       $WHDP   5
     CSR                   PARM           $DESC  50
     CSR                   PARM           $DEV   10
     CSR                   PARM           $FORM  10
     CSR                   MOVEL$DESC     RPTTTL
     C*
     C*   Center report title.
     C*
     CSR                   MOVELRPTTTL    $PSTR  60
     CSR                   Z-ADD50        $PLEN   30
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*   Get Device and Form
     CSR                   CALL 'GETRPT'
     CSR                   PARM '*DEVFORM'$CMD    8
     CSR                   PARM 'RN325PR' $LRPT  10
     CSR                   PARM 0         $WHSE   30
     CSR                   PARM ' '       $STYP   1
     CSR                   PARM ' '       $WHDP   5
     CSR                   PARM           $DESC  50
     CSR                   PARM           $DEV   10
     CSR                   PARM           $FORM  10
     CSR                   MOVEL$DEV      W1DEV
     CSR                   MOVEL$FORM     W1FORM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE #PROG     #PGMQ
     CSR                   MOVE #PROG     $CVPRG
     CSR                   MOVE #PROG     $LPRG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
     CSR                   MOVE W1WHSE    $PWHSE
     C*    From/To dates converted & loaded in ZZCHK1
     CSR                   MOVE W1CREW    $PCREW
     CSR                   MOVE W1EMP#    $PEMP#
500bACSR                   MOVE W1RCOD    $PRCOD
     CSR                   MOVE W1DETL    $PDETL
     CSR                   MOVE W1JOBQ    $PJOBQ
     CSR                   MOVE W1DEV     $PDEV
     CSR                   MOVE W1FORM    $PFORM
     CSR                   MOVE '*OK     '$PRTN
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Job queue option
Printer device
Form type
Crew code
Employee number
Detail report
Return Code
