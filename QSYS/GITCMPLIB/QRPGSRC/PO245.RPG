     F*----------------------------------------------------------------
     F*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  PO245     Prompt for Retail UPC code
     F*  11 April 2003
     F*  Max Blacknik
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
417 AF*    04/11/03  MLB  4.17
     F*      - Created.
     F*
700aA*    11/03/16  LMC  7.00a
     *      - Enh: UPC file has a database change. Recompile only.
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  01 - 20   FIELD POSITION TO
     F*  21 - 40   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT KEY)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPO245FM CF  E                    WORKSTN                        UC
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FUPC2    IF  E           K        DISK
     F            UPREC                             KRENAMERECORD
     FOPTIONS IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   5 50
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                    P  19  200$PWHSE
     I                                       21  29 $PPO
     I                                    P  30  320$PSEQ
     I                                       33  47 $PITEM
     I*----------------------------------------------------------------
     I*  *RCVLBL  -  Receiving options
     I*----------------------------------------------------------------
     I*
     I*    11/ 6/96  RH
     I*      - Added OPPASL & OPPFRC.
     I*    05/14/97  RH
     I*      - Added OPPLCN
     I*    04/09/98  HNK
     I*      - Added OPPCHM
     I*      - Added OPPMIX
     I*    08/19/99  MLB
     I*      - Added OPAPCK
     I*    10/09/00  DAS
     I*      - Added OPFRCM
     I*    09/24/01  DAS
     I*      - Added OPEXCK, OPCWEN, OPCWTP, OPCWFR
     I*    12/12/01  DAS
     I*      - Added OPIPCK
     I*
     I*    04/11/03  MLB
     I*      - Added OPRUPC, OPRUPS, OPRUPF.
     I* Fields
     I*
     I*    OPLPGM  -  Receiving label program.
     I*    OPUPCT  -  Ideal slot usage percentage.
     I*    OPPDEF  -  PIR minimum number of slots priority.
     I*    OPPBLD  -  PIR same build code priority.
     I*    OPPACT  -  PIR same activity zone priority.
     I*    OPFDEF  -  Fixed minimum number or slots priority.
     I*    OPFASL  -  Fixed same aisle priority.
     I*    OPFSID  -  Fixed same side priority.
     I*    OPFLVL  -  Fixed highest level priority.
     I*    OPPASL  -  PIR same aisle priority.
     I*    OPPFRC  -  Force PIR same aisle priority.
     I*    OPPLCN  -  Use generic license plates.
     I*    OPPCHM  -  Check chimney (Y,N)
     I*    OPPMIX  -  Allow mixed expiration dates in fixed home slot(Y,N)
     I*    OPAPCK  -  Assign pick slots automatically for fixed items(Y,N)
     I*    OPFRCM  -  Force marking of all P.O. detail lines (Y,N)
     I*    OPEXCK  -  Expiration date check (1,2,3)
     I*                What to do when exp date being rcvd comes
     I*                earlier than existing overflow exp date ...
     I*                  1 = Don't allow it.
     I*                  2 = Warn user & log when overriden
     I*                  3 = Let it go through.
     I*    OPCWEN  -  Allow catch weight entry (1,2,3)
     I*                  1 = Every receipt
     I*                  2 = First receipt only
     I*                  3 = Never
     I*    OPCWTP  -  Catch weight value entered (1,2,3)
     I*                  1 = User choice
     I*                  2 = Average
     I*                  3 = Total
     I*    OPCWFR  -  Force catch weight entry (Y,N)
     I*    OPIPCK  -  Ignore existing pick slots (Y,N)
     I*    OPRUPC  -  Prompt user for Retail UPC during receiving? (Y,N)
     I*    OPRUPS  -  Allow user to skip Retail UPC scan? (Y,N)
     I*    OPRUPF  -  Require Retail UPC on file before receipt? (Y,N)
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I                                        1  10 OPLPGM
     I                                       11  130OPUPCT
     I                                       14  140OPPDEF
     I                                       15  150OPPBLD
     I                                       16  160OPPACT
     I                                       17  170OPFDEF
     I                                       18  180OPFASL
     I                                       19  190OPFSID
     I                                       20  200OPFLVL
     I                                       21  210OPPASL
     I                                       22  22 OPPFRC
     I                                       23  23 OPPLCN
     I                                       24  24 OPPCHM
     I                                       25  25 OPPMIX
     I                                       26  26 OPAPCK
     I                                       27  27 OPFRCM
     I                                       28  28 OPEXCK
     I                                       29  29 OPCWEN
     I                                       30  30 OPCWTP
     I                                       31  31 OPCWFR
     I                                       32  32 OPIPCK
     I                                       33  33 OPRUPC
     I                                       34  34 OPRUPS
     I                                       35  35 OPRUPF
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVDOP
     I                                        9  140$CVDD6
     I                                       15  220$CVDD8
     I*
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*
     C*      $PCMD   *VERIFY  - Verify Retail UPC on file.
     C*      $PPRG   Program to send messages back to.
     C*      $PUKY   Contains key.
     C*
     C*    Returned Parameters
     C*
     C*      $PUKY   Contains file key info
     C*              Unchanged for everything else.
     C*      $PRTN   *OK      - Retail UPC on file. OK to continue.
     C*              *SKIPPED - User skiped verification of Retail UPC
     C*              *OKOVRID - Retail UPC not on file. User overrode to
     C*                         continue.
     C*              *ERROR   - Error occured. Message in $PERM
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     MOVE *BLANKS   $PRTN
     C                     MOVE *BLANKS   $PERM
     C*
     C                     EXSR ZZINZ2
     C*
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     ENDIF
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     ENDCS
     C                     ENDDO
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C*
     C                     RETRN
     C*
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   ENDIF
     C*
     C*   Overlay screen with error messages
     C*
     CSR                   EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR                   SELEC
     CSR         NXTSCR    WHEQ '01 '
     CSR                   EXFMTSCREEN1                50
     C*
     CSR                   ENDSL
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     303132
     CSR                   SETOF                     333435
     CSR                   SETOF                     363738
     CSR                   SETOF                     3940
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   MOVE *ZEROS    E       30
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     C*
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   MOVE *OFF      OVRRID
     C*
     CSR         $PCMD     IFEQ '*VIEW   '
     CSR                   MOVE *ON       *IN90
     CSR                   ENDIF
     C*
     C*  Get field defaults if this is an add.
     C*
     CSR         $PCMD     IFEQ '*ADD    '
     CSR                   EXSR ZZDFT1
     CSR                   ELSE
     C*
     C*  Otherwise get existing record(s).
     C*
     CSR                   MOVE *ON       *IN91
     CSR                   EXSR ZZGET1
     CSR                   EXSR ZZFIL1
     CSR                   ENDIF
     C*
     CSR         END01I    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR         *LOVAL    SETLLRECORD
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR         $PCMD     IFNE '*VIEW   '
     CSR                   EXSR SCR01I
     CSR                   ENDIF
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR         *LOVAL    SETLLRECORD
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   GOTO ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for other command keys BEFORE verification and update.
     C*
     CSR                   MOVE '*BEFORE 'CMDCHK  8
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update/verify record.
     C*
     CSR         $PCMD     IFEQ '*ADD    '
     CSR         $PCMD     OREQ '*CHANGE '
     CSR         $PCMD     OREQ '*VERIFY '
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   EXSR ZZUPD
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   ENDIF
     C*
     C*  Test for other command keys AFTER verification and update.
     C*
     CSR                   MOVE '*AFTER  'CMDCHK
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*
     C*  Everything went fine so get out of program.
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR         ENDSC1    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     CSR                   Z-ADD0         ENTER   50
     CSR                   Z-ADD2         COMAND  50
     CSR                   Z-ADD01122     ROLLUP  50
     CSR                   Z-ADD01123     ROLLDN  50
     CSR                   Z-ADD01124     CLEAR   50
     CSR                   Z-ADD01125     HELP    50
     CSR                   MOVE '*CLOSED 'DISPLY  8
     C*
     C*   Call user added initialization routine.
     C*
     CSR                   EXSR ZZINZ
     C*
     CSR                   ENDSR
     C*
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     C*
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     C*
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     C*
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     C*
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     C*
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   ENDIF
     C*
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDIF
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     C*
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     C*
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     C*
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     C*
     C*    Display file is open, close it before leaving.
     CSR         DISPLY    IFEQ '*OPEN   '
     CSR                   CLOSEPO245FM
     CSR                   MOVE '*CLOSED 'DISPLY
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     C*
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   ENDIF
     C*
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR0201  Code must be entered for create.
     C*
     CSR         ZM01      BEGSR
     C*
     CSR                   MOVE 'PIR0201' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEADESC      ERRMSG
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     C*
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     C*
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*   PIR0202  Code already exists.
     C*
     CSR         ZM02      BEGSR
     C*
     CSR                   MOVE 'PIR0202' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEADESC      ERRMSG
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     C*
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     C*
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     C*
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify UPC scanned by user.
     C*
     CSR         W1SCAN    IFEQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVELDESC,1    ERRMSG    P
     CSR                   EXSR ZM0105
     CSR                   ENDIF
     C*
     C*  Verify UPC scanned by user is on file.
     C*
     CSR         W1SCAN    IFGT *BLANKS
     C*
     C*   Right adjust UPC code for file.
     C*
     CSR                   MOVELW1SCAN    $CSTR     P
     CSR                   CALL 'ADJUST'
     CSR                   PARM '*RIGHT  '$CCMD   8
     CSR                   PARM           $CSTR  30
     CSR                   MOVE $CSTR     KYUPC
     C*
     CSR         FILEKY    SETLLUPC2                     79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE '*MISSING'RETUPC
     CSR                   ELSE
     CSR                   MOVE '*FOUND  'RETUPC
     CSR                   ENDIF
     C*
     CSR                   SELEC
     C*
     C*    Retail UPC not on file. Must be on file to receive item.
     C*
     CSR         RETUPC    WHEQ '*MISSING'
     CSR         OPRUPF    ANDEQ'Y'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVELDESC,3    ERRMSG    P
     CSR                   EXSR ZM0105
     C*
     C*    Retail UPC not on file. User allowed to override and continue.
     C*
     CSR         OPRUPF    WHNE 'Y'
     C*
     C*    UPCCHK Flag is used to force F10 for each test.
     C*    Values: Flag = 0 Do check.
     C*            Flag = 1 Check was done, ok.
     C*            Flag = 2 Warning condition.
     C*
     C*    Did scanned UPC change? If yes, reset flag.
     C*
     CSR         SVSCAN    IFNE W1SCAN
     CSR                   MOVE '0'       UPCCHK  1
     CSR                   ENDIF
     C*
     C*    Warning message issued, User accepted. Proceed.
     CSR         UPCCHK    IFEQ '2'
     CSR         OVRRID    ANDEQ*ON
     CSR                   MOVE '1'       UPCCHK
     CSR                   ENDIF
     C*
     C*    Warning
     CSR         UPCCHK    IFNE '1'
     CSR                   MOVE '1'       UPCCHK
     C*
     CSR         RETUPC    IFEQ '*MISSING'
     CSR                   MOVE '2'       UPCCHK
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVELDESC,5    ERRMSG    P
     CSR                   EXSR ZM0105
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDSL
     CSR                   ENDIF                           W1SCAN gt ' '
     C*
     CSR                   MOVE W1SCAN    SVSCAN
     C*
     CSR         ENDCK1    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     C*
     C*  Command keys to check BEFORE verification and update.
     C*
     CSR         CMDCHK    IFEQ '*BEFORE '
     C*
     CSR                   SELEC
     C*
     C*    F9=Skip selected. User allowed to skip Retail UPC verification.
     CSR         *INKI     WHEQ *ON
     CSR         OPRUPS    ANDEQ'Y'
     CSR                   MOVE *ON       CMDTKN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*SKIPPED'RETUPC
     C*
     C*    F9=Skip selected. Not allowed to skip Retail UPC verification.
     CSR         *INKI     WHEQ *ON
     CSR         OPRUPS    ANDNE'Y'
     CSR                   MOVE *ON       CMDTKN
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVELDESC,2    ERRMSG    P
     CSR                   EXSR ZM0105
     C*
     CSR                   ENDSL
     C*
     C*      F10 - Accept Warning.
     C*
     CSR                   SELEC
     C*
     C*      Retail UPC must be on file. F10=Override not allowed.
     CSR         OPRUPF    WHEQ 'Y'
     CSR         *INKJ     ANDEQ*ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVELDESC,4    ERRMSG    P
     CSR                   EXSR ZM0105
     C*
     C*      Retail UPC not required to be on file. F10=Override allowed.
     CSR         OPRUPF    WHNE 'Y'
     CSR         *INKJ     IFEQ *ON
     CSR                   MOVE *ON       OVRRID
     CSR                   ELSE
     CSR                   MOVE *OFF      OVRRID
     CSR                   ENDIF
     C*
     CSR                   ENDSL
     C*
     CSR                   ENDIF
     C*
     C*  Command keys to check AFTER verification and update.
     C*
     CSR         CMDCHK    IFEQ '*AFTER  '
     C*
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZDFT1   Get default values for add.
     C*
     CSR         ZZDFT1    BEGSR
     C*
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     CSR                   MOVE *BLANKS   W1SCAN
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZGET1   Get record(s) for screen 1.
     C*
     CSR         ZZGET1    BEGSR
     C*
     C*
     CSR         ENDGT1    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define key for main file.
     C*
     CSR         *LIKE     DEFN UPUPC     KYUPC
     CSR         FILEKY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     CSR                   KFLD           KYUPC
     C*
     C*  Define key for OPTIONS file.
     C*
     CSR         *LIKE     DEFN OPCODE    KYCODE
     CSR                   MOVE '*RCVLBL 'KYCODE
     CSR         OPKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           KYCODE
     C*
     CSR                   MOVE *OFF      FOREVR  1
     CSR                   MOVE *OFF      OVRRID  1
     CSR         *LIKE     DEFN W1SCAN    SVSCAN
     CSR                   MOVE *BLANKS   RETUPC  8
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*    If display is closed, open file for processing.
     CSR         DISPLY    IFEQ '*CLOSED '
     CSR                   OPEN PO245FM
     CSR                   MOVE '*OPEN   'DISPLY
     CSR                   ENDIF
     C*
     C*   Write Window Definition format.
     C*
     CSR                   Z-ADD1         WROW
     CSR                   Z-ADD2         WCOL
     CSR                   WRITESCREEN1W               50
     C*
     C*  Initialize message handling
     C*
     CSR                   EXSR ZMIMSG
     C*
     C*   Retrieve Receiving Options.
     C*
     CSR         OPKEY     CHAINOPTIONS              79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE 'N'       OPRUPC
     CSR                   MOVE 'N'       OPRUPS
     CSR                   MOVE 'N'       OPRUPF
     CSR                   ENDIF
     C*
     C*    Prompt user for Retail UPC.
     CSR         OPRUPC    IFEQ 'Y'
     C*
     CSR                   EXSR SCR01I
     C*
     C*    Exit program if prompt for Retail UPC not allowed.
     CSR                   ELSE
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     CSR                   SELEC
     C*
     C*    Retail UPC on file. Continue with receiving.
     CSR         RETUPC    WHEQ '*FOUND  '
     CSR                   MOVE '*OK     '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     C*    Retail UPC NOT on file. Continue with receiving.
     CSR         RETUPC    WHEQ '*MISSING'
     CSR         OPRUPF    ANDNE'Y'
     CSR                   MOVE '*OKOVRID'$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     C*    Retail UPC check skipped by user. Continue with receiving.
     CSR         RETUPC    WHEQ '*SKIPPED'
     CSR                   MOVE '*OKOVRID'$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR                   ENDSL
     C*
     CSR         ENDUPD    ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Retail UPC is blank.
F9=Skip not allowed. Must scan Retail UPC.
UPC not on file. Enter UPC, then proceed.
UPC must be on file. F10=Override not allowed.
UPC not on file. F10=Override to continue.
