     F*----------------------------------------------------------------
     F*
     F*  IV540     Move inventory count records to history files.
     F*  01 March 2000
     F*  Dave Sommerville
     F*
     F*  Revisions
413 AF*    03/01/00 DAS  4.13
     F*      - Created
414aAF*    05/12/00  RH   4.14a
     F*      - Recompiled, file INVSLTH changed, IVSADJ added.
     F*      - File INVSLOT changed.
416 AF*  09/25/01  RH   4.16
     F*    - Recompile - INVSLOT file changed.
416aAF*  09/17/02  RH   4.16a
     F*    - Add department.
500 AF*  08/31/05  MLB  5.00
     F*    - Enh: Revised program to receive $PCMD from IV505
     F*      to allow for better recovery processing.
500aAF*  12/14/05  RH   5.00a
     F*    - Fix: ENH 500  was applied to wrong version of program.
500bAF*  06/07/06  RH   5.00b
     F*    - ENH: Copy Inv Catch wt records to history and export files
510aAF*  06/15/07  RH   5.10a
     F*    - ENH: Copy Inv Slot assignment to history files.
520aAF*  05/08/08  RH   5.20a
     F*    - ENH: Remove inv assignment work file records.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  78        Chain indicator
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FINVSLOT UF  E           K        DISK
     FINVPAGE UF  E           K        DISK
     FINVBSLT UF  E           K        DISK
     FINVEXCP UF  E           K        DISK
500b FINVSCW  UF  E           K        DISK
510a FINVSASN UF  E           K        DISK
520a FIV270A  UF  E           K        DISK
     F*
     FINVSLTH UF  E           K        DISK                      A
     F            IVSREC                            KRENAMEIVSRECH
     FINVPAGH UF  E           K        DISK                      A
     F            IVPREC                            KRENAMEIVPRECH
     FINVBSLH UF  E           K        DISK                      A
     F            IBSREC                            KRENAMEIBSRECH
     FINVEXCH UF  E           K        DISK                      A
     F            IVEREC                            KRENAMEIVERECH
500b FINVSCWH UF  E           K        DISK                      A
500b F            IVWREC                            KRENAMEIVWRECH
500b FEXPSCW  UF  E           K        DISK                      A
510a FINVSASH UF  E           K        DISK                      A
510a F            IVGREC                            KRENAMEIVGRECH
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
500 AC*      $PCMD   *POSTINV - Post physical inventory/cyle counts
500 AC*              *RESTART - Restart abnormally ended batch.
     C*      $PWHSE  Warehouse
     C*
     C*    Returned Parameters
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
500 AC                     PARM           $PCMD   8
     C                     PARM           $PWHSE  30
416  C                     PARM           $PWHDP  5
     C*----------------------------------------------------------------
     C*  Main line
     C*
500 AC*
500 AC*    Error occured during clearing/moving inventory records
500 AC*    to history files. Skip clearing history files when in
500 AC*    recovery mode and go directly to resume moving inventory
500 AC*    records to history files.
500 AC*
500 AC           $PCMD     CABEQ'*RESTART'#RESTR
500 AC*
     C*  Remove records from History files
     C*
     C*    File INVSLTH
     C*
     C           KEYWHS    SETLLINVSLTH
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVSLTH                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVSWHD
416  C                     GOTO SKIP1
416  C                     ENDIF
     C                     DELETIVSRECH
416  C           SKIP1     TAG
     C                     ENDDO
     C*
     C*    File INVPAGH
     C*
     C           KEYWHS    SETLLINVPAGH
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVPAGH                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVPWHD
416  C                     GOTO SKIP2
416  C                     ENDIF
     C                     DELETIVPRECH
416  C           SKIP2     TAG
     C                     ENDDO
     C*
     C*    File INVBSLH
     C*
     C           KEYWHS    SETLLINVBSLH
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVBSLH                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIBSWHD
416  C                     GOTO SKIP3
416  C                     ENDIF
     C                     DELETIBSRECH
416  C           SKIP3     TAG
     C                     ENDDO
     C*
     C*    File INVEXCH
     C*
     C           KEYWHS    SETLLINVEXCH
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVEXCH                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVEWHD
416  C                     GOTO SKIP4
416  C                     ENDIF
     C                     DELETIVERECH
416  C           SKIP4     TAG
     C                     ENDDO
510a C*
510a C*    File INVSASH
510a C*
510a C           KEYWHS    SETLLINVSASH
510a C           FOREVR    DOUNEFOREVR
510a C           KEYWHS    READEINVSASH                  79
510a C           *IN79     IFEQ *ON
510a C                     LEAVE
510a C                     ENDIF
510a C           $PWHDP    IFNE '*ALL'
510a C           $PWHDP    ANDNEIVGWHD
510a C                     GOTO SKIP12
510a C                     ENDIF
510a C                     DELETIVGRECH
510a C           SKIP12    TAG
510a C                     ENDDO
500b C*
500b C*    File INVSCWH
500b C*
500b C           KEYWHS    SETLLINVSCWH
500b C           FOREVR    DOUNEFOREVR
500b C           KEYWHS    READEINVSCWH                  79
500b C           *IN79     IFEQ *ON
500b C                     LEAVE
500b C                     ENDIF
500b C           $PWHDP    IFNE '*ALL'
500b C           $PWHDP    ANDNEIVWWHD
500b C                     GOTO SKIP9
500b C                     ENDIF
500b C                     DELETIVWRECH
500b C           SKIP9     TAG
500b C                     ENDDO
500b C*
500b C*    File EXPSCW
500b C*
500b C           KEYWHS    SETLLEXPSCW
500b C           FOREVR    DOUNEFOREVR
500b C           KEYWHS    READEEXPSCW                   79
500b C           *IN79     IFEQ *ON
500b C                     LEAVE
500b C                     ENDIF
500b C           $PWHDP    IFNE '*ALL'
500b C           $PWHDP    ANDNEEVWWHD
500b C                     GOTO SKIP11
500b C                     ENDIF
500b C                     DELETEVWREC
500b C           SKIP11    TAG
500b C                     ENDDO
     C*
     C*
     C*  Copy records to history files and remove from std files
     C*
500 AC           #RESTR    TAG
500 AC*
     C*    File INVSLOT
     C*
     C           KEYWHS    SETLLINVSLOT
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVSLOT                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVSWHD
416  C                     GOTO SKIP5
416  C                     ENDIF
     C                     WRITEIVSRECH
     C                     DELETIVSREC
416  C           SKIP5     TAG
     C                     ENDDO
     C*
     C*    File INVPAGE
     C*
     C           KEYWHS    SETLLINVPAGE
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVPAGE                  78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVPWHD
416  C                     GOTO SKIP6
416  C                     ENDIF
     C                     WRITEIVPRECH
     C                     DELETIVPREC
416  C           SKIP6     TAG
     C                     ENDDO
     C*
     C*    File INVBSLT
     C*
     C           KEYWHS    SETLLINVBSLT
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEINVBSLT                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIBSWHD
416  C                     GOTO SKIP7
416  C                     ENDIF
     C                     WRITEIBSRECH
     C                     DELETIBSREC
416  C           SKIP7     TAG
     C                     ENDDO
     C*
     C*    File INVEXCP
     C*
     C           KEYWHS    SETLLINVEXCP
     C           FOREVR    DOUNEFOREVR
     C*
     C           KEYWHS    READEINVEXCP                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
416  C           $PWHDP    IFNE '*ALL'
416  C           $PWHDP    ANDNEIVEWHD
416  C                     GOTO SKIP8
416  C                     ENDIF
     C                     WRITEIVERECH
     C                     DELETIVEREC
416  C           SKIP8     TAG
     C                     ENDDO
510a C*    File INVSASN
510a C*
510a C           KEYWHS    SETLLINVSASN
510a C           FOREVR    DOUNEFOREVR
510a C           KEYWHS    READEINVSASN                  79
510a C           *IN79     IFEQ *ON
510a C                     LEAVE
510a C                     ENDIF
510a C           $PWHDP    IFNE '*ALL'
510a C           $PWHDP    ANDNEIVGWHD
510a C                     GOTO SKIP13
510a C                     ENDIF
510a C                     WRITEIVGRECH
510a C                     DELETIVGREC
510a C           SKIP13    TAG
510a C                     ENDDO
520a C*    File IV270A Assignment work file
520a C*
520a C           KEYWHS    SETLLIV270A
520a C           FOREVR    DOUNEFOREVR
520a C           KEYWHS    READEIV270A                   79
520a C           *IN79     IFEQ *ON
520a C                     LEAVE
520a C                     ENDIF
520a C           $PWHDP    IFNE '*ALL'
520a C           $PWHDP    ANDNEIVVWHD
520a C                     GOTO SKIP14
520a C                     ENDIF
520a C                     DELETIVVREC
520a C           SKIP14    TAG
520a C                     ENDDO
500b C*
500b C*    File INVSCW
500b C*
500b C           KEYWHS    SETLLINVSCW
500b C           FOREVR    DOUNEFOREVR
500b C*
500b C           KEYWHS    READEINVSCW                   79
500b C           *IN79     IFEQ *ON
500b C                     LEAVE
500b C                     ENDIF
500b C           $PWHDP    IFNE '*ALL'
500b C           $PWHDP    ANDNEIVWWHD
500b C                     GOTO SKIP10
500b C                     ENDIF
500b C                     WRITEIVWRECH
500b C                     Z-ADDIVWWHS    EVWWHS
500b C                     MOVE IVWDSP    EVWDSP
500b C                     MOVE IVWWHD    EVWWHD
500b C                     MOVE IVWASL    EVWASL
500b C                     Z-ADDIVWLOC    EVWLOC
500b C                     Z-ADDIVWLVL    EVWLVL
500b C                     MOVE IVWHND    EVWHND
500b C                     MOVE IVWSTP    EVWSTP
500b C                     MOVE IVWITM    EVWITM
500b C                     Z-ADDIVWSID    EVWSID
500b C                     Z-ADDIVWWGT    EVWWGT
500b C                     Z-ADDIVWLRG    EVWLRG
500b C                     Z-ADDIVWHRG    EVWHRG
500b C                     MOVE IVWWTX    EVWWTX
500b C                     WRITEEVWREC
500b C                     DELETIVWREC
500b C           SKIP10    TAG
500b C                     ENDDO
     C*
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Define partial key.
     C*
     CSR         KEYWHS    KLIST
     CSR                   KFLD           $PWHSE
     C*
     CSR                   ENDSR
