     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV354     Clear inventory exceptions - Clear
     F*  06 May 1999
     F*  Roxanne Harrison
     F*
     F*  SK114 - 1.00
     F*
     F*  ***  THIS PROGRAM'S PROCESS WILL MIRROR IV212.  ***
     F*
     F*  Revisions
414aAF*    05/15/00  RH   4.14a
     F*      - Recompiled, file INVSLOT changed.
416 AF*  09/25/01  RH   4.16
     F*    - Recompile - INVSLOT file changed.
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FINVEXCP2UF  E           K        DISK
     F            IVEREC                            KRENAMERECORD
     FINVSLOT UF  E           K        DISK
411 AFSLOT1   IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    MSG     1   1 50
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHSE
     I                                       22  26 $PWHDP
     I                                       27  38 $PDISP
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   Not used
     C*      $PPRG   Program to send messages back to.
     C*      $PUKY   Contains unique key for delete.
     C*
     C*    Returned Parameters
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured. Message in $PERM
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*              *OK      - Record deleted successfully.
     C*                         Message sent to pgmq.
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C           FILEKY    CHAINRECORD               79
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZDEL
     C           ERROR     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     EXSR ZZOK
     C                     END
     C                     ELSE
     C                     EXSR ZM75
     C                     MOVE '*PGMQ   '$PRTN
     C                     END
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*     PIR7975  Error while deleting record.
     C*
     CSR         ZM75      BEGSR
     CSR                   MOVE 'PIR7975' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     IV35421  Exception cleared.
     C*
     CSR         ZM21      BEGSR
     CSR                   MOVE 'IV35421' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDEL    Delete corresponding records.
     C*
     CSR         ZZDEL     BEGSR
     CSR                   MOVE *OFF      ERROR   1
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for main file.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PWHDP
     CSR                   KFLD           $PDISP
     C*
     CSR         *LIKE     DEFN IVEASL    IKYASL
     CSR         *LIKE     DEFN IVELOC    IKYLOC
     CSR         *LIKE     DEFN IVELVL    IKYLVL
     CSR         *LIKE     DEFN IVEHND    IKYHND
     C*
     C*  Define key for INVSLOT file.
     C*
     CSR         SLKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PWHDP
     CSR                   KFLD           IKYASL
     CSR                   KFLD           IKYLOC
     CSR                   KFLD           IKYLVL
     CSR                   KFLD           IKYHND
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   MOVE #PROG     $CVPRG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZOK     Send back "successful" message.
     C*
     CSR         ZZOK      BEGSR
     CSR                   MOVE *BLANKS   $MD
     CSR                   MOVEAMSG,1     $MD,1
     CSR                   MOVEAIVEDSP    $MD,6
     CSR                   EXSR ZM21
     CSR                   MOVE '*OK     '$PRTN
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     C*
     CSR                   MOVE IVEASL    IKYASL
     CSR                   MOVE IVELOC    IKYLOC
     CSR                   MOVE IVELVL    IKYLVL
     CSR                   MOVE IVEHND    IKYHND
     C*
     C*   Update INVSLOT status to SLOT status.
     C*
     CSR         SLKEY     CHAININVSLOT              74
     CSR         *IN74     IFEQ *OFF
     CSR         IVSSTA    ANDEQ'X '
     CSR         SLKEY     CHAINSLOT1                75
     CSR         *IN75     IFEQ *OFF
     CSR                   MOVE SLSTAT    IVSSTA
     CSR                   UPDATIVSREC                 76
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*   Update record.
     C*
     CSR                   MOVE '4'       IVESTA
     CSR                   UPDATRECORD
404 MCSR         ENDUP     ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
**
Slot
