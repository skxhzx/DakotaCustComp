     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT380C    Print replenishment labels. - Options
     F*  Hemant Kapadia
     F*  15 May, 1996
     F*
     F*  Revisions
     F*
409  F*    04/07/98  RH   4.09
     F*      - *ALL not allowed for # of Replenishments.
     F*
414  F*    10/10/00  DAS  4.14
     F*      - Recompiled because LAYOUTH changed.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT380CFMCF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FLAYOUTH IF  E           K        DISK                               GFG
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   7 60
     I*----------------------------------------------------------------
     I*  Error message parameter
     I*
     I$CERM       DS
     I                                        1  60 XCERM
     I                                        1   4 $CMSG
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I                                       22  23 $LSDEF                GFG
     I*  Valid characters for qty fields.                                 GFG
     I              '0123456789 '         C         VALID                 GFG
     I*  Field text                                                       GFG
     I              'Number of replenishm-C         NRPLTX                GFG
     I              'ent labels'                                          GFG
     I*  Valid values for brokencase field.                               GFG
     I              '*BROKEN   '          C         @TYPE1                GFG
     I              '*NONBROKEN'          C         @TYPE2                GFG
     I              '*ALL      '          C         @TYPE3                GFG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I*                                                                   GFG
     I                                        1  30 ERFLD                 GFG
     I                                       31  40 ERFROM                GFG
     I                                       41  50 ERTO                  GFG
     I*                                                                   GFG
     I                                        1   4 ERAISL                GFG
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $WHSE            - Warehouse number
     C*      $WHDP            - Warehouse department
     C*      $NRPL            - *ALL or number of replenishments          GFG
     C*      $FRSL            - Free overflow slots                       GFG
     C*      $AISL            - Aisle to replenish                        GFG
     C*      $SDEF            - Overflow slot type to select              GFG
     C*      $JOBQ            - Put on a job que (Y,N)                    GFG
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE   3
     C                     PARM           $WHDP   5
     C                     PARM           $NRPL   4                       GFG
     C                     PARM           $FRSL   4                       GFG
     C                     PARM           $AISL   4                       GFG
     C                     PARM           $SDEF   4                       GFG
     C                     PARM           $TYPE  10                       GFG
     C                     PARM           $PRTN   8
     C                     PARM           $JOBQ   1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE '1'       *IN97
     C                     MOVE '0'       *IN90
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ '1'
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE '1'       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ '1'
     CSR                   MOVE '1'       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE '0'       WARN    1
     CSR                   MOVE '0'       ERROR   1
     CSR                   MOVE '0'       CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ '1'
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------   GFG
     C*   PIR0112  Invalid range entered.                                 GFG
     C*                                                                   GFG
     CSR         ZM0112    BEGSR                                          GFG
     CSR                   MOVE 'PIR0112' #MSGID                          GFG
     CSR                   MOVE '*DIAG  ' #MSGTP                          GFG
     CSR                   MOVEAERRMSG    $MD                             GFG
     CSR                   EXSR ZMPMSG                                    GFG
     CSR                   ENDSR                                          GFG
     C*----------------------------------------------------------------   GFG
     C*   PIR0122  Invalid aisle entered for replenishments.              GFG
     C*                                                                   GFG
     CSR         ZM0122    BEGSR                                          GFG
     CSR                   MOVE 'PIR0122' #MSGID                          GFG
     CSR                   MOVE '*DIAG  ' #MSGTP                          GFG
     CSR                   MOVEAERRMSG    $MD,1                           GFG
     CSR                   EXSR ZMPMSG                                    GFG
     CSR                   ENDSR                                          GFG
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1006  Invalid jobq option for view report option.
     C*
     CSR         ZM1006    BEGSR
     CSR                   MOVE 'PIR1006' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify warehouse department.
     C*
     CSR         W1WHDP    IFNE '*ALL'
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*                                                                   GFG
     C*                                                                   GFG
     C*  Validate number of replenishments entered                        GFG
     C*                                                                   GFG
409 DC*          W1NRPL    IFNE '*ALL'                     Limited replen.GFG
     C           VALID     CHECKW1NRPL    POS     30                      GFG
     C           POS       IFGT *ZEROS                     Invalid char's GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN23                           GFG
     C                     MOVE *ON       *IN03                           GFG
     C                     MOVE *BLANKS   ERRMSG
     C                     MOVELDESC,1    ERRMSG
     C                     EXSR ZM1001                                    GFG
     C*                                                                   GFG
     C                     ELSE                                           GFG
     C*                                                                   GFG
     C*  Convert string to number of replenishments.                      GFG
     C*                                                                   GFG
     C                     MOVE '*STR2QTY'$CCMD                           GFG
     C                     MOVE #PROG     $CPRG                           GFG
     C*                                                                   GFG
     C                     MOVELW1NRPL    $CQSTR                          GFG
     C                     CALL 'CVTQTY'                                  GFG
     C                     PARM           $CCMD   8                       GFG
     C                     PARM           $CPRG  10                       GFG
     C                     PARM           $CQTY1  70                      GFG
     C                     PARM           $CQTY2  70                      GFG
     C                     PARM           $CQTY3  70                      GFG
     C                     PARM           $CUM1   2                       GFG
     C                     PARM           $CUM2   2                       GFG
     C                     PARM           $CUM3   2                       GFG
     C                     PARM           $CQSTR 10                       GFG
     C                     PARM           $CUSTR 17                       GFG
     C                     PARM           $CRTN   8                       GFG
     C                     PARM           $CERM                           GFG
     C           $CRTN     IFEQ '*PGMQ   '                                GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN23                           GFG
     C                     MOVE *ON       *IN03                           GFG
     C                     MOVE $CMSG     #MSGK                           GFG
     C                     EXSR ZMQMSG                                    GFG
     C                     ENDIF                                          GFG
     C*                                                                   GFG
     C*     Check if number of labels is out of range.                    GFG
     C*                                                                   GFG
     C           $CQTY1    IFLT 1                                         GFG
     C           $CQTY1    ORGT 999                                       GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN23                           GFG
     C                     MOVE *ON       *IN03                           GFG
     C                     MOVELNRPLTX    ERFLD                           GFG
     C                     MOVEL1         ERFROM                          GFG
     C                     MOVEL'999'     ERTO                            GFG
     C                     EXSR ZM0112                                    GFG
     C                     ENDIF                                          GFG
     C*                                                                   GFG
     C           ERROR     IFEQ *OFF                                      GFG
     C                     MOVE $CQTY1    W1NRPL                          GFG
     C                     ENDIF                                          GFG
     C                     ENDIF                                          GFG
409 DC*                    ENDIF                                          GFG
     C*
     C*  Verify free slot only option
     C*
     CSR         W1FRSL    IFNE '*YES'
     CSR         W1FRSL    ANDNE'*NO '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN24
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,6    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*                                                                   GFG
     C*  Validate aisle entered by user.                                  GFG
     C*                                                                   GFG
     C           W1AISL    IFNE '*ALL'                                    GFG
     C                     CALL 'ADJUST'                                  GFG
     C                     PARM '*RIGHT  '$CCMD   8                       GFG
     C                     PARM W1AISL    $CSTR  30                       GFG
     C                     MOVE $CSTR     W1AISL                          GFG
     C                     MOVE $CSTR     LHAISL                          GFG
     C           KEYLH     SETLLLHREC                    79               GFG
     C           *IN79     IFEQ *OFF                                      GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN25                           GFG
     C                     MOVE *ON       *IN05                           GFG
     C                     MOVE *BLANKS   ERRMSG                          GFG
     C                     MOVE W1AISL    ERAISL                          GFG
     C                     EXSR ZM0122                                    GFG
     C                     ENDIF                                          GFG
     C                     ENDIF                                          GFG
     C*                                                                   GFG
     C*  Validate slot type entered by user.                              GFG
     C*                                                                   GFG
     C           W1SDEF    IFNE '*ALL'                                    GFG
     C                     CALL 'ADJUST'                                  GFG
     C                     PARM '*LEFT   '$CCMD                           GFG
     C                     PARM W1SDEF    $CSTR                           GFG
     C                     MOVEL$CSTR     W1SDEF                          GFG
     C*                                                                   GFG
     C                     MOVE '*VERIFY '$LCMD                           GFG
     C                     Z-ADDW1WHSE    $LWHSE                          GFG
     C                     MOVELW1SDEF    $LSDEF                          GFG
     C                     CALL 'SD900'                                   GFG
     C                     PARM           $LPARM                          GFG
     C           $LRTN     IFEQ '*ERROR  '                                GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN26                           GFG
     C                     MOVE *ON       *IN06                           GFG
     C                     MOVEL$LERM     ERRMSG                          GFG
     C                     EXSR ZM0105                                    GFG
     C                     ELSE                                           GFG
     C           $LRTN     IFEQ '*PGMQ   '                                GFG
     C                     MOVE *ON       ERROR                           GFG
     C                     MOVE *ON       *IN26                           GFG
     C                     MOVE *ON       *IN06                           GFG
     C                     MOVE $LMSG     #MSGK                           GFG
     C                     EXSR ZMQMSG                                    GFG
     C                     ENDIF                                          GFG
     C                     ENDIF                                          GFG
     C                     ENDIF                                          GFG
     C*                                                                   GFG
     C*  Validate printer device.                                         GFG
     C*                                                                   GFG
     C*R                   MOVELW1DEV     WORK1A  1
     C*R         WORK1A    IFEQ '*'
     C*R         W1DEV     ANDNE'*SYSVAL'
     C*R         W1DEV     ANDNE'*JOB'
     C*R         W1DEV     OREQ *BLANKS
     C*R                   MOVE '1'       ERROR
     C*R                   MOVE '1'       *IN24
     C*R                   MOVE '1'       *IN04
     C*R                   MOVE *BLANKS   ERRMSG
     C*R                   MOVELDESC,4    ERRMSG
     C*R                   EXSR ZM1001
     C*R                   ENDIF
     C*
     C*  Verify broken case option
     C*
     CSR         W1TYPE    IFNE @TYPE1
     CSR         W1TYPE    ANDNE@TYPE2
     CSR         W1TYPE    ANDNE@TYPE3
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN27
     CSR                   MOVE *ON       *IN07
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,7    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN28
     CSR                   MOVE *ON       *IN08
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE '0'       CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     C*     Warehouse.
     C*
     CSR                   SELEC
     CSR         ROW#      WHEQ 08
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE #PROG     $LPRG
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
     C*     Warehouse department.
     C*
     CSR         ROW#      WHEQ 09
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LWHDP    W1WHDP
     CSR                   ENDIF
     C*                                                                   GFG
     C*     Overflow slot type.                                           GFG
     C*                                                                   GFG
     C           CSRRCD    WHEQ 'SCREEN1'                                 GFG
     C           CSRFLD    ANDEQ'W1SDEF'                                  GFG
     C                     MOVE *ON       *IN06                           GFG
     C                     MOVE '*LOOKUP '$LCMD                           GFG
     C                     MOVE #PROG     $LPRG                           GFG
     C                     Z-ADDW1WHSE    $LWHSE                          GFG
     C                     MOVE W1SDEF    $LSDEF                          GFG
     C                     CALL 'SD900'                                   GFG
     C                     PARM           $LPARM                          GFG
     C           $LRTN     IFEQ '*SELECT '                                GFG
     C                     MOVEL$LSDEF    W1SDEF    P                     GFG
     C                     ENDIF                                          GFG
     C*
     CSR                   OTHER
     C*
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
210 AC*
210 ACSR                   CALL 'GETWHSE'
210 ACSR                   PARM           #USER
210 ACSR         W1WHSE    PARM 0         #WHSE   30
210 ACSR                   Z-ADDW1WHSE    WKWHSE  30
     C*
409 DC*R                   MOVE '*ALL'    W1NRPL                          GFG
409 MCSR                   MOVE '   0'    W1NRPL                          GFG
     CSR                   MOVE '*YES'    W1FRSL                          GFG
     CSR                   MOVE '*ALL'    W1AISL                          GFG
     CSR                   MOVE '*ALL'    W1SDEF                          GFG
     C*R                   MOVEL'*JOB'    W1DEV                           GFG
     C*R                   MOVEL@TYPE3    W1TYPE                          GFG
     CSR                   MOVE 'N'       W1JOBQ
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE #PROG     $LPRG
     C*
     C           KEYLH     KLIST                                          GFG
     C                     KFLD           W1WHSE                          GFG
     C                     KFLD           W1WHDP                          GFG
     C                     KFLD           LHAISL                          GFG
     C*                                                                   GFG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
     CSR                   MOVE W1WHSE    $WHSE
     CSR                   MOVE W1WHDP    $WHDP
     C*
     CSR                   MOVE W1NRPL    $NRPL                           GFG
     CSR                   MOVE W1FRSL    $FRSL                           GFG
     CSR                   MOVE W1AISL    $AISL                           GFG
     CSR                   MOVE W1SDEF    $SDEF                           GFG
     CSR                   MOVE W1TYPE    $TYPE                           GFG
     C*                                                                   GFG
     CSR                   MOVE W1JOBQ    $JOBQ
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
number
Print option
Job queue option
Printer device
Form type
Overflow slot option
Broken case option
