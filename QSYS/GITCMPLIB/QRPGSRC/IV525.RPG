     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV525     List Export Inventory Count
     F*  24 July, 2000
     F*  Roxanne Harrison
     F*
     F*----------------------------------------------------------------
     F*
     F*  Program Modification History
     F*  Revisions:
     F*
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FEXPCNT  IP  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FIV525PR O   E             90     PRINTER
     I*****************************************************************
     I**REC
     I**                                            ITWHDPL1
     I*****************************************************************
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     I*
     C*----------------------------------------------------------------
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE   30
     C                     Z-ADD$WHSE     $KWHSE  30
     C*                    MOVE $WHSE     $KWHSE  30
     C*----------------------------------------------------------------
     C*
     C*  Key fields
     C*
     C           ITKEY     KLIST
     C                     KFLD           $KWHSE
     C                     KFLD           CEITEM
     C*
     C*
     C           WHKEY     KLIST
     C                     KFLD           $KWHSE
     C*
     C*----------------------------------------------------------------
     C           CEWHSE    IFNE $KWHSE
     C                     GOTO OUT
     C                     ENDIF
     C*
     C*
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*
     C* Detail Calcs
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   ITDESC
     C                     ENDIF
     C*
     C                     WRITEDETAIL1
     C*
     C                     MOVE '0'       FIRST
     C*
     C           OUT       TAG
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   TIME           TIME
     CSR                   MOVE '1'       FIRST   1
     CSR                   Z-ADD0         COUNT   30
     CSR                   Z-ADD0         PRTLIN  30
     CSR                   ENDSR
     C*****************************************************************
     C*
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WOWHSE
     C           WHKEY     CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    WOWHSE
     C                     ELSE
     C                     MOVE *BLANKS   WOWHSE
     C                     ENDIF
     C*
     C                     MOVE *BLANKS   $PSTR
     C           WOWHSE    CAT  '/':0     WOWHSE
     C*          WOWHSE    CAT  SVDESC:0  WOWHSE
     C*          WOWHSE    CAT  '/':0     WOWHSE
     C*          WOWHSE    CAT  W1STYP:0  WOWHSE
     C*
     C                     MOVELWOWHSE    $PSTR  60
     C                     Z-ADD60        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WOWHSE
     C*
     C                     ENDSR
     C*****************************************************************
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR                   MOVE '0'       *IN90
     CSR                   WRITEHEAD1
     CSR                   WRITECOLUMNS
     C*
     CSR                   ENDSR
