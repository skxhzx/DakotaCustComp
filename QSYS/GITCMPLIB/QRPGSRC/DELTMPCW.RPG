     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  DELTMPCW  Delete Catch Weight Records from Work file.
     F*  12 April 2001
     F*  Max Blacknik
     F*
     F*  Revisions
     F*
415 AF*   04/12/01  MLB  4.15
     F*     -Created.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPOCWTMP UF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                    P  19  200$PWHSE
     I                                       21  29 $PPO
     I                                    P  30  320$PSEQ
     C*----------------------------------------------------------------
     C*
     C*  Input parameters
     C*
     C*      $PCMD   *DELETE  - Remove previously entered records.
     C*      $PPRG   Program to send messages back to.
     C*      $PUKY   Contains key for everything by *ADD.
     C*
     C*    Returned Parameters
     C*      $PUKY   Contains new batch number for *ADD.
     C*              Unchanged for everything else.
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured. Message in $PERM
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*   Remove any records entered previously for this item.
     C*
     C                     MOVE *OFF      ERROR
     C           KEYPCW    SETLLPOCWTMP
     C*
     C           FOREVR    DOWEQFOREVR
     C*
     C           KEYPCW    READEPOCWTMP                  79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE
     C*
     C                     OTHER
     C*
     C                     DELETPCWREC                 78
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       ERROR
     C                     ENDIF
     C                     ENDSL
     C*
     C                     ENDDO
     C*
     C                     SELEC
     C           ERROR     WHEQ *OFF
     C                     MOVE '*OK     '$PRTN
     C*
     C           ERROR     WHEQ *ON
     C                     MOVE '*ERROR  '$PRTN
     C*
     C                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     CSR                   MOVE *OFF      ERROR   1
     C*
     C*  Define partial key for POCWTMP file.
     C*
     CSR         KEYPCW    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PPO
     CSR                   KFLD           $PSEQ
     C*
     CSR                   ENDSR
