     F*----------------------------------------------------------------
     F*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  DD210     Create item tie/highs - Options
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
213 AF*    05/21/96  HNK  2.13
     F*      - Overhang option removed from screen.
     F*      - Options file added to get the overhang value.
     F*
404 AF*    07/23/97  DAS  4.04
     F*      - Added section type.
408 AF*    01/20/98  RH   4.08
     F*      - Added New definitions option, create from slot type,
     F*        and create from slot type item occupies options.
     F*
500 AF*    08/31/04  MLB  5.00
     F*      - Enh: Added aisle to selection criteria for creating
     F*        item tie/highs.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FDD210FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
213 AFOPTIONS IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
404 DE*                   DESC    1   3 60
408 DE*                   DESC    1   4 60
500 DE*                   DESC    1   6 60
500 ME                    DESC    1   7 60
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I*
     I                                       22  36 $LITEM
213 AI*----------------------------------------------------------------
213 AI*  *PATTERN  -  Tie pattern options.
213 AI*----------------------------------------------------------------
213 AI*
213 AI* Fields
213 AI*
213 AI*    OPHANG  -  Pallet overhang.
213 AI*    OPDINC  -  Dimension increment.
213 AI*
213 AI* Data structure
213 AI*
213 AIOPDATA      DS
213 AI                                        1   32OPHANG
213 AI                                        4   62OPDINC
213 AI                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE  155
     C                     PARM           $WHDP   5
404 AC                     PARM           $STYP   1
500 AC                     PARM           $AISL   4
     C                     PARM           $ITEM  15
     C                     PARM           $HANG  155
408 AC                     PARM           $NEWD   1
408 AC                     PARM           $TDEF   1
     C                     PARM           $CLRF   1
     C                     PARM           $PRTN   8
     C                     PARM           $JOBQ   1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE '1'       *IN97
     C                     MOVE '0'       *IN90
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       ERROR
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ '1'
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE '1'       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ '1'
     CSR                   MOVE '1'       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
500 ACSR                   SETOF                     3111
     CSR                   SETOF                     98
     CSR                   MOVE '0'       WARN    1
     CSR                   MOVE '0'       ERROR   1
     CSR                   MOVE '0'       CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ '1'
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR9903  Must select one option.
408 AC*
408 ACSR         ZM9903    BEGSR
408 ACSR                   MOVE 'PIR9903' #MSGID
408 ACSR                   MOVE '*DIAG  ' #MSGTP
408 ACSR                   MOVE *BLANKS   $MD
408 ACSR                   EXSR ZMPMSG
408 ACSR                   ENDSR
408 AC*----------------------------------------------------------------
408 AC*   PIR9904  Only select one option.
408 AC*
408 ACSR         ZM9904    BEGSR
408 ACSR                   MOVE 'PIR9904' #MSGID
408 ACSR                   MOVE '*DIAG  ' #MSGTP
408 ACSR                   MOVE *BLANKS   $MD
408 ACSR                   EXSR ZMPMSG
408 ACSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1006  Invalid jobq option for view report option.
     C*
     CSR         ZM1006    BEGSR
     CSR                   MOVE 'PIR1006' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify warehouse department.
     C*
     CSR         W1WHDP    IFNE '*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
404 AC*
404 AC*  Verify section type.
404 AC*
404 ACSR         W1STYP    IFNE 'P'
404 ACSR         W1STYP    ANDNE'F'
404 ACSR         W1STYP    ANDNE'B'
404 ACSR                   MOVE *ON       ERROR
404 ACSR                   MOVE *ON       *IN27
404 ACSR                   MOVE *ON       *IN13
404 ACSR                   MOVE *BLANKS   ERRMSG
404 ACSR                   MOVELDESC,4    ERRMSG
404 ACSR                   EXSR ZM1001
404 ACSR                   ENDIF
500 AC*
500 AC*  Verify aisle value.
500 AC*
500 ACSR         W1AISL    IFEQ ' '
500 ACSR                   MOVE *ON       ERROR
500 ACSR                   MOVE *ON       *IN31
500 ACSR                   MOVE *ON       *IN11
500 ACSR                   MOVE *BLANKS   ERRMSG
500 ACSR                   MOVELDESC,7    ERRMSG
500 ACSR                   EXSR ZM1001
500 ACSR                   ENDIF
     C*
     C*  Verify item
     C*
     CSR         W1ITEM    IFNE '*ALL    '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1ITEM    $LITEM
     CSR                   CALL 'IT900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
213 DC*
213 DC*  Verify overhang value
213 DC*
213 DC*R         W1HANG    IFLE 0
213 DC*R                   MOVE *ON       ERROR
213 DC*R                   MOVE *ON       *IN24
213 DC*R                   MOVE *ON       *IN04
213 DC*R                   MOVE *BLANKS   ERRMSG
213 DC*R                   MOVELDESC,1    ERRMSG
213 DC*R                   EXSR ZM1001
213 DC*R                   ENDIF
408 AC*
408 AC*  Verify New Definitions option.
408 AC*
408 ACSR         W1NEWD    IFNE 'Y'
408 ACSR         W1NEWD    ANDNE'N'
408 ACSR                   MOVE *ON       ERROR
408 ACSR                   MOVE *ON       *IN28
408 ACSR                   MOVE *ON       *IN08
408 ACSR                   MOVE *BLANKS   ERRMSG
408 ACSR                   MOVELDESC,5    ERRMSG
408 ACSR                   EXSR ZM1001
408 ACSR                   ENDIF
408 AC*
408 AC*  Verify Create from slot type option.
408 AC*
408 ACSR         W1TDEF    IFNE 'Y'
408 ACSR         W1TDEF    ANDNE'N'
408 ACSR                   MOVE *ON       ERROR
408 ACSR                   MOVE *ON       *IN29
408 ACSR                   MOVE *ON       *IN09
408 ACSR                   MOVE *BLANKS   ERRMSG
408 ACSR                   MOVELDESC,6    ERRMSG
408 ACSR                   EXSR ZM1001
408 ACSR                   ENDIF
408 AC*
408 AC*  Verify Create from Slot type item occupies option.
408 AC*
408 ACSR         W1SLOT    IFNE 'Y'
408 ACSR         W1SLOT    ANDNE'N'
408 ACSR                   MOVE *ON       ERROR
408 ACSR                   MOVE *ON       *IN30
408 ACSR                   MOVE *ON       *IN10
408 ACSR                   MOVE *BLANKS   ERRMSG
408 ACSR                   MOVELDESC,6    ERRMSG
408 ACSR                   EXSR ZM1001
408 ACSR                   ENDIF
408 AC*
408 AC*  Must select one option, create by slot type or slot occupied.
408 AC*
408 ACSR         W1TDEF    IFEQ 'N'
408 ACSR         W1SLOT    ANDEQ'N'
408 ACSR                   MOVE *ON       ERROR
408 ACSR                   MOVE *ON       *IN29
408 ACSR                   MOVE *ON       *IN30
408 ACSR                   MOVE *ON       *IN09
408 ACSR                   MOVE *BLANKS   ERRMSG
408 ACSR                   EXSR ZM9903
408 ACSR                   ENDIF
408 AC*
408 AC*  Select only one option, create by slot type or slot occupied.
408 AC*
408 ACSR         W1TDEF    IFEQ 'Y'
408 ACSR         W1SLOT    ANDEQ'Y'
408 ACSR                   MOVE *ON       ERROR
408 ACSR                   MOVE *ON       *IN29
408 ACSR                   MOVE *ON       *IN30
408 ACSR                   MOVE *ON       *IN09
408 ACSR                   MOVE *BLANKS   ERRMSG
408 ACSR                   EXSR ZM9904
408 ACSR                   ENDIF
     C*
     C*  Verify Clear file option
     C*
     CSR         W1CLRF    IFNE 'Y'
     CSR         W1CLRF    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN25
     CSR                   MOVE *ON       *IN05
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,2    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN26
     CSR                   MOVE *ON       *IN06
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     C*
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE '0'       CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     C*     Warehouse.
     C*
     CSR                   SELEC
     CSR         ROW#      WHEQ 08
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE #PROG     $LPRG
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
     C*     Warehouse department.
     C*
     CSR         ROW#      WHEQ 09
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   MOVE W1WHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVE $LWHDP    W1WHDP
     CSR                   ENDIF
     C*
     CSR                   OTHER
     C*
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     CSR                   CALL 'GETWHSE'
     CSR                   PARM           #USER
     CSR         W1WHSE    PARM 0         #WHSE   30
     CSR                   Z-ADDW1WHSE    WKWHSE  30
     C*
     C*
     CSR                   MOVEL'*ALL '   W1WHDP
     CSR                   MOVEL'*ALL '   W1ITEM
213 DC*R                   Z-ADD0         W1HANG
408 ACSR                   MOVE 'N'       W1NEWD
408 ACSR                   MOVE 'Y'       W1TDEF
408 ACSR                   MOVE 'N'       W1SLOT
     CSR                   MOVE 'N'       W1CLRF
     CSR                   MOVE 'N'       W1JOBQ
404 ACSR                   MOVE 'B'       W1STYP
500 ACSR                   MOVEL'*ALL'    W1AISL
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
213 AC*
213 AC*  Define key for options file.
213 AC*
213 ACSR                   Z-ADD0         $PWHSE
213 ACSR         OPTNKY    KLIST
213 ACSR                   KFLD           $PWHSE  30
213 ACSR                   KFLD           OPCODE
     C*
     CSR                   MOVE #PROG     $LPRG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
213 AC*
213 AC*   Get tie pattern options.
213 AC*
213 ACSR                   MOVE '*PATTERN'OPCODE
213 ACSR         OPTNKY    CHAINOPTIONS              79
213 ACSR         *IN79     IFEQ *ON
213 ACSR                   Z-ADD1         OPHANG
213 ACSR                   Z-ADD.5        OPDINC
213 ACSR                   ENDIF
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
     CSR                   Z-ADDW1WHSE    $WHSE
     CSR                   MOVE W1WHDP    $WHDP
404 ACSR                   MOVE W1STYP    $STYP
500 AC*
500 AC*    Right adjust aisle for processing.
500 ACSR         W1AISL    IFNE '*ALL'
500 ACSR                   MOVELW1AISL    $CSTR     P
500 ACSR                   CALL 'ADJUST'
500 ACSR                   PARM '*RIGHT  '$CCMD   8
500 ACSR                   PARM           $CSTR  30
500 ACSR                   MOVE $CSTR     $AISL
500 ACSR                   ELSE
500 ACSR                   MOVE W1AISL    $AISL
500 ACSR                   ENDIF
500 AC*
     CSR                   MOVE W1ITEM    $ITEM
213 MCSR                   Z-ADDOPHANG    $HANG
408 ACSR                   MOVE W1NEWD    $NEWD
     CSR                   MOVE W1CLRF    $CLRF
408 ACSR         W1TDEF    IFEQ 'Y'
408 ACSR                   MOVE W1TDEF    $TDEF
408 ACSR                   ELSE
408 ACSR                   MOVE 'N'       $TDEF
408 ACSR                   ENDIF
     C*
     CSR                   MOVE W1JOBQ    $JOBQ
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Overhang value
Clear file
Job queue option
Section
New definition option
Create from option
Aisle
