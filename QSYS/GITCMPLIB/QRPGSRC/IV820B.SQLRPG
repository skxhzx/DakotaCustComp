     F*----------------------------------------------------------------
     F*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV820B    Clear slots for aisle.
     F*  Roxanne Harrison
     F*  08 July 2004
     F*
     F*  REVISIONS:
417aAF*    07/08/04  RH   4.17a
     F*      - Created
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     I*----------------------------------------------------------------
     I*Q1REC    E DSSLOT                      1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE 155
     C                     PARM           $PWHDP  5
     C                     PARM           $PAISL  3
     C                     Z-ADD$PWHSE    W1WHSE  30
     C*  convert current date.
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    TODAY   80
     C*
     C*  Selected department.
     C*
     C*  Clear reg slots
     C/EXEC SQL
     C+ UPDATE SLOT SET SLSDTE = :TODAY, SLSTIM = 180000, SLSTK1 = 0,
     C+ SLSTK2 = 0, SLSTK3 = 0, SLALC1 = 0, SLALC2 = 0, SLALC3 = 0,
     C+ SLTFR1 = 0, SLTFR2 = 0, SLTFR3 = 0, SLPCK1 = 0, SLPCK2 = 0,
     C+ SLPCK3 = 0, SLRCV1 = 0, SLRCV2 = 0, SLRCV3 = 0, SLITEM = ' ',
     C+ SLENTD = 0, SLEXPD = 0, SLSTAT = 'V' WHERE (SLAISL = :$PAISL
     C+ AND SLAISL <> 'WIL' AND SLRSRV <> 'V' AND SLWHDP = :$PWHDP AND
     C+ SLWHSE = :W1WHSE)
     C/END-EXEC
     C*
     C*  Delete virtual slots
     C/EXEC SQL
     C+  DELETE
     C+  FROM   SLOT
     C+  WHERE  (SLWHSE = :W1WHSE  AND  SLWHDP = :$PWHDP AND
     C+         SLAISL = :$PAISL AND SLAISL <> 'WIL' AND SLRSRV = 'V')
     C/END-EXEC
     C*
     C/EXEC SQL
     C+  DELETE
     C+  FROM   VSLOT
     C+  WHERE  (VSWHSE = :W1WHSE  AND  VSWHDP = :$PWHDP AND
     C+         VSAISL = :$PAISL AND VSAISL <> 'WIL')
     C/END-EXEC
     C*
     C                     RETRN
