     F*----------------------------------------------------------------
     F*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  EX310   RF Will Call Transfer exception report.
     F*  18 July 2002
     F*  Max Blacknik
     F*
     F*  Revisions:
     F*
416 AF*    07/18/02  MLB  4.16
     F*      - Created.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FEX310   IP  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FEX310PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
     E                    DESC    1   3 50
     I*----------------------------------------------------------------
     IOQREC
     I                                              EXESTAL2
     I                                              EXWHDPL1
     I                                              EXSTYPL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C*
     C*  Format Dept/Section/Desig
     C*
     C           EXSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           EXSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Dept:'   CAT  EXWHDP:1  P#DESC
     C           P#DESC    CAT  'Type:':2 P#DESC
     C           P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Dept/Section
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     ENDIF
     C*
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Convert Generated date
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE EXGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    GENDTE  60
     C*
     C*  Retrieve item description.
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   ITDESC
     C                     ENDIF
     C*
     C*  Setup error description for printing.
     C*
     C                     SELEC
     C           EXERID    WHEQ 'WCTFRPK'
     C                     MOVELDESC,3    ERRMSG
     C                     OTHER
     C                     MOVELEXERID    ERRMSG    P
     C                     ENDSL
     C*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     CSR         ITKEY     KLIST
     CSR                   KFLD           EXWHSE
     CSR                   KFLD           EXITEM
     C*
     CSR         EXKEY     KLIST
     CSR                   KFLD           EXWHSE
     CSR                   KFLD           EXWHDP
     CSR                   KFLD           EXSTYP
     CSR                   KFLD           EXITEM
     CSR                   KFLD           EXGDTE
     CSR                   KFLD           EXGTIM
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'EX310PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     CSR                   MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     C*
     C*   Convert dates to MMDDYY format.
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $FROM     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    FMDT    60
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $TO       $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    TODT    60
     C*
     C*
     CSR                   MOVE #PROG     $CVPRG
     C*
     CSR                   Z-ADD3         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     CSR                   MOVE *BLANKS   WHSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR         EXWHSE    CHAINWAREHS               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELWHDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     WHSE
     C*
     C*  Format Dept/Section/Desig
     C*
     CSR         EXSTYP    IFEQ 'P'
     CSR                   MOVE 'PIR  '   TYPE    5
     CSR                   ELSE
     CSR         EXSTYP    IFEQ 'F'
     CSR                   MOVE 'Fixed'   TYPE
     CSR                   ELSE
     CSR                   MOVE '*None'   TYPE
     CSR                   END
     CSR                   END
     CSR                   MOVE *BLANKS   P#DESC
     CSR         'Dept:'   CAT  EXWHDP:1  P#DESC
     CSR         P#DESC    CAT  'Type:':2 P#DESC
     CSR         P#DESC    CAT  TYPE:1    P#DESC
     CSR                   MOVELP#DESC    $LSTR  60
     CSR                   Z-ADD40        $LLEN   30
     CSR                   CALL 'CENTER'
     CSR                   PARM           $LSTR
     CSR                   PARM           $LLEN
     CSR                   MOVEL$LSTR     P#DESC
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         *INL1     IFEQ '0'
     CSR                   MOVE '1'       *IN91
     CSR                   END
     C*
     CSR                   MOVE *OFF      *IN90
     CSR                   EXSR ZRFRMT
     C*
     CSR                   WRITEHEAD1
     CSR                   WRITEHEAD2
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRROLL    BEGSR
     C*
     CSR         *LIKE     DEFN L         M
     CSR         L         ADD  1         M
     C*
     CSR                   ADD  1         LVLK,M
     CSR                   ADD  RECK,L    RECK,M
     C*
     CSR                   ADD  CLS,L     CLS,M
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRTOTL    BEGSR
     C*
     CSR                   Z-ADDCLS,L     T#CLS
     CSR                   MOVE *BLANKS   T#DESC
     CSR         EXWHDP    CAT  '/':1     T#DESC
     CSR         T#DESC    CAT  TYPE:1    T#DESC
     C*
     CSR         L         IFEQ 1
     CSR                   WRITETOTAL1
     CSR                   ENDIF
     C*
     CSR         L         IFEQ 2
     CSR                   WRITETOTAL2
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRZERO    BEGSR
     C*
     CSR                   Z-ADD0         LVLK,L
     CSR                   Z-ADD0         RECK,L
     C*
     CSR                   Z-ADD0         CLS,L
     C*
     C*    Setup heading for either open/closed exceptions.
     CSR                   SELEC
     CSR         EXESTA    WHEQ '1 '                       Open excpt
     CSR                   MOVELDESC,1    P#STAT
     C*
     CSR         EXESTA    WHEQ '4 '                       Closed excpt
     CSR                   MOVELDESC,2    P#STAT
     CSR                   ENDSL
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
** Descriptions
            Open Exceptions
             Closed Exceptions
Itm not in slot
