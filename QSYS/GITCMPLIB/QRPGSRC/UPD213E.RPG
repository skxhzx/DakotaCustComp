     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  UPD213E   Initialize new PIRITEM field - Ver 2.13
     F*  Dave Sommeville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM UP  E           K        DISK
     FSLOT3   IF  E           K        DISK
     FPIRITMS IF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Initialize ITFLG1 to 'Y' if breakdown 1 has been defined.
     C*    Otherwise, initialize it to 'N'.
     C*
     C           ITFLG1    IFEQ ' '
     C           ITUM2     IFEQ ' '
     C                     MOVE 'N'       ITFLG1
     C                     ELSE
     C                     MOVE 'Y'       ITFLG1
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Initialize ITFLG2 to 'Y' if breakdown 2 has been defined.
     C*    Otherwise, initialize it to 'N'.
     C*
     C           ITFLG2    IFEQ ' '
     C           ITUM3     IFEQ ' '
     C                     MOVE 'N'       ITFLG2
     C                     ELSE
     C                     MOVE 'Y'       ITFLG2
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Initialize ITFLGD to 'Y' if any slots exist with expiration date.
     C*
     C           ITFLGD    IFEQ ' '
     C                     MOVE 'N'       ITFLGD
     C           KEYSL3    SETLLSLOT3
     C           FOREVR    DOUNEFOREVR
     C           KEYSL3    READESLOT3                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C           SLEXPD    IFNE 0
     C           SLSTAT    ANDNE'V'
     C           SLSTAT    ANDNE'Z'
     C                     MOVE 'Y'       ITFLGD
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C*  Initialize ITFLGB to 'N' if item is date sensitive.
     C*    Otherwise, initialize it to 'Y'.
     C*
     C           ITFLGB    IFEQ ' '
     C           ITFLGD    IFEQ 'Y'
     C                     MOVE 'N'       ITFLGB
     C                     ELSE
     C                     MOVE 'Y'       ITFLGB
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Get item shelf life from other file.
     C*
     C           KEYIS     SETLLPIRITMS
     C           FOREVR    DOUNEFOREVR
     C           KEYIS     READEPIRITMS                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     Z-ADDISSLIF    ITSLIF
     C                     Z-ADDISNDAY    ITNDAY
     C                     ENDDO
     C*
     C*  Update record.
     C*
     C                     UPDATITREC
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Define partial key for SLOT3 file.
     C*
     CSR         KEYSL3    KLIST
     CSR                   KFLD           ITWHSE
     CSR                   KFLD           ITITEM
     C*
     C*  Define partial key for PIRITMS file.
     C*
     CSR         KEYIS     KLIST
     CSR                   KFLD           ITWHSE
     CSR                   KFLD           ITITEM
     CSR                   ENDSR
