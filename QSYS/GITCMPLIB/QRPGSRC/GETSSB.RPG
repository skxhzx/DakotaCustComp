     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETSSB    Get Same Slot Breakdown item info for base item.
     F*  23 November 1999
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IF  E           K        DISK
     FITEMLNK1IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITEM   -  Base item
     I*
     I*  Output parameters
     I*
     I*     $PSITM   - SSB item number
     I*     $PDESC   - SSB description
     I*     $PPDSC   - SSB pack description
     I*     $PWHDP   - SSB department
     I*     $PSTYP   - SSB section
     I*     $PUM     - SSB unit of measure code
     I*     $PUM2    - SSB breakdown 1 unit of measure code
     I*     $PUMQ2   - SSB breakdown 1 quantity
     I*     $PUM3    - SSB breakdown 2 unit of measure code
     I*     $PUMQ3   - SSB breakdown 2 quantity
     I*     $PRTN    - *FOUND   -  SSB item found for base item
     I*                *NOTFND  -  SSB item not found for base item
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PSITM 15
     C                     PARM           $PDESC 30
     C                     PARM           $PPDSC 15
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUMQ2  30
     C                     PARM           $PUM3   2
     C                     PARM           $PUMQ3  30
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     CLEAR$PSITM
     C                     CLEAR$PDESC
     C                     CLEAR$PPDSC
     C                     CLEAR$PSTYP
     C                     CLEAR$PUM1
     C                     CLEAR$PUM2
     C                     CLEAR$PUMQ2
     C                     CLEAR$PUM3
     C                     CLEAR$PUMQ3
     C                     MOVE '*NOTFND '$PRTN
     C*
     C*    Loop through linked items for Base item.
     C*
     C           KEYIL1    SETLLITEMLNK1
     C           FOREVR    DOWEQFOREVR
     C           KEYIL1    READEITEMLNK1                 77
     C           *IN77     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*      Get item record.
     C*
     C           KEYIT     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     ITER
     C                     ENDIF
     C*
     C*      Looking for SSB item.
     C*
     C           ITTYPE    IFNE 'S'
     C                     ITER
     C                     ENDIF
     C*
     C*      Found SSB item.
     C*
     C                     MOVE '*FOUND  '$PRTN
     C                     MOVE ITITEM    $PSITM
     C                     MOVE ITDESC    $PDESC
     C                     MOVE ITPDSC    $PPDSC
     C                     MOVE ITWHDP    $PWHDP
     C                     MOVE ITSTYP    $PSTYP
     C                     MOVE ITUM1     $PUM1
     C                     MOVE ITUM2     $PUM2
     C                     Z-ADDITUMQ2    $PUMQ2
     C                     MOVE ITUM3     $PUM3
     C                     Z-ADDITUMQ3    $PUMQ3
     C                     LEAVE
     C*
     C                     ENDDO
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           ILWHSE
     CSR                   KFLD           ILITEM
     C*
     C*  Define key for ITEMLNK1 file.
     C*
     CSR         KEYIL1    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     C*
     CSR                   ENDSR
