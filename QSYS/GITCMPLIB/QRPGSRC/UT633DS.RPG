     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  UT633DS  RESET ORDER DETAIL RECORDS TO OPEN STATUS
     F*  06 AUGUST 2001
     F*  MAX BLACKNIL
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FORDH3   IF  E           K        DISK
     FORDD    UF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Redefinition data structures
     I*
     I$AERM       DS
     I                                        1  60 XAERM
     I                                        1   4 $AMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Hard code warehouse and route id.
     C*
     C                     Z-ADD900       KYWHSE
     C                     MOVE '01018'   KYRTE
     C*
     C*    RESET ORDER DETAIL FILE records
     C*
     C                     EXSR RESET
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Define key for ORDH3 file.
     C*
     CSR         *LIKE     DEFN OHWHSE    KYWHSE
     CSR         *LIKE     DEFN OHRTE     KYRTE
     C*
     CSR         KEYOH     KLIST
     CSR                   KFLD           KYWHSE
     CSR                   KFLD           KYRTE
     C*
     C*  Define key for ORDD  file.
     C*
     CSR         KEYOD     KLIST
     CSR                   KFLD           OHWHSE
     CSR                   KFLD           OHORD
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  RESET    RESET ORDER DETAIL RECORDS TO OPEN STATUS
     C*
     CSR         RESET     BEGSR
     C*
     C*  Loop through label records for route.
     C*
     CSR         KEYOH     SETLLORDH3
     CSR         FOREVR    DOUNEFOREVR
     CSR         KEYOH     READEORDH3                    78
     CSR         *IN78     IFEQ *ON
     CSR                   LEAVE
     CSR                   ENDIF
     C*
     C*  Loop through DETAIL records for ORDER.
     CSR         KEYOD     SETLLORDD
     CSR                   MOVE *OFF      *IN77
     CSR         FOREVR    DOUNEFOREVR
     CSR         KEYOD     READEORDD                     77
     CSR         *IN77     IFEQ *ON
     CSR                   LEAVE
     CSR                   ENDIF
     CSR                   MOVE 'O'       ODSTAT
     CSR                   UPDATODREC
     CSR                   ENDDO
     C*
     CSR                   ENDDO
     C*
     CSR                   ENDSR
