     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OI245     Import/pick will call orders - Post
     F*  27 July 1997
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
404 AF*    09/05/97  DAS  4.04
     F*      - Recompiled because file IMPORDD changed for cross dock.
409 AF*    03/20/98  RH   4.09
     F*      - Revised to import for entered order# only.
411 AF*    05/04/99  DAS  4.11
     F*      - Revised to send route id back to calling program.
     F*        Note: This program process a single order and therefore
     F*              a single route.
413 AF*    11/29/99  HNK  4.13
     F*      - Revised to handle cross docking. Copy of 4.04, 4.05,
     F*        4.06 and 4.07 from OI225
     F*----------------------------------------------------------------
     F*  Notes
     F*
404 AF*    This program has not been changed to look for will calls.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  61        - Special order (Customer # > 0)
     F*  78        - EOF for IMPORDD
     F*  79        - EOF for IMPORDH
     F*  91        - Overflow indicator
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
409 DF*MPORDH1UF  E           K        DISK
409 MFIMPORDH2UF  E           K        DISK
     FIMPORDD UF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FRTESUM  UF  E           K        DISK                      A
413 AFSLOT1   UF  E           K        DISK                      A
413 AFSLOT4   UF  E           K        DISK
413 AF            SLREC                             KRENAMESLREC4
413 AFPODTL1  IF  E           K        DISK
     FRTEHED  O   E           K        DISK                      A
     FORDH    O   E           K        DISK                      A
     FORDD    O   E           K        DISK                      A
413 AFCROSSDK O   E           K        DISK                      A
     FOI245PR O   E             91     PRINTER
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
413 DE*                   DESC    1   3 20
413 ME                    DESC    1   6 20
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHS   Warehouse to process.
     C*      $PTYPE  Order types to process.
     C*              ' ' - Normal
     C*              'T' - Transfer
     C*              'W' - Will Call
     C*      $PTRUK  Truck code.
     C*
     C*    Returned Parameters
411 AC*      $PRTID  Route id assigned to will call order.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHS  155
     C                     PARM           $PTYPE  1
     C                     PARM           $PTRUK 10
409 AC                     PARM           $PIORD  70
411 AC                     PARM           $PRTID  5
     C*----------------------------------------------------------------
     C                     Z-ADD$PWHS     $PWHSE  30
413 AC*
413 AC*  Convert today's date into century format.
413 AC*
413 AC                     MOVE '*CURCMD '$CVCMD
413 AC                     CALL @CVTDT
413 AC                     PARM           $CVTDT
413 AC                     Z-ADD$CVD8O    TODAY   80
413 AC*
413 AC*  Variables and constants.
413 AC*
413 AC                     MOVE *OFF      FOREVR  1
     C* Key definitions
     C*
     C* Download order header file - Partial key
     C*
     C           OIHKEY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           $PTYPE
409 AC                     KFLD           $PIORD
     C*
     C* Download order detail file - Partial key
     C*
     C           OIDKEY    KLIST
     C                     KFLD           OIHWHS
     C                     KFLD           OIHORD
     C*
     C* Item file
     C*
     C           ITKEY     KLIST
     C                     KFLD           OIDWHS
     C                     KFLD           OIDITM
     C*
     C* Route summary file
     C*
     C           KEYRS     KLIST
     C                     KFLD           RHWHSE
     C                     KFLD           RHRTID
     C                     KFLD           ITWHDP
413 AC*
413 AC* PO DETAIL file
413 AC*
413 AC           KEYPD1    KLIST
413 AC                     KFLD           $PWHSE
413 AC                     KFLD           OIDPO
413 AC                     KFLD           OIDITM
413 AC*
413 AC* SLOT1 file
413 AC*
413 AC           *LIKE     DEFN SLAISL    KYAISL
413 AC           KEYSL1    KLIST
413 AC                     KFLD           $PWHSE
413 AC                     KFLD           ITWHDP
413 AC                     KFLD           KYAISL
413 AC* SLOT4 file
413 AC*
413 AC           *LIKE     DEFN SLDISP    KYDISP
413 AC           KEYSL4    KLIST
413 AC                     KFLD           $PWHSE
413 AC                     KFLD           ITWHDP
413 AC                     KFLD           OIDITM
413 AC                     KFLD           KYDISP
     C*----------------------------------------------------------------
     C* Main line
     C*
     C* Get import date/time.
     C*
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    IMPDTE  80
     C                     TIME           IMPTIM  60
     C*
     C* Get report heading.
     C*
     C                     MOVE *BLANKS   W1HEAD
     C                     MOVEL'OI245PR' RPPRTF
     C           RPPRTF    CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD50        $PLEN
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     W1HEAD
     C*
     C*   Get warehouse description.
     C*
     C           $PWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   WHDESC
     C                     END
     C*
     C*   Create warehouse heading.
     C*
     C                     MOVE $PWHSE    WORK3A  3
     C           ' 0'      CHECKWORK3A    POS     10
     C           4         SUB  POS       LEN     10
     C           LEN       SUBSTWORK3A:POSWHHEAD
     C           WHHEAD    CAT  '-':1     WHHEAD
     C           WHHEAD    CAT  WHDESC:1  WHHEAD
     C                     CALL 'CENTER'
     C                     PARM WHHEAD    $CSTR  60
     C                     PARM 40        $CLEN   30
     C                     MOVEL$CSTR     WHHEAD
     C*
     C* Initialize grand total fields
     C*
     C                     Z-ADD0         GRDORD
     C                     Z-ADD0         GRDLIN
     C                     Z-ADD0         PREOH
     C                     Z-ADD0         PREOD
     C                     Z-ADD0         NOITEM
     C                     Z-ADD0         BADBRK
     C*
     C* Whip through all the download header records
     C*
     C           *LIKE     DEFN OIHRTE    LSTRTE
     C                     MOVE '@@@@@'   LSTRTE
     C                     MOVE *OFF      PFLAG   1
     C                     MOVE *OFF      EOFH    1
     C                     Z-ADD55        LINPPG  30       MAX LINES PER PAGE
     C                     Z-ADD0         COUNT   30       LINE COUNTER
409 DC*          OIHKEY    SETLLIMPORDH1
409 MC           OIHKEY    SETLLIMPORDH2
     C           EOFH      DOUEQ*ON
     C*
409 DC*          OIHKEY    READEIMPORDH1                 79
409 AC           OIHKEY    READEIMPORDH2                 79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       EOFH
     C                     GOTO ENDDOH
     C                     ENDIF
     C*
     C*  Print report heading if necessary.
     C*
     C           PFLAG     IFEQ *OFF
     C           COUNT     ORGE LINPPG
     C                     Z-ADD0         COUNT
     C                     WRITEHEAD1
     C                     ADD  3         COUNT
     C                     MOVE *OFF      PFLAG
     C                     ENDIF
     C*
     C*  Add record to route header file when route changes.
     C*
     C           OIHRTE    IFNE LSTRTE
     C                     EXSR FILLRH
     C                     WRITERHREC                  49
     C                     MOVE OIHRTE    LSTRTE
411 AC                     MOVE RHRTID    $PRTID
     C                     ENDIF
     C*
     C*  Add record to order header file. If an error occurs, assume
     C*    it was caused by a duplicate record.
     C*
     C                     MOVE *BLANKS   HEDMSG
     C                     EXSR FILLOH
     C                     WRITEOHREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    HEDMSG
     C                     ADD  1         PREOH
     C                     ENDIF
     C*
     C*    Print order heading
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD2
     C                     ADD  2         COUNT
     C                     ENDIF
     C*
     C                     WRITEHEAD3
     C                     ADD  2         COUNT
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD4
     C                     ADD  2         COUNT
     C                     MOVE *ON       PFLAG
     C                     ENDIF
     C*
     C*    Initialize order subtotals
     C*
     C                     Z-ADD0         SUBLIN  30
     C                     Z-ADD0         SUBQ1   70
     C                     Z-ADD0         SUBQ2   70
     C                     Z-ADD0         SUBQ3   70
     C*
     C*    Whip through all the download detail records
     C*
     C                     MOVE *OFF      EOFD    1
     C           OIDKEY    SETLLIMPORDD
     C           EOFD      DOUEQ*ON
     C*
     C           OIDKEY    READEIMPORDD                  78
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       EOFD
     C                     GOTO ENDDOD
     C                     ENDIF
     C                     MOVE *BLANKS   DETMSG
     C                     MOVE 'N'       GOOD1
413 AC                     MOVE 'N'       GOOD2
     C*
     C*       If item doesn't exist write error message.
     C*
     C           ITKEY     CHAINPIRITEM              76
     C           *IN76     IFEQ *ON
     C                     MOVE DESC,2    DETMSG
     C                     ADD  1         NOITEM
     C                     MOVE *BLANKS   ITDESC
     C                     MOVEL'*****'   ITDESC
     C                     ELSE
     C*       Check if breakdown qty is allowed using piritem file.
     C*       Add rec if qty1, brk qty2 or brk qty3 has good qty.
     C                     EXSR ZZBKDN
     C*
     C*       If qty1, brkdown qty2 or brkdown qty3 has good qty,
     C*        add record to order detail file. If an error
     C*        occurs, assume was caused by a duplicate record.
     C*        Update route summary with detail information.
     C*
     C           GOOD1     IFEQ 'Y'
413 AC                     EXSR ZZXDCK
413 AC           GOOD2     IFEQ 'Y'
     C                     EXSR FILLOD
     C                     WRITEODREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    DETMSG
     C                     ADD  1         PREOD
     C                     ENDIF
     C                     EXSR UPDSUM
413 AC                     ENDIF
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C*       Print order detail and update subtotals
     C*
     C                     EXSR ZRHEAD
     C                     WRITEDETAIL
     C                     ADD  1         COUNT
     C                     ADD  1         SUBLIN
     C                     ADD  OIDQ1     SUBQ1
     C                     ADD  OIDQ2     SUBQ2
     C                     ADD  OIDQ3     SUBQ3
     C*
     C*       Delete detail record from download file
     C*
     C                     DELETOIDREC
     C*
     C           ENDDOD    TAG
     C                     ENDDO
     C*
     C*    Print order subtotals and update grand totals
     C*
     C**                   EXSR ZRHEAD
     C                     WRITESUBTTL
     C                     ADD  3         COUNT
     C                     ADD  1         GRDORD
     C                     ADD  SUBLIN    GRDLIN
     C*
     C*  Delete header record from download file
     C*
     C                     DELETOIHREC
     C*
     C           ENDDOH    TAG
     C                     ENDDO
     C*
     C*  Print order grand totals
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     IFGE LINPPG
     C                     WRITEHEAD1
     C                     ENDIF
     C                     WRITEGRNDTTL
     C                     ENDIF
     C*
     C*  We are finished so get out
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  FILLOD  Fill order detail fields.
     C*
     CSR         FILLOD    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEARODREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIDWHS    ODWHSE
     CSR                   Z-ADDOHORD     ODORD
     CSR                   Z-ADDOIDSEQ    ODSEQ
     CSR                   MOVE OIDITM    ODITEM
     CSR                   Z-ADDOIDQ1     ODQOR1
     CSR         ITFLG1    IFEQ 'Y'
     CSR                   Z-ADDOIDQ2     ODQOR2
     CSR                   ELSE
     CSR                   Z-ADD0         ODQOR2
     CSR                   ENDIF
     CSR         ITFLG2    IFEQ 'Y'
     CSR                   Z-ADDOIDQ3     ODQOR3
     CSR                   ELSE
     CSR                   Z-ADD0         ODQOR3
     CSR                   ENDIF
     CSR                   Z-ADDOIDWH2    ODWH2
     CSR                   Z-ADDOIDOR2    ODOR2
     CSR                   MOVE OIDOIT    ODOITM
     CSR                   MOVE OIDSPC    ODSPCL
     CSR                   MOVE 'O'       ODSTAT
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLOH   Fill order header fields.
     C*
     CSR         FILLOH    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEAROHREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIHWHS    OHWHSE
     CSR                   CALL 'PIRORD#'
     CSR                   PARM OHWHSE    $OWHSE  30
     CSR         OHORD     PARM 0         $OORD   70
     CSR                   MOVE OIHTYP    OHTYPE
     CSR                   Z-ADDOIHORD    OHCORD
     CSR                   MOVE OIHCPO    OHCPO
     CSR                   Z-ADDOIHCUS    OHCUST
     CSR                   MOVE OIHCNM    OHCNAM
     CSR                   MOVE OIHCA1    OHCAD1
     CSR                   MOVE OIHCA2    OHCAD2
     CSR                   MOVE OIHCCT    OHCCTY
     CSR                   MOVE OIHCST    OHCST
     CSR                   MOVE OIHCZP    OHCZIP
     CSR                   MOVE RHRTID    OHRTE
     CSR                   Z-ADDOIHSTP    OHSTOP
     C**                   MOVE OIHTRK    OHTRUK
     CSR                   MOVE $PTRUK    OHTRUK
     CSR                   MOVE OIHMSC    OHMISC
     CSR                   MOVE 'O'       OHSTAT
     CSR                   Z-ADDIMPDTE    OHSDTE
     CSR                   Z-ADDIMPTIM    OHSTIM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLRH   Fill route header fields.
     C*
     CSR         FILLRH    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEARRHREC
     C*
     C*  Update fields.
     C*
     CSR                   Z-ADDOIHWHS    RHWHSE
     CSR                   CALL 'PIRRTE#'
     CSR                   PARM RHWHSE    $RWHSE  30
     CSR                   PARM ' '       RHRTID
     CSR                   MOVE OIHRTE    RHRTE
     CSR                   MOVE OIHTYP    RHTYPE
     CSR                   MOVE OIHTRK    RHTRUK
     CSR                   MOVE OIHTRK    $PTRUK
     CSR                   MOVE '1'       RHSTAT
     CSR                   Z-ADDIMPDTE    RHSTSD
     CSR                   Z-ADDIMPTIM    RHSTST
     CSR                   Z-ADDIMPDTE    RHIMPD
     CSR                   Z-ADDIMPTIM    RHIMPT
     CSR                   Z-ADDIMPDTE    RHRPTD
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  UPDSUM   Update route summary file with detail record.
     C*
     CSR         UPDSUM    BEGSR
     C*
     C*  Define work fields
     C*
     CSR         *LIKE     DEFN ITCUBE    TMPCUB
     CSR         *LIKE     DEFN ITSWGT    TMPWGT
     C*
     C*  Try to get existing summary records for department.
     C*
     CSR         KEYRS     CHAINRTESUM               77
     C*
     C*     Record does not exist - Initialize fields.
     C*
     CSR         *IN77     IFEQ *ON
     CSR                   Z-ADDRHWHSE    RSWHSE
     CSR                   MOVE RHRTID    RSRTID
     CSR                   MOVE ITWHDP    RSWHDP
     CSR                   Z-ADD0         RSORDC
     CSR                   Z-ADD0         RSORDW
     CSR                   Z-ADD0         RSORDP
     CSR                   Z-ADD0         RSPCKC
     CSR                   Z-ADD0         RSPCKW
     CSR                   Z-ADD0         RSPCKP
     CSR                   ENDIF
     C*
     C*     Update department totals.
     C*
     CSR                   ADD  ODQOR1    RSORDP
     CSR         ITCUBE    MULT ODQOR1    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
     CSR         ITSWGT    MULT ODQOR1    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     C*
     CSR                   ADD  ODQOR2    RSORDP
     CSR         ODQOR2    IFNE 0
     CSR         ITUMQ2    ANDGT0
     CSR         ITCUBE    DIV  ITUMQ2    TMPCUB    H
     CSR         TMPCUB    MULT ODQOR2    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
     CSR         ITSWGT    DIV  ITUMQ2    TMPWGT    H
     CSR         TMPWGT    MULT ODQOR2    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     CSR                   ENDIF
     C*
     CSR                   ADD  ODQOR3    RSORDP
     CSR         ODQOR3    IFNE 0
     CSR         ITUMQ3    ANDGT0
     CSR         ITCUBE    DIV  ITUMQ3    TMPCUB    H
     CSR         TMPCUB    MULT ODQOR3    TMPCUB
     CSR                   ADD  TMPCUB    RSORDC
     CSR         ITSWGT    DIV  ITUMQ3    TMPWGT    H
     CSR         TMPWGT    MULT ODQOR3    TMPWGT
     CSR                   ADD  TMPWGT    RSORDW
     CSR                   ENDIF
     C*
     C*     Add/Update summary record.
     C*
     CSR         *IN77     IFEQ *ON
     CSR                   WRITERSREC
     CSR                   ELSE
     CSR                   UPDATRSREC
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZBKDN  Check breakdown quanties.
     C*
     CSR         ZZBKDN    BEGSR
     CSR         OIDQ2     IFNE 0
     CSR         ITFLG1    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   ENDIF
     C*
     CSR         OIDQ3     IFNE 0
     CSR         ITFLG2    ANDNE'Y'
     CSR                   MOVE DESC,3    DETMSG
     CSR                   ADD  1         BADBRK
     CSR                   ENDIF
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR         OIDQ1     IFNE 0
     CSR                   MOVE 'Y'       GOOD1   1
     CSR                   ELSE
     CSR         OIDQ2     IFNE 0
     CSR         ITFLG1    ANDEQ'Y'
     CSR                   MOVE 'Y'       GOOD1
     CSR                   ELSE
     CSR         OIDQ3     IFNE 0
     CSR         ITFLG2    ANDEQ'Y'
     CSR                   MOVE 'Y'       GOOD1
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
413 AC*
413 AC*  ZZXDCK  Attach cross dock item to PO detail line.
413 AC*
413 ACSR         ZZXDCK    BEGSR
     C* Only work with cross dock items.
     CSR         OIDSPC    IFNE 'XD'
     CSR                   MOVE 'Y'       GOOD2
     CSR                   GOTO ENDXD
     CSR                   ENDIF
     C* Loop through P.O. detail records looking for item.
     CSR         KEYPD1    SETLLPODTL1
     CSR         FOREVR    DOWEQFOREVR
     CSR         KEYPD1    READEPODTL1                   79
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADD0         PDSEQ
     CSR                   MOVE OIDPO     PDPO
     CSR                   ENDIF
     C* If cross dock slot recrod exists, update
     CSR                   MOVEL'XDK'     KYDISP
     CSR         KEYSL4    SETLLSLOT4
     CSR         FOREVR    DOWEQFOREVR                                     DO2
     CSR                   READ SLOT4                    79
     CSR         *IN79     IFEQ *OFF
     CSR         SLSTAT    ANDEQ'XD'
     CSR         SLWHSE    ANDEQ$PWHSE
     CSR         SLWHDP    ANDEQITWHDP
     CSR         SLITEM    ANDEQOIDITM
     CSR         SLAISL    ANDEQ'XDK'
     C* Pre-allocate product in case receiving is done before picking.
     CSR                   ADD  OIDQ1     SLALC1
     CSR         ITFLG1    IFEQ 'Y'
     CSR                   ADD  OIDQ2     SLALC2
     CSR                   ENDIF
     CSR         ITFLG2    IFEQ 'Y'
     CSR                   ADD  OIDQ3     SLALC3
     CSR                   ENDIF
     CSR                   UPDATSLREC4
     CSR                   GOTO ADDXDK
     CSR                   ELSE
     CSR                   UNLCKSLOT4
     CSR                   LEAVE
     CSR                   ENDIF
     CSR                   ENDDO                                          END2
     C* Create cross dock slot record.
     CSR                   Z-ADD1         TMPLOC  30
     CSR                   MOVE 'XDK'     KYAISL
     CSR         KEYSL1    SETLLSLOT1
     CSR         FOREVR    DOWEQFOREVR
     CSR         KEYSL1    READESLOT1               N    79
     CSR         *IN79     IFEQ *ON
     CSR         SLLOC     ORGT TMPLOC
     CSR                   LEAVE
     CSR                   ENDIF
     CSR         SLLOC     ADD  1         TMPLOC
     CSR                   ENDDO
     C*
     CSR                   CLEARSLREC
     CSR                   Z-ADDOIDWHS    SLWHSE
     CSR                   MOVE ITWHDP    SLWHDP
     CSR                   MOVE 'XDK'     SLAISL
     CSR                   Z-ADDTMPLOC    SLLOC
     CSR                   Z-ADD1         SLRLVL
     CSR                   MOVE *BLANKS   SLHAND
     CSR                   MOVE 'X'       SLSTYP
     CSR                   Z-ADDTMPLOC    SLPSEQ
     CSR                   MOVE OIDITM    SLITEM
     CSR                   MOVE OIDSPC    SLSTAT
     CSR                   Z-ADDTODAY     SLSDTE
     CSR                   TIME           SLSTIM
     CSR                   MOVE 'N'       SLPICK
     CSR                   MOVE 'N'       SLRSRV
     C* Format display slot.
     CSR                   CALL 'FRMTSL'
     CSR                   PARM           SLWHSE
     CSR                   PARM           SLWHDP
     CSR                   PARM           SLAISL
     CSR                   PARM           SLLOC
     CSR                   PARM           SLRLVL
     CSR                   PARM           SLHAND
     CSR                   PARM           SLSTYP
     CSR                   PARM           SLPSEQ
     CSR                   PARM           SLDISP
     C* Pre-allocate product in case receiving is done before picking.
     CSR                   Z-ADDOIDQ1     SLALC1
     CSR         ITFLG1    IFEQ 'Y'
     CSR                   Z-ADDOIDQ2     SLALC2
     CSR                   ENDIF
     CSR         ITFLG2    IFEQ 'Y'
     CSR                   Z-ADDOIDQ3     SLALC3
     CSR                   ENDIF
     C* Add record
     CSR         SLLOC     DIV  2         DUMMY   30
     CSR                   MVR            SLSIDE
     CSR         SLSIDE    IFLT 0
     CSR                   Z-SUBSLSIDE    SLSIDE
     CSR                   ENDIF
     CSR                   WRITESLREC                  77
     C* Error if slot not created.
     CSR         *IN77     IFEQ *ON
     CSR                   MOVE DESC,5    DETMSG
     CSR                   ADD  1         BADPO
     CSR                   LEAVE
     CSR                   ENDIF
     C* Create cross dock record.
     CSR         ADDXDK    TAG
     CSR                   Z-ADDSLWHSE    CDWHSE
     CSR                   MOVE SLWHDP    CDWHDP
     CSR                   MOVE SLDISP    CDSLOT
     CSR                   Z-ADDOHORD     CDORD
     CSR                   Z-ADDOIDSEQ    CDORSQ
     CSR                   Z-ADD0         CDORT#
     CSR                   Z-ADD0         CDORL#
     CSR                   MOVE PDPO      CDPO
     CSR                   Z-ADDPDSEQ     CDPOSQ
     CSR                   Z-ADD0         CDPOT#
     CSR                   Z-ADD0         CDPOL#
     CSR                   WRITECDREC                  79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE DESC,6    DETMSG
     CSR                   ADD  1         BADPO
     CSR                   ELSE
     CSR                   MOVE 'Y'       GOOD2   1
     CSR                   ENDIF
     CSR                   LEAVE
     CSR                   ENDDO
     C*
413 ACSR         ENDXD     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZRHEAD   Print report headings.
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         COUNT     IFGE LINPPG
     CSR                   SETON                     81
     CSR                   Z-ADD0         COUNT
     CSR                   WRITEHEAD1
     CSR                   ADD  3         COUNT
     CSR                   WRITEHEAD2
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD3
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD4
     CSR                   ADD  2         COUNT
     CSR                   SETOF                     81
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  COMPILE TIME TABLES
     O*
**
*EXIST*
*INVALID ITEM*
*BREAKDOWN INVALID*
*INVALID X-DOCK PO*
*XDK SLOT ERROR*
*NO XDK REC CREATED*
