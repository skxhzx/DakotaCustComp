     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  CHKSSB    Check for existance of corresponding SSB item.
     F*  05 July 1999
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IF  E           K        DISK
     FITEMLNK1IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITEM   -  Base item
     I*
     I*  Output parameters
     I*
     I*     $PEXST   - SSB Exists (Y,N)
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PEXST  1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     MOVE 'N'       $PEXST
     C*
     C*    Loop through Contract items for Base item.
     C*
     C           KEYIL1    SETLLITEMLNK1
     C           FOREVR    DOWEQFOREVR
     C           KEYIL1    READEITEMLNK1                 77
     C           *IN77     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*      Get item record.
     C*
     C           KEYIT     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     ITER
     C                     ENDIF
     C*
     C*      Only work with Contract items.
     C*
     C           ITTYPE    IFNE 'S'
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVE 'Y'       $PEXST
     C                     LEAVE
     C*
     C                     ENDDO
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           ILWHSE
     CSR                   KFLD           ILITEM
     C*
     C*  Define key for ITEMLNK1 file.
     C*
     CSR         KEYIL1    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     C*
     CSR                   ENDSR
