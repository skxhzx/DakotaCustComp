     F*----------------------------------------------------------------
     F*
     F*  SZ815    Auto create stage zone defs
     F*  22 June 2000
     F*  Dave Sommerville
     F*
     F*  Note:  The CL program preceding this program selects records
     F*         from the SLOT file and groups the records by
     F*         Whse/Aisle/Dept/Stype.
     F*
     F*  Revisions
     F*
     F*    06/22/00  DAS  4.14
     F*      - Created
     F*
     F*----------------------------------------------------------------
     FSZ815   IP  E           K        DISK
     FSTAGH   O   E           K        DISK                      A
     FSTAGD   O   E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  30 ERFLD
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I              'Zone for aisle'      C         @DESC1
     I*----------------------------------------------------------------
     I            DS
     I                                        1  10 DSTRUK
     I                                        1   8 DSTRK
     I                                        1   5 DSTR
     I                                        6   8 DSWHT
     I                                        9  10 DSDGT
     I*
     I                                        1   7 DSWIL
     I                                        1   4 DSWL
     I                                        5   7 DSWHW
     I                                        8   9 DSDGW
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $WHSE   Warehouse number
     C*
     C*    Returned Parameters
     C*      $PRTN   Return code
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE  155
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C* Create zone header record for each unique aisle.
     C*
     C           SLWHSE    IFNE LSTWHS
     C           SLAISL    ORNE LSTASL
     C                     EXSR CRTZH
     C                     Z-ADDSLWHSE    LSTWHS
     C                     MOVE SLAISL    LSTASL
     C                     ENDIF
     C*
     C* Create zone detail.
     C*
     C                     EXSR CRTZD
     C*
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE '0'       ERROR   1
     CSR                   Z-ADD0         SHZSEQ
     CSR         *LIKE     DEFN SLWHSE    LSTWHS
     CSR         *LIKE     DEFN SLAISL    LSTASL
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  CRTZH    Create STAGH record.
     C*
     CSR         CRTZH     BEGSR
     C*
     C*  Initialize zone fields.
     C*
     C*    Zone code will be left justified aisle.
     C*
     CSR                   Z-ADDSLWHSE    SHWHSE
     CSR                   ADD  10        SHZSEQ
     C*
     CSR         ' '       CHECKSLAISL    P       30
     CSR         P         IFEQ 0
     CSR                   MOVELSLAISL    SHZONE    P
     CSR                   ELSE
     CSR         4         SUB  P         LEN     30
     CSR         LEN       SUBSTSLAISL:P  SHZONE    P
     CSR                   ENDIF
     C*
     CSR                   MOVEL@DESC1    SHDESC    P
     CSR         SHDESC    CAT  SHZONE:1  SHDESC
     C*
     C*   Create record.
     C*
     CSR                   WRITESHREC                  78
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  CRTZD    Create STAGD record.
     C*
     CSR         CRTZD     BEGSR
     CSR                   Z-ADDSHWHSE    SDWHSE
     CSR                   MOVE SHZONE    SDZONE
     CSR                   MOVE SLWHDP    SDWHDP
     CSR                   MOVE SLSTYP    SDSTYP
     CSR                   MOVE SLAISL    SDAISL
     CSR                   Z-ADD0         SDLOC1
     CSR                   Z-ADD0         SDLOC2
     C*
     CSR                   WRITESDREC                  78
     CSR                   ENDSR
