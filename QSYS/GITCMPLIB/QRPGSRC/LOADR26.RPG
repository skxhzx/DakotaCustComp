     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  LOADR26: Route Comment History - View/Print - Options
     F*          Loader Module
     F*
     F*  September 4, 2007
     F*  Jose Duque
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FLOAD26FMCF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   9 60
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   6 T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I*
     I                                       22  23 $LCODE
     I                                       77 111 $LCDSC
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1   60ERDATE
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE   3
     C                     PARM           $FDATE  8
     C                     PARM           $TDATE  8
     C                     PARM           $FDTE6  6
     C                     PARM           $TDTE6  6
     C                     PARM           $PRTN   8
     C                     PARM           $VP     1
     C                     PARM           $JOBQ   1
     C                     PARM           $DEV   10
     C                     PARM           $FORM  10
     C                     PARM           $RTE#   5
     C                     PARM           $CTYPE  2
     C                     PARM           $SORT   1
     C                     PARM           $RCODE  2
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE '1'       *IN97
     C                     MOVE '0'       *IN90
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       ERROR
     C                     MOVE #PROG     $LPRG
     C                     MOVE #PROG     $CVPRG
     C                     SETON                     02
      *
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ '1'
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C           NXTSCR    CASEQ'02 '     SC2
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE '1'       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ '1'
     CSR                   MOVE '1'       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     CSR         NXTSCR    IFEQ '02 '
     CSR                   EXFMTWINDW1                 50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE '0'       WARN    1
     CSR                   MOVE '0'       ERROR   1
     CSR                   MOVE '0'       CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   SETON                     02
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*  SC1  -  Screen 1
     C*----------------------------------------------------------------
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ '1'
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     C*>         CMDTKN    CABEQ*ON       ENDSC1
      *
     C           CMDTKN    IFEQ *ON
     C           ROW#      ANDEQ12
     C                     EXSR ZZCHK1
     C           ERROR     IFEQ '1'
     C                     MOVE '01 '     NXTSCR
     C                     GOTO ENDSC1
     C                     ELSE
     C                     GOTO ENDSC1
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*
     C*----------------------------------------------------------------
     C*  SC2  -  Windw 1
     C*----------------------------------------------------------------
     CSR         SC2       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC2
     CSR                   END
     C*
     C*> Test for F5 - Refresh
     C*
     C*>         *INKE     IFEQ '1'
     C*>                   EXSR SCR01I
     C*>                   GOTO ENDSC2
     C*>                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE '01 '     NXTSCR
     CSR                   GOTO ENDSC2
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     C*>                   EXSR ZZCMD1
     C*>         CMDTKN    CABEQ*ON       ENDSC2
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     C                     EXSR ZZCHK2
     C           ERROR     CABEQ'1'       ENDSC2
      *
      *
     C           $LRTN     IFEQ '*CANCEL '
     C                     MOVE '02 '     NXTSCR
     C                     GOTO ENDSC2
     C                     ENDIF
      *
     C           $LRTN     IFEQ '*EXIT   '
     C                     MOVE '01 '     NXTSCR
     C                     GOTO ENDSC2
     C                     ENDIF
      *
     C                     EXSR ZZNXT1
     C                     MOVE '*OK     '$PRTN
     C                     MOVE 'EOJ'     NXTSCR
      *
     C           ENDSC2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C                     ENDSR
     C*
     C*---------------------------------------------------------------
     C* Convert 'Gregorian' date (mmddyy) to 'Julian' date
     C* (Partially copied from Pgm:ER211)
     C*---------------------------------------------------------------
     C           GTOJ$     BEGSR
     C*---------------------------------------------------------------
     C* Split date into Month, Day, Year fields
     C*
     C                     MOVELG$DATE    G$MM    20
     C                     MOVE G$DATE    WORK4   4
     C                     MOVELWORK4     G$DD    20
     C                     MOVE WORK4     G$YY    20
     C* Create 4 Digit Year
     C                     MOVE G$YY      G$YYYY  40
     C           G$YY      IFGE 60
     C                     MOVEL'19'      G$YYYY
     C                     ELSE
     C                     MOVEL'20'      G$YYYY
     C                     END
     C                     Z-ADD0         G$JD
     C           G$MM      SUB  3         G$MMWK  20
     C                     Z-ADDG$MMWK    GMWKSV  20
     C           G$MMWK    IFLT 0
     C                     ADD  12        G$MMWK
     C                     END
     C           GMWKSV    IFNE 0
     C           G$MMWK    MULT 30.6      G$JD      H
     C                     END
     C                     ADD  G$DD      G$JD    50
     C           G$YYYY    SUB  1900      G$YYWK  30
     C           G$YYWK    IFNE 0
     C           GMWKSV    IFLT 0
     C                     SUB  1         G$YYWK
     C                     END
     C                     END
     C           G$YYWK    MULT 365.25    G$JYD   70
     C                     ADD  G$JYD     G$JD             DAYSDAYS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C* Convert 'Juliann' date (mmddyy) to 'Julian' date
     C* (Partially copied from Pgm:ER211)
     C*  JTOJ$    Julian to Gregorian conversion
     C*----------------------------------------------------------------
     C           JTOG$     BEGSR
     C*----------------------------------------------------------------
     C           J$JD      DIV  365.25    J$YYWK  30
     C           J$JD      DIV  365.25    J$TST   99
     C           J$TST     IFEQ 0
     C                     SUB  1         J$YYWK
     C                     END
     C           J$YYWK    MULT 365.25    J$YD    70
     C           J$JD      SUB  J$YD      J$YD
     C           J$YD      IFGT 306
     C                     ADD  1         J$YYWK
     C                     END
     C                     Z-ADD0         J$X     20
     C           J$YD      DOULEJ$MD
     C                     ADD  1         J$X
     C           J$X       MULT 30.6      J$MD    30H
     C                     END
     C                     SUB  1         J$X
     C           J$X       MULT 30.6      J$MD      H
     C           J$YD      SUB  J$MD      J$DD    20
     C           J$X       ADD  3         J$MM    20
     C           J$MM      IFGT 12
     C                     SUB  12        J$MM
     C                     END
     C           J$YYWK    ADD  1900      J$YYYY  40
     C                     MOVE J$YYYY    WORK2   2
     C                     MOVELJ$MM      WORK4
     C                     MOVE J$DD      WORK4
     C                     MOVE WORK2     J$DATE  60
     C                     MOVELWORK4     J$DATE           MMDDYY
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*    PIR9906  Invalid date entered.
     C*
     CSR         ZM9906    BEGSR
     CSR                   MOVE 'PIR9906' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1001  Invalid option entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    PIR1006  Invalid jobq option for view report option.
     C*
     CSR         ZM1006    BEGSR
     CSR                   MOVE 'PIR1006' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     CSR         ZZCHK1    BEGSR
     C*----------------------------------------------------------------
     CSR                   MOVE *OFF      ERROR
     CSR                   MOVE *BLANKS   ERRMSG
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify route #
     C*
     C           W1RTE#    IFEQ *BLANKS
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN28
     C                     MOVE *ON       *IN08
     C           ERRMSG    IFEQ *BLANKS
     C                     MOVELDESC,7    ERRMSG
     C                     EXSR ZM1001
     C                     ENDIF
     C                     ENDIF
     C*
     C*
     C*     Verify FROM date
     C*
     CSR                   MOVE '*MDYEDT '$CVCMD
     CSR                   MOVE W1FRDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVEL$CVERM    ERRMSG
     CSR                   EXSR ZM0105
     C                     ENDIF
     CSR                   ELSE
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN22
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE $CVMSG    #MSGK
     CSR                   Z-ADDW1FRDT    ERDATE
     CSR                   EXSR ZM9906
     CSR                   ELSE
     CSR                   MOVE $CVD6O    W1FRDT
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*
     C*     Verify TO date
     C*
     CSR                   MOVE '*MDYEDT '$CVCMD
     CSR                   MOVE W1TODT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR         $CVRTN    IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVEL$CVERM    ERRMSG
     CSR                   EXSR ZM0105
     C                     ENDIF
     CSR                   ELSE
     CSR         $CVRTN    IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE $CVMSG    #MSGK
     CSR                   Z-ADDW1FRDT    ERDATE
     CSR                   EXSR ZM9906
     CSR                   ELSE
     CSR                   MOVE $CVD6O    W1TODT
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C           ERROR     IFEQ *ON
     C                     GOTO TYPE
     C                     ENDIF
     C*
     C*     Convert input date from 'mmddyy' to 'yyyymmdd' to pass back
     C*
     CSR                   MOVE '*MDYCMD '$CVCMD
     CSR                   MOVE W1FRDT    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    $FDATE
     C*
     C*
     C*
     C*  Convert date from 'MDY' TO 'JULIAN' to validate input date
     C*
     C                     MOVE W1FRDT    G$DATE  60
     C                     EXSR GTOJ$
     C                     Z-ADDG$JD      FRMDTE  50       Juliandte
     C*
     C                     MOVE W1TODT    G$DATE  60
     C                     EXSR GTOJ$
     C                     Z-ADDG$JD      TODTE   50       Juliandte
     C*
     C                     Z-ADD*ZEROS    J$JD    50
     C*
     C*  Add 1 day to 'TO DATE' julian conversion above
     C*  to convert it to Gregorian date.
CBI AC*  Note: Truck build print label day goes from about
CBI AC*  4 p.m. to 3 a.m. next day.
     C*
     C           TODTE     ADD  1         J$JD
     C                     EXSR JTOG$
     C                     Z-ADDJ$DATE    W1TODT
     C*
     CSR                   MOVE '*MDYCMD '$CVCMD
     C                     MOVE J$DATE    $CVD6I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   MOVE $CVD8O    $TDATE
      *
     C*  'FROM' date must be less than 'TO' date
     C*
     CSR         FRMDTE    IFGT TODTE
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVELDESC,6    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C           TYPE      TAG
     C*
     C*  Verify report comment types
     C*
     C           W1TYPE    IFNE '1'
     C           W1TYPE    ANDNE'2'
     C           W1TYPE    ANDNE'3'
     C           W1TYPE    ANDNE'4'
     C           W1TYPE    ANDNE'5'
     C           W1TYPE    ANDNE' '
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN29
     C                     MOVE *ON       *IN09
     C           ERRMSG    IFEQ *BLANKS
     C                     MOVELDESC,8    ERRMSG
     C                     EXSR ZM1001
     C                     ENDIF
     C                     ENDIF
     C*
     C*
     C*  Verify view/print.
     C*
     C           W1VP      IFNE 'V'
     C           W1VP      ANDNE'P'
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN24
     C                     MOVE *ON       *IN04
     C           ERRMSG    IFEQ *BLANKS
     C                     MOVELDESC,2    ERRMSG
     C                     EXSR ZM1001
     C                     ENDIF
     C                     ENDIF
     C*
     C           W1SORT    IFNE '1'
     C           W1SORT    ANDNE'2'
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN30
     C                     MOVE *ON       *IN10
     C           ERRMSG    IFEQ *BLANKS
     C                     MOVELDESC,9    ERRMSG
     C                     EXSR ZM1001
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Verify jobq option
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN25
     CSR                   MOVE *ON       *IN05
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVELDESC,3    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Cannot run on jobq, if we want to view the report.
     C*
     C           W1VP      IFEQ 'V'
     C           W1JOBQ    ANDEQ'Y'
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN25
     C                     MOVE *ON       *IN05
     C                     EXSR ZM1006
     C                     ENDIF
     C*
     C*  Verify default device
     C*
     CSR                   MOVELW1DEV     WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1DEV     ANDNE'*SYSVAL'
     CSR         W1DEV     ANDNE'*JOB'
     CSR         W1DEV     OREQ *BLANKS
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE '1'       *IN26
     CSR                   MOVE '1'       *IN06
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVELDESC,4    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify default form type
     C*
     CSR                   MOVELW1FORM    WORK1A  1
     CSR         WORK1A    IFEQ '*'
     CSR         W1FORM    ANDNE'*STD'
     CSR         W1FORM    OREQ *BLANKS
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE '1'       *IN27
     CSR                   MOVE '1'       *IN07
     C           ERRMSG    IFEQ *BLANKS
     CSR                   MOVELDESC,5    ERRMSG
     CSR                   EXSR ZM1001
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*  ZZCHK2   Window1 error checking.
     C*----------------------------------------------------------------
     CSR         ZZCHK2    BEGSR
      *
     C                     MOVE '0'       ERROR
     C                     MOVE *BLANKS   ERRMSG
      *
      *  Check if user id is found in user file.
      *
     C           W1TRNP    IFEQ *BLANK
     C           W1TRLR    ANDEQ*BLANK
     C           W1LODR    ANDEQ*BLANK
     C           W1DLAY    ANDEQ*BLANK
     C           W1OTHR    ANDEQ*BLANK
     C                     MOVE *ON       ERROR
     C                     MOVE *ON       *IN20
     C                     MOVE *ON       *IN21
     C                     MOVE *ON       *IN22
     C                     MOVE *ON       *IN23
     C                     MOVE *ON       *IN24
     C                     MOVEADESC,1    ERRMSG
     C                     EXSR ZM0105
     C                     GOTO ENDCK2
     C                     ENDIF
      *
     C                     SELEC
     C           W1TRNP    WHNE *BLANKS
     C                     MOVE '1 '      $LCODE
     C                     MOVE '1 '      $CTYPE
     C                     EXSR ZZRSNC
     C                     MOVE *BLANKS   W1TRNP
      *
     C           W1TRLR    WHNE *BLANKS
     C                     MOVE '2 '      $LCODE
     C                     MOVE '2 '      $CTYPE
     C                     EXSR ZZRSNC
     C                     MOVE *BLANKS   W1TRLR
      *
     C           W1LODR    WHNE *BLANKS
     C                     MOVE '3 '      $LCODE
     C                     MOVE '3 '      $CTYPE
     C                     EXSR ZZRSNC
     C                     MOVE *BLANKS   W1LODR
      *
     C           W1DLAY    WHNE *BLANKS
     C                     MOVE '4 '      $LCODE
     C                     MOVE '4 '      $CTYPE
     C                     EXSR ZZRSNC
     C                     MOVE *BLANKS   W1DLAY
      *
     C           W1OTHR    WHNE *BLANKS
     C                     MOVE '5 '      $CTYPE
     C                     SETON                     0325
     C                     MOVE *BLANKS   W1OTHR
      *
     C                     OTHER
      *
     C                     ENDSL
      *
     C           ENDCK2    ENDSR
     C*
     C*----------------------------------------------------------------
     C*  ZZCMD1   User defined command keys for screen 1.
     C*----------------------------------------------------------------
     C           ZZCMD1    BEGSR
     C                     MOVE '0'       CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     C           *INKD     IFEQ *ON
     C                     MOVE *ON       CMDTKN
     C                     Z-ADD0         ROW#
     C                     MOVE ROWIN     ROW
     C                     Z-ADD0         COL#
     C                     MOVE COLIN     COL
     C*
     C*     Warehouse department.
     C*
     C                     SELEC
      *
     C           ROW#      WHEQ 07
     C                     MOVE '*LOOKUP '$LCMD
     C                     MOVE #PROG     $LPRG
     C                     Z-ADDW1WHSE    $LWHSE
     C                     CALL 'WH900'
     C                     PARM           $LPARM
     C           $LRTN     IFEQ '*SELECT '
     C                     Z-ADD$LWHSE    W1WHSE
     C                     ENDIF
      *
     C           ROW#      WHEQ 12
     C                     MOVE '02 '     NXTSCR
     C                     GOTO ENDCM1
     C*
     C                     OTHER
     C*
     C*
     C*     Cursor not on a valid lookup field.
     C*
     C                     MOVE *ON       ERROR
     C                     EXSR ZM9905
     C                     ENDSL
     C                     ENDIF
     C*
     C           ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     C*   Initialize printer device/form
     C*
     C*      Get warehouse for user.
     C*
     CSR                   CALL 'GETWHSE'
     CSR                   PARM           #USER
     CSR         W1WHSE    PARM 0         #WHSE   30
     C*
     CSR                   Z-ADDW1WHSE    WKWHSE  30
     C                     MOVE '*ALL '   W1RTE#
     C                     Z-ADD*ZEROS    W1FRDT
     C                     Z-ADD*ZEROS    W1TODT
     C                     MOVE *BLANK    W1TYPE
     C                     MOVE 'V'       W1VP
     CSR                   MOVE 'N'       W1JOBQ
     CSR                   MOVE '1'       W1SORT
     C*
     CSR                   MOVEL'*JOB    'W1DEV
     CSR                   MOVEL'*STD    'W1FORM
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*----------------------------------------------------------------
     C           ZZNXT1    BEGSR
     C*
      *
     C                     MOVE W1RTE#    $RTE#
     C                     MOVE W1FRDT    $FDTE6
     C                     MOVE W1TODT    $TDTE6
     C                     MOVE W1WHSE    $WHSE
     C                     MOVE W1VP      $VP
     C                     MOVE W1SORT    $SORT
     C                     MOVE W1JOBQ    $JOBQ
     C                     MOVE W1FORM    $FORM
     C           NXTSCR    IFNE '02 '
     C                     MOVELW1TYPE    $CTYPE
     C                     MOVE *BLANKS   $RCODE
     C                     ENDIF
     C                     MOVE W1DEV     $DEV
     C*
     C                     MOVE 'EOJ'     NXTSCR
     C                     ENDSR
     C*
     O*----------------------------------------------------------------*********
     C*  ZZRSNC   Get reason code.
     C*----------------------------------------------------------------
     CSR         ZZRSNC    BEGSR
      *
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   Z-ADD1         $LWHSE
     CSR                   MOVE #PROG     $LPRG
     C*>                   MOVE *BLANKS   $LCODE
     CSR                   CALL 'RC904'
     CSR                   PARM           $LPARM
      *
     C           $LRTN     IFEQ '*CANCEL '
     C                     MOVE '02 '     NXTSCR
     C                     GOTO ENDRSN
     C                     ENDIF
      *
     C           $LRTN     IFEQ '*EXIT   '
     C                     MOVE '01 '     NXTSCR
     C                     GOTO ENDRSN
     C                     ENDIF
      *
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   MOVEL$LCODE    $RCODE
     C                     MOVE '02 '     NXTSCR
     C*>         COMKEY    CHAINLCOMRC               79
     C*>         *IN79     IFEQ '0'
     C*>                   MOVELLCSDSC    W1REAS
     C*>                   ENDIF
     C                     ENDIF
     C                     SETON                     03
     C                     SETOF                     02
      *
     CSR         ENDRSN    ENDSR
     O*
     O*  COMPILE TIME TABLES
     O*
**
Section type
Print option
Job queue option
Printer device
Form type
'From' date can't be greater than 'To' date
For route #
Type option
Order report option
