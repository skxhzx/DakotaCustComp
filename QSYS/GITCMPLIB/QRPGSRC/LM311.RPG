     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 Roosevelt Rd.  Bldg 8, Suite 51
     F*   WEST CHICAGO, IL 60158
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  LM311     Lumper Report.
     F*  02 January, 2003
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*  01/02/03  HNK  4.16
     F*   - Created
     F*
417 AF*  03/25/03  HNK  4.17
     F*   - Add palletized cost parameters and print total cost.
     F*
417aAF*  05/27/03  HNK  4.17a
     F*   - print palletized cost and total qty.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FLM311   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FPOHDR   IF  E           K        DISK
     FPODTL   IF  E           K        DISK
     FPOHHDR  IF  E           K        DISK
     FPOHDTL  IF  E           K        DISK
     FLM311PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    A9          9  1
     E*
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
417 AC                     PARM           $PPALY  52
417 AC                     PARM           $PPALN  52
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Page overflow.
     C*
     C*  PAGE OVERFLOW
     C*
     C           FIRST     IFEQ *ON
     C           *IN90     OREQ *ON
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE LMRDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    W1RDTE
     C*
     C*  Get PO details.
     C*
     C                     SELEC
     C           LMHEDT    WHGT 0
     C           PHHKEY    CHAINPOHHDR               79
     C           *IN79     IFEQ *ON
     C                     GOTO SKIP
     C                     ENDIF
     C                     Z-ADDPHHVEN    WKVNDR  90
     C                     MOVE PHHVNA    WKVNAM 40
     C                     EXSR ZZVNDR
     C                     EXSR ZZHDTL
     C*
     C           LMHEDT    WHEQ 0
     C           PHKEY     CHAINPOHDR                79
     C           *IN79     IFEQ *ON
     C                     GOTO SKIP
     C                     ENDIF
     C                     Z-ADDPHVEN     WKVNDR  90
     C                     MOVE PHVNAM    WKVNAM 40
     C                     EXSR ZZVNDR
     C                     EXSR ZZDTL
     C*
     C                     ENDSL
     C*
417 AC                     Z-ADD0         W1COST
417 AC           LMPALT    IFEQ 'Y'
417 AC           W1QRC1    MULT $PPALY    W1COST
417aAC                     Z-ADD$PPALY    W1PALT
417 AC                     ENDIF
417 AC*
417 AC           LMPALT    IFEQ 'N'
417 AC           W1QRC1    MULT $PPALN    W1COST
417aAC                     Z-ADD$PPALN    W1PALT
417 AC                     ENDIF
417 AC*
417 AC                     ADD  W1COST    T#COST
417aAC                     ADD  W1QRC1    T#QTY
     C*
     C*  Write detail line.
     C*
     C                     WRITEDETAIL1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      FIRST
     C                     MOVE *OFF      *IN91
     C*
     C           SKIP      TAG
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD1         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
417 ACLR                   WRITETOTAL1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C*  Define key for POHHDR file.
     C*
     CSR         PHHKEY    KLIST
     CSR                   KFLD           LMWHSE
     CSR                   KFLD           LMPO
     CSR                   KFLD           LMHEDT
     CSR                   KFLD           LMHETI
     C*
     C*  Define partial key for POHDTL file.
     C*
     CSR         PDHKEY    KLIST
     CSR                   KFLD           LMWHSE
     CSR                   KFLD           LMPO
     C*
     C*  Define key for POHDR file.
     C*
     CSR         PHKEY     KLIST
     CSR                   KFLD           LMWHSE
     CSR                   KFLD           LMPO
     C*
     C*  Define partial key for PODTL file.
     C*
     CSR         PDKEY     KLIST
     CSR                   KFLD           LMWHSE
     CSR                   KFLD           LMPO
     C*
417 AC                     Z-ADD0         T#COST
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     MOVE *OFF      FOREVR  1
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           LMWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C                     EXSR ZRFRMT
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C* PO details
     C           ZZDTL     BEGSR
     C*
     C                     Z-ADD0         W1QOR1
     C                     Z-ADD0         W1QRC1
     C           PDKEY     SETLLPODTL
     C           FOREVR    DOWEQFOREVR
     C           PDKEY     READEPODTL                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     ADD  PDQOR1    W1QOR1
     C                     ADD  PDQRC1    W1QRC1
     C                     ENDDO
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C* PO History details
     C           ZZHDTL    BEGSR
     C*
     C                     Z-ADD0         W1QOR1
     C                     Z-ADD0         W1QRC1
     C           PDHKEY    SETLLPOHDTL
     C           FOREVR    DOWEQFOREVR
     C           PDHKEY    READEPOHDTL                   79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     ADD  PHDQO1    W1QOR1
     C                     ADD  PHDQR1    W1QRC1
     C                     ENDDO
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C* Format Vendor
     C           ZZVNDR    BEGSR
     C*
     C                     MOVE *BLANKS   W1VNDR
     C*
     C*  Left justify vendor number (suppress zeros)
     C*
     C                     Z-ADD0         X       20
     C                     MOVE *OFF      FLAG    1
     C                     MOVE WKVNDR    TEMP9A  9
     C                     MOVEATEMP9A    A9
     C           A9,1      IFEQ '0'
     C           2         DO   9         I       20
     C           A9,I      IFNE '0'
     C           FLAG      OREQ *ON
     C                     ADD  1         X
     C                     MOVE A9,I      A9,X
     C                     MOVE *ON       FLAG
     C                     ENDIF
     C                     MOVE ' '       A9,I
     C                     ENDDO
     C                     ENDIF
     C*
     C*  Put everything together
     C*
     C                     MOVEAA9        W1VNDR
     C           W1VNDR    CAT  '-':1     W1VNDR
     C           W1VNDR    CAT  WKVNAM:1  W1VNDR
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
