     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  LOADR30   Route Loading Comments report
     F*  September 11, 2007
     F*
     F*  Jose Duque
     F*
     F*  Revisions:
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*  NOTE: File 'RTECOMMW' below is created by qry: 'RTECOMMW'.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FRTECOMMWIP  E           K        DISK
     F            RTECOMMW                          KRENAMECOMMW2
     F*
     FWAREHS  IF  E           K        DISK
     F*
     FPIRUSER1IF  E           K        DISK
     F*
     FLDRCOMC IF  E           K        DISK
     F*
     FLOAD30PRO   E             90     PRINTER
     F*
     E*----------------------------------------------------------------
     I*
     I*  Data structures for date manipulation
     I*
     I            DS
     I                                        1   80LOGIN
     I                                        1   20CCIN
     I                                        3   40YYIN
     I                                        5   60MMIN
     I                                        7   80DDIN
     I            DS
     I                                        1   60LOGOUT
     I                                        1   20MMOUT
     I                                        3   40DDOUT
     I                                        5   60YYOUT
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $CTYPE  Comment type (MMDDYY)
     C*      $FRMDT  From date    (MMDDYY)
     C*      $TODT   To   date    (MMDDYY)
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $CTYPE  2
     C                     PARM           $FRMDT  6
     C                     PARM           $TODT   6
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
      *
     C*
     C           FIRST     IFEQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     MOVE *OFF      FIRST
     C                     ENDIF
      *
     C*>         COUNT     IFEQ RECPAG
     C*>                   EXSR ZRHEAD
     C*>                   ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C           $CTYPE    IFEQ *BLANKS
     C                     EXSR ZRDETL
     C                     ELSE
     C           LCDTYP    IFEQ $CTYPE
     C                     EXSR ZRDETL
     C                     ENDIF
     C                     ENDIF
     C*----------------------------------------------------------------
     C*
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR         REC#      IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD0         REC#    30
     C                     Z-ADD0         COUNT   30
     C                     Z-ADD0         RECDPT  30
     C                     Z-ADD30        RECPAG  30
     C*
     C                     MOVE $FRMDT    FRMDTE
     C                     MOVE $TODT     TODTE
     C*
     C           DSCKEY    KLIST
     C                     KFLD           LCHWHS
     C                     KFLD           LCDTYP
     C                     KFLD           LCDRSC
      *
     C                     EXSR ZRCLER
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           LCHWHS    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C                     MOVE *OFF      *IN90
     C*
     C*  Print headings for the next page.
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C                     WRITECOLUMNS
     C*
     C*
     C*  Print previous page detail lines.
     C*
     C                     Z-ADD0         COUNT
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRCLER    BEGSR
     C*
     C*   Initialize all data structures.
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRDETL    BEGSR
     C*  Print detail line.
      *
     C           LCHRTI    IFEQ HLDRTI
     C           LCHEMP    ANDEQHLEMP1
     C           LCDEMP    ANDEQHLEMP2
     C           LCHRTI    OREQ HLDRTI
     C           LCHEMP    ANDEQHLEMP1
     C           LCDEMP    ANDEQ*ZEROS
     C                     SETOF                     15
     C                     ELSE
     C                     MOVE *BLANKS   WRITTN
     C           LCHEMP    CHAINPIRUSER1             79
     C           *IN79     IFEQ *OFF
     C                     MOVE USCODE    WRITTN
     C                     ENDIF
      *
     C                     MOVE *BLANKS   COMMBY
     C           LCDEMP    CHAINPIRUSER1             79
     C           *IN79     IFEQ *OFF
     C                     MOVE USCODE    COMMBY
     C                     ENDIF
      *
     C                     Z-ADDLCHSTD    LOGIN
     C                     Z-ADDMMIN      MMOUT
     C                     Z-ADDDDIN      DDOUT
     C                     Z-ADDYYIN      YYOUT
     C                     Z-ADDLOGOUT    DATE
      *
     C                     SELEC
     C           LCDTYP    WHEQ '1'
     C                     MOVEL'TRAN'    TYPE
      *
     C           LCDTYP    WHEQ '2'
     C                     MOVEL'TRLR'    TYPE
      *
     C           LCDTYP    WHEQ '3'
     C                     MOVEL'LODR'    TYPE
      *
     C           LCDTYP    WHEQ '4'
     C                     MOVEL'LDEL'    TYPE
      *
     C                     OTHER
     C                     MOVEL'OTHR'    TYPE
     C                     ENDSL
      *
      *
     C                     MOVE *BLANKS   LCSDSC
     C           LCDTYP    IFNE '5'
     C           DSCKEY    CHAINLDRCOMC              79
     C                     ENDIF
      *
     C           RECPAG    SUB  COUNT     LNLEFT  30
     C           LNLEFT    IFLT 12
     C                     EXSR ZRHEAD
     C                     MOVELLCHRTI    HLDRTI  5
     C                     SETON                     15
     C                     ELSE
     C                     WRITESKIP1
     C                     MOVELLCHRTI    HLDRTI
     C                     Z-ADDLCHEMP    HLEMP1  50
     C                     Z-ADDLCDEMP    HLEMP2  50
     C                     SETON                     15
     C                     ENDIF
     C                     ENDIF
      *
     C                     WRITEDETAIL1
     C                     ADD  1         COUNT
     C                     ADD  1         REC#
     C                     ADD  1         RECDPT
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*
     C           ZRTOTL    BEGSR
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
