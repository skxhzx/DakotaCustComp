     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT316     Inventory adjustment report
     F*  Hemant Kapadia
     F*  09 November, 1994
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT316   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FIT316PR O   E             90     PRINTER
     I*****************************************************************
     IOQREC
     I                                              IADATEL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $FROM  - From date - returned as unpacked numeric
     C*      $TO    - To date - returned as unpacked numeric
     C*
     C*    Returned Parameters
     C*      None
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C           *IN90     IFEQ '1'
     C           FIRST     OREQ '1'
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  L1 break - Date change.
     C*
     C           *INL1     IFEQ '1'
     C                     MOVE '1'       *IN91
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE IADATE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    W1DATE  60
     C                     END
     C*
     C* Detail Calcs
     C*
     C           IATIME    DIV  100       W1TIME  40
     C*
     C*    Create a quantity string to print.
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDIAQTY1    $PQTY1
     C                     Z-ADDIAQTY2    $PQTY2
     C                     Z-ADDIAQTY3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM
     C*
     C                     MOVE $PQSTR    W1QTY
     C                     WRITEDETAIL1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       FIRST
     C                     MOVE '0'       *IN91
     C*
     CL1                   WRITESKIP1
     C*
     CLR                   WRITEFOOT1
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   TIME           TIME
     CSR                   MOVE '1'       FIRST   1
     C*
     C*   Convert dates to MMDDYY format.
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $FROM     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    W1FROM  60
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $TO       $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    W1TO    60
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           IAWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     ENDSR
     C*****************************************************************
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         *INL1     IFEQ '0'
     CSR                   MOVE '1'       *IN91
     CSR                   END
     C*
     CSR                   MOVE '0'       *IN90
     CSR                   WRITEHEAD1
     CSR                   WRITECOLUMNS
     C*
     CSR                   ENDSR
