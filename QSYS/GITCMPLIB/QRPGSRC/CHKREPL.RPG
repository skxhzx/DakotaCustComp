     F*----------------------------------------------------------------
     F*   Copyright (C) 1995 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  CHKREPL   Check for replenishment slot.
     F*  03 March 1995
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
412 AF*    08/18/99  DAS  4.12
     F*      - Revised to not exclude slots with enter date = 0.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT3   IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse number.
     C*      $PITEM  Item number.
     C*
     C*    Returned Parameters
     C*      $PRTN   *YES     - Replenishment slot exists for item.
     C*              *NO      - No replenishment slots exist for item.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Start off by assuming there are no replenishment slots.
     C*
     C                     MOVE '*NO     '$PRTN
     C*
     C*    Then see if one exists by looking for active non-picking
     C*    slots which have an enter date. Slots without an enter
     C*    date are assumed to be empty
     C*
     C*    4.12 change: Enter date is not checked anymore.
     C*
     C           *LIKE     DEFN SLSTAT    KYSTAT
     C           *LIKE     DEFN SLPICK    KYPICK
     C           SL3KEY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           $PITEM
     C                     KFLD           KYSTAT
     C                     KFLD           KYPICK
     C*
     C                     MOVE 'A '      KYSTAT
     C                     MOVE 'N'       KYPICK
     C           SL3KEY    SETLLSLOT3
     C*
     C                     MOVE *OFF      *IN79
     C           *IN79     DOUEQ*ON
     C           SL3KEY    READESLOT3                    79
     C           *IN79     IFEQ *OFF
412 DC*          SLENTD    ANDNE0
     C                     MOVE '*YES    '$PRTN
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C                     RETRN
