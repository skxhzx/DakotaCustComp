     F*----------------------------------------------------------------
     F*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  UPD500MBB  Update *GENPICK, *VOICE, create new *RUNNER options
     F*  30 October 2005
     F*  Max Blacknik
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
500 AF*    10/30/05  MLB  5.00
     F*      - Add *RUNNER option record to OPTIONS file.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FWAREHS  IF  E           K        DISK
     FOPTIONS UF  E           K        DISK                      A
     I*----------------------------------------------------------------
     I*
500gAI*----------------------------------------------------------------
500gAI*  *RUNNER  -  PIR Runner Flag Options
500gAI*----------------------------------------------------------------
500gAI*
500gAI* Fields
500gAI*
500gAI*    OPCRTM  -  Create Manual Runner entries for shorts/outs (Y/N)
500gAI*    OPUPDL  -  Update Label with quantity shorted during manual
500gAI*               flag entry (Y/N)
500gAI*               NOTE: Any change to this field will cause the same
500gAI*                     field in the *VOICE options record to be
500gAI*                     updated.
500gAI*    OPRPLF  -  Replenish flagged items before print report? (Y/N)
500gAI*    OPMRGF  -  Create Runner entries for Merge records? (Y/N)
500gAI*    OPCLRF  -  Clear Runner flags if qty still short/out? (Y/N)
500gAI*    OPRNRM  -  Send break message to user when new Runner
500gAI*               task has been assigned. (Y/N)
500gAI*    OPCLSF  -  Update Label quantity at flag close (Y/N).
500gAI*    OPOUTF  -  Create Runner entry after slot marked out. (Y/N)
500iAI*    OPRPCK  -  Print Runner pick list. (Y/N)
500iAI*    OPRLBL  -  Print Runner pick labels. 0=No, 1=full size Zebra
500iAI*               printer, 2=Belt printer
500gAI*
500gAI* Data structure
500gAI*
500gAIOPDATA      DS
500gAI                                        1   1 OPCRTM
500gAI                                        2   2 OPUPDL
500gAI                                        3   3 OPRPLF
500gAI                                        4   4 OPMRGF
500gAI                                        5   5 OPCLRF
500gAI                                        6   6 OPRNRM
500gAI                                        7   7 OPCLSF
500gAI                                        8   8 OPOUTF
500iAI                                        9   9 OPRPCK
500iAI                                       10  10 OPRLBL
     I                                      117 117 OPTEND
500gAI*
410aAI*----------------------------------------------------------------
410aAI*  *GENPICK -  General picking options.
410aAI*----------------------------------------------------------------
410 AI*
    DI*
    DI*     03/31/05  HNK
    DI*       - OPPSET added.
410 AI* Fields
410 AI*
410 AI*    OPERPT  -  Print exception (short) error report (Y,N).
410 AI*    OPELBL  -  Print exception (short) labels (Y,N)
410BAI*    OPRFWC  -  Using RF will call (Y,N).
413cAI*    OPNRSC  -  Number of pallet sections (1-9).
414aAI*    OPHPWC  -  Host pick Will Call (Y/N).
415cAI*    OPMRGA  -  Merge Add-on order(s) with Open Routes.
415dAI*    OPPLSQ  -  Pallet number sequence. 1=Unload, 2=Load.
416eAI*    OPEADJ  -  Close short labels without adjustment (Y,N).
417dAI*    OPNOPK  -  Pick items without pick slot as bulk pick? (Y/N)
500fDI*    OPGPK1  -  <Not Used / Available> Was OPCLRF.
500fDI*    OPGPK2  -  <Not Used / Available> Was OPRNRM
417kAI*               task has been assigned. (Y/N)
500fMI*    OPJITR  -  Create JIT Letdown or JIT Replenishment
500fAI*                 0=None. No replenishments are created.
500fAI*                 1=Letdowns are created during Truck Builder
500fAI*                   Pick/Print processing.
500fAI*                 2=Priority Replenishments are created during
500fAI*                   Truck Builder Pick/Print processing.
500fAI*                 3=JIT Letdowns created when transaction is
500fAI*                   checked out.
500fAI*                 4=JIT Priority Replenishments created when
500fAI*                   transaction is checked out.
500bDI*    OPPSET  -  Print separate set summary label for each pallet.
410 AI*
410 AI* Data structure
410 AI*
410 AIOPDTA2      DS
410 AI                                        1   1 OPERPT
410 AI                                        2   2 OPELBL
410BAI                                        3   3 OPRFWC
413cAI                                        4   40OPNRSC
414aAI                                        5   5 OPHPWC
415cAI                                        6   6 OPMRGA
415dAI                                        7   7 OPPLSQ
416eAI                                        8   8 OPEADJ
417dAI                                        9   9 OPNOPK
     I                                       10  10 OPGPK1
     I                                       11  11 OPGPK2
417mAI                                       12  12 OPJITR
500bAI                                       13  13 OPPSET
410 AI                                      117 117 OPEND
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *OFF      *IN79
     C           *LIKE     DEFN OPCODE    KYCODE
     C           OPKEY     KLIST
     C                     KFLD           WHCODE
     C                     KFLD           KYCODE
     C*
     C           *LOVAL    SETLLWAREHS
     C           *IN79     DOWEQ*OFF
     C                     READ WAREHS                   79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE                           EXIT LOOP
     C*
     C                     OTHER
     C*
     C                     MOVE '*RUNNER 'KYCODE
     C           OPKEY     CHAINOPTIONS              78
     C                     SELEC
     C*
     C*    Update Runner options record. Set defaults.
     C           *IN78     WHEQ *OFF
     C           OPRPCK    IFEQ ' '
     C                     MOVE 'Y'       OPRPCK
     C                     ENDIF
     C           OPRLBL    IFEQ ' '
     C                     MOVE '0'       OPRLBL
     C                     ENDIF
     C                     UPDATOPREC
     C*
     C*    Create new Runner options record. Set defaults.
     C           *IN78     WHEQ *ON
     C                     MOVE WHCODE    OPWHSE
     C                     MOVE KYCODE    OPCODE
     C                     MOVE 'N'       OPCRTM
     C                     MOVE 'N'       OPUPDL
     C                     MOVE 'N'       OPRPLF
     C                     MOVE 'N'       OPMRGF
     C                     MOVE 'Y'       OPCLRF
     C                     MOVE 'N'       OPRNRM
     C                     MOVE 'N'       OPCLSF
500gAC                     MOVE 'Y'       OPOUTF
500iAC                     MOVE 'Y'       OPRPCK
500iAC                     MOVE '0'       OPRLBL
     C                     WRITEOPREC
     C*
     C*
     C                     MOVE '*GENPICK'KYCODE
     C           OPKEY     CHAINOPTIONS              78
     C                     MOVE *BLANKS   SVCLRF
     C                     MOVE *BLANKS   SVRNRM
     C                     SELEC
     C*
     C*    Retrieve Runner options from Gen. Pick options.
     C*    Move them to new options record. Clear flds in Gen Pick.
     C           *IN78     WHEQ *OFF
     C                     MOVELOPDATA    OPDTA2
     C           OPGPK1    IFGT ' '
     C                     MOVE OPGPK1    SVCLRF  1
     C                     ENDIF
     C           OPGPK2    IFGT ' '
     C                     MOVE OPGPK2    SVRNRM  1
     C                     ENDIF
     C                     MOVE *BLANKS   OPGPK1
     C                     MOVE *BLANKS   OPGPK2
     C                     MOVELOPDTA2    OPDATA
     C                     UPDATOPREC
     C*
     C*
     C                     MOVE '*RUNNER 'KYCODE
     C           OPKEY     CHAINOPTIONS              78
     C                     SELEC
     C*
     C*    Restore Runner options from Gen. Pick record.
     C           *IN78     WHEQ *OFF
     C           SVCLRF    IFGT ' '
     C                     MOVE SVCLRF    OPCLRF
     C                     ENDIF
     C           SVRNRM    IFGT ' '
     C                     MOVE SVRNRM    OPRNRM
     C                     ENDIF
     C                     UPDATOPREC
     C                     ENDSL
     C                     ENDSL
     C*
     C                     ENDSL
     C                     ENDSL
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*
