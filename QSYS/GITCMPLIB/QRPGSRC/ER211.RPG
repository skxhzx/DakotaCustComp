     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  ER211     Selectors % of standards by week
     F*  11 January, 1996
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
409 AF*  06/03/98  HNK  4.09
     F*    - Control break added on date.
     F*
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FER211   IP  E           K        DISK
     FREPORTE IF  E           K        DISK
     FER211PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
409 ME                    RPD     1   4 25
409 ME                    RECK        4  7 0
409 ME                    LVLK        4  7 0
409 DE*                   RPD     1   3 25
409 DE*                   RECK        3  7 0
409 DE*                   LVLK        3  7 0
     E*
     E                    DTE     1   4 22
     E                    A22        22  1
     E*
409 ME                    SUN         4  5 2
409 ME                    MON         4  5 2
409 ME                    TUE         4  5 2
409 ME                    WED         4  5 2
409 ME                    THU         4  5 2
409 ME                    FRI         4  5 2
409 ME                    SAT         4  5 2
     E*
409 ME                    SUNT        4  7 2
409 ME                    MONT        4  7 2
409 ME                    TUET        4  7 2
409 ME                    WEDT        4  7 2
409 ME                    THUT        4  7 2
409 ME                    FRIT        4  7 2
409 ME                    SATT        4  7 2
     E*
409 DE*                   SUN         3  5 2
409 DE*                   MON         3  5 2
409 DE*                   TUE         3  5 2
409 DE*                   WED         3  5 2
409 DE*                   THU         3  5 2
409 DE*                   FRI         3  5 2
409 DE*                   SAT         3  5 2
     E*
409 DE*                   SUNT        3  7 2
409 DE*                   MONT        3  7 2
409 DE*                   TUET        3  7 2
409 DE*                   WEDT        3  7 2
409 DE*                   THUT        3  7 2
409 DE*                   FRIT        3  7 2
409 DE*                   SATT        3  7 2
     E*
     E                    WKB        99  8 0
     E                    WKE        99  8 0
     E*
     I*----------------------------------------------------------------
     IWKREC
409 MI                                              WKWNUML3
409 MI                                              WKEMP#L2
409 AI                                              WKGDTEL1
     I*
409 DI*                                             WKWNUML2
409 DI*                                             WKEMP#L1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   60T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     I*  Date redefinition data structure
     I*
     I$PFMDT      DS
     I                                        1   2 $PFMCC
     I                                        3   4 $PFMYY
     I                                        5   6 $PFMMM
     I                                        7   8 $PFMDD
     I$PTODT      DS
     I                                        1   2 $PTOCC
     I                                        3   4 $PTOYY
     I                                        5   6 $PTOMM
     I                                        7   8 $PTODD
     I*----------------------------------------------------------------
     I              'Task code:'          C         @DESC
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PFMDT  From date (YYYYMMDD)
     C*      $PTODT  To date (YYYYMMDD)
     C*      $PTASK  Task code
     C*      $PWEEK  Number of weeks
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PFMDT
     C                     PARM           $PTODT
     C                     PARM           $PTASK  6
     C                     PARM           $PWEEK  2
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
409 MC*  Level 3 break - Week
     C*
409 MC           *INL3     IFEQ *ON
409 MC                     Z-ADD3         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
409 MC*  Level 2 break - Employee number
     C*
409 MC           *INL2     IFEQ *ON
409 MC                     Z-ADD2         L
     C                     EXSR ZRZERO
     C                     ENDIF
409 AC*
409 AC*  Level 1 break - Date
409 AC*
409 AC           *INL1     IFEQ *ON
409 AC                     Z-ADD1         L
409 AC                     EXSR ZRZERO
409 AC                     ENDIF
     C*
     C*  Print headings.
     C*
409 MC           *IN93     IFEQ *ON
409 MC           *INL3     OREQ *ON
409 DC***        *IN92     IFEQ *ON
409 DC***        *INL2     OREQ *ON
     C                     EXSR ZRDATE
     C                     WRITEDTHEAD
     C                     WRITECOLUMNS
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C* Convert date
     C                     Z-ADD0         T#DATE
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    T#DATE  60
     C*  Find day of the week
     C                     ADD  0         DTFRMT  10
     C                     MOVE T#DATE    G$DATE
     C                     Z-ADD2         DTFRMT
     C                     EXSR GTOJ$
     C*
     C                     SELEC
     C* Sunday
     C           G$DW      WHEQ 1
     C                     Z-ADDWKPCNT    SUNPCN  52
     C* Monday
     C           G$DW      WHEQ 2
     C                     Z-ADDWKPCNT    MONPCN  52
     C* Tuesday
     C           G$DW      WHEQ 3
     C                     Z-ADDWKPCNT    TUEPCN  52
     C* Wednesday
     C           G$DW      WHEQ 4
     C                     Z-ADDWKPCNT    WEDPCN  52
     C* Thursday
     C           G$DW      WHEQ 5
     C                     Z-ADDWKPCNT    THUPCN  52
     C* Friday
     C           G$DW      WHEQ 6
     C                     Z-ADDWKPCNT    FRIPCN  52
     C* Saturday
     C           G$DW      WHEQ 7
     C                     Z-ADDWKPCNT    SATPCN  52
     C*
     C                     ENDSL
     C*  Accumulate totals
     C*
     C                     ADD  SUNPCN    SUNT,1
     C                     ADD  MONPCN    MONT,1
     C                     ADD  TUEPCN    TUET,1
     C                     ADD  WEDPCN    WEDT,1
     C                     ADD  THUPCN    THUT,1
     C                     ADD  FRIPCN    FRIT,1
     C                     ADD  SATPCN    SATT,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
409 AC                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
409 AC*----------------------------------------------------------------
409 AC*
409 AC* Date
409 AC*
409 ACL1                   Z-ADD1         L
409 ACL1                   EXSR ZRROLL
     C*
     C*----------------------------------------------------------------
     C*
     C* Employee number
     C*
409 MCL2                   Z-ADD2         L
409 MCL2                   EXSR ZRROLL
     C*
409 MCL2         RECK,L    IFGT 0
409 MCL2                   EXSR ZRTOTL
409 MCL2                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Week totals
     C*
409 MCL3                   Z-ADD3         L
409 MCL3                   EXSR ZRROLL
     C*
409 MCL3         RECK,L    IFGT 0
409 MCL3                   EXSR ZRTOTL
409 MCL3                   WRITESKIP1
409 MCL3                   WRITESKIP1
409 MCL3                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Grand totals
     C*
409 MCLR                   Z-ADD4         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C                     Z-ADD0         J$JD    50
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     MOVE $PWEEK    WEEK    20
     C                     MOVE #PROG     $CVPRG
     C                     EXSR ZRZERO
     C                     EXSR ZZWARY
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           GTOJ$     BEGSR
     C                     MOVE G$DATE    G$DATE  60
     C* SPLIT DATE INTO MONTH, DAY, YEAR FIELDS
     C           DTFRMT    IFEQ 1                          BLVL-01
     C                     MOVELG$DATE    G$YY    20
     C                     MOVE G$DATE    WORK4   4
     C                     MOVELWORK4     G$MM    20
     C                     MOVE WORK4     G$DD    20
     C                     ELSE                            EELS-01
     C                     MOVELG$DATE    G$MM
     C                     MOVE G$DATE    WORK4
     C                     MOVELWORK4     G$DD
     C                     MOVE WORK4     G$YY
     C                     END                             EEND-01
     C* CREATE 4 DIGIT YEAR
     C                     MOVE G$YY      G$YYYY  40
     C           G$YY      IFGE 60                         BLVL-01
     C                     MOVEL'19'      G$YYYY
     C                     ELSE                            EELS-01
     C                     MOVEL'20'      G$YYYY
     C                     END                             EEND-01
     C                     Z-ADD0         G$JD
     C           G$MM      SUB  3         G$MMWK  20
     C                     Z-ADDG$MMWK    GMWKSV  20
     C           G$MMWK    IFLT 0                           LVL-01
     C                     ADD  12        G$MMWK
     C                     END                              END-01
     C           GMWKSV    IFNE 0                           LVL-01
     C           G$MMWK    MULT 30.6      G$JD      H
     C                     END                              END-01
     C                     ADD  G$DD      G$JD    50
     C           G$YYYY    SUB  1900      G$YYWK  30
     C           G$YYWK    IFNE 0                           LVL-01
     C           GMWKSV    IFLT 0                           LVL-02
     C                     SUB  1         G$YYWK
     C                     END                              END-02
     C                     END                              END-01
     C           G$YYWK    MULT 365.25    G$JYD   70
     C                     ADD  G$JYD     G$JD
     C           G$JD      DIV  7         G$WK7   70
     C                     MVR            G$DW    10
     C* MAKE SUNDAY = 1ST DAY OF THE WEEK
     C           G$DW      IFLE 3
     C                     ADD  4         G$DW
     C                     ELSE
     C                     SUB  3         G$DW
     C                     ENDIF
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  JTOJ$    Julian to Julian date conversion
     C*
     C           JTOG$     BEGSR
     C           J$JD      DIV  365.25    J$YYWK  30
     C           J$JD      DIV  365.25    J$TST   99
     C           J$TST     IFEQ 0                           LVL-01
     C                     SUB  1         J$YYWK
     C                     END                              END-01
     C           J$YYWK    MULT 365.25    J$YD    70
     C           J$JD      SUB  J$YD      J$YD
     C           J$YD      IFGT 306                         LVL-01
     C                     ADD  1         J$YYWK
     C                     END                              END-01
     C                     Z-ADD0         J$X     20
     C           J$YD      DOULEJ$MD                        LVL-01
     C                     ADD  1         J$X
     C           J$X       MULT 30.6      J$MD    30H
     C                     END                              END-01
     C                     SUB  1         J$X
     C           J$X       MULT 30.6      J$MD      H
     C           J$YD      SUB  J$MD      J$DD    20
     C           J$X       ADD  3         J$MM    20
     C           J$MM      IFGT 12                          LVL-01
     C                     SUB  12        J$MM
     C                     END                              END-01
     C           J$YYWK    ADD  1900      J$YYYY  40
     C           DTFRMT    IFEQ 1                          BLVL-01
     C                     MOVE J$YYYY    WORK2   2
     C                     MOVELJ$MM      WORK4   4
     C                     MOVE J$DD      WORK4
     C                     MOVELWORK2     J$DATE  60
     C                     MOVE WORK4     J$DATE
     C                     ELSE                            EELS-01
     C                     MOVE J$YYYY    WORK2
     C                     MOVELJ$MM      WORK4
     C                     MOVE J$DD      WORK4
     C                     MOVE WORK2     J$DATE
     C                     MOVELWORK4     J$DATE
     C                     END                             EEND-01
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRDATE    BEGSR
     C                     Z-ADDWKWNUM    N
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKB,N     $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    BGDATE
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE WKE,N     $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    ENDATE
     C*
     C                     ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C*
     C                     MOVEL'ER211PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTE              79
     C           *IN79     IFEQ *OFF
     C                     MOVELERDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C*  Format date range
     C*
     C                     SELEC
     C           $PFMDT    WHNE *BLANKS
     C           $PTODT    ANDNE*BLANKS
     C                     MOVEADTE,1     A22
     C                     MOVEA$PFMMM    A22,1
     C                     MOVEA$PFMDD    A22,4
     C                     MOVEA$PFMYY    A22,7
     C                     MOVEA$PTOMM    A22,15
     C                     MOVEA$PTODD    A22,18
     C                     MOVEA$PTOYY    A22,21
     C                     MOVEAA22       DTERNG
     C*
     C           $PFMDT    WHNE *BLANKS
     C                     MOVEADTE,2     A22
     C                     MOVEA$PFMMM    A22,11
     C                     MOVEA$PFMDD    A22,14
     C                     MOVEA$PFMYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C*          $PTODT    WHNE *BLANKS
     C*                    MOVEADTE,3     A22
     C                     MOVEA$PTOMM    A22,11
     C                     MOVEA$PTODD    A22,14
     C                     MOVEA$PTOYY    A22,17
     C                     MOVEAA22       DTERNG
     C*
     C                     OTHER
     C                     MOVEADTE,4     DTERNG
     C*
     C                     ENDSL
     C*
     C                     MOVE *BLANKS   SUBTTL
     C           @DESC     CAT  $PTASK    SUBTTL
     C*
     C*   Center report sub-title.
     C*
     C                     MOVELSUBTTL    $PSTR
     C                     Z-ADD40        $PLEN
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     SUBTTL
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
409 AC           *INL3     IFEQ *OFF
409 AC                     MOVE *ON       *IN93
409 AC                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITESKIP1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  SUNT,L    SUNT,M
     C                     ADD  MONT,L    MONT,M
     C                     ADD  TUET,L    TUET,M
     C                     ADD  WEDT,L    WEDT,M
     C                     ADD  THUT,L    THUT,M
     C                     ADD  FRIT,L    FRIT,M
     C                     ADD  SATT,L    SATT,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDSUNT,L    T#SUNT
     C                     Z-ADDMONT,L    T#MONT
     C                     Z-ADDTUET,L    T#TUET
     C                     Z-ADDWEDT,L    T#WEDT
     C                     Z-ADDTHUT,L    T#THUT
     C                     Z-ADDFRIT,L    T#FRIT
     C                     Z-ADDSATT,L    T#SATT
     C                     Z-ADDRECK,L    T#RECK  30
     C*
     C                     MOVE RPD,L     $RPD   25
     C*
409 MC           L         IFEQ 2
     C                     WRITEDETAIL1
     C                     ENDIF
409 MC           L         IFEQ 3
     C           T#SUNT    DIV  RECK,L    T#SUNT    H
     C           T#MONT    DIV  RECK,L    T#MONT    H
     C           T#TUET    DIV  RECK,L    T#TUET    H
     C           T#WEDT    DIV  RECK,L    T#WEDT    H
     C           T#THUT    DIV  RECK,L    T#THUT    H
     C           T#FRIT    DIV  RECK,L    T#FRIT    H
     C           T#SATT    DIV  RECK,L    T#SATT    H
     C                     WRITETOTAL1
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         SUNT,L
     C                     Z-ADD0         MONT,L
     C                     Z-ADD0         TUET,L
     C                     Z-ADD0         WEDT,L
     C                     Z-ADD0         THUT,L
     C                     Z-ADD0         FRIT,L
     C                     Z-ADD0         SATT,L
     C*
     C                     Z-ADD0         SUNPCN
     C                     Z-ADD0         MONPCN
     C                     Z-ADD0         TUEPCN
     C                     Z-ADD0         WEDPCN
     C                     Z-ADD0         THUPCN
     C                     Z-ADD0         FRIPCN
     C                     Z-ADD0         SATPCN
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZWARY  Fill week array
     C*
     CSR         ZZWARY    BEGSR
     C*  CONVERT DATE FROM CMD TO MDY
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE $PFMDT    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    T#DATE  60
     C*  CONVERT DATE FROM MDY TO JULIAN
     C                     MOVE T#DATE    G$DATE
     C                     Z-ADD2         DTFRMT  10
     C                     EXSR GTOJ$
     C                     Z-ADDG$JD      BEGJUL  50
     C*
     C*  Find end date of the last week.
     C*
     C                     Z-ADD0         NDAYS   50
     C           WEEK      SUB  1         NWEEK   20
     C           NWEEK     MULT 7         NDAYS
     C                     ADD  6         NDAYS
     C           BEGJUL    ADD  NDAYS     ENDJUL
     C                     Z-ADDENDJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    ENDGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE ENDGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    TODT    80
     C                     MOVE $CVD8O    $PTODT
     C*
     C*  Fill array with individual week's beg. and end date.
     C*
     C           BEGJUL    ADD  6         ENDJUL  50
     C*
     C                     DO   WEEK      N       30
     C*  Begining of the week
     C                     Z-ADDBEGJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    BEGGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE BEGGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    BEGCMD  80
     C*  End of the week
     C                     Z-ADDENDJUL    J$JD    50
     C                     EXSR JTOG$
     C                     Z-ADDJ$MM      MONTH
     C                     Z-ADDJ$DD      DAY
     C                     MOVE J$YYYY    YEAR
     C                     MOVELT#DATE    ENDGRG  6
     C                     MOVE '*MDYCMD '$CVCMD
     C                     MOVE ENDGRG    $CVD6I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     MOVE $CVD8O    ENDCMD  80
     C*
     C                     Z-ADDBEGCMD    WKB,N
     C                     Z-ADDENDCMD    WKE,N
     C           ENDJUL    ADD  1         BEGJUL
     C           BEGJUL    ADD  6         ENDJUL
     C                     ENDDO
     C*
     CSR                   ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions
 * Not used *
 * Not used *
 Average for week:
 *** Grand totals ***
**      DTE   Date headings
99/99/99 thru 99/99/99
     From 99/99/99
     Thru 99/99/99

