     F*----------------------------------------------------------------
     F*   Copyright (C) 1995 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  UPD209    Convert *PICK options to OPTIOND file.
     F*  16 February 1995
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  01 - 20   FIELD POSITION TO
     F*  21 - 40   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT KEY)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FOPTIONS IP  E           K        DISK
     FWHDEPT  IF  E           K        DISK
     FOPTIOND O   E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  *PICK  -  Picking options
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPNXTP  -  Next pallet number.
     I*    OPBPCK  -  Bulk pick definition.
     I*    OPPCUB  -  Maximum cube for PIR.
     I*    OPFPAL  -  Pallets on fixed fork lift.
     I*    OPFCUB  -  Maximum cube per fixed pallet.
     I*    OPFSTP  -  Maximum stops per fixed pallet.
     I*    OPINCL  -  Include PIR in fixed cube.
     I*    OPSRCH  -  Number of previous pallets to search for space.
     I*    OPFBLD  -  Fixed pallet building method.
     I*                1 = By piece without concern for splitting stop.
     I*                2 = By stop without splitting stop between plts.
     I*
     I* Data structure for OPDREC
     I*
     IOPDDTA      DS
     I                                       27  270OPFBLD
     I                                      117 117 OPDEND
     I*
     I* Data structure of old format.
     I*
     IOPDATA      DS
     I                                      117 117 OPTEND
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Define partial key for WHDEPT file.
     C*
     C           KEYWD     KLIST
     C                     KFLD           OPWHSE
     C*
     C*    Create one OPTIOND record for each department.
     C*
     C           OPCODE    IFEQ '*PICK   '
     C                     MOVE OPCODE    OPDCOD
     C                     Z-ADDOPWHSE    OPDWHS
     C                     MOVE OPDATA    OPDDTA
     C                     Z-ADD1         OPFBLD
     C           KEYWD     SETLLWHDEPT
     C                     MOVE *OFF      *IN79
     C           *IN79     DOUEQ*ON
     C           KEYWD     READEWHDEPT                   79
     C           *IN79     IFEQ *OFF
     C                     MOVE WDCODE    OPDWDP
     C                     WRITEOPDREC
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
