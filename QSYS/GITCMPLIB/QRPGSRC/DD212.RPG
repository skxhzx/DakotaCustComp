     F*----------------------------------------------------------------
     F*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  DD212     Populate TIEHIGH file.
     F*  24 April, 1996
     F*  Hemant Kapadia
     F*
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
217 AF*    12/05/96  HNK  2.17
     F*      - ZZDEL subroutine executed when $PCLRF = 'Y' instead of
     F*        'N'.
     F*      - Record not added to TIEHIGH file, if Tie/High value= 0
     F*
404 AF*    07/23/97  DAS  4.04
     F*      - Added section.
408 AF*    01/20/98  RH   4.08
     F*      - Added new definitions only option and ITEMDEF file.
410 AF*    12/16/98  RH   4.10
     F*      - Change to round length and width to next .5
     F*      - If high of tiehigh is >1, z-add 0 to high.
     F*
500 AF*    09/01/04  MLB  5.00
     F*      - Enh: Revised program to include aisle in checks
     F*        for item definitions to create. Added SLOT6 to pgm.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IP  E           K        DISK
     FDEPTDEF IF  E           K        DISK
     FSLOTDEF IF  E           K        DISK
     FPACKAGE IF  E           K        DISK
     FPALLET  IF  E           K        DISK
408 AFITEMDEF IF  E           K        DISK
     FTIEHIGH UF  E           K        DISK                      A
500 AFSLOT6   IF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE  155
     C                     PARM           $PWHDP  5
404 AC                     PARM           $PSTYP  1
500 AC                     PARM           $PAISL  4
     C                     PARM           $PITEM 15
     C                     PARM           $HANG  155
     C                     PARM           $PCLRF  1
408 AC                     PARM           $NEWD   1
     C*----------------------------------------------------------------
     C*  Main line
     C*
408 AC           $NEWD     IFEQ 'Y'
408 AC           IDKEY     SETLLITEMDEF                  77
408 AC*   Match found
408 AC           *IN77     CABEQ*ON       SKIPIT
408 AC                     ENDIF
     C*
     C*   Round length and width.
     C*
     C                     Z-ADDITULEN    DECMAL  22
     C                     SUB  DECMAL    ITULEN
410 DC*          DECMAL    IFGE .5
410 DC*                    ADD  .5        ITULEN
410 DC*                    ENDIF
410 AC*
410 AC                     SELEC
410 AC           DECMAL    WHGT .5
410 AC                     ADD  1         ITULEN
410 AC           DECMAL    WHGT 0
410 AC                     ADD  .5        ITULEN
410 AC                     ENDSL
     C*
     C                     Z-ADDITUWDT    DECMAL  22
     C                     SUB  DECMAL    ITUWDT
410 DC*          DECMAL    IFGE .5
410 DC*                    ADD  .5        ITUWDT
410 DC*                    ENDIF
410 AC*
410 AC                     SELEC
410 AC           DECMAL    WHGT .5
410 AC                     ADD  1         ITUWDT
410 AC           DECMAL    WHGT 0
410 AC                     ADD  .5        ITUWDT
410 AC                     ENDSL
     C*
     C           SDKEY     SETLLSLOTDEF              79
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           SDKEY     READESLOTDEF                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C           DDKEY     CHAINDEPTDEF              76
     C           *IN76     IFEQ *OFF
500 AC*
500 AC*    Check if slot type exists in aisle selected by user.
500 AC           $PAISL    IFNE '*ALL'
500 AC                     EXSR ZZCKAL
500 AC*   If *YES, slot type not found in this aisle.
500 AC           NOTFND    IFEQ '*YES'
500 AC                     ITER
500 AC                     ENDIF
500 AC                     ENDIF
500 AC*
     C                     EXSR ZZGET
     C                     EXSR ZZADD
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
408 AC           SKIPIT    TAG
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for PACKAGE file.
     C*
     CSR         PKKEY     KLIST
     CSR                   KFLD           SDPAL
     CSR                   KFLD           $PHANG
     CSR                   KFLD           ITULEN
     CSR                   KFLD           ITUWDT
     C*
     C*  Define key for DEPTDEF file.
     C*
     CSR         DDKEY     KLIST
     CSR                   KFLD           ITWHSE
     CSR                   KFLD           ITWHDP
     CSR                   KFLD           ITSTYP
     CSR                   KFLD           SDCODE
     C*
     C*  Define key for SLOTDEF file.
     C*
     CSR         SDKEY     KLIST
     CSR                   KFLD           ITWHSE
500 AC*
500 AC*  Define key for SLOTDEF file.
500 AC*
500 ACSR         SLKEY     KLIST
500 ACSR                   KFLD           ITWHSE
500 ACSR                   KFLD           ITWHDP
500 ACSR                   KFLD           ITSTYP
500 ACSR                   KFLD           ITDESG
500 ACSR                   KFLD           SDCODE
500 ACSR                   KFLD           ITBLD
500 ACSR                   KFLD           ITACTV
500 ACSR                   KFLD           WKAISL
     C*
     C*  Define key for TIEHIGH file.
     C*
     CSR         THKEY     KLIST
     CSR                   KFLD           THWHSE
     CSR                   KFLD           THITEM
     CSR                   KFLD           THWHDP
     CSR                   KFLD           THSTYP
     CSR                   KFLD           THSDEF
     CSR                   KFLD           THPAL
     CSR                   KFLD           THHANG
     C*
     C*  Define PARTIAL key for TIEHIGH file.
     C*
     CSR         THKEY1    KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*  Define key for PALLET file.
     C*
     CSR         PLKEY     KLIST
     CSR                   KFLD           SDPAL
408 AC*
408 AC*  Define key for ITEMDEF file.
408 AC*
408 ACSR         IDKEY     KLIST
408 ACSR                   KFLD           ITWHSE
408 ACSR                   KFLD           ITITEM
     C*
     CSR                   EXSR ZZINZ2
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZADD   Add records to TIEHIGH file.
     C*
     CSR         ZZADD     BEGSR
     CSR         THKEY     CHAINTIEHIGH              77
     CSR         *IN77     IFEQ *ON
217 ACSR         THTIE     ANDGT0
217 ACSR         THHIGH    ANDGT0
     CSR                   WRITETHREC
     CSR                   ENDIF
     CSR                   ENDSR
500 AC*---------------------------------------------------------------
500 AC*
500 AC*  ZZCKAL   Check to see if slot def exists in aisle.
500 AC*
500 ACSR         ZZCKAL    BEGSR
500 ACSR                   MOVE '*NO '    NOTFND  4
500 ACSR         SLKEY     SETLLSLOT6
500 ACSR         SLKEY     READESLOT6                    75
500 ACSR         *IN75     IFEQ *ON
500 ACSR                   MOVE '*YES'    NOTFND
500 ACSR                   ENDIF
500 ACSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZDEL   Delete records first that match selection criteria.
     C*
     CSR         ZZDEL     BEGSR
     CSR         THKEY1    SETLLTIEHIGH              79
     CSR                   MOVE *IN79     EOF     1
     CSR         EOF       DOWEQ*OFF
     CSR         THKEY1    READETIEHIGH                  79
     CSR                   MOVE *IN79     EOF
     CSR         *IN79     IFEQ *OFF
     C*
     CSR         $PWHDP    IFNE '*ALL '
     CSR         THWHDP    ANDNE$PWHDP
     CSR                   ITER
     CSR                   ENDIF
404 AC*
404 ACSR         $PSTYP    IFNE 'B'
404 ACSR         THSTYP    ANDNE$PSTYP
404 ACSR                   ITER
404 ACSR                   ENDIF
     C*
     CSR         $PITEM    IFNE '*ALL '
     CSR         THITEM    ANDNE$PITEM
     CSR                   ITER
     CSR                   ENDIF
     C*
     CSR                   DELETTHREC
     CSR                   ENDIF
     CSR                   ENDDO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZGET   Get all info needed for TIEHIGH record.
     C*
     CSR         ZZGET     BEGSR
     CSR                   Z-ADDITWHSE    THWHSE
     CSR                   MOVE ITWHDP    THWHDP
     CSR                   MOVE ITSTYP    THSTYP
     CSR                   MOVE ITITEM    THITEM
     CSR                   MOVE SDCODE    THSDEF
     CSR                   MOVE SDPAL     THPAL
     CSR                   Z-ADD$PHANG    THHANG
     CSR         PKKEY     SETLLPACKAGE              78
     CSR         *IN78     IFEQ *ON
     CSR                   Z-ADD0         THTIE
     CSR                   ELSE
     CSR         PKKEY     READEPACKAGE                  78
     CSR         *IN78     IFEQ *OFF
     CSR                   Z-ADDPKTIE     THTIE
     CSR                   ELSE
     CSR                   Z-ADD0         THTIE
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         PLKEY     CHAINPALLET               78
     CSR         *IN78     IFEQ *OFF
     CSR                   Z-ADDPLHGT     PALHGT  30
     CSR                   ELSE
     CSR                   Z-ADD0         PALHGT
     CSR                   ENDIF
     C*
     CSR         SDHGT     SUB  PALHGT    T#HGT   30
     CSR         ITUHGT    IFNE 0
     CSR         T#HGT     DIV  ITUHGT    THHIGH
     CSR                   ELSE
     CSR                   Z-ADD0         THHIGH
     CSR                   ENDIF
     C*
     CSR         THHIGH    IFLT 1
410 DC*R                   Z-ADD1         THHIGH
410 MCSR                   Z-ADD0         THHIGH
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  ZZINZ2  Program specific initialization.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*  Delete existing records from tie/high file.
     C*
     CSR                   Z-ADD$WHSE     $PWHSE  30
     CSR                   Z-ADD$HANG     $PHANG  32
500 AC*
500 AC*    Move passed in Aisle to work field same size as aisle in slot.
500 AC           $PAISL    IFNE '*ALL'
500 AC                     MOVE $PAISL    WKAISL
500 AC           *LIKE     DEFN SLAISL    WKAISL
500 AC                     ENDIF
500 AC*
217 DC*R         $PCLRF    IFEQ 'N'
217 ACSR         $PCLRF    IFEQ 'Y'
     CSR                   EXSR ZZDEL
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
