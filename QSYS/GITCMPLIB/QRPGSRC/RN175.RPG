     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  RN175   Customer Returns - Update
     F*  19 October 2001
     F*  Max Blacknik
     F*
     F*  Revisions
416 AF*   - Created
416aAF*    12/03/01  RH   4.16a
     F*      - ORRTN file changed.
416bAA*    Ver 4.16b 07/03/02  RH
     F*      - ORRTN file changed.
416c!A*    10/18/02  MLB  4.16c
     F*      - Fix: Added UNLCK for file ORRTN1.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FORRTN1  UF  E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*
     I*----------------------------------------------------------------
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Message text(s)
     I*
     I              'Qty Chg @ Close. Trn-C         TXT01
     I              ':'
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse number
     C*      $PPBAT  Batch number
     C*      $PTRN#  Transaction number
     C*      $PITEM  Item number
     C*      $PRQTY  Return quantity
     C*      $PUCOD  Return unit of measure code (from label rcd)
     C*      $PUTYP  Return unit of measure type (from label rcd)
     C*      $PCWGT  Catchweight flag
     C*      $PSWGT  Shipping weight
     C*      $PUM2   Breakdown 2 unit of measure
     C*      $PUMQ2  Breakdown qty 2
     C*      $PUM3   Breakdown 3 unit of measure
     C*      $PUMQ3  Breakdown qty 3
     C*
     C*    Returned Parameters
     C*      $PSTAT  Status
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PPBAT  70
     C                     PARM           $PTRN#  70
     C                     PARM           $PITEM 15
     C                     PARM           $PRQTY  70
     C                     PARM           $PUCOD  2
     C                     PARM           $PUTYP  1
     C                     PARM           $PCWGT  1
     C                     PARM           $PSWGT  72
     C                     PARM           $PUM2   2
     C                     PARM           $PUMQ2  30
     C                     PARM           $PUM3   2
     C                     PARM           $PUMQ3  30
     C                     PARM           $PSTAT  8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $PSTAT
     C                     MOVE $PTRN#    WKTRNA
     C*
     C           RNKEY     SETLLORRTN1
     C           RNKEY     READEORRTN1                   79
     C                     SELEC
     C*
     C*    Write out new record with changes in quantity.
     C           *IN79     WHEQ *OFF
     C*
     C*    If a catchweight was entered,  re-adjust catchweight based on
     C*    unit weight from item master and adjusted quantities.
     C           ORCWTR    IFNE *ZEROS
     C           $PSWGT    ANDGT*ZEROS
     C                     EXSR ZZCWGT
     C                     ENDIF
     C*
     C*
     C*  Convert today's date into century format.
     C*
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    ORRDTE
     C*
     C                     TIME           ORRTIM
     C                     MOVE #USER     ORRUSR
     C*
     C                     SELEC
     C*    Load new Case qty into return record.
     C           $PUTYP    WHEQ 'N'
     C                     Z-ADD$PRQTY    ORQTY1
     C                     MOVE *ZEROS    ORQTY2
     C                     MOVE *ZEROS    ORQTY3
     C*    Load new Breakdown qty 1 into return record.
     C           $PUTYP    WHEQ '1'
     C                     Z-ADD$PRQTY    ORQTY2
     C                     MOVE *ZEROS    ORQTY1
     C                     MOVE *ZEROS    ORQTY3
     C*    Load new Breakdown qty 2 into return record.
     C           $PUTYP    WHEQ '2'
     C                     Z-ADD$PRQTY    ORQTY3
     C                     MOVE *ZEROS    ORQTY1
     C                     MOVE *ZEROS    ORQTY2
     C*
     C                     ENDSL
     C*
     C                     MOVE 'U'       ORTYPE
     C           TXT01     CAT  WKTRNA:1  ORRMEM    P
     C                     MOVE *ZEROS    ORUDTE
     C                     MOVE *ZEROS    ORUTIM
     C                     WRITEORREC                  78
     C                     MOVE '*OK     '$PSTAT
     C                     ENDSL
     C*
416cAC*    Do cleanup before leaving.
416cAC                     UNLCKORRTN1
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define key for ORRTN1 file.
     C*
     CSR         RNKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PPBAT
     CSR                   KFLD           $PITEM
     C*
     C*    Define working variables.
     C*
     CSR                   MOVE *BLANKS   WKTRNA  7
     CSR         *LIKE     DEFN ORCWTR    EXWGT
     CSR         *LIKE     DEFN ORCWTR    EXWGT2
     CSR         *LIKE     DEFN ORCWTR    EXWGT3
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCWGT   Calculate Catchweight Amount Based on Quantity.
     C*
     CSR         ZZCWGT    BEGSR
     C*
     CSR                   MOVE *ZEROS    EXWGT
     CSR                   MOVE *ZEROS    EXWGT2
     CSR                   MOVE *ZEROS    EXWGT3
     C*
     C*
     C*    Compute new catchweight for return.
     CSR                   SELEC
     C*
     C*    Compute new Case catchweight.
     CSR         $PUTYP    WHEQ 'N'
     CSR         $PRQTY    ANDNE*ZEROS
     C*
     CSR         $PRQTY    MULT $PSWGT    EXWGT
     C*
     C*    Compute new Breakdown qty 1 catchweight.
     CSR         $PUTYP    WHEQ '1'
     CSR         $PRQTY    ANDNE*ZEROS
     C*
     CSR         $PUMQ2    IFNE *ZEROS
     CSR         $PSWGT    DIV  $PUMQ2    EXWGT2
     CSR                   ENDIF
     CSR         $PRQTY    MULT EXWGT2    EXWGT2
     CSR                   ADD  EXWGT2    EXWGT
     C*
     C*    Compute new Breakdown qty 2 catchweight.
     CSR         $PUTYP    WHEQ '2'
     CSR         $PRQTY    ANDNE*ZEROS
     C*
     CSR         $PUMQ3    IFNE *ZEROS
     CSR         $PSWGT    DIV  $PUMQ3    EXWGT3
     CSR                   ENDIF
     CSR         $PRQTY    MULT EXWGT3    EXWGT3
     CSR                   ADD  EXWGT3    EXWGT
     C*
     CSR                   ENDSL
     C*
     C*    Update Customer Return record with new weight.
     CSR                   Z-ADDEXWGT     ORCWTR
     C*
     CSR                   ENDSR
     C*---------------------------------------------------------------
