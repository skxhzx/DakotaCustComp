     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 8, Suite 51
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETALSW   Selection window for Alias item info for base item
     F*
     F*  14 October 2008
     F*  Roxanne Harrison
     F*
     F*  Revisions
     F*    -Created using GETSSB  V5.20
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FGETALSFMCF  E                    WORKSTN
     F                                        RECNO KSFILE WINDWS
     FPIRITEM IF  E           K        DISK
     FITEMLNK1IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITEM   -  Base item
     I*
     I*  Output parameters
     I*
     I*     $PSITM   - item number selection
     I*     $PRTN    - *FOUND   -  Alias item found for base item
     I*                *NOTFND  -  Alias item not found for base item
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PITEM 15
     C                     PARM           $PSITM 15
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     Z-ADD0         RECNO   20
     C                     MOVE *ON       *IN98
     C                     WRITEWINDWSC
     C                     MOVE *OFF      *IN98
     C                     CLEAR$PSITM
     C                     MOVE '*NOTFND '$PRTN
     C                     MOVE $PWHSE    WHSE    30
     C                     MOVE *BLANKS   WSEL
     C*
     C*    Loop through linked items for Base item.
     C*
     C           KEYIL1    SETLLITEMLNK1
     C           FOREVR    DOWEQFOREVR
     C           KEYIL1    READEITEMLNK1                 77
     C           *IN77     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*      Get item record.
     C*
     C           KEYIT     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     ITER
     C                     ENDIF
     C*
     C*      Looking for Alias item.
     C*
     C           ITTYPE    IFNE 'A'
     C                     ITER
     C                     ENDIF
     C*
     C*      Found Alias item.
     C*
     C                     MOVE '*FOUND  '$PRTN
     C                     MOVE ITITEM    WAITEM
     C                     ADD  1         RECNO
     C                     WRITEWINDWS
     C*
     C                     ENDDO
     C                     Z-ADD1         RECNO
     C                     EXFMTWINDWSC
     C           *INKL     IFNE '1'
     C                     READCWINDWS                   91
     C           *IN91     IFNE '1'
     C                     MOVE WAITEM    $PSITM
     C                     ENDIF
     C                     ENDIF
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           ILWHSE
     CSR                   KFLD           ILITEM
     C*
     C*  Define key for ITEMLNK1 file.
     C*
     CSR         KEYIL1    KLIST
     CSR                   KFLD           WHSE
     CSR                   KFLD           $PITEM
     C*
     CSR                   ENDSR
