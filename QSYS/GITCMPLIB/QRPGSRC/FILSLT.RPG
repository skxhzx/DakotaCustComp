     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FILSLT    Fill pick slots - % of Tie/High
     F*  10 June 1999
     F*  Hemant Kapadia
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
413 AF*  01/13/00  HNK  4.13
     F*    - Do not fill slot if it is at a status of 'V' and it's a
     F*      pick slot.
     F*
     F*  Note: This program must be run every morning before receiving.
     F*        The program fills pick slots to it's Tie/Hi value, so
     F*        that product cannot be received into. At startup all
     F*        slots are defined as Fixed slots. Everything except
     F*        picking and will calls will be done on BFC side.  When
     F*        ready to do picking, inventory of all pick slots will be
     F*        taken and then this program should be deleted from the
     F*        system so that it is not run accidentally.
     F*
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FITEMDEF IF  E           K        DISK
     FSLOT3   UF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     I*----------------------------------------------------------------
     I*  Misc. data structures
     I*
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C*----------------------------------------------------------------
     C*  File keys
     C*
     C*    ITEMDEF file.
     C*
     C           KEYID     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C                     KFLD           SLSDEF
     C*
     C*    SLOT3 file.
     C*
     C           KEYSL     KLIST
     C                     KFLD           $PWHSE
     C*
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*
     C*  Update slot file.
     C*
     C                     EXSR ZZUPD
     C*
     C*  Do some clean up before leaving.
     C*
     C           OUT       TAG
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   MOVE *OFF      FOREVR  1
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD   Update pick slots only.
     C*
     CSR         ZZUPD     BEGSR
     C*
     C*  Read slot file.
     C*
     CSR         KEYSL     SETLLSLOT3
     C*
     CSR         FOREVR    DOWEQFOREVR
     C*
     C*     Read record from slot file.
     C*
     CSR         KEYSL     READESLOT3                    79
     C*
     CSR         *IN79     IFEQ *ON
     CSR                   LEAVE
     CSR                   ENDIF
     C*
     CSR         SLWHSE    IFNE $PWHSE
     CSR                   ITER
     CSR                   ENDIF
     C*
     CSR         SLPICK    IFEQ 'N'
     CSR                   ITER
     CSR                   ENDIF
     C*
413 ACSR         SLSTAT    IFEQ 'V '
413 ACSR                   ITER
413 ACSR                   ENDIF
     C*
     CSR                   EXSR GETQTY
     CSR                   Z-ADDW1QTY     SLSTK1
     CSR                   UPDATSLREC
     C*
     CSR                   ENDDO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  GETQTY  Calculate qty.
     C*
     CSR         GETQTY    BEGSR
     C*
     CSR         KEYID     CHAINITEMDEF              77
     CSR                   Z-ADD0         W1QTY
     CSR         *IN77     IFEQ *OFF
     CSR         IDTIE     MULT IDHIGH    W1QTY   50
     CSR                   ELSE
     CSR                   Z-ADD50        W1QTY
     CSR                   ENDIF
     C*
     CSR                   ENDSR
