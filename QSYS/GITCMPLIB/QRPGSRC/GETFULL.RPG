     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETFULL   Get full case item info for broken case.
     F*  03 July 1996
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IF  E           K        DISK
     FITEMLNK IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITMB   -  Broken case item number
     I*
     I*  Output parameters
     I*
     I*     $PITMF   - Full case item number
     I*     $PDESC   - Full case description
     I*     $PPDSC   - Full case pack description
     I*     $PWHDP   - Full case department
     I*     $PSTYP   - Full case section
     I*     $PUM     - Full case unit of measure code
     I*     $PUM2    - Full case breakdown 1 unit of measure code
     I*     $PUMQ2   - Full case breakdown 1 quantity
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITMB 15
     C                     PARM           $PITMF 15
     C                     PARM           $PDESC 30
     C                     PARM           $PPDSC 15
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  1
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUMQ2  30
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     CLEAR$PITMF
     C                     CLEAR$PDESC
     C                     CLEAR$PPDSC
     C                     CLEAR$PSTYP
     C                     CLEAR$PUM1
     C                     CLEAR$PUM2
     C                     CLEAR$PUMQ2
     C*
     C*    Get corresponding full case item from link file.
     C*
     C           KEYIL     CHAINITEMLNK              79
     C           *IN79     IFEQ *OFF
     C                     MOVE ILITM2    $PITMF
     C*
     C*    Get corresponding full case item from item file.
     C*
     C           KEYIT     CHAINPIRITEM              78
     C           *IN78     IFEQ *OFF
     C                     MOVE ITDESC    $PDESC
     C                     MOVE ITPDSC    $PPDSC
     C                     MOVE ITWHDP    $PWHDP
     C                     MOVE ITSTYP    $PSTYP
     C                     MOVE ITUM1     $PUM1
     C                     MOVE ITUM2     $PUM2
     C                     Z-ADDITUMQ2    $PUMQ2
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           ILITM2
     C*
     C*  Define key for ITEMLNK file.
     C*
     CSR         KEYIL     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITMB
     C*
     CSR                   ENDSR
