     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETCON    Get Contract item qty total for Base item.
     F*  05 July 1999
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IF  E           K        DISK
     FITEMLNK1IF  E           K        DISK
     FITEMQTY IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITEM   -  Base item
     I*
     I*  Output parameters
     I*
     I*     $PCNT    - Total number of contract items.
     I*     $PQTY1   - Total normal stock qty of contract items
     I*     $PQTY2   - Total breakdown 1 stock qty of contract items
     I*     $PQTY3   - Total breakdown 2 stock qty of contract items
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PCNT   30
     C                     PARM           $PQTY1  50
     C                     PARM           $PQTY2  50
     C                     PARM           $PQTY3  50
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     CLEAR$PCNT
     C                     CLEAR$PQTY1
     C                     CLEAR$PQTY2
     C                     CLEAR$PQTY3
     C*
     C*    Loop through Contract items for Base item.
     C*
     C           KEYIL1    SETLLITEMLNK1
     C           FOREVR    DOWEQFOREVR
     C           KEYIL1    READEITEMLNK1                 77
     C           *IN77     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*      Get item record.
     C*
     C           KEYIT     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     ITER
     C                     ENDIF
     C*
     C*      Only work with Contract items.
     C*
     C           ITTYPE    IFNE 'C'
     C                     ITER
     C                     ENDIF
     C*
     C*      Get item quantity record for Contract item.
     C*
     C           KEYIT     CHAINITEMQTY              79
     C           *IN79     IFEQ *ON
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  1         $PCNT
     C                     ADD  IQSTK1    $PQTY1
     C                     ADD  IQSTK2    $PQTY2
     C                     ADD  IQSTK3    $PQTY3
     C*
     C                     ENDDO
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           ILWHSE
     CSR                   KFLD           ILITEM
     C*
     C*  Define key for ITEMLNK1 file.
     C*
     CSR         KEYIL1    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     C*
     CSR                   ENDSR
