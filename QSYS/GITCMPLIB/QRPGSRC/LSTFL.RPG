     F*  (c) COPYRIGHT GFG FOODSERVICE, INC.
     F*       PROGRAM PROPERTY OF GFG
     F*
     F* LIBRARY NAME            - GFGTOOL
     F*
     F* ENVIRONMENT             - NATIVE
     F*
     F* PROGRAM NAME            - LSTFL
     F*
     F* TITLE                   - LIST FILE LAYOUTS
     F*
     F* FUNCTION                - LIST FILE LAYOUT DOCUMENTATION
     F*
     F* CHANGE ACTIVITY         - FIXED OVERFLOW ERROR WHEN BUILDING      MLB
     F*                           KEY FIELD ARRAY. (PTF01  02-94)         MLB
     F*
     F*                         - ADDED CODE TO SUPPORT 'B'INARY FIELDS   MLB
     F*                           $CALC ROUTINE. (PTF02  04-98)           MLB
     F*
     F* CALLING PROGRAMS)       - LSTFLCL
     F*
     F* INPUT FILE(S)           - SOURCE  (MEMBER SOURCE)
     F*                         - SOURCE1 (ACCESS INFO)
     F*
     F* UPDATE FILE(S)          - NONE
     F*
     F* OUTPUT FILE(S)          - LSTFLP
     F*
     F* RECEIVED EXTERNAL DATA  - OPT  - DISPLAY OPTION
     F*
     F* PASSED EXTERNAL DATA    - NONE
     F*
     F* DISPLAYS : DEVICE/FILE  - NONE
     F*
     F*            SOURCE MBR   -
     F*
     F*            FORMAT(S) &  -
     F*
     F*            SCREEN ID(S) -
     F*
     F* REPORT(S)               - FILE LAYOUT
     F*
     F* OPERATOR RESPONSE(S)    - NONE
     F*
     F* MESSAGE(S)              - NONE
     F*
     F* SUBROUTINE(S)           - $CALC   - SETUP LINE INFO
     F*                           $CLRSF  - CLEAR SUBFILE
     F*                           $FILSF  - FILL THE SUBFILE
     F*                           $FIRST  - FIRST TIME PROCESS
     F*                           $PRTHD  - PRINT HEADING
     F*                           $PRTLN  - PRINT DETAIL LINE
     F*
     F* INDICATOR ASSIGNMENT
     F*
     F* ID F  C  H  L        FUNCTION OF INDICATOR(S)
     F*       22             SUBFILE FULL
     F*       36             ROLL UP
     F*       40             NUMERIC FIELD
     F*       41             PACKED FIELD
     F*       42             BINARY FIELD                                 PTF02
     F*       45             KEYED ACCESS FILE
     F*       46             SECOND LINE OF FILE KEY FIELDS               PTF01
     F*       55             CLEAR SUBFILE
     F*       56             SUBFILE EMPTY
     F*       71             SUBFILE END
     F*       90             FIRST TIME
     F*       95             END OF KEY FIELDS
     F*       98             PAGE OVERFLOW
     F*       99             EOF ON SOURCE1
     F*             LR       PROGRAM END; EOF ON SOURCE
     F*
     FLSTFLFM CF  E                    WORKSTN                        UC
     F                                        RECNUMKSFILE SUBFILE
0041 FSOURCE  IF  F     488            DISK
0041 FSOURCE1 IF  F     596            DISK
0041 FSOURCE2 IF  F     118            DISK
0048 FLSTFLP  O   E             98     PRINTER
     E                    KEY        60  1
    AE                    KEY1       60  1                                PTF01
     E                    KNAM       10  1
     ISOURCE  NS
     I                                        1  10 FILE
     I                                       11  20 LIB
     I                                       28  28 FTYP
     I                                       47  56 NAME
     I*                                      70 119 TEXT
     I                                      125 1290RLEN
     I                                      130 139 FLDI
     I                                      140 149 FLDE
     I                                      150 1540FOBO
     I                                      160 1640FLDB
     I                                      165 1660FLDD
     I                                      167 1680FLDP
     I                                      169 218 FTXT
     I                                      169 188 TXT20
     I                                      322 322 FLDT
     ISOURCE1 NS
     I                                       71 120 TEXT
     I                                      148 148 ACCP
     I                                    P 280 2810MXKL
     ISOURCE2 NS
     I                                    P  99 1000APNKYF
     I                                      101 110 KEYF
     I                                      111 111 KSEQ
     I*
     I            DS
     I*       ***   DATA STRUCTURES   ***
     I* SIZING
     I                                        1   50RECNUM
0081 I            DS
0082 I                                        1   50START
0082 I                                        8   90LINNR
0082 I                                       10  110X
0082 I                                       12  130Y
0081 I            DS
0082 I                                        1  60 KEYS
0082 I                                       61  61 ON
0082 I                                       62  62 OFF
    AI                                       63 122 KEYS1                 PTF01
    AI                                      123 182 KEYS2                 PTF01
     I*
     C* MAINLINE PROCESS
     C           *INLR     DOWEQ'0'
     C                     READ SOURCE                   LR
     C           *INLR     CABEQON        #END
     C           *IN90     IFEQ '0'
     C                     EXSR $FIRST                     FIRST TIME
     C                     END
     C                     EXSR $CALC                      CALC POS.
     C           OPT       CASEQ'*PRINT'  $PRTLN           PRINT LINE
     C           OPT       CASEQ'*'       $FILSF           FILL SUBFIL
     C                     END
     C           LINNR     IFEQ 45                         IF PAGE FULL
     C                     EXSR $PRTHD                     PRINT HEADING
     C                     END
     C           #END      TAG
     C                     END
     C           OPT       IFEQ '*'
     C                     WRITEFKEYS                      FUNCTION KEYS
     C                     EXFMTSUBCTL
     C                     END
     C                     RETRN
     C**
     C****************************************************** SUBROUTINES *
     C*
     C* SETUP
     C           $FIRST    BEGSR                           $FIRST        *
     C           *ENTRY    PLIST
     C                     PARM           OPT     6
     C                     MOVE '1'       ON
     C                     MOVE '0'       OFF
     C                     Z-ADD0         LINNR
     C                     Z-ADD0         LEN
     C                     Z-ADD0         NRKYF
     C                     Z-ADD1         X
    AC                     SELEC                                          MLB
    AC           OPT       WHEQ '*'                        Display only   MLB
    AC                     OPEN LSTFLFM                                   MLB
    AC                     ENDSL                                          MLB
     C                     READ SOURCE1                  99
     C           FTYP      IFEQ 'P'
     C                     MOVE 'Physical'TYPN    8        PHY FILE
     C                     ELSE
     C                     MOVE 'Logical 'TYPN             LGL FILE
     C                     END
     C                     MOVE *BLANKS   ACCES
     C                     MOVE OFF       *IN45
     C           ACCP      IFEQ 'K'
     C                     MOVE 'Keyed  ' ACCES   7        KEYED ACCESS
     C                     MOVE ON        *IN45
     C                     END
     C           ACCP      IFEQ 'A'
     C                     MOVE 'Arrival' ACCES            ARRIVAL SEQ
     C                     END
     C           ACCP      IFEQ 'S'
     C                     MOVE 'Shared ' ACCES            SHARED ACESS
     C                     END
     C*
     C           *IN95     DOWEQOFF
     C                     READ SOURCE2                  95
     C           *IN95     CABEQON        #ENDS
     C                     ADD  1         NRKYF   30
     C           NRKYF     CABGTAPNKYF    #ENDS            JOIN FILE
     C*
     C           X         IFNE 1
     C                     SELEC
     C           *IN46     WHEQ *OFF
     C           *IN47     ANDEQ*OFF
     C           ' '       CHEKRKEYF      NUM     30
     C           ' '       CHEKRKEYS      NUMKY   30
     C                     Z-ADDNUMKY     LEN
     C                     ADD  NUM       LEN     50
     C                     ADD  3         LEN
     C           *IN46     WHEQ *ON
     C           *IN47     ANDEQ*OFF
     C           ' '       CHEKRKEYF      NUM     30
     C           ' '       CHEKRKEYS1     NUMKY   30
     C                     Z-ADDNUMKY     LEN
     C                     ADD  NUM       LEN
     C                     ADD  3         LEN
     C           *IN46     WHEQ *ON
     C           *IN47     ANDEQ*ON
     C           ' '       CHEKRKEYF      NUM     30
     C           ' '       CHEKRKEYS2     NUMKY   30
     C                     Z-ADDNUMKY     LEN
     C                     ADD  NUM       LEN
     C                     ADD  3         LEN
    AC                     ENDSL
     C*
     C                     SELEC
     C           LEN       WHGT 60
     C           *IN46     ANDEQ*OFF
     C           *IN47     ANDEQ*OFF
     C                     MOVE *ZEROS    LEN
     C                     Z-ADD1         X
     C                     MOVE *ON       *IN46
     C           LEN       WHGT 60
     C           *IN46     ANDEQ*ON
     C           *IN47     ANDEQ*OFF
     C                     MOVE *ZEROS    LEN
     C                     Z-ADD1         X
     C                     MOVE *ON       *IN47
     C                     ENDSL
    AC                     ENDIF
     C*
     C                     SELEC
     C           *IN46     WHEQ *OFF
     C           *IN47     ANDEQ*OFF
     C           X         IFGT 1
     C           KEYS      CAT  '/':0     KEYS
     C                     ENDIF
     C           KEYS      CAT  KEYF:0    KEYS
     C           KEYS      CAT  ',':0     KEYS
     C           KEYS      CAT  KSEQ:0    KEYS
     C           *IN46     WHEQ *ON
     C           *IN47     ANDEQ*OFF
     C           X         IFGT 1
     C           KEYS1     CAT  '/':0     KEYS1
     C                     ENDIF
     C           KEYS1     CAT  KEYF:0    KEYS1
     C           KEYS1     CAT  ',':0     KEYS1
     C           KEYS1     CAT  KSEQ:0    KEYS1
     C           *IN46     WHEQ *ON
     C           *IN47     ANDEQ*ON
     C           X         IFGT 1
     C           KEYS2     CAT  '/':0     KEYS2
     C                     ENDIF
     C           KEYS2     CAT  KEYF:0    KEYS2
     C           KEYS2     CAT  ',':0     KEYS2
     C           KEYS2     CAT  KSEQ:0    KEYS2
    AC                     ENDSL
    AC                     ADD  1         X                               PTF01
     C*
     C           #ENDS     TAG
     C                     END
     C           OPT       CASEQ'*PRINT'  $PRTHD           PRINT HEADING
     C           OPT       CASEQ'*'       $CLRSF           CLEAR SUBFILE
     C                     END
     C                     MOVE ON        *IN90
     C                     ENDSR
     C*
     C* CLEAR SUBFILE
     C           $CLRSF    BEGSR                           $CLRSF        *
     C                     MOVE OFF       *IN56
     C                     MOVE ON        *IN55
     C                     WRITESUBCTL
     C                     MOVE OFF       *IN55
     C                     MOVE ON        *IN71
     C                     MOVE *ZEROS    RECNUM
     C                     ENDSR
     C*
     C* FILL SUBFILE
     C           $FILSF    BEGSR                           $FILSF        *
     C                     ADD  1         RECNUM
     C                     WRITESUBFILE                  22
     C           RECNUM    IFEQ *ZEROS
     C                     MOVE ON        *IN56
     C                     END
     C                     ENDSR
     C*
     C* CALCULATE FROM AND TO POSITIONS
     C           $CALC     BEGSR                           $CALC         *
     C                     MOVE OFF       *IN40
     C                     MOVE OFF       *IN41
    AC                     MOVE OFF       *IN42                           PTF02
    AC                     MOVE *BLANKS   FLTYP                           PTF02
     C                     Z-ADDFOBO      FROM
     C           FOBO      SUB  1         START
     C           START     ADD  FLDB      TO
     C           FLDT      IFEQ 'P'
    AC                     MOVE FLDT      FLTYP                           PTF02
     C                     MOVE ON        *IN41
     C                     MOVE ON        *IN40
     C                     Z-ADDFLDD      FLDB
     C                     END
    AC           FLDT      IFEQ 'B'                                       PTF02
    AC                     MOVE FLDT      FLTYP                           PTF02
    AC                     MOVE ON        *IN41                           PTF02
    AC                     MOVE ON        *IN42                           PTF02
    AC                     MOVE ON        *IN40                           PTF02
    AC                     Z-ADDFLDD      FLDB                            PTF02
    AC                     END                                            PTF02
     C           FLDT      IFEQ 'N'
     C           FLDT      OREQ 'S'
     C                     MOVE ON        *IN40
     C                     Z-ADDFLDD      FLDB
     C                     END
     C                     ENDSR
     C*
     C* PRINT LINE
     C           $PRTLN    BEGSR                           $PRTLN        *
     C                     WRITEBODY$
     C           LINNR     ADD  1         LINNR
     C                     ENDSR
     C*
     C* PRINT HEADING
     C           $PRTHD    BEGSR                           $PRTHD        *
0136 C                     WRITEHEAD$
     C                     Z-ADD0         LINNR
     C                     ENDSR
     C*
