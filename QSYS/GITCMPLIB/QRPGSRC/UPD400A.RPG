     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  UPD400A   Initialize RTEHED/RTESUM from existing orders.
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FORDH    UP  E           K        DISK
     FORDD    IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FRTESUM  UF  E           K        DISK                      A
     FRTEHED  O   E           K        DISK                      A
     C*----------------------------------------------------------------
     C* Main line
     C*
     C*
     C*  Add record to route header file when route changes.
     C*
     C           OHRTE     IFNE LSTRTE
     C                     EXSR FILLRH
     C                     WRITERHREC                  49
     C                     MOVE OHRTE     LSTRTE
     C                     ENDIF
     C*
     C*  Update order header with route id.
     C*
     C                     MOVE RHRTID    OHRTE
     C                     UPDATOHREC                  49
     C*
     C*  Upate route summary with detail information.
     C*
     C           KEYOD     SETLLORDD
     C           FOREVR    DOWEQFOREVR
     C*
     C           KEYOD     READEORDD                     78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           KEYIT     CHAINPIRITEM              76
     C           *IN76     IFEQ *OFF
     C                     EXSR UPDSUM
     C                     ENDIF
     C*
     C                     ENDDO
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C* Key definitions
     C*
     C* Order detail file
     C*
     CSR         KEYOD     KLIST
     CSR                   KFLD           OHWHSE
     CSR                   KFLD           OHORD
     C*
     C* Item file
     C*
     CSR         KEYIT     KLIST
     CSR                   KFLD           ODWHSE
     CSR                   KFLD           ODITEM
     C*
     C* Route summary file
     C*
     CSR         KEYRS     KLIST
     CSR                   KFLD           RHWHSE
     CSR                   KFLD           RHRTID
     CSR                   KFLD           ITWHDP
     C*
     C* Initialize variables
     C*
     CSR         *LIKE     DEFN OHRTE     LSTRTE
     CSR                   MOVE '@@@@@'   LSTRTE
     C*
     CSR                   ENDSR
400 AC*----------------------------------------------------------------
400 AC*
400 AC*  FILLRH   Fill route header fields.
400 AC*
400 ACSR         FILLRH    BEGSR
400 AC*
400 AC*  Initialize record
400 AC*
400 ACSR                   CLEARRHREC
400 AC*
400 AC*  Update fields.
400 AC*
400 ACSR                   Z-ADDOHWHSE    RHWHSE
400 ACSR                   CALL 'PIRRTE#'
400 ACSR                   PARM RHWHSE    $RWHSE  30
400 ACSR                   PARM ' '       RHRTID
400 ACSR                   MOVE OHRTE     RHRTE
400 ACSR                   MOVE OHTYPE    RHTYPE
400 ACSR                   MOVE OHTRUK    RHTRUK
    ACSR                   SELEC
    ACSR         OHSTAT    WHEQ 'C'
400 ACSR                   MOVE '6'       RHSTAT
    ACSR         OHSTAT    WHEQ 'P'
400 ACSR                   MOVE '4'       RHSTAT
    ACSR                   OTHER
400 ACSR                   MOVE '1'       RHSTAT
    ACSR                   ENDSL
400 ACSR                   Z-ADDOHSDTE    RHSTSD
400 ACSR                   Z-ADDOHSTIM    RHSTST
400 ACSR                   Z-ADDOHSDTE    RHIMPD
400 ACSR                   Z-ADDOHSTIM    RHIMPT
400 ACSR                   Z-ADDOHSDTE    RHRPTD
400 AC*
400 ACSR                   ENDSR
400 AC*----------------------------------------------------------------
400 AC*
400 AC*  UPDSUM   Update route summary file with detail record.
400 AC*
400 ACSR         UPDSUM    BEGSR
400 AC*
400 AC*  Define work fields
400 AC*
400 ACSR         *LIKE     DEFN ITCUBE    TMPCUB
400 ACSR         *LIKE     DEFN ITSWGT    TMPWGT
400 AC*
400 AC*  Try to get existing summary records for department.
400 AC*
400 ACSR         KEYRS     CHAINRTESUM               77
400 AC*
400 AC*     Record does not exist - Initialize fields.
400 AC*
400 ACSR         *IN77     IFEQ *ON
400 ACSR                   Z-ADDRHWHSE    RSWHSE
400 ACSR                   MOVE RHRTID    RSRTID
400 ACSR                   MOVE ITWHDP    RSWHDP
400 ACSR                   Z-ADD0         RSORDC
400 ACSR                   Z-ADD0         RSORDW
400 ACSR                   Z-ADD0         RSORDP
400 ACSR                   Z-ADD0         RSPCKC
400 ACSR                   Z-ADD0         RSPCKW
400 ACSR                   Z-ADD0         RSPCKP
400 ACSR                   ENDIF
400 AC*
400 AC*     Update department totals.
400 AC*
400 ACSR                   ADD  ODQOR1    RSORDP
400 ACSR         ITCUBE    MULT ODQOR1    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    MULT ODQOR1    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 AC*
400 ACSR                   ADD  ODQOR2    RSORDP
400 ACSR         ODQOR2    IFNE 0
400 ACSR         ITUMQ2    ANDGT0
400 ACSR         ITCUBE    DIV  ITUMQ2    TMPCUB    H
400 ACSR         TMPCUB    MULT ODQOR2    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    DIV  ITUMQ2    TMPWGT    H
400 ACSR         TMPWGT    MULT ODQOR2    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 ACSR                   ENDIF
400 AC*
400 ACSR                   ADD  ODQOR3    RSORDP
400 ACSR         ODQOR3    IFNE 0
400 ACSR         ITUMQ3    ANDGT0
400 ACSR         ITCUBE    DIV  ITUMQ3    TMPCUB    H
400 ACSR         TMPCUB    MULT ODQOR3    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    DIV  ITUMQ3    TMPWGT    H
400 ACSR         TMPWGT    MULT ODQOR3    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 ACSR                   ENDIF
400 AC*
400 AC*     Add/Update summary record.
400 AC*
400 ACSR         *IN77     IFEQ *ON
400 ACSR                   WRITERSREC
400 ACSR                   ELSE
400 ACSR                   UPDATRSREC
400 ACSR                   ENDIF
400 AC*
400 ACSR                   ENDSR
