     F*----------------------------------------------------------------
     F*
     F*  INZTPH    Initialize TRANPH file.
     F*  28 December, 1998
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
     FTRANPH  UF  E           K        DISK                      A
     F            TPREC                             KRENAMERECORD
     FTASKDET IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  30 ERFLD
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  OTHER DATA STRUCTURE
     I*
     I            DS
     I                                        1   60ETIME
     I                                        1   40EHHMM
     I                                        5   60ESS
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PTASK  Task code or *ALL
     C*      $PWHSE  Warehouse  or *ALL
     C*      $PWHDP  Department or *ALL
     C*      $PSTYP  Section or *ALL
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PTASK  6
     C                     PARM           $PWHSE  4
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  4
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C           $PTASK    IFNE '*ALL  '
     C                     MOVE $PTASK    PTASK
     C                     ELSE
     C                     MOVE *BLANKS   PTASK
     C                     ENDIF
     C*
     C           $PWHSE    IFNE '*ALL'
     C                     MOVE $PWHSE    WHSE
     C                     ELSE
     C                     Z-ADD0         WHSE
     C                     ENDIF
     C*
     C           $PWHDP    IFNE '*ALL '
     C                     MOVE $PWHDP    WHDP
     C                     ELSE
     C                     MOVE *BLANKS   WHDP
     C                     ENDIF
     C*
     C           $PSTYP    IFNE '*ALL'
     C                     MOVE $PSTYP    STYP
     C                     ELSE
     C                     MOVE *BLANKS   STYP
     C                     ENDIF
     C*
     C                     SELEC
     C           $PTASK    WHEQ '*ALL  '
     C           $PWHSE    ANDEQ'*ALL'
     C           $PWHDP    ANDEQ'*ALL '
     C           $PSTYP    ANDEQ'*ALL'
     C*
     C           *LOVAL    SETLLTASKDET
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C                     READ TASKDET                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C           $PTASK    WHNE '*ALL  '
     C           $PWHSE    ANDNE'*ALL'
     C           $PWHDP    ANDNE'*ALL '
     C           $PSTYP    ANDNE'*ALL'
     C*
     C           TDKEY     SETLLTASKDET
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           TDKEY     READETASKDET                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C           $PTASK    WHEQ '*ALL  '
     C           $PWHSE    ANDEQ'*ALL'
     C           $PWHDP    ANDEQ'*ALL '
     C           $PSTYP    ANDNE'*ALL'
     C*
     C           TDKEY3    SETLLTASKDET
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           TDKEY3    READETASKDET                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C           $PTASK    WHEQ '*ALL  '
     C           $PWHSE    ANDEQ'*ALL'
     C           $PWHDP    ANDNE'*ALL '
     C           $PSTYP    ANDNE'*ALL'
     C*
     C           TDKEY2    SETLLTASKDET
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           TDKEY2    READETASKDET                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C           $PTASK    WHEQ '*ALL  '
     C           $PWHSE    ANDNE'*ALL'
     C           $PWHDP    ANDNE'*ALL '
     C           $PSTYP    ANDNE'*ALL'
     C*
     C           TDKEY1    SETLLTASKDET
     C                     MOVE *IN79     EOF     1
     C           EOF       DOWEQ*OFF
     C           TDKEY1    READETASKDET                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE '0'       ERROR   1
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     C**                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C**                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK  Select task
     C*
     CSR         ZZCHK     BEGSR
     CSR                   MOVE *OFF      FLAG    1
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     CSR         *LIKE     DEFN TDCODE    PTASK
     CSR         *LIKE     DEFN TDWHSE    WHSE
     CSR         *LIKE     DEFN TDWHDP    WHDP
     CSR         *LIKE     DEFN TDSTYP    STYP
     C*
     C*  Define key for TASKDET file.
     C*
     CSR         TDKEY     KLIST
     CSR                   KFLD           PTASK
     CSR                   KFLD           WHSE
     CSR                   KFLD           WHDP
     CSR                   KFLD           STYP
     C*
     C*  Define partial key for TASKDET file.
     C*
     CSR         TDKEY1    KLIST
     CSR                   KFLD           PTASK
     C*
     C*  Define partial key for TASKDET file.
     C*
     CSR         TDKEY2    KLIST
     CSR                   KFLD           PTASK
     CSR                   KFLD           WHSE
     C*
     C*  Define partial key for TASKDET file.
     C*
     CSR         TDKEY3    KLIST
     CSR                   KFLD           PTASK
     CSR                   KFLD           WHSE
     CSR                   KFLD           WHDP
     C*
     C*  Define key for TASK file.
     C*
     CSR         TKKEY     KLIST
     CSR                   KFLD           TDCODE
     C*
     C*  Define key for TRANPH file.
     C*
     CSR         TPKEY     KLIST
     CSR                   KFLD           TDCODE
     CSR                   KFLD           TDWHSE
     CSR                   KFLD           TDWHDP
     CSR                   KFLD           TDSTYP
     C*
     CSR                   MOVE #PROG     $PPRG  10
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE '0'       ERROR
     C*
     CSR         TPKEY     CHAINTRANPH               79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE TDCODE    TPCODE
     CSR                   Z-ADDTDWHSE    TPWHSE
     CSR                   MOVE TDWHDP    TPWHDP
     CSR                   MOVE TDSTYP    TPSTYP
     CSR                   Z-ADD0         TPSTPH
     C*   WRITE record
     CSR                   WRITERECORD                 79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM72
     CSR                   GOTO ENDUPD
     CSR                   ENDIF
     C*
     CSR                   ELSE
     C*   UPDATE RECORD.
     CSR                   UPDATRECORD                 79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM72
     CSR                   GOTO ENDUPD
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         ENDUPD    ENDSR
