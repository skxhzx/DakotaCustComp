     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV173     Inventory variance re-count sheet for Aisle- Print
     F*  24 September, 2006
     F*  Roxanne Harrison
     F*
     F*  Revisions:
     F*
510a F*    09/24/06  RH   5.10a
     F*      - Created using IV172, process for aisle, do not
     F*      print all items, only slots with variance.
     F*
     F*  Custom Revisions:
VIPa F*    09/25/06  RH   VIPa
     F*      - Add client code, COLUMN1 for VIP
     F*  Custom Revisions:
CWa  F*    03/25/08  RH   CWa
     F*      - Add client code, Detail2 for City Wholesale
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*  DTL4      Used to print DETAIL4 at bottom of page
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIV173   IP  E           K        DISK
     F            WKREC                             KRENAMERECORD
     FPIRITEM IF  E           K        DISK
416b FINVEXCP2IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FIV173PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RPD     1   3 25
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    A22        22  1
     E*
     E*
510 AI*----------------------------------------------------------------
510 AI*  Customer id
510 AI*
510 AI              'GETCLIENT'           C         @GETCL
510 AI*
510 A /COPY QCOPYSRC,IL#VIP
CWa A /COPY QCOPYSRC,IL#CITYWHO
     I*----------------------------------------------------------------
     IRECORD
     I*             WKSWHS                          WPSWHSL4
     I                                              WKSWHSL5
     I                                              WKSPAG
     I                                              WKSDSP
     I                                              WKSSTA
     I                                              WKSWHDL4
     I                                              WKSASLL2
     I                                              WKSLOC
     I                                              WKSLVL
     I                                              WKSHND
     I                                              WKSSTPL3
     I                                              WKSITM
     I                                              WKSQT1
     I                                              WKSQT2
     I                                              WKSQT3
     I                                              WKITEM
     I                                              WKSTK1
     I                                              WKSTK2
     I                                              WKSTK3
     I                                              WKSTAT
     I                                              WKPICKL1
     I                                              WKSEBY
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   60T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse #
     C*      $PASL   Aisle or *ALL
     C*      $PVAR   Minimum variance
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHS   3
     C                     PARM           $PASL   4
     C                     PARM           $PVAR   5
     C                     MOVE $PWHS     $PWHSE  30
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL5     OREQ *ON
     C           *INL4     OREQ *ON
     C           *INL3     OREQ *ON
     C           *INL2     OREQ *ON
     C           *INL1     OREQ *ON
     C           FIRST     IFEQ *OFF
     C           DTL4      ANDEQ*OFF
     C                     WRITEDETAIL4
     C                     ENDIF
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break -
     C*
     C*          *INL1     IFEQ *ON
     C*                    Z-ADD1         L
     C*                    EXSR ZRZERO
     C*                    ENDIF
     C*
     C*  Print headings if level break.
     C*
     C           *INL5     IFEQ *ON
     C           *IN94     OREQ *ON
     C           *INL4     OREQ *ON
     C           *IN93     OREQ *ON
     C           *INL3     OREQ *ON
     C           *IN92     OREQ *ON
     C           *INL2     OREQ *ON
     C           *IN91     OREQ *ON
     C           *INL1     OREQ *ON
     C                     WRITESBHEAD
VIPaAC           CLIENT    IFEQ VIP
VIP  C                     WRITECOLUMN1
     C                     ELSE
     C                     WRITECOLUMNS
CWa AC           CLIENT    IFEQ CITYWH
CWa  C                     WRITESKIP1
CWa  C                     WRITESKIP1
CWa  C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C                     MOVE WKSITM    SVSITM 15
     C                     MOVE *OFF      *IN81
     C                     MOVE *OFF      PCKFLG  1
     C*
     C*  Calculate difference of NEW qty's SUB OLD qyt's.
     C*
     C*
     C           ITKEY1    CHAINPIRITEM              78
     C           *IN78     IFEQ *OFF
     C                     MOVE ITDESC    IDESC
     C                     MOVE ITPDSC    PDESC
     C                     ELSE
     C                     MOVE *BLANKS   IDESC
     C                     MOVE *BLANKS   PDESC
     C                     ENDIF
     C           WKPICK    IFEQ 'Y'
     C                     MOVE 'Y'       WOPICK  1
     C                     ELSE
     C                     MOVE ' '       WOPICK
     C                     ENDIF
     C*
416b C* Print X if exception record exists for slot.
416b C           EXKEY2    CHAININVEXCP2             78
416b C*          *IN78     CABEQ*OFF      SKIPEX
416b C           *IN78     IFEQ *OFF
416b C                     MOVE 'X'       WOEXCP
416b C                     ELSE
416b C                     MOVE ' '       WOEXCP
416b C                     ENDIF
     C                     MOVE *OFF      DTL4    1
     C                     WRITEDETAIL1                90
CWa AC           CLIENT    IFEQ CITYWH
CWa  C                     WRITEDETAIL2
CWa  C                     ENDIF
     C  N90                WRITESKIP2                  90
     C           *IN90     IFEQ *ON
     C                     WRITEDETAIL4
     C                     MOVE *ON       DTL4
     C                     ENDIF
     C                     MOVE *ON       *IN81
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*                    ENDDO
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN94
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Level 1 break
     C*
     C*1                   Z-ADD1         L
     C*1                   EXSR ZRROLL
     C*
     C*1         RECK,L    IFGT 0
     C*1                   EXSR ZRTOTL
     C*1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Grand totals
     C*
     C*R                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     C*R                   EXSR ZRTOTL
     CLR         DTL4      IFEQ *OFF
     CLR                   WRITEDETAIL4
     CLR                   ENDIF
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     CLR                   RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
510 AC*
510 AC* Get client id.
510 AC*
510 AC                     CALL @GETCL
510 AC                     PARM           CLIENT 10
     C*
     C           SLKEY     KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKSWHD
     C                     KFLD           WKSITM
     C*
     C*
     C           PARTKY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKSWHD
     C                     KFLD           WKSSTP
     C                     KFLD           WKSITM
     C*
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C           ITKEY1    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKSITM
     C*
     C           ITKEY2    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKITEM
     C*
     C           EXKEY2    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKSWHD
     C                     KFLD           WKSDSP
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     MOVE *BLANKS   WKBQT1 12
     C                     MOVE *BLANKS   WKBQT2 12
     C                     MOVE *BLANKS   WKBQT3 12
     C                     MOVE #PROG     $CVPRG
     C                     MOVE *ON       FOREVR  1
     C*
     C                     MOVE $PVAR     PMVAR   50
     C           PMVAR     MULT -1        PNVAR   50
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C*
     C                     MOVEL'IV172PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL3     IFEQ *OFF
     C                     MOVE *ON       *IN93
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITESKIP1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C*
     C                     MOVE RPD,L     $RPD   25
     C*
     C*                    WRITETOTAL1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C*
     C                     ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions
 * Not used *
 Average for week:
 *** Grand totals ***
