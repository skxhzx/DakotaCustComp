     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  TD911     Verify options for command INZTSKTTL
     F*  February 28, 1996
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (Reverse Display)
     F*  90        View mode (Protect input fields)
     F*  91        Change mode (Protect key)
     F*  97        Always ON (Used for SFLEND Keyword)
     F*  98        Error on screen (Sound buzzer)
     F*  99        Universal record indicator (Very temporary usage)
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  24 $LCAT
     I                                       25  30 $LSUB
     I*
     I                                       19  24 $LTASK
     I*
     I                                       19  210$LWHSE
     I                                       22  26 $LWHDP
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1   60ERDATE
     I*
     I                                    P   1   50ERPO#
     I                                        6  20 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCAT   Category code or *ALL
     C*      $PSUB   Sub-category code or *ALL
     C*      $PTASK  Task code or *ALL
     C*      $PWHSE  Warehouse  or *ALL
     C*      $PWHDP  Department or *ALL
     C*      $PSTYP  Section or *ALL
     C*      $PETIM  Ending time (HHMM)
     C*
     C*    Returned Parameters
     C*      $PRTN   *OK      - Parameters are ok.
     C*              *ERROR   - At least one parameter is incorrect.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCAT   6
     C                     PARM           $PSUB   6
     C                     PARM           $PTASK  6
     C                     PARM           $PWHSE  4
     C                     PARM           $PWHDP  5
     C                     PARM           $PSTYP  4
     C                     PARM           $PETIM  40
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE 'OK      '$PRTN
     C*
     C*  Check parameters.
     C*
     C                     EXSR ZZCHK1
     C*
     C*  If error was found then send messages to calling program.
     C*
     C           ERROR     IFEQ *ON
     C                     CALL 'MOVMSGP'
     C                     PARM           #PROG
     C                     MOVE '*ERROR  '$PRTN
     C                     ENDIF
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   MOVE #PROG     $LPRG
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     C*R                   MOVE *OFF      *IN97
     C*R                   WRITEMSGCTL
     C*R                   Z-ADD*ZERO     MSGK    40
     C*R                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK   4
     C*          #MSGID    IFNE '*CLEAR '
     C*                    ADD  1         MSGK
     C*                    WRITEMSGREC
     C*                    END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PD91001  Invalid section entered.
     C*
     CSR         ZM9101    BEGSR
     CSR                   MOVE 'PD91001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR   1
     C*
     C*  Verify Category code.
     C*
     CSR         $PCAT     IFNE '*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PCAT     $LCAT
     CSR                   CALL 'TC900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Sub-Category code.
     C*
     CSR         $PSUB     IFNE '*ALL '
     CSR         $PCAT     ANDNE'*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PCAT     $LCAT
     CSR                   MOVE $PSUB     $LSUB
     CSR                   CALL 'TS900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Task code.
     C*
     CSR         $PTASK    IFNE '*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PTASK    $LTASK
     CSR                   CALL 'TK900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify warehouse code.
     C*
     CSR         $PWHSE    IFNE '*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PWHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*    Make sure valid department was enterd.
     C*
     CSR         $PWHDP    IFNE '*ALL '
     CSR         $PWHSE    ANDNE'*ALL '
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PWHSE    $LWHSE
     CSR                   MOVE $PWHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*    Make sure section was entered.
     C*
     CSR         $PSTYP    IFNE '*ALL'
     CSR         $PSTYP    ANDNE'F'
     CSR         $PSTYP    ANDNE'P'
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9101
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
