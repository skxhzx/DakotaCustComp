     F*----------------------------------------------------------------
     F*
     F*  IV530     Post inventory counts - FTP export
     F*  02 March 2000
     F*  Dave Sommerville
     F*
     F*  Revisions
413 AF*    03/02/00 DAS  4.13
     F*      - Created
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  78        Chain indicator
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FEXPCNT  UF  E           K        DISK
     FEXPFCNT UF  E           K        DISK                      A
     F            CEREC                             KRENAMECEFREC
     I*----------------------------------------------------------------
     I*
     I* Rename FTP file qty fields because they are defined differently
     I*
     ICEFREC
     I              CEQTY1                          CEFQT1
     I              CEQTY2                          CEFQT2
     I              CEQTY3                          CEFQT3
     I*----------------------------------------------------------------
     C*  Parameters
     C*    Input Parameters
     C*      $PWHSE  Warehouse
     C*
     C*    Returned Parameters
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSA  3
     C                     MOVE $PWHSA    $PWHSE  30
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Move records from EXPCNT to EXPFCNT
     C*
     C           KEYWHS    SETLLEXPCNT
     C           FOREVR    DOUNEFOREVR
     C           KEYWHS    READEEXPCNT                   79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     Z-ADDCEQTY1    CEFQT1
     C                     Z-ADDCEQTY2    CEFQT2
     C                     Z-ADDCEQTY3    CEFQT3
     C                     WRITECEFREC
     C                     DELETCEREC
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Define partial key.
     C*
     CSR         KEYWHS    KLIST
     CSR                   KFLD           $PWHSE
     C*
     C*  Fool compiler
     C*
     CSR         FOREVR    IFNE FOREVR
     CSR                   READ EXPFCNT                  79
     CSR                   ENDIF
     C*
     CSR                   ENDSR
