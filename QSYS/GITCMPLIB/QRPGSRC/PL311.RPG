     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PL311     Pallet utilization report by length
     F*  12 April, 1996
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPL311   IP  E           K        DISK
     FPALLET  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPL311PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RPD     1   4 25
     E                    RECK        4  7 0
     E                    LVLK        4  7 0
     E*
     E                    EXTP        4  9 2
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              PKPAL L3
     I                                              PKHANGL2
     I                                              PKLEN L1
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*     - Pallet length changed
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL1     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*  Level 3 break - Pallet code
     C*
     C           *INL3     IFEQ *ON
     C                     Z-ADD3         L
     C                     EXSR ZRZERO
     C*
     C*   Get pallet description
     C*
     C           PKPAL     CHAINPALLET               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   PLDESC
     C                     Z-ADD0         PLLEN
     C                     Z-ADD0         PLWDTH
     C                     Z-ADD0         PLHGT
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Level 2 break - Overhang
     C*
     C           *INL2     IFEQ *ON
     C                     Z-ADD2         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*  Level 1 break - Length
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*  Print headings.
     C*
     C           *IN93     IFEQ *ON
     C           *INL3     OREQ *ON
     C                     WRITEPLHEAD
     C                     ENDIF
     C*
     C           *IN92     IFEQ *ON
     C           *INL2     OREQ *ON
     C                     WRITEOHHEAD
     C                     ENDIF
     C*
     C           *IN91     IFEQ *ON
     C           *INL1     OREQ *ON
     C                     WRITELNHEAD
     C                     WRITECOLUMNS
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C           PKLEN     MULT PKWDTH    SQINCH
     C*
     C                     WRITEDETAIL1
     C*  Accumulate totals
     C*
     C**                   ADD  EXTPRC    EXTP,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Length totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     C**                   EXSR ZRTOTL
     C**                   WRITESKIP1
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Overhang totals
     C*
     CL2                   Z-ADD2         L
     CL2                   EXSR ZRROLL
     C*
     CL2         RECK,L    IFGT 0
     C**                   EXSR ZRTOTL
     C**                   WRITESKIP1
     CL2                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Pallet code totals
     C*
     CL3                   Z-ADD3         L
     CL3                   EXSR ZRROLL
     C*
     CL3         RECK,L    IFGT 0
     C**                   EXSR ZRTOTL
     C**                   WRITESKIP1
     CL3                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Batch totals
     C*
     CLR                   Z-ADD4         L
     C*
     CLR         RECK,L    IFGT 0
     C**                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C                     MOVEL'PL311PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL3     IFEQ *OFF
     C                     MOVE *ON       *IN93
     C                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  EXTP,L    EXTP,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C*                    Z-ADDEXTP,L    T#EXTP
     C*
     C                     MOVE RPD,L     $RPD   25
     C*
     C                     WRITETOTAL1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         EXTP,L
     C*
     C                     ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions
 * Length totals *
 ** Overhang totals **
 *** Pallet code totals ***
 **** Grand totals ****
