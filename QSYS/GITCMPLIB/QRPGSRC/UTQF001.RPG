     F*----------------------------------------------------------------
     F*
     F*  UTQF001   Reverse Truck pallet unload seq. Quality custom
     F*  13 March 2001
     F*  Roxanne Harrison
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FTRUCKH  IP  E           K        DISK
     FTRUCKD1 UF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Hard code warehouse and department.
     C*
     C                     Z-ADD900       KYWHSE
     C*                    MOVE 'DRY  '   KYWHDP    P
     C*
     C*  Only process for entered warehouse.
     C           THWHSE    CABNEKYWHSE    ENDTH
     C*
     C                     MOVE 'D'       KYCOMP
     C                     EXSR ZZRSEQ
     C*
     C                     MOVE 'F'       KYCOMP
     C                     EXSR ZZRSEQ
     C*
     C                     MOVE 'R'       KYCOMP
     C                     EXSR ZZRSEQ
     C*
     C           ENDTH     TAG
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C                     MOVE *ON       FOREVR  1
     C*
     C*  Define partial key for TRUCKD1.
     C*
     C           *LIKE     DEFN THWHSE    KYWHSE
     C           *LIKE     DEFN TDCOMP    KYCOMP
     C           TDKEY     KLIST
     C                     KFLD           KYWHSE
     C                     KFLD           THTRUK
     C                     KFLD           KYCOMP
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZRSEQ  Initialization subrotine
     C*
     CSR         ZZRSEQ    BEGSR
     C                     Z-ADD0         SCTR    30
     C*
     C*    - Count number of records to renumber.
     C           TDKEY     SETLLTRUCKD1
     C                     MOVE *OFF      STOP    1
     C           STOP      DOUEQ*ON
     C           TDKEY     READETRUCKD1                  79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       STOP
     C                     ELSE
     C                     ADD  1         SCTR
     C                     ENDIF
     C                     ENDDO
     C*
     C*    - First renumber seq by adding 20, then renumber.
     C*      Read truck detail.
     C           TDKEY     SETLLTRUCKD1
     C                     DO   SCTR
     C           TDKEY     READETRUCKD1                  79
     C           *IN79     IFEQ *OFF
     C                     ADD  20        TDPICK
     C                     UPDATTDREC
     C                     ENDIF
     C                     ENDDO
     C*
     C                     Z-ADD0         NCTR    30
     C           TDKEY     SETGTTRUCKD1
     C                     DO   SCTR
     C           TDKEY     REDPETRUCKD1                  79
     C           *IN79     IFEQ *OFF
     C                     ADD  1         NCTR
     C                     Z-ADDNCTR      TDPICK
     C                     UPDATTDREC
     C                     ENDIF
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
