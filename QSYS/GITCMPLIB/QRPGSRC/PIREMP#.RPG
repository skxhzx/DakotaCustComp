     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PIREMP#     Get next PIR employee number
     F*  02 November 1995
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*
     F*  File specs
     F*
     FPIRUSER1IF  E           K        DISK
     FOPTIONE UF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     IOPEDTA      DS
     I                                        1   1 OPMAN
     I I            0                         2   60OPNXTE
     C*----------------------------------------------------------------
     C*
     C*  Subprogram parameters
     C*
     C*    Input Parameters
     C*    - None -
     C*
     C*    Returned Parameters
     C*    $NXTNM  Next PIR employee number (guarenteed to be
     C*            unique; not guarenteed to be the next used in
     C*            sequence; 00000 is not used)
     C*
     C*----------------------------------------------------------------
     C* Main Line
     C*
     C           *ENTRY    PLIST
     C                     PARM           $NXTNM  50
     C*
     C                     MOVE *OFF      UPDFLG  1
     C           *LIKE     DEFN USEMP#    NEXT#
     C                     MOVE '*USER   'ECODE
     C                     Z-ADD0         EWHSE
     C                     MOVE *BLANKS   EWHDP
     C           OPKEY     CHAINOPTIONE              79
     C           *IN79     IFEQ *ON
     C                     Z-ADD0         NEXT#
     C                     ELSE
     C                     MOVE *ON       UPDFLG
     C                     Z-ADDOPNXTE    NEXT#
     C                     ENDIF
     C*
     C*  Data area contains the next sequence number to use.  If it
     C*  has already been used then keep adding 1 until we find one
     C*  that hasn't been used (we will take the chance that all
     C*  99999 have not been used).
     C*
     C                     MOVE '0'       *IN79
     C           *IN79     DOUEQ'1'
     C           NEXT#     IFEQ 0
     C                     Z-ADD1         NEXT#
     C                     ENDIF
     C                     Z-ADDNEXT#     USEMP#
     C           FILEKY    CHAINPIRUSER1             79
     C           *IN79     IFEQ *OFF
     C                     ADD  1         NEXT#
     C                     ENDIF
     C                     ENDDO
     C*
     C*  Return the unique sequence number.
     C*
     C                     Z-ADDNEXT#     $NXTNM
     C*
     C*  Update the data area with the next number to be used.
     C*
     C                     ADD  1         NEXT#
     C                     Z-ADDNEXT#     OPNXTE
     C           UPDFLG    IFEQ *ON
     C                     UPDATOPEREC
     C                     ENDIF
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define partial key for PIRUSER1.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           USEMP#
     C*
     C*  Define key for OPTIONE.
     C*
     CSR         *LIKE     DEFN OPECOD    ECODE
     CSR         *LIKE     DEFN OPEWHS    EWHSE
     CSR         *LIKE     DEFN OPEWDP    EWHDP
     CSR         OPKEY     KLIST
     CSR                   KFLD           ECODE
     CSR                   KFLD           EWHSE
     CSR                   KFLD           EWHDP
     CSR                   ENDSR
