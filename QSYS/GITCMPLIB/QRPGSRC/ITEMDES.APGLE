      *copy *libl/qcopysrc,hspecs
     Hcopyright('(c) Copyright BFC Software, Inc - 2016')
     H optimize(*NONE)
     H decedit(*JOBRUN)
     H truncnbr(*NO)
     H option(*srcstmt : *nodebugio)
     H Dftactgrp(*No)
      *----------------------------------------------------------
      *   Copyright (C) 2016 BFC Software, Inc. - All Rights Rese
      *   BFC Software, Inc.
      *----------------------------------------------------------

      *----------------------------------------------------------
      *
      *  ITEMDES     SQL Function call to SCLSTAT
      *  28 August 2016
      *  Thomas Bryant
      *
      *  Revisions:
      *
     *    08/28/16  TAB  7.00
     *      - Created
      *
      *----------------------------------------------------------
      * File Specs
      *----------------------------------------------------------

     Fpiritem   if   e           k disk
     Fbfcdbgflg if   e           k disk    usropn
     Fbfcdebug  o    e           k disk

      *----------------------------------------------------------
      *  Program information data structure
      *----------------------------------------------------------

      /copy qcopysrc,p.bfcdteti
     D*copy qcopysrc,c#pgminfds

      *----------------------------------------------------------
      * Begin C#PGMINFDS - Program information data structure
      *  12/20/02  DAS  4.16  Created
      *  07/12/08  DAS  6.00a
      *    - Added #curruser, #prog, #jobn
      *  03/02/11  MLB  6.40
      *    - Added #parms
      *----------------------------------------------------------

     D pssrflag        s               n

     D                sds
     D  #pgm                   1     10
     D  #prog                  1     10
     D  #status               11     15
     D  #stmt                 21     28  0
     D  #routine              29     36
     D  #parms                37     39
     D  #pgmmsg               91    170
     D  #job                 244    253
     D  #user                254    263
     D  #jobnbr              264    269
     D  #jobn                264    269
     D  #jobdate             276    281  0
     D  #jobtime             282    287  0
     D  #curruser            358    367

      *** End C#PGMINFDS ***

      *----------------------------------------------------------
      *  Variables
      *----------------------------------------------------------

     D dbgflgExists    s               n
     D debug           s               n
     D Error           s               n
     D i               s              3  0
     D len             s              3  0
     D whse            s              3  0
     D itemdesc        S             40    Inz(' ')
     D itemColor       s             20
     D text            s            200    varying

      *----------------------------------------------------------
      *  Variables
      *----------------------------------------------------------

      *----------------------------------------------------------
      *  Standard SQL variables and constants
      *----------------------------------------------------------

      *copy qcopysrc,c#stdsql

     D  itemdes        pr                  extpgm('ITEMDES')
     D  pSessId                      40    varying
     D  pUser                        10    varying
     D  pDict                        10    varying
     D  pPgm                         10    varying
     D  pItemD                       40    varying
     D  pWhse                         3p 0
650aMD  pItem                        15    varying
     D  pOutStr                     200    varying

     D  itemdes        pi
     D  pSessId                      40    varying
     D  pUser                        10    varying
     D  pDict                        10    varying
     D  pPgm                         10    varying
     D  pItemD                       40    varying
     D  pWhse                         3p 0
     D  pItem                        15    varying
     D  pOutStr                     200    varying

      /free

         *inlr = *on;
         #curruser = pUser;


          if debug;
            exsr wrtDebugParms;
          endif;

           exsr GetItem;
           exsr bldjson;
      /end-free
      *----------------------------------------------------------
      *  BldJson  Build JSON string
      *----------------------------------------------------------
      /free
          BegSR BldJson;
          // Put together JSON string
          pOutStr = '{'
                 +  '"scid":"'  + %trimr(pSessID) + '",'
                 +  '"scu":"'  + %trimr(pUser) + '",'
                 +  '"scd":"'  + %trimr(pDict) + '",'
                 +  '"itemd":"'  + %trimr(pItemD) + '",'
                 +  '"stc":"'  + %trimr(itemColor) + '",'
                 +  '"whse":"'  + %char(pWhse) + '",'
                 +  '"item":"'  + %trimr(pItem) + '"';

          // Close off the JSON string
          pOutStr = %trim(pOutStr) + '}';
          EndSR;
      /end-free

      *----------------------------------------------------------
      *  GetItem  Check item master file valid item
      *----------------------------------------------------------
      /free
          BegSR GetItem;

             Chain (pWhse : pItem) piritem;
                If %Found;
                   pItemD = ITDESC;
                   itemColor = 'green';
                   Error = *Off;
                Else;
                   pItemd = 'Invalid Item ID';
                   itemColor = 'red';
                   Error = *On;
               EndIf;
          EndSR;
      /end-free
      *----------------------------------------------------------
      *  *INZSR   Initialization subroutine
      *----------------------------------------------------------

      /free
          begsr *INZSR;
             open(e) bfcdbgflg;
             if %error;
               dbgflgExists = *off;
             else;
               dbgflgExists = *on;
             endif;

          if dbgflgExists;
            chain(e) (#pgm) bfcdbgflg;
            if %found(bfcdbgflg);
              debug = (bdfflag = 1);
            else;
              debug = *off;
            endif;
          endif;

           endsr;

      /end-free

      *----------------------------------------------------------
      *  wrtDebugParms  Write out debug parm record
      *----------------------------------------------------------

      /free
       begsr wrtDebugParms;

         bdtype = 'PARMS';

         bdvalues = '&pSessID = ' + pSessID
                  + '&pUser=' + %trimr(pUser)
                  + '&pDict=' + %trimr(PDict)
                  + '&pPgm=' + %trimr(pPgm)
                  + '&pItemD=' + %trimr(pItemD)
                  + '&pWhse =' + %char(pWhse)
                  + '&pItem =' + %trimr(pItem)
                  + '&pOut=' + %trimr(pOutStr);

         bdaddts = %timestamp();
         bdaddpgm = #pgm;
         bdaddcusr = #curruser;
         bdaddjusr = #user;
         bdaddjob = #job;
         bdaddnbr = #jobnbr;

         write(e) bdrec;

       endsr;
      /end-free

      *----------------------------------------------------------
      *  wrtDebugError  Write out debug error
      *----------------------------------------------------------

      /free
       begsr wrtDebugError;

         bdtype = 'ERROR';

         bdvalues = ' ';

         bdaddts = %timestamp();
         bdaddpgm = #pgm;
         bdaddcusr = #curruser;
         bdaddjusr = #user;
         bdaddjob = #job;
         bdaddnbr = #jobnbr;

         write(e) bdrec;

       endsr;
      /end-free
