     F*----------------------------------------------------------------
     F*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  UPD417MBG  Initialize fields in *GENPICK by warehouse.
     F*  03 July 2003
     F*  Max Blacknik
     F*
     F*  Revisions
417 AF*    07/03/03  MLB  4.17
     F*      - Initialize OPNOPK.
417aAF*    05/18/04  MLB  4.17a
     F*      - Initialize OPCLRF.
417bAF*    08/19/04  MLB  4.17b
     F*      - Initialize OPRNRM.
417cAF*    09/21/04  RH   4.17c
     F*      - Initialize OPJITR.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FWAREHS  IF  E           K        DISK
     FOPTIONS UF  E           K        DISK
     I*----------------------------------------------------------------
     I*
410aAI*----------------------------------------------------------------
410aAI*  *GENPICK -  General picking options.
410aAI*----------------------------------------------------------------
410 AI*
410 AI* Fields
410 AI*
410 AI*    OPERPT  -  Print exception (short) error report (Y,N).
410 AI*    OPELBL  -  Print exception (short) labels (Y,N)
410BAI*    OPRFWC  -  Using RF will call (Y,N).
413cAI*    OPNRSC  -  Number of pallet sections (1-9).
414aAI*    OPHPWC  -  Host pick Will Call (Y/N).
415cAI*    OPMRGA  -  Merge Add-on order(s) with Open Routes.
415dAI*    OPPLSQ  -  Pallet number sequence. 1=Unload, 2=Load.
416eAI*    OPEADJ  -  Close short labels without adjustment (Y,N).
417dAI*    OPNOPK  -  Pick items without pick slot as bulk pick? (Y/N)
417jAI*    OPCLRF  -  Clear runner flag if qty still short/out? (Y/N)
417bAI*    OPRNRM  -  Send break message to user when new Flag Runner
417bAI*               task has been assigned. (Y/N)
417cAI*    OPJITR  -  Create JIT Letdown or JIT Replenishment (L/R)
410 AI*
410 AI* Data structure
410 AI*
410 AIOPDATA      DS
410 AI                                        1   1 OPERPT
410 AI                                        2   2 OPELBL
410BAI                                        3   3 OPRFWC
413cAI                                        4   40OPNRSC
414aAI                                        5   5 OPHPWC
415cAI                                        6   6 OPMRGA
415dAI                                        7   7 OPPLSQ
416eAI                                        8   8 OPEADJ
417dAI                                        9   9 OPNOPK
417jAI                                       10  10 OPCLRF
417bAI                                       11  11 OPRNRM
417cAI                                       12  12 OPJITR
417 AI                                      117 117 OPTEND
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *OFF      *IN79
     C           *LIKE     DEFN OPCODE    KYCODE
     C           OPKEY     KLIST
     C                     KFLD           WHCODE
     C                     KFLD           KYCODE
     C                     MOVE '*GENPICK'KYCODE
     C*
     C           *LOVAL    SETLLWAREHS
     C           *IN79     DOWEQ*OFF
     C                     READ WAREHS                   79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE                           EXIT LOOP
     C*
     C                     OTHER
     C*
     C           OPKEY     CHAINOPTIONS              78
     C                     SELEC
     C           *IN78     WHEQ *OFF
     C           OPNOPK    IFEQ ' '
     C                     MOVE 'Y'       OPNOPK
     C                     ENDIF
417aAC           OPCLRF    IFEQ ' '
417aAC                     MOVE 'Y'       OPCLRF
417aAC                     ENDIF
417bAC           OPRNRM    IFEQ ' '
417bAC                     MOVE 'N'       OPRNRM
417bAC                     ENDIF
417cAC           OPJITR    IFEQ ' '
417cAC                     MOVE 'R'       OPJITR
417cAC                     ENDIF
     C                     UPDATOPREC
     C*
     C                     ENDSL
     C                     ENDSL
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*
