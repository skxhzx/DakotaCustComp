     F*----------------------------------------------------------------
     F*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  EX156   Catch weight exception report.
     F*  13 September 2004
     F*  Roxanne Harrison
     F*
     F*----------------------------------------------------------------
     F*  Revisions:
     F*
417 AF*    09/13/04  RH   4.17
     F*      - Created using EX126
     F*
500 AF*    04/19/06  JCJ  5.00
     F*      - Added Client ID
500aAF*    05/25/06  RH   5.00a
     F*      - ENH: Added Route, replaced Task on report
     F*
510 AF*    12/29/06  MLB  5.10
     F*      - Fix: Corrected program id used to retrieve report title.
510aAF*    12/29/06  MLB  5.10a
     F*      - Fix: Corrected to use OVRTKN for error massage.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: City Line
     F*
CLD  F*    04/19/06  JCJ  CLD
     F*      - Skip some logic for City Line - Remove Item Break
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: Springfield Grocer
SGC  F*    10/24/17  RH   SGC
     F*      - Add to change for City Line - Remove Item Break
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FCWEXCP  IP  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
500a FRTEHED  IF  E           K        DISK
500a FLABEL   IF  E           K        DISK
     FEX156PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
     E                    DESC    1   3 50
500 AI*----------------------------------------------------------------
500 AI*  Customer id
500 AI*
500 AI              'GETCLIENT'           C         @GETCL
500 AI*
CLD AI/COPY QCOPYSRC,IL#CITYLIN
SGC AI/COPY QCOPYSRC,IL#SGC
     I*----------------------------------------------------------------
     ICXREC
     I                                              CXESTAL2
     I                                              CXITEML1
     I*----------------------------------------------------------------
     I*  Redefinition data structures
     I*
     I            DS
     I                                        1  12 EXFDIS
     I                                       13  24 EXTDIS
     I                                        1   9 DSPO
     I                                       10  160DSLBL
     I                                       17  240DSEXPD
417c I                                        1  20 DSUPC
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I*
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FROM   8
     C                     PARM           $TO     8
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C*
     C                     MOVE *BLANKS   P#DESC
     C*          'Dept:'   CAT  EXWHDP:1  P#DESC
     C*          P#DESC    CAT  'Type:':2 P#DESC
     C*          P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Item
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     ENDIF
     C*
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
CLD AC                     SELEC
CLD AC           CLIENT    WHEQ CITYLN
CLD AC           *INL1     ANDEQ'1'
CLD AC           FPASS     ANDEQ'N'
SGC AC           CLIENT    WHEQ SGC
SGC AC           *INL1     ANDEQ'1'
SGC AC           FPASS     ANDEQ'N'
CLD AC                     OTHER
     C                     WRITECOLUMNS
CLD AC                     MOVE 'N'       FPASS   1
CLD AC                     ENDSL
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Convert Generated date
     C*
     C                     MOVE '*CMDMDY '$CVCMD
     C                     MOVE CXGDTE    $CVD8I
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD6O    GENDTE  60
     C*
     C*  Retrieve item description.
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   ITDESC
     C                     ENDIF
500a C*
500a C*  Retrieve Route #.
500a C*
500a C                     MOVE *BLANKS   ROUTE
500a C           CXLBL#    CHAINLABEL                79
500a C           *IN79     IFEQ *OFF
500a C           RHKEY     CHAINRTEHED               78
500a C           *IN78     IFEQ *OFF
500a C                     MOVE RHRTE     ROUTE
500a C                     ENDIF
500a C                     ENDIF
     C*
     C*  Setup error description for printing.
     C*
     C                     SELEC
510aDC*          CXERID    WHEQ 'EXPDOVR'
510aMC           CXERID    WHEQ 'OVRTKN '
     C                     MOVELDESC,3    ERRMSG
     C                     OTHER
     C                     MOVELCXERID    ERRMSG    P
     C                     ENDSL
     C*
     C*
     C*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C           ITKEY     KLIST
     C                     KFLD           CXWHSE
     C                     KFLD           CXITEM
     C*
500a C           RHKEY     KLIST
500a C                     KFLD           LBWHSE
500a C                     KFLD           LBRTE
     C*
500 AC* Get client id.
500 AC*
500 AC                     CALL @GETCL
500 AC                     PARM           CLIENT 10
     C*
     C*
     C*   Get report title.
     C*
510 DC*                    MOVEL'CX126PR 'WKPRTF
510 MC                     MOVEL'EX156PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     C*
     C*   Convert dates to MMDDYY format.
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $FROM     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    FMDT    60
     C*
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   MOVE $TO       $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    TODT    60
     C*
     C*
     CSR                   MOVE #PROG     $CVPRG
     C*
     C                     Z-ADD3         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           CXWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     MOVE *BLANKS   P#DESC
     C*          'Dept:'   CAT  EXWHDP:1  P#DESC
     C*          P#DESC    CAT  'Type:':2 P#DESC
     C*          P#DESC    CAT  TYPE:1    P#DESC
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C                     EXSR ZRFRMT
     C*
CLD AC                     SELEC
CLD AC           CLIENT    WHEQ CITYLN
CLD AC           *INL1     ANDEQ'1'
CLD AC           FPASS     ANDEQ'N'
SGC AC           CLIENT    WHEQ SGC
SGC AC           *INL1     ANDEQ'1'
SGC AC           FPASS     ANDEQ'N'
CLD AC                     OTHER
     C                     WRITEHEAD1
     C                     WRITEHEAD2
CLD AC                     ENDSL
     C*                    WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDCLS,L     T#CLS
     C                     MOVE *BLANKS   T#DESC
     C           CXITEM    CAT  '/':1     T#DESC
     C*          T#DESC    CAT  TYPE:1    T#DESC
     C*
     C           L         IFEQ 1
     C                     WRITETOTAL1
     C                     ENDIF
     C*
     C           L         IFEQ 2
     C                     WRITETOTAL2
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         CLS,L
     C*
     C*    Setup heading for either open/closed exceptions.
     CSR                   SELEC
     CSR         CXESTA    WHEQ '1 '                       Open excpt
     CSR                   MOVELDESC,1    P#STAT
CLD ACSR                   MOVE *BLANKS   P#STA2
CLD ACSR         CLIENT    IFEQ CITYLN
CLD ACSR                   MOVELDESC,1    P#STA2
CLD ACSR                   ENDIF
SGC ACSR         CLIENT    IFEQ SGC
SGC ACSR                   MOVELDESC,1    P#STA2
SGC ACSR                   ENDIF
     C*
     CSR         CXESTA    WHEQ '4 '                       Closed excpt
     CSR                   MOVELDESC,2    P#STAT
CLD ACSR                   MOVE *BLANKS   P#STA2
CLD ACSR         CLIENT    IFEQ CITYLN
CLD ACSR                   MOVELDESC,2    P#STA2
CLD ACSR                   ENDIF
SGC ACSR         CLIENT    IFEQ SGC
SGC ACSR                   MOVELDESC,2    P#STA2
SGC ACSR                   ENDIF
     CSR                   ENDSL
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
** Descriptions
            Open Exceptions
             Closed Exceptions
ovrride
