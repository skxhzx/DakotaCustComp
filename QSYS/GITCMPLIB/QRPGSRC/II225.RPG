     F*----------------------------------------------------------------
     F*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  II225     Import item updates - Post
     F*  19 May 1999
     F*  Roxanne Harrison
     F*
     F*      For add or change, can not update ITEMDEF records,
     F*      user will need to make any changes in item maintenance.
     F*
     F*----------------------------------------------------------------
411bAF*    07/04/99  DAS  4.11b
     F*      - In routine ZZCHKI changed ITTYPE in and IF statement
     F*        to use IUTYPE.
     F*
412 AF*    07/19/99  DAS  4.12
     F*      - Revised to allow UM2 qty to be zero if UM2 code exists.
     F*      - Revised to add Desig, Actv, Bld and Um codes if they do
     F*        not exist. (Also added files for output).
     F*      - Revised Y/N options to default to no.
     F*
412 AF*    07/20/99  DAS  4.12
     F*      - Revised to make sure the master item has a breakdown qty
     F*        for a type 'S' item.
     F*
412 AF*    07/21/99  DAS  4.12
     F*      - Revised method of adding ITEMDEF records.  Changed to first
     F*        delete all existing defs and then add them.  Also changed
     F*        the DO loop to do a SETGT at the end in order to reset
     F*        the pointer for ITEMDEF.
     F*
412 AF*    07/23/99  DAS  4.12
     F*      - Revised ZZUPD routine to not cause an error when an alias
     F*        item already exists and the update option is 'A'.
     F*      - Revised ZZUPD2 to update ITEMMSC if it already exists.
     F*      - Revised ZZUPD2 to only create ITEMQTY if it does not
     F*        already exist.
     F*
412 AF*    08/06/99  RH   4.12
     F*      - Recompile, ITEMMSC file changed.
     F*
412bAF*    09/17/99  MLB  4.12b
     F*      - In routines ZZUPD1, ZZUPD2 added code to initialize
     F*        new field IMNRPK.
     F*
413 AF*    11/08/99  DAS  4.13
     F*      - Change routine ZZXOPT to use PXFLG instead of UXFLAG.
     F*        PXFLG is used in program for extended desc. whereas
     F*        UXFLAG is not used anywhere else.
     F*
413aAF*    11/22/99  DAS  4.13a
     F*      - Added new grand total fields to identify creation
     F*        of new table codes.
     F*      - Added new ITEMMSC fields that were added in ver 4.12.
     F*        These fields are not part of the import file.
     F*      - Revised ZZCHKI to ignore whether update type is A.
     F*      - In ZZCHKI, moved "Full case item number must be
     F*        different from broken case item" check to before
     F*        the full case item number is verified.
     F*      - Revised ZZUPD to always get base record.
     F*      - Revised to include section type when updating
     F*        corresponding record with base fields.
     F*      - Revised criteria for calculating cube to only do it
     F*        if existing item record does not have a cube already.
     F*      - Revised ZZDELI to unconditionally check for existing
     F*        ITEMLNK records.
     F*
413bAF*    01/10/00  DAS  4.13b
     F*      - Revised to update base item record with UOM's from
     F*        'S' type item, if sent.
     F*      - Revised to clear ITEMMSC fields if record doesn't
     F*        exist for base item.
     F*
414 AF*    09/27/00  DAS  4.14
     F*      - Revised to only initialize fields IMWRNC, IMRG1C,
     F*        IMRG2C, IMAPCK and IMNRPK when and ITEMMSC record is
     F*        being added. They were being initialized all of the
     F*        time, thus wiping out any user changes that may have
     F*        been made.
     F*
416 AF*    08/14/01  DAS  4.16
     F*      - When deleting, revised to check ITEMQTY for base item.
     F*        If quantity exists, item cannot be deleted.
     F*      - Revised to only initialzie IMNRPK to 1.
     F*
416aAF*    09/11/01  DAS  4.16a
     F*      - Revised routine ZZCHKI to only allow the item type to
     F*        change from 'A' to ' '.
     F*      - Added new DESC message for invalid item type changes.
     F*      - Revised routine ZZUPD1 to always try to delete ITEMLNK
     F*        record for a Normal item. This will get rid of the
     F*        ITEMLNK record when an item is changed from 'A' to ' '.
     F*
416bAF*    11/19/01  DAS  4.16b
     F*      - Revised routine ZZUPD1 to create ITEMQTY record when
416cAF*    05/07/02  RH   4.16c
     F*      - Revised routine ZZDELI to skip checks for Alias "A" item
     F*      - Revised routine ZZDELI to only check break qty type "S"
416dAF*    01/23/03  HNK  4.16d
     F*      - Delete records from UPC file, ir item is deleted.
     F*
417aAF*    04/03/03  RH   4.17a
     F*      - Recompiled, ITEMMSC file changed.
417bAF*    04/19/04  MLB  4.17b
     F*      - Revised program to update item misc. file
     F*        and set IMLOT and IMLTRK to N=No on add.
     F*
510 AF*    12/14/06  MLB  5.10
     F*      - Enh: Revised program to remove item type C=Contract item
     F*        as valid type. Not supported at this time.
700aAF*    11/02/16  LMC  7.00a
     F*      - Enh: Recompile only - UPC file has changed.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  61        - Special order (Customer # > 0)
     F*  79        - EOF for IMPITEM
     F*  91        - Overflow indicator
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FIMPITEM UF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPIRITEM UF  E           K        DISK                      A
     FITEMDSC UF  E           K        DISK                      A
     FITEMDEF UF  E           K        DISK                      A
     FITEMLNK UF  E           K        DISK                      A
     FITEMLNK1IF  E           K        DISK
     F            ILREC                             KRENAMERECORD1
     FITEMMSC UF  E           K        DISK                      A
     FOPTIONS IF  E           K        DISK
     FITEMQTY UF  E           K        DISK                      A
     FII225PR O   E             91     PRINTER
412 AFDESIG   O   E           K        DISK                      A
412 AFACTIVITYO   E           K        DISK                      A
412 AFUNMESR  O   E           K        DISK                      A
412 AFBLDCODE O   E           K        DISK                      A
416dAFUPC1    UF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
416aAE                    A30        30  1
416aDE**                  DESC    1  44 30
510 DE*                   DESC    1  45 30
510 ME                    DESC    1  46 30
412 AE                    CDSC    1   1 30
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  23 $LSDEF
     I                                       22  26 $LWHDP
     I                                       22  36 $LITEM
     I*
     I                                       19  23 $LDESG
     I*
     I                                       19  20 $LACTV
     I*
     I                                       19  20 $LUM
     I*
     I                                       19  20 $LBC
     I*
     I                                       19  210$LWHS1
     I                                       22  36 $LITM1
     I                                       37  41 $LWHD1
     I                                       42  42 $LSTY1
     I                                       43  44 $LST1
     I                                       45  50 $LPAL1
     I                                       51  532$LOVR1
     I*
     I                                       19  210$LWHS2
     I                                       22  26 $LWHD2
     I                                       27  41 $LITM2
     I                                       42  440$LCNT
     I                                       45  45 $LREPL
     I                                       46  502$LULEN
     I                                       51  552$LUWDT
     I                                       56  56 $LSTYP
     I*----------------------------------------------------------------
     I*  *ITEMMNT  -  Item maintenance options.
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPSTYP  -  Default section type
     I*    OPWHDP  -  Default warehouse department
     I*    OPUM1   -  Default normal unit of measure
     I*    OPDUM   -  Unit of measure for dimensions
     I*    OPCUM   -  Unit of measure for cube
     I*    OPCDIV  -  Divisor for cube
     I*    OPCAUT  -  Automaticaly calculate cube when zero (Y/N)
     I*    OPCWRN  -  Display cube warning (Y/N)
     I*    OPCRNG  -  Display cube warning range percentage
     I*    OPWUM   -  Unit of measure for weight
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I*                                       1   1 OPSTYP
     I*                                       2   6 OPWHDP
     I*                                       7   8 OPUM1
     I                                        9  23 OPDUM
     I                                       24  43 OPCUM
     I                                       44  522OPCDIV
     I                                       53  53 OPCAUT
     I                                       54  54 OPCWRN
     I                                       55  573OPCRNG
     I                                       58  72 OPWUM
     I                                       73  73 OPXDSC
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  *IMPITEM  -  Import Item options.
     I*----------------------------------------------------------------
     I*
     I*    Y = Maintained by Host, imported to FSWMS
     I* Fields
     I*
     I*    OPITYP  -  Item type
     I*    OPDESC  -  Item description
     I*    OPWHDP  -  Warehouse department
     I*    OPSTYP  -  Section type
     I*    OPWHDP  -  Default warehouse department
     I*    OPDESG  -  Designation
     I*    OPACTV  -  Activity
     I*    OPBLD   -  Build code
     I*    OPVEN#  -  Vendor number
     I*    OPVIT#  -  Vendor item number
     I*    OPPDSC  -  Pack description
     I*    OPUM1   -  Normal unit of measure
     I*    OPUM2   -  Breakdown 1 unit of measure
     I*    OPUMQ2  -  Breakdown 1 UM quantity
     I*    OPFLG1  -  Allow Breakdown 1 Sale
     I*    OPUM3   -  Breakdown 2 unit of measure
     I*    OPUMQ3  -  Breakdown 2 UM quantity
     I*    OPFLG2  -  Allow Breakdown 2 Sale
     I*    OPULEN  -  Unit length
     I*    OPUWDT  -  Unit width
     I*    OPUHGT  -  Unit height
     I*    OPCUBE  -  Cube/Unit
     I*    OPSWGT  -  Shipping weight
     I*    OPCWGT  -  Catch weight
     I*    OPREPL  -  Replenish level
     I*    OPFLGD  -  Date sensitive
     I*    OPFLGB  -  Allow bulk pick
     I*    OPSLIF  -  Shelf life days
     I*    OPNDAY  -  Notice days
     I*    OPTIE   -  Mfr. Tie
     I*    OPHIGH  -  Mfr. High
     I*    OPMFLG  -  Manufacturing date
     I*    OPDDAY  -  Distributor days
     I*    OPUDAY  -  End user days
     I*    OPMDAY  -  Mfr. shelf life days
     I*    OPMITM  -  Master item number
     I*    OPCITM  -  Contract Base item #
     I*
     I* Data structure
     I*
     IOPDAT2      DS
     I                                        1   1 OPITYP
     I                                        2   2 OPDESC
     I                                        3   3 OPWHDP
     I                                        4   4 OPSTYP
     I                                        5   5 OPDESG
     I                                        6   6 OPACTV
     I                                        7   7 OPBLD
     I                                        8   8 OPVEN#
     I                                        9   9 OPVIT#
     I                                       10  10 OPPDSC
     I                                       11  11 OPUM1
     I                                       12  12 OPUM2
     I                                       13  13 OPUMQ2
     I                                       14  14 OPFLG1
     I                                       15  15 OPUM3
     I                                       16  16 OPUMQ3
     I                                       17  17 OPFLG2
     I                                       18  18 OPULEN
     I                                       19  19 OPUWDT
     I                                       20  20 OPUHGT
     I                                       21  21 OPCUBE
     I                                       22  22 OPSWGT
     I                                       23  23 OPCWGT
     I                                       24  24 OPREPL
     I                                       25  25 OPFLGD
     I                                       26  26 OPFLGB
     I                                       27  27 OPSLIF
     I                                       28  28 OPNDAY
     I                                       29  29 OPTIE
     I                                       30  30 OPHIGH
     I                                       31  31 OPMFLG
     I                                       32  32 OPDDAY
     I                                       33  33 OPUDAY
     I                                       34  34 OPMDAY
     I                                       35  35 OPMITM
     I                                       36  36 OPCITM
     I                                      117 117 OPTEN2
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHS   Warehouse to process.
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHS  155
     C*----------------------------------------------------------------
     C* Main line
     C*
     C                     EXSR ZZINZ2
     C*
     C* Whip through all the download Update Item records
     C*
     C                     MOVE *ON       PFLAG   1
     C                     MOVE *OFF      EOFI    1
     C                     Z-ADD55        LINPPG  30       MAX LINES PER PAGE
     C                     Z-ADD0         COUNT   30       LINE COUNTER
     C           IUKEY     SETLLIMPITEM
     C           EOFI      DOUEQ*ON
     C*
     C           IUKEY     READEIMPITEM                  79
     C                     MOVE *OFF      ERROR   1
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       EOFI
     C                     GOTO ENDDIU
     C                     ENDIF
     C*
     C*  Print report heading if necessary.
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     ORGE LINPPG
     C                     Z-ADD0         COUNT
     C                     WRITEHEAD1
     C                     ADD  3         COUNT
     C                     MOVE *OFF      PFLAG
     C                     ENDIF
     C*
     C*    Make sure item was entered.
     C*
     C           IUITEM    IFEQ *BLANKS
     C                     MOVE *ON       ERROR
     C                     MOVE DESC,26   DETMSG
     C                     GOTO ENDDIU
     C                     END
     C*
     C*    Make sure warehouse was entered.
     C*
     C           IUWHSE    IFEQ 0
     C                     MOVE *ON       ERROR
     C                     MOVE DESC,1    DETMSG
     C                     GOTO ENDDIU
     C                     END
     C*
     C* DELETE RECORD OR ERROR CHECK FOR UPDATE/ADD
     C           IUUTYP    IFEQ 'D'
     C                     EXSR ZZDELI
     C                     ELSE
     C                     EXSR ZZCHKI
     C                     ENDIF
     C*
     C           ERROR     IFEQ *OFF
     C           IUUTYP    ANDNE'D'
     C                     MOVE *OFF      ADDFLG  1
     C                     MOVE *OFF      CHGFLG  1
     C                     EXSR ZZUPD
     C                     ENDIF
     C*
     C           ERROR     IFEQ *ON
     C           CHGFLG    OREQ *ON
     C           ADDFLG    OREQ *ON
     C                     WRITEDETAIL
     C                     ADD  1         COUNT
     C                     ENDIF
     C*
     C*
     C*       Delete detail record from download file
     C*
     C                     DELETIUREC
     C*
     C           ENDDIU    TAG
     C                     ENDDO
     C*
     C*
     C*  Print order grand totals
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     IFGE LINPPG
     C                     WRITEHEAD1
     C                     ENDIF
     C                     WRITEGRNDTTL
     C                     ENDIF
     C*
     C*  We are finished so get out
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C                     Z-ADD$PWHS     $PWHSE  30
     C* Key definitions
     C*
     C* Download item updates file - Partial key
     C*
     C           IUKEY     KLIST
     C                     KFLD           $PWHSE
     C*
     C*  Define key for PIRITEM file.
     C*
     CSR         ITKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUITEM
416 AC*
416 AC*  Define key for PIRITEM file for base item.
416 AC*
416 ACSR         ITKEYB    KLIST
416 ACSR                   KFLD           $PWHSE
416 ACSR                   KFLD           #BITEM
     C*
     C*  Define key for PIRITEM file using Master Item.
     C*
     CSR         ITKEY2    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUMITM
     C*
     C*  Define key for PIRITEM file using Contract Base item.
     C*
     CSR         ITKEY4    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUCITM
     C*
     C*  Define key for PIRITEM file using Linked Item.
     C*
     CSR         ITKEY3    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           ILITM2
     C*
     C*  Define key for ITEMLNK file.
     C*
     CSR         ILKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUITEM
     C*
     C*  Define key for ITEMLNK1 file.
     C*
     CSR         IL1KEY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUITEM
     C*
     C*  Define key for ITEMDSC file.
     C*
     CSR         IXKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUITEM
     C*
     C*  Define key for ITEMDEF file.
     C*
     CSR         IDKEY     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IUITEM
     CSR                   KFLD           KYSDEF  2
     C*
     C*
     C*  Define key for item misc info file.
     C*
     CSR         IMKEY     KLIST
     CSR                   KFLD           IUWHSE
     CSR                   KFLD           IUITEM
     C*
     C*
     C*  Define key for options file.
     C*
     CSR         OPTNKY    KLIST
411 DC*R                   KFLD           $PWHSE
     CSR                   KFLD           KYWHSE  30
     CSR                   KFLD           OPCODE
     C*
416dAC*  Define key for UPC file.
     C*
416dACSR         UPKEY     KLIST
416dACSR                   KFLD           IUWHSE
416dACSR                   KFLD           IUITEM
     C*
     C*  Variables and constants.
     C*
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   MOVE #PROG     $LPRG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHKI   Item update error checking.
     C*
     CSR         ZZCHKI    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
413aDC**         IUUTYP    IFEQ 'A'
     C*
     C*  For base item, master item must be blank.
     C*
     CSR         IUTYPE    IFEQ ' '
     CSR         IUMITM    ANDNE*BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,41   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
510 AC*
510 AC*  Invalid Item Type,
510 AC*
510 AC           IUTYPE    IFNE ' '
510 AC           IUTYPE    ANDNE'A'
510 AC           IUTYPE    ANDNE'B'
510 AC           IUTYPE    ANDNE'S'
510 AC                     MOVE *ON       ERROR
510 AC                     MOVE DESC,46   DETMSG
510 AC           DETMSG    CAT  IUTYPE:0  DETMSG
510 AC           DETMSG    CAT  '''':0    DETMSG
510 AC                     GOTO ENDCKI
510 AC                     ENDIF
     C*
     C*  For Alias, Brkdown, Contract, and Same slot Brkdown,
     C*  Master item can't be blank.
     C*
     CSR         IUTYPE    IFEQ 'A'
     CSR         IUTYPE    OREQ 'B'
510 DC*          IUTYPE    OREQ 'C'
     CSR         IUTYPE    OREQ 'S'
     CSR         IUMITM    IFEQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,37   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  For Contract breakdown item, Contract base item can't be
     C*  blank.
     C*
510 DC*          IUTYPE    IFEQ 'C'
510 DC*          ITKEY2    CHAINPIRITEM             N76
510 DC*          ITTYPE    IFEQ 'B'
510 DC*          IUCITM    ANDEQ*BLANKS
510 DC*                    MOVE *ON       ERROR
510 DC*                    MOVE DESC,43   DETMSG
510 DC*                    GOTO ENDCKI
510 DC*                    ENDIF
510 DC*                    ENDIF
     C*
     C*  For same slot brkdown, brkdown must be defined.
     C*
     CSR         IUTYPE    IFEQ 'S'
     CSR         ITKEY2    CHAINPIRITEM             N76
     CSR         ITUM2     IFEQ *BLANKS
     CSR         ITUM3     ANDEQ*BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,42   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR         ITFLG1    IFEQ 'Y'
413aMCSR         ITFLG2    OREQ 'Y'
413aDC**         ITFLG2    ANDEQ'Y'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,42   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
413aDC*R                   ENDIF
     C*
     C*
     C*  Verify section type.
     C*
     CSR         OPSTYP    IFEQ 'Y'
     CSR         IUSTYP    IFNE 'F'
     CSR         IUSTYP    ANDNE'P'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,2    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify warehouse department.
     C*
     CSR         OPWHDP    IFEQ 'Y'
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADD$PWHSE    $LWHSE
     CSR                   MOVE IUWHDP    $LWHDP
     CSR                   CALL 'WD900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,3    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify designation area (optional).
     C*
     CSR         OPDESG    IFEQ 'Y'
     CSR         IUDESG    ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUDESG    $LDESG
     CSR                   CALL 'DS900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUDESG    DSCODE
412 ACSR                   MOVELCDSC,1    DSDESC
412 ACSR                   WRITEDSREC                  77
413aACSR                   ADD  1         NEWDSG
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,4    DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify activity zone (optional).
     C*
     CSR         OPACTV    IFEQ 'Y'
     CSR         IUACTV    ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUACTV    $LACTV
     CSR                   CALL 'AC900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUACTV    ACCODE
412 ACSR                   MOVELCDSC,1    ACDESC
412 ACSR                   WRITEACREC                  77
413aACSR                   ADD  1         NEWACT
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,5    DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Date Sensitive option.
     C*
     CSR         OPFLGD    IFEQ 'Y'
     CSR         IUFLGD    IFNE 'Y'
     CSR         IUFLGD    ANDNE'N'
412 ACSR                   MOVE 'N'       IUFLGD
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,6    DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Allow Bulk Pick option.
     C*
     CSR         OPFLGB    IFEQ 'Y'
     CSR         IUFLGB    IFNE 'Y'
     CSR         IUFLGB    ANDNE'N'
412 ACSR                   MOVE 'N'       IUFLGB
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,7    DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify vendor number is within range.
     C*
     CSR         OPVEN#    IFEQ 'Y'
     CSR         IUVEN#    IFLT 0
     CSR         IUVEN#    ORGT 9999999999
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,33   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify replenishment level is within range.
     C*
     CSR         OPREPL    IFEQ 'Y'
     CSR         IUREPL    IFLT 0
     CSR         IUREPL    ORGT 99999
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,34   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify master item was entered.
     C*
411bDC*R         ITTYPE    IFEQ 'B'
411bDC*R         ITTYPE    OREQ 'A'
411bDC*R         ITTYPE    OREQ 'C'
411bDC*R         ITTYPE    OREQ 'S'
411bMCSR         IUTYPE    IFEQ 'B'
411bMCSR         IUTYPE    OREQ 'A'
510 DC*          IUTYPE    OREQ 'C'
411bMCSR         IUTYPE    OREQ 'S'
     CSR         IUMITM    IFEQ *BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,37   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify full case item.
     C*
     CSR         OPMITM    IFEQ 'Y'
     CSR         IUMITM    IFNE *BLANKS
413aAC*  Full case item number must be different from broken case item.
413aACSR         IUMITM    IFEQ IUITEM
413aACSR                   MOVE *ON       ERROR
413aACSR                   MOVE DESC,10   DETMSG
413aACSR                   GOTO ENDCKI
413aACSR                   ENDIF
413aAC*  Get full case item.
     CSR         ITKEY2    CHAINPIRITEM             N79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,8    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     C*
     C*  Brk 1 quantity must be >0.
     CSR         *IN79     IFEQ *OFF
412 ACSR         ITUMQ2    ANDLE0
     CSR         IUTYPE    IFEQ 'B'
412 ACSR         IUTYPE    OREQ 'S'
412 DC*R         ITUMQ2    ANDLE0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,9    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
413aDC*  Full case item number must be different from broken case item.
413aDC*R         IUMITM    IFEQ IUITEM
413aDC*R                   MOVE *ON       ERROR
413aDC*R                   MOVE DESC,10   DETMSG
413aDC*R                   GOTO ENDCKI
413aDC*R                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Brk 1 quantity must be >0 if UOM is entered.
     CSR         OPUM2     IFEQ 'Y'
     CSR         IUUM2     IFNE *BLANKS
412 MCSR         IUUMQ2    ANDLT0
412 DC*R         IUUMQ2    ANDLE0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,9    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  UOM must be entered if Brk1 qty is entered.
     CSR         OPUMQ2    IFEQ 'Y'
     CSR         IUUMQ2    IFNE 0
     CSR         IUUM2     ANDEQ*BLANKS
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,9    DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify Contract Base item for contract broken case item.
     C*
     CSR         OPCITM    IFEQ 'Y'
     CSR         IUCITM    ANDNE*BLANKS
     CSR         ITKEY4    CHAINPIRITEM             N79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,44   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*
     C*  Verify Mfg date flag.
     C*
     CSR         OPMFLG    IFEQ 'Y'
     CSR         IUMFLG    IFNE 'Y'
     CSR         IUMFLG    ANDNE'N'
412 ACSR                   MOVE 'N'       IUMFLG
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,11   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify date sensitive flag, if Mfg date flag = 'Y'.
     C*
     CSR         OPMFLG    IFEQ 'Y'                         BEG01
     CSR         OPFLGD    IFEQ 'Y'
     CSR         IUMFLG    IFEQ 'Y'
     CSR         IUFLGD    ANDNE'Y'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,12   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify mfg shelf life days, if Mfg date flag = 'Y'.
     C*
     CSR         OPMDAY    IFEQ 'Y'
     CSR         IUMFLG    IFEQ 'Y'
     CSR         IUMDAY    ANDEQ0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,13   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify distributor days, if Mfg date flag = 'Y'.
     C*
     CSR         OPDDAY    IFEQ 'Y'
     CSR         IUMFLG    IFEQ 'Y'
     CSR         IUDDAY    ANDEQ0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,14   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify user days, if Mfg date flag = 'Y'.
     C*
     CSR         OPUDAY    IFEQ 'Y'
     CSR         IUMFLG    IFEQ 'Y'
     CSR         IUUDAY    ANDEQ0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,15   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF                            END01
     C**************
     C*
     C*  Verify catch weight.
     C*
     CSR         OPCWGT    IFEQ 'Y'
     CSR         IUCWGT    IFNE 'Y'
     CSR         IUCWGT    ANDNE'N'
412 ACSR                   MOVE 'N'       IUCWGT
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,16   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify normal unit of measure.
     C*
     CSR         OPUM1     IFEQ 'Y'
     CSR         IUUM1     ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUUM1     $LUM
     CSR                   CALL 'UM900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUUM1     UMCODE
412 ACSR                   MOVELIUUM1     UMSHRT
412 ACSR                   MOVELCDSC,1    UMDESC
412 ACSR                   WRITEUMREC                  77
413aACSR                   ADD  1         NEWUM
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,17   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 1 unit of measure.
     C*
     CSR         OPUM2     IFEQ 'Y'
     CSR         IUUM2     ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUUM2     $LUM
     CSR                   CALL 'UM900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUUM2     UMCODE
412 ACSR                   MOVELIUUM2     UMSHRT
412 ACSR                   MOVELCDSC,1    UMDESC
412 ACSR                   WRITEUMREC                  77
413aACSR                   ADD  1         NEWUM
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,17   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 1 unit of measure quantity.
     C*
     CSR         OPUMQ2    IFEQ 'Y'
     CSR         IUUMQ2    IFLT 0
     CSR         IUUMQ2    ORGT 999
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,18   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 1 Info Only option.
     C*
     CSR         OPFLG1    IFEQ 'Y'
     CSR         IUFLG1    IFNE 'Y'
     CSR         IUFLG1    ANDNE'N'
412 ACSR                   MOVE 'N'       IUFLG1
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,19   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 2 unit of measure.
     C*
     CSR         OPUM3     IFEQ 'Y'
     CSR         IUUM3     ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUUM3     $LUM
     CSR                   CALL 'UM900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUUM3     UMCODE
412 ACSR                   MOVELIUUM3     UMSHRT
412 ACSR                   MOVELCDSC,1    UMDESC
412 ACSR                   WRITEUMREC                  77
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,17   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 2 unit of measure quantity.
     C*
     CSR         OPUMQ3    IFEQ 'Y'
     CSR         IUUMQ3    IFLT 0
     CSR         IUUMQ3    ORGT 999
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,18   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify breakdown 2 Info Only option.
     C*
     CSR         OPFLG2    IFEQ 'Y'
     CSR         IUFLG2    IFNE 'Y'
     CSR         IUFLG2    ANDNE'N'
412 ACSR                   MOVE 'N'       IUFLG2
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,19   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Dimensions must be within range.
     C*
     CSR         OPULEN    IFEQ 'Y'
     CSR         IUULEN    IFLT 0
     CSR         IUULEN    ORGT 999.99
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,20   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         OPUWDT    IFEQ 'Y'
     CSR         IUUWDT    IFLT 0
     CSR         IUUWDT    ORGT 999.99
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,20   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         OPUHGT    IFEQ 'Y'
     CSR         IUUHGT    IFLT 0
     CSR         IUUHGT    ORGT 999.99
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,20   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*
     C*  Verify shipping weight is within range.
     C*
     CSR         OPSWGT    IFEQ 'Y'
     CSR         IUSWGT    IFLT 0
     CSR         IUSWGT    ORGT 99999.99
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,22   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Verify build code.(Optional)
     C*
     CSR         OPBLD     IFEQ 'Y'
     CSR         IUBLD     ANDNE*BLANKS
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE IUBLD     $LBC
     CSR                   CALL 'BC900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
412 ACSR                   MOVE IUBLD     BCCODE
412 ACSR                   Z-ADD0         BCSEQ
412 ACSR                   MOVELCDSC,1    BCDESC
412 ACSR                   WRITEBCREC                  77
413aACSR                   ADD  1         NEWBLD
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,23   DETMSG
412 DC*R                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Shelf life must must be >= 0.
     C*
     CSR         OPSLIF    IFEQ 'Y'
     CSR         IUSLIF    IFLT 0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,24   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*  Notice days must be >= 0.
     C*
     CSR         OPNDAY    IFEQ 'Y'
     CSR         IUNDAY    IFLT 0
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,25   DETMSG
     CSR                   GOTO ENDCKI
     CSR                   ENDIF
     CSR                   ENDIF
416aAC*
416aAC*  See if item type is changing and if it is ok to do so.
416aAC*
416aACSR         OPITYP    IFEQ 'Y'
416aACSR                   EXSR ZZCHKT
416aACSR         ERROR     CABEQ*ON       ENDCKI
416aACSR                   ENDIF
     C*
     CSR         ENDCKI    ENDSR
416aAC*----------------------------------------------------------------
416aAC*
416aAC*  ZZCHKT   Check if item type can be changed.
416aAC*
416aACSR         ZZCHKT    BEGSR
     CSR                   MOVE *OFF      A2NFLG  1
     C*
     C*  See if import item exists in PIRITEM file.
     C*
     CSR         ITKEY     CHAINPIRITEM             N79
     C*
     C*  If it doesn't, move on.
     C*
     CSR         *IN79     CABEQ*ON       ENDCKT
     C*
     C*  If type didn't change, move on.
     C*
     CSR         IUTYPE    CABEQITTYPE    ENDCKT
     C*
     C*  Only allowed to change from 'A' to ' '.
     C*
     CSR         ITTYPE    IFEQ 'A'
     CSR         IUTYPE    ANDEQ' '
     CSR                   MOVE *ON       A2NFLG
     CSR                   GOTO ENDCKT
     CSR                   ENDIF
     C*
     C*  For now, everthing else is an error.
     C*
     CSR                   MOVE *ON       ERROR
     CSR                   MOVEADESC,45   A30
     CSR                   MOVE ITTYPE    A30,22
     CSR                   MOVE IUTYPE    A30,29
     CSR                   MOVEAA30       DETMSG
     C*
     CSR         ENDCKT    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDELI   Item delete.
     C*
     CSR         ZZDELI    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C           ITKEY     CHAINPIRITEM             N79
     C           *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     C                     MOVE DESC,36   DETMSG
     C                     GOTO ENDDEL
     C                     ENDIF
     C*
     C*
     C*   Cannot delete, link record exists.
     C*
413aDC*R                   SELEC
413aDC*R         IUTYPE    WHEQ ' '
413aDC*R         IUTYPE    OREQ 'B'
     C*
     CSR         IL1KEY    SETLLITEMLNK1             79
     CSR         *IN79     IFEQ *OFF
     CSR         IL1KEY    READEITEMLNK1                 79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE *ON       ERROR
     C                     MOVE DESC,38   DETMSG
     C                     GOTO ENDDEL
     CSR                   ENDIF
     CSR                   ENDIF
413aDC*R                   ENDSL
416C C           ITTYPE    CABEQ'A'       SKIP1
416 AC*
416 AC*   Get corresponding base item.
416 AC*
416 ACSR                   EXSR ZZGETB
     C*
416 DC**                   SELEC
416 DC**         IUTYPE    WHEQ ' '
416 DC**         IUTYPE    OREQ 'B'
416 DC**         IUTYPE    OREQ 'C'
     C*
     C*   Get item quantity record.
416 AC*      for Base item.
     C*
416 DC**         ITKEY     CHAINITEMQTY              7978
416 MCSR         ITKEYB    CHAINITEMQTY              7978
     CSR                   SELEC
     CSR         *IN78     WHEQ *ON
     CSR                   MOVE *ON       ERROR
     C                     MOVE DESC,39   DETMSG
     C                     GOTO ENDDEL
     C*
     CSR         *IN79     WHEQ *OFF
     C*
     C*      Cannot delete if a quantity exists for item.
416c C*
416c C*      Only check break quantities for breakdown item
416c CSR         ITTYPE    IFEQ 'S'
416c C*
416c CSR         IQSTK2    IFNE 0
416c CSR         IQSTK3    ORNE 0
416c CSR         IQALC2    ORNE 0
416c CSR         IQALC3    ORNE 0
416c CSR         IQHLD2    ORNE 0
416c CSR         IQHLD3    ORNE 0
416c CSR         IQTRI2    ORNE 0
416c CSR         IQTRI3    ORNE 0
416c CSR         IQTRO2    ORNE 0
416c CSR         IQTRO3    ORNE 0
416c CSR         IQORD1    ORNE 0
416c CSR                   MOVE *ON       ERROR
416c C                     MOVE DESC,39   DETMSG
416c C                     GOTO ENDDEL
416c CSR                   ENDIF
416c CSR                   ELSE
     C*
416c C*      check all quantities for other item types.
     CSR         IQSTK1    IFNE 0
     CSR         IQSTK2    ORNE 0
     CSR         IQSTK3    ORNE 0
     CSR         IQALC1    ORNE 0
     CSR         IQALC2    ORNE 0
     CSR         IQALC3    ORNE 0
     CSR         IQHLD1    ORNE 0
     CSR         IQHLD2    ORNE 0
     CSR         IQHLD3    ORNE 0
     CSR         IQTRI1    ORNE 0
     CSR         IQTRI2    ORNE 0
     CSR         IQTRI3    ORNE 0
     CSR         IQTRO1    ORNE 0
     CSR         IQTRO2    ORNE 0
     CSR         IQTRO3    ORNE 0
     CSR         IQORD1    ORNE 0
     CSR                   MOVE *ON       ERROR
     C                     MOVE DESC,39   DETMSG
     C                     GOTO ENDDEL
     CSR                   ENDIF
416c CSR                   ENDIF
     C*
     C*      Delete item quantity record.
     C*
416 AC*       Don't do delete for 'A' and 'S' items.
416 AC*
416 ACSR         ITTYPE    IFNE 'A'
416 ACSR         ITTYPE    ANDNE'S'
     CSR                   DELETIQREC                  77
416 ACSR                   ENDIF
     C*
     CSR                   ENDSL
416 DC**                   ENDSL
     C*
     C*   Delete ITEMDEF records.
     C*
     CSR         ITKEY     SETLLITEMDEF
     CSR                   MOVE *OFF      *IN79
     CSR         *IN79     DOUEQ*ON
     CSR         ITKEY     READEITEMDEF                7879
     C*
     C*     If record is locked then send message back and abort.
     C*
     CSR         *IN78     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     C                     MOVE DESC,40   DETMSG
     C                     GOTO ENDDEL
     CSR                   ENDIF
     C*
     C*     Otherwise, delete record.
     C*
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETIDREC                  77
     CSR                   ENDIF
     C*
     CSR                   ENDDO
     C*
     C*     Delete ITEMDSC extended description rec.
     C*
     CSR         IXKEY     CHAINITEMDSC              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETIXREC
     CSR                   ENDIF
     C*
     C*     Delete ITEMMSC misc  record
     C*
     CSR         ITKEY     CHAINITEMMSC              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETIMREC
     CSR                   ENDIF
     C*
416c CSR         SKIP1     TAG
     C*
     CSR         ITTYPE    IFEQ 'A'
     CSR         ITTYPE    OREQ 'B'
510 DC*          ITTYPE    OREQ 'C'
     CSR         ITTYPE    OREQ 'S'
     CSR         ILKEY     CHAINITEMLNK              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETILREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
416dACSR         UPKEY     SETLLUPC1
416dACSR         FOREVR    DOWEQFOREVR
416dACSR         UPKEY     READEUPC1                     79
416dACSR         *IN79     IFEQ *ON
416dACSR                   LEAVE
416dACSR                   ENDIF
416dACSR                   DELETUPREC
416dACSR                   ENDDO
     C*
     CSR         ITKEY     CHAINPIRITEM              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETITREC
     CSR                   ENDIF
     C*
     CSR         ENDDEL    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill Item fields.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     C*
     C           OPWHDP    IFEQ 'Y'
     C           IUWHDP    ANDNEITWHDP
     C                     MOVE DESC,29   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C           OPSTYP    IFEQ 'Y'
     C           IUSTYP    ANDNEITSTYP
     C                     MOVE DESC,30   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C           OPDESG    IFEQ 'Y'
     C           IUDESG    ANDNEITDESG
     C                     MOVE DESC,31   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C           OPULEN    IFEQ 'Y'
     C           IUULEN    ANDNEITULEN
     C                     MOVE DESC,32   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C           OPUWDT    IFEQ 'Y'
     C           IUUWDT    ANDNEITUWDT
     C                     MOVE DESC,32   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C           OPUHGT    IFEQ 'Y'
     C           IUUHGT    ANDNEITUHGT
     C                     MOVE DESC,32   DETMSG
     C                     MOVE *ON       CHGFLG
     C                     ENDIF
     C*
     C*  Move fields from download file to PIRITEM fields
     C*
     CSR         OPITYP    IFEQ 'Y'
     CSR                   MOVELIUTYPE    ITTYPE
     CSR                   ENDIF
     CSR         OPDESC    IFEQ 'Y'
     CSR                   MOVELIUDESC    ITDESC
     CSR                   ENDIF
     CSR         OPSTYP    IFEQ 'Y'
     CSR                   MOVE IUSTYP    ITSTYP
     CSR                   ENDIF
     CSR         OPWHDP    IFEQ 'Y'
     CSR                   MOVE IUWHDP    ITWHDP
     CSR                   ENDIF
     CSR         OPDESG    IFEQ 'Y'
     CSR                   MOVE IUDESG    ITDESG
     CSR                   ENDIF
     CSR         OPACTV    IFEQ 'Y'
     CSR                   MOVE IUACTV    ITACTV
     CSR                   ENDIF
     CSR         OPFLGD    IFEQ 'Y'
     CSR                   MOVE IUFLGD    ITFLGD
     CSR                   ENDIF
     CSR         OPFLGB    IFEQ 'Y'
     CSR                   MOVE IUFLGB    ITFLGB
     CSR                   ENDIF
     CSR                   MOVE *BLANKS   ITUPC
     CSR         OPVEN#    IFEQ 'Y'
     CSR                   Z-ADDIUVEN#    ITVEN#
     CSR                   ENDIF
     CSR         OPVIT#    IFEQ 'Y'
     CSR                   MOVE IUVIT#    ITVIT#
     CSR                   ENDIF
     CSR         OPREPL    IFEQ 'Y'
     CSR                   Z-ADDIUREPL    ITREPL
     CSR                   ENDIF
     CSR         OPPDSC    IFEQ 'Y'
     CSR                   MOVE IUPDSC    ITPDSC
     CSR                   ENDIF
     CSR         OPUM1     IFEQ 'Y'
     CSR                   MOVE IUUM1     ITUM1
     CSR                   ENDIF
     CSR         OPUM2     IFEQ 'Y'
     CSR                   MOVE IUUM2     ITUM2
     CSR                   ENDIF
     CSR         OPUMQ2    IFEQ 'Y'
     CSR                   Z-ADDIUUMQ2    ITUMQ2
     CSR                   ENDIF
     CSR         OPFLG1    IFEQ 'Y'
     CSR         IUUM2     IFEQ ' '
     CSR                   MOVE 'N'       ITFLG1
     CSR                   ELSE
     CSR                   MOVE IUFLG1    ITFLG1
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         OPUM3     IFEQ 'Y'
     CSR                   MOVE IUUM3     ITUM3
     CSR                   ENDIF
     CSR         OPUMQ3    IFEQ 'Y'
     CSR                   Z-ADDIUUMQ3    ITUMQ3
     CSR                   ENDIF
     CSR         OPFLG2    IFEQ 'Y'
     CSR         IUUM3     IFEQ ' '
     CSR                   MOVE 'N'       ITFLG2
     CSR                   ELSE
     CSR                   MOVE IUFLG2    ITFLG2
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         OPULEN    IFEQ 'Y'
     CSR                   Z-ADDIUULEN    ITULEN
     CSR                   ENDIF
     CSR         OPUWDT    IFEQ 'Y'
     CSR                   Z-ADDIUUWDT    ITUWDT
     CSR                   ENDIF
     CSR         OPUHGT    IFEQ 'Y'
     CSR                   Z-ADDIUUHGT    ITUHGT
     CSR                   ENDIF
     C*
     CSR         OPCUBE    IFEQ 'Y'
     CSR                   Z-ADDIUCUBE    ITCUBE
     CSR                   ELSE
     CSR         OPULEN    IFEQ 'Y'
     CSR         OPUWDT    ANDEQ'Y'
     CSR         OPUHGT    ANDEQ'Y'
     C*  Calculate actual cube according to dimensions entered.
     C*
     CSR         *LIKE     DEFN ITCUBE    ACTCUB
     CSR         OPCDIV    IFEQ 0
     CSR                   Z-ADD0         ACTCUB
     CSR                   ELSE
     CSR         ITULEN    MULT ITUWDT    TMPCUB 156
     CSR                   MULT ITUHGT    TMPCUB
     CSR         TMPCUB    DIV  OPCDIV    ACTCUB    H
     CSR                   END
     C*
     C*   Use calculated cube if ...
     C*      Auto calculate option is 'Y'
413aAC*      and Cube is zero
     C*      and Cube is not imported from host.
     C*
     CSR         OPCAUT    IFEQ 'Y'
413aACSR         ITCUBE    ANDEQ0
     CSR                   Z-ADDACTCUB    ITCUBE
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR         OPSWGT    IFEQ 'Y'
     CSR                   Z-ADDIUSWGT    ITSWGT
     CSR                   ENDIF
     CSR         OPCWGT    IFEQ 'Y'
     CSR                   MOVE IUCWGT    ITCWGT
     CSR                   ENDIF
     CSR         OPBLD     IFEQ 'Y'
     CSR                   MOVE IUBLD     ITBLD
     CSR                   ENDIF
     CSR         OPSLIF    IFEQ 'Y'
     CSR                   Z-ADDIUSLIF    ITSLIF
     CSR                   ENDIF
     CSR         OPNDAY    IFEQ 'Y'
     CSR                   Z-ADDIUNDAY    ITNDAY
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL2   Fill Item fields.
     C*
     CSR         ZZFIL2    BEGSR
     C*
     CSR         OPITYP    IFEQ 'Y'
     CSR                   MOVELIUTYPE    ITTYPE
     CSR                   ENDIF
     C*   Fill PIRITEM using saved base item fields.
     CSR                   MOVE SVWHDP    ITWHDP
413aACSR                   MOVE SVSTYP    ITSTYP
     CSR                   MOVE SVDESG    ITDESG
     CSR                   MOVE SVACTV    ITACTV
     CSR                   MOVE SVUPC     ITUPC
     CSR                   MOVE SVBLD     ITBLD
     CSR                   Z-ADDSVVEN#    ITVEN#
     CSR                   MOVE SVVIT#    ITVIT#
     CSR                   MOVE SVUM1     ITUM1
     CSR                   MOVE SVUM2     ITUM2
     CSR                   Z-ADDSVUMQ2    ITUMQ2
     CSR                   MOVE SVFLG1    ITFLG1
     CSR                   MOVE SVUM3     ITUM3
     CSR                   Z-ADDSVUMQ3    ITUMQ3
     CSR                   MOVE SVFLG2    ITFLG2
     CSR                   Z-ADDSVULEN    ITULEN
     CSR                   Z-ADDSVUWDT    ITUWDT
     CSR                   Z-ADDSVUHGT    ITUHGT
     CSR                   Z-ADDSVCUBE    ITCUBE
     CSR                   Z-ADDSVSWGT    ITSWGT
     CSR                   MOVE SVCWGT    ITCWGT
     CSR                   Z-ADDSVREOR    ITREOR
     CSR                   Z-ADDSVREPL    ITREPL
     CSR                   Z-ADDSVRCVD    ITRCVD
     CSR                   MOVE SVFLGD    ITFLGD
     CSR                   MOVE SVFLGB    ITFLGB
     CSR                   Z-ADDSVSLIF    ITSLIF
     CSR                   Z-ADDSVNDAY    ITNDAY
     C*
412 DC*R                   MOVE IUDESC    ITDESC
412 MCSR                   MOVELIUDESC    ITDESC
     CSR                   MOVE IUPDSC    ITPDSC
     C*
     C                     ENDSR
416 AC*----------------------------------------------------------------
416 AC*
416 AC*  ZZGETB   Get base item to use for checking ITEMQTY.
416 AC*
416 ACSR         ZZGETB    BEGSR
     C*
     C*  Initialize base item info with current item.
     C*
     CSR                   MOVE ITITEM    #BITEM
     C*
     C*  If Alias or SSB Item
     C*   Then get Base item.
     C*
     CSR         ITTYPE    IFEQ 'A'
     CSR         ITTYPE    OREQ 'S'
     C*
     CSR                   CALL 'GETBASE'
     CSR                   PARM ITWHSE    $GWHSE  30
     CSR                   PARM ITITEM    $GITEM 15
     CSR                   PARM ' '       #BITEM 15
     CSR                   PARM           #BITYP  1
     CSR                   PARM           #BDESC 30
     CSR                   PARM           #BPDSC 15
     CSR                   PARM           #BWHDP  5
     CSR                   PARM           #BSTYP  1
     CSR                   PARM           #BUM    2
     CSR                   PARM           #BUM2   2
     CSR                   PARM           #BUMQ2  30
     CSR                   PARM           #BUM3   2
     CSR                   PARM           #BUMQ3  30
     CSR                   PARM ' '       #BCITM 15
     CSR                   PARM           #BCTYP  1
     CSR                   PARM           #BCDSC 30
     CSR                   PARM           #BCPDS 15
     CSR                   PARM           #BCWDP  5
     CSR                   PARM           #BCSTP  1
     CSR                   PARM           #BCUM   2
     CSR                   PARM           #BCUM2  2
     CSR                   PARM           #BCUQ2  30
     CSR                   PARM           #BCUM3  2
     CSR                   PARM           #BCUQ3  30
     CSR                   PARM           #BRTN   8
     C*
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C* Get import date/time.
     C*
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    IMPDTE  80
     C                     TIME           IMPTIM  60
     C*
     C* Get report heading.
     C*
     C                     MOVE *BLANKS   W1HEAD
     C                     MOVEL'II225PR' RPPRTF
     C           RPPRTF    CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD50        $PLEN
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     W1HEAD
     C*
     C*   Get warehouse description.
     C*
     C           $PWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   WHDESC
     C                     END
     C*
     C*   Create warehouse heading.
     C*
     C                     MOVE $PWHSE    WORK3A  3
     C           ' 0'      CHECKWORK3A    POS     10
     C           4         SUB  POS       LEN     10
     C           LEN       SUBSTWORK3A:POSWHHEAD
     C           WHHEAD    CAT  '-':1     WHHEAD
     C           WHHEAD    CAT  WHDESC:1  WHHEAD
     C                     CALL 'CENTER'
     C                     PARM WHHEAD    $CSTR  60
     C                     PARM 40        $CLEN   30
     C                     MOVEL$CSTR     WHHEAD
     C*
     C* Initialize grand total fields
     C*
     C                     Z-ADD0         GRDORD  50
     C                     Z-ADD0         GRDLIN
     C*                    Z-ADD0         PREOH
     C*                    Z-ADD0         PREOD
     C                     Z-ADD0         NOITEM
     C                     Z-ADD0         BADBRK
413aAC                     Z-ADD0         NEWDSG
413aAC                     Z-ADD0         NEWACT
413aAC                     Z-ADD0         NEWBLD
413aAC                     Z-ADD0         NEWUM
     C*
     C*   Get item maintenance options.
     C*
     C                     EXSR ZZXOPT
     C*
     C*   Get import item fields options.
     C*
     C                     Z-ADD0         KYWHSE
     CSR                   MOVE '*IMPITEM'OPCODE
     CSR         OPTNKY    CHAINOPTIONS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE OPDATA    OPDAT2
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZSVIT   Save PIRITEM fields.
     C*
     CSR         ZZSVIT    BEGSR
     C*
     C*   Save base item fields.
     CSR                   Z-ADDITWHSE    SVWHSE  30
     CSR                   MOVE ITITEM    SVITEM 15
     CSR                   MOVE ITSTYP    SVSTYP  1
     CSR                   MOVE ITWHDP    SVWHDP  5
     CSR                   MOVE ITDESG    SVDESG  5
     CSR                   MOVE ITACTV    SVACTV  2
     CSR                   MOVE ITUPC     SVUPC  15
     CSR                   MOVE ITBLD     SVBLD   2
     CSR                   Z-ADDITVEN#    SVVEN# 100
     CSR                   MOVE ITVIT#    SVVIT# 15
     CSR                   MOVE ITUM1     SVUM1   2
     CSR                   MOVE ITUM2     SVUM2   2
     CSR                   Z-ADDITUMQ2    SVUMQ2  30
     CSR                   MOVE ITFLG1    SVFLG1  1
     CSR                   MOVE ITUM3     SVUM3   2
     CSR                   Z-ADDITUMQ3    SVUMQ3  30
     CSR                   MOVE ITFLG2    SVFLG2  1
     CSR                   Z-ADDITULEN    SVULEN  52
     CSR                   Z-ADDITUWDT    SVUWDT  52
     CSR                   Z-ADDITUHGT    SVUHGT  52
     CSR                   Z-ADDITCUBE    SVCUBE  73
     CSR                   Z-ADDITSWGT    SVSWGT  72
     CSR                   MOVE ITCWGT    SVCWGT  1
     CSR                   Z-ADDITREOR    SVREOR  50
     CSR                   Z-ADDITREPL    SVREPL  50
     CSR                   Z-ADDITRCVD    SVRCVD  80
     CSR                   MOVE ITFLGD    SVFLGD  1
     CSR                   MOVE ITFLGB    SVFLGB  1
     CSR                   Z-ADDITSLIF    SVSLIF  30
     CSR                   Z-ADDITNDAY    SVNDAY  30
     C*
     C                     ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update for item type.
     C*
     CSR         ZZUPD     BEGSR
     C*
     C                     SELEC
     C*
     C*  Base or Broken item type
     C           IUTYPE    WHEQ ' '
     C           IUTYPE    OREQ 'B'
     C*  Get PIRITEM record
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     CSR                   CLEARITREC
     CSR                   Z-ADDIUWHSE    ITWHSE
     CSR                   MOVE IUITEM    ITITEM
     C                     ENDIF
     C*
     C                     EXSR ZZFIL1
     C                     EXSR ZZUPD1
     C*
     C*  Alias and Contract item type
     C           IUTYPE    WHEQ 'A'
510 DC*          IUTYPE    OREQ 'C'
     C           IUTYPE    OREQ 'S'
     C*
413aDC*          IUUTYP    IFEQ 'A'
413aDC*  For Add, Get PIRITEM record for Base item
     C*
413bAC*  If 'S' type record and Normal UOM is not blank
413bAC*    Then get base item record for later update.
413bAC*    Otherwise get base item record as non-update.
     C*
413bAC           IUTYPE    IFEQ 'S'
413bAC           IUUM1     ANDNE' '
413bAC           ITKEY2    CHAINPIRITEM              79
413bAC                     ELSE
     C           ITKEY2    CHAINPIRITEM             N79
413bAC                     ENDIF
     C           *IN79     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,37   DETMSG
     CSR                   GOTO ENDUPD
     CSR                   ELSE
     C*
413bAC*     If 'S' type record and Normal UOM is not blank
413bAC*        force 'S' item UM1 into base UM2
413bAC*        force 'S' item UM2 into base UM3
413bAC*        force 'S' item UM2 QTY into base UM3 QTY
413bAC*        update base item record.
413bAC*
413bAC           IUTYPE    IFEQ 'S'
413bAC           IUUM1     ANDNE' '
413bACSR                   MOVE IUUM1     ITUM2   2
413bACSR                   MOVE IUUM2     ITUM3   2
413bACSR                   Z-ADDIUUMQ2    ITUMQ3  30
413bACSR                   UPDATITREC
413bACSR                   ENDIF
     CSR                   EXSR ZZSVIT
     CSR                   ENDIF
     C*  For Add, Get ITEMMSC record for Base item
     CSR         ITKEY2    CHAINITEMMSC             N79
     C*   Save base record for ITEMMSC file
413bAC           *IN79     IFEQ *ON
413bACSR                   Z-ADD0         SVMTIE
413bACSR                   Z-ADD0         SVMHIG
413bACSR                   MOVE *BLANKS   SVMFLG
413bACSR                   Z-ADD0         SVDDAY
413bACSR                   Z-ADD0         SVUDAY
413bACSR                   Z-ADD0         SVMDAY
413bACSR                   MOVE *BLANKS   SVWRNC
413bACSR                   Z-ADD0         SVRG1C
413bACSR                   Z-ADD0         SVRG2C
413bACSR                   MOVE *BLANKS   SVAPCK
413bACSR                   Z-ADD0         SVNRPK
417bACSR                   MOVE 'N'       SVNSLT
417bACSR                   MOVE 'N'       SVXDCK
417bACSR                   Z-ADD0         SVBNBR
417bACSR                   MOVE *BLANKS   SVBNAM
417bACSR                   MOVE *BLANKS   SVVNAM
417bACSR                   MOVE *BLANKS   SVBRND
417bACSR                   MOVE 'N'       SVLOT
417bACSR                   MOVE 'N'       SVLTRK
413bACSR                   ELSE
     CSR                   Z-ADDIMTIE     SVMTIE
     CSR                   Z-ADDIMHIGH    SVMHIG
     CSR                   MOVE IMMFLG    SVMFLG
     CSR                   Z-ADDIMDDAY    SVDDAY
     CSR                   Z-ADDIMUDAY    SVUDAY
     CSR                   Z-ADDIMMDAY    SVMDAY
413aACSR                   MOVE IMWRNC    SVWRNC
413aACSR                   Z-ADDIMRG1C    SVRG1C
413aACSR                   Z-ADDIMRG2C    SVRG2C
413aACSR                   MOVE IMAPCK    SVAPCK
413aACSR                   Z-ADDIMNRPK    SVNRPK
417bACSR                   MOVE IMNSLT    SVNSLT
417bACSR                   MOVE IMXDCK    SVXDCK
417bACSR                   Z-ADDIMBNBR    SVBNBR
417bACSR                   MOVE IMBNAM    SVBNAM
417bACSR                   MOVE IMVNAM    SVVNAM
417bACSR                   MOVE IMBRND    SVBRND
417bACSR                   MOVE IMLOT     SVLOT
417bACSR                   MOVE IMLTRK    SVLTRK
413aDC**                   ENDIF
413bACSR                   ENDIF
     C*
     C           ITKEY     CHAINPIRITEM              79
412 DC*          IUUTYP    IFEQ 'A'
412 DC*          *IN79     ANDEQ*OFF
412 DC*R                   MOVE *ON       ERROR
412 DC*R                   MOVE DESC,27   DETMSG
412 DC*R                   GOTO ENDUPD
412 DC*                    ENDIF
     C           *IN79     IFEQ *ON
     CSR                   CLEARITREC
     CSR                   Z-ADDIUWHSE    ITWHSE
     CSR                   MOVE IUITEM    ITITEM
     C                     ENDIF
     C*
     C                     EXSR ZZFIL2
     C                     EXSR ZZUPD2
     C*
     C                     ENDSL
     C*
     C           ENDUPD    ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD1   Update files for item type ' ' or 'B'
     C*
     CSR         ZZUPD1    BEGSR
     C*
411  C*   Add record
     C*
     CSR         *IN79     IFEQ *ON                        BEG01
     CSR                   WRITEITREC                  77
     CSR         *IN77     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,27   DETMSG
     CSR                   GOTO ENDUP1
     CSR                   END
     C*
     CSR                   MOVE *ON       ADDFLG
     CSR                   MOVE DESC,28   DETMSG
     C*
     C*       Add record to item quantity file.
     C*
     CSR                   CLEARIQREC
     CSR                   Z-ADDIUWHSE    IQWHSE
     CSR                   MOVE IUITEM    IQITEM
     CSR                   WRITEIQREC                  77
     CSR         *IN77     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,35   DETMSG
     CSR                   GOTO ENDUP1
     CSR                   ENDIF
     C*
     CSR                   ELSE                            CHANGE
     CSR                   UPDATITREC
     CSR                   ENDIF                           END01
416bAC*
416bAC*   Add record to item quantity file when type changes.
416bAC*     If error occurs, assume record already exists.
416bAC*
416bACSR         A2NFLG    IFEQ *ON
416bACSR                   CLEARIQREC
416bACSR                   Z-ADDIUWHSE    IQWHSE
416bACSR                   MOVE IUITEM    IQITEM
416bACSR                   WRITEIQREC                  77
416bACSR                   ENDIF
     C*
     C*
     C*   Add record to ITEMDSC file.
     C*
     CSR         OPDESC    IFEQ 'Y'
     CSR         PXFLG     ANDEQ'1'
     CSR         IXKEY     CHAINITEMDSC              76
     CSR                   MOVELIUDESC    IXDSC1
     CSR                   MOVE IUDESC    IXDSC2
     CSR         *IN76     IFEQ *OFF
     CSR                   UPDATIXREC
     CSR                   ELSE
     CSR                   Z-ADDITWHSE    IXWHSE
     CSR                   MOVE ITITEM    IXITEM
     CSR                   WRITEIXREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*   Add/Change record to ITEMMSC file.
     C*
     CSR         IMKEY     CHAINITEMMSC              79
     CSR         OPTIE     IFEQ 'Y'
     CSR                   Z-ADDIUTIE     IMTIE
     CSR                   ENDIF
     CSR         OPHIGH    IFEQ 'Y'
     CSR                   Z-ADDIUHIGH    IMHIGH
     CSR                   ENDIF
     CSR         OPMFLG    IFEQ 'Y'
     CSR                   MOVE IUMFLG    IMMFLG
     CSR                   ENDIF
     CSR         OPDDAY    IFEQ 'Y'
     CSR                   Z-ADDIUDDAY    IMDDAY
     CSR                   ENDIF
     CSR         OPUDAY    IFEQ 'Y'
     CSR                   Z-ADDIUUDAY    IMUDAY
     CSR                   ENDIF
     CSR         OPMDAY    IFEQ 'Y'
     CSR                   Z-ADDIUMDAY    IMMDAY
     CSR                   ENDIF
414 DC**                   MOVE 'Y'       IMWRNC
414 DC**                   Z-ADD0         IMRG1C
414 DC**                   Z-ADD0         IMRG2C
414 DC**                   MOVE 'N'       IMAPCK
414 DC**                   Z-ADD0         IMNRPK
     CSR         *IN79     IFEQ *OFF
     CSR                   UPDATIMREC
     CSR                   ELSE
414 ACSR                   MOVE 'Y'       IMWRNC
414 ACSR                   Z-ADD0         IMRG1C
414 ACSR                   Z-ADD0         IMRG2C
414 ACSR                   MOVE 'N'       IMAPCK
     CSR                   Z-ADDIUWHSE    IMWHSE
     CSR                   MOVE IUITEM    IMITEM
412bACSR         IUSTYP    IFEQ 'F'
412bACSR                   Z-ADD1         IMNRPK
412bACSR                   ELSE
412bACSR                   MOVE *ZEROS    IMNRPK
412bACSR                   ENDIF
417bACSR                   MOVE 'N'       IMLOT
417bACSR                   MOVE 'N'       IMLTRK
     CSR                   WRITEIMREC
     CSR                   ENDIF
     C*   Save base record for ITEMMSC file
     CSR                   Z-ADDIMTIE     SVMTIE  40
     CSR                   Z-ADDIMHIGH    SVMHIG  20
     CSR                   MOVE IMMFLG    SVMFLG  1
     CSR                   Z-ADDIMDDAY    SVDDAY  30
     CSR                   Z-ADDIMUDAY    SVUDAY  30
     CSR                   Z-ADDIMMDAY    SVMDAY  30
413aACSR                   MOVE IMWRNC    SVWRNC  1
413aACSR                   Z-ADDIMRG1C    SVRG1C  31
413aACSR                   Z-ADDIMRG2C    SVRG2C  31
413aACSR                   MOVE IMAPCK    SVAPCK  1
413aACSR                   Z-ADDIMNRPK    SVNRPK  30
417bACSR                   MOVE IMNSLT    SVNSLT  1
417bACSR                   MOVE IMXDCK    SVXDCK  1
417bACSR                   Z-ADDIMBNBR    SVBNBR  30
417bACSR                   MOVE IMBNAM    SVBNAM 40
417bACSR                   MOVE IMVNAM    SVVNAM 40
417bACSR                   MOVE IMBRND    SVBRND 10
417bACSR                   MOVE IMLOT     SVLOT   1
417bACSR                   MOVE IMLTRK    SVLTRK  1
417bACSR                   Z-ADDIMODAY    SVODAY  30
     C*
     C*
     C*   Add record to ITEMLNK file.
416aAC*     for Brokencase item.
     C*
     CSR                   SELEC
     CSR         IUTYPE    WHEQ 'B'
     C*
     CSR         OPMITM    IFEQ 'Y'
     CSR         IUMITM    ANDNE*BLANKS
     CSR         ILKEY     CHAINITEMLNK              79
     CSR                   MOVE IUMITM    ILITM2
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADDIUWHSE    ILWHSE
     CSR                   MOVE IUITEM    ILITEM
     CSR                   WRITEILREC
     CSR                   ELSE
     CSR                   UPDATILREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*   Delete record from ITEMLNK file.
416aAC*     for Normal item.
416aAC*
416aACSR                   OTHER
     C*
416aDC**         OPMITM    IFEQ 'Y'
416aDC**         IUMITM    ANDEQ*BLANKS
     CSR         ILKEY     CHAINITEMLNK              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETILREC
     CSR                   ENDIF
416aDC**                   ENDIF
     C*
     CSR                   ENDSL
     C*
     C*   Update any dependent file if update type = Change
     C*
     CSR                   SELEC
     CSR         IUUTYP    WHEQ 'C'
     C*   Save base item fields.
     CSR                   EXSR ZZSVIT
     CSR                   MOVE *OFF      EOFL    1
     CSR         ILKEY     SETLLITEMLNK1
     CSR         EOFL      DOUEQ*ON
     C*
     CSR         ILKEY     READEITEMLNK1                 75
     CSR         *IN75     IFEQ *ON
     CSR                   MOVE *ON       EOFL
     CSR                   GOTO ENDUP1
     C*
     C           ITKEY3    CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     CSR                   GOTO ENDUP1
     CSR                   ENDIF
     CSR         ITTYPE    IFEQ 'A'
510 DC*          ITTYPE    OREQ 'C'
     CSR         ITTYPE    OREQ 'S'
     C*   Create PIRITEM using saved base item fields.
     CSR                   MOVE SVWHDP    ITWHDP
413aACSR                   MOVE SVSTYP    ITSTYP
     CSR                   MOVE SVDESG    ITDESG
     CSR                   MOVE SVACTV    ITACTV
     CSR                   MOVE SVUPC     ITUPC
     CSR                   MOVE SVBLD     ITBLD
     CSR                   Z-ADDSVVEN#    ITVEN#
     CSR                   MOVE SVVIT#    ITVIT#
     CSR                   MOVE SVUM1     ITUM1
     CSR                   MOVE SVUM2     ITUM2
     CSR                   Z-ADDSVUMQ2    ITUMQ2
     CSR                   MOVE SVFLG1    ITFLG1
     CSR                   MOVE SVUM3     ITUM3
     CSR                   Z-ADDSVUMQ3    ITUMQ3
     CSR                   MOVE SVFLG2    ITFLG2
     CSR                   Z-ADDSVULEN    ITULEN
     CSR                   Z-ADDSVUWDT    ITUWDT
     CSR                   Z-ADDSVUHGT    ITUHGT
     CSR                   Z-ADDSVCUBE    ITCUBE
     CSR                   Z-ADDSVSWGT    ITSWGT
     CSR                   MOVE SVCWGT    ITCWGT
     CSR                   Z-ADDSVREOR    ITREOR
     CSR                   Z-ADDSVREPL    ITREPL
     CSR                   Z-ADDSVRCVD    ITRCVD
     CSR                   MOVE SVFLGD    ITFLGD
     CSR                   MOVE SVFLGB    ITFLGB
     CSR                   Z-ADDSVSLIF    ITSLIF
     CSR                   Z-ADDSVNDAY    ITNDAY
     CSR                   UPDATITREC
     C*
     C           ITKEY3    CHAINITEMMSC              79
     C           *IN79     IFEQ *ON
     CSR                   GOTO ENDUP1
     CSR                   ENDIF
     C*   Create ITEMMSC using saved base item fields.
     CSR                   Z-ADDSVMTIE    IMTIE
     CSR                   Z-ADDSVMHIG    IMHIGH
     CSR                   MOVE SVMFLG    IMMFLG
     CSR                   Z-ADDSVDDAY    IMDDAY
     CSR                   Z-ADDSVUDAY    IMUDAY
     CSR                   Z-ADDSVMDAY    IMMDAY
413aACSR                   MOVE SVWRNC    IMWRNC
413aACSR                   Z-ADDSVRG1C    IMRG1C
413aACSR                   Z-ADDSVRG2C    IMRG2C
413aACSR                   MOVE SVAPCK    IMAPCK
413aACSR                   Z-ADDSVNRPK    IMNRPK
417bACSR                   MOVE SVNSLT    IMNSLT
417bACSR                   MOVE SVXDCK    IMXDCK
417bACSR                   Z-ADDSVBNBR    IMBNBR
417bACSR                   MOVE SVBNAM    IMBNAM
417bACSR                   MOVE SVVNAM    IMVNAM
417bACSR                   MOVE SVBRND    IMBRND
417bACSR                   MOVE SVLOT     IMLOT
417bACSR                   MOVE SVLTRK    IMLTRK
417bACSR                   Z-ADDSVODAY    IMODAY
     C                     ENDIF
     C*
     C                     ENDIF
     C           ENDUP1    TAG
     C                     ENDDO
     CSR                   ENDSL
     C                     ENDSR
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD2   Update files for item type 'A' or 'C'
     C*
     CSR         ZZUPD2    BEGSR
     C*
411  C*   Add record
     C*
     CSR         *IN79     IFEQ *ON                        BEG01
     CSR                   WRITEITREC                  77
     CSR         *IN77     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE DESC,27   DETMSG
     CSR                   GOTO ENDUP2
     CSR                   END
     C*
     CSR                   MOVE *ON       ADDFLG
     CSR                   MOVE DESC,28   DETMSG
     C*
     CSR                   ELSE                            CHANGE
     CSR                   UPDATITREC
     CSR                   ENDIF                           END01
     C*
     C*
     C*   Add record to ITEMDSC file.
     C*
     CSR         OPDESC    IFEQ 'Y'
     CSR         PXFLG     ANDEQ'1'
     CSR         IXKEY     CHAINITEMDSC              76
     CSR                   MOVELIUDESC    IXDSC1
     CSR                   MOVE IUDESC    IXDSC2
     CSR         *IN76     IFEQ *OFF
     CSR                   UPDATIXREC
     CSR                   ELSE
     CSR                   Z-ADDITWHSE    IXWHSE
     CSR                   MOVE ITITEM    IXITEM
     CSR                   WRITEIXREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*   Add/Change record to ITEMMSC file.
     C*
     CSR         ITKEY     CHAINITEMMSC              79
412 DC*R         *IN79     IFEQ *ON
     CSR                   Z-ADDIUWHSE    IMWHSE
     CSR                   MOVE IUITEM    IMITEM
     C*   crete using saved base fields.
     CSR                   Z-ADDSVMTIE    IMTIE
     CSR                   Z-ADDSVMHIG    IMHIGH
     CSR                   MOVE SVMFLG    IMMFLG
     CSR                   Z-ADDSVDDAY    IMDDAY
     CSR                   Z-ADDSVUDAY    IMUDAY
     CSR                   Z-ADDSVMDAY    IMMDAY
413aACSR                   MOVE SVWRNC    IMWRNC
413aACSR                   Z-ADDSVRG1C    IMRG1C
413aACSR                   Z-ADDSVRG2C    IMRG2C
413aACSR                   MOVE SVAPCK    IMAPCK
413aACSR                   Z-ADDSVNRPK    IMNRPK
417bACSR                   MOVE SVNSLT    IMNSLT
417bACSR                   MOVE SVXDCK    IMXDCK
417bACSR                   Z-ADDSVBNBR    IMBNBR
417bACSR                   MOVE SVBNAM    IMBNAM
417bACSR                   MOVE SVVNAM    IMVNAM
417bACSR                   MOVE SVBRND    IMBRND
417bACSR                   MOVE SVLOT     IMLOT
417bACSR                   MOVE SVLTRK    IMLTRK
417bACSR                   Z-ADDSVODAY    IMODAY
     C*
412 ACSR         *IN79     IFEQ *ON
416 DC**                   MOVE *ZEROS    IMNRPK
416 ACSR                   Z-ADD1         IMNRPK
     CSR                   WRITEIMREC
412 ACSR                   ELSE
412 ACSR                   UPDATIMREC
     CSR                   ENDIF
     C**
     C*   Add record to ITEMDEF file using base fields.
     C*
412 AC*     First, delete an existing ITEMDEF records.
412 AC*
412 ACSR         ITKEY     SETLLITEMDEF
412 ACSR                   MOVE *OFF      *IN79
412 ACSR         *IN79     DOUEQ*ON
412 ACSR         ITKEY     READEITEMDEF                  79
412 ACSR         *IN79     IFEQ *OFF
412 ACSR                   DELETIDREC                  77
412 ACSR                   ENDIF
412 ACSR                   ENDDO
412 AC*
412 AC*     Then add the ITEMDEF records.
412 AC*
     CSR                   MOVE *OFF      EOFD    1
     CSR         ITKEY2    SETLLITEMDEF
     CSR         EOFD      DOUEQ*ON
     C*
     CSR         ITKEY2    READEITEMDEF             N    75
     CSR         *IN75     IFEQ *ON
     CSR                   MOVE *ON       EOFD
     CSR                   GOTO ENDDU2
     CSR                   ENDIF
     C*   Save ITEMDEF base item fields.
     C*R                   Z-ADDIDWHSE    SVWHSE  30
     C*R                   MOVE IDITEM    SVITEM 15
     CSR                   MOVE IDSDEF    SVSDEF  2
     CSR                   MOVE IDTIE     SVTIE   40
     CSR                   MOVE IDHIGH    SVHIGH  20
     CSR                   MOVE IDUM      SVUM    1
     CSR                   MOVE IDPREF    SVPREF  1
     CSR                   MOVE SVSDEF    KYSDEF
     C           IDKEY     CHAINITEMDEF              75
     CSR                   Z-ADD$PWHSE    IDWHSE
     CSR                   MOVE IUITEM    IDITEM
     CSR                   MOVE SVSDEF    IDSDEF
     CSR                   MOVE SVTIE     IDTIE
     CSR                   MOVE SVHIGH    IDHIGH
     CSR                   MOVE SVUM      IDUM
     CSR                   MOVE SVPREF    IDPREF
412 ACSR         *IN75     IFEQ *ON
     CSR                   WRITEIDREC
412 ACSR                   ELSE
412 ACSR                   UPDATIDREC
412 ACSR                   ENDIF
     C*
412 ACSR         IDKEY     SETGTITEMDEF
     CSR         ENDDU2    TAG
     CSR                   ENDDO
     C*
     C*   Add record to ITEMLNK file.
     C*
     C*
     CSR         OPMITM    IFEQ 'Y'
     CSR         IUMITM    ANDNE*BLANKS
     CSR         ILKEY     CHAINITEMLNK              79
     CSR                   MOVE IUMITM    ILITM2
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADDIUWHSE    ILWHSE
     CSR                   MOVE IUITEM    ILITEM
     CSR                   WRITEILREC
     CSR                   ELSE
     CSR                   UPDATILREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*   Delete record from ITEMLNK file.
     C*
     CSR         OPMITM    IFEQ 'Y'
     CSR         IUMITM    ANDEQ*BLANKS
     CSR         ILKEY     CHAINITEMLNK              79
     CSR         *IN79     IFEQ *OFF
     CSR                   DELETILREC
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     C*       Add record to item quantity file for Contract item.
     C*
510 DC*          IUTYPE    IFEQ 'C'
413aDC**         IUUTYP    ANDEQ'A'
510 DC*          ITKEY     CHAINITEMQTY              79
510 DC*          *IN79     IFEQ *ON
510 DC*                    CLEARIQREC
510 DC*                    Z-ADDIUWHSE    IQWHSE
510 DC*                    MOVE IUITEM    IQITEM
510 DC*                    WRITEIQREC                  77
510 DC*          *IN77     IFEQ *ON
510 DC*                    MOVE *ON       ERROR
510 DC*                    MOVE DESC,35   DETMSG
510 DC*                    GOTO ENDUP2
510 DC*                    ENDIF
412 AC*                    ENDIF
510 DC*                    ENDIF
     C*
     CSR         ENDUP2    TAG
     C*
     C                     ENDSR
     C*
     C*
     C*----------------------------------------------------------------
     C*
     C*  ZZXOPT   Get use extended description option
     C*
     CSR         ZZXOPT    BEGSR
     C*
     C*   Get extended description option.
     C                     Z-ADD$PWHSE    KYWHSE
     CSR                   MOVE '0'       PXFLG   1
     CSR                   MOVE '*ITEMMNT'OPCODE  8
     C*
     CSR         OPTNKY    CHAINOPTIONS              79
     CSR         *IN79     IFEQ *OFF
     CSR         OPXDSC    IFEQ 'Y'
413 DC*R                   MOVE '1'       UXFLAG  1
413 MCSR                   MOVE '1'       PXFLG
     CSR                   ENDIF
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*
     O*----------------------------------------------------------------
     C*
     C*  ZRHEAD   Print report headings.
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         COUNT     IFGE LINPPG
     CSR                   SETON                     81
     CSR                   Z-ADD0         COUNT
     CSR                   WRITEHEAD1
     CSR                   ADD  3         COUNT
     CSR                   SETOF                     81
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
*INVALID WAREHOUSE
*INVALID SECTION
*INVALID DEPARTMENT
*INVALID DESIGNATION
*INVALID ACTIVITY
*INVALID DATE SENSITIVE
*INVALID BULK PICK OPT.
*INVALID MASTER ITEM
*INVALID BREAKDOWN QTY
*INVALID ITEM & MASTER
*MANUFACTURER DATE FLAG
*INVALID DATE SENSITIVE
*INVALID MFG SHELF DAYS
*INVALID DIST. DAYS
*INVALID USER DAYS
*INVALID CATCH WEIGHT
*INVALID UNIT MEASURE
*INVALID UOM QUANTITY
*INVALID ALLOW BRK SALE
*INVALID DIMENSIONS
*INVALID CUBE
*INVALID SHIP WEIGHT
*INVALID BUILD CODE
*INVALID SHELF LIFE
*INVALID NOTICE DAYS
*INVALID ITEM
*ERROR ADDING RECORD
*ADDED RECORD
*DEPT CHANGED
*SECTION CHANGED
*DESIGNATION CHANGED
*DIMENSIONS CHANGED
*INVALID VENDOR #
*INVALID REPLENISHMENT LEVEL
*ERROR ADDING ITEMQTY RECORD
*DELETE RECORD NOT FOUND
*MISSING MASTER ITEM
*ERROR DELETING-ITEMLNK EXISTS
*ERROR DELETING-ITEMQTY EXISTS
*ERROR DELETING ITEMDEF RECORD
*MASTER ITEM MUST BE BLANK
*BREAKDOWN NOT DEFINED
*CONTRACT BASE ITEM MISSING
*INVALID CONTRACT BASE ITEM
*CANT CHG TYPE FROM 'X' TO 'Y'
*INVALID ITEM TYPE '
**
*Auto created by item update*
