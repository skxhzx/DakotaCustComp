     F*----------------------------------------------------------------
     F*
     F*  CENTER    Center a string.
     F*  28 April 1993
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
0493 E*----------------------------------------------------------------
.    E*  Table and array definitions
0493 E*
     E                    $PSTR      60  1
     E                    CSTR       60  1
0493 I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PSTR   String to center.
     C*      $PLEN   Length of centered output string.
     C*
     C*    Returned Parameters
     C*      $PSTR   Centered string.
     C*
0493 C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PSTR
     C                     PARM           $PLEN   30
     C*----------------------------------------------------------------
     C* Main line
     C*
     C* Define variables and constants
     C*
     C           *LIKE     DEFN $PLEN     MAXLEN
     C                     Z-ADD60        MAXLEN
     C*
     C           *LIKE     DEFN $PLEN     X
     C           *LIKE     DEFN $PLEN     BP
     C           *LIKE     DEFN $PLEN     EP
     C           *LIKE     DEFN $PLEN     STRLEN
     C           *LIKE     DEFN $PLEN     LFTOVR
0251 C*
0252 C* Find beginning position of string.
0253 C*
0254 C                     Z-ADD1         X
0258 C           X         DOWLEMAXLEN
0259 C           $PSTR,X   ANDEQ*BLANK
0257 C                     ADD  1         X
0262 C                     END
     C           X         CABGTMAXLEN    BOTTOM
     C                     Z-ADDX         BP
0251 C*
0252 C* Find end position of string.
0253 C*
0254 C                     Z-ADDMAXLEN    X
0258 C           X         DOWGT0
0259 C           $PSTR,X   ANDEQ*BLANK
0257 C                     SUB  1         X
0262 C                     END
     C                     Z-ADDX         EP
0251 C*
0252 C* Calculate string length.
0253 C*
     C           EP        SUB  BP        STRLEN
     C                     ADD  1         STRLEN
0283 C*
0284 C* Center string within length specified.
0285 C*
     C                     MOVE *BLANKS   CSTR
0286 C           STRLEN    IFGE $PLEN
0287 C                     MOVEA$PSTR,BP  CSTR,1
0288 C                     ELSE
0289 C           $PLEN     SUB  STRLEN    LFTOVR
0290 C           LFTOVR    DIV  2         X
0291 C                     ADD  1         X
0293 C                     MOVEA$PSTR,BP  CSTR,X
0294 C                     END
     C                     MOVEACSTR      $PSTR
     C           BOTTOM    TAG
     C                     RETRN
