     F*----------------------------------------------------------------
     F*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  IT725     Export item adjustments - Post
     F*  07 November, 1994
     F*  Dave Sommerville
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
213 AF*    05/20/96  DAS
213 AF*      - Added fields IAWHDP, IADISP and IACWTA.
     F*
416 AF*    12/27/01  DAS  4.16
     F*      - Added file ADJMISC.
     F*      - Revised to only write a record to the export file if
     F*        the adjustment code is defined for updating the Host
     F*        system.
     F*
500 AF*    12/06/05  MLB  5.00
     F*      - Enh: Revised program to not write adjustments to export
     F*        file that have blank item numbers.
520 AF*    09/15/09  RH   5.20
     F*      - Enh: Revised to not write adjustments to export file
     F*        if code not found in ADJMISC for ADMSYS system to update
610aAF*    01/06/10  RH   6.10a
     F*      - Enh: Revised to not export '@' system adjustments.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  79        - EOF for ITEMADJ
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FITEMADJ UP  E           K        DISK
416 AFADJMISC IF  E           K        DISK
     FEXPADJ  O   E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  Constants
     I*
     I              '1'                   C         FOREVR
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C* Main line
416 AC*
416 AC*  Get system to update.
416 AC*
416 AC           IACODE    CHAINADJMISC              79
520aAC           *IN79     IFEQ *ON
520aAC                     MOVE *OFF      UPDHST
416 AC                     ELSE
520aDC*          *IN79     IFEQ *OFF
520aDC*          ADMSYS    ANDEQ'D'
520aAC*
520aAC           ADMSYS    IFEQ 'D'
416 AC                     MOVE *OFF      UPDHST  1
416 AC                     ELSE
416 AC                     MOVE *ON       UPDHST
416 AC                     ENDIF
610aAC                     MOVE *BLANKS   TEMPL   1
610aAC                     MOVELIACODE    TEMPL
610aAC           TEMPL     IFEQ '@'
610aAC                     MOVE *OFF      UPDHST
610aAC                     ENDIF
520aAC                     ENDIF
     C*
     C*  Add item adjustments to export file.
     C*
416 AC           UPDHST    IFEQ *ON
500 AC           IAITEM    ANDGT*BLANKS
     C                     EXSR FILIE
     C                     WRITEIEREC                  79
416 AC                     ENDIF
     C*
     C*  Update item adjustment file with upload date and time.
     C*
     C                     Z-ADDTODAY     IAUDTE
     C                     TIME           IAUTIM
     C                     UPDATIAREC
     C*
     C           BOTTOM    TAG
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILIE    Fill export fields.
     C*
     CSR         FILIE     BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEARIEREC
     C*
     C*  Move fields from item adjustment file to export file.
     C*
     CSR                   Z-ADDIAWHSE    IEWHSE
     CSR                   MOVE IAITEM    IEITEM
     CSR                   Z-ADDIADATE    IEDATE
     CSR                   Z-ADDIATIME    IETIME
     CSR                   MOVE IABY      IEBY
213 ACSR                   MOVE IAWHDP    IEWHDP
213 ACSR                   MOVE IADISP    IEDISP
213 ACSR                   Z-ADDIACWTA    IECWTA
     CSR                   Z-ADDIAQTY1    IEQTY1
     CSR                   Z-ADDIAQTY2    IEQTY2
     CSR                   Z-ADDIAQTY3    IEQTY3
     CSR                   MOVE IACODE    IECODE
     CSR                   MOVE IAMEMO    IEMEMO
     C*
     CSR                   ENDSR
