     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR560     Pallet summary Report.
     F*  15 October, 1994
     F*  Hemant Kapadia
     F*
     F*  Revisions:
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FOR560   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FOR560PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    TTL     1   2 40
     E                    RPD     1   5 25
     E                    RECK        5  7 0
     E                    LVLK        5  7 0
     E*
     E                    QPCK        5  5 0
     E                    CUBE        5  9 3
     E                    WGHT        5  9 2
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              LBWHDPL4
     I                                              LQPAL#L3
     I                                              LBSTOPL2
     I                                              LBSEQ L1
     I                                              LBITEML1
     I                                              LBUCODL1
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $CMD    8
     C                     PARM           $RTYPE  8
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*  Level 1 break - Item
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*  Level 2 break - Stop
     C*
     C           *INL2     IFEQ *ON
     C                     Z-ADD2         L
     C                     EXSR ZRZERO
     C*
     C                     MOVE *BLANKS   $CSTR
     C                     MOVE *BLANKS   CUST#
     C                     MOVE OHCUST    $CSTR
     C                     CALL 'ADJUST'
     C                     PARM '*LEFT   '$CCMD   8
     C                     PARM           $CSTR  30
     C                     MOVEL$CSTR     CUST#  11
     C                     MOVELOHCNAM    CSNAME
     C                     ENDIF
     C*
     C*  Level 3 break - Pallet
     C*
     C           *INL3     IFEQ *ON
     C                     Z-ADD3         L
     C                     EXSR ZRZERO
     C*
     C                     MOVE *BLANKS   $CSTR
     C                     MOVE *BLANKS   LBPAL
     C                     MOVE LQPAL#    $CSTR
     C                     CALL 'ADJUST'
     C                     PARM '*LEFT   '$CCMD   8
     C                     PARM           $CSTR  30
     C                     MOVEL$CSTR     LBPAL   7
     C                     MOVE *BLANKS   P#DESC
     C           P#DESC    CAT  '** ':1   P#DESC
     C           P#DESC    CAT  'PALLET':1P#DESC
     C           P#DESC    CAT  '-':1     P#DESC
     C           P#DESC    CAT  LBPAL:1   P#DESC
     C           P#DESC    CAT  ' ** - ':1P#DESC
     C           P#DESC    CAT  LBWHDP:1  P#DESC
     C                     ENDIF
     C*
     C*  Level 4 break - Department
     C*
     C           *INL4     IFEQ *ON
     C                     Z-ADD4         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*  Print headings.
     C*
     C           *IN93     IFEQ *ON
     C           *INL3     OREQ *ON
     C                     WRITEHEAD2
     C                     ENDIF
     C*
     C           *IN92     IFEQ *ON
     C           *INL2     OREQ *ON
     C           $RTYPE    IFEQ '*DETAIL '
     C                     WRITEDETAIL1
     C                     ENDIF
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Accumulate totals
     C*
     C                     ADD  LBQPCK    QPCK,1
     C                     ADD  LBCUBE    CUBE,1
     C                     ADD  LBSWGT    WGHT,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN94
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Item totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   EXSR ZRTOTL
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Stop totals
     C*
     CL2                   Z-ADD2         L
     CL2                   EXSR ZRROLL
     C*
     CL2         RECK,L    IFGT 0
     CL2                   EXSR ZRTOTL
     CL2                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Pallet totals
     C*
     CL3                   Z-ADD3         L
     CL3                   EXSR ZRROLL
     C*
     CL3         RECK,L    IFGT 0
     CL3                   EXSR ZRTOTL
     CL3                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Department totals
     C*
     CL4                   Z-ADD4         L
     CL4                   EXSR ZRROLL
     C*
     CL4         RECK,L    IFGT 0
     CL4                   EXSR ZRTOTL
     CL4                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Batch totals
     C*
     CLR                   Z-ADD5         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRTOTL
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C           $RTYPE    IFEQ '*SUMMARY'
     C                     MOVE TTL,1     TITLE
     C                     ELSE
     C                     MOVE TTL,2     TITLE
     C                     ENDIF
     C                     MOVELTITLE     $PSTR  60
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     TITLE
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           LBWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     MOVE *BLANKS   $CSTR
     C                     MOVE *BLANKS   LBBAT
     C                     MOVE LBPBAT    $CSTR
     C                     CALL 'ADJUST'
     C                     PARM '*LEFT   '$CCMD   8
     C                     PARM           $CSTR  30
     C                     MOVEL$CSTR     LBBAT   7
     C*
     C*   Format Batch number and Route number.
     C*
     C                     MOVE *BLANKS   BATCH
     C                     MOVE *BLANKS   $PSTR
     C           BATCH     CAT  'Batch:':1BATCH
     C           BATCH     CAT  LBBAT:1   BATCH
     C           BATCH     CAT  '/':1     BATCH
     C           BATCH     CAT  'Route:':1BATCH
     C           BATCH     CAT  LBRTE:1   BATCH
     C                     MOVELBATCH     $PSTR  60
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center batch/route description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     BATCH
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL4     IFEQ *OFF
     C                     MOVE *ON       *IN94
     C                     END
     C*
     C           *INL3     IFEQ *OFF
     C                     MOVE *ON       *IN93
     C                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  QPCK,L    QPCK,M
     C                     ADD  CUBE,L    CUBE,M
     C                     ADD  WGHT,L    WGHT,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDQPCK,L    T#QPCK
     C                     Z-ADDCUBE,L    T#CUBE
     C                     Z-ADDWGHT,L    T#WGHT
     C*
     C                     MOVE RPD,L     $RPD
     C*
     C           L         IFEQ 1
     C           $RTYPE    IFEQ '*DETAIL '
     C                     MOVELITDESC    DESCIT
     C                     WRITEDETAIL2
     C                     ENDIF
     C                     ENDIF
     C*
     C           L         IFEQ 2
     C           $RTYPE    IFEQ '*DETAIL '
     C                     WRITESKIP1
     C                     WRITETOTALD
     C                     ELSE
     C                     WRITETOTAL1
     C                     ENDIF
     C                     ENDIF
     C*
     C           L         IFEQ 3
     C                     WRITESKIP1
     C                     WRITETOTAL2
     C                     END
     C*
     C           L         IFEQ 4
     C                     WRITESKIP1
     C                     WRITETOTAL3
     C                     END
     C*
     C           L         IFEQ 5
     C                     WRITESKIP1
     C                     WRITETOTAL4
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C*                    Z-ADD0         CLS,L
     C                     Z-ADD0         QPCK,L
     C                     Z-ADD0         CUBE,L
     C                     Z-ADD0         WGHT,L
     C*
     C                     ENDSR
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      TTL   Report title descriptions
Pallet Summary Report by Route
Pallet Detail Report by Route
**      RPD   Report level descriptions

 * Stop totals *
 ** Pallet totals **
 *** Department totals ***
 **** Route totals ****
