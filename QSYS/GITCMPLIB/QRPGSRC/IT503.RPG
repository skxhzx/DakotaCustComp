     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT503     FS-WMS Item Sales Detail Cleanup - Update
     F*  29 December 1999
     F*  Max Blacknik
     F*
     F*  SK112 - 1.01
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        CHAIN INDICATOR
     F*  90        OVERFLOW INDICATOR FOR EXCEPTION REPORT
     F*  91        OVERFLOW INDICATOR FOR CATCH WEIGHT REPORT
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FITEMSLS UF  E           K        DISK
     FOPTIONS IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A40        40  1
     E                    $MD       128  1
     I*----------------------------------------------------------------
     I*  Redefinition data structures
     I*
     I$AERM       DS
     I                                        1  60 XAERM
     I                                        1   4 $AMSG
     I$CERM       DS
     I                                        1  60 XCERM
     I                                        1   4 $CMSG
     I*----------------------------------------------------------------
     I*  *DAYEND  -  Day end processing options.
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPSTR1  -  Sunday - Warehouse start time
     I*    OPSTR2  -  Monday - Warehouse start time
     I*    OPSTR3  -  Tuesday - Warehouse start time
     I*    OPSTR4  -  Wednesday - Warehouse start time
     I*    OPSTR5  -  Thursday - Warehouse start time
     I*    OPSTR6  -  Friday - Warehouse start time
     I*    OPSTR7  -  Saturday - Warehouse start time
     I*    OPLTDT  -  Last transaction processed date
     I*    OPLTTM  -  Last transaction processed time
     I*    OPLODT  -  Last order processed date
     I*    OPLOTM  -  Last order processed time
     I*    OPWEEK  -  Current week number
     I*    OPWKAV  -  Number of weeks for average
     I*    OPDYAV  -  Day to compute averages
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I                                        1   60OPSTR1
     I                                        7  120OPSTR2
     I                                       13  180OPSTR3
     I                                       19  240OPSTR4
     I                                       25  300OPSTR5
     I                                       31  360OPSTR6
     I                                       37  420OPSTR7
     I                                        1  42 WTIM
     I                                       43  500OPLTDT
     I                                       51  560OPLTTM
     I                                       57  640OPLODT
     I                                       65  700OPLOTM
     I                                       71  720OPWEEK
     I                                       73  750OPWKAV
     I                                       76  760OPDYAV
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I              'CLCDUR'              C         @CLCDR
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I                                      133 140 $CVDR1
     I                                      141 1450$CVVL1
     I                                      146 153 $CVDR2
     I                                      154 1580$CVVL2
     I                                      159 166 $CVDR3
     I                                      167 1710$CVVL3
     I                                      172 179 $CVDR4
     I                                      180 1840$CVVL4
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I                                        1  10 ERRCL1
     I                                       11  16 ERRCL2
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   Not used at this time.
     C*      $PPRG   Program to send messages back to.
     C*      $WHSE   Warehouse
     C*
     C*    Returned Parameters
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured. Message sent to pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $WHSE   3
     C                     PARM           $PRTN   8
     C                     PARM           $PMSG   4
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*
     C* Loop through items to remove.
     C*
     C           RUNCLN    IFEQ '*YES'
     C*
     C           FOREVR    DOWEQFOREVR
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE                           Exit loop
     C*
     C                     OTHER
     C*
     C                     MOVE *OFF      EOFIH
     C           KEYIH     SETLLITEMSLS
     C*
     C           EOFIH     DOUEQ*ON
     C                     EXSR GETIH
     C           EOFIH     IFEQ *ON
     C           IHSTRD    ORGE CUTOFF
     C*
     C*    Position pointer to next item number.
     C*
     C           KEYIH     SETGTITEMSLS
     C           $PWHSE    READEITEMSLS                  79
     C                     MOVE IHITEM    KYITEM
     C*
     C                     LEAVE
     C                     ENDIF
     C*
     C*    Test date and see if old enough to remove.
     C*
     C           IHSTRD    IFLT CUTOFF
     C                     DELETIHREC
     C                     ENDIF
     C                     ENDDO                           End IH loop
     C                     ENDSL                           End Select stmt
     C                     ENDDO                           End RH loop.
     C                     ENDIF                           End IF loop.
     C*
     C* Close up shop and return home.
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   MOVE *BLANKS   $PRTN
     CSR                   MOVE $WHSE     $PWHSE  30
     CSR                   MOVE *ON       FOREVR  1
     CSR                   MOVE *OFF      EOFIH   1
     CSR                   MOVE *ZEROS    X       30
     CSR         *LIKE     DEFN IHSTRD    CUTOFF
     CSR         *LIKE     DEFN IHITEM    KYITEM
     C*
     C*  Define partial key for ITEMAVG file.
     C*
     CSR         KEYIG     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           IHITEM
     C*
     C*  Define partial key for ITEMSLS1 file.
     C*
     CSR         KEYIH     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           KYITEM
     C*
     C*  Define partial key for OPTIONS file.
     C*
     CSR         KEYOP     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           OPCODE
     C*
     C*    Retrieve Day end processing options.
     C*
     CSR                   MOVE '*DAYEND 'OPCODE
     CSR         KEYOP     CHAINOPTIONS              79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ALL'0'   WTIM
     CSR                   ENDIF
     C*
     C*    Retrieve today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     C*
     C*    Convert Current date to day number.
     C*
     CSR                   MOVE '*CMDNBR '$CVCMD
     CSR                   MOVE TODAY     $CVD8I
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    T       10
     C*
     C*   Test day number to see if today we run sales cleanup.
     C*
     CSR         T         IFEQ OPDYAV
     CSR                   MOVE '*YES'    RUNCLN  4
     C*
     C*   Get first item number in file.
     C*
     CSR         $PWHSE    SETLLITEMSLS
     CSR         $PWHSE    READEITEMSLS                  79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE IHITEM    KYITEM
     CSR                   ENDIF
     C*
     C*   Compute Sunday date for current week.
     C*
     CSR                   MOVE '*CMDCMD '$CVCMD
     CSR                   MOVE TODAY     $CVD8I
     CSR                   MOVE '*SUBDAYS'$CVDR1
     CSR         T         SUB  1         $CVVL1
     CSR                   CALL @CLCDR
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    CUTOFF
     C*
     C*   Compute cutoff date.
     C*
     CSR                   MOVE '*CMDCMD '$CVCMD
     CSR                   MOVE CUTOFF    $CVD8I
     CSR                   MOVE '*SUBDAYS'$CVDR1
     CSR         OPWKAV    MULT 7         $CVVL1
     CSR                   CALL @CLCDR
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    CUTOFF
     C*
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ  10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*
     C*  GETIH   Get item detail sales record.
     C*
     CSR         GETIH     BEGSR
     C*
     CSR         KEYIH     READEITEMSLS                  79
     C*
     C*   Set flag if there are no more detail records.
     C*
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       EOFIH
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
