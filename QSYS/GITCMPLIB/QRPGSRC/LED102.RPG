     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Associates
     F*   245 W Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL 60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  LED102    Q & D for Ledyards to populate overflow slots
     F*  24 April, 2003
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*
     F*  Notes
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  90        View mode (protect all fields)
     F*  91        Change mode (protect key fields)
     F*  97        Alawys on (used for SFLEND keyword)
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSTORGF  IP  E                    DISK
     FSLOT2   UF  E           K        DISK                      A
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  15 ERITEM
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*
     I            DS
     I                                        1   2 WKAISL
     I                                        1   1 WKASL1
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*      None
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*----------------------------------------------------------------
     C*  Main Line
     C*
     C           SGAISL    CABEQ*BLANKS   SKIP
     C           SGSLOT    CABEQ*ZEROS    SKIP
     C           SGLEVL    CABEQ*ZEROS    SKIP
     C                     Z-ADD1         W1WHSE  30
     C                     MOVE *BLANKS   W1WHDP  5
     C*
     C                     MOVE SGAISL    WKAISL
     C                     SELEC
     C           WKASL1    WHEQ '5'
     C           WKASL1    OREQ 'A'
     C           WKASL1    OREQ 'B'
     C           WKASL1    OREQ 'C'
     C           WKASL1    OREQ 'D'
     C           WKASL1    OREQ 'E'
     C           WKASL1    OREQ 'F'
     C           WKASL1    OREQ 'G'
     C           WKASL1    OREQ 'H'
     C           WKASL1    OREQ 'I'
     C           WKASL1    OREQ 'J'
     C           WKASL1    OREQ 'K'
     C           WKASL1    OREQ 'L'
     C           WKASL1    OREQ 'M'
     C           WKASL1    OREQ 'N'
     C           WKASL1    OREQ 'O'
     C           WKASL1    OREQ 'P'
     C           WKASL1    OREQ 'Q'
     C           WKASL1    OREQ 'R'
     C           WKASL1    OREQ 'S'
     C           WKASL1    OREQ 'T'
     C           WKASL1    OREQ 'U'
     C           WKASL1    OREQ 'V'
     C           WKASL1    OREQ 'W'
     C           WKASL1    OREQ 'X'
     C           WKASL1    OREQ 'Y'
     C           WKASL1    OREQ 'Z'
     C                     MOVE 'DRY  '   W1WHDP
     C           WKASL1    WHEQ '6'
     C                     MOVE 'EQUIP'   W1WHDP
     C           WKASL1    WHEQ '1'
     C                     MOVE 'FRZ  '   W1WHDP
     C           WKASL1    WHEQ '9'
     C                     MOVE 'REFER'   W1WHDP
     C                     OTHER
     C                     GOTO SKIP
     C                     ENDSL
     C*
     C*    Right justify aisle
     C*
     C                     CALL 'ADJUST'
     C                     PARM '*RIGHT  '$CCMD   8
     C                     PARM SGAISL    $CSTR  30
     C                     MOVE $CSTR     W1AISL  3
     C                     Z-ADDSGSLOT    W1LOC   30
     C                     MOVE SGLEVL    W1RLVL  20
     C                     MOVE *BLANKS   W1HAND  2
     C                     MOVE 'F'       W1STYP  1
     C                     Z-ADDSGSLOT    W1PSEQ  50
     C                     MOVE *BLANKS   W1DISP 12
     C                     CALL 'FRMTSL'
     C                     PARM           W1WHSE
     C                     PARM           W1WHDP
     C                     PARM           W1AISL
     C                     PARM           W1LOC
     C                     PARM           W1RLVL
     C                     PARM           W1HAND
     C                     PARM           W1STYP
     C                     PARM           W1PSEQ
     C                     PARM           W1DISP
     C*
     C           SLKEY     CHAINSLOT2                79
     C           *IN79     IFEQ *ON
     C                     Z-ADDW1WHSE    SLWHSE
     C                     MOVE W1WHDP    SLWHDP
     C                     MOVE W1AISL    SLAISL
     C                     Z-ADDW1LOC     SLLOC
     C                     Z-ADDW1RLVL    SLRLVL
     C                     MOVE W1HAND    SLHAND
     C           SLLOC     DIV  2         DUMMY   30
     C                     MVR            SLSIDE
     C                     MOVE W1STYP    SLSTYP
     C                     MOVE W1DISP    SLDISP
     C*
     C*  Convert today's date into century format.
     C*
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
     C                     Z-ADD$CVD8O    TODAY   80
     C                     Z-ADDTODAY     SLSDTE
     C                     TIME           SLSTIM
     C                     MOVE *BLANKS   SLSDEF
     C                     MOVE *BLANKS   SLDESG
     C                     MOVE *BLANKS   SLACTV
     C                     MOVE 'N'       SLPICK
     C                     Z-ADDW1PSEQ    SLPSEQ
     C                     Z-ADD0         SLPRTY
     C                     Z-ADD0         SLENTD
     C                     Z-ADD0         SLEXPD
     C                     Z-ADD0         SLSLFD
     C                     MOVE 'N'       SLRSRV
     C           SGITEM    IFGT 0
     C                     MOVELSGITEM    SLITEM
     C*
     C           SGQTY     IFGT 0
     C                     Z-ADDSGQTY     SLSTK1
     C                     MOVE 'A '      SLSTAT
     C                     ELSE
     C                     MOVE 'I '      SLSTAT
     C                     Z-ADD0         SLSTK1
     C                     ENDIF
     C*
     C                     ELSE
     C                     MOVE *BLANKS   SLITEM
     C                     MOVE 'I '      SLSTAT
     C                     Z-ADD0         SLSTK1
     C                     ENDIF
     C                     Z-ADD0         SLSTK2
     C                     Z-ADD0         SLSTK3
     C                     Z-ADD0         SLALC1
     C                     Z-ADD0         SLALC2
     C                     Z-ADD0         SLALC3
     C                     Z-ADD0         SLTFR1
     C                     Z-ADD0         SLTFR2
     C                     Z-ADD0         SLTFR3
     C                     Z-ADD0         SLPCK1
     C                     Z-ADD0         SLPCK2
     C                     Z-ADD0         SLPCK3
     C                     Z-ADD0         SLRCV1
     C                     Z-ADD0         SLRCV2
     C                     Z-ADD0         SLRCV3
     C                     WRITESLREC
     C                     ELSE
     C                     UPDATSLREC
     C                     ENDIF
     C*
     C           SKIP      TAG
     C*
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize variables
     C*
     CSR                   MOVE *ON       FOREVR  1
     CSR                   MOVE *OFF      ERROR   1
     C*
     C           SLKEY     KLIST
     C                     KFLD           W1WHSE
     C                     KFLD           W1WHDP
     C                     KFLD           W1DISP
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
