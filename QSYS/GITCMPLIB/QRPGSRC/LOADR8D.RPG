     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 Roosevelt Rd.  Bldg 8, Suite 51
     F*   WEST CHICAGO, IL 60158
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  LOADR8D   Route Summary History - File creation
     F*  March 13, 2008
     F*  Jose Duque
     F*
     F*  Revisions:
     F*
520 AF*    06/17/09  RBD  5.20
     F*      - Removed hardcoding of start and end times, will use
     F*        new pgm GETLOARNG instead to retrieve *LOADER options.
     F*      - Added call to GETWHSE.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FLDRHHDR IF  E           K        DISK
     F*
     FRTEHSUMMO   E           K        DISK
     F*
     E*----------------------------------------------------------------
     I*
     I*  Data structures for time calculation
     I*
     I            DS
     I                                        1   60LHHENT
     I                                        1   20PHH
     I                                        3   40PMM
     I                                        5   60PSS
     I            DS
     I                                        1   60LHHSTT
     I                                        1   20IHH
     I                                        3   40IMM
     I                                        5   60ISS
     I            DS
     I                                        1   60TDIFF
     I                                        1   20TDHR
     I                                        3   40TDMIN
     I                                        5   60TDSEC
     I            DS
     I                                        1   52HLDMM2
     I                                        1   30MM
     I                                        4   50MINPER
      *----------------------------------------------------------------
     I*  Constants
     I*
520 AI              'GETLOARNG'           C         GETLOA
      *----------------------------------------------------------------
520 AI*  Program info data structure
520 AI*
520 AI           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     E*----------------------------------------------------------------
     C*
     C*  CALC the time difference between 'Start Route Loading'
     C*  and 'End Route Loading' times and write it out, along
     C*  with info from current record read in, into file
     C*  'RTEHSUMM' (Route Summary History) used later to be
     C*  displayed or written on to a report.
     C*
     C           *ENTRY    PLIST
     C                     PARM           $FDATE  8
     C                     PARM           $TDATE  8
     C*
520 AC*      Get warehouse for user.
     C*
520 AC                     CALL 'GETWHSE'
520 AC                     PARM           #USER
520 AC                     PARM 0         #WHSE   30
      *
520 AC*      Get loader start and end times.
     C*
520 AC                     CALL GETLOA
520 AC                     PARM           #WHSE
520 AC                     PARM 0         STIME   60
520 AC                     PARM 0         ETIME   60
     C*
     C                     READ LHHREC                   LR
     C*
     C                     MOVE $FDATE    FRMDTE  80
     C                     MOVE $TDATE    TODTE   80
     C*
     C           *INLR     DOWNE*ON
     C*
     C           LHHSTD    IFEQ FRMDTE
520 MC           LHHSTT    ANDLTSTIME
     C                     GOTO BYPASS
     C                     ENDIF
     C*
     C           LHHSTD    IFEQ TODTE
520 MC           LHHSTT    ANDGTETIME
     C                     GOTO BYPASS
     C                     ENDIF
     C*
     C           LHHSTA    IFEQ '6'                        DeletedRoute
     C                     Z-ADD*ZEROS    TDIFF
     C                     GOTO WRITE
     C                     ENDIF
     C*
      *  The code below compare two time values (start and ending
      *  times) and calculate the time difference between the two.
      *
      *  *IN89 drives part of the code based on which date is greater.
      *  If 'ending time' is greater then both time are converted to
      *  seconds, 'start time' is subtracted from 'ending time' and
      *  the result in seconds is converted back to HR:MM:SS to be
      *  displayed as the time difference between the two.
      *
      *  If on the other hand 'ending time' is lower then 'start time'
      *  is converted to seconds along with 24:00:00 to calc the
      *  elapsed time from 'start time' to the end of the day.
      *  The result in seconds is converted back to HH:MM:NN and
      *  added to 'ending date'
      *
     C           LHHENT    COMP LHHSTT               89
      *
      *  Convert 'start time' to seconds
      *
     C           IHH       MULT 60        HLDMM   60       ConvHrtoMin
     C           HLDMM     MULT 60        HLDSS1  60       ConvMintoSec
     C           IMM       MULT 60        HLDSS2  60       CnovMintoSec
     C           HLDSS2    ADD  ISS       HLDSS2           AddupAllSecs
     C           HLDSS1    ADD  HLDSS2    TOTIMS  60       TotalImpSecs
      *
      *
      *  Ending time is greater, convert to seconds
      *
     C           *IN89     IFEQ *ON
     C           PHH       MULT 60        HLDMM   60       ConvHrtoMin
     C           HLDMM     MULT 60        HLDSS1  60       ConvMintoSec
     C           PMM       MULT 60        HLDSS2  60       CnovMintoSec
     C           HLDSS2    ADD  PSS       HLDSS2           AddupAllSecs
     C           HLDSS1    ADD  HLDSS2    TOTPRS  60       TotalPrtSecs
     C                     ENDIF
      *
      *  Ending time is lower, subtract the calc time between start
      *  time and the end of the day from 24:00:00 represented in
      *  seconds.
     C  N89      86400     SUB  TOTIMS    SECDIF  60
      *
      *  Ending time is greater, subtract start time from it
     C   89      TOTPRS    SUB  TOTIMS    SECDIF  60
      *
     C           SECDIF    DIV  60        HLDMM2           ConvSectoMin
     C           MINPER    DIV  100       HLDPER  32       SetSecPercent
     C           HLDPER    MULT 60        HLDSS1    H      CalcSeconds
     C                     Z-ADDHLDSS1    TDSEC
      *
      *
      *  Take care of minutes overflow
     C           MM        IFGT 59
     C           MM        DIV  60        HLDHR   20
     C                     MVR            TDMIN            TDIFF
     C                     Z-ADDHLDHR     TDHR
     C                     ELSE
     C                     Z-ADD*ZEROS    TDHR
     C                     Z-ADDMM        TDMIN
     C                     ENDIF
      *
      *  Write output record.
     C           *IN89     IFEQ *ON
     C                     GOTO WRITE
     C                     ENDIF
      *
      *
      *
      *  Ending time is lower, add calc result of start time
      *  to end of day to ending time
     C           TDIFF     ADD  LHHENT    TDIFF
      *
      *
      *  Take care of seconds overflow
     C           TDSEC     IFGT 59
     C           TDSEC     DIV  60        HLDMM
     C                     MVR            HLDSS1
     C                     ADD  HLDMM     TDMIN
     C                     Z-ADDHLDSS1    TDSEC
     C                     ENDIF
      *
      *
      *  Take care of minutes overflow
     C           TDMIN     IFGT 59
     C           TDMIN     DIV  60        HLDHR   20
     C                     MVR            TDMIN
     C                     ADD  HLDHR     TDHR
     C                     ENDIF
      *
      *
     C           WRITE     TAG
     C                     Z-ADDTDIFF     LHHDIF
     C                     WRITELHSHRC
     C*
     C*
     C           BYPASS    TAG
     C                     READ LHHREC                   LR
     C                     ENDDO
     C*
     C*
