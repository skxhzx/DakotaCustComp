     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR500     Pick orders - Parameters
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*    08/23/94 DAS
     F*      - Revised to work with warehouse.
     F*
     F*    01/12/96  DAS  2.10
     F*      - Took out PIRUSER file and corresponding code and replaced
     F*        with call to GETWHSE.
     F*
213 AF*    05/21/96  DAS
     F*      - Added route and jobq options.
     F*
214 AF*    07/05/96  DAS
     F*      - Moved check for verifying route into routine ZZNXT1
     F*        after checking for unfinished batch.
217  F*    10/16/96  RH   2.17
     F*      - Output parameter $PWHSE changed to (15 5) from (3 0     )
221  F*    05/21/97  RH   2.21
     F*      - Move program name to parm $LPRG
510aAF*    12/06/06  RH   5.10a
     F*      - Enh: Remove BFCADR, no longer used.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT LOG NUMBER)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FOR500FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FBATCTL1 UF  E           K        DISK
210 DF*IRUSER IF  E           K        DISK
213 AFORDH2   IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   1 60
     E                    STAT    1   6 30
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
213 AI                                    P   1   20ERWHSE
213 AI                                        3   7 ERRTE
     I*
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
217 MC*      $PWHSE  Warehouse
213 AC*      $PTYPE  *PICK    - Pick normal customer orders (all routes)
213 AC*              *PICK1   - Pick normal customer orders (one route)
     C*      $PBAT   Batch number for *RESTART
     C*      $PSTAT  Status for *RESTART
     C*      $PRTE   Route for *RESTART
     C*      $PORD   Order number for *RESTART
     C*      $PRTN   *EXIT    - CMD3 was entered
     C*              *CANCEL  - CMD12 was entered
     C*              *PICK    - Pick orders
     C*              *RESTART - Restart abnormally ended batch
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
217 DC*                    PARM           $PWHSE  30
217 AC                     PARM           $PWHSE 155
     C                     PARM           $PTYPE  8
     C                     PARM           $PBAT   70
     C                     PARM           $PSTAT  1
     C                     PARM           $PJOBQ  1
     C                     PARM           $PRTE   5
     C                     PARM           $PORD   70
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C                     MOVE *BLANKS   $PRTN
510a C*                    CALL 'BFCADR'
510a C*                    PARM           $RTN    8
510a C*          $RTN      IFEQ '*EXPIRED'
510a C*                    MOVE 'EOJ'     NXTSCR
510a C*                    MOVE '*EXIT   '$PRTN
510a C*                    ELSE
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE '*EXIT   '$PRTN
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
510aDC*                    END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     C*  Get existing record.
     C*
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ *ON
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ *ON
     CSR                   EXSR SCR01I
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ *ON
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys
     C*
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ*ON       ENDSC1
     CSR                   EXSR ZZNXT1
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C*R                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR1005  Value must be 'Y' or 'N'
     C*
     CSR         ZM1005    BEGSR
     CSR                   MOVE 'PIR1005' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   OR50019  No open orders found for warehouse.
     C*
     CSR         ZM19      BEGSR
     CSR                   MOVE 'OR50019' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   OR50020  No open orders found for route.
     C*
     CSR         ZM20      BEGSR
     CSR                   MOVE 'OR50020' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify warehouse code.
     C*
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVEL$LERM     ERRMSG
     CSR                   EXSR ZM0105
     CSR                   ELSE
     CSR         $LRTN     IFEQ '*PGMQ   '
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE $LMSG     #MSGK
     CSR                   EXSR ZMQMSG
     CSR                   ENDIF
     CSR                   ENDIF
214 DC*
214 DC*  Verify route.
214 DC*
214 DC*                    SELEC
214 DC*          W1RTE     WHEQ '*ALL'
214 DC*          KEYOH1    SETLLORDH2                    50
214 DC*          *IN50     IFNE *ON
214 DC*                    MOVE *ON       ERROR
214 DC*                    MOVE *ON       *IN22
214 DC*                    MOVE *ON       *IN02
214 DC*                    MOVE *BLANKS   ERRMSG
214 DC*                    Z-ADDW1WHSE    ERWHSE
214 DC*                    EXSR ZM19
214 DC*                    ENDIF
214 DC*
214 DC*                    OTHER
214 DC*          KEYOH2    SETLLORDH2                    50
214 DC*          *IN50     IFNE *ON
214 DC*                    MOVE *ON       ERROR
214 DC*                    MOVE *ON       *IN22
214 DC*                    MOVE *ON       *IN02
214 DC*                    MOVE *BLANKS   ERRMSG
214 DC*                    MOVE W1RTE     ERRTE
214 DC*                    Z-ADDW1WHSE    ERWHSE
214 DC*                    EXSR ZM20
214 DC*                    ENDIF
214 DC*
214 DC*                    ENDSL
     C*
     C*  Verify jobq option.
     C*
     CSR         W1JOBQ    IFNE 'Y'
     CSR         W1JOBQ    ANDNE'N'
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN23
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,1    ERRMSG
     CSR                   EXSR ZM1005
     CSR                   ENDIF
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     C*
     C*  Test for F4 - Lookup
     C*
     CSR         *INKD     IFEQ *ON
     CSR                   MOVE *ON       CMDTKN
     CSR                   Z-ADD0         ROW#
     CSR                   MOVE ROWIN     ROW
     CSR                   Z-ADD0         COL#
     CSR                   MOVE COLIN     COL
     C*
     C*     Warehouse
     C*
     CSR                   SELEC
     CSR         ROW#      WHEQ 08
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE '*LOOKUP '$LCMD
     CSR                   MOVE #PROG     $LPRG
     CSR                   Z-ADDW1WHSE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*SELECT '
     CSR                   Z-ADD$LWHSE    W1WHSE
     CSR                   ENDIF
     C*
     C*     Cursor not on a valid lookup field.
     C*
     CSR                   OTHER
     CSR                   MOVE *ON       ERROR
     CSR                   EXSR ZM9905
     CSR                   ENDSL
     CSR                   ENDIF
     C*
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     C*      Get warehouse for user.
     C*
210 DC*R         #USER     CHAINPIRUSER              79
210 DC*R         *IN79     IFEQ *ON
210 DC*R                   Z-ADD1         W1WHSE
210 DC*R                   ELSE
210 DC*R                   Z-ADDUSWHSE    W1WHSE
210 DC*R                   ENDIF
210 AC*
210 ACSR                   CALL 'GETWHSE'
210 ACSR                   PARM           #USER
210 ACSR         W1WHSE    PARM 0         #WHSE   30
210 AC*
     C*
213 ACSR                   MOVE '*ALL '   W1RTE
     CSR                   MOVE 'N'       W1JOBQ
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define partial key for batch control file.
     C*
     CSR         KEYBTP    KLIST
     CSR                   KFLD           BTWHSE
213 AC*
213 AC*   Define partial key for order header file (ORDH2)
213 AC*
213 ACSR         *LIKE     DEFN OHSTAT    KYSTAT
213 ACSR                   MOVE 'O'       KYSTAT
213 ACSR         KEYOH1    KLIST
213 ACSR                   KFLD           W1WHSE
213 ACSR                   KFLD           KYSTAT
214 ACSR                   KFLD           KYOTYP  1
213 ACSR         KEYOH2    KLIST
213 ACSR                   KFLD           W1WHSE
213 ACSR                   KFLD           KYSTAT
214 ACSR                   KFLD           KYOTYP
213 ACSR                   KFLD           W1RTE
     C*
221 ACSR                   MOVE #PROG     $LPRG
     C*
     C*  Dummy update statement to satisfy compiler.
     C*
     CSR                   MOVE *OFF      *IN99
     CSR 99                UPDATBTREC
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Select first screen to display.
     C*
     CSR                   EXSR SCR01I
     C*
     CSR         ENDIN2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZNXT1   What to do next when screen 1 input is ok.
     C*
     CSR         ZZNXT1    BEGSR
     C*
     CSR                   Z-ADD0         $PBAT
     CSR                   Z-ADD0         $PORD
213 AC*
213 AC*   Initialize batch type being run.
213 AC*
213 ACSR         W1RTE     IFEQ '*ALL'
213 ACSR                   MOVE '*PICK   '$PTYPE
213 ACSR                   ELSE
213 ACSR                   MOVE '*PICK1  '$PTYPE
213 ACSR                   ENDIF
214 ACSR                   MOVE ' '       KYOTYP
213 AC*
213 AC*   Skip logic looking for active batch.
213 AC*
213 ACSR                   GOTO SKIP1
     C*
     C*   See if a batch is currently being processed.
     C*
     C*     During picking each process (status) updates and then
     C*     locks the batch control record. Therefore, if we read
     C*     the batch control file and run into a locked record then
     C*     we can assume a batch is running.
     C*
     CSR                   Z-ADDW1WHSE    BTWHSE
     CSR         KEYBTP    SETLLBATCTL1
     CSR                   MOVE *OFF      STOP    1
     CSR         STOP      DOUEQ*ON
     CSR         KEYBTP    READEBATCTL1                7879
     CSR         *IN79     IFEQ *ON
     CSR         BTSTAT    OREQ '9'
     CSR                   MOVE *ON       STOP
     CSR                   ELSE
     CSR         *IN78     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   EXFMTSCREEN2
     CSR                   GOTO ENDNX1
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDDO
213 ACSR         SKIP1     TAG
     C*
     C*   See if a batch ended abnormally.
     C*
     C*      If a batch ends normally then its status will be '9'.
     C*
     C*      If a batch is found without a status of '9' then the
     C*        user has the choice of exiting the program or
     C*        restarting from where the last batch left off.
     C*
     CSR                   Z-ADDW1WHSE    BTWHSE
     CSR         KEYBTP    SETLLBATCTL1
     CSR                   MOVE *OFF      STOP    1
     CSR         STOP      DOUEQ*ON
     CSR         KEYBTP    READEBATCTL1                7879
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *ON       STOP
     CSR                   ELSE
     CSR         BTTYPE    IFEQ $PTYPE
     CSR         BTTYPE    OREQ '*IMPORD '
     CSR         BTSTAT    IFNE '9'
     CSR                   Z-ADDBTBAT#    W1BAT
     CSR                   MOVE BTSTRD    $CVD8I
     CSR                   MOVE '*CMDMDY '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    W1DATE
     CSR                   Z-ADDBTSTRT    W1TIME
     CSR                   MOVE BTSTRB    W1USER
     CSR                   MOVE BTSTAT    X       10
     CSR                   MOVE STAT,X    W1STAT
     CSR                   MOVE *ON       *IN98
     CSR                   MOVE *OFF      *INKJ
     CSR                   MOVE *OFF      *INKC
     CSR         *INKC     DOWEQ*OFF
     CSR         *INKJ     ANDEQ*OFF
     CSR                   EXFMTSCREEN3
     C*
     CSR                   SELEC
     CSR         *INKC     WHEQ *ON
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   GOTO ENDNX1
     C*
     CSR         *INKJ     WHEQ *ON
     CSR                   Z-ADDBTWHSE    $PWHSE
     CSR                   Z-ADDBTBAT#    $PBAT
     CSR                   MOVE BTSTAT    $PSTAT
     CSR                   MOVE BTRTE     $PRTE
     CSR                   Z-ADDBTORD     $PORD
     CSR                   MOVE '*RESTART'$PRTN
     CSR                   MOVE *OFF      *IN98
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   GOTO ENDNX1
     CSR                   ENDSL
     C*
     CSR                   ENDDO
     C*
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDDO
214 AC*
214 AC*  Verify route.
214 AC*
214 ACSR                   SELEC
214 ACSR         W1RTE     WHEQ '*ALL'
214 ACSR         KEYOH1    SETLLORDH2                    50
214 ACSR         *IN50     IFNE *ON
214 ACSR                   MOVE *ON       ERROR
214 ACSR                   MOVE *ON       *IN22
214 ACSR                   MOVE *ON       *IN02
214 ACSR                   MOVE *BLANKS   ERRMSG
214 ACSR                   Z-ADDW1WHSE    ERWHSE
214 ACSR                   EXSR ZM19
214 ACSR                   GOTO ENDNX1
214 ACSR                   ENDIF
214 AC*
214 ACSR                   OTHER
214 ACSR         KEYOH2    SETLLORDH2                    50
214 ACSR         *IN50     IFNE *ON
214 ACSR                   MOVE *ON       ERROR
214 ACSR                   MOVE *ON       *IN22
214 ACSR                   MOVE *ON       *IN02
214 ACSR                   MOVE *BLANKS   ERRMSG
214 ACSR                   MOVE W1RTE     ERRTE
214 ACSR                   Z-ADDW1WHSE    ERWHSE
214 ACSR                   EXSR ZM20
214 ACSR                   GOTO ENDNX1
214 ACSR                   ENDIF
214 AC*
214 ACSR                   ENDSL
     C*
     CSR                   MOVE '*PICK   '$PRTN
     CSR                   Z-ADDW1WHSE    $PWHSE
     CSR                   MOVE W1RTE     $PRTE
     CSR                   MOVE W1JOBQ    $PJOBQ
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR         ENDNX1    ENDSR
     C*
     O*----------------------------------------------------------------     ****
     O*
     O*  COMPILE TIME TABLES
     O*
**
Place on Jobq
**
1 - Allocating items
2 - Building pallets
3 - Picking stock from slots
4 - Printing labels
5 - Printing loading sheet
6 - Printing pallet summary
