     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETRPT    Get report information
     F*  07 July 1994
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FREPORTS IF  E           K        DISK
     FRPTOVR1 IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $CMD     -  *DESC     - Get description only.
     I*                 *DEVFORM  - Get device and form to use.
     I*     $PRTF    -  Printer file
     I*     $WHSE    -  Warehouse
     I*     $STYP    -  Section type
     I*     $WHDP    -  Department
     I*
     I*  Output parameters
     I*
     I*     $DESC    -  Description
     I*     $DEV     -  Device to use
     I*     $FORM    -  Form type
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $CMD    8
     C                     PARM           $PRTF  10
     C                     PARM           $WHSE   30
     C                     PARM           $STYP   1
     C                     PARM           $WHDP   5
     C                     PARM           $DESC  50
     C                     PARM           $DEV   10
     C                     PARM           $FORM  10
     C*
     C*  Define key for RPTOVR file
     C*
     C           ROKEY     KLIST
     C                     KFLD           $PRTF
     C                     KFLD           ROWHSE
     C                     KFLD           ROSTYP
     C                     KFLD           ROWHDP
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $DESC
     C                     MOVE *BLANKS   $DEV
     C                     MOVE *BLANKS   $FORM
     C*
     C           $CMD      CASEQ'*DESC   'GETDSC
     C           $CMD      CASEQ'*DEVFORM'GETDEV
     C                     END
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  GETDEV  Get device and form
     C*
     CSR         GETDEV    BEGSR
     C*
     C*   Setup key
     C*
     CSR                   Z-ADD$WHSE     ROWHSE
     CSR                   MOVE $STYP     ROSTYP
     CSR                   MOVE $WHDP     ROWHDP
     C*
     C*   Look for overrides in the following order:
     C*
     C*           Warehouse/Section/Department
     C*           Warehouse/Section
     C*           Warehouse
     C*
     C*   If override is not found then get default from REPORTS.
     C*
     CSR                   MOVE *OFF      STOP    1
     CSR                   MOVE *OFF      FOUND   1
     CSR         STOP      DOUEQ*ON
     CSR         ROKEY     CHAINRPTOVR1              79
     C*
     C*      Override found.
     C*
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE RODEV     $DEV
     CSR                   MOVE ROFORM    $FORM
     CSR                   MOVE *ON       STOP
     CSR                   MOVE *ON       FOUND
     CSR                   GOTO ENDDO1
     CSR                   ENDIF
     C*
     C*      Override not found. Try another combination.
     C*
     CSR         ROWHDP    IFNE *BLANKS
     CSR                   MOVE *BLANKS   ROWHDP
     CSR                   ELSE
     CSR         ROSTYP    IFNE *BLANK
     CSR                   MOVE *BLANK    ROSTYP
     CSR                   ELSE
     CSR                   MOVE *ON       STOP
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         ENDDO1    TAG
     CSR                   ENDDO
     C*
     C*   If override wasn't found then get default from REPORTS.
     C*
     CSR         FOUND     IFEQ *OFF
     CSR         $PRTF     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE RPDEV     $DEV
     CSR                   MOVE RPFORM    $FORM
     CSR                   ELSE
     CSR                   MOVEL'*JOB'    $DEV
     CSR                   MOVEL'*STD'    $FORM
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  GETDSC  Get description
     C*
     CSR         GETDSC    BEGSR
     C*
     CSR         $PRTF     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVE RPDESC    $DESC
     CSR                   ENDIF
     CSR                   ENDSR
