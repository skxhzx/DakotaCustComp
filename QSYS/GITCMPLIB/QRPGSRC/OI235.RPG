     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OI235     Import will call orders - Post
     F*  28 August 1997
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
404 AF*    09/05/97  DAS  4.04
     F*      - Recompiled because file IMPORDD changed for cross dock.
411 AF*    04/15/99  RH   4.11
     F*      - Recompiled because OI235PR object was missing.
     F*
530 AF*    03/15/10  MLB  5.30
     F*      - Enh: Added client custom code support program.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: Dierk's Waukesha
     F*
DRK AF*    03/15/10  MLB  DRK
     F*      - Revised program to allow route number received from
     F*        prompt program to be imported into Dakota.
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
404 AF*    This program has not been changed to look for will calls.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  61        - Special order (Customer # > 0)
     F*  78        - EOF for IMPORDD
     F*  79        - EOF for IMPORDH
     F*  91        - Overflow indicator
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
400 DF*MPORDH UF  E           K        DISK
400 MFIMPORDH1UF  E           K        DISK
     FIMPORDD UF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
400 AFRTESUM  UF  E           K        DISK                      A
400 AFRTEHED  O   E           K        DISK                      A
     FORDH    O   E           K        DISK                      A
     FORDD    O   E           K        DISK                      A
     FOI235PR O   E             91     PRINTER
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    DESC    1   3 20
530 A*----------------------------------------------------------------
530 A*  Customer id
530 A*
530 AI              'GETCLIENT'           C         @GETCL
530 A*
530 AI/COPY QCOPYSRC,IL#DIERKS
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
216 MC*      $PWHS   Warehouse to process.
400 AC*      $PTYPE  Order types to process.
400 AC*              ' ' - Normal
400 AC*              'T' - Transfer
400 AC*              'W' - Will Call
DRK AC*      $PRTE   Will-Call route to import.
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
216 MC                     PARM           $PWHS  155
400 AC                     PARM           $PTYPE  1
DRK AC                     PARM           $PRTE   5
     C*----------------------------------------------------------------
216 AC                     Z-ADD$PWHS     $PWHSE  30
     C* Key definitions
     C*
     C* Download order header file - Partial key
     C*
     C           OIHKEY    KLIST
     C                     KFLD           $PWHSE
404 AC                     KFLD           $PTYPE
     C*
     C* Download order detail file - Partial key
     C*
     C           OIDKEY    KLIST
     C                     KFLD           OIHWHS
     C                     KFLD           OIHORD
     C*
     C* Item file
     C*
     C           ITKEY     KLIST
     C                     KFLD           OIDWHS
     C                     KFLD           OIDITM
400 AC*
400 AC* Route summary file
400 AC*
400 AC           KEYRS     KLIST
400 AC                     KFLD           RHWHSE
400 AC                     KFLD           RHRTID
400 AC                     KFLD           ITWHDP
     C*----------------------------------------------------------------
     C* Main line
400 AC*
400 AC* Get import date/time.
400 AC*
400 AC                     MOVE '*CURCMD '$CVCMD
400 AC                     CALL @CVTDT
400 AC                     PARM           $CVTDT
400 AC                     Z-ADD$CVD8O    IMPDTE  80
400 AC                     TIME           IMPTIM  60
     C*
     C* Get report heading.
     C*
     C                     MOVE *BLANKS   W1HEAD
     C                     MOVEL'OI235PR' RPPRTF
     C           RPPRTF    CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD50        $PLEN
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR  60
     C                     PARM           $PLEN   30
     C                     MOVEL$PSTR     W1HEAD
     C*
     C*   Get warehouse description.
     C*
     C           $PWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   WHDESC
     C                     END
     C*
     C*   Create warehouse heading.
     C*
     C                     MOVE $PWHSE    WORK3A  3
     C           ' 0'      CHECKWORK3A    POS     10
     C           4         SUB  POS       LEN     10
     C           LEN       SUBSTWORK3A:POSWHHEAD
     C           WHHEAD    CAT  '-':1     WHHEAD
     C           WHHEAD    CAT  WHDESC:1  WHHEAD
     C                     CALL 'CENTER'
     C                     PARM WHHEAD    $CSTR  60
     C                     PARM 40        $CLEN   30
     C                     MOVEL$CSTR     WHHEAD
     C*
     C* Initialize grand total fields
     C*
     C                     Z-ADD0         GRDORD
     C                     Z-ADD0         GRDLIN
     C                     Z-ADD0         PREOH
     C                     Z-ADD0         PREOD
     C                     Z-ADD0         NOITEM
214 AC                     Z-ADD0         BADBRK
     C*
     C* Whip through all the download header records
     C*
400 AC           *LIKE     DEFN OIHRTE    LSTRTE
400 AC                     MOVE '@@@@@'   LSTRTE
     C                     MOVE *OFF      PFLAG   1
     C                     MOVE *OFF      EOFH    1
     C                     Z-ADD55        LINPPG  30       MAX LINES PER PAGE
     C                     Z-ADD0         COUNT   30       LINE COUNTER
400 DC*          OIHKEY    SETLLIMPORDH
400 MC           OIHKEY    SETLLIMPORDH1
     C           EOFH      DOUEQ*ON
     C*
400 DC*          OIHKEY    READEIMPORDH                  79
400 MC           OIHKEY    READEIMPORDH1                 79
     C           *IN79     IFEQ *ON
     C                     MOVE *ON       EOFH
     C                     GOTO ENDDOH
     C                     ENDIF
DRK AC*
DRK AC*  Print report heading if necessary.
DRK AC           CLIENT    IFEQ DIERKS
DRK AC*    Not the correct will-call route for import.
DRK AC           $PTYPE    IFEQ 'W'
DRK AC           OIHRTE    ANDNE$PRTE
DRK AC                     ITER
DRK AC                     ENDIF
DRK AC                     ENDIF
     C*
     C*  Print report heading if necessary.
     C*
     C           PFLAG     IFEQ *OFF
     C           COUNT     ORGE LINPPG
     C                     Z-ADD0         COUNT
     C                     WRITEHEAD1
     C                     ADD  3         COUNT
     C                     MOVE *OFF      PFLAG
     C                     ENDIF
400 AC*
400 AC*  Add record to route header file when route changes.
400 AC*
400 AC           OIHRTE    IFNE LSTRTE
400 AC                     EXSR FILLRH
400 AC                     WRITERHREC                  49
400 AC                     MOVE OIHRTE    LSTRTE
400 AC                     ENDIF
     C*
     C*  Add record to order header file. If an error occurs, assume
     C*    it was caused by a duplicate record.
     C*
     C                     MOVE *BLANKS   HEDMSG
     C                     EXSR FILLOH
     C                     WRITEOHREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    HEDMSG
     C                     ADD  1         PREOH
     C                     ENDIF
     C*
     C*    Print order heading
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD2
     C                     ADD  2         COUNT
     C                     ENDIF
     C*
     C                     WRITEHEAD3
     C                     ADD  2         COUNT
     C*
     C           PFLAG     IFEQ *OFF
     C                     WRITEHEAD4
     C                     ADD  2         COUNT
     C                     MOVE *ON       PFLAG
     C                     ENDIF
     C*
     C*    Initialize order subtotals
     C*
     C                     Z-ADD0         SUBLIN  30
     C                     Z-ADD0         SUBQ1   70
     C                     Z-ADD0         SUBQ2   70
     C                     Z-ADD0         SUBQ3   70
     C*
     C*    Whip through all the download detail records
     C*
     C                     MOVE *OFF      EOFD    1
     C           OIDKEY    SETLLIMPORDD
     C           EOFD      DOUEQ*ON
     C*
     C           OIDKEY    READEIMPORDD                  78
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       EOFD
     C                     GOTO ENDDOD
     C                     ENDIF
     C                     MOVE *BLANKS   DETMSG
     C                     MOVE 'N'       GOOD1
     C*
     C*       If item doesn't exist write error message.
     C*
     C           ITKEY     CHAINPIRITEM              76
     C           *IN76     IFEQ *ON
     C                     MOVE DESC,2    DETMSG
     C                     ADD  1         NOITEM
     C                     MOVE *BLANKS   ITDESC
     C                     MOVEL'*****'   ITDESC
     C                     ELSE
     C*       Check if breakdown qty is allowed using piritem file.
     C*       Add rec if qty1, brk qty2 or brk qty3 has good qty.
214 AC                     EXSR ZZBKDN
     C*
214 AC*       If qty1, brkdown qty2 or brkdown qty3 has good qty,
     C*        add record to order detail file. If an error
     C*        occurs, assume was caused by a duplicate record.
400 AC*        Update route summary with detail information.
     C*
214 AC           GOOD1     IFEQ 'Y'
     C                     EXSR FILLOD
     C                     WRITEODREC                  49
     C           *IN49     IFEQ *ON
     C                     MOVE DESC,1    DETMSG
     C                     ADD  1         PREOD
     C                     ENDIF
400 AC                     EXSR UPDSUM
214 AC                     ENDIF
     C*
     C                     ENDIF
     C*
     C*       Print order detail and update subtotals
     C*
     C                     EXSR ZRHEAD
     C                     WRITEDETAIL
     C                     ADD  1         COUNT
     C                     ADD  1         SUBLIN
     C                     ADD  OIDQ1     SUBQ1
     C                     ADD  OIDQ2     SUBQ2
     C                     ADD  OIDQ3     SUBQ3
     C*
     C*       Delete detail record from download file
     C*
     C                     DELETOIDREC
     C*
     C           ENDDOD    TAG
     C                     ENDDO
     C*
     C*    Print order subtotals and update grand totals
     C*
     C**                   EXSR ZRHEAD
     C                     WRITESUBTTL
     C                     ADD  3         COUNT
     C                     ADD  1         GRDORD
     C                     ADD  SUBLIN    GRDLIN
     C*
     C*  Delete header record from download file
     C*
     C                     DELETOIHREC
     C*
     C           ENDDOH    TAG
     C                     ENDDO
     C*
     C*  Print order grand totals
     C*
     C           PFLAG     IFEQ *ON
     C           COUNT     IFGE LINPPG
     C                     WRITEHEAD1
     C                     ENDIF
     C                     WRITEGRNDTTL
     C                     ENDIF
     C*
     C*  We are finished so get out
     C*
     C                     MOVE *ON       *INLR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
530 AC*
530 AC*  *INZSR  Initialization subroutine
530 AC*
530 AC           *INZSR    BEGSR
530 A*
530 A* Get client id.
530 A*
530 AC                     CALL @GETCL
530 AC                     PARM           CLIENT 10
530 AC                     PARM           CLILOC 10
530 A*
530 AC                     ENDSR
530 A*
530 AC*----------------------------------------------------------------
     C*
     C*  FILLOD  Fill order detail fields.
     C*
     CSR         FILLOD    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEARODREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIDWHS    ODWHSE
213 DC*                    Z-ADDOIDORD    ODORD
213 MCSR                   Z-ADDOHORD     ODORD
     CSR                   Z-ADDOIDSEQ    ODSEQ
     CSR                   MOVE OIDITM    ODITEM
     CSR                   Z-ADDOIDQ1     ODQOR1
214 ACSR         ITFLG1    IFEQ 'Y'
     CSR                   Z-ADDOIDQ2     ODQOR2
214 ACSR                   ELSE
214 ACSR                   Z-ADD0         ODQOR2
214 ACSR                   ENDIF
214 ACSR         ITFLG2    IFEQ 'Y'
     CSR                   Z-ADDOIDQ3     ODQOR3
214 ACSR                   ELSE
214 ACSR                   Z-ADD0         ODQOR3
214 ACSR                   ENDIF
     CSR                   Z-ADDOIDWH2    ODWH2
     CSR                   Z-ADDOIDOR2    ODOR2
     CSR                   MOVE OIDOIT    ODOITM
     CSR                   MOVE OIDSPC    ODSPCL
     CSR                   MOVE 'O'       ODSTAT
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FILLOH   Fill order header fields.
     C*
     CSR         FILLOH    BEGSR
     C*
     C*  Initialize record
     C*
     CSR                   CLEAROHREC
     C*
     C*  Move fields from download file to purchase order header fields
     C*
     CSR                   Z-ADDOIHWHS    OHWHSE
213 DC*                    Z-ADDOIHORD    OHORD
213 ACSR                   CALL 'PIRORD#'
213 ACSR                   PARM OHWHSE    $OWHSE  30
213 ACSR         OHORD     PARM 0         $OORD   70
     CSR                   MOVE OIHTYP    OHTYPE
213 ACSR                   Z-ADDOIHORD    OHCORD
     CSR                   MOVE OIHCPO    OHCPO
     CSR                   Z-ADDOIHCUS    OHCUST
     CSR                   MOVE OIHCNM    OHCNAM
     CSR                   MOVE OIHCA1    OHCAD1
     CSR                   MOVE OIHCA2    OHCAD2
     CSR                   MOVE OIHCCT    OHCCTY
     CSR                   MOVE OIHCST    OHCST
     CSR                   MOVE OIHCZP    OHCZIP
400 DC*                    MOVE OIHRTE    OHRTE
400 MCSR                   MOVE RHRTID    OHRTE
     CSR                   Z-ADDOIHSTP    OHSTOP
     CSR                   MOVE OIHTRK    OHTRUK
     CSR                   MOVE OIHMSC    OHMISC
     CSR                   MOVE 'O'       OHSTAT
400 DC*                    MOVE '*CURCMD '$CVCMD
400 DC*                    CALL @CVTDT
400 DC*                    PARM           $CVTDT
400 DC*                    Z-ADD$CVD8O    OHSDTE
400 DC*                    TIME           OHSTIM
400 MCSR                   Z-ADDIMPDTE    OHSDTE
400 MCSR                   Z-ADDIMPTIM    OHSTIM
     C*
     CSR                   ENDSR
400 AC*----------------------------------------------------------------
400 AC*
400 AC*  FILLRH   Fill route header fields.
400 AC*
400 ACSR         FILLRH    BEGSR
400 AC*
400 AC*  Initialize record
400 AC*
400 ACSR                   CLEARRHREC
400 AC*
400 AC*  Update fields.
400 AC*
400 ACSR                   Z-ADDOIHWHS    RHWHSE
400 ACSR                   CALL 'PIRRTE#'
400 ACSR                   PARM RHWHSE    $RWHSE  30
400 ACSR                   PARM ' '       RHRTID
400 ACSR                   MOVE OIHRTE    RHRTE
400 ACSR                   MOVE OIHTYP    RHTYPE
400 ACSR                   MOVE OIHTRK    RHTRUK
400 ACSR                   MOVE '1'       RHSTAT
400 ACSR                   Z-ADDIMPDTE    RHSTSD
400 ACSR                   Z-ADDIMPTIM    RHSTST
400 ACSR                   Z-ADDIMPDTE    RHIMPD
400 ACSR                   Z-ADDIMPTIM    RHIMPT
400 ACSR                   Z-ADDIMPDTE    RHRPTD
400 AC*
400 ACSR                   ENDSR
400 AC*----------------------------------------------------------------
400 AC*
400 AC*  UPDSUM   Update route summary file with detail record.
400 AC*
400 ACSR         UPDSUM    BEGSR
400 AC*
400 AC*  Define work fields
400 AC*
400 ACSR         *LIKE     DEFN ITCUBE    TMPCUB
400 ACSR         *LIKE     DEFN ITSWGT    TMPWGT
400 AC*
400 AC*  Try to get existing summary records for department.
400 AC*
400 ACSR         KEYRS     CHAINRTESUM               77
400 AC*
400 AC*     Record does not exist - Initialize fields.
400 AC*
400 ACSR         *IN77     IFEQ *ON
400 ACSR                   Z-ADDRHWHSE    RSWHSE
400 ACSR                   MOVE RHRTID    RSRTID
400 ACSR                   MOVE ITWHDP    RSWHDP
400 ACSR                   Z-ADD0         RSORDC
400 ACSR                   Z-ADD0         RSORDW
400 ACSR                   Z-ADD0         RSORDP
400 ACSR                   Z-ADD0         RSPCKC
400 ACSR                   Z-ADD0         RSPCKW
400 ACSR                   Z-ADD0         RSPCKP
400 ACSR                   ENDIF
400 AC*
400 AC*     Update department totals.
400 AC*
400 ACSR                   ADD  ODQOR1    RSORDP
400 ACSR         ITCUBE    MULT ODQOR1    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    MULT ODQOR1    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 AC*
400 ACSR                   ADD  ODQOR2    RSORDP
400 ACSR         ODQOR2    IFNE 0
400 ACSR         ITUMQ2    ANDGT0
400 ACSR         ITCUBE    DIV  ITUMQ2    TMPCUB    H
400 ACSR         TMPCUB    MULT ODQOR2    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    DIV  ITUMQ2    TMPWGT    H
400 ACSR         TMPWGT    MULT ODQOR2    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 ACSR                   ENDIF
400 AC*
400 ACSR                   ADD  ODQOR3    RSORDP
400 ACSR         ODQOR3    IFNE 0
400 ACSR         ITUMQ3    ANDGT0
400 ACSR         ITCUBE    DIV  ITUMQ3    TMPCUB    H
400 ACSR         TMPCUB    MULT ODQOR3    TMPCUB
400 ACSR                   ADD  TMPCUB    RSORDC
400 ACSR         ITSWGT    DIV  ITUMQ3    TMPWGT    H
400 ACSR         TMPWGT    MULT ODQOR3    TMPWGT
400 ACSR                   ADD  TMPWGT    RSORDW
400 ACSR                   ENDIF
400 AC*
400 AC*     Add/Update summary record.
400 AC*
400 ACSR         *IN77     IFEQ *ON
400 ACSR                   WRITERSREC
400 ACSR                   ELSE
400 ACSR                   UPDATRSREC
400 ACSR                   ENDIF
400 AC*
400 ACSR                   ENDSR
     C*----------------------------------------------------------------
214 AC*
214 AC*  ZZBKDN  Check breakdown quanties.
214 AC*
214 ACSR         ZZBKDN    BEGSR
214 ACSR         OIDQ2     IFNE 0
213 ACSR         ITFLG1    ANDNE'Y'
213 ACSR                   MOVE DESC,3    DETMSG
213 ACSR                   ADD  1         BADBRK
213 ACSR                   ENDIF
214 AC*
214 ACSR         OIDQ3     IFNE 0
213 ACSR         ITFLG2    ANDNE'Y'
213 ACSR                   MOVE DESC,3    DETMSG
213 ACSR                   ADD  1         BADBRK
213 ACSR                   ENDIF
214 AC*
214 AC*  Move fields from download file to purchase order header fields
214 AC*
214 ACSR         OIDQ1     IFNE 0
213 ACSR                   MOVE 'Y'       GOOD1   1
213 ACSR                   ELSE
214 ACSR         OIDQ2     IFNE 0
214 ACSR         ITFLG1    ANDEQ'Y'
214 ACSR                   MOVE 'Y'       GOOD1
214 ACSR                   ELSE
214 ACSR         OIDQ3     IFNE 0
214 ACSR         ITFLG2    ANDEQ'Y'
214 ACSR                   MOVE 'Y'       GOOD1
214 ACSR                   ENDIF
214 ACSR                   ENDIF
214 ACSR                   ENDIF
214 AC*
214 ACSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZRHEAD   Print report headings.
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         COUNT     IFGE LINPPG
     CSR                   SETON                     81
     CSR                   Z-ADD0         COUNT
     CSR                   WRITEHEAD1
     CSR                   ADD  3         COUNT
     CSR                   WRITEHEAD2
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD3
     CSR                   ADD  2         COUNT
     CSR                   WRITEHEAD4
     CSR                   ADD  2         COUNT
     CSR                   SETOF                     81
     CSR                   ENDIF
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
*EXIST*
*INVALID ITEM*
*BREAKDOWN INVALID*
