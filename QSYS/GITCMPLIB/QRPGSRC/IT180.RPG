     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (630) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT180     FS-WMS Warehouse start time maintenance - Change
     F*  28 December 1999
     F*  Max Blacknik
     F*
     F*  SK112 - 1.01
     F*
     F*  Revisions
     F*
413 AF*    12/28/99  MLB  4.13
     F*      - Created.
     F*
413aAF*    03/16/00  DAS  4.13a
     F*      - Changed format. Removed all times but Sunday. Took
     F*        seconds off time. Rearranged fields on screen.
     F*      - Changed ZZCKTM to ignore seconds.
     F*      - Changed ZZCHK1 to only check Sunday's time.
     F*      - Changed ZZUPD to update all times with Sunday's time.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  01 - 20   FIELD POSITION TO
     F*  21 - 40   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT KEY)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT180FM CF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
     FOPTIONS UF  E           K        DISK                      A
     F            OPREC                             KRENAMERECORD
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A10        10  1
     E                    $MD       128  1
     E                    DESC    1   4 50
     E                    DAYNM   1   7  9
     E                    TABDN   7   7  3   TABDNR  1 0 Current day nbr.
     I*----------------------------------------------------------------
     I*  Validation variables
     I*
     IWKDS        DS
     I                                        1   20WKHOUR
     I                                        3   40WKMIN
     I                                        5   60WKSEC
413aDI**                                      1   60WKTIME
413aMI                                        1   40WKTIME
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHSE
     I*----------------------------------------------------------------
     I*  *DAYEND  -  Day end processing options.
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPSTR1  -  Sunday - Warehouse start time
     I*    OPSTR2  -  Monday - Warehouse start time
     I*    OPSTR3  -  Tuesday - Warehouse start time
     I*    OPSTR4  -  Wednesday - Warehouse start time
     I*    OPSTR5  -  Thursday - Warehouse start time
     I*    OPSTR6  -  Friday - Warehouse start time
     I*    OPSTR7  -  Saturday - Warehouse start time
413aAI*    OPLTDT  -  Last transaction processed date
413aAI*    OPLTTM  -  Last transaction processed time
413aAI*    OPLODT  -  Last order processed date
413aAI*    OPLOTM  -  Last order processed time
413aAI*    OPWEEK  -  Current week number
413aAI*    OPAVWK  -  Number of weeks for average
413aAI*    OPDYAV  -  Day to compute averages (1=Sun - 7=Sat)
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I                                        1   60OPSTR1
     I                                        7  120OPSTR2
     I                                       13  180OPSTR3
     I                                       19  240OPSTR4
     I                                       25  300OPSTR5
     I                                       31  360OPSTR6
     I                                       37  420OPSTR7
     I                                       43  500OPLTDT
     I                                       51  560OPLTTM
     I                                       57  640OPLODT
     I                                       65  700OPLOTM
     I                                       71  720OPWEEK
     I                                       73  750OPWKAV
     I                                       76  760OPDYAV
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVDOP
     I                                        9  140$CVDD6
     I                                       15  220$CVDD8
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  30 ERFLD
     I                                       31  40 ERFROM
     I                                       41  50 ERTO
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   *ADD     - Create new record.
     C*              *EDIT    - Edit record.
     C*              *PRINT   - Print record.
     C*              *VIEW    - View record.
     C*      $PPRG   Program to send messages back to.
     C*      $PUKY   Contains unique key for everything but *ADD.
     C*
     C*    Returned Parameters
     C*      $PUKY   Contains new unique key for *ADD.
     C*              Unchanged for everything else.
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured. Message in $PERM
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE '1'       *IN97
     C                     MOVE '0'       *IN90
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       ERROR
     C                     MOVE *BLANKS   $PRTN
     C                     MOVE *BLANKS   $PERM
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ '1'
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ '1'
     CSR                   MOVE '1'       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     040506
     CSR                   SETOF                     070809
     CSR                   SETOF                     101112
     CSR                   SETOF                     131415
     CSR                   SETOF                     161718
     CSR                   SETOF                     1920
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     303132
     CSR                   SETOF                     333435
     CSR                   SETOF                     363738
     CSR                   SETOF                     3940
     CSR                   SETOF                     98
     CSR                   MOVE '0'       WARN    1
     CSR                   MOVE '0'       ERROR   1
     CSR                   MOVE '0'       CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     C*
     CSR         $PCMD     IFEQ '*VIEW   '
     CSR                   MOVE '1'       *IN90
     CSR                   ENDIF
     C*
     C*  Get field defaults if this is an add.
     C*
     CSR         $PCMD     IFEQ '*ADD    '
     CSR                   EXSR ZZDFT1
     CSR                   ELSE
     C*
     C*  Otherwise get existing record(s).
     C*
     CSR                   MOVE '1'       *IN91
     CSR                   EXSR ZZGET1
     CSR                   EXSR ZZFIL1
     CSR                   END
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     C*  Test for F3 - Exit
     C*
     CSR         *INKC     IFEQ '1'
     CSR         *LOVAL    SETLLRECORD
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*EXIT   '$PRTN
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F5 - Refresh
     C*
     CSR         *INKE     IFEQ '1'
     CSR         $PCMD     IFNE '*VIEW   '
     CSR                   EXSR SCR01I
     CSR                   END
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for F12 - Previous
     C*
     CSR         *INKL     IFEQ '1'
     CSR         *LOVAL    SETLLRECORD
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR                   MOVE '*CANCEL '$PRTN
     CSR                   GOTO ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys BEFORE verification and update.
     C*
     CSR                   MOVE '*BEFORE 'CMDCHK  8
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Some other key pressed.
     C*
     C*     Check input and write/update record.
     C*
     CSR         $PCMD     IFEQ '*ADD    '
     CSR         $PCMD     OREQ '*CHANGE '
     CSR                   EXSR ZZCHK1
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   EXSR ZZUPD
     CSR         ERROR     CABEQ'1'       ENDSC1
     CSR                   END
     C*
     C*  Test for other command keys AFTER verification and update.
     C*
     CSR                   MOVE '*AFTER  'CMDCHK
     CSR                   EXSR ZZCMD1
     CSR         CMDTKN    CABEQ*ON       ENDSC1
     C*
     C*  Everything went fine so get out of program.
     C*
     CSR                   MOVE 'EOJ'     NXTSCR
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*      0105
     C*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     PIR7973  Record does not exist.
     C*
     CSR         ZM73      BEGSR
     CSR                   MOVE 'PIR7973' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     PIR7974  Record is locked.
     C*
     CSR         ZM74      BEGSR
     CSR                   MOVE 'PIR7974' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR9905  Cursor not in correct position for lookup.
     C*
     CSR         ZM9905    BEGSR
     CSR                   MOVE 'PIR9905' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0110  Number must be greater than zero.
     C*
     CSR         ZM0110    BEGSR
     CSR                   MOVE 'PIR0110' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0112  Invalid range entered.
     C*
     CSR         ZM0112    BEGSR
     CSR                   MOVE 'PIR0112' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0124  Invalid day name abbreviation entered.
     C*
     CSR         ZM0124    BEGSR
     CSR                   MOVE 'PIR0124' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR1001  Invalid value entered.
     C*
     CSR         ZM1001    BEGSR
     CSR                   MOVE 'PIR1001' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE '0'       ERROR
     C*
     C*    Verify Sunday start time.
     C*
     CSR                   Z-ADDW1STR1    WKTIME
     CSR                   Z-ADD1         DAYNBR
     CSR                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Monday start time.
413aDC**
413aDC**                   Z-ADDW1STR2    WKTIME
413aDC**                   Z-ADD2         DAYNBR
413aDC**                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Tuesday start time.
413aDC**
413aDC**                   Z-ADDW1STR3    WKTIME
413aDC**                   Z-ADD3         DAYNBR
413aDC**                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Wednesday start time.
413aDC**
413aDC**                   Z-ADDW1STR4    WKTIME
413aDC**                   Z-ADD4         DAYNBR
413aDC**                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Thursday start time.
413aDC**
413aDC**                   Z-ADDW1STR5    WKTIME
413aDC**                   Z-ADD5         DAYNBR
413aDC**                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Friday start time.
413aDC**
413aDC**                   Z-ADDW1STR6    WKTIME
413aDC**                   Z-ADD6         DAYNBR
413aDC**                   EXSR ZZCKTM
413aDC**
413aDC**   Verify Saturday start time.
413aDC**
413aDC**                   Z-ADDW1STR7    WKTIME
413aDC**                   Z-ADD7         DAYNBR
413aDC**                   EXSR ZZCKTM
     C*
     C*    Verify Average weeks value.
     C*
     CSR         W1WKAV    IFLT 1
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN28
     CSR                   MOVE *ON       *IN08
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELDESC,4    ERFLD
     CSR                   EXSR ZM0110
     CSR                   ENDIF
     C*
     C*    Verify Day to compute averages.
     C*
     CSR         W1DYAV    LOKUPTABDN                    40
     CSR         *IN40     IFEQ *OFF
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN29
     CSR                   MOVE *ON       *IN09
     CSR                   MOVE *BLANKS   ERRMSG
     CSR                   MOVELW1DYAV    ERRMSG
     CSR                   EXSR ZM0124
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCKTM   Check start times to verify validity.
     C*
     CSR         ZZCKTM    BEGSR
     CSR                   Z-ADDDAYNBR    D
     C*
     C*    Validate start hour.
     C*
     CSR         WKHOUR    IFLT 00
     CSR         WKHOUR    ORGT 23
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE *BLANKS   ERRMSG
413aDC**         DAYNM,D   CAT  DESC,1:1  ERFLD
413aMCSR                   MOVELDESC,1    ERFLD
     CSR                   MOVEL'00'      ERFROM
     CSR                   MOVEL'23'      ERTO
     CSR                   EXSR ZM0112
     CSR                   ENDIF
     C*
     C*    Validate start minute.
     C*
     CSR         WKMIN     IFLT 00
     CSR         WKMIN     ORGT 59
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE *BLANKS   ERRMSG
413aDC**         DAYNM,D   CAT  DESC,2:1  ERFLD
413aMCSR                   MOVELDESC,2    ERFLD
     CSR                   MOVEL'00'      ERFROM
     CSR                   MOVEL'59'      ERTO
     CSR                   EXSR ZM0112
     CSR                   ENDIF
413aDC**
413aDC**   Validate start second.
413aDC**
413aDC**         WKSEC     IFLT 00
413aDC**         WKSEC     ORGT 59
413aDC**                   MOVE '1'       ERROR
413aDC**                   MOVE *BLANKS   ERRMSG
413aDC**         DAYNM,D   CAT  DESC,3:1  ERFLD
413aDC**                   MOVEL'00'      ERFROM
413aDC**                   MOVEL'59'      ERTO
413aDC**                   EXSR ZM0112
413aDC**                   ENDIF
     C*
     C*    Seton RI ind and PC ind for field in error.
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   SELEC
     CSR         DAYNBR    WHEQ 1
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE *ON       *IN21
     CSR         DAYNBR    WHEQ 2
     CSR                   MOVE *ON       *IN02
     CSR                   MOVE *ON       *IN22
     CSR         DAYNBR    WHEQ 3
     CSR                   MOVE *ON       *IN03
     CSR                   MOVE *ON       *IN23
     CSR         DAYNBR    WHEQ 4
     CSR                   MOVE *ON       *IN04
     CSR                   MOVE *ON       *IN24
     CSR         DAYNBR    WHEQ 5
     CSR                   MOVE *ON       *IN05
     CSR                   MOVE *ON       *IN25
     CSR         DAYNBR    WHEQ 6
     CSR                   MOVE *ON       *IN06
     CSR                   MOVE *ON       *IN26
     CSR         DAYNBR    WHEQ 7
     CSR                   MOVE *ON       *IN07
     CSR                   MOVE *ON       *IN27
     CSR                   ENDSL
     CSR                   ENDIF
     CSR         ENDCKT    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     C*
     C*  Command keys to check BEFORE verification and update.
     C*
     CSR         CMDCHK    IFEQ '*BEFORE '
     CSR                   END
     C*
     C*  Command keys to check AFTER verification and update.
     C*
     CSR         CMDCHK    IFEQ '*AFTER  '
     CSR                   END
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDFT1   Get default values for add.
     C*
     CSR         ZZDFT1    BEGSR
     CSR                   MOVE *ZEROS    W1STR1
413aDC**                   MOVE *ZEROS    W1STR2
413aDC**                   MOVE *ZEROS    W1STR3
413aDC**                   MOVE *ZEROS    W1STR4
413aDC**                   MOVE *ZEROS    W1STR5
413aDC**                   MOVE *ZEROS    W1STR6
413aDC**                   MOVE *ZEROS    W1STR7
     CSR                   MOVE *ZEROS    W1WKAV
     CSR                   MOVE *BLANKS   W1DYAV
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
413aDC**                   MOVE OPSTR1    W1STR1
413aMCSR                   MOVELOPSTR1    W1STR1
413aDC**                   MOVE OPSTR2    W1STR2
413aDC**                   MOVE OPSTR3    W1STR3
413aDC**                   MOVE OPSTR4    W1STR4
413aDC**                   MOVE OPSTR5    W1STR5
413aDC**                   MOVE OPSTR6    W1STR6
413aDC**                   MOVE OPSTR7    W1STR7
     CSR                   MOVE OPWKAV    W1WKAV
     CSR                   MOVE OPDYAV    W1DYAV
     CSR         OPDYAV    LOKUPTABDNR    TABDN          40
     CSR         *IN40     IFEQ *ON
     CSR                   MOVE TABDN     W1DYAV
     CSR                   ELSE
     CSR                   MOVE *BLANKS   W1DYAV
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZGET1   Get record(s) for screen 1.
     C*
     CSR         ZZGET1    BEGSR
     C*
     CSR                   Z-ADD$PWHSE    OPWHSE
     CSR                   MOVE '*DAYEND 'OPCODE
     C*
     CSR                   MOVE *OFF      TMPFLG  1
     C*
     CSR         BEGGT1    TAG
     C*
     C*   Get record from main file.
     C*
     CSR         $PCMD     IFEQ '*VIEW   '
     CSR         FILEKY    CHAINRECORD              N7978
     CSR                   ELSE
     CSR         FILEKY    CHAINRECORD               7978
     CSR                   ENDIF
     C*
     C*  Record locked.
     C*
     CSR         *IN78     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE '*PGMQ   '$PRTN
     CSR                   EXSR ZM74
     CSR                   GOTO ENDGT1
     CSR                   END
     C*
     C*  Record not found - Create it.
     C*
     CSR         *IN79     IFEQ *ON
     CSR         TMPFLG    IFEQ *OFF
     CSR                   MOVE *ON       TMPFLG
     CSR                   MOVE *ZEROS    OPSTR1
     CSR                   MOVE *ZEROS    OPSTR2
     CSR                   MOVE *ZEROS    OPSTR3
     CSR                   MOVE *ZEROS    OPSTR4
     CSR                   MOVE *ZEROS    OPSTR5
     CSR                   MOVE *ZEROS    OPSTR6
     CSR                   MOVE *ZEROS    OPSTR7
     CSR                   MOVE *ZEROS    OPWEEK
     CSR                   MOVE *ZEROS    OPLTDT
     CSR                   MOVE *ZEROS    OPLTTM
     CSR                   MOVE *ZEROS    OPLODT
     CSR                   MOVE *ZEROS    OPLOTM
     CSR                   MOVE *ZEROS    OPWKAV
     CSR                   MOVE *ZEROS    OPDYAV
     CSR                   WRITERECORD
     CSR                   GOTO BEGGT1
     CSR                   ELSE
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE '*PGMQ   '$PRTN
     CSR                   EXSR ZM73
     CSR                   GOTO ENDGT1
     CSR                   END
     CSR                   END
     C*
     CSR         ENDGT1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define key for main file.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           OPWHSE
     CSR                   KFLD           OPCODE
413aAC*
413aAC*  Fool compiler
413aAC*
413aACSR                   MOVE DAYNM,1   ERFLD
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     C*   Get and verify warehouse description.
     C*
     CSR                   Z-ADD$PWHSE    WHCODE
     CSR         WHCODE    IFEQ 0
     CSR                   MOVE *BLANKS   WHDESC
     CSR         'All ware'CAT  'houses':0WHDESC
     CSR                   ELSE
     CSR                   MOVE '*VERIFY '$LCMD
     CSR                   MOVE $PPRG     $LPRG
     CSR                   Z-ADDWHCODE    $LWHSE
     CSR                   CALL 'WH900'
     CSR                   PARM           $LPARM
     CSR         $LRTN     IFEQ '*ERROR  '
     CSR         $LRTN     OREQ '*PGMQ   '
     CSR                   MOVE '1'       ERROR
     CSR                   MOVE $LRTN     $PRTN
     CSR                   MOVE $LERM     $PERM
     CSR                   GOTO ENDIN2
     CSR                   ELSE
     CSR                   MOVEL$LERM     WHDESC
     CSR                   END
     CSR                   END
     C*
     CSR                   MOVE *ZEROS    DAYNBR  10
     CSR         *LIKE     DEFN DAYNBR    D
     CSR                   MOVE *ZEROS    WKDS
     C*
     C*   Select first screen to display.
     C*
     CSR                   MOVE #PROG     $LPRG
     CSR                   EXSR SCR01I
     CSR         ENDIN2    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE '0'       ERROR
     C*
     C*  Move screen fields to record fields
     C*
413aDC**                   MOVE W1STR1    OPSTR1
413aDC**                   MOVE W1STR2    OPSTR2
413aDC**                   MOVE W1STR3    OPSTR3
413aDC**                   MOVE W1STR4    OPSTR4
413aDC**                   MOVE W1STR5    OPSTR5
413aDC**                   MOVE W1STR6    OPSTR6
413aDC**                   MOVE W1STR7    OPSTR7
413aMCSR         100       MULT W1STR1    OPSTR1
413aMCSR                   Z-ADDOPSTR1    OPSTR2
413aMCSR                   Z-ADDOPSTR1    OPSTR3
413aMCSR                   Z-ADDOPSTR1    OPSTR4
413aMCSR                   Z-ADDOPSTR1    OPSTR5
413aMCSR                   Z-ADDOPSTR1    OPSTR6
413aMCSR                   Z-ADDOPSTR1    OPSTR7
     CSR                   MOVE W1WKAV    OPWKAV
     CSR         W1DYAV    LOKUPTABDN     TABDNR         40
     CSR         *IN40     IFEQ *ON
     CSR                   Z-ADDTABDNR    OPDYAV
     CSR                   ELSE
     CSR                   MOVE *ZEROS    OPDYAV
     CSR                   ENDIF
     C*
     C*   Update record (it was read and locked in ZZGET1)
     C*
     CSR                   UPDATRECORD                 79
     CSR         *IN79     IFEQ '1'
     CSR                   MOVE '1'       ERROR
     CSR                   EXSR ZM72
     CSR                   GOTO ENDUPD
     CSR                   END
     C*
     CSR         ENDUPD    ENDSR
     C*
     O*----------------------------------------------------------------*********
     O*
     O*  COMPILE TIME TABLES
     O*
**
Start hour
Start minute
Start second
Number of Weeks to Average
**
Sunday
Monday
Tuesday
Wednesday
Thursday
Friday
Saturday
**
SUN1MON2TUE3WED4THU5FRI6SAT7
