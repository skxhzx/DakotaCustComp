     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT765     Remove Replenish/Letdown exceptions - Post
     F*  23 April, 1997
     F*  Dave Sommerville
     F*  Roxanne Harrison
     F*
     F*  Revisions
     F*
414aAF*    07/06/00  MLB  4.14a
     F*      - Re-compiled. Changes to ITMEXCP.
     F*
     F*----------------------------------------------------------------
     F* Indicators
     F*
     F*  79        - EOF for ITMEXCP
     F*
     F*----------------------------------------------------------------
     F* File Specs
     F*
     FITMEXCP UP  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    DESC    1   1 50
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                    P   1   30ERRECS
     I                                        4  48 ERDESC
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PPRG   - Program to send message back to.
     C*      $PWHSE  - Warehouse
     C*      $PDATE  - Generated date
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PPRG  10
     C                     PARM           $PWHSE  3
     C                     PARM           $PDATE  8
     C                     MOVE $PWHSE    $KWHSE  30
     C                     MOVE $PDATE    $KDATE  80
     C*----------------------------------------------------------------
     C* Main line
     C*
     C*  Although the following conditions are tested by open query
     C*  in the preceding CL program, this is tested here again for
     C*  additional safety.
     C*
     C           EXWHSE    IFNE $KWHSE
     C           EXGDTE    ORGE $KDATE
414aAC           EXESTA    ORNE '4 '
     C                     GOTO BOTTOM
     C                     ENDIF
     C*
     C*  Delete exported item adjustments records.
     C*
     C                     ADD  1         ERRECS
     C                     DELETEXREC                  79
     C*
     C           BOTTOM    TAG
     C*
     CLR                   EXSR ZM0750
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   Z-ADD0         ERRECS
     CSR                   MOVELDESC,1    ERDESC
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*     PIR0750  Records removed.
     C*
     CSR         ZM0750    BEGSR
     CSR                   MOVE 'PIR0750' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
**
Replenish/Letdown exceptions
