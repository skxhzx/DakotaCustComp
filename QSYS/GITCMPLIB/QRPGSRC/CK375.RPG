     F*----------------------------------------------------------------
     F*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  CK375     Manager Ship List - Print    CBI
     F*  30 December 2003
     F*  Max Blacknik
     F*
     F*----------------------------------------------------------------
     F*  Revisions:
     F*
CBI AF*    12/30/03  MLB  CBI
     F*      - Created.
     F*
520 AF*    02/05/09  CWM/MLB 5.20  01/28  02/02
520 AF*      - Enh: Added support for new extended stop number.
     F*        Retrieve stop # in subroutine ZZFGTS and pgm GETSTOP
     F*
650aAF*    05/29/13  RBD  6.50a
     F*      - Enh: Print new stop/drop display instead of 5 dig stop.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions
     F*
CBI1AF*    05/20/08  CBI-JGD
     F*      Add logic to provide the name of the selector
     F*      that picked the orders within the route/trans
     F*      being processed.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FCK375   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FITEMMSC IF  E           K        DISK
     FITEMDSC IF  E           K        DISK
     FRTEHED  IF  E           K        DISK
     FPLTSUM2 IF  E           K        DISK
     FORDH    IF  E           K        DISK
CBI1AFPIRTRAN IF  E           K        DISK
CBI1AFPIRUSER1IF  E           K        DISK
     FCK375PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RECK        4  7 0
     E                    LVLK        4  7 0
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              WBWHDPL4
     I                                              WBSTOPL3
     I                                              WBPAL#L2
     I*----------------------------------------------------------------
     I* Convert date work fields
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I              'CVTDTE'              C         @CVTDT
CBI1 I              'Selector:'           C         @SELD
     I*----------------------------------------------------------------
     I*  Pallet id data structure
     I*
     I*    DSPLID  -  Pallet id.
     I*    DSPLXX  -  Unused.
     I*    DSPLGN  -  Generation.
     I*    DSPLRW  -  Pallet row.
     I*    DSPLCL  -  Pallet column.
     I*    DSPLSQ  -  Sequence number.
     I*               Equal to zero     - Only one pallet in this position.
     I*               Greater than zero - Multiple pallets in this position.
     I*
     I*
     IPLTID       DS
     I I            0                         1   70DSPLID
     I                                        1   20DSPLXX
     I                                        3   30DSPLGN
     I                                        4   50DSPLRW
     I                                        6   60DSPLCL
     I                                        7   70DSPLSQ
     I*
     I*----------------------------------------------------------------
     I            DS
     I                                        1  20 DSMISC
     I                                        1   8 DSSDTE
     I                                        9  11 DSSPCL
     I                                       12  12 DSRPCK
     I                                       13  14 DSDOOR
     I                                       15  15 DSMEAT
     I                                       18  18 DSCLST
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PRTID  5
     C                     PARM           $PORD#  7
     C                     PARM           $RTNCD  8
     C*
     C*  Report Heading
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL4     OREQ *ON
     C                     EXSR ZRHEAD
     C           *INL2     IFEQ *OFF
     C                     WRITESKIP1                  90
     C                     WRITEHEAD2                  90
     C                     WRITEHEAD3                  90
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 2 break - PALLET
     C*
     C           *INL2     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     EXSR ZRFILL
     C           *INL3     IFEQ *ON
     C           WBTRN#    ORNE SVTRN#
     C           WBWHDP    ORNE SVWHDP
     C*
     C           *IN90     IFEQ *ON
     C                     EXSR ZRHEAD
     C                     ENDIF
     C           *IN90     IFEQ *OFF
     C                     WRITESKIP1                  90
     C                     ENDIF
     C           *IN90     IFEQ *OFF
     C                     WRITEHEAD2                  90
     C                     ENDIF
     C           *IN90     IFEQ *OFF
     C                     WRITEHEAD3                  90
     C                     ENDIF
     C*
     C*    Page overflow.
     C           *IN90     IFEQ *ON
     C                     EXSR ZRHEAD
     C                     WRITESKIP1                  90
     C                     WRITEHEAD2                  90
     C                     WRITEHEAD3                  90
     C                     ENDIF
     C*
     C                     Z-ADDWBTRN#    SVTRN#
     C                     MOVE WBWHDP    SVWHDP
     C                     ELSE
     C*
     C           *IN90     IFEQ *OFF
     C                     WRITESKIP1                  90
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL2     OREQ *ON
     C                     MOVE *OFF      FIRST
     C*                    WRITERPHEAD
     C*                    WRITECOLUMNS
     C                     ENDIF
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C                     MOVELWBITEM    W1ITEM
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   W1PDSC 15
     C                     ELSE
     C                     MOVE ITPDSC    W1PDSC
     C                     ENDIF
     C*
     C           ITKEY     CHAINITEMDSC              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   W1DESC 60
     C                     ELSE
     C           IXDSC1    CAT  IXDSC2    W1DESC
     C                     ENDIF
     C           W1DESC    IFEQ *BLANKS
     C                     MOVELITDESC    W1DESC
     C                     ENDIF
     C*
     C           ITKEY     CHAINITEMMSC              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   W1BRND 10
     C                     ELSE
     C                     MOVE IMBRND    W1BRND
     C                     ENDIF
     C*
     C*    Page overflow.
     C           *IN90     IFEQ *ON
     C                     EXSR ZRHEAD
     C                     WRITESKIP1                  90
     C                     WRITEHEAD2                  90
     C                     WRITEHEAD3                  90
     C                     ENDIF
     C*
     C                     WRITEDETAIL                 90
     C*
     C*  Increment record counters
     C*
     C                     MOVE *ON       PRINT   1
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* New Pallet
     C*
     CL2                   Z-ADD1         L
     CL2                   EXSR ZRROLL
     C*
     CL2         RECK,L    IFGT 0
     C*L2                   WRITESKIP1
     CL2                   ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Report Totals
     C*
     CLR                   Z-ADD3         L
     CLR                   EXSR ZRROLL
     C*
     CLR         PRINT     IFEQ *ON
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         *LIKE     DEFN WBTRN#    SVTRN#
     CSR         *LIKE     DEFN WBWHDP    SVWHDP
     C*
     CSR         ITKEY     KLIST
     CSR                   KFLD           WBWHSE
     CSR                   KFLD           WBITEM
     C*
     CSR         ORKEY     KLIST
     CSR                   KFLD           WBWHSE
     CSR                   KFLD           WBORD
     C*
     CSR         PLKEY     KLIST
     CSR                   KFLD           WBWHSE
     CSR                   KFLD           WBRTE
     CSR                   KFLD           KYTYPE
     CSR                   KFLD           DSPLGN
     CSR                   KFLD           DSPLRW
     CSR                   KFLD           DSPLCL
     C*
     C*
     CSR         RTKEY     KLIST
     CSR                   KFLD           WBWHSE
     CSR                   KFLD           WBRTE
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get warehouse description.
     C*
     CSR                   MOVE *BLANKS   WHSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   MOVE $PWHSE    WBWHSE
     CSR         WBWHSE    CHAINWAREHS               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELWHDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     C*
     C*   Center warehouse description.
     C*
     CSR                   Z-ADD30        $PLEN
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     WHSE
     C*
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'CK375PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     CSR                   MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   MOVE *ON       FIRST   1
     CSR                   MOVE '*CURMDY '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD6O    RDATE
     CSR                   TIME           RTIME
     C*
     CSR                   Z-ADD3         L       10
     CSR                   Z-ADD0         N       30
     CSR                   EXSR ZRZERO
     CSR         L         IFGT 3
     CSR                   WRITEHEAD2
     CSR                   WRITEHEAD3
     CSR                   ENDIF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFILL    BEGSR
     C*
     C*  Get info to fill HEAD2 fields.
     C*
     CSR                   MOVE WBWHDP    W1WHDP
     CSR                   Z-ADDWBTRN#    W1TRN#
CBI1AC                     MOVE *BLANKS   W1SELN
CBI1AC*
CBI1AC           WBTRN#    CHAINPIRTRAN              79
CBI1AC           *IN79     IFEQ *OFF
CBI1AC           PTEMP#    CHAINPIRUSER1             79
CBI1AC           *IN79     IFEQ *OFF
CBI1AC                     EXSR ZZFNAM
CBI1AC                     ENDIF
CBI1AC                     ENDIF
     C*
     CSR         RTKEY     CHAINRTEHED               79
     CSR         *IN79     IFEQ *ON
     CSR                   MOVE *BLANKS   W1RTE
     CSR                   ELSE
     CSR                   MOVE RHRTE     W1RTE
     CSR                   ENDIF
     C*
520 AC                     EXSR ZZFGTS
520 AC           $SSTOP    IFGT 0
650aDC*                    Z-ADD$SSTOP    W1STOP
650aMC                     MOVE $SSD      W1STOP
520 AC                     ELSE
650aDC*R                   Z-ADDWBSTOP    W1STOP
650aMCSR                   MOVE WBSTOP    W1STOP
520 AC                     ENDIF
     C*
     CSR                   MOVE 'O'       KYTYPE  1
     CSR                   Z-ADDWBPAL#    DSPLID
     CSR         PLKEY     CHAINPLTSUM2              79
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADD0         W1PLT
     CSR                   ELSE
     CSR                   Z-ADDPSPAL#    W1PLT
     CSR                   ENDIF
     C*
     CSR         ORKEY     CHAINORDH                 79
     CSR         *IN79     IFEQ *ON
     CSR                   Z-ADD0         W1CUST
     CSR                   MOVEL*BLANKS   W1CNAM
     CSR                   Z-ADD0         W1DOOR
     CSR                   ELSE
     CSR                   Z-ADDOHCUST    W1CUST
     CSR                   MOVELOHCNAM    W1CNAM
     CSR                   MOVELOHMISC    DSMISC
     CSR                   MOVE DSDOOR    W1DOOR
CBI1AC           OHTYPE    IFNE 'W'
CBI1AC                     MOVE *BLANKS   W1SELN
CBI1AC                     MOVE *BLANKS   W1SELD
CBI1AC                     ENDIF
     CSR                   ENDIF
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRHEAD    BEGSR
     C*
     CSR         *INL2     IFEQ *OFF
     CSR                   MOVE *ON       *IN91
     CSR                   ENDIF
     C*
     CSR                   MOVE *OFF      *IN90
     C*
     CSR                   WRITEHEAD1                  90
     CSR                   WRITECOLUMNS                90
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRROLL    BEGSR
     C*
     CSR         *LIKE     DEFN L         M
     CSR         L         ADD  1         M
     C*
     CSR                   ADD  1         LVLK,M
     CSR                   ADD  RECK,L    RECK,M
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     CSR         ZRZERO    BEGSR
     C*
     CSR                   Z-ADD0         LVLK,L
     CSR                   Z-ADD0         RECK,L
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     C*
     C*  ZZFNAM   Format loader name.  (Routine added under CBI1 Mod.)
     C*
     C*----------------------------------------------------------------
CBI1ACSR         ZZFNAM    BEGSR
     C*
     C                     MOVE *BLANKS   W1SELN
     C                     MOVE @SELD     W1SELD
     C                     MOVE *BLANKS   FNAME
     C                     MOVE *BLANKS   LNAME
     C*
     C           ' '       SCAN USFNAM    P1      20
     C           P1        SUBSTUSFNAM:1  FNAME  12
     C           ' '       SCAN USLNAM    P2      20
     C           P2        SUBSTUSLNAM:1  LNAME  16
     C           FNAME     CAT  LNAME:1   W1SELN
     C*
      *
     C                     ENDSR
520 AC*----------------------------------------------------------------
520 AC*
520 AC*  ZZFGTS   Get Host Stop Number
520 AC*
520 AC           ZZFGTS    BEGSR
520 AC*
520 AC*    Get Stop From RTESTP
520 AC*
520 AC                     CALL 'GETSTOP'
520 MC                     PARM '*ORDSTP '$SCMD   8
520 MC                     PARM WBWHSE    $SWHSE  30
520 MC                     PARM WBRTE     $SRTID  5
520 MC                     PARM WBORD     $SORD   70
520 MC                     PARM 0         $SDSTP  20
520 MC                     PARM           $SSTOP  50
650aAC                     PARM           $SDROP  3
650aAC                     PARM           $SEXTD 30
650aAC                     PARM           $SSD    9
520 AC*
520 AC                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
