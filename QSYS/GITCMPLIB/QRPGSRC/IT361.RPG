     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT361     Tie/High Report
     F*  Hemant Kapadia
     F*  26 March 1993
     F*
     F*  Revisions
     F*
     F*    04/19/96  DAS  2.12
     F*      - Recompiled for new SLOTDEF layout.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: City Wholesale
CWa AF*    04/03/08  RH   CWa
     F*      - Revised to bypass item with activity = 'DC'
     *----------------------------------------------------------------
     *  Client Custom Revisions: A&D Foods
     *
ADFa *    12/27/16  RH   ADFa
     *      -  Enh: Revised program to only print items with tie/hi
     *
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IP  E           K        DISK
     FITEMDEF IF  E           K        DISK
     FSLOTDEF IF  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FIT361PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
520 AI*----------------------------------------------------------------
520 AI*  Customer id
520 AI*
520 AI              'GETCLIENT'           C         @GETCL
520 AI*
CWa A /COPY QCOPYSRC,IL#CITYWHO
ADF A /COPY QCOPYSRC,IL#ADF
     I*----------------------------------------------------------------
     IITREC
     I                                              ITSTYPL1
     I                                              ITWHDPL1
     C*----------------------------------------------------------------
     C*
     C*  Key fields
     C*
     C*  Partial key for ITEMDEF file
     C           ITDKEY    KLIST
     C                     KFLD           ITWHSE
     C                     KFLD           ITITEM
     C*
     C           SLDKEY    KLIST
     C                     KFLD           ITWHSE
     C                     KFLD           IDSDEF
     C*
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C           ITSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           ITSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           TYPE      CAT  '/':0     P#DESC
     C           P#DESC    CAT  ITWHDP:0  P#DESC
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type/Department
     C*
     C           *INL1     IFEQ '1'
     C                     Z-ADD1         L
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C*  Write detail line
     C*
     C           ITDKEY    SETLLITEMDEF              79
     C                     MOVE *BLANKS   DASH
     C                     MOVE *BLANKS   SLASH
     C                     Z-ADD0         COUNT
     C                     SETON                     61
     C                     MOVE *IN79     EOF     1
CWa A * For City Wholesale, don't print if item is activity DC, Discontinued
CWa AC           CLIENT    IFEQ 'CITYWHO'
CWa  C           ITACTV    IFEQ 'DC'
CWa  C                     MOVE *ON       EOF
CWa  C                     ENDIF
CWa  C                     ENDIF
     C*
     C           EOF       DOWEQ*OFF
     C           ITDKEY    READEITEMDEF                  79
     C                     MOVE *IN79     EOF
     C           *IN79     IFEQ *OFF
     C           SLDKEY    CHAINSLOTDEF              78
     C           *IN78     IFEQ *ON
     C                     MOVE *BLANKS   SDDESC
     C                     ENDIF
     C                     ADD  1         COUNT
     C*
     C                     SELEC
     C           IDUM      WHEQ 'N'
     C                     MOVE ITUM1     UM
     C           IDUM      WHEQ '2'
     C                     MOVE ITUM2     UM
     C           IDUM      WHEQ '3'
     C                     MOVE ITUM3     UM
     C                     ENDSL
     C*
     C*  Do not print item info., if more than 1 item found
     C*
     C           COUNT     IFGT 1
     C                     SETOF                     61
     C                     ENDIF
     C                     MOVE '-'       DASH
     C                     MOVE '/'       SLASH
     C                     WRITEDETAIL1
     C*
     C                     ENDIF
     C                     ENDDO
     C*
     C* If no records found in ITEMDEF, just print the item info.
     C*
     C           COUNT     IFEQ 0
     C                     MOVE *BLANKS   IDSDEF
     C                     MOVE *BLANKS   SDDESC
     C                     MOVE *BLANKS   DASH
     C                     MOVE *BLANKS   SLASH
     C                     Z-ADD0         IDTIE
     C                     Z-ADD0         IDHIGH
     C                     MOVE *BLANKS   UM
ADFaA * For A&D, don't print if item has not Tie/Hi                    inued
ADFaAC           CLIENT    IFEQ AANDDF
ADFaAC                     GOTO JUMPD
ADFaAC                     ENDIF
     C                     WRITEDETAIL1
     C                     ENDIF
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C           JUMPD     TAG
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type/Department totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   END
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
520 AC*
520 AC* Get client id.
520 AC*
520 AC                     CALL @GETCL
520 AC                     PARM           CLIENT 10
520 AC*
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'IT361PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD0         COUNT   30
     CSR                   Z-ADD2         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           ITWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
