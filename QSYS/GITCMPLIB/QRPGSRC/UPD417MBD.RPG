     F*----------------------------------------------------------------
     F*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  UPD417MBD  Add records for *VIRTUAL option by warehouse.
     F*  30 May 2003
     F*  Max Blacknik
     F*
     F*  Revisions
417 AF*    05/30/03  MLB  4.17
     F*      - Add *VIRTUAL option record to OPTIONS file.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FWAREHS  IF  E           K        DISK
     FOPTIONS UF  E           K        DISK                      A
     I*----------------------------------------------------------------
     I*
417 AI*----------------------------------------------------------------
417 AI*  *VIRTUAL  -  Virtual Slot options
417 AI*----------------------------------------------------------------
417 AI*
417 AI* Fields
417 AI*
417 AI*    OPVALL  -  Always create Virtual slot. (Y/N)
417 AI*    OPVPCK  -  Allow Virtual pick slots to be created.  (Y/N)
417 AI*    OPVSLT  -  Create Virtual slot for item when base slot and
417 AI*               corresponding Virtual slot(s) item(s) are different.
417 AI*               Always Yes.
417 AI*    OPVEXP  -  Create Virtual slot when item exists and
417 AI*               expiration date is different.  (Y/N)
417 AI*    OPVEND  -  Create Virtual slot when item exists and
417 AI*               entered date is different.  (Y/N)
417 AI*    OPVSAM  -  Merge product when entered/expiration dates
417 AI*               are the same.  (Y/N)
417 AI*    OPVEDF  -  Merge product when entered dates are different and
417 AI*               expiration dates are the same.  (Y/N)
417 AI*
417 AI* Data structure
417 AI*
417 AIOPDATA      DS
417 AI                                        1   1 OPVALL
417 AI                                        2   2 OPVPCK
417 AI                                        3   3 OPVSLT
417 AI                                        4   4 OPVEXP
417 AI                                        5   5 OPVEND
417 AI                                        6   6 OPVSAM
417 AI                                        7   7 OPVEDF
     I                                      117 117 OPTEND
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *OFF      *IN79
     C           *LIKE     DEFN OPCODE    KYCODE
     C           OPKEY     KLIST
     C                     KFLD           WHCODE
     C                     KFLD           KYCODE
     C                     MOVE '*VIRTUAL'KYCODE
     C*
     C           *LOVAL    SETLLWAREHS
     C           *IN79     DOWEQ*OFF
     C                     READ WAREHS                   79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE                           Exit Loop
     C*
     C                     OTHER
     C*
     C           OPKEY     CHAINOPTIONS              78
     C                     SELEC
     C           *IN78     WHEQ *ON
     C                     MOVE WHCODE    OPWHSE
     C                     MOVE KYCODE    OPCODE
417 AC                     MOVE 'N'       OPVPCK
417 AC                     MOVE 'N'       OPVALL
417 AC                     MOVE 'Y'       OPVSLT
417 AC                     MOVE 'N'       OPVEXP
417 AC                     MOVE 'N'       OPVEND
417 AC                     MOVE 'Y'       OPVSAM
417 AC                     MOVE 'Y'       OPVEDF
     C                     WRITEOPREC
     C*
     C                     ENDSL
     C                     ENDSL
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
     C*
