     F*----------------------------------------------------------------
     F*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  EX114     Clear Item Replenishment Exceptions - Clear
     F*  30 June 2000
     F*  Max Blacknik
     F*
     F*  SK114 - 1.00
     F*
     F*----------------------------------------------------------------
     F*  Revisions
416aAF*    12/12/01  RH   4.16a
     F*      - Recompiled, ITMEXCP file changed.
     F*
500 AF*    03/11/06  MLB  5.00
     F*      - Fix: Revised program to do READE instead of CHAIN
     F*        for instances when multiple exceptions are created for
     F*        the same item with same date/time stamp.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Chain indicator
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FITMEXCP UF  E           K        DISK
     F            EXREC                             KRENAMERECORD
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    MSG     1   1 50
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHSE
     I                                       22  36 $PITEM
     I                                       37  440$PGDTE
     I                                       45  500$PGTIM
     I                                       51  55 $PWHDP
     I                                       56  56 $PSTYP
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   Not used
     C*      $PPRG   Program to send messages back to.
     C*      $PUKY   Contains unique key for delete.
     C*
     C*    Returned Parameters
     C*      $PRTN   blank    - No problem encountered.
     C*              *ERROR   - Error occured. Message in $PERM
     C*              *PGMQ    - Error occured. Message sent to pgmq.
     C*              *OK      - Record deleted successfully.
     C*                         Message sent to pgmq.
     C*      $PERM   Error message or pointer to message in pgmq.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C*
500 AC           FILEKY    SETLLRECORD
500 AC           FOREVR    DOWEQFOREVR
500 DC*          FILEKY    CHAINRECORD               79
500 MC           FILEKY    READERECORD                   79
500 AC                     SELEC
500 AC           *IN79     WHEQ '1'
500 AC                     LEAVE
500 AC*
500 DC*          *IN79     IFEQ *OFF
500 MC           *IN79     WHEQ *OFF
500 AC           EXESTA    ANDEQ'1'
     C                     EXSR ZZUPD
500 AC                     ENDSL
500 AC                     ENDDO
500 AC*
     C           ERROR     IFEQ *OFF
     C                     EXSR ZZOK
500 DC*                    ENDIF
     C                     ELSE
     C                     EXSR ZM75
     C                     MOVE '*PGMQ   '$PRTN
     C                     ENDIF
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*   Call user added initialization routine.
     C*
     C*R                   EXSR ZZINZ
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*     PIR7975  Error while deleting record.
     C*
     CSR         ZM75      BEGSR
     CSR                   MOVE 'PIR7975' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD,1
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*     IV35421  Exception cleared.
     C*
     CSR         ZM21      BEGSR
     CSR                   MOVE 'IV35421' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   EXSR ZMSMSG
     CSR                   MOVE #MSGK     $PMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZDEL    Delete corresponding records.
     C*
     CSR         ZZDEL     BEGSR
     CSR                   MOVE *OFF      ERROR   1
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for main file.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PWHDP
     CSR                   KFLD           $PSTYP
     CSR                   KFLD           $PITEM
     CSR                   KFLD           $PGDTE
     CSR                   KFLD           $PGTIM
     C*
     C*  Convert today's date into century format.
     C*
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TODAY   80
     CSR                   TIME           CURTIM  60
     C*
     CSR                   MOVE #PROG     $CVPRG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZOK     Send back "successful" message.
     C*
     CSR         ZZOK      BEGSR
     CSR                   MOVE *BLANKS   $MD
     CSR                   MOVEAMSG,1     $MD,1
     CSR                   MOVEAEXFDIS    $MD,6
     CSR                   EXSR ZM21
     CSR                   MOVE '*OK     '$PRTN
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE *OFF      ERROR   1
     C*
     C*
     C*   Update record.
     C*
     CSR                   Z-ADDTODAY     EXCDTE
     CSR                   Z-ADDCURTIM    EXCTIM
     CSR                   MOVE #USER     EXUSER
     CSR                   MOVE '4 '      EXESTA
     CSR                   UPDATRECORD                 99
     CSR         *IN99     IFEQ *ON
     CSR                   MOVE *ON       ERROR
     CSR                   ENDIF
     CSR         ENDUP     ENDSR
     O*----------------------------------------------------------------
     O*
     O*  Compile time tables
     O*
**
Slot
