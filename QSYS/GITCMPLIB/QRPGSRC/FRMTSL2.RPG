     F*----------------------------------------------------------------
     F*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FRMTSL    Format whs/dpt/asl/loc/lvl/hnd/dsp/side
     F*  23 May 2000
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
415 AF*   04/19/01  DAS  4.15
     F*     - Defined numeric parameters as alpha.
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A2          2  1
     E                    A3          3  1
     E                    A38        38  1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse
     C*      $PWHDP  Department
     C*      $PAISL  Aisle
     C*      $PLOC   Location
     C*      $PRLVL  Level
     C*      $PHAND  Handstack indicator
     C*      $PDISP  Slot display
     C*      $PSIDE  Side
     C*
     C*    Returned Parameters
     C*      $PSTR   Formated string.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
415 MC                     PARM           $PWHSE  3
     C                     PARM           $PWHDP  5
     C                     PARM           $PAISL  3
415 MC                     PARM           $PLOC   3
415 MC                     PARM           $PRLVL  2
     C                     PARM           $PHAND  2
     C                     PARM           $PDISP 12
415 MC                     PARM           $PSIDE  1
     C                     PARM           $PSTR  38
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     Z-ADD0         X       20
     C                     MOVE *BLANKS   $PSTR
     C                     MOVE *BLANKS   A38
     C*
     C*    Warehouse (suppress zeros)
     C*
     C                     MOVE $PWHSE    WORK3A  3
     C                     MOVEAWORK3A    A3
     C                     MOVE *ON       SUPRES  1
     C           1         DO   3         I
     C           SUPRES    IFEQ *OFF
     C                     ADD  1         X
     C                     MOVE A3,I      A38,X
     C                     ELSE
     C           A3,I      IFNE '0'
     C                     ADD  1         X
     C                     MOVE A3,I      A38,X
     C                     MOVE *OFF      SUPRES
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C*
     C*    Aisle (suppress blanks)
     C*
     C                     ADD  1         X
     C                     MOVE '/'       A38,X
     C                     MOVEA$PAISL    A3
     C           1         DO   3         I       20
     C           A3,I      IFNE ' '
     C                     ADD  1         X
     C                     MOVE A3,I      A38,X
     C                     ENDIF
     C                     ENDDO
     C*
     C*    Picking location (do not suppress zeros)
     C*
     C                     ADD  1         X
     C                     MOVE '/'       A38,X
     C                     ADD  1         X
     C                     MOVE $PLOC     WORK3A  3
     C                     MOVEAWORK3A    A38,X
     C                     ADD  3         X
     C*
     C*    Rack level (suppress zeros)
     C*
     C                     MOVE '/'       A38,X
     C                     MOVE $PRLVL    WORK2A  2
     C                     MOVEAWORK2A    A2
     C                     MOVE *ON       SUPRES  1
     C           1         DO   2         I
     C           SUPRES    IFEQ *OFF
     C                     ADD  1         X
     C                     MOVE A2,I      A38,X
     C                     ELSE
     C           A2,I      IFNE '0'
     C                     ADD  1         X
     C                     MOVE A2,I      A38,X
     C                     MOVE *OFF      SUPRES
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C*
     C*    Handstack indicator (suppress blanks)
     C*
     C                     ADD  1         X
     C                     MOVE '/'       A38,X
     C                     MOVEA$PHAND    A2
     C           1         DO   2         I
     C           A2,I      IFNE ' '
     C                     ADD  1         X
     C                     MOVE A2,I      A38,X
     C                     ENDIF
     C                     ENDDO
     C*
     C*    Slot Display
     C*
     C                     ADD  1         X
     C                     MOVE '/'       A38,X
     C                     ADD  1         X
     C                     MOVEA$PDISP    A38,X
     C*
     C*    Side
     C*
     C                     MOVEAA38       $PSTR
     C           $PSTR     CAT  '/':0     $PSTR
     C                     MOVE $PSIDE    WORKA1  1
     C           $PSTR     CAT  WORKA1:0  $PSTR
     C*
     C                     RETRN
