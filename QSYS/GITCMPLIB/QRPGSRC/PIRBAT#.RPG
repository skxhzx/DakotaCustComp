     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PIRBAT#     Get next PIR batch number
     F*  26 February 1993
     F*  Dave Sommerville
     F*
     F*  Revisions:
510aAF*    07/20/06  RH    5.10a
     F*      - FIX: Change READ to READE to make sure we get unique #
     F*----------------------------------------------------------------
     F*
     F*  File specs
     F*
     FORDH1   IF  E           K        DISK
     FBATCTL  IF  E           K        DISK
     C*----------------------------------------------------------------
     C*
     C*  Subprogram parameters
     C*
     C*    Input Parameters
     C*    - None -
     C*
     C*    Returned Parameters
     C*    $NXTNM  Next PIR batch number (guarenteed to be
     C*            unique; not guarenteed to be the next used in
     C*            sequence; 000000 is not used)
     C*
     C*----------------------------------------------------------------
     C* Main Line
     C*
     C           *ENTRY    PLIST
     C                     PARM           $NXTNM  70
     C           *NAMVAR   DEFN PIRBATCH  NEXT#   70
     C           *LOCK     IN   NEXT#
     C*
     C*  Data area contains the next sequence number to use.  If it
     C*  has already been used then keep adding 1 until we find one
     C*  that hasn't been used (we will take the chance that all
     C*  9999999 have not been used).
     C*
     C                     MOVE '0'       *IN79
     C           *IN79     DOUEQ'1'
     C           NEXT#     IFEQ 0
     C                     Z-ADD1         NEXT#
     C                     END
     C                     Z-ADDNEXT#     OHPBAT
     C           FILEKY    SETLLORDH1
510aDC*                    READ ORDH1                    79
510aMC           FILEKY    READEORDH1                    79
     C           *IN79     IFEQ '1'
     C           FILEKY    CHAINBATCTL               79
     C                     END
     C           *IN79     IFEQ '0'
     C                     ADD  1         NEXT#
     C                     END
     C                     END
     C*
     C*  Return the unique sequence number.
     C*
     C                     Z-ADDNEXT#     $NXTNM
     C*
     C*  Update the data area with the next number to be used.
     C*
     C                     ADD  1         NEXT#
     C                     OUT  NEXT#
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define partial key for ORDH1.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           OHPBAT
     CSR                   ENDSR
