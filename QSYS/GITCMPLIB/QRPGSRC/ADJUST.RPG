     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  ADJUST    Right/Left adjust
     F*  08 August 1994
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
416 AF*  04/24/02  DAS/HNK  4.16
     F*    Fix Do loop.  It was truncating last character if 5 chars
     F*    were sent in.
416aAF*    01/13/03  MLB  4.16a
     F*      - Enh: Added new command *LEFTNOZ - Do not remove
     F*        leading zeros.
     F*
417 AF*  10/06/04  MLB  4.17
     F*    Enh: Added 2 new commands: *RTSTRIP to right adjust
     F*    string stripping all blanks. *LTSTRIP to left adjust
     F*    string stripping all blanks.
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A30        30  1
417 AE                    B30        30  1
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCMD   *RIGHT - Right adjust string.
417 AC*              *RTSTRIP - Right adjust string, stripping all blanks.
     C*              *LEFT  - Left adjust string.
416aAC*              *LEFTNOZ - Left adjust string - Keep leading zeros.
417 AC*              *LTSTRIP - Left adjust string, stripping all blanks.
     C*      $PSTR   String to be adjusted.
     C*
     C*    Returned Parameters
     C*      $PSTR   Adjusted string.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PSTR  30
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     SELEC
     C*
     C*  Left adjust string.
     C*
     C           $PCMD     WHEQ '*LEFT   '
     C           ' 0'      CHECK$PSTR     POS     30
     C           POS       IFGT 0
     C           31        SUB  POS       LEN     30
     C           LEN       SUBST$PSTR:POS $PSTR     P
     C                     ENDIF
416aAC*
416aAC*  Left adjust string - Keep leading zeros.
416aAC*
416aAC           $PCMD     WHEQ '*LEFTNOZ'
416aAC           ' '       CHECK$PSTR     POS     30
416aAC           POS       IFGT 0
416aAC           31        SUB  POS       LEN     30
416aAC           LEN       SUBST$PSTR:POS $PSTR     P
416aAC                     ENDIF
     C*
     C*  Right adjust string.
     C*
     C           $PCMD     WHEQ '*RIGHT'
     C                     MOVEA$PSTR     A30
     C                     Z-ADD30        POS
     C           POS       DOUEQ0
416 DC**         A30,POS   ORNE ' '
416 AC           A30,POS   IFNE ' '
416 AC                     LEAVE
416 AC                     ENDIF
     C                     SUB  1         POS
     C                     ENDDO
     C           POS       IFGT 0
     C           30        SUB  POS       BLK     30
     C           ' '       CAT  $PSTR:BLK $PSTR     P
     C                     ENDIF
417 AC*
417 AC*  Left adjust string, stripping all blanks.
417 AC*
417 AC           $PCMD     WHEQ '*LTSTRIP'
417 AC                     MOVEA$PSTR     A30
417 AC                     MOVE *BLANKS   B30
417 AC                     Z-ADD1         POS
417 AC                     Z-ADD1         B       30
417 AC           POS       DOWLE30
417 AC           A30,POS   IFGT ' '
417 AC                     MOVE A30,POS   B30,B
417 AC                     ADD  1         B
417 AC                     ENDIF
417 AC                     ADD  1         POS
417 AC                     ENDDO
417 AC                     MOVEAB30       $PSTR
417 AC*
417 AC*  Right adjust string, stripping all blanks.
417 AC*
417 AC           $PCMD     WHEQ '*RTSTRIP'
417 AC                     MOVEA$PSTR     A30
417 AC                     MOVE *BLANKS   B30
417 AC                     Z-ADD30        POS
417 AC                     Z-ADD30        B       30
417 AC           POS       DOWGE1
417 AC           A30,POS   IFGT ' '
417 AC                     MOVE A30,POS   B30,B
417 AC                     SUB  1         B
417 AC                     ENDIF
417 AC                     SUB  1         POS
417 AC                     ENDDO
417 AC                     MOVEAB30       $PSTR
     C*
     C                     ENDSL
     C*
     C                     RETRN
