     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  RFSCRN   RF Screen Select Menu 8 X 20
     F*  1 July, 1999
     F*  Roxanne Harrison
     F*
     F*  Notes
     F*
     F*    This program allows a user to select the size menu for
     F*     the equipment he will use.
     F*    8 X 20 Screens for hand held scanners
     F*
     F*  Revisions
     F*
415 AF*   06/11/01  DAS  4.15
     F*     - Changed option 4 to call menu RF1620.
     F*     - Changed signoff to option 90 and put on second screen.
     F*     - Changed description of option 3 to 'Handheld 8-line'.
     F*
416 AF*    05/10/02  DAS  4.16
     F*      - Added logic to display user's default warehouse in
     F*        message line.
510 AF*    11/10/06  RH   5.10
     F*      - Enh: Return *FEATURE to CL to display Stand Alone Screens.
     F*
640 AF*    02/02/11  MLB  6.40
     F*      - Enh: Added custom client code support to program.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: Dairyland
     F*
DRY AF*    02/02/11  MLB  DRY   * Temporary Only*
     F*      -  Enh: Revised program to force OPFEAT=2= Full System
     F*         so that all RF menu options will display.
     F*      -  Remove this option once Dairyland is fully live with
     F*         Inbound Dakota. All dept's live.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  21 - 30   Field input error (reverse display)
     F*  81        Rollup
     F*  82        Rolldown
     F*  98        Error on screen (sound alarm)
     F*  99        Universal record indicator (very temporary usage)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FRFSCRNFMCF  E                    WORKSTN
     F                                              KINFDS INFDS
     F                                        MSGK  KSFILE MSGREC
510aAFOPTIONS IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    $MD       128  1
     E                    MNU1    1   5 20
     E                    MNU2    1   5 20
     E                    MNU3    1   5 20
640 AI*----------------------------------------------------------------
640 AI*  Customer id
640 AI*
640 AI              'GETCLIENT'           C         @GETCL
640 AI*
DRY A /COPY QCOPYSRC,IL#DAIRYLA
     I*----------------------------------------------------------------
     I*  Data structure for menu display
     I*
     IDSMENU      DS
     I                                        1  20 WOLIN1
     I                                       21  40 WOLIN2
     I                                       41  60 WOLIN3
     I                                       61  80 WOLIN4
     I                                       81 100 WOLIN5
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1   2 EROPT
     I            DS
     I                                    P   1   20$EC
510 AI*----------------------------------------------------------------
510 AI*  *FEATURE  -  PIR Feature options.
510 AI*----------------------------------------------------------------
510 AI*
510 AI* Fields
510 AI*
510 AI*    OPFEAT  -  PIR Installed feature.
510 AI*                 1=Locator system installed.
510 AI*                 2=Full system installed.
510 AI*
510 AI* Data structure
510 AI*
510 AIOPDAT3      DS
510 AI                                        1   1 OPFEAT
510 AI                                      117 117 OP3END
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Workstation exception data structure
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I                                      370 370 ROWIN
     I                                      371 371 COLIN
     I*----------------------------------------------------------------
     I*  Workstation exception redefinition data structure
     I*
     I            DS
     I                                    B   1   20ROW#
     I                                        2   2 ROW
     I                                    B   3   40COL#
     I                                        4   4 COL
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      None
     C*
     C*    Returned Parameters
     C*
     C*      $POPT     Selected option (0 = Exit)
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $POPT   2
510  C                     PARM           $PFEAT  1
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $POPT
510  C                     MOVE *BLANKS   $PFEAT
     C                     MOVE *ON       *IN97
     C                     MOVE *OFF      *IN90
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      ERROR
     C*
     C                     EXSR ZZINZ2
     C           ERROR     IFEQ *ON
     C                     MOVE 'EOJ'     NXTSCR
     C                     END
     C*
     C           NXTSCR    DOWNE'EOJ'
     C                     EXSR DSPSCR
     C           NXTSCR    CASEQ'01 '     SC1
     C                     END
     C                     END
     C*
     C*   Do some clean up before leaving.
     C*
     C                     EXSR ZSCLR
     C                     RETRN
     C*----------------------------------------------------------------
     C*  DSPSCR - Display screen
     C*
     CSR         DSPSCR    BEGSR
     C*
     C*   Set ON alarm indicator if error occured
     C*
     CSR         ERROR     IFEQ *ON
     CSR                   MOVE *ON       *IN98
     CSR                   END
     C*
     C*   Overlay screen with error messages
     C*
     C                     EXSR ZMDMSG
     C*
     C*  Overlay screen with screen to be processed.
     C*
     CSR         NXTSCR    IFEQ '01 '
     CSR                   EXFMTSCREEN1                50
     CSR                   END
     C*
     C*   Initialize error indicators and fields
     C*
     CSR                   SETOF                     010203
     CSR                   SETOF                     212223
     CSR                   SETOF                     242526
     CSR                   SETOF                     272829
     CSR                   SETOF                     30
     CSR                   SETOF                     98
     CSR                   MOVE *OFF      WARN    1
     CSR                   MOVE *OFF      ERROR   1
     CSR                   MOVE *OFF      CMDTKN  1
     CSR                   Z-ADD0         E       20
     C*
     C*   Initialize error subfile
     C*
     CSR                   EXSR ZMCMSG
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*     Screen initialization and wrap-up routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  Screen 01 initialization
     C*
     CSR         SCR01I    BEGSR
     CSR                   MOVE '01 '     NXTSCR  3
     CSR                   EXSR ZZFIL1
     CSR         END01I    ENDSR
     C*----------------------------------------------------------------
     C*
     C*              Screen Processing Routines
     C*
     C*----------------------------------------------------------------
     C*
     C*  SC1  -  Screen 1
     C*
     CSR         SC1       BEGSR
     C*
     CSR                   SELEC
     C**         STATUS    WHEQ ROLLUP
     CSR         *IN81     WHEQ *ON
     CSR         MENU      IFEQ 1
     CSR                   MOVEAMNU2      DSMENU
     CSR                   Z-ADD2         MENU
     CSR                   ELSE
     CSR                   MOVEAMNU3      DSMENU
     CSR                   Z-ADD3         MENU
     CSR                   ENDIF
416 ACSR                   EXSR ZZWHSE
     C*
     C**         STATUS    WHEQ ROLLDN
     CSR         *IN82     WHEQ *ON
     CSR         MENU      IFEQ 3
     CSR                   MOVEAMNU2      DSMENU
     CSR                   Z-ADD2         MENU
     CSR                   ELSE
     CSR                   MOVEAMNU1      DSMENU
     CSR                   Z-ADD1         MENU
     CSR                   ENDIF
416 ACSR                   EXSR ZZWHSE
     C*
     C**         STATUS    WHEQ COMAND
     CSR         *INKC     WHEQ *ON
     CSR                   MOVE 'F3'      $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR         *INKL     WHEQ *ON
     CSR                   MOVE 'F2'      $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     C**                   ELSE
     C**                   EXSR ZZCMD1
     C**                   ENDIF
     C*
     CSR                   OTHER
     CSR                   EXSR ZZCHK1
     CSR         ERROR     IFEQ *OFF
     CSR                   EXSR ZZUPD1
     CSR                   ENDIF
     CSR                   ENDSL
     C*
     CSR         ENDSC1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
640 AC*
640 AC* Get client id.
640 AC*
640 AC                     CALL @GETCL
640 AC                     PARM           CLIENT 10
640 AC                     PARM           CLILOC 10
     C*
     C*  Initialize special keys
     C*
     C                     Z-ADD0         ENTER   50
     C                     Z-ADD2         COMAND  50
     C                     Z-ADD01122     ROLLUP  50
     C                     Z-ADD01123     ROLLDN  50
     C                     Z-ADD01124     CLEAR   50
     C                     Z-ADD01125     HELP    50
     C*
510 AC*  Define key for OPTIONS file.
510 AC*
510 AC*     OPTIONS
510 AC*
510 AC           KEYOP     KLIST
510 AC                     KFLD           #WHSE
510 AC                     KFLD           OPCODE
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE *OFF      *IN97
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     CSR                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE *ON       *IN97
     CSR         MSGK      IFGT 0
     CSR                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     CSR                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     CSR                   WRITEMSGREC
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
416 AC*----------------------------------------------------------------
416 AC*   PIR0105  Generic message.
416 AC*
     CSR         ZM0105    BEGSR
     CSR                   MOVE 'PIR0105' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR0108  Invalid option entered.
     C*
     CSR         ZM0108    BEGSR
     CSR                   MOVE 'PIR0108' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVEAERRMSG    $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCHK1   Screen 1 error checking.
     C*
     CSR         ZZCHK1    BEGSR
     CSR                   MOVE *OFF      ERROR
     C*
     C*  Verify option.
     C*
     CSR         W1OPT     IFLT 1
     CSR         W1OPT     ORGT 4
415 ACSR         W1OPT     ANDNE90
     CSR                   MOVE *ON       ERROR
     CSR                   MOVE *ON       *IN21
     CSR                   MOVE *ON       *IN01
     CSR                   MOVE W1OPT     EROPT
     CSR         W1OPT     IFLT 5
415 ACSR         W1OPT     OREQ 90
     CSR                   MOVEL' '       EROPT
     CSR                   ENDIF
     CSR                   EXSR ZM0108
     CSR                   ENDIF
     C*
     CSR         ENDCK1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZCMD1   User defined command keys for screen 1.
     C*
     CSR         ZZCMD1    BEGSR
     CSR                   MOVE *OFF      CMDTKN
     CSR         ENDCM1    ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZFIL1   Fill screen 1 fields with info from file.
     C*
     CSR         ZZFIL1    BEGSR
     C*
     CSR                   MOVEAMNU1      DSMENU
     CSR                   Z-ADD0         W1OPT
     CSR                   Z-ADD1         MENU    10
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ2   Program initialization. Called from main line.
     C*
     CSR         ZZINZ2    BEGSR
     C*
     CSR                   EXSR SCR01I
416 ACSR                   EXSR ZZWHSE
510 AC*
510 ACSR                   MOVE '*FEATURE'OPCODE
510 ACSR         KEYOP     CHAINOPTIONS              79
510 ACSR         *IN79     IFEQ *ON
DRY AC           CLIENT    OREQ DAIRYL
510 ACSR                   MOVE '2'       OPFEAT
510 ACSR                   ELSE
510 ACSR                   MOVE OPDATA    OPDAT3
510 ACSR                   ENDIF
510 ACSR                   MOVE OPFEAT    $PFEAT
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD1   Update for screen 1.
     C*
     CSR         ZZUPD1    BEGSR
     C*
     CSR                   MOVE W1OPT     $POPT
     CSR                   MOVE 'EOJ'     NXTSCR
     C*
     CSR         ENDUP1    ENDSR
416 AC*----------------------------------------------------------------
416 AC*
416 AC*  ZZWHSE   Display user's default warehouse in message line.
416 AC*
416 ACSR         ZZWHSE    BEGSR
     C*
     CSR                   CALL 'GETWHSE2'
     CSR                   PARM           #USER
     CSR                   PARM 0         #WHSE   30
     CSR                   PARM ' '       #DESC  30
     CSR                   PARM ' '       #FRMT  40
     CSR                   MOVEL#FRMT     ERRMSG    P
     CSR                   EXSR ZM0105
     C*
     CSR                   ENDSR
     O*----------------------------------------------------------------
     O*
     O*  COMPILE TIME TABLES
**  MNU1
1. Full screen
 2. Fork lift screen
 3. Handheld 8-line
 4. Handheld 16-line

**  MNU2
90. Signoff




**  MNU3





