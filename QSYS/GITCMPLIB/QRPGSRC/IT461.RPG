     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT461     List Tie/High work file in slot order.
     F*  Hemant Kapadia
     F*  12 May, 1999
     F*
     F*  Revisions
412 AF*    08/06/99  RH   4.12
     F*      - Recompile, ITEMMSC file changed.
416 AF*    07/02/02  RH   4.16
     F*      - Recompiled, ITEMMSC changed.
417aAF*    04/03/03  RH   4.17a
     F*      - Recompiled, ITEMMSC file changed.
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT461   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FITEMMSC IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FTIEHIGH IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FIT461PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     I*----------------------------------------------------------------
     IOQREC
     I                                              SLSTYPL1
     I                                              SLWHDPL1
     C*----------------------------------------------------------------
     C*
     C*  Key fields
     C*
     C*  Partial key for ITEMMSC file
     C           IMKEY     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C*  Key for PIRITEM file
     C           ITKEY     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C*  Key for TIEHIGH file
     C           THKEY     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C                     KFLD           SLWHDP
     C                     KFLD           SLSTYP
     C*
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C           SLSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           SLSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           TYPE      CAT  '/':0     P#DESC
     C           P#DESC    CAT  SLWHDP:0  P#DESC
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type/Department
     C*
     C           *INL1     IFEQ '1'
     C                     Z-ADD1         L
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C*  Write detail line
     C*
     C                     MOVE *BLANKS   SLASH1
     C                     MOVE *BLANKS   SLASH2
     C                     Z-ADD0         COUNT
     C                     SETON                     61
     C*
     C           IMKEY     CHAINITEMMSC              79
     C           *IN79     IFEQ *ON
     C                     Z-ADD0         IMTIE
     C                     Z-ADD0         IMHIGH
     C                     ENDIF
     C*
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *ON
     C                     MOVE *BLANKS   W1DESC
     C                     ELSE
     C                     MOVELITDESC    W1DESC
     C                     ENDIF
     C                     ADD  1         COUNT
     C                     MOVE '/'       SLASH1
     C                     MOVE '/'       SLASH2
     C*
     C                     MOVE *OFF      FOREVR  1
     C           THKEY     SETLLTIEHIGH
     C           FOREVR    DOWEQFOREVR
     C           THKEY     READETIEHIGH                  79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C           SLSDEF    IFNE THSDEF
     C                     MOVE *BLANKS   W1DISP
     C                     ELSE
     C                     MOVE SLDISP    W1DISP
     C                     ENDIF
     C*
     C*  Do not print item info., if more than 1 item found
     C*
     C           THITEM    IFEQ SVITEM
     C                     SETOF                     61
     C                     ENDIF
     C                     MOVE THITEM    SVITEM 15
     C                     WRITEDETAIL1
     C                     ENDDO
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type/Department totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   END
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'IT461PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD0         COUNT   30
     CSR                   Z-ADD2         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           SLWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
