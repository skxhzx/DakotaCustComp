     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  SL421     Available pick slots - Print.
650aAF*            Added option to print overflow slots.
     F*  22 January, 2002
     F*  Hemant Kapadia
     F*
     F*  Created 4.16
     F*
     F*  Revisions:
     F*    04/08/05  RLP  5.00
     F*      - Changed to use new logical SLOT8 created for the
     F*        "view" option just added
     F*
640a F*  02/18/11 GJA 6.40a
     F*    - Enh:  Add '*' when slot has virtuals.
     F*
650aAF*  05/02/13 RBD 6.50a
     F*    - Enh:  Added option to print overflow slots.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
500 DF*LOT1   IP  E           K        DISK
     FSLOT8   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
640aAFVSLOT2  IF  E           K        DISK
     FSL421PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
650aAE                    TITL    1   2 24
     E                    RECK        2  7 0
     E*
     I*----------------------------------------------------------------
     ISLREC
     I                                              SLWHSEL1
     I                                              SLWHDPL1
     I                                              SLAISLL1
     I*----------------------------------------------------------------
     I*  General Data structure.
     I*
     IDS0         DS
     I                                        1  12 C0SLOT
     I                                       13  17 C0DESG
     I                                       18  19 C0DEF
     I                                       20  21 C0STAT
640aAI                                       22  22 C0VSLT
     I*  Data structure for column 1.
     I*
     IDS1         DS                         50
     I                                        1  12 C1SLOT
     I                                       13  17 C1DESG
     I                                       18  19 C1DEF
     I                                       20  21 C1STAT
640aAI                                       22  22 C1VSLT
     I*
     I*  Data structure for column 2.
     I*
     IDS2         DS                         50
     I                                        1  12 C2SLOT
     I                                       13  17 C2DESG
     I                                       18  19 C2DEF
     I                                       20  21 C2STAT
640aAI                                       22  22 C2VSLT
     I*
     I*  Data structure for column 3.
     I*
     IDS3         DS                         50
     I                                        1  12 C3SLOT
     I                                       13  17 C3DESG
     I                                       18  19 C3DEF
     I                                       20  21 C3STAT
640aAI                                       22  22 C3VSLT
     I*  Data structure for column 4.
     I*
     IDS4         DS                         50
     I                                        1  12 C4SLOT
     I                                       13  17 C4DESG
     I                                       18  19 C4DEF
     I                                       20  21 C4STAT
640aAI                                       22  22 C4VSLT
     I*
     I*  Data structure for column 5.
     I*
    DI*S5         DS                         50
     I*                                       1   2 C5DEF
     I*                                       3   4 C5ACTV
     I*                                       5  16 C5SLOT
     I*----------------------------------------------------------------
     I*
     I$PERM       DS
     I                                        1  60 XXERM
     I                                        1   4 $PMSG
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Level break occurred.
     C*     - Filled page.
     C*
     C           *INL1     IFEQ *ON
     C           COUNT     OREQ RECPAG
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Warehouse #/Dept/Aisle
     C*
     C           *INL1     IFEQ *ON
     C                     Z-ADD1         L
     C                     EXSR ZRZERO
     C                     ENDIF
     C*
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
640aAC                     MOVE ' '       C0VSLT
     C                     MOVE SLSDEF    C0DEF
     C                     MOVE SLDESG    C0DESG
     C                     MOVE SLDISP    C0SLOT
     C                     MOVE SLSTAT    C0STAT
640aAC           VSKEY     CHAINVSLOT2               79
640aAC           *IN79     IFEQ *OFF
640aAC                     MOVE '*'       C0VSLT
640aAC                     ENDIF
     C*
     C*  Store info. from first 50 records into data structure DS1.
     C*  Next 50 records into DS2 and then into DS3, and so on to DS9.
     C*
     C                     SELEC
     C           C1        WHLE RECCOL
     C           C1        OCUR DS1
     C                     MOVE DS0       DS1
     C                     ADD  1         C1
     C*
     C           C2        WHLE RECCOL
     C           C2        OCUR DS2
     C                     MOVE DS0       DS2
     C                     ADD  1         C2
     C*
     C           C3        WHLE RECCOL
     C           C3        OCUR DS3
     C                     MOVE DS0       DS3
     C                     ADD  1         C3
     C*
     C           C4        WHLE RECCOL
     C           C4        OCUR DS4
     C                     MOVE DS0       DS4
     C                     ADD  1         C4
     C*
     C*          C5        WHLE RECCOL
     C*          C5        OCUR DS5
     C*                    MOVE DS0       DS5
     C*                    ADD  1         C5
     C                     ENDSL
     C*
     C                     ADD  1         COUNT
     C*
     C*
     C*  Accumulate totals
     C*                    ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C*----------------------------------------------------------------
     C*
     C* Whse/Dept/Aisle
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     C**                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   EXSR ZRDETL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD0         COUNT   30
     C                     Z-ADD2         L       10
     C                     Z-ADD1         X       30
     C                     Z-ADD1         C1      30
     C                     Z-ADD1         C2      30
     C                     Z-ADD1         C3      30
     C                     Z-ADD1         C4      30
     C*                    Z-ADD1         C5      30
     C                     Z-ADD50        RECCOL  30
     C                     Z-ADD200       RECPAG  30
     C*                    Z-ADD250       RECPAG  30
     C                     EXSR ZRCLER
     C                     EXSR ZRZERO
     C*
640aAC           VSKEY     KLIST
640aAC                     KFLD           SLWHSE
640aAC                     KFLD           SLWHDP
640aAC                     KFLD           SLDISP
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           SLWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C*  Format Dept/Section/Aisle
     C*
     C           SLSTYP    IFEQ 'P'
     C                     MOVE 'PIR  '   TYPE    5
     C                     ELSE
     C           SLSTYP    IFEQ 'F'
     C                     MOVE 'Fixed'   TYPE
     C                     ELSE
     C                     MOVE '*None'   TYPE
     C                     END
     C                     END
     C                     MOVE *BLANKS   P#DESC
     C           'Type:'   CAT  TYPE:1    P#DESC
     C           P#DESC    CAT  'Dept:':2 P#DESC
     C           P#DESC    CAT  SLWHDP:1  P#DESC
     C           P#DESC    CAT  'Aisle':2 P#DESC
     C           P#DESC    CAT  ':':0     P#DESC
     C           SLAISL    IFEQ *BLANKS
     C           P#DESC    CAT  '*NONE':1 P#DESC
     C                     ELSE
     C           P#DESC    CAT  SLAISL:1  P#DESC
     C                     END
     C                     MOVELP#DESC    $LSTR  60
     C                     Z-ADD40        $LLEN   30
     C                     CALL 'CENTER'
     C                     PARM           $LSTR
     C                     PARM           $LLEN
     C                     MOVEL$LSTR     P#DESC
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*  Print previous page detail lines.
     C*
     C           RECK,L    IFGT 0
     C                     EXSR ZRDETL
     C                     Z-ADD0         COUNT
     C                     ENDIF
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
650aAC           SLPICK    IFEQ 'Y'
650aAC                     MOVE TITL,1    RPTITL
650aAC                     ELSE
650aAC                     MOVE TITL,2    RPTITL
650aAC                     END
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C                     WRITECOLUMNS
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRCLER    BEGSR
     C*
     C*   Initialize all data structures.
     C*
640aAC                     CLEARC0VSLT
     C                     CLEARC0DEF
     C                     CLEARC0DESG
     C                     CLEARC0SLOT
     C                     CLEARC0STAT
     C           1         DO   RECCOL    X
     C           X         OCUR DS1
     C                     MOVE DS0       DS1
     C           X         OCUR DS2
     C                     MOVE DS0       DS2
     C           X         OCUR DS3
     C                     MOVE DS0       DS3
     C           X         OCUR DS4
     C                     MOVE DS0       DS4
     C*          X         OCUR DS5
     C*                    MOVE DS0       DS5
     C                     ENDDO
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRDETL    BEGSR
     C*  Print detail line.
640aAC                     CLEARC0VSLT
     C                     CLEARC0DEF
     C                     CLEARC0DESG
     C                     CLEARC0SLOT
     C                     CLEARC0STAT
     C           1         DO   RECCOL    X
     C           X         OCUR DS1
     C           X         OCUR DS2
     C           X         OCUR DS3
     C           X         OCUR DS4
     C*          X         OCUR DS5
     C                     WRITEDETAIL1
     C                     MOVE DS0       DS1
     C                     MOVE DS0       DS2
     C                     MOVE DS0       DS3
     C                     MOVE DS0       DS4
     C*                    MOVE DS0       DS5
     C                     ENDDO
     C*
     C*  Initialize record counters for each columns.
     C*
     C                     Z-ADD1         C1
     C                     Z-ADD1         C2
     C                     Z-ADD1         C3
     C                     Z-ADD1         C4
     C*                    Z-ADD1         C5
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*                    ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C*                    ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C*                    Z-ADDCLS,L     T#CLS
     C*                    MOVE TYPE      T#DESC
     C*
     C*                    MOVE RPD,L     $RPD
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C*                    Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C*                    Z-ADD0         CLS,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
**
Available Pick Slots
Available Overflow Slots
