     F*----------------------------------------------------------------
     F*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  QDSGC003  Create ITEMDEF records from item pick slot.
     F*  26 August 2004
     F*  Max Blacknik
     F*
     F*  Copied from UT250MB.
     F*
     F*  Revisions
     F*
520 AF*    05/19/09  MLB  5.20
     F*      - Created.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  01 - 20   FIELD POSITION TO
     F*  21 - 40   FIELD INPUT ERROR (REVERSE DISPLAY)
     F*  90        VIEW MODE (PROTECT INPUT FIELDS)
     F*  91        CHANGE MODE (PROTECT KEY)
     F*  97        ALWAYS ON (USED FOR SFLEND KEYWORD)
     F*  98        ERROR ON SCREEN (SOUND BUZZER)
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT1   IF  E           K        DISK
     FITEMMSC IF  E           K        DISK
     FITEMDEF IF  E           K        DISK                      A
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PWHDP  5
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE $PWHSE    KYWHSE  30
     C                     MOVE *ON       FOREVR  1
     C*
     C           IMKEY     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C*
     C           IDKEY     KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLITEM
     C                     KFLD           SLSDEF
     C*
     C           KYWHSE    SETLLSLOT1
     C           FOREVR    DOWEQFOREVR
     C           KYWHSE    READESLOT1                    79
     C                     SELEC
     C           *IN79     WHEQ *ON
     C                     LEAVE
     C           $PWHDP    WHNE '*ALL '
     C           SLWHDP    ANDNE$PWHDP
     C                     ITER
     C           SLPICK    WHEQ 'Y'
     C           SLITEM    ANDGT' '
     C           SLDESG    ANDNE'SPCL'
     C           SLDESG    ANDNE'SPCLC'
     C*
     C           IMKEY     CHAINITEMMSC              70
     C           *IN70     IFEQ *ON
     C                     MOVE *ZEROS    IMTIE
     C                     MOVE *ZEROS    IMHIGH
     C                     ENDIF
     C*
     C           IDKEY     CHAINITEMDEF              75
     C           *IN75     IFEQ *ON
     C                     Z-ADDSLWHSE    IDWHSE
     C                     MOVE SLITEM    IDITEM
     C                     MOVE SLSDEF    IDSDEF
     C                     Z-ADDIMTIE     IDTIE
     C                     Z-ADDIMHIGH    IDHIGH
     C                     MOVE 'N'       IDUM
     C                     WRITEIDREC                  78
     C                     ENDIF
     C*
     C                     ENDSL
     C                     ENDDO
     C*
     C*   Do some clean up before leaving.
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
