     F*----------------------------------------------------------------
     F*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FRMTVS    Format virtual slot display field.
     F*  25 September 1999
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*    - When the physical slot is a handstack slot ($PFLAG = *ON)
     F*      only use the last character of the handstack sent in.
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A2          2  1
     E                    A3          3  1
     E                    A12        12  1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PDSPI  Slot display In
     C*      $PHAND  Handstack indicator for FA-PIR location.
     C*
     C*    Returned Parameters
     C*      $PDSPO  Slot display Out
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PDSPI 12
     C                     PARM           $PFLAG  1
     C                     PARM           $PHAND  2
     C                     PARM           $PDSPO 12
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Create virtual slot display by adding handstack to end
     C*    of physical slot display field.
     C*
     C                     Z-ADD0         X       20
     C*
     C*    Find last character of physical slot display.
     C*
     C                     MOVEA$PDSPI    A12
     C                     Z-ADD12        I       20
     C           1         DO   12
     C           A12,I     IFNE ' '
     C                     LEAVE
     C                     ENDIF
     C                     SUB  1         I
     C                     ENDDO
     C                     Z-ADDI         X
     C*
     C*    Add virtual handstack indicator (suppress blanks)
     C*
     C                     MOVEA$PHAND    A2
     C           $PFLAG    IFEQ *ON
     C                     MOVE *BLANK    A2,1
     C                     ENDIF
     C           1         DO   2         I
     C           A2,I      IFNE ' '
     C                     ADD  1         X
     C           X         IFLE 12
     C                     MOVE A2,I      A12,X
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C*
     C*    Move display array to output parameter.
     C*
     C                     MOVEAA12       $PDSPO
     C*
     C                     RETRN
