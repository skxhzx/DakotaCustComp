     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  CHKCREW  Check Authorized User Crew Code
     F*  September 6, 2006
     F*  Jose Duque
     F*
     F*  Revisions
     F*      - Created
     F*
520 AF*    07/09/08  RBD  5.20   P#00060
     F*      - Enh: Added client custom code support to program.
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions - Cheney
     F*
CBI AF*    09/26/06  CBI-JGD
     F*      Modify user access control logic in 'ZZCHK' Rtn.
     F*
CBIaAF*    04/04/07  CBI-JGD
     F*      Cancel access to 'SHIFT3-Crew Codes-Flgrun-&-Crinvn'
     F*      (per Keiths).
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        CHAIN INDICATOR
     F*  99        UNIVERSAL RECORD INDICATOR (VERY TEMPORARY USAGE)
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRUSER IF  E           K        DISK
     FSUPERUSRIF  E           K        DISK
     F*
     FFLGDLOG O   E           K        DISK
     E*----------------------------------------------------------------
520 AI*  Client ID's
520 AI*
CBI A /COPY QCOPYSRC,IL#CHENEY
520 AI*
520 AI              'GETCLIENT'           C         @GETCL
520 AI*
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Constants
     I              'PIRCUST2CL'          C         CUST2
     I              'RU310CL'             C         RU310
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PUSRI  User executing the program
     C*      $PROC   Procedure to execute within pgm
     C*
     C*    Returned Parameters
     C*      $PRTN   *YES     - Authorized User
     C*              *NO      - Non-Authorized User
     C*
     C*----------------------------------------------------------------
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PUSRI 10
     C                     PARM           $PRTN  10
     C                     PARM           $PROC  10
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C* Execute appropriate routine.
     C*
     C                     EXSR ZZCHK
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*  *INZSR  Initialization subrotine
     C*----------------------------------------------------------------
     C           *INZSR    BEGSR
     C*
     *
520 A*  Get Client ID
     *
520 AC                     CALL @GETCL
520 AC                     PARM           CLIENT 10
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*
     C*---------------------------------------------------------------
     C*  FLGLOG   Record log of users clearing flags using PIRFLG/Opt4
     C*---------------------------------------------------------------
     C           FLGLOG    BEGSR
      *
      * Get current date in century format
      *
     C                     MOVE USEMP#    LOGEMP
     C                     MOVE '*CURCMD '$CVCMD
     C                     CALL @CVTDT
     C                     PARM           $CVTDT
      *
     C                     MOVE $CVD8O    LOGSTD
     C                     TIME           LOGSTT
     C*
     C                     WRITELOGREC
     C*
     C                     ENDSR
     C*---------------------------------------------------------------
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*---------------------------------------------------------------
     C           ZZINZ     BEGSR
     C*
     C*  Define full key for PGMUSRID file.
     C*
     C           USRKEY    KLIST
     C                     KFLD           $PUSRI
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*   Check if user is authorized to initiate the update action
     C*----------------------------------------------------------------
     C*
     C           ZZCHK     BEGSR
     C*
     C           USRKEY    CHAINPIRUSER              79
     C*
     C           *IN79     IFEQ *ON
     C                     MOVEL'*NO'     $PRTN     P      UserNotFound
     C                     GOTO ENDCHK                     InEmplFile
     C                     ENDIF
     C*
     C           $PUSRI    CHAINSUPERUSR             79    Users with
     C*                                                    total access
     C           *IN79     IFEQ *OFF
     C                     MOVEL'*YES'    $PRTN     P
     C           $PROC     IFEQ RU310
     C                     MOVE 'SPRUSR'  LOGTYP
     C                     EXSR FLGLOG
     C                     ENDIF
     C                     GOTO ENDCHK
     C                     ENDIF
     C*
     C*
     C                     SELEC
     C*
CBI AC           CLIENT    WHEQ CHENEY
CBI AC           $PROC     ANDEQCUST2                      Limited
CBI AC           USSHFT    ANDEQ'SHIFT3'                   access to
     C*                                                    night users.
CBI AC           USCREW    IFEQ 'CRADMN'                   Only crew
CBI AC           USCREW    OREQ 'CRINVN'                   codes allwed
CBI AC           USCREW    OREQ 'CRROAD'
CBI AC           USCREW    OREQ 'CRSUPN'
CBI AC                     MOVEL'*YES'    $PRTN     P
CBI AC                     ELSE
CBI CC                     MOVEL'*NO'     $PRTN     P
CBI AC                     ENDIF
      *
CBIaAC           CLIENT    WHEQ CHENEY
CBIaAC           $PROC     ANDEQRU310                      Same as
CBIaAC           USSHFT    IFNE 'SHIFT3'                         above
CBIaAC                     MOVEL'*YES'    $PRTN     P
CBIaAC                     MOVE USSHFT    LOGTYP
CBIaAC                     EXSR FLGLOG
CBIaAC                     ELSE
CBIaCC                     MOVEL'*NO'     $PRTN     P
CBIaAC                     ENDIF
     C*
     C                     ENDSL
      *
     C           ENDCHK    ENDSR
