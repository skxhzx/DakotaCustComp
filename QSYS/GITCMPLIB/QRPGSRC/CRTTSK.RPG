     F*----------------------------------------------------------------
     F*
     F*  CRTTSK    Create task detail records.
     F*  22 December, 1995
     F*  Hemant Kapadia
     F*
     F*  Revisions
     F*
     FTASKDET O   E           K        DISK                      A
     F            TDREC                             KRENAMERECORD
     FWAREHS  IF  E           K        DISK
     FWHDEPT  IF  E           K        DISK
     I*----------------------------------------------------------------
     I*  Program parameters
     I*
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  24 $PCODE
     I                                       25  25 $PTLVL
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I*----------------------------------------------------------------
     I*  Called program parameters
     I*
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I*
     I                                        1  30 ERFLD
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PCODE  Task code
     C*      $PTLVL  Track level
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     SELEC
     C*
     C*  Track level 'W'- Warehouse level.
     C*
     C           $PTLVL    WHEQ 'W'
     C* Create detail record for each warehouse.
     C           *LOVAL    SETLLWAREHS
     C           *IN77     DOUEQ*ON
     C                     READ WAREHS                   77
     C                     MOVE WHCODE    TDWHSE
     C                     MOVE *BLANKS   TDWHDP
     C                     MOVE *BLANKS   TDSTYP
     C                     EXSR ZZUPD
     C                     ENDDO
     C*
     C*  Track level 'D'- Department level.
     C*
     C           $PTLVL    WHEQ 'D'
     C* Create detail record for each warehouse/department combonitaion
     C           *LOVAL    SETLLWAREHS
     C           *IN77     DOUEQ*ON
     C                     READ WAREHS                   77
     C                     MOVE WHCODE    TDWHSE
     C*
     C           WDKEY     SETLLWHDEPT               78
     C                     MOVE *IN78     EOF     1
     C           EOF       DOWEQ*OFF
     C           WDKEY     READEWHDEPT                   78
     C                     MOVE *IN78     EOF
     C           *IN78     IFEQ *OFF
     C                     MOVE WDCODE    TDWHDP
     C                     MOVE *BLANKS   TDSTYP
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDDO
     C*
     C*  Track level 'S'- Section level.
     C*
     C           $PTLVL    WHEQ 'S'
     C* Create detail record for each warehouse/department/section
     C* combination.
     C*
     C           *LOVAL    SETLLWAREHS
     C           *IN77     DOUEQ*ON
     C                     READ WAREHS                   77
     C                     MOVE WHCODE    TDWHSE
     C*
     C           WDKEY     SETLLWHDEPT               78
     C                     MOVE *IN78     EOF     1
     C           EOF       DOWEQ*OFF
     C           WDKEY     READEWHDEPT                   78
     C                     MOVE *IN78     EOF
     C           *IN78     IFEQ *OFF
     C                     MOVE WDCODE    TDWHDP
     C                     MOVE 'F'       TDSTYP
     C                     EXSR ZZUPD
     C                     MOVE 'P'       TDSTYP
     C                     EXSR ZZUPD
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDDO
     C*
     C                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE '0'       ERROR   1
     C*
     C*  Initialize message handling
     C*
     C                     EXSR ZMIMSG
     C*
     C*   Call user added initialization routine.
     C*
     C                     EXSR ZZINZ
     C                     ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMCMSG  Clear message record subfile
     C*
     CSR         ZMCMSG    BEGSR
     CSR                   MOVE '0'       *IN97
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK    40
     C**                   WRITEMSGCLR
     CSR                   MOVE '*CLEAR ' #MSGID  7
     CSR                   MOVE '*NULL  ' #MSGTP  7
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*
     C*    ZMDMSG  Display message record subfile
     C*
     CSR         ZMDMSG    BEGSR
     CSR                   MOVE '1'       *IN97
     CSR         MSGK      IFGT 0
     C**                   WRITEMSGCTL
     CSR                   Z-ADD*ZERO     MSGK
     CSR                   END
     CSR                   ENDSR
     C*
     C*    ZMIMSG  Initialization necessary for message subfile
     C*
     CSR         ZMIMSG    BEGSR
     CSR                   MOVE #PROG     #PGMQ  10
     CSR                   EXSR ZMCMSG
     CSR                   ENDSR
     C*
     C*    ZMPMSG  Add message record to subfile
     C*
     CSR         ZMPMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM           #PGMQ
     CSR                   PARM           #MSGK
     CSR         #MSGID    IFNE '*CLEAR '
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   END
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*
     C*    ZMQMSG  Add message record to subfile from program queue
     C*
     CSR         ZMQMSG    BEGSR
     CSR                   ADD  1         MSGK
     C**                   WRITEMSGREC
     CSR                   ENDSR
     C*
     C*    ZSCLR   Clear screen
     C*
     CSR         ZSCLR     BEGSR
     C**                   WRITECLRSCR
     CSR                   MOVE *ON       SCLEAR  1
     CSR                   ENDSR
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID
     CSR                   PARM           #MSGTP
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*    ZMnnnn  Build and send message nnnn to this program
     C*----------------------------------------------------------------
     C*   PIR7971  Error in writing a record.
     C*
     CSR         ZM71      BEGSR
     CSR                   MOVE 'PIR7971' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*   PIR7972  Error in updating a record.
     C*
     CSR         ZM72      BEGSR
     CSR                   MOVE 'PIR7972' #MSGID
     CSR                   MOVE '*DIAG  ' #MSGTP
     CSR                   MOVE *BLANKS   $MD
     CSR                   EXSR ZMPMSG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZINZ    Extra program initialization. Called from *INZSR.
     C*
     CSR         ZZINZ     BEGSR
     C*
     C*  Define partial key for WHDEPT file.
     C*
     CSR         WDKEY     KLIST
     CSR                   KFLD           WHCODE
     C*
     CSR                   MOVE #PROG     $PPRG
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  ZZUPD    Update record.
     C*
     CSR         ZZUPD     BEGSR
     CSR                   MOVE '0'       ERROR
     C*
     CSR                   MOVE $PCODE    TDCODE
     CSR                   Z-ADD0         TDETIM
     CSR                   MOVE '*CURCMD '$CVCMD
     CSR                   CALL @CVTDT
     CSR                   PARM           $CVTDT
     CSR                   Z-ADD$CVD8O    TDIDTE
     CSR                   TIME           TDITIM
     CSR                   Z-ADD0         TDSTDJ
     CSR                   Z-ADD0         TDSTDC
     CSR                   Z-ADD0         TDSTDW
     CSR                   Z-ADD0         TDSTDP
     CSR                   Z-ADD0         TDSTD1
     CSR                   Z-ADD0         TDSTD2
     CSR                   Z-ADD0         TDSTD3
     CSR                   Z-ADD0         TDSDTE
     CSR                   Z-ADD0         TDSTIM
     CSR                   MOVE *BLANKS   TDSBY
     CSR                   Z-ADD0         TDPNDJ
     CSR                   Z-ADD0         TDPNDC
     CSR                   Z-ADD0         TDPNDW
     CSR                   Z-ADD0         TDPNDP
     CSR                   Z-ADD0         TDPND1
     CSR                   Z-ADD0         TDPND2
     CSR                   Z-ADD0         TDPND3
     CSR                   Z-ADD0         TDACTJ
     CSR                   Z-ADD0         TDACTC
     CSR                   Z-ADD0         TDACTW
     CSR                   Z-ADD0         TDACTP
     CSR                   Z-ADD0         TDACT1
     CSR                   Z-ADD0         TDACT2
     CSR                   Z-ADD0         TDACT3
     CSR                   Z-ADD0         TDFINJ
     CSR                   Z-ADD0         TDFINC
     CSR                   Z-ADD0         TDFINW
     CSR                   Z-ADD0         TDFINP
     CSR                   Z-ADD0         TDFIN1
     CSR                   Z-ADD0         TDFIN2
     CSR                   Z-ADD0         TDFIN3
     CSR                   Z-ADD0         TDCLSJ
     CSR                   Z-ADD0         TDCLSC
     CSR                   Z-ADD0         TDCLSW
     CSR                   Z-ADD0         TDCLSP
     CSR                   Z-ADD0         TDCLS1
     CSR                   Z-ADD0         TDCLS2
     CSR                   Z-ADD0         TDCLS3
     C*
     C*   Add record
     C*
     CSR         $PCMD     IFEQ '*ADD    '
     CSR                   WRITERECORD                 79
     CSR         *IN79     IFEQ '1'
     CSR                   MOVE '1'       ERROR
     CSR                   EXSR ZM71
     CSR                   GOTO ENDUPD
     CSR                   ENDIF
     CSR                   ENDIF
     CSR         ENDUPD    ENDSR
