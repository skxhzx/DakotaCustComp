     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W Roosevelt Rd, Bldg 8, Suite 51
     F*   West Chicago, IL 60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  IT491     Inventory qty level
     F*  20 Jan, 2003
     F*  Hemant Kapadia
     F*
     F*----------------------------------------------------------------
     F*
     F*  Program Modification History
     F*  Revisions:
     F*
     F*    01/20/03  HNK 4.16
     A*      - Created
700a F*    12/02/16  RH  7.00a
     A*      - Enh: Added client code
     F*
     F*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions: CDI
CDI AF*    12/02/16  RH   CDI
     F*      - Change pgm to skip slot with 0 quantity
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  91        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIT491   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FIT491PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    CLS         3  5 0
700aAI*----------------------------------------------------------------
700aAI*  Customer id
700aAI*
700aAI              'GETCLIENT'           C         @GETCL
700aAI*
CDI A /COPY QCOPYSRC,IL#CDI
     I*----------------------------------------------------------------
     IOQREC
     I                                              ITWHDPL1
     C*----------------------------------------------------------------
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSE   3
     C                     PARM           $PRTN   8
     C                     PARM           $PQTY   5
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Slot type
     C*
     C           *INL1     IFEQ '1'
     C                     Z-ADD1         L
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C                     MOVE $PQTY     WKQTY   50
     C                     Z-ADD0         AVAIL1  50
     C                     Z-ADD0         AVAIL2  30
     C                     Z-ADD0         AVAIL3  30
     C*
     C*    Create a quantity string to print.
     C*
     C*
     C*   Calculate available quantities.
     C*
     C           IQSTK1    SUB  IQALC1    AVAIL1
     C                     SUB  IQHLD1    AVAIL1
     C*
     C           IQSTK2    SUB  IQALC2    AVAIL2
     C                     SUB  IQHLD2    AVAIL2
     C*
     C           IQSTK3    SUB  IQALC3    AVAIL3
     C                     SUB  IQHLD3    AVAIL3
     C*
     C           AVAIL1    IFGE WKQTY
     C                     GOTO SKIP
     C                     ENDIF
     C*
CDI AC           CLIENT    IFEQ CDI
CDI AC           IQSTK1    IFEQ 0
CDI AC           AVAIL1    ANDEQ0
CDI AC           IQSTK2    ANDEQ0
CDI AC           AVAIL2    ANDEQ0
CDI AC           IQSTK3    ANDEQ0
CDI AC           AVAIL3    ANDEQ0
CDI AC                     GOTO SKIP
CDI AC                     ENDIF
CDI AC                     ENDIF
     C*
     C                     MOVE '*QTY2STR'$PCMD
     C                     Z-ADDAVAIL1    $PQTY1
     C                     Z-ADDAVAIL2    $PQTY2
     C                     Z-ADDAVAIL3    $PQTY3
     C                     MOVE *BLANKS   $PQSTR
     C*
     C                     CALL 'CVTQTY'
     C                     PARM           $PCMD   8
     C                     PARM           $PPRG  10
     C                     PARM           $PQTY1  70
     C                     PARM           $PQTY2  70
     C                     PARM           $PQTY3  70
     C                     PARM           $PUM1   2
     C                     PARM           $PUM2   2
     C                     PARM           $PUM3   2
     C                     PARM           $PQSTR 10
     C                     PARM           $PUSTR 17
     C                     PARM           $PRTN   8
     C                     PARM           $PERM  60
     C*
     C                     MOVE $PQSTR    W1QTY
     C*
     C*
     C*  Write detail line
     C*
     C                     WRITEDETAIL1
     C*
     C*  Accumulate totals
     C*
     C                     ADD  1         CLS,1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C           SKIP      TAG
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN92
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     CL1         RECK,L    IFGT 0
     CL1                   WRITESKIP1
     CL1                   EXSR ZRTOTL
     CL1                   END
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   END
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
700aAC*
700aAC* Get client id.
700aAC*
700aAC                     CALL @GETCL
700aAC                     PARM           CLIENT 10
700aAC                     PARM           CLILOC 10
700aAC*
     C*
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'IT491PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD3         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C                     MOVE ITWHDP    TYPE
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           ITWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C                     WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ADD  CLS,L     CLS,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C                     Z-ADDCLS,L     T#CLS
     C*                    MOVE TYPE      T#DESC
     C*
     C*                    MOVE RPD,L     $RPD
     C*          L         IFEQ 1
     C*                    WRITETOTAL1
     C*                    END
     C*
     C           L         IFEQ 2
     C                     WRITETOTAL2
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     Z-ADD0         CLS,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
