     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Associates, Inc.
     F*   245 W Roosevelt Rd. #8-51
     F*   West Chicago, IL 60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  SL152B    Verify zero slots, multiple virutal only
     F*  Roxanne Harrison
     F*  14 July, 2008
     F*  Program will zero verify empty virutal slots leaving empty
     F*  base slot or one empty virutal slots to be physically verified
     F*
     F*  Revisions
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FSLOT1   IP  E           K        DISK
     FVSLOT1  IF  E           K        DISK
     I*****************************************************************
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E*                   $MD       128  1
     E*                   MSG     1   1 50
     I*----------------------------------------------------------------
     I*  Called programs
     I              'SL154'               C         @DELPG
     I*
     I*----------------------------------------------------------------
     I*  Partial key redefinition
     I*
     I$PRTKY      DS
     I                                        1  50 $LEN50
     I                                        1   30$KWHSE
     I                                        4   8 $KWHDP
     I                                        9  20 $KDISP
     I*----------------------------------------------------------------
     I*  Called program parameters
     I$PARMS      DS
     I                                        1   8 $PCMD
     I                                        9  18 $PPRG
     I                                       19  68 $PUKY
     I                                       69  76 $PRTN
     I                                       77 136 $PERM
     I                                       77  80 $PMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$PWHSE
     I                                       22  26 $PWHDP
     I                                       27  38 $PDISP
     I*
     I*----------------------------------------------------------------
     I*
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
     I                                       19  210$LWHSE
     I                                       22  36 $LITEM
     I*
     I*----------------------------------------------------------------
     I*  Data structure for error message parameters
     I*
     I$MDT        DS
     I                                        1  50 ERRMSG
     I            DS
     I                                    P   1   20$EC
     I*----------------------------------------------------------------
     I*  Message control
     I*
     I              'PIRMSGF   '          C         #MSGF
     I*
     I            DS
     I                                        1 128 #MSGDT
     I                                        1 128 $MD
     I*----------------------------------------------------------------
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $WHSE  - Warehouse
     C*      $RTNCD - Return code
     C*
     C*    Returned Parameters
     C*      None
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PARMS
     C                     PARM           $TLSLT  50
     C                     PARM           $PRG   10
     C*----------------------------------------------------------------
     C*
     C*  Main line
     C*
     C*  ONLY PROCESS EMPTY SLOTS
     C           SLWHSE    IFEQ $PWHSE
     C           SLSTAT    ANDEQ'Z'
     C*
     C*  Get base slot.
     C           SLRSRV    IFEQ 'V'
     C           SLKEY     CHAINVSLOT1               78
     C           *IN78     IFEQ *OFF
     C                     MOVE VSPDIS    KYPDSP
     C                     ENDIF
     C                     ELSE
     C                     MOVE SLDISP    KYPDSP
     C                     ENDIF
     C*  IF BASE SLOT IS SAME AS PREVIOUS, DELETE
     C           SVPDSP    IFEQ KYPDSP
     C*  Call delete program
     C*
     C                     EXSR INZPAR
     C                     MOVE '*DELETE '$PCMD
     C                     MOVE $PRTKY    $PUKY
     C                     CALL @DELPG
     C                     PARM           $PARMS
     C*
     C*     Delete was successful.
     C*
     C           $PRTN     IFEQ '*OK     '
     C*                    MOVE $PMSG     #MSGK
     C*                    EXSR ZMSMSG
     C                     ADD  1         TLSLT
     C                     ENDIF
     C                     ENDIF                            MULTIPLE
     C                     MOVE KYPDSP    SVPDSP
     C                     ENDIF                            MULTIPLE
     C*
     CLR                   Z-ADDTLSLT     $TLSLT
     CLR                   RETRN
     C*
     C*
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   Z-ADD0         TLSLT   50
     CSR                   MOVE '1'       FIRST   1
     CSR         *LIKE     DEFN SLWHSE    KYWHSE
     CSR         *LIKE     DEFN SLWHDP    KYWHDP
     CSR         *LIKE     DEFN SLDISP    KYPDSP
     CSR         *LIKE     DEFN SLDISP    SVPDSP
     C*
     C*
     C*  Define keys.
     C*
     C*    VSLOT   file
     C*
     CSR         SLKEY     KLIST
     CSR                   KFLD           SLWHSE
     CSR                   KFLD           SLWHDP
     CSR                   KFLD           SLDISP
     C*    VSLOT2  file
     C*
     CSR         VSKEY     KLIST
     CSR                   KFLD           KYWHSE
     CSR                   KFLD           KYWHDP
     CSR                   KFLD           KYPDSP
     C*
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  INZPAR   Initialize parameters for calling add/chg/del program
     C*
     CSR         INZPAR    BEGSR
     CSR                   CLEAR$PARMS
     CSR                   MOVE $PRG      $PPRG
     CSR                   Z-ADDSLWHSE    $KWHSE
     CSR                   MOVE SLWHDP    $KWHDP
     CSR                   MOVE SLDISP    $KDISP
     C**                   MOVE $PRTKY    $PUKY
     CSR                   ENDSR
     C*---------------------------------------------------------------
     C*
     C*  SUBROUTINE ZMxxxx  Control message display subfile
     C*
     C*    ZMSMSG  Send program message to a different program msgq
     C*
     CSR         ZMSMSG    BEGSR
     CSR         $MSGF     IFEQ *BLANKS
     CSR                   MOVE #MSGF     $MSGF  10
     CSR                   END
     CSR                   CALL 'PUTMSG'
     CSR                   PARM           $MSGF
     CSR                   PARM           #MSGID  7
     CSR                   PARM           #MSGTP  7
     CSR                   PARM           #MSGDT
     CSR                   PARM $PPRG     #PGMQ2 10
     CSR                   PARM           #MSGK   4
     CSR                   MOVE *BLANKS   #MSGDT
     CSR                   MOVE *BLANKS   $MSGF
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C                     ENDSR
     C*****************************************************************
     C*
