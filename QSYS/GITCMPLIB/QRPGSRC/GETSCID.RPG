     F*----------------------------------------------------------------
     F*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL  60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  GETSCID   Get pallet section id for splits
     F*  20 April 2005
     F*  Max Blacknik
     F*
     F*----------------------------------------------------------------
     F*  Revisions
     F*
500 AF*   04/20/05  MLB  5.00
     F*     -Created.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FLABELPS IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PCMD    - Not used.
     I*     $PWHSE   - Warehouse number
     I*     $PLBL#   - Label number.
     I*     $PCAS#   - Case number of item being printed.
     I*
     I*  Output parameters
     I*
     I*     $PSCID   - Pallet section id for case#.
     I*     $PRTN    - *OK      -  Pallet section record found.
     I*                *ERROR   -  Pallet section record not found.
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PCMD   8
     C                     PARM           $PWHSE  30
     C                     PARM           $PLBL#  70
     C                     PARM           $PCAS#  50
     C                     PARM           $PSCID  2
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     MOVE *BLANKS   $PSCID
     C                     MOVE '*ERROR  '$PRTN
     C*
     C*      Get pallet section id for item split between pallet sections.
     C*
     C           KEYLS     CHAINLABELPS              79
     C                     SELEC
     C           *IN79     WHEQ *OFF
     C                     MOVE LSSCID    $PSCID
     C                     MOVE '*OK     '$PRTN
     C                     ENDSL
     C*
     C*
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *OFF      FOREVR  1
     C*
     C*  Define key for LABEL11 file.
     C*
     CSR         KEYLS     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PLBL#
     CSR                   KFLD           $PCAS#
     C*
     CSR                   ENDSR
