     F*----------------------------------------------------------------
     F*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  PIRRTE#     Get next PIR route id
     F*  02 June 1997
     F*  Dave Sommerville
     F*
     F*----------------------------------------------------------------
     F*
     F*  File specs
     F*
     FPIRRTE# UF  E           K        DISK                      A
     FRTEHED  IF  E           K        DISK
     C*----------------------------------------------------------------
     C*
     C*  Subprogram parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehose.
     C*
     C*    Returned Parameters
     C*      $NXTNM  Next PIR route id
     C*
     C*----------------------------------------------------------------
     C* Main Line
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $NXTID  5
     C*
     C*  Get record with next number.  Create it if necessary.
     C*
     C           $PWHSE    CHAINPIRRTE#              79
     C           *IN79     IFEQ *ON
     C                     Z-ADD$PWHSE    NXRWHS
     C                     Z-ADD0         NXRNUM
     C                     WRITENXRREC
     C           $PWHSE    CHAINPIRRTE#              79
     C                     ENDIF
     C                     Z-ADDNXRNUM    NEXT#   50
     C*
     C*  File contains the next sequence number to use.  If it
     C*  has already been used then keep adding 1 until we find one
     C*  that hasn't been used (we will take the chance that all
     C*  99999 have not been used).
     C*
     C                     MOVE *OFF      STOP    1
     C           STOP      DOUEQ*ON
     C           NEXT#     IFEQ 0
     C                     Z-ADD1         NEXT#
     C                     ENDIF
     C                     MOVE NEXT#     RHRTID
     C           FILEKY    CHAINRTEHED               78
     C           *IN78     IFEQ *ON
     C                     MOVE *ON       STOP
     C                     ELSE
     C                     ADD  1         NEXT#
     C                     ENDIF
     C                     ENDDO
     C*
     C*  Return the unique sequence number.
     C*
     C                     MOVE NEXT#     $NXTID
     C*
     C*  Update the file with the next number to be used.
     C*
     C           NEXT#     ADD  1         NXRNUM
     C                     UPDATNXRREC
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     C*  Define key for RTEHED.
     C*
     CSR         FILEKY    KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           RHRTID
     CSR                   ENDSR
