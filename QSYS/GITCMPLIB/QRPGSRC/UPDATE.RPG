     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  UPDATE    Update files using UPDFILE file.
     F*  Dave Sommeville
     F*
     F*  Revisions
402 AF*  07/17/97  RH   4.02
     F*    - Add parameters $RLIB and $RLIBF libraries to be updated.
416 AF*  02/14/03  RH   4.16
     F*    - Add parameters $RLIB and $RLIBF to call UPDCALL.
     F*    - Add parameters $RLIB and $RLIBF to call UPDRPTCL
416aAF*  04/09/03  DAS  4.16a
     F*    - Add parameters UFPGM and $RLIB to call UPDCPY.
     F*    - If *ERROR occured, send messages back to calling program.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FUPDFILESUF  E           K        DISK
     FPIRDBR1 IF  E           K        DISK
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*
402 AC*      $RLIB   Program library to be updated.
402 AC*      $RLIBF  File library to be updated
     C*      None
     C*
     C*    Returned Parameters
     C*      $PRTN   *OK      - No problem encountered.
     C*              *ERROR   - Error occured.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
402 AC                     PARM           $RLIB  10
402 AC                     PARM           $RLIBF 10
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*  Execute any 'Before' programs.
     C*
     C                     MOVE '*OK     '$PRTN
     C                     MOVE *ON       FOREVR  1
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFPGM     IFNE *BLANKS
     C           UFBA      ANDEQ'B'
     C           UFCVT     ANDNE'Y'
     C                     CALL 'UPDCALL'
     C                     PARM           $RLIB
     C                     PARM           $RLIBF
     C                     PARM           UFPGM
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFCVT
     C                     UPDATUFREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Copy old file to new file.
     C*
     C                     MOVE *ON       FOREVR  1
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFTYPE    IFEQ 'U'
     C           UFFTYP    ANDEQ'P'
     C           UFCPY     ANDNE'Y'
     C                     CALL 'UPDCPY'
     C                     PARM           UFNEWF
     C                     PARM           UFOLDF
416aAC                     PARM           UFPGM
416aAC                     PARM           $RLIB
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFCPY
     C                     UPDATUFREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Delete current logical files.
     C*
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFTYPE    IFEQ 'U'
     C           UFTYPE    OREQ 'R'
     C           UFFTYP    IFEQ 'P'
     C           UFDLTL    ANDNE'Y'
     C*
     C*     Whip through corresponding logical files and delete.
     C*
     C           UFOLDF    SETLLPIRDBR1
     C           FOREVR    DOUNEFOREVR
     C           UFOLDF    READEPIRDBR1                  78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           WHREFI    IFNE *BLANKS
     C                     CALL 'UPDDLT'
     C                     PARM           WHREFI
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     MOVE 'Y'       UFDLTL
     C                     UPDATUFREC
     C*
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Delete current physical files.
214 AC*     and logical files that are being updated.
     C*
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFTYPE    IFEQ 'U'
     C           UFTYPE    OREQ 'R'
     C*
     C           UFFTYP    IFEQ 'P'
     C           UFDLTP    ANDNE'Y'
214 AC           UFFTYP    OREQ 'L'
214 AC           UFDLTP    ANDNE'Y'
     C                     CALL 'UPDDLT'
     C                     PARM           UFOLDF
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFDLTP
     C                     UPDATUFREC
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Rename 'N.' files.
     C*
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFTYPE    IFEQ 'U'
     C           UFTYPE    OREQ 'N'
     C           UFTYPE    OREQ 'R'
     C           UFFTYP    IFEQ 'P'
     C           UFFTYP    OREQ 'D'
     C           UFRNM     IFNE 'Y'
     C                     CALL 'UPDRNM'
     C                     PARM           UFNEWF
     C                     PARM           UFOLDF
     C                     PARM           UFTYPE
     C                     PARM           UFFTYP
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFRNM
     C                     UPDATUFREC
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Create old logical files.
     C*
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFTYPE    IFEQ 'U'
     C           UFTYPE    OREQ 'R'
     C           UFFTYP    IFEQ 'P'
     C           UFCRTL    ANDNE'Y'
     C*
     C*     Whip through corresponding logical files and create.
     C*
     C           UFOLDF    SETLLPIRDBR1
     C           FOREVR    DOUNEFOREVR
     C           UFOLDF    READEPIRDBR1                  78
     C           *IN78     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           WHREFI    IFNE *BLANKS
     C                     CALL 'UPDCRTLF'
     C                     PARM           WHREFI
     C                     PARM           UFTYPE
402 AC                     PARM           $RLIB
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     MOVE 'Y'       UFCRTL
     C                     UPDATUFREC
     C*
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Create new logical files.
214 AC*     and logical files that are being updated.
     C*
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFFTYP    IFEQ 'L'
     C           UFCRTL    ANDNE'Y'
     C                     CALL 'UPDCRTLF'
     C                     PARM           UFOLDF
     C                     PARM           UFTYPE
402 AC                     PARM           $RLIB
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFCRTL
     C                     UPDATUFREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Execute any 'After' programs.
     C*
     C                     MOVE *ON       FOREVR  1
     C           *LOVAL    SETLLUPDFILES
     C           FOREVR    DOUNEFOREVR
     C                     READ UPDFILES                 79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           UFPGM     IFNE *BLANKS
     C           UFBA      ANDEQ'A'
     C           UFCVT     ANDNE'Y'
     C                     CALL 'UPDCALL'
     C                     PARM           $RLIB
     C                     PARM           $RLIBF
     C                     PARM           UFPGM
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C                     MOVE 'Y'       UFCVT
     C                     UPDATUFREC
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C*  Finally, update REPORTS file with any new reports.
     C*
     C                     CALL 'UPDRPTCL'
416  C                     PARM           $RLIB
416  C                     PARM           $RLIBF
402 AC                     PARM           $RLIBF
     C                     PARM           $PRTN
     C           $PRTN     CABEQ'*ERROR  'BOTTOM
     C*
     C*  Finished.
     C*
     C           BOTTOM    TAG
416aAC*
416aAC*  On error, send messages back to calling program.
416aAC*
416aAC           $PRTN     IFEQ '*ERROR'
416aAC                     CALL 'PUTMSGPA'
416aAC                     PARM 'UPDATECL'PRVPGM 10
416aAC                     ENDIF
     C*
     C                     MOVE *ON       *INLR
