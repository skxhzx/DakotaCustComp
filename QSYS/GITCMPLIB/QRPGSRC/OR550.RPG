     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  OR550     Route loading sheet
     F*  Dave Sommerville
     F*  10 October 1994
     F*
     F*  Revisions
214 AF*    07/15/96  DAS
     F*      - Change OR550PR to use OHCORD instead of LBORD.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FOR550   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FOR550PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E                    WDP        50  7
     I*----------------------------------------------------------------
     IOQREC
     I                                              LBPBATL1
     I                                              LBWHSEL1
     I                                              LBRTE L1
     I*----------------------------------------------------------------
     I*  Department/Section summary data structure.
     I*
     IDSSUM       DS                         50
     I                                        1   7 DSSORT
     I                                        1   5 DSWHDP
     I                                        6   7 DSPNTR
     I                                        8  120DSFPCS
     I                                       13  203DSFCUB
     I                                       21  292DSFWGT
     I                                       30  340DSPPCS
     I                                       35  423DSPCUB
     I                                       43  512DSPWGT
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PBAT   Batch number
     C*      $PWHSE  Warehouse
     C*      $PRTE   Route
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $CMD    8
     C                     PARM           $PBAT   70
     C                     PARM           $PWHSE  30
     C                     PARM           $PRTE   5
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C*
     C           *IN90     IFEQ '1'
     C           *INL1     OREQ '1'
     C                     EXSR ZRZERO
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break - Stop/Department
     C*
     C           *INL1     IFEQ '1'
     C                     Z-ADD1         L
     C                     END
     C*
     C*  Output Headings
     C*
     C           *IN91     IFEQ '1'
     C           *INL1     OREQ '1'
     C           *IN91     IFEQ '1'
     C                     WRITERPHEAD
     C                     END
     C                     WRITECOLUMNS
     C                     END
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*  Write detail line
     C*
     C           OQPPCS    ADD  OQFPCS    PIECES
     C                     Z-ADDLBSTOP    W1STOP
     C                     WRITEDETAIL1
     C*
     C*  Update summary totals
     C*
     C                     MOVE *OFF      FOUND   1
     C           1         DO   SUMCNT    X       20
     C           X         OCUR DSSUM
     C           DSWHDP    IFEQ LBWHDP
     C                     ADD  OQPPCS    DSPPCS
     C                     ADD  OQPCUB    DSPCUB
     C                     ADD  OQPWGT    DSPWGT
     C                     ADD  OQFPCS    DSFPCS
     C                     ADD  OQFCUB    DSFCUB
     C                     ADD  OQFWGT    DSFWGT
     C                     MOVE *ON       FOUND
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           FOUND     IFEQ *OFF
     C                     ADD  1         SUMCNT  20
     C           SUMCNT    OCUR DSSUM
     C                     MOVE SUMCNT    DSPNTR
     C                     MOVE LBWHDP    DSWHDP
     C                     Z-ADDOQPPCS    DSPPCS
     C                     Z-ADDOQPCUB    DSPCUB
     C                     Z-ADDOQPWGT    DSPWGT
     C                     Z-ADDOQFPCS    DSFPCS
     C                     Z-ADDOQFCUB    DSFCUB
     C                     Z-ADDOQFWGT    DSFWGT
     C                     Z-ADDSUMCNT    X
     C                     MOVE DSSORT    WDP,X
     C                     ENDIF
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN91
     C*----------------------------------------------------------------
     C*
     C* Slot type/Department totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     C*
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD2         L
     C*
     CLR         RECK,L    IFGT 0
     C*
     C*    Sort summary records.
     C*
     CLR                   SORTAWDP
     C*
     C*    Print headings.
     C*
     CLR                   MOVE 'Summary' HEDDSC
     CLR                   WRITEHEAD1
     C*R                   WRITERTHEAD
     CLR                   WRITESUMCOL
     C*
     C*    Print detail.
     C*
     CLR                   Z-ADD0         TTLPCS
     CLR                   Z-ADD0         TTLCUB
     CLR                   Z-ADD0         TTLWGT
     CLR         1         DO   SUMCNT    X
     CLR                   MOVE WDP,X     X2      20
     CLR         X2        OCUR DSSUM
     CLR                   MOVE DSWHDP    W1WHDP
     C*
     CLR         DSFPCS    IFGT 0
     CLR                   MOVE 'FIXED'   W1SECT
     CLR                   Z-ADDDSFPCS    SUBPCS
     CLR                   ADD  DSFPCS    TTLPCS
     CLR                   Z-ADDDSFCUB    SUBCUB
     CLR                   ADD  DSFCUB    TTLCUB
     CLR                   Z-ADDDSFWGT    SUBWGT
     CLR                   ADD  DSFWGT    TTLWGT
     CLR                   WRITESUMDET
     CLR                   ENDIF
     C*
     CLR         DSPPCS    IFGT 0
     CLR                   MOVE 'PIR  '   W1SECT
     CLR                   Z-ADDDSPPCS    SUBPCS
     CLR                   ADD  DSPPCS    TTLPCS
     CLR                   Z-ADDDSPCUB    SUBCUB
     CLR                   ADD  DSPCUB    TTLCUB
     CLR                   Z-ADDDSPWGT    SUBWGT
     CLR                   ADD  DSPWGT    TTLWGT
     CLR                   WRITESUMDET
     CLR                   ENDIF
     C*
     CLR                   ENDDO
     C*
     C*    Print totals.
     C*
     CLR                   WRITESUMTTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*
     C*   Initialize summary array.
     C*
     C                     MOVE *HIVAL    WDP
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'OR550PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     CSR                   MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD0         COUNT   30
     CSR                   Z-ADD2         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C           *INL1     IFEQ '0'
     C                     MOVE '1'       *IN91
     C                     END
     C*
     C                     MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     C                     MOVE *BLANKS   WHSE
     C                     MOVE *BLANKS   $PSTR
     C           $PWHSE    CHAINWAREHS               79
     C           *IN79     IFEQ *OFF
     C                     MOVELWHDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     WHSE
     C*
     C                     WRITEHEAD1
     C*                    WRITEHEAD2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
