     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IT312A    Item/Slot activity report - Write work records
     F*  Roxanne Harrison
     F*  21 May, 2003
     F*
     F*  Revisions
     F*    05/21/03  RH   4.17 Created
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPIRITEM IF  E           K        DISK
     FITEMLNK1IF  E           K        DISK
     FITEMLNK IF  E           K        DISK
     F            ILREC                             KRENAMERECORD
     FIT312   O   E           K        DISK                      A
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I*----------------------------------------------------------------
     I*
     I*  Program parameters
     I*
     I$LPARM      DS
     I                                        1   8 $LCMD
     I                                        9  18 $LPRG
     I                                       19  68 $LUKY
     I                                       69  76 $LRTN
     I                                       77 136 $LERM
     I                                       77  80 $LMSG
     I*
     I*   Redefine key
     I*
214 AI                                       19  210$LWHSE
214 AI                                       22  36 $LITEM
214 AI                                       37  51 $LITMF
214 AI                                       52  83 $LDESC
     I*
     I*----------------------------------------------------------------
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $WHSE  - Warehouse
     C*      $ITEM  - Item
     C*      $LINK  - Y if link item exists
     C*      $RTNCD - Return code
     C*
     C*    Returned Parameters
     C*      None
     C*
     C           *ENTRY    PLIST
     C                     PARM           $WHSEA  3
     C                     PARM           $ITEM  15
     C                     PARM           $LINK   1
     C                     PARM           $RTNCD  8
     C*----------------------------------------------------------------
     C*
     C*  Main line
     C*  Write entered item to work file
     C                     Z-ADD$WHSE     WKWHSE
     C                     MOVE $ITEM     WKITEM
     C                     WRITEWKREC
     C*  Write alias item to work file
     C                     Z-ADD$WHSE     KYWHSE
     C                     MOVE $ITEM     KYITEM
     C           ITKEY     CHAINPIRITEM              79
     C           *IN79     IFEQ *OFF
     C           ITTYPE    ANDEQ' '
     C           ITKEY     SETLLITEMLNK1             79
     C           STOP      DOUEQ'1'
     C           ITKEY     READEITEMLNK1                 79
     C           *IN79     IFEQ '1'
     C                     MOVE '1'       STOP
     C                     ELSE
     C                     Z-ADDILWHSE    WKWHSE
     C                     MOVE ILITEM    WKITEM
     C                     WRITEWKREC
     C                     ITER
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C*  Check if linked item exists.
     C*  Check if alias item was entered.
     C                     MOVE 'N'       $LINK
     C           ITKEY     CHAINITEMLNK              78
     C           *IN78     IFEQ *OFF
     C                     MOVE 'Y'       $LINK
     C                     ELSE
     C*  Check if base item was entered.
     C           ITKEY     CHAINITEMLNK1             78
     C           *IN78     IFEQ *OFF
     C                     MOVE 'Y'       $LINK
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVE *ON       *INLR
     C                     RETRN
     C*
     C*****************************************************************
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR                   MOVE '1'       FIRST   1
     CSR                   MOVE '0'       STOP    1
     CSR                   MOVE $WHSEA    $WHSE   30
     C*
     C*
     C*  Define keys.
     C*
     C*    PIRITEM file
     C*
     CSR         *LIKE     DEFN $ITEM     KYITEM
     CSR         ITKEY     KLIST
     CSR                   KFLD           KYWHSE  30
     CSR                   KFLD           KYITEM
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*****************************************************************
     C*
