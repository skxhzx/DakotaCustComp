     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FRMTSL    Format display slot number.
     F*  26 July 1994
     F*  Dave Sommerville
     F*
     F*  Revisions
404  F*    09/03/97  RH      4.04
     F*      - Add code to process if type is not "F" or "P".
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FOPTIONS IF  E           K        DISK
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A2          2  1
     E                    A3          3  1
     E                    A12        12  1
     I*----------------------------------------------------------------
     I*  *SLOT  -  Slot options.
     I*----------------------------------------------------------------
     I*
     I* Fields
     I*
     I*    OPPFMT  -  PIR section shelf label format.
     I*    OPFFMT  -  Fixed section shelf label format.
     I*    OPFSEQ  -  Fixed section picking sequence.
     I*
     I* Data structure
     I*
     IOPDATA      DS
     I                                        1   1 OPPFMT
     I                                        2   2 OPFFMT
     I                                        3   3 OPFSEQ
     I                                      117 117 OPTEND
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse for FA-PIR location.
     C*      $PWHDP  Department for FA-PIR location.
     C*      $PAISL  Aisle for FA-PIR location.
     C*      $PLOC   Aisle location number for FA-PIR location.
     C*      $PRLVL  Level for FA-PIR location.
     C*      $PHAND  Handstack indicator for FA-PIR location.
     C*      $PSTYP  Section type for FA-PIR location.
     C*      $PPSEQ  Pick seq aisle location.
     C*
     C*    Returned Parameters
     C*      $PDISP  Display slot number.
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PWHDP  5
     C                     PARM           $PAISL  3
     C                     PARM           $PLOC   30
     C                     PARM           $PRLVL  20
     C                     PARM           $PHAND  2
     C                     PARM           $PSTYP  1
     C                     PARM           $PPSEQ  50
     C                     PARM           $PDISP 12
     C*----------------------------------------------------------------
     C*  Define keys
     C*
     C*    OPTIONS file
     C*
     C           OPTNKY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           OPCODE
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*   Get slot format options.
     C*
     C           $PWHSE    IFNE SVWHSE
     C                     Z-ADD$PWHSE    SVWHSE  30
     C                     MOVE '*SLOT   'OPCODE
     C           OPTNKY    CHAINOPTIONS              79
     C           *IN79     IFEQ *ON
     C                     CLEAROPDATA
     C                     ENDIF
     C                     ENDIF
     C*
     C*  Format display slot for ...
     C*
     C                     SELEC
     C*
     C*    Fixed slot type - Normal FA-PIR location format.
     C*
     C           $PSTYP    WHEQ 'F'
     C           OPFFMT    ANDEQ'1'
     C                     Z-ADD$PLOC     LOCTN   30
     C                     EXSR FAFRMT
     C*
     C*    Fixed slot type - Custom format.
     C*
     C           $PSTYP    WHEQ 'F'
     C           OPFFMT    ANDEQ'2'
     C                     EXSR CUSTOM
     C*
     C*    PIR slot type - Normal FA-PIR location format.
     C*
     C           $PSTYP    WHEQ 'P'
     C           OPPFMT    ANDEQ'1'
     C                     Z-ADD$PLOC     LOCTN
     C                     EXSR FAFRMT
     C*
     C*    PIR slot type - Normal FA-PIR picking order format.
     C*
     C           $PSTYP    WHEQ 'P'
     C           OPPFMT    ANDEQ'2'
     C                     Z-ADD$PPSEQ    LOCTN
     C                     EXSR FAFRMT
     C*
     C*    PIR slot type - Custom format.
     C*
     C           $PSTYP    WHEQ 'P'
     C           OPPFMT    ANDEQ'3'
     C                     EXSR CUSTOM
     C*
404 AC                     OTHER
404 AC                     Z-ADD$PPSEQ    LOCTN
404 AC                     EXSR FAFRMT
     C*
     C                     ENDSL
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  CUSTOM  Custom display format.
     C*
     CSR         CUSTOM    BEGSR
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C*  FAFRMT  FA-PIR display format (ie. A-101-1A).
     C*
     CSR         FAFRMT    BEGSR
     C*
     CSR                   Z-ADD0         X       20
     CSR                   MOVE *BLANKS   A12
     C*
     C*    Aisle (suppress blanks)
     C*
     CSR                   MOVEA$PAISL    A3
     CSR         1         DO   3         I       20
     CSR         A3,I      IFNE ' '
     CSR                   ADD  1         X
     CSR                   MOVE A3,I      A12,X
     CSR                   ENDIF
     CSR                   ENDDO
     C*
     C*    Picking location (do not suppress zeros)
     C*
     CSR                   ADD  1         X
     CSR                   MOVE '-'       A12,X
     CSR                   ADD  1         X
     CSR                   MOVE LOCTN     WORK3A  3
     CSR                   MOVEAWORK3A    A12,X
     CSR                   ADD  3         X
     C*
     C*    Rack level (suppress zeros)
     C*
     CSR                   MOVE '-'       A12,X
     CSR                   MOVE $PRLVL    WORK2A  2
     CSR                   MOVEAWORK2A    A2
     CSR                   MOVE *ON       SUPRES  1
     CSR         1         DO   2         I
     CSR         SUPRES    IFEQ *OFF
     CSR                   ADD  1         X
     CSR                   MOVE A2,I      A12,X
     CSR                   ELSE
     CSR         A2,I      IFNE '0'
     CSR                   ADD  1         X
     CSR                   MOVE A2,I      A12,X
     CSR                   MOVE *OFF      SUPRES
     CSR                   ENDIF
     CSR                   ENDIF
     CSR                   ENDDO
     C*
     C*    Handstack indicator (suppress blanks)
     C*
     CSR                   MOVEA$PHAND    A2
     CSR         1         DO   2         I
     CSR         A2,I      IFNE ' '
     CSR                   ADD  1         X
     CSR                   MOVE A2,I      A12,X
     CSR                   ENDIF
     CSR                   ENDDO
     C*
     C*    Move display array to output parameter.
     C*
     CSR                   MOVEAA12       $PDISP
     C*
     CSR                   ENDSR
