     F*----------------------------------------------------------------
     F*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  IV172     Inventory variance re-count sheet - Print
     F*  01 May, 2002
     F*  Roxanne Harrison
     F*
     F*  Revisions:
     F*
416a F*    05/15/02  RH   4.16a
     F*      - Add total lines for pick slot items.
416a F*    05/16/02  RH   4.16b
     F*      - Add field to print if slot has exception.
416b F*    05/17/02  RH   4.16a
     F*      - Comment out total lines for pick slot items.
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*  9X        Top of page and level X continues.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FIV172   IP  E           K        DISK
     F            WKREC                             KRENAMERECORD
     FIV171   IF  E           K        DISK
     FPIRITEM IF  E           K        DISK
     FSLOT4   IF  E           K        DISK
     F*TEMQTY IF  E           K        DISK
416b FINVEXCP2IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FIV172PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E                    RPD     1   3 25
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E*
     E                    A22        22  1
     E*
     E*
     I*----------------------------------------------------------------
     I*ECORD
     I*                                             WPSWHSL4
     I*                                             WPSWHDL3
     I*                                             WPSSTPL2
     I*                                             WPSITML1
     IRECORD
     I              WKSWHS                          WPSWHSL4
     I              WKSDSP                          WPSDSP
     I              WKSSTA                          WPSSTA
     I              WKSWHD                          WPSWHDL3
     I              WKSASL                          WPSASL
     I              WKSLOC                          WPSLOC
     I              WKSLVL                          WPSLVL
     I              WKSHND                          WPSHND
     I              WKSSTP                          WPSSTPL2
     I              WKSITM                          WPSITML1
     I*----------------------------------------------------------------
     I*  Program info data structure
     I*
     I           SDS
     I                                     *PROGRAM #PROG
     I                                      244 253 #JOB
     I                                      254 263 #USER
     I                                      264 269 #JOBN
     I                                      276 2810#JOBDT
     I                                      282 2870#JOBTM
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     I            DS
     I                                        1   60T#DATE
     I                                        1   20MONTH
     I                                        3   40DAY
     I                                        5   60YEAR
     I*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse #
     C*      $PITM   Item or *ALL
416a C*      $PVAR   Minimum variance
     C*
     C*    Returned Parameters
     C*      None
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHS   3
     C                     PARM           $PITM  15
416a C                     PARM           $PVAR   5
     C                     MOVE $PWHS     $PWHSE  30
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C*
     C*  Report Heading
     C*
     C*
     C*  Start new page if one of following happened.
     C*     - Overflow occurred.
     C*
     C           *IN90     IFEQ *ON
     C           FIRST     OREQ *ON
     C           *INL4     OREQ *ON
     C           *INL3     OREQ *ON
     C           *INL2     OREQ *ON
     C           *INL1     OREQ *ON
     C                     EXSR ZRFRMT
     C                     EXSR ZRHEAD
     C                     ENDIF
     C*
     C*  Init total levels
     C*
     C*
     C*  Level 1 break -
     C*
     C*          *INL1     IFEQ *ON
     C*                    Z-ADD1         L
     C*                    EXSR ZRZERO
     C*                    ENDIF
     C*
     C*  Print headings if level break.
     C*
     C           *IN94     IFEQ *ON
     C           *INL4     OREQ *ON
     C           *IN93     OREQ *ON
     C           *INL3     OREQ *ON
     C           *IN92     OREQ *ON
     C           *INL2     OREQ *ON
     C           *IN91     OREQ *ON
     C           *INL1     OREQ *ON
     C                     WRITESBHEAD
     C                     WRITECOLUMNS
     C                     ENDIF
     C*
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C                     MOVE WPSITM    SVSITM 15
     C                     MOVE *OFF      *IN81
     C                     MOVE *OFF      PCKFLG
     C           PARTKY    SETLLIV171
     C           FOREVR    DOUNEFOREVR
     C           PARTKY    READEIV171                    79
     C           *IN79     IFEQ *ON
     C                     EXSR ZZSLOT
     C                     WRITEDETAIL3
     C                     WRITESKIP2
     C                     WRITEDETAIL3
     C                     WRITESKIP2
416b C**                   EXSR ZZTOTL
     C                     LEAVE
     C                     ENDIF
     C           WKSITM    IFNE SVSITM
     C                     EXSR ZZSLOT
     C                     WRITEDETAIL3
     C                     WRITESKIP2
     C                     WRITEDETAIL3
     C                     WRITESKIP2
416b C**                   EXSR ZZTOTL
     C                     LEAVE
     C                     ENDIF
     C*
     C*  Set pick slot flag and save pick slot quantity
416a C           WKSITM    IFEQ SVSITM
416a C           WKPICK    ANDEQ'Y'
416a C                     MOVE *ON       PCKFLG  1
416a C                     ENDIF
     C*
     C*  Calculate difference of NEW qty's SUB OLD qyt's.
     C*
     C*          WKSQT1    SUB  WKSTK1    WKDQT1
     C*          WKSQT2    SUB  WKSTK2    WKDQT2
     C*          WKSQT3    SUB  WKSTK3    WKDQT3
     C*
     C           ITKEY1    CHAINPIRITEM              78
     C           *IN78     IFEQ *OFF
     C                     MOVE ITDESC    IDESC
     C                     ELSE
     C                     MOVE *BLANKS   IDESC
     C                     ENDIF
     C           WKPICK    IFEQ 'Y'
     C                     MOVE 'Y'       WOPICK  1
     C                     ELSE
     C                     MOVE ' '       WOPICK
     C                     ENDIF
     C*
416b C* Print X if exception record exists for slot.
416b C           EXKEY2    CHAININVEXCP2             78
416b C*          *IN78     CABEQ*OFF      SKIPEX
416b C           *IN78     IFEQ *OFF
416b C                     MOVE 'X'       WOEXCP
416b C                     ELSE
416b C                     MOVE ' '       WOEXCP
416b C                     ENDIF
     C                     WRITEDETAIL1
     C                     WRITESKIP2
     C                     MOVE *ON       *IN81
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C                     ENDDO
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE *OFF      *IN91
     C                     MOVE *OFF      *IN92
     C                     MOVE *OFF      *IN93
     C                     MOVE *OFF      *IN94
     C                     MOVE *OFF      FIRST
     C*----------------------------------------------------------------
     C*
     C* Level 1 break
     C*
     C*1                   Z-ADD1         L
     C*1                   EXSR ZRROLL
     C*
     C*1         RECK,L    IFGT 0
     C*1                   EXSR ZRTOTL
     C*1                   ENDIF
     C*----------------------------------------------------------------
     C*
     C* Grand totals
     C*
     C*R                   Z-ADD3         L
     C*
     CLR         RECK,L    IFGT 0
     C*R                   EXSR ZRTOTL
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     CLR                   RETRN
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     C           *INZSR    BEGSR
     C*
     C           SLKEY     KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WPSWHD
     C                     KFLD           WPSITM
     C*
     C           IKEY71    KLIST
     C                     KFLD           SLWHSE
     C                     KFLD           SLWHDP
     C                     KFLD           SLSTYP
     C                     KFLD           SLITEM
     C                     KFLD           SLDISP
     C*
     C           PARTKY    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WPSWHD
     C                     KFLD           WPSSTP
     C                     KFLD           WPSITM
     C*
     C*
     C           RPKEY     KLIST
     C                     KFLD           WKPRTF 10
     C*
     C           ITKEY1    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WPSITM
     C*
     C           ITKEY2    KLIST
     C                     KFLD           $PWHSE
     C                     KFLD           WKITEM
416b C*
416b C           EXKEY2    KLIST
416b C                     KFLD           $PWHSE
416b C                     KFLD           WKSWHD
416b C                     KFLD           WKSDSP
     C*
     C                     TIME           TIME
     C                     MOVE *ON       FIRST   1
     C                     Z-ADD1         L       10
     C                     Z-ADD0         N       30
     C                     MOVE *BLANKS   WKBQT1 12
     C                     MOVE *BLANKS   WKBQT2 12
     C                     MOVE *BLANKS   WKBQT3 12
     C                     MOVE #PROG     $CVPRG
     CSR                   MOVE *ON       FOREVR  1
416a C           $PITM     IFNE '*ALL'
416a C                     Z-ADD0         PMVAR   50
416a C                     Z-ADD0         PNVAR   50
416a C                     ELSE
416a C                     MOVE $PVAR     PMVAR   50
416a C           PMVAR     MULT -1        PNVAR   50
416a C                     ENDIF
     C                     EXSR ZRZERO
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRFRMT    BEGSR
     C*
     C*   Format report title.
     C*
     C*  Get report title.
     C*
     C                     MOVEL'IV172PR 'WKPRTF
     C                     MOVE *BLANKS   RPTTTL
     C           RPKEY     CHAINREPORTS              79
     C           *IN79     IFEQ *OFF
     C                     MOVELRPDESC    $PSTR  60
     C                     ELSE
     C                     MOVE *BLANKS   $PSTR
     C                     ENDIF
     C                     Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     C                     CALL 'CENTER'
     C                     PARM           $PSTR
     C                     PARM           $PLEN
     C                     MOVEL$PSTR     RPTTTL
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     C*
     C           *INL1     IFEQ *OFF
     C                     MOVE *ON       *IN91
     C                     END
     C*
     C           *INL2     IFEQ *OFF
     C                     MOVE *ON       *IN92
     C                     END
     C*
     C           *INL3     IFEQ *OFF
     C                     MOVE *ON       *IN93
     C                     END
     C*
     C                     MOVE *OFF      *IN90
     C*
     C                     WRITEHEAD1
     C                     WRITESKIP1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C*
     C                     MOVE RPD,L     $RPD   25
     C*
     C*                    WRITETOTAL1
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZZSLOT    BEGSR
     C*
     C           PCKFLG    IFEQ *ON
     C           SLKEY     SETLLSLOT4
     C           FOREVR    DOUNEFOREVR
     C           SLKEY     READESLOT4                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           IKEY71    CHAINIV171                77
     C           *IN77     IFEQ *OFF
     C                     ITER
     C                     ELSE
     C                     WRITEDETAIL2
     C                     WRITESKIP2
     C*  Increment record counters
     C*
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C*
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZZTOTL    BEGSR
     C*
     C*
     C           PCKFLG    IFEQ *ON
     C                     MOVE RPD,L     $RPD   25
     C*
     C                     Z-ADD0         TLSTK1
     C                     Z-ADD0         TLSTK2
     C           SLKEY     SETLLSLOT4
     C           FOREVR    DOUNEFOREVR
     C           SLKEY     READESLOT4                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ELSE
     C           SLPICK    IFNE 'Y'
     C                     ADD  SLSTK1    TLSTK1
     C                     ADD  SLSTK2    TLSTK2
     C                     ENDIF
     C                     ENDIF
     C                     ITER
     C                     ENDDO
     C*
     C                     WRITETOTAL1
     C                     ENDIF
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
      *----------------------------------------------------------------
      *
      * Compile time tables
      *
**      RPD   Report level descriptions
 * Not used *
 Average for week:
 *** Grand totals ***
