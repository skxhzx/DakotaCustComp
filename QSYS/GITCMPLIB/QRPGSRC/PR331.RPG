     F*----------------------------------------------------------------
     F*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     F*   BFC Associates Inc.
     F*   245 Roosevelt Rd.  Bldg 8, Suite 51
     F*   West Chicago, IL 60185
     F*   (630) 562-0375
     F*----------------------------------------------------------------
     F*
     F*  PR331     Print Priority Replenishment report
     F*  Bob Perry
     F*  19 January 2005
     F*
     F*  01/20/05  RLP 5.00
     F*  - Created using RL342
     F*
     F*  Revisions
     F*
     F*
     F*
     F*----------------------------------------------------------------
     F*  Indicator usage
     F*
     F*  79        Not on file/End of file indicator
     F*  90        Overflow Indicator
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FPR331   IP  E           K        DISK
     FWAREHS  IF  E           K        DISK
     FREPORTS IF  E           K        DISK
     FPR331PR O   E             90     PRINTER
     E*----------------------------------------------------------------
     E*
     E*  Table and array
     E*
     E*                   RPD     1   3 30
     E                    RECK        3  7 0
     E                    LVLK        3  7 0
     E                    A22        22  1
     E*
     I*----------------------------------------------------------------
     IPRREC
     I                                              PRWHSE
     I                                              PRWHDPL2
     I                                              PRFDISL1
     I*----------------------------------------------------------------
     I*  Called programs
     I*
     I              'CVTDTE'              C         @CVTDT
     I$CVTDT      DS
     I                                        1   8 $CVCMD
     I                                        9  18 $CVPRG
     I                                       19  24 $CVD6I
     I                                       25  32 $CVD8I
     I                                       33  380$CVD6O
     I                                       39  460$CVD8O
     I                                       47  64 $CVSTO
     I                                       65  72 $CVRTN
     I                                       73 132 $CVERM
     I                                       73  76 $CVMSG
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  3
     C                     PARM           $PWHDP  5
     C*
     C*----------------------------------------------------------------
     C*
     C*  Report Heading
     C*
     C           *IN90     IFEQ '1'
     C           FIRST     OREQ *ON
     C                     EXSR ZRHEAD
     C                     END
     C*
     C*  Init total levels
     C*
     C*  L2 break - DEPT change.
     C*
     C           *INL2     IFEQ '1'
     C                     Z-ADD2         L
     C                     EXSR ZRZERO
     C           FIRST     IFEQ *OFF
     C                     MOVE '1'       *IN92
     C                     ENDIF
     C                     END
     C*
     C*  L1 break - SLOT change.
     C*
     C*          *INL1     IFEQ '1'
     C*                    MOVE '1'       *IN91
     C*                    Z-ADD1         L
     C*                    EXSR ZRZERO
     C*                    END
     C*
     C*
     C*  Output DEPT Heading
     C*
     C           *IN92     IFEQ *ON
     C           *INL2     OREQ *ON
     C           RECK,1    IFGT 0
     C                     WRITEDPHEAD
     C                     ENDIF
     C                     ENDIF
     C*----------------------------------------------------------------
     C*
     C* Detail Calcs
     C*
     C*
     C*  Write detail line - only processing OPEN replenishments
     C*
     C           PRFLAG    IFEQ 'S'
     C                     WRITEDETAIL1
     C*
     C*  Increment record counters
     C*
     C                     ADD  1         SLCNT
     C                     ADD  1         COUNT
     C                     ADD  1         RECK,1
     C                     ADD  1         LVLK,1
     C                     ENDIF
     C*
     C*  Set off top-of-page indicators
     C*
     C                     MOVE '0'       *IN91
     C                     MOVE '0'       *IN92
     C                     MOVE *OFF      FIRST
     C                     MOVE *OFF      STOP    1
     C*----------------------------------------------------------------
     C*
     C* Slot totals
     C*
     CL1                   Z-ADD1         L
     CL1                   EXSR ZRROLL
     CL1                   Z-ADDSLCNT     TRNCNT
     C*1                   WRITETOTAL1
     C*1                   Z-ADD0         SLCNT
     C*
     C*----------------------------------------------------------------
     C*
     C* Department totals
     C*
     CL2                   Z-ADD2         L
     CL2                   EXSR ZRROLL
     CL2                   Z-ADDCOUNT     TRNCNT
     CL2         RECK,L    IFGT 0
     CL2                   WRITETOTAL1
     CL2                   ENDIF
     CL2                   Z-ADD0         COUNT
     C*
     C*----------------------------------------------------------------
     C*
     C* REPORT TOTALS
     C*
     CLR                   Z-ADD1         L
     C*
     CLR         RECK,L    IFGT 0
     CLR                   WRITESKIP1
     CLR                   WRITEFOOT1
     CLR                   ELSE
     CLR         FIRST     IFEQ *ON
     CLR                   EXSR ZRHEAD
     CLR                   ENDIF
     CLR                   WRITENOREC
     CLR                   WRITEFOOT1
     CLR                   ENDIF
     C*----------------------------------------------------------------
     C*
     C*          SUBROUTINES IN ALPHABETICAL ORDER
     C*
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     C*
     CSR         RPKEY     KLIST
     CSR                   KFLD           WKPRTF 10
     C*   Query key
     CSR         DETKEY    KLIST
     CSR                   KFLD           PRWHSE
     CSR                   KFLD           PRWHDP
     C*
     C*   Get report title.
     C*
     CSR                   MOVEL'PR331PR 'WKPRTF
     CSR                   MOVE *BLANKS   RPTTTL
     C                     MOVE *BLANKS   $PSTR
     CSR         RPKEY     CHAINREPORTS              79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELRPDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD40        $PLEN   30
     C*
     C*   Center report title.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     RPTTTL
     C*
     C*  Initialize variables
     C*
     CSR                   TIME           TIME
     CSR                   Z-ADD0         COUNT   30
     CSR                   Z-ADD0         SLCNT   30
     CSR                   MOVE *ON       FIRST   1
     CSR                   Z-ADD1         L       10
     CSR                   EXSR ZRZERO
     CSR                   ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRHEAD    BEGSR
     C*
     CSR         *INL1     IFEQ '0'
     CSR                   MOVE '1'       *IN91
     CSR                   END
     C*
     CSR         *INL2     IFEQ '0'
     CSR                   MOVE '1'       *IN92
     CSR                   END
     C*
     CSR                   MOVE '0'       *IN90
     C*
     C*   Get warehouse description.
     C*
     CSR                   MOVE *BLANKS   WHSE   50
     CSR                   MOVE *BLANKS   $PSTR
     CSR         PRWHSE    CHAINWAREHS               79
     CSR         *IN79     IFEQ *OFF
     CSR                   MOVELWHDESC    $PSTR  60
     CSR                   ELSE
     CSR                   MOVE *BLANKS   $PSTR
     CSR                   ENDIF
     CSR                   Z-ADD30        $PLEN   30
     C*
     C*   Center warehouse description.
     C*
     CSR                   CALL 'CENTER'
     CSR                   PARM           $PSTR
     CSR                   PARM           $PLEN
     CSR                   MOVEL$PSTR     WHSE
     CSR                   MOVEL$PSTR     WKWHSE
     C*
     CSR                   WRITEHEAD1
     CSR                   WRITECOLUMNS1
     CSR                   WRITECOLUMNS2
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRROLL    BEGSR
     C*
     C           *LIKE     DEFN L         M
     C           L         ADD  1         M
     C*
     C                     ADD  1         LVLK,M
     C                     ADD  RECK,L    RECK,M
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRTOTL    BEGSR
     C*
     C           L         IFEQ 1
     C*                    WRITETOTAL1
     C                     END
     C*
     C                     ENDSR
     C*----------------------------------------------------------------
     C*
     C           ZRZERO    BEGSR
     C*
     C                     Z-ADD0         LVLK,L
     C                     Z-ADD0         RECK,L
     C*
     C                     ENDSR
     O*----------------------------------------------------------------
     O*
     O* Compile time tables
     O*
