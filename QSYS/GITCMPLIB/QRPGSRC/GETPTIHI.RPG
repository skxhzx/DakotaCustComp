     F*----------------------------------------------------------------
     F*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  GETPTIHI  Get preferred tie/high for item
     F*  11 March 2001
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
415 AF*   03/11/01  DAS  4.15
     F*     -Created.
     F*
     F*----------------------------------------------------------------
     F*  Notes
     F*
     F*   This will return either the first tie/high that is marked
     F*   as preferred or the first tie/high alphabetically if none
     F*   are marked as preferred.
     F*
     F*----------------------------------------------------------------
     F*  File Specs
     F*
     FITEMDEF IF  E           K        DISK
     I*----------------------------------------------------------------
     I*
     I*  Input parameters
     I*
     I*     $PWHSE   -  Warehouse
     I*     $PITEM   -  Item
     I*
     I*  Output parameters
     I*
     I*     $PTIE    - Preferred Tie
     I*     $PHIGH   - Preferred High
     I*     $PTIHI   - Combined tie/high for display purposes.
     I*     $PRTN    - Not used
     I*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PITEM 15
     C                     PARM           $PTIE   40
     C                     PARM           $PHIGH  20
     C                     PARM           $PTIHI  7
     C                     PARM           $PRTN   8
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C*    Initialize return parameters.
     C*
     C                     MOVE *BLANKS   $PRTN
     C*
     C                     Z-ADD0         PRFTI   40
     C                     Z-ADD0         PRFHI   20
     C                     Z-ADD0         SVTIE   40
     C                     Z-ADD0         SVHI    20
     C*
     C           KEYID     SETLLITEMDEF
     C           FOREVR    DOWEQFOREVR
     C           KEYID     READEIDREC                    79
     C           *IN79     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C           SVTIE     IFEQ 0
     C           SVHI      ANDEQ0
     C                     Z-ADDIDTIE     SVTIE
     C                     Z-ADDIDHIGH    SVHI
     C                     ENDIF
     C           IDPREF    IFEQ 'Y'
     C                     Z-ADDIDTIE     PRFTI
     C                     Z-ADDIDHIGH    PRFHI
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C           PRFTI     IFEQ 0
     C           PRFHI     ANDEQ0
     C                     Z-ADDSVTIE     PRFTI
     C                     Z-ADDSVHI      PRFHI
     C                     ENDIF
     C*
     C                     Z-ADDPRFTI     $PTIE
     C                     Z-ADDPRFHI     $PHIGH
     C*
     C                     CALL 'FRMTTIHI'
     C                     PARM           PRFTI
     C                     PARM           PRFHI
     C                     PARM           $PTIHI
     C*
     C                     RETRN
     C*----------------------------------------------------------------
     C*
     C*  *INZSR  Initialization subrotine
     C*
     CSR         *INZSR    BEGSR
     CSR                   MOVE *ON       FOREVR  1
     C*
     C*  Define partial key for ITEMDEF file.
     C*
     CSR         KEYID     KLIST
     CSR                   KFLD           $PWHSE
     CSR                   KFLD           $PITEM
     C*
     CSR                   ENDSR
