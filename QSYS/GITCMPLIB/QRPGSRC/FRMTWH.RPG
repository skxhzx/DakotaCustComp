     F*----------------------------------------------------------------
     F*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     F*   BFC Software, Inc.
     F*   799 Roosevelt Rd.  Bldg 6, Suite 108
     F*   Glen Ellyn, IL  60137
     F*   (708) 790-8383
     F*----------------------------------------------------------------
     F*
     F*  FRMTWH    Format warehouse number and description.
     F*  22 May 1996
     F*  Dave Sommerville
     F*
     F*  Revisions
     F*
404 AF*    09/05/97  DAS  4.04
     F*      - Fixed problem with warehouse numbers greater than 100.
     F*
     E*----------------------------------------------------------------
     E*  Table and array definitions
     E*
     E                    A3          3  1
     C*----------------------------------------------------------------
     C*  Parameters
     C*
     C*    Input Parameters
     C*      $PWHSE  Warehouse number.
     C*      $PDESC  Warehouse descripiton.
     C*
     C*    Returned Parameters
     C*      $PSTR   Description string .
     C*
     C*----------------------------------------------------------------
     C*  Let the show begin .....
     C*
     C           *ENTRY    PLIST
     C                     PARM           $PWHSE  30
     C                     PARM           $PDESC 30
     C                     PARM           $PSTR  40
     C*----------------------------------------------------------------
     C*  Main line
     C*
     C                     MOVE *BLANKS   $PSTR
     C*
     C*  Left justify warehouse number (suppress zeros)
     C*
     C                     Z-ADD0         X       20
     C                     MOVE *OFF      FLAG    1
     C                     MOVE $PWHSE    TEMP3A  3
     C                     MOVEATEMP3A    A3
404 AC           A3,1      IFEQ '0'
404 DC*          1         DO   3         I       20
404 MC           2         DO   3         I       20
     C           A3,I      IFNE '0'
     C           FLAG      OREQ *ON
     C                     ADD  1         X
     C                     MOVE A3,I      A3,X
     C                     MOVE *ON       FLAG
     C                     ENDIF
     C                     MOVE ' '       A3,I
     C                     ENDDO
404 AC                     ENDIF
     C*
     C*  Put everything together
     C*
     C                     MOVEAA3        $PSTR
     C           $PSTR     CAT  '-':1     $PSTR
     C           $PSTR     CAT  $PDESC:1  $PSTR
     C*
     C                     RETRN
