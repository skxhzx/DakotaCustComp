     A*%%TS  SD  20110519  103414  JEFF        REL-V6R1M0  5761-WDS
     *----------------------------------------------------------------
     *  For some clients, use CHGDSPF to set RSTDSP to *YES for the
     *  screen specs.  When running pgms OR910 or OR920 using iSeries
     *  Access, and the user selects an item with an error, OR200
     *  would blow up with an I/O error.
     *----------------------------------------------------------------
500fAA*----------------------------------------------------------------
500fAA*   Multilingual Version
500fAA*   Every screen has a corresponding Spanish screen.
500fAA*   All Spanish screens are at the end.
500fAA*   Any changes must be made to both screens.
500fAA*   If necessary, contact appropriate person to do translation.
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A********************************************************************
     A*
     A* NOTE: COMPILE WITH DEFER WRITE *NO.
     A*
     A********************************************************************
     A*
415bAA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS.
     A*
     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  OR200FM      RF single order pick
     A*  29 May 2002
     A*  David Sommerville
     A*
     A*  Revisions
     A*
416 AA*  05/29/02  DAS  4.16
     A*    - Created.
     A*
416aAA*  07/18/02  DAS  4.16a
     A*    - Added F4=Slots to SCREEN62
     A*
416bAA*  07/19/02  DAS  4.16b
     A*    - Revised SCREEN65 to use F7/F8 instead of F10.
     A*    - Removed pallet # from SCREEN66.
     A*    - Created SCREEN68 for pallet #.
416cAA*  07/19/02  MLB  4.16c
     A*    - Formatted screens 1 - 8 for 8x40 support.
416dAA*  07/29/02  MLB  4.16d
     A*    - Added CHECK(ER) to Screen 6, Screen 66.
     A*
416eAA*  07/30/02  DAS  4.16e
     A*    - Added CHECK(ER) to Screen 8, Screen 68.
     A*
416fAA*  08/01/02  DAS  4.16f
     A*    - Revised small screens to use WOITM2 instead of WOITEM.
     A*      WOITM2 is a combination of item/desc/pack.
     A*    - Removed description field from small screens.
     A*    - Rearranged fields on SCREEN3 to show UOM.
     A*    - Chopped wording on SCREEN 5 to fit in 20 character space.
416gAA*  10/17/02  MLB  4.16g
     A*    - Enh: Added CA10 function key with indicator to screen 9
     A*      and screen 69.
416hAA*  10/24/02  RH   4.16h
     A*    - Enh: Change screen 2 to display item description in
     A*      2 character fields. (WOITMA & WOITMB)
500aAA*  07/19/05  RH   5.00a
     A*    - Enh: Increase W6SCAN equal to SLDISP (12 char)
500bAA*  07/19/05  DAS  5.00b
     A*    - Fix: Changed CLRL(08) to CLRL(07) because it was erasing
     A*      the message line.
500cAA*  07/20/05  RH   5.00c
     A*    - Fix: Removed CLRL from records.  Writing CLRSCR before each
     A*      EXFMT.  OVRLAY must be in records for error message to show
     A*      through.  See IT222 4.15
500dAA*  08/23/05  RH   5.00d
     A*    - ENH: Add pallet license field W6LIC to stage screens.
     A*      Add # of pallet licnese to be staged to stage screens.
500eAA*  09/21/05  RH   5.00e
     A*    - FIX: Move W2LIC from line 12, it was overwriting error msg
     A*           Add Protect to field so cursor won't position there.
     A*
500fAA*   06/06/06  JCJ  5.00f
     A*     - Merged Spanish screens from PIRS.
640aAA*   05/05/11  RH   6.40a
     A*     - ENH: Renamed screen format for web facing.
     A*     - ENH: Add screen10, scrn10sp, scrn60sp, screen60 for UPC
640bAA*   05/06/11  RH   6.40b
     A*     - ENH: Renamed screen format for web facing.
     A*       Add screen4a, scrn4asp, scrn6Asp, screen6A for Total
     A*       weight and quantity entry.
640cAA*   05/18/11  JCJ  6.40c
     A*     - ENH: Renamed screen format for web facing.
     A*       Add screen11, screen71, scrn11sp, scrn71sp for scanning
     A*       of license plate.
640dAA*   05/24/11  RH   6.40d
     A*     - ENH: Renamed screen format for web facing.
     A*     - ENH: Remove F7=New pallet from screens.
     A*            Can re-instate later to deal with double pallets
700aAA*  04/05/16  LMC  7.00a
     A*    - Renamed from OR20004 to OR20005
     A*    - Enh: Adding logic for Metric.
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A**                                    CA12
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500 DA**                                    CLRL(08)
500cDA*                                     CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR200.01'
     A                                  1 11'Single Pick'
     A                                      DSPATR(HI)
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  4  2'Order:'
     A            W1ORD          7Y 0B  4  9EDTCDE(4)
     A                                      DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20A  O  5  2
     A            WOINF2        20A  O  6  2
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA04
640cAA                                      CA06
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                      CSRLOC(WOROW      WOCOL)
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR200.02'
     A                                  1 11'Single Pick'
     A                                  2  1'Ord:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Itm:'
416h A*           WOITM2        70A  O  3  6
416h A            WOITMA        15A  O  3  6
416h A            WOITMB        55A  O  4  6
416h A                                  5  1'Slt:'
416h A            WODISP        12A  O  5  6
416h A*                                 4  1'Slt:'
416h A*           WODISP        12A  O  4  6
     A                                  6  1'Slot:'
     A            W2SLOT        12A  B  6  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N94                                  CHECK(ER)
640cDA***                               7  2'F4=Slots F12=Cancel'
640cDA                                  7  2'F4=Slots F6=License F12=Cancel'
     A                                      COLOR(BLU)
500eDA*                                12  1'Lic#:'
500eMA                                  6 21'Lic#:'
     A N94                                  DSPATR(ND)
500eDA**          W2LIC         15A  B 12  7
500eMA            W2LIC         15A  B  6 27
     A N94                                  DSPATR(ND)
500e A N94                                  DSPATR(PR)
     A  94                                  CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  2 14'/'
640a A          R SCREEN10
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A*                                     CLRL(15)
    AA                                      OVERLAY
     A                                  1  2'OR200.10'
     A                                  1 11'Single Pick'
     A                                  2  1'Ord:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A                                  2 14'/'
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Itm:'
     A            WOITM2        70A  O  3  6
     A*                                 4  1'Slt:'
     A*           WODISP        12A  O  4  6
     A                                  5  1'UPC:'
     A            WIUPCD        50A  B  5  6CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  7  2'F9=Skip F12=Cancel'
640a A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.03'
     A                                  1 11'Single Pick'
     A                                  2  1'Ord:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A                                  2 14'/'
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Itm:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Slt:'
     A            WODISP        12A  O  4  6
     A                                  5  1'Qty:'
     A            WOQTY          5Y 0O  5  7EDTCDE(4)
     A                                  5 14'Picked'
     A                                      DSPATR(UL)
     A            WOUM           5A  O  6  1
     A            W3QTY          5Y 0B  6  7EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      CHECK(ER)
     A            WOPCK          5Y 0O  6 15EDTCDE(4)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA07
640bAA                                      CA09
     A                                      CA12
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.04'
     A                                  1 11'Single Pick'
     A                                  2  1'Ord:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Itm:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Qty:'
     A            WOQTY          5Y 0O  4  6EDTCDE(4)
     A            WOUM           5A  O  4 12
700aDA*                                 4 19'Weight'
700aMA                                  4 19'Wt'
700aAA            W4KGLB         3A  O  4 22DSPATR(HI)
     A            W4CNT          3Y 0O  4 26EDTCDE(4)
     A                                  4 30'of'
     A            W4QTY          3Y 0O  4 33EDTCDE(4)
     A                                  5  1'Pck:'
     A            WOPCK          5Y 0O  5  6EDTCDE(4)
     A                                  5 19'Prv wgt:'
     A            W4LAST         7Y 2O  5 28EDTCDE(4)
     A            W4CW          50A  B  6  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O  7  1COLOR(BLU)
700aDA*                                 7  1'F7=Prev  F9=Total  F13=No More'
640bDA*                                 7  1'F7=Prev  F13=No More'
700aDA*                                     COLOR(BLU)
     A                                  2 14'/'
640bAA          R SCREEN4A
640bAA*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
640bAA                                      CA03
640bAA**                                    CA07
640bAA                                      CA12
640bAA**                                    CA13
700aAA                                      CA19
640bAA  98                                  ALARM
640bAA                                      OVERLAY
640bAA                                  1  2'OR20004A'
640bAA                                  1 11'Single Pick'
640bAA                                  2  1'Ord:'
640bAA            WOORD          7Y 0O  2  6EDTCDE(4)
640bAA                                  2 14'/'
640bAA            WOLINE         5Y 0O  2 16EDTCDE(4)
640bAA                                  3  1'Itm:'
640bAA            WOITM2        70A  O  3  6
640bAA                                  4  1'Qty:'
640bAA            WOQTY          5Y 0O  4  6EDTCDE(4)
640bAA            WOUM           5A  O  4 12
640bAA**                                4 19'Weight'
640bAA**          W4CNT          3Y 0O  4 26EDTCDE(4)
640bAA**                                4 30'of'
640bAA**          W4QTY          3Y 0O  4 33EDTCDE(4)
640bAA                                  4 19'Pck:'
640bAA            WOPCK          5Y 0O  4 24EDTCDE(4)
640bAA                                  5  1'Tot:'
640bAA            W4TQT          5Y 0B  5  6EDTCDE(4)
640bAA N23                                  DSPATR(HI)
640bAA  23                                  DSPATR(RI)
640bAA N23                                  DSPATR(UL)
640bAA  03                                  DSPATR(PC)
640bAA                                      CHECK(ER)
700aAA            W4KGLB         3A  O  6  1DSPATR(HI)
700aDA*           W4CW          50A  B  6  1
700aMA            W4CW          50A  B  6  5
640bAA N21                                  DSPATR(HI)
640bAA  21                                  DSPATR(RI)
640bAA  01                                  DSPATR(PC)
640bAA N21                                  DSPATR(UL)
640bAA                                      CHECK(ER)
700aAA            CMDLN1        79A  O  7  1COLOR(BLU)
700aDA*                                 7  1'F12=Cancel'
700aDA*                                     COLOR(BLU)
     A          R SCREEN5
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
640dDA*                                     CA07
     A                                      CA08
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.05'
     A                                  1 11'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Enter=Select item'
640dDA*                                 5  1'F7=Start new pallet'
     A                                  6  1'F8=Finish and stage'
640dDA*                                 7  2'F7=New Plt  F8=Done'
640dMA                                  7  2'            F8=Done'
     A                                      COLOR(BLU)
     A          R SCREEN6
     A*%%TS  SD  20050824  101815  ROCKY       REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR200.06'
     A                                  1 11'Single Pick'
     A                                  2  2'Ord:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  2 15'Stg'
     A            WOLCTR         3Y 0O  2 19EDTCDE(4)
     A                                  3  2'license:'
     A            W6LIC          7Y 0B  3 11EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  4  2'Stage location:'
     A            W6STAG        12A  B  5  4CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN7
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.07'
     A                                  1 11'Single Pick'
     A                                  2  2'Ord:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  4  1'Pallet license:'
     A            W8LIC          7Y 0B  5  6EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN8
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500b A*                                     CLRL(15)
500c A*                                     CLRL(07)
500cAA                                      OVERLAY
     A                                  1  2'OR200.08'
     A                                  1 11'Single Pick'
     A                                  2  2'Ord:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  4  4'Pallet license:'
     A            W8LIC          7Y 0B  5  7EDTCDE(4)
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN9
     A*%%TS  SD  20020719  183236  BFCMAX      REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR200.09'
     A                                  1 11'Single Pick'
     A                                  2  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  3  1
     A            WOLN02        20A  O  3 22
     A            WOLN03        20A  O  4  1
     A            WOLN04        20A  O  4 22
     A            WOLN05        20A  O  5  1
     A            WOLN06        20A  O  5 22
     A            WOLN07        20A  O  6  1
     A            WOLN08        20A  O  6 22
     A            WOLN09        20A  O  7  1
     A            WOLN10        20A  O  7 22
     A                                  8  1'F12=Cancel'
     A                                      COLOR(BLU)
640cAA          R SCREEN11
640cAA*%%TS  SD  20110519  103414  JEFF        REL-V6R1M0  5761-WDS
640cAA                                      CA03
640cAA                                      CA12
640cAA  98                                  ALARM
640cAA                                      OVERLAY
640cAA                                  1  2'OR200.11'
640cAA                                  1 11'Single Pick'
640cAA                                  2  2'Ord:'
640cAA            WOORD          7Y 0O  2  7EDTCDE(4)
640cAA                                  4  2'License:'
640cAA            W8LICA        15A  B  5  2
640cAA                                      CHECK(ER)
640cAA  21                                  DSPATR(RI)
640cAA  01                                  DSPATR(PC)
640cAA N21                                  DSPATR(UL)
640cAA                                  7  1'F12=Cancel'
640cAA                                      COLOR(BLU)
415bAA*****************************************************************
415bAA*
415bAA*  16 line screens.
415bAA*
     A          R SCREEN61
     A*%%TS  SD  20020603  114115  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20061'
     A                                  1 10'Single Pick'
     A                                      DSPATR(HI)
     A                                  4  2'Whse:'
     A            W1WHSE         3Y 0B  4  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  5  2'Order:'
     A            W1ORD          7Y 0B  5  9EDTCDE(4)
     A                                      DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20   O  8  2
     A            WOINF2        20   O  9  2
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN62
     A*%%TS  SD  20020529  133812  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
416aAA                                      CA04
640cAA                                      CA06
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
     A                                      CSRLOC(WOROW WOCOL)
500c A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'OR20062'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Slt:'
     A            WODISP        12A  O  7  6
     A                                 11  1'Slot:'
     A            W2SLOT        12A  B 11  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N94                                  CHECK(ER)
     A                                 12  1'Lic#:'
     A N94                                  DSPATR(ND)
     A            W2LIC         15A  B 12  7
     A N94                                  DSPATR(ND)
500eAA N94                                  DSPATR(PR)
     A  94                                  CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
640cD ***                              15  1'F4=Slots F12=Cancel'
640cMA                                 15  1'F4=Slots F6=License F12=Cancel'
     A                                      COLOR(BLU)
640a A          R SCREEN60
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A                                      CSRLOC(WOROW WOCOL)
500c A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'OR20060'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A**                                7  1'Slt:'
     A**          WODISP        12A  O  7  6
     A                                  8  1'UPC:'
     A            WIUPCD        50A  B  8  6CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                 15  2'F9=Skip F12=Cancel'
640a A                                      COLOR(BLU)
     A          R SCREEN63
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20063'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Slt:'
     A            WODISP        12A  O  7  6
     A                                  8  1'Qty:'
     A            WOQTY          5Y 0O  8  6EDTCDE(4)
     A                                 10 16'Picked'
     A                                      DSPATR(UL)
     A            WOUM           5A  O 11  2
     A            W3QTY          5Y 0B 11 10EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      CHECK(ER)
     A            WOPCK          5Y 0O 11 16EDTCDE(4)
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN64
     A*%%TS  SD  20020530  204022  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA07
640bAA                                      CA09
     A                                      CA12
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20064'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Qty:'
     A            WOQTY          5Y 0O  7  6EDTCDE(4)
     A            WOUM           5A  O  7 12
     A                                  8  1'Pck:'
     A            WOPCK          5Y 0O  8  6EDTCDE(4)
700aDA*                                10  1'Weight'
700aMA                                 10  1'Wt'
700aAA            W4KGLB         3A  O 10  4DSPATR(HI)
     A            W4CNT          3Y 0O 10  8EDTCDE(4)
     A                                 10 12'of'
     A            W4QTY          3Y 0O 10 15EDTCDE(4)
     A            W4CW          50A  B 11  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
     A                                 13  1'Prv wgt:'
     A            W4LAST         7Y 2O 13 10EDTCDE(4)
700aAA            CMDLN1        79A  O 15  1COLOR(BLU)
700aDA*                                15  1'F7=Prev  F9=Total  F13=No More'
640bDA*                                15  1'F7=Prev  F13=No More'
700aDA*                                     COLOR(BLU)
416bDA*                                     CA10
640bAA          R SCREEN6A
640bAA*%%TS  SD  20020530  204022  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
640bDA*                                     CA07
     A                                      CA12
640bDA*                                     CA13
700aAA                                      CA19
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR2004A'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Qty:'
     A            WOQTY          5Y 0O  7  6EDTCDE(4)
     A            WOUM           5A  O  7 12
     A                                  8  1'Pck:'
     A            WOPCK          5Y 0O  8  6EDTCDE(4)
     A**                               10  1'Weight'
     A**          W4CNT          3Y 0O 10  8EDTCDE(4)
     A**                               10 12'of'
     A**          W4QTY          3Y 0O 10 15EDTCDE(4)
640bAA                                 10  1'Tot:'
640bAA            W4TQT          5Y 0B 10  6EDTCDE(4)
640bAA N23                                  DSPATR(HI)
640bAA  23                                  DSPATR(RI)
640bAA N23                                  DSPATR(UL)
640bAA  03                                  DSPATR(PC)
640bAA                                      CHECK(ER)
700aAA            W4KGLB         3A  O 11  1DSPATR(HI)
700aDA*           W4CW          50A  B 11  1
700aMA            W4CW          50A  B 11  5
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O 15  1COLOR(BLU)
700aDA*                                15  1'F12=Cancel'
700aDA*                                     COLOR(BLU)
     A          R SCREEN65
     A*%%TS  SD  20020719  051440  DAVE        REL-V4R4M0  5769-PW1
640dDA*                                     CA07
     A                                      CA08
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20065'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  5  4'Press Enter to'
     A                                  6  2'select another item'
640dDA*                                 8  4'Press F7 to'
640dDA*                                 9  2'start new pallet'
     A                                 11  4'Press F8 to'
     A                                 12  2'finish and stage'
640dDA*                                15  1'F7=New Plt  F8=Done'
640dMA                                 15  1'            F8=Done'
     A                                      COLOR(BLU)
     A          R SCREEN66
     A*%%TS  SD  20050824  101815  ROCKY       REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR20066'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  3 15'Stg'
     A            WOLCTR         3Y 0O  3 19EDTCDE(4)
     A                                  7  2'license:'
     A            W6LIC          7Y 0B  7 11CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  8  2'Stage location:'
     A            W6STAG        12A  B  9  5CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN68
     A*%%TS  SD  20020719  051440  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20068'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  7  2'Pallet license:'
     A            W8LIC          7Y 0B  9  5EDTCDE(4)
416eAA                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN69
     A*%%TS  SD  20020531  124648  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20069'
     A                                  1 10'Single Pick'
     A                                  3  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  5  1
     A            WOLN02        20A  O  6  1
     A            WOLN03        20A  O  7  1
     A            WOLN04        20A  O  8  1
     A            WOLN05        20A  O  9  1
     A            WOLN06        20A  O 10  1
     A            WOLN07        20A  O 11  1
     A            WOLN08        20A  O 12  1
     A            WOLN09        20A  O 13  1
     A            WOLN10        20A  O 14  1
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
640cAA          R SCREEN71
640cAA*%%TS  SD  20110519  103414  JEFF        REL-V6R1M0  5761-WDS
640cAA                                      CA03
640cAA                                      CA12
640cAA  98                                  ALARM
640cAA                                      OVERLAY
640cAA                                  1  2'OR20071 '
640cAA                                  1 10'Single Pick'
640cAA                                  3  1'Ord:'
640cAA            WOORD          7Y 0O  3  6EDTCDE(4)
640cAA                                  7  2'License:'
640cAA            W8LICA        15A  B  9  2CHECK(ER)
640cAA  21                                  DSPATR(RI)
640cAA  01                                  DSPATR(PC)
640cAA N21                                  DSPATR(UL)
640cAA                                 15  1'F12=Cancel'
640cAA                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
415bAA*****************************************************************
415bAA*
415bAA* Error Message Subfile Records for 16 line screens
415bAA*
415bAA*    Error Message Subfile Record
415bAA*
415bAA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415bAA*
415bAA*    Error Message Subfile Control Record
415bAA*
415bAA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415bAA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
500fAA*
500fAA*****************************************************************
500fAA* Spanish Screens
500fAA*****************************************************************
500fAA*----------------------------------------------------------------
500fAA          R SCRN1SP
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500 DA**                                    CLRL(08)
500cDA*                                     CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR200.01'
     A                                  1 11'Pick singular'
     A                                      DSPATR(HI)
     A                                  3  2'Almc:'
     A            W1WHSE         3Y 0B  3  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  4  2'Pedido:'
     A            W1ORD          7Y 0B  4 10EDTCDE(4)
     A                                      DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20A  O  5  2
     A            WOINF2        20A  O  6  2
     A                                  7  2'F3=Salir'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN2SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA04
640cAA                                      CA06
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                      CSRLOC(WOROW      WOCOL)
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR200.02'
     A                                  1 11'Pick Singular'
     A                                  2  1'Ped:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Prd:'
416h A*           WOITM2        70A  O  3  6
416h A            WOITMA        15A  O  3  6
416h A            WOITMB        55A  O  4  6
416h A                                  5  1'Slt:'
416h A            WODISP        12A  O  5  6
416h A*                                 4  1'Slt:'
416h A*           WODISP        12A  O  4  6
     A                                  6  1'Slot:'
     A            W2SLOT        12A  B  6  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N94                                  CHECK(ER)
     A                                  7  2'F4=Slots F12=Cancelar'
     A                                      COLOR(BLU)
500eDA*                                12  1'#Lic:'
500eMA                                  6 21'#Lic:'
     A N94                                  DSPATR(ND)
500eDA**          W2LIC         15A  B 12  7
500eMA            W2LIC         15A  B  6 27
     A N94                                  DSPATR(ND)
500e A N94                                  DSPATR(PR)
     A  94                                  CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  2 14'/'
500fAA*----------------------------------------------------------------
500fAA          R SCRN3SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.03'
     A                                  1 11'Pick Singular'
     A                                  2  1'Ped:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A                                  2 14'/'
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Prd:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Slt:'
     A            WODISP        12A  O  4  6
     A                                  5  1'Ctd:'
     A            WOQTY          5Y 0O  5  7EDTCDE(4)
     A                                  5 14'Escogido'
     A                                      DSPATR(UL)
     A            WOUM           5A  O  6  1
     A            W3QTY          5Y 0B  6  7EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      CHECK(ER)
     A            WOPCK          5Y 0O  6 15EDTCDE(4)
     A                                  7  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN4SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA07
640bAA                                      CA09
     A                                      CA12
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.04'
     A                                  1 11'Pick Singular'
     A                                  2  1'Ped:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Prd:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Ctd:'
     A            WOQTY          5Y 0O  4  6EDTCDE(4)
     A            WOUM           5A  O  4 12
700aDA*                                 4 19'Peso  '
700aMA                                  4 19'Ps'
700aAA            W4KGLB         3A  O  4 22DSPATR(HI)
     A            W4CNT          3Y 0O  4 26EDTCDE(4)
     A                                  4 30'de'
     A            W4QTY          3Y 0O  4 33EDTCDE(4)
     A                                  5  1'Pck:'
     A            WOPCK          5Y 0O  5  6EDTCDE(4)
     A                                  5 19'Pso Antr'
     A            W4LAST         7Y 2O  5 28EDTCDE(4)
     A            W4CW          50A  B  6  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O  7  1COLOR(BLU)
700aDA*                                 7  1'F7=Antror  F9=Total  F13=No Mas'
640bDA*                                 7  1'F7=Antror  F13=No Mas'
700aDA*                                     COLOR(BLU)
     A                                  2 14'/'
640bAA*----------------------------------------------------------------
640bAA          R SCRN4ASP
640bAA*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
640bDA*                                     CA07
     A                                      CA12
640bDA*                                     CA13
700aAA                                      CA19
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR20004A'
     A                                  1 11'Pick Singular'
     A                                  2  1'Ped:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A                                  2 14'/'
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Prd:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Ctd:'
     A            WOQTY          5Y 0O  4  6EDTCDE(4)
     A            WOUM           5A  O  4 12
     A*                                 4 19'Peso  '
     A*           W4CNT          3Y 0O  4 26EDTCDE(4)
     A*                                 4 30'de'
     A*           W4QTY          3Y 0O  4 33EDTCDE(4)
     A                                  4 19'Pck:'
     A            WOPCK          5Y 0O  4 23EDTCDE(4)
640bAA                                  5  1'Tot:'
640bAA            W4TQT          5Y 0B  5  6EDTCDE(4)
640bAA N23                                  DSPATR(HI)
640bAA  23                                  DSPATR(RI)
640bAA N23                                  DSPATR(UL)
640bAA  03                                  DSPATR(PC)
640bAA                                      CHECK(ER)
700aAA            W4KGLB         3A  O  6  1DSPATR(HI)
700aDA*           W4CW          50A  B  6  1
700aMA            W4CW          50A  B  6  5
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O  7  1COLOR(BLU)
700aDA*                                 7  1'F12=Cancelar'
700aDA*                                     COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN5SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
640dDA*                                     CA07
     A                                      CA08
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.05'
     A                                  1 11'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Enter=Escoge prdc'
640dDA*                                 5  1'F7=Empeza pleta nva'
     A                                  6  1'F8=Termina y prprar'
640dDA*                                 7  2'F7=Plta Nva F8=Termino'
640dMA                                  7  2'            F8=Termino'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN6SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                  1  2'OR200.06'
     A                                  1 11'Pck Singular'
     A                                  2  2'Ped:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  2 15'Prp'
     A            WOLCTR         3Y 0O  2 19EDTCDE(4)
     A                                  3  2'licencia'
     A            W6LIC          7Y 0B  3 11EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  4  2'Ubcacion prprar'
     A            W6STAG        12A  B  5  4CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  7  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN7SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500cAA                                      OVERLAY
     A                                  1  2'OR200.07'
     A                                  1 11'Pck Singular'
     A                                  2  2'Ped:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  4  3'Licencia paleta'
     A            W8LIC          7Y 0B  5  6EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  7  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN8SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500b A*                                     CLRL(15)
500c A*                                     CLRL(07)
500cAA                                      OVERLAY
     A                                  1  2'OR200.08'
     A                                  1 11'Pck Singular'
     A                                  2  2'Ped:'
     A            WOORD          7Y 0O  2  7EDTCDE(4)
     A                                  4  4'Licencia paleta'
     A            W8LIC          7Y 0B  5  7EDTCDE(4)
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  7  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN9SP
     A*%%TS  SD  20020719  183236  BFCMAX      REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR200.09'
     A                                  1 11'Pck Singular'
     A                                  2  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  3  1
     A            WOLN02        20A  O  3 22
     A            WOLN03        20A  O  4  1
     A            WOLN04        20A  O  4 22
     A            WOLN05        20A  O  5  1
     A            WOLN06        20A  O  5 22
     A            WOLN07        20A  O  6  1
     A            WOLN08        20A  O  6 22
     A            WOLN09        20A  O  7  1
     A            WOLN10        20A  O  7 22
     A                                  8  1'F12=Cancelar'
     A                                      COLOR(BLU)
640aAA*----------------------------------------------------------------
640aAA          R SCRN10SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                      CSRLOC(WOROW      WOCOL)
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR200.10'
     A                                  1 11'Pick Singular'
     A                                  2  1'Ped:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Prd:'
     A            WOITMA        15A  O  3  6
     A            WOITMB        55A  O  4  6
     A**                                5  1'Slt:'
     A**          WODISP        12A  O  5  6
     A                                  6  1'UPC:'
     A            WIUPCD        50A  B  6  6CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  7  2'F9=Skip  F12=Cancelar'
640aAA                                      COLOR(BLU)
640cAA          R SCRN11SP
640cAA*%%TS  SD  20110519  103414  JEFF        REL-V6R1M0  5761-WDS
640cAA                                      CA03
640cAA                                      CA12
640cAA  98                                  ALARM
640cAA                                      OVERLAY
640cAA                                  1  2'OR200.11'
640cAA                                  1 11'Pck Singular'
640cAA                                  2  2'Ped:'
640cAA            WOORD          7Y 0O  2  7EDTCDE(4)
640cAA                                  4  2'Licencia'
640cAA            W8LICA        15A  B  5  2
640cAA                                      CHECK(ER)
640cAA  21                                  DSPATR(RI)
640cAA  01                                  DSPATR(PC)
640cAA N21                                  DSPATR(UL)
640cAA                                  7  1'F12=Cancelar'
640cAA                                      COLOR(BLU)
415bAA*****************************************************************
415bAA*
415bAA*  16 line screens.
415bAA*
640aAA*----------------------------------------------------------------
640aAA          R SCRN60SP
     A*%%TS  SD  20020529  133812  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A*                                     CLRL(15)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'OR20060'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Prd:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A**                                7  1'Slt:'
     A**          WODISP        12A  O  7  6
     A                                  9  1'UPC:'
     A            WIUPCD        50A  B  9  6CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                 15  1'F9=Skip  F12=Cancelar'
640aAA                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN61SP
     A*%%TS  SD  20020603  114115  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20061'
     A                                  1 10'Pck Singular'
     A                                      DSPATR(HI)
     A                                  4  2'Almcn'
     A            W1WHSE         3Y 0B  4  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  5  2'Pedido'
     A            W1ORD          7Y 0B  5  9EDTCDE(4)
     A                                      DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20   O  8  2
     A            WOINF2        20   O  9  2
     A                                 15  2'F3=Salir'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN62SP
     A*%%TS  SD  20020529  133812  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
416aAA                                      CA04
640cAA                                      CA06
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
     A                                      CSRLOC(WOROW WOCOL)
500c A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'OR20062'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Prd:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Slt:'
     A            WODISP        12A  O  7  6
     A                                 11  1'Slot:'
     A            W2SLOT        12A  B 11  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A N94                                  CHECK(ER)
     A                                 12  1'#Lic:'
     A N94                                  DSPATR(ND)
     A            W2LIC         15A  B 12  7
     A N94                                  DSPATR(ND)
500eAA N94                                  DSPATR(PR)
     A  94                                  CHECK(ER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
416aMA                                 15  1'F4=Slots F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN63SP
     A*%%TS  SD  20020801  095415  VIGO        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20063'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Prd:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Slt:'
     A            WODISP        12A  O  7  6
     A                                  8  1'Ctd:'
     A            WOQTY          5Y 0O  8  6EDTCDE(4)
     A                                 10 16'Escogido'
     A                                      DSPATR(UL)
     A            WOUM           5A  O 11  2
     A            W3QTY          5Y 0B 11 10EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      CHECK(ER)
     A            WOPCK          5Y 0O 11 16EDTCDE(4)
     A                                 15  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN64SP
     A*%%TS  SD  20020530  204022  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA07
640bAA                                      CA09
     A                                      CA12
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20064'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Prd:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Ctd:'
     A            WOQTY          5Y 0O  7  6EDTCDE(4)
     A            WOUM           5A  O  7 12
     A                                  8  1'Pck:'
     A            WOPCK          5Y 0O  8  6EDTCDE(4)
700aDA*                                10  1'Peso  '
700aMA                                 10  1'Ps'
700aAA            W4KGLB         3A  O 10  4DSPATR(HI)
     A            W4CNT          3Y 0O 10  8EDTCDE(4)
     A                                 10 12'de'
     A            W4QTY          3Y 0O 10 15EDTCDE(4)
     A            W4CW          50A  B 11  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
     A                                 13  1'Pso Antr'
     A            W4LAST         7Y 2O 13 10EDTCDE(4)
700aAA            CMDLN1        79A  O 15  1COLOR(BLU)
700aDA*                                15  1'F7=Antr  F9=Total  F13=No Mas'
640bDA*                                15  1'F7=Antr  F13=No Mas'
700aDA*                                     COLOR(BLU)
416bDA*                                     CA10
640bAA*----------------------------------------------------------------
640bAA          R SCRN6ASP
     A*%%TS  SD  20020530  204022  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
640bDA*                                     CA07
     A                                      CA12
640bDA*                                     CA13
700aAA                                      CA19
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR2004A'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Prd:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
     A                                  7  1'Ctd:'
     A            WOQTY          5Y 0O  7  6EDTCDE(4)
     A            WOUM           5A  O  7 12
     A                                  8  1'Pck:'
     A            WOPCK          5Y 0O  8  6EDTCDE(4)
     A*                                10  1'Peso  '
     A*           W4CNT          3Y 0O 10  8EDTCDE(4)
     A*                                10 12'de'
     A*           W4QTY          3Y 0O 10 15EDTCDE(4)
640bAA                                 10  1'Tot:'
640bAA            W4TQT          5Y 0B 10  6EDTCDE(4)
640bAA N23                                  DSPATR(HI)
640bAA  23                                  DSPATR(RI)
640bAA N23                                  DSPATR(UL)
640bAA  03                                  DSPATR(PC)
640bAA                                      CHECK(ER)
700aAA            W4KGLB         3A  O 11  1DSPATR(HI)
700aDA*           W4CW          50A  B 11  1
700aMA            W4CW          50A  B 11  5
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O 15  1COLOR(BLU)
700aDA*                                15  1'F12=Cancelar'
700aDA*                                     COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN65SP
     A*%%TS  SD  20020719  051440  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA07
     A                                      CA08
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20065'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  5  4'Oprima Enter para'
     A                                  6  2'escoger otro prdc'
     A                                  8  4'Oprima F7 para'
     A                                  9  2'empezar paleta nva'
     A                                 11  4'Oprima F8 para'
     A                                 12  2'terminar y preparar'
     A                                 15  1'F7=Plta Nva  F8=Termino'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN66SP
     A*%%TS  SD  20020719  051440  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR20066'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  3 15'Prp'
     A            WOLCTR         3Y 0O  3 19EDTCDE(4)
     A                                  7  2'licencia'
     A            W6LIC          7Y 0B  7 11CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  8  2'Ubcacn preparar'
     A            W6STAG        12A  B  9  5CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                 15  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN68SP
     A*%%TS  SD  20020719  051440  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20068'
     A                                  1 10'Pck Singular'
     A                                  3  1'Ped:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  7  2'Licencia Paleta'
     A            W8LIC          7Y 0B  9  5EDTCDE(4)
416eAA                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                 15  1'F12=Cancelar'
     A                                      COLOR(BLU)
500fAA*----------------------------------------------------------------
500fAA          R SCRN69SP
     A*%%TS  SD  20020531  124648  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR20069'
     A                                  1 10'Pck Singular'
     A                                  3  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  5  1
     A            WOLN02        20A  O  6  1
     A            WOLN03        20A  O  7  1
     A            WOLN04        20A  O  8  1
     A            WOLN05        20A  O  9  1
     A            WOLN06        20A  O 10  1
     A            WOLN07        20A  O 11  1
     A            WOLN08        20A  O 12  1
     A            WOLN09        20A  O 13  1
     A            WOLN10        20A  O 14  1
     A                                 15  1'F12=Cancelar'
     A                                      COLOR(BLU)
640cAA          R SCRN71SP
640cAA*%%TS  SD  20110519  103414  JEFF        REL-V6R1M0  5761-WDS
640cAA                                      CA03
640cAA                                      CA06
640cAA                                      CA12
640cAA  98                                  ALARM
640cAA                                      OVERLAY
640cAA                                  1  2'OR20071 '
640cAA                                  1 10'Pck Singular'
640cAA                                  3  1'Ped:'
640cAA            WOORD          7Y 0O  3  6EDTCDE(4)
640cAA                                  7  2'Licencia'
640cAA            W8LICA        15A  B  9  2CHECK(ER)
640cAA  21                                  DSPATR(RI)
640cAA  01                                  DSPATR(PC)
640cAA N21                                  DSPATR(UL)
640cAA                                 15  1'F12=Cancelar'
640cAA                                      COLOR(BLU)
