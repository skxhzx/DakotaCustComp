      *%%TS  SD  20061207  201630  JEFF        REL-V5R3M0  5722-WDS
      ********************************************************************
      *   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
      *   BFC Software, Inc.                                             *
      *   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
      *   West Chicago, IL  60185                                        *
      *   (630) 562-0375                                                 *
      *****************************************************************
      *
      *  CK110AFM  Check Right
      *  22 June 2009
      *  Jeffrey Jahnke
      *
      *****************************************************************
      *  Revisions:
      *
      *    06/22/09  JCJ
      *      - Created. Copied from TX110FM
      *
      *****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'CK110'
     A                                  1 23'Work with Check Right FTP Status'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 32'List Display'
     A                                      DSPATR(HI)
     A            CURVIEW       12A  O  2 69
     A                                      DSPATR(RI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 48'Position to date created.'
     A            PTINP2         6Y 0B  3 74EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  4  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  4 48'Position to route . . . .'
     A            PTINP3         5A  B  4 74
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PFRTE     R        O 11  6REFFLD(CKFTPR/PFRTE CKFTPLOG)
     A            W1TYPE        12   O 11 12
     A            W1STAT        15A  O 11 25
     A            W1FMSG1       40A  O 11 41
     A            W1FMSG2       25A  O 12 41
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLMODE(&DSPMODE)
     A  51                                  SFLDROP(CF09)
     A N51                                  SFLFOLD(CF09)
     A            DSPMODE        1A  H      TEXT('Hidden: Display Mode')
     A            PTDMY1        78A  O  3  2
     A            WHCODE         3Y 0O  4  2EDTCDE(3)
     A                                  4  6'-'
     A            WHDESC        30A  O  4  8
     A                                  4 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  4 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 48'Position to date created.'
     A            PTINP2         6Y 0B  5 74EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  6 48'Position to route . . . .'
     A            PTINP3         5A  B  6 74
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A*           FLD001         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 12'Host Type   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'FTP Status     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'FTP Status Message                -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display - View 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20061207  201630  JEFF        REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PFRTE     R        O 11  5REFFLD(CKFTPR/PFRTE CKFTPLOG)
     A            PFRTID    R        O 11 11REFFLD(CKFTPR/PFRTID CKFTPLOG)
     A            PFTRN#    R        O 11 17REFFLD(CKFTPR/PFTRN# CKFTPLOG)
     A                                      EDTCDE(Z)
     A            W2STAT         1   O 11 25
     A            W2CRTD         6Y 0O 11 27EDTCDE(Y)
     A            W2CRTT          T  O 11 36
     A            W2EXPD         6Y 0O 11 45EDTCDE(Y)
     A            W2EXPT          T  O 11 54
     A            W2LSTD         6Y 0O 11 63EDTCDE(Y)
     A            W2LSTT          T  O 11 72
     A            W2FILLR       25A  O 12  1
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20061207  201630  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLMODE(&DSPMODE)
     A  51                                  SFLDROP(CF09)
     A N51                                  SFLFOLD(CF09)
     A            DSPMODE        1A  H      TEXT('Hidden: Display Mode')
     A            PTDMY1        78A  O  3  2
     A            WHCODE         3Y 0O  4  2EDTCDE(3)
     A                                  4  6'-'
     A            WHDESC        30A  O  4  8
     A                                  4 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  4 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 48'Position to date created.'
     A            PTINP2         6Y 0B  5 74EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  6 48'Position to route . . . .'
     A            PTINP3         5A  B  6 74
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A                                  9 11'Route'
     A                                      DSPATR(HI)
     A                                  9 17'Tran'
     A                                      DSPATR(HI)
     A                                  9 27'---- Created ----'
     A                                      DSPATR(HI)
     A                                  9 45'---- Exported ---'
     A                                      DSPATR(HI)
     A                                  9 63'-- Last Export --'
     A                                      DSPATR(HI)
     A                                 10  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 11'ID   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Number '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 45'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display - View 3
     A*
     A          R VW3SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PFRTE     R        O 11  6REFFLD(CKFTPR/PFRTE  CKFTPLOG)
     A            PFRTID    R        O 11 13REFFLD(CKFTPR/PFRTID CKFTPLOG)
     A            PFMBR     R        O 11 20REFFLD(CKFTPR/PFMBR  CKFTPLOG)
     A            PFEXPF    R        O 11 32REFFLD(CKFTPR/PFEXPF CKFTPLOG)
     A            PFLUSR    R        O 11 65REFFLD(CKFTPR/PFLUSR CKFTPLOG)
     A            W3FILLR       25A  O 12  1
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLMODE(&DSPMODE)
     A  51                                  SFLDROP(CF09)
     A N51                                  SFLFOLD(CF09)
     A            DSPMODE        1A  H      TEXT('Hidden: Display Mode')
     A            PTDMY1        78A  O  3  2
     A            WHCODE         3Y 0O  4  2EDTCDE(3)
     A                                  4  6'-'
     A            WHDESC        30A  O  4  8
     A                                  4 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  4 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 48'Position to date created.'
     A            PTINP2         6Y 0B  5 74EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  6 48'Position to route . . . .'
     A            PTINP3         5A  B  6 74
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A                                  9 13'Route'
     A                                      DSPATR(HI)
     A                                  9 20'Export'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'ID   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 20'Data Mbr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32'Export File Name              '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Last Sent By'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20060720  154854  ROBIN       REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* Option, Command and EOF Message Lines for Lookup Panels
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A*  Moved to POSTNREC
     A*                                 1  2'Type options, press Enter.'
     A*                                     COLOR(BLU)
     A            OPTLN1        75A  O  1  2COLOR(BLU)
     A            OPTLN2        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'More.../Bottom  Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    Delete Display Instructions
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Clear FTP error.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    Display ReSend FTP file instructions
     A*
     A          R OP1REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Resend FTP file.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12=Cancel to return to chan-
     A                                       ge your choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R PASWRD
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
     A                                      DSPATR(ND)
