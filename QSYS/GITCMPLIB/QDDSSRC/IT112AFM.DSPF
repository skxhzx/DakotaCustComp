     A*%%TS  SD  20000208  131513  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT112AFM   Item maintenance - Change screen
     A*  19 June 1999
     A*  Kenneth Elder
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
413  A*    02/08/00  DAS 4.13
     A*      - Modified description of the third item entry field to
     A*        'Base Contract Item'.
     A*      - Modified 'Note' that went along with field.
     A*      - Added indicator 91 to non-display those fields if the
     A*        item is not a contract item.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA14
     A          R SCREEN1
     A*%%TS  SD  20000208  131513  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT112A.1'
     A                                  1 33'Item Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            HDTYPE        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  B  8 31
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                  9  4'Base Item . . . . . . . .'
     A            W1ITM2        15A  B  9 31
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 10  4'Base Contract Item  . . .'
     A  91                                  DSPATR(ND)
     A            W1ITM3        15A  B 10 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A  91                                  DSPATR(ND)
     A                                 10 48'Note: Enter when Base Item is'
     A                                      DSPATR(HI)
     A  91                                  DSPATR(ND)
     A                                 11 48'a broken case item (Type=''B'')'
     A                                      DSPATR(HI)
     A  91                                  DSPATR(ND)
     A                                 12  4'Description . . . . . . .'
     A            W1DESC        30A  B 12 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 13  4'Pack Description  . . . .'
     A            W1PKDG        15A  B 13 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 16  4'Extended Descriptions . .'
     A            W1DSC1        40A  B 16 31
     A  96                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      CHECK(LC)
     A            W1DSC2        40A  B 17 31
     A  96                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      CHECK(LC)
     A                                 22  2'F3=Exit  F5=Refresh  F12=Cancel  F-
     A                                      9=View Warehouse Quantities'
     A                                      COLOR(BLU)
     A            FLD001        25A  O 23  2COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
