     A*%%TS  SD  20020530  133737  DAVE        REL-V4R4M0  5769-PW1
     *----------------------------------------------------------------
     *  For some clients, use CHGDSPF to set RSTDSP to *YES for the
     *  screen specs.  When running pgms OR910 or OR920 using iSeries
     *  Access, and the user selects an item with an error, OR200
     *  would blow up with an I/O error.
     *----------------------------------------------------------------
     A********************************************************************
     A*
     A* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS.
     A*       RSTDSP(*YES) IF CLIENT IS USING ISERIES ACCESS AS THE
     A*       EMULATOR
     A*
     A*
     A********************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR910FM    RF single order pick detail by line - Display
     A*  29 May 2002
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
416 AA*  05/29/02  DAS  4.16
     A*    - Created
     A*
416aAA*  07/18/02  DAS  4.16a
     A*    - Combined item and descripiton into single field.
640aAA*  04/01/11  GJA  6.40A
     A*    - FIX:  ISERIES ACCESS ISSUE ADDED DUMMY RECORD
640bAA*  08/11/11  RH   6.40b
     A*    - Enh:  Add F7 to display All lines or balance only lines
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
640 AA                                      CF07
     A                                      CF09
     A          R TITLE
     A*%%TS  SD  19961212  143846  DAVE4       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'OR91001'
     A                                  1 10'Order by Line'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A*         R POSTNREC
     A*                                     OVERLAY
     A*                                     LOCK
     A*                                     SLNO(03)
     A*           WHCODE         3Y 0O  1  2EDTCDE(3)
     A*                                 1  6'-'
     A*           WHDESC        30A  O  1  8
     A*           FLD002         1A  O  3  2
     A*                                 2  2'PO #   '
     A*           $KPO           9A  O  2 10
     A*                                 2 41'Position to line . . .'
     A*           PTINP1        15A  B  2 65
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19961219  095828  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
415 AA            TOUCH          1A  O  4  2
     A            OPTION         1A  B  4  4
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416aDA**          ODITEM    R        O  4 12REFFLD(ODREC/ODITEM *LIBL/ORDD)
     A            ODSEQ     R        O  4  6REFFLD(ODREC/ODSEQ *LIBL/ORDD)
     A                                      EDTCDE(4)
416aDA**          ITDESC    R        O  4 28REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
416aAA            W1ITEM        46A  O  4 12
415 AA            WOQOR          7A  O  4 60
415 AA            WOQRC          7A  O  4 68
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20020529  110939  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  2  2'1=Select'
     A                                      COLOR(BLU)
     A                                  3  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Line#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA**                                3 12'Item           '
416aMA                                  3 12'Item/Description                  -
416aMA                                                  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA**                                3 28'Description                   '
416aDA**                                    DSPATR(HI)
416aDA**                                    DSPATR(UL)
     A                                  3 60'Ordered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 68'Picked '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
415 AA*****************************************************************
415 AA*
415 AA* SUBFILE FOR LIST DISPLAY (16-Lines)
415 AA*
     A          R D16SFL                    SFL
     A*%%TS  SD  20020530  133737  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            WOLBL#         7  0H
     A            TOUCH          1A  O  4  2
     A            OPTION         1A  B  4  4
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416aDA**          ODITEM    R        O  4 12REFFLD(ODREC/ODITEM *LIBL/ORDD)
     A            ODSEQ     R        O  4  6REFFLD(ODREC/ODSEQ *LIBL/ORDD)
     A                                      EDTCDE(4)
416aDA**          ITDESC    R        O  4 28REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
416aAA            W1ITEM        46A  O  4 12
     A            WOQOR          7A  O  4 60
     A            WOQRC          7A  O  4 68
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A*%%TS  SD  20020530  133737  DAVE        REL-V4R4M0  5769-PW1
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                  2  2'1=Select'
     A                                      COLOR(BLU)
     A                                  3  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Line#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA**                                3 12'Item           '
416aMA                                  3 12'Item/Description                  -
416aMA                                                  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA**                                3 28'Description                   '
416aDA**                                    DSPATR(HI)
416aDA**                                    DSPATR(UL)
     A                                  3 60'Ordered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 68'Picked '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(08)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
415 AA*****************************************************************
415 AA*
415 AA* Error Message Subfile Records for 16 line screens
415 AA*
415 AA*    Error Message Subfile Record
415 AA*
415 AA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415 AA*
415 AA*    Error Message Subfile Control Record
415 AA*
415 AA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415 AA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(20)
     A            OPTLN1        75A  O  1  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(7)
     A            CMDLN1        79   O  1  2COLOR(BLU)
415 AA          R C16REC
415 AA*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
415 AA                                      LOCK
415 AA                                      OVERLAY
415 AA                                      SLNO(15)
415 AA            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A*         R MORREC
     A*                                     LOCK
     A*                                     OVERLAY
     A*                                     SLNO(21)
     A*           MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(2)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A*%%TS  SD  19961212  143846  DAVE4       REL-V3R1M0  5763-PW1
     A*                                     SLNO(01)
     A                                      SLNO(02)
     A                                      OVERLAY
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R VERIFY
     A                                      WINDOW(2 1 5 20)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Are you sure you '
     A                                      COLOR(WHT)
     A                                  3  2'want to check as'
     A                                      COLOR(WHT)
     A                                  4  2'touched ?'
     A                                      COLOR(WHT)
     A            WOVRFY         1A  B  4 12COLOR(WHT)
     A                                      CHECK(ER)
640A A          R DUMMY                     KEEP
     A                                      ASSUME
     A                                  2  1' '
