     A*%%TS  SD  19960131  092854  DAVE        REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*  Revisions
     A*
     A*    06/07/06  RH   5.00
     A*      - CREATED.
     A*----------------------------------------------------------------
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960131  092854  DAVE        REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(23)
     A                                      OVERLAY
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A                                  1  2'IV387.01'
     A                                  1 29'Print Inv BreakDown Qty   '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 49'1-999'
     A                                 10  4'Department . . . . . . . . .'
     A            W1WHDP         5   B 10 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'Name, *ALL'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
