     A*%%TS  SD  20051012  152806  DAVE        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  FIXOUTLBL   Fix Out label with new slot number
     A*  12 October 2005
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    10/12/05  DAS  5.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20051012  152806  DAVE        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'FIXOUTLBL.01'
     A                                  1 30'Fix Out Label Records'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'This program will go through the O-
     A                                      ut label records for the transactio-
     A                                      n.'
     A                                  7  4'If the label record does not conta-
     A                                      in a pick slot or the pick slot is -
     A                                      not'
     A                                  8  4'active, this program will attempt -
     A                                      to get a valid pick slot for the it-
     A                                      em.'
     A                                 11  4'Selection transaction number . . .'
     A            W1TRN#         7Y 0B 11 39EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                 14  4'Number of Out labels with an inval-
     A                                      id pick slot:'
     A            W1BAD          3  0O 14 53
     A                                 15  4'Number of Out labels updated with -
     A                                      new pick slot:'
     A            W1UPD          3  0O 15 53
500 AA*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
