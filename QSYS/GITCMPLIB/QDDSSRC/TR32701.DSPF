     A*%%TS  SD  20111208  090823  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR327FM    TRAX stop item detail - Display
     A*  Ginger Andrews
     A*
     A*  Revisions
     A*
640a A*    10/11/11  GJA  6.40a
     A*      - Created.
     A*
640bAA*    11/29/11  RBD  6.40b
     A*      - Reduced some hdgs to make more room.
     A*      - Added second view.
     A*      - Added display of item description.
     A*      - Fixed end of subfile indicator.
     A*
640cAA*    12/08/11  RBD  6.40c
     A*      - Changed sort criteria to item and scan time.
     A*      - Removed selection criteria for whse, route, stop,
     A*        delivery date, and item.
     A*
650aAA*    11/26/13  RBD  6.50a
     A*      - Changed date and time heading from 'Scan' to 'Delivery'.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20111129  153610  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTIITM    R        O  8  5REFFLD(TTIREC/TTIITM *LIBL/TTITM)
     A            W1DESC        30   O  8 21
     A            W1QDLV         6  0O  8 52EDTCDE(4)
     A            W1QDLN         6Y 0O  8 59EDTCDE(4)
     A            W1QCRT         6Y 0O  8 66EDTCDE(4)
     A            W1QPRT         6Y 0O  8 73EDTCDE(4)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20111129  153610  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Trax Item Detail'
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 53'Pcs'
     A                                      DSPATR(HI)
     A                                  6 59'Pcs'
     A                                      DSPATR(HI)
     A                                  6 68'Curr / Prev'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                  7  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Desc                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 53'Dlvrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 59'NonScn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 67'Pcs Returned'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20111129  153610  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTIITM    R        O  8  5REFFLD(TTIREC/TTIITM *LIBL/TTITM)
     A            W1DESC        30   O  8 21
     A            TTIUM     R        O  8 52REFFLD(TTIREC/TTIUM *LIBL/TTITM)
     A            TTIOPF    R        O  8 56REFFLD(TTIREC/TTIOPF *LIBL/TTITM)
     A            W1SRD          6Y 0O  8 59EDTWRD('  /  /  ')
     A            TTISRT    R        O  8 68REFFLD(TTIREC/TTISRT *LIBL/TTITM)
     A                                      EDTWRD('  :  :  ')
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20111129  153610  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Trax Item Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 55'Opr'
     A                                      DSPATR(HI)
650aDA**                                6 65'Scan'
650aMA                                  6 63'Delivery'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                  7  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Desc                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 52'UM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 59'    Date/Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit F5=Refresh F9=Sel/Sor-
     A                                      t F10=Prv view F11=Nxt view -
     A                                      F12=Cncl F21=Prt'
     A                                      COLOR(BLU)
     A          R MKEY1
     A                                      OVERLAY
     A            W1MSG1        75A  O 24  3
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A          R WINDOW1
     A*%%TS  SD  20111208  090823  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order  . . . . . .'
     A            W1SORT         1A  B  3 20DSPATR(HI)
     A                                  3 25'Ascending, Descending'
     A                                  4  1'Order by . . . . .'
     A            W1OBY          1A  B  4 20DSPATR(HI)
     A                                  4 25'1 = Item'
650aDA**                                5 25'2 = Scan time'
650aMA                                  5 25'2 = Delivery time'
     A                                  7  1'Non-scanned only .'
     A            W1NSCN         1A  B  7 20DSPATR(HI)
     A                                  7 25'Y = Yes'
     A                                  8  1'Returns only . . .'
     A            W1RETN         1A  B  8 20DSPATR(HI)
     A                                  8 25'Y = Yes'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
