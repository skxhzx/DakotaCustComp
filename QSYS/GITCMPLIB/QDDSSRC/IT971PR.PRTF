     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  IT971PR    Item Quantity Compare Report
     A*  15 January 2010
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*    01/15/10  RBD  5.30
     A*      - Created.
530aAA*    02/22/10  MLB  5.30a
     A*      - Enh: Updated file names used in pgm due to change in
     A*        import file name used for report.
     A*
640a A*    03/07/11  GJA  6.40a
     A*      - Enh:  Add pick slot.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W1HEAD        60A  O    30
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
      *
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A            WHHEAD        40A  O    40
     A                                   121
     A                                      'IT971'
     A                                      SPACEA(002)
     A                                    53
     A                                      'Q u a n t i t y  1'
     A                                    77
     A                                      'Q u a n t i t y  2'
530aAA                                   101
530aAA                                      'Q u a n t i t y  3'
     A                                      SPACEA(001)
     A                                     2
     A                                      'Item'
     A                                    18
     A                                      'Description'
     A                                    52
     A                                      'Host'
     A                                    58
     A                                      'Dakota'
     A                                    68
     A                                      'Diff'
     A                                    76
     A                                      'Host'
     A                                    82
     A                                      'Dakota'
     A                                    90
     A                                      'Diff'
530aAA                                    98
530aAA                                      'Host'
530aAA                                   104
530aAA                                      'Dakota'
530aAA                                   113
530aAA                                      'Diff'
640aAA                                   120
640aAA                                      'Pick Slot'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            IQITEM    R        O     2
     A                                      REFFLD(ITITEM PIRITEM)
     A            ITDESC    R        O    18
     A                                      REFFLD(ITDESC PIRITEM)
530aMA            ICTQH1    R        O    49
530aMA                                      REFFLD(ICTQH1 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK1    R        O    57
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF1     R        O    65
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
530aMA            ICTQH2    R        O    73
530aMA                                      REFFLD(ICTQH2 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK2    R        O    81
     A                                      REFFLD(IQSTK2 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF2     R        O    87
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
530aAA            ICTQH3    R        O    95
530aAA                                      REFFLD(ICTQH3 IMPFCQTY)
530aAA                                      EDTCDE(M)
530aAA            IQSTK3    R        O   103
530aAA                                      REFFLD(IQSTK3 ITEMQTY)
530aAA                                      EDTCDE(M)
530aAA            DIFF3     R        O   109
530aAA                                      REFFLD(IQSTK1 ITEMQTY)
530aAA                                      EDTCDE(M)
640aAA            SLDISP    R        O   119
     A                                      REFFLD(SLDISP SLOT)
     A*
     A          R HEAD2
     A                                      SKIPB(002)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W2HEAD        60A  O    30
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A            WHHEAD        40A  O    40
     A                                   121
     A                                      'IT971'
     A                                      SPACEA(002)
     A                                    59
     A                                      'Q u a n t i t y  1'
     A                                    83
     A                                      'Q u a n t i t y  2'
530aAA                                   107
530aAA                                      'Q u a n t i t y  3'
     A                                      SPACEA(001)
     A                                     8
     A                                      'Item'
     A                                    24
     A                                      'Description'
     A                                    58
     A                                      'Host'
     A                                    64
     A                                      'Dakota'
     A                                    74
     A                                      'Diff'
     A                                    82
     A                                      'Host'
     A                                    88
     A                                      'Dakota'
     A                                    98
     A                                      'Diff'
530aAA                                   106
530aAA                                      'Host'
530aAA                                   112
530aAA                                      'Dakota'
530aAA                                   122
530aAA                                      'Diff'
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
530aMA            ICITEM    R        O     8
530aMA                                      REFFLD(ICITEM IMPFCQTY)
530aMA            ICTQH1    R        O    55
530aMA                                      REFFLD(ICTQH1 IMPFCQTY)
     A                                      EDTCDE(L)
530aMA            ICTQH2    R        O    79
530aMA                                      REFFLD(ICTQH2 IMPFCQTY)
     A                                      EDTCDE(L)
530aAA            ICTQH3    R        O   103
530aAA                                      REFFLD(ICTQH3 IMPFCQTY)
530aAA                                      EDTCDE(L)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT971: End of Report')
     A*
