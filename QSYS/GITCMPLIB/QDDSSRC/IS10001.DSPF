     A*%%TS  SD  20170417  183103  KENNETHE    REL-V7R2M0  5770-WDS
     A**************************************************************************
     A*  Revisions:
     A*
     A*    04/13/17  KDE  7.10
     A*     - Created
     A*
     A*    02/20/18  KDE  7.20
     A*     - Removed Keyword ASSUME per webfacing issues.
     A*****************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA20
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20170417  183103  KENNETHE    REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IS100.01'
     A                                  1 33'Inventory Cost       '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Maintenance '
     A                                      DSPATR(HI)
     A            WOWHSE        55A  O  4 15
     A            WOITEM        55A  O  5 15
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  8'Last Purchase Order Cost ..:'
     A                                 12  8'Average Cost ..............:'
     A                                 14  8'Standard Cost .............:'
     A                                 16  8'Last Update ...............:'
     A                                 18  8'Update Source .............:'
     A                                 20  8'Last User to Update .......:'
     A            WOPOCOST       9Y 4B 10 37EDTWRD('     .    ')
     A            WOAVGCOST      9Y 4B 12 37EDTWRD('     .    ')
     A            WOSTDCOST      9Y 4B 14 37EDTWRD('     .    ')
     A            WOUSER        10A  O 20 37
     A            WOORIG        10A  O 18 37
     A            WOTS          20A  O 16 37
     A                                 23  2'F5=Refresh    F12=Previous    F20=-
     A                                      Override Warnings'
     A                                      COLOR(BLU)
     A                                  4  8'Whse:'
     A                                  5  8'Item:'
     A                                  7 33'* New Cost Record'
     A N94                                  DSPATR(ND)
     A                                      COLOR(PNK)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
