     A*%%TS  SD  20020627  110730  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  IT262FM      RF Will Call Transfer
     A*  27 June 2002
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*
     A*  Revisions:
     A*
416 AA*  06/27/02  MLB  4.16
     A*    - Created.
416aAA*  07/30/02  MLB  4.16a
     A*    - ENH: Revised SCREEN 3 to allow F4=Prompt display list
     A*      of available pick slots.
416bAA*  10/24/02  RH   4.16b
     A*    - ENH: Revised SCREEN 3 to allow F4=Prompt display list
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT26261'
     A                                  1 10'Willcal Tfr'
     A                                      DSPATR(HI)
     A                                  5  4'W i l l   C a l l'
     A                                      DSPATR(HI)
     A                                  7  4'T r a n s f e r s'
     A                                      DSPATR(HI)
     A                                 11  2'Warehouse  . :'
     A            W1WHSE         3Y 0B 11 17
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A  98                                  ALARM
     A  51                                  CA03
     A N51                                  CF10
     A  51                                  CA12
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                  1  2'IT26262'
     A                                  1 10'WillCal Tfr'
     A                                  4  2'Items picked :'
     A            W2NRPK         3Y 0O  4 17EDTCDE(J)
     A                                  6  2'Last item  . :'
     A            W2LSIT        15A  O  7  4
     A                                 10  2'Item . . . . :'
     A            W2ITEM        15A  B 11  4
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A            W2CMDLN       19A  O 15  2COLOR(BLU)
     A          R SCREEN3
     A                                      CA03
416aAA                                      CA04
     A                                      CF10
     A                                      CA12
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                  1  2'IT26263'
     A                                  1 10'Willcal Tfr'
     A                                  3  1'Item'
     A            W3ITEM        15A  O  3  6
     A                                  4  1'Desc'
     A            W3DESA        15A  O  4  6
     A            W3DESB        15A  O  5  6
416b A*           W3DESC        30A  O  4  6
416b A                                  6  1'Pack'
416b A            W3PDSC        15A  O  6  6
416b A*                                 5  1'Pack'
416b A*           W3PDSC        15A  O  5  6
416b A                                  7  1'Dept'
416b A            W3WHDP         5A  O  7  6
416b A*                                 6  1'Dept'
416b A*           W3WHDP         5A  O  6  6
416b A                                  8  1'Slot'
416b A            W3DISP        12A  O  8  6
416b A*                                 7  1'Slot'
416b A*           W3DISP        12A  O  7  6
     A                                  9  1'Scan slot  . . . :'
     A            W3SCAN        12A  B 10  3
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A                                      CHECK(ER)
416aAA                                 13  2'F4=Prompt'
416aAA                                      COLOR(BLU)
     A                                 14  2'F10=Exception'
     A                                      COLOR(BLU)
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A                                      CA03
     A                                      CF09
     A                                      CA12
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                  1  2'IT26264'
     A                                  1 10'Willcal Tfr'
     A                                  3  1'Item'
     A            W4ITEM        15A  O  3  6
     A                                  4  1'Desc'
     A            W4DESC        30A  O  4  6
     A                                  5  1'Pack'
     A            W4PDSC        15A  O  5  6
     A                                  6  1'Dept'
     A            W4WHDP         5A  O  6  6
     A                                  7  1'Slot'
     A            W4DISP        12A  O  7  6
     A                                  8  1'Avl '
     A            W4AVAL        15A  O  8  6
     A                                 10  1'Qty  . . :'
     A            W4QTY1         5S 0B 10 12
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A            W4UM1          2A  O 10 19
     A            W4QTY2         5S 0B 11 12
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A N52                                  DSPATR(ND)
     A            W4UM2          2A  O 11 19
     A N52                                  DSPATR(ND)
     A            W4QTY3         5S 0B 12 12
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A N53                                  DSPATR(ND)
     A            W4UM3          2A  O 12 19
     A N52                                  DSPATR(ND)
     A                                 14  1'F9=Slot info'
     A                                      COLOR(BLU)
     A                                 15  1'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 15 12'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN9
     A                                      CF10
     A                                      CA12
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                  1  2'IT26269'
     A                                  1 11'Willcall Tfr'
     A            W9DUMMY        1A  B  2  2
     A                                  4  2'To finish the'
     A                                  5  2'Will Call Transfer:'
     A                                  7  3'- Take items to'
     A                                  8  5'Will Call area.'
     A                                  9  1' '
     A                                 10  3'- Press F10 to end'
     A                                 11  5'transaction.'
     A                                 14  2'F10=Finish'
     A                                      COLOR(BLU)
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN99
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A*                                     OVERLAY
     A                                  1  2'IT26299'
     A                                  1 11'Willcall Tfr'
     A            W9TXT01      273   B  2  2CNTFLD(21)
     A                                      DSPATR(PR)
     A  53                                  DSPATR(UL)
     A                                 15  2'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
