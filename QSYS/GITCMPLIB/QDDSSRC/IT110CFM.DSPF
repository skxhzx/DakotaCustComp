     A*%%TS  SD  20020826  131034  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT110CFM   Item maintenance by description Dispaly screen
     A*  26 August, 2002
     A*  Hemant Kapadia
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*    08/26/02  HNK  4.17
     A*      - Created
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CF04
     A                                      CF06
     A          R TITLE
     A*%%TS  SD  19940709  103817  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'IT110C.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 33'Item Maintenance by Desc'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20020826  131034  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2 27'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 51EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  3 23'Position to Description . .'
     A            PTINP2        30A  B  3 51CHECK(LC)
     A  02                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990613  214818  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ITITEM    R        O 11  7REFFLD(ITREC/ITITEM *LIBL/PIRITEM)
     A            ITDESC    R        O 11 23REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A            ITSTYP    R        O 11 60REFFLD(ITREC/ITSTYP *LIBL/PIRITEM)
     A            ITWHDP    R        O 11 64REFFLD(ITREC/ITWHDP *LIBL/PIRITEM)
     A            ITDESG    R        O 11 71REFFLD(ITREC/ITDESG *LIBL/PIRITEM)
     A            ITACTV    R        O 11 78REFFLD(ITREC/ITACTV *LIBL/PIRITEM)
     A            ITTYPE         1   O 11 56
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990613  214818  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
413aDA*                                     CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001        78A  O  9  2
     A                                 10  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 59'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 77'Actv'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 54'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R DSPSFL1                   SFL
     A*%%TS  SD  19990613  214818  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ITITEM    R        O 11  7REFFLD(ITREC/ITITEM *LIBL/PIRITEM)
     A            IXDSC1    R        O 11 23REFFLD(IXREC/IXDSC1 *LIBL/ITEMDSC)
     A            ITSTYP    R        O 11 72REFFLD(ITREC/ITSTYP *LIBL/PIRITEM)
     A            ITWHDP    R        O 11 76REFFLD(ITREC/ITWHDP *LIBL/PIRITEM)
     A            IXDSC2    R        O 12 23REFFLD(IXREC/IXDSC2 *LIBL/ITEMDSC)
     A            ITTYPE         1   O 11 66
     A*
     A          R DSPSFC1                   SFLCTL(DSPSFL1)
     A*%%TS  SD  19990613  214818  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0005)
413aDA*                                     CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Description                       -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001        78A  O  9  2
     A                                 10  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 71'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
