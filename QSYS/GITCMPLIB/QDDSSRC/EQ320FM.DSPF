     A*%%TS  SD  20030131  134931  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EQ320FM    Equipment Check In/Out History
     A*  Roxanne Harrison
     A*
     A*  Revisions
     A*
417 AA*    02/26/03  RH   4.17
     A*      - Created.
417bAA*    07/21/03  RH   4.17b
     A*      - Add Equip type to select.
417cAA*    07/23/03  RH   4.17c
     A*      - Add sort by employee #.
417dAA*    04/29/04  RH   4.17d
     A*      - Increase equip ID from 6 to 8.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA12
     A                                      CF09
     A                                      CA21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20020823  162605  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE       75A  O  6  5
     A            SFLHIDE       49   H
     A**          SFLHIDE       45   H
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20030130  144849  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A            RRN1           4S 0H      SFLRCDNBR
     A            SCRNAME        8A  O  1  2
     A                                  1 31'Equipment History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        50A  O  2 16
     A                                  2 71TIME
     A            SCOPT         75A  O  3  2
     A            SFLHEAD1      79A  O  4  1
     A                                      DSPATR(HI)
     A            SFLHEAD       79A  O  5  1
     A*
     A          R FKEY1
     A*
     A            SCKEY         78A  O 23  2
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20030129  150453  DAVE        REL-V4R4M0  5769-PW1
417B A**                                    WINDOW(6 20 14 40)
417B A                                      WINDOW(6 20 16 40)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Warehouse . .'
     A            W1WHSE         3Y 0B  3 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5  1'From date . .'
     A            W1FROM         6Y 0B  5 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5 22'MMDDYY, 0=First'
     A                                  7  1'To date . . .'
     A            W1TO           6Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  7 22'MMDDYY, 999999=Last'
417b A                                  8  1'Equip type. .'
417b A            W1TYPE         6A  B  8 15DSPATR(HI)
417b A                                  8 22'Code,*ALL'
     A                                  9  1'-OR-  '
     A                                 10  1'Equip id  . .'
417dDA*           W1CODE         6A  B 10 15DSPATR(HI)
417d A            W1CODE         8A  B 10 15DSPATR(HI)
     A                                 10 24'Id, *ALL'
     A                                 12  1'Sort by . . .'
     A            W1SORT         1A  B 12 15DSPATR(HI)
417c A*                                12 22'1=Date, 2=Id'
417c A                                 12 22'1=Date, 2=Id, 3=Emp'
     A                                 14  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A*%%TS  SD  20030131  134931  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
