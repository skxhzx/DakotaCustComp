     A*%%TS  SD  19960116  131942  DAVE        REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      CF17
     A                                      CF18
     A          R TITLE
     A*%%TS  SD  19960110  100254  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  3'TK310.01 '
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 34'Task Overview'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19960116  131942  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 52'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 77EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            W1WHSE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960116  131942  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            TKCAT     R        O 11  7REFFLD(TKREC/TKCAT *LIBL/TASK)
     A            TKSUB     R        O 11 16REFFLD(TKREC/TKSUB *LIBL/TASK)
     A            TKCODE    R        O 11 24REFFLD(TKREC/TKCODE *LIBL/TASK)
     A            TDWHDP    R        O 11 31REFFLD(TDREC/TDWHDP *LIBL/TASKDET)
     A            TDSTYP    R        O 11 38REFFLD(TDREC/TDSTYP *LIBL/TASKDET)
     A            TDPNDJ    R        B 11 42REFFLD(TDREC/TDPNDJ *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            TDACTJ    R        B 11 48REFFLD(TDREC/TDACTJ *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            TDFINJ    R        B 11 54REFFLD(TDREC/TDFINJ *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            W1REM          4Y 0O 11 61EDTWRD('  :  ')
     A            W1PCT          5Y 2O 11 67EDTCDE(4)
     A            W1NEED         3Y 1O 11 75EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960116  131714  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                 10  2'+/-'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(ND)
     A                                  9 15'Sub'
     A                                      DSPATR(HI)
     A                                 10  6'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Pend '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Actv '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Comp '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Comp  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'----- Jobs ------'
     A                                      DSPATR(HI)
     A                                 10 74'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 74'Emply'
     A                                      DSPATR(HI)
     A                                  9 67'Pct'
     A                                      DSPATR(HI)
     A                                  9 61'Rem'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19960111  164237  DAVE3       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            TKCAT     R        O 11  7REFFLD(TKREC/TKCAT *LIBL/TASK)
     A            TKSUB     R        O 11 16REFFLD(TKREC/TKSUB *LIBL/TASK)
     A            TKCODE    R        O 11 24REFFLD(TKREC/TKCODE *LIBL/TASK)
     A            TDWHDP    R        O 11 31REFFLD(TDREC/TDWHDP *LIBL/TASKDET)
     A            TDSTYP    R        O 11 38REFFLD(TDREC/TDSTYP *LIBL/TASKDET)
     A            TDPNDP    R        B 11 42REFFLD(TDREC/TDPNDP *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            TDACTP    R        B 11 48REFFLD(TDREC/TDACTP *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            TDFINP    R        B 11 54REFFLD(TDREC/TDFINP *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            W1REM          4Y 0O 11 61EDTWRD('  :  ')
     A            W1PCT          5Y 2O 11 67EDTCDE(4)
     A            W1NEED         3Y 1O 11 75EDTCDE(4)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19960111  131002  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'+/-'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'Sub'
     A                                      DSPATR(HI)
     A                                 10  6'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Pend '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Actv '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Comp '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Comp  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'---- Pieces -----'
     A                                      DSPATR(HI)
     A                                 10 74'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 74'Emply'
     A                                      DSPATR(HI)
     A                                  9 67'Pct'
     A                                      DSPATR(HI)
     A                                  9 61'Rem'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19960111  164237  DAVE3       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            TKCAT     R        O 11  7REFFLD(TKREC/TKCAT *LIBL/TASK)
     A            TKSUB     R        O 11 16REFFLD(TKREC/TKSUB *LIBL/TASK)
     A            TKCODE    R        O 11 24REFFLD(TKREC/TKCODE *LIBL/TASK)
     A            TDWHDP    R        O 11 31REFFLD(TDREC/TDWHDP *LIBL/TASKDET)
     A            TDSTYP    R        O 11 38REFFLD(TDREC/TDSTYP *LIBL/TASKDET)
     A            W1REM          4Y 0O 11 61EDTWRD('  :  ')
     A            W1PCT          5Y 2O 11 67EDTCDE(4)
     A            W1NEED         3Y 1O 11 75EDTCDE(4)
     A            TDSTDP    R        B 11 43REFFLD(TDREC/TDSTDP *LIBL/TASKDET)
     A                                      EDTCDE(4)
     A            W1ETIM         4Y 0B 11 52EDTWRD('  :  ')
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19960111  131002  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A            RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'+/-'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'Sub'
     A                                      DSPATR(HI)
     A                                 10  6'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Category'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Comp  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 74'Emply'
     A                                      DSPATR(HI)
     A                                  9 67'Pct'
     A                                      DSPATR(HI)
     A                                  9 61'Rem'
     A                                      DSPATR(HI)
     A                                 10 43'Pcs/Hr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'End Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
