500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
416 AA* ***************************************************************
416 AA* *****  If the position of any slot entry field is changed *****
416 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
416 AA* *****  program.                                           *****
416 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LP120FM   Pallet License Plate - View RF
     A*  12 February 1997
     A*  Dave Sommerville
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
414dAA*    09/13/00  HNK  4.14d
     A*      - Add pick slot on screen 2
     A*
416 AA*    03/28/02  DAS  4.16
     A*      - Added W2ITM2 to top of screen.
     A*        Re: Item number comes last in W2ITEM and would get
     A*            chopped off. AFI relies on item number.
416aAF*    05/23/02  RRH  4.16a
     F*      - Increase W1LCNS and W2LCNS to 15.
     A*
500 AA*   05/31/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   10/29/07  LMC  5.10a
     A*     - 16 x 20 screens for handheld
     A*     - 16 X 20 Screens for spanish have been copied from the
     A*       original spanish screens, no changes were made to them
     A*       at this time, if we have a client that needs them they
     A*       will be reformatted at that time.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.01'
     A                                  1 13'License'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'Tran. #.'
     A            W1TRN#         7Y 0B  4 11CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  5  2'-OR-'
     A                                  6  2'License.'
416aDA*           W1LCNS         7Y 0B  6 11CHECK(ER)
416aMA            W1LCNS        15A  B  6 11CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20020328  134848  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'License'
     A                                      DSPATR(HI)
416aDA*                                 2  1'License:'
416aMA                                  2  1'Licn:'
416aDA*           W2LCNS         7Y 0O  2 10EDTCDE(3)
416aMA            W2LCNS        15A  O  2 10
416aMA                                  2 25'Item'
416aMA            W2ITM2        15   O  2 26
416aDA*                                 2 21'Item#'
416aDA*           W2ITM2        15   O  2 27
     A                                  3  1'Trans.#:'
     A            W2TRN#         7Y 0O  3 10EDTCDE(3)
     A                                  4  1'Item:'
     A            W2ITEM        49A  O  4  7
     A                                  5  1'Slot:'
     A            W2DISP        12A  O  5  7
     A                                  6  1'Qty:'
     A            W2PQTY         5Y 0O  6  6EDTCDE(4)
     A            W2PDTE         6Y 0O  6 13EDTCDE(Y)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A            W2PSLT        12A  O  5 29
     A                                  5 20'Pck Slt:'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.01'
     A                                  1 13'Licencia'
     A                                      DSPATR(HI)
     A                                  3  2'Alm '
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'# de Tran'
     A            W1TRN#         7Y 0B  4 12CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  5  2'-O-'
     A                                  6  2'Licencia.'
416aDA*           W1LCNS         7Y 0B  6 12CHECK(ER)
416aMA            W1LCNS        15A  B  6 12CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20020328  134848  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'Licencia'
     A                                      DSPATR(HI)
416aDA*                                 2  1'License:'
416aMA                                  2  1'Licn:'
416aDA*           W2LCNS         7Y 0O  2 10EDTCDE(3)
416aMA            W2LCNS        15A  O  2 10
416aMA                                  2 25'Prdc'
416aMA            W2ITM2        15   O  2 26
416aDA*                                 2 21'#Prdc'
416aDA*           W2ITM2        15   O  2 27
     A                                  3  1'# Trans:'
     A            W2TRN#         7Y 0O  3 10EDTCDE(3)
     A                                  4  1'Prdc:'
     A            W2ITEM        49A  O  4  7
     A                                  5  1'Slot:'
     A            W2DISP        12A  O  5  7
     A                                  6  1'Ctd:'
     A            W2PQTY         5Y 0O  6  6EDTCDE(4)
     A            W2PDTE         6Y 0O  6 13EDTCDE(Y)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A            W2PSLT        12A  O  5 29
     A                                  5 20'Pck Slt:'
510aAA          R SCREEN61
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      CLRL(15)
510aAA  98                                  ALARM
510aAA                                  1  2'LP120.61'
510aAA                                  1 13'License'
510aAA                                      DSPATR(HI)
510aAA                                  3  1'Whse'
510aAA            W1WHSE         3Y 0B  3  6
510aAA N21                                  DSPATR(HI)
510aAA  21                                  DSPATR(RI)
510aAA N21N90                               DSPATR(UL)
510aAA  01                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA                                      EDTCDE(4)
510aAA                                  4  1'Trn#'
510aAA            W1TRN#         7Y 0B  4  6CHECK(ER)
510aAA  23                                  DSPATR(RI)
510aAA N23N90                               DSPATR(UL)
510aAA  03
510aAAON35                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA N23N90                               DSPATR(HI)
510aAA                                      EDTCDE(4)
510aAA                                  5  1'-OR-'
510aAA                                  6  1'Lic#'
510aAA            W1LCNS        15A  B  6  6CHECK(ER)
510aAA  24                                  DSPATR(RI)
510aAA N24N90                               DSPATR(UL)
510aAA  04
510aAAON32                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA N24N90                               DSPATR(HI)
510aAA                                 15  2'F3=Exit  F12=Cancel'
510aAA                                      COLOR(BLU)
510aAA          R SCREEN62
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      CLRL(15)
510aAA  98                                  ALARM
510aAA                                  1  2'LP120.02'
510aAA                                  1 11'License'
510aAA                                      DSPATR(HI)
510aAA                                  2  1'Lic#'
510aAA            W2LCNS        15A  O  2  6
510aAA                                  3  1'Item'
510aAA            W2ITM2        15A  O  3  6
510aAA                                  4  1'Trn#'
510aAA            W2TRN#         7Y 0O  4  6EDTCDE(3)
510aAA                                  5  1'Item'
510aAA            W2ITEM        49A  O  5  6
510aAA                                  6  1'Slot'
510aAA            W2DISP        12A  O  6  9
510aAA                                  8  1'Qty:'
510aAA            W2PQTY         5Y 0O  8  6EDTCDE(4)
510aAA            W2PDTE         6Y 0O  8 13EDTCDE(Y)
510aAA                                 15  2'F3=Exit  F12=Cancel'
510aAA                                      COLOR(BLU)
510aAA            W2PSLT        12A  O  7  9
510aAA                                  7  1'Pck Slt'
510aAA*
510aAA* ERROR MESSAGE SUBFILE RECORDS
510aAA*
510aAA*    ERROR MESSAGE SUBFILE RECORD
510aAA*
510aAA          R M16REC                    SFL
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      SFLMSGRCD(16)
510aAA            #MSGK                     SFLMSGKEY
510aAA            #PGMQ                     SFLPGMQ(10)
510aAA*    ERROR MESSAGE SUBFILE CONTROL RECORD
510aAA*
510aAA          R M16CTL                    SFLCTL(M16REC)
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      SFLSIZ(0002)
510aAA                                      SFLPAG(0001)
510aAA  98                                  ALARM
510aAA                                      LOCK
510aAA                                      OVERLAY
510aAA N97                                  SFLCLR
510aAA  97                                  SFLDSP
510aAA  97                                  SFLEND
510aAA          R M16CLR
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      LOCK
510aAA                                      SLNO(16)
510aAA                                      CLRL(01)
510aAA*****************************************************************
510aAA* Spanish Screens
510aAA*****************************************************************
510aAA*----------------------------------------------------------------
510aAA          R SCRN61SP
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      CLRL(15)
510aAA  98                                  ALARM
510aAA                                  1  2'LP120.01'
510aAA                                  1 13'Licencia'
510aAA                                      DSPATR(HI)
510aAA                                  3  2'Alm '
510aAA            W1WHSE         3Y 0B  3  7
510aAA N21                                  DSPATR(HI)
510aAA  21                                  DSPATR(RI)
510aAA N21N90                               DSPATR(UL)
510aAA  01                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA                                      EDTCDE(4)
510aAA                                  4  2'# de Tran'
510aAA            W1TRN#         7Y 0B  4 12CHECK(ER)
510aAA  23                                  DSPATR(RI)
510aAA N23N90                               DSPATR(UL)
510aAA  03
510aAAON35                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA N23N90                               DSPATR(HI)
510aAA                                      EDTCDE(4)
510aAA                                  5  2'-O-'
510aAA                                  6  2'Licencia.'
510aAA            W1LCNS        15A  B  6 12CHECK(ER)
510aAA  24                                  DSPATR(RI)
510aAA N24N90                               DSPATR(UL)
510aAA  04
510aAAON32                                  DSPATR(PC)
510aAA  90                                  DSPATR(PR)
510aAA N24N90                               DSPATR(HI)
510aAA                                  7  2'F3=Salir  F12=Cancelar'
510aAA                                      COLOR(BLU)
510aAA          R SCRN62SP
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      CLRL(15)
510aAA  98                                  ALARM
510aAA                                  1  2'LP120.02'
510aAA                                  1 11'Licencia'
510aAA                                      DSPATR(HI)
510aAA                                  2  1'Licn:'
510aAA            W2LCNS        15A  O  2 10
510aAA                                  2 25'Prdc'
510aAA            W2ITM2        15A  O  2 26
510aAA                                  3  1'# Trans:'
510aAA            W2TRN#         7Y 0O  3 10EDTCDE(3)
510aAA                                  4  1'Prdc:'
510aAA            W2ITEM        49A  O  4  7
510aAA                                  5  1'Slot:'
510aAA            W2DISP        12A  O  5  7
510aAA                                  6  1'Ctd:'
510aAA            W2PQTY         5Y 0O  6  6EDTCDE(4)
510aAA            W2PDTE         6Y 0O  6 13EDTCDE(Y)
510aAA                                  7  2'F3=Salir  F12=Cancelar'
510aAA                                      COLOR(BLU)
510aAA            W2PSLT        12A  O  5 29
510aAA                                  5 20'Pck Slt:'
