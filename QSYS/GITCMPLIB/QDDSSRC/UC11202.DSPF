     A*%%TS  SD  20000804  144652  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UC112FM   UPC Code maintenance - Change screen
     A*  04 April, 1997
     A*  Roxanne Harrison
     A*
     A*  Revisions
     A*    05/28/03  RH   4.17a
     A*      - Enh: Do not allow Item, or Vendor number to change.
     A*    06/29/06  RH   5.10a
     A*      - Enh: Add F20 Override for invalid UPC.
530a A*    10/26/09  RH   5.30a
     A*      - Enh: Add code to allow vendor # to be scanned w/o
     A*        pressing field exit, change endit code.
     A*
650a A*    08/19/13  LMC  6.50a
     A*      - Enh: Added the ability to scan a GS1 barcode to load
     A*        the UPC file.
     A*      - Renamed record format to UC11201.
700a A*    10/14/16  LMC  7.00a
     A*      - Enh: UPC file now has uom fields.
     A*      - rename display file from UC11201 to UC11202.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000804  144652  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
510a A                                      CF20
     A  98                                  ALARM
     A                                  1  2'UC112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'UPC Code Maintenance'
     A                                      DSPATR(HI)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 13EDTCDE(3)
     A                                  5 17'-'
     A            WHDESC        30A  O  5 19
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
650aDA*                                10  4'UPC Code . . . . . . .'
650aDA                                 09  4'UPC Code . . . . . . .'
650aDA*           W1UPC         20A  B 10 28
650aMA            W1UPC        100A  B 09 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Item . . . . . . . . .'
     A            W1ITEM        15A  B 11 28
     A  22                                  DSPATR(RI)
417a A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A  02                                  DSPATR(PC)
417a A  90
417a AO 91                                  DSPATR(PR)
     A                                 15  4'Brand  . . . . . . . .'
     A            W1BRAN        10A  B 15 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Vendor number  . . . .'
530aDA*           W1VNUM        11Y 0B 12 28EDTCDE(3)
530aMA            W1VNUM        11Y 0B 12 28EDTCDE(4)
530aMA                                      CHECK(ER)
     A  24                                  DSPATR(RI)
417a A N24N90N91                            DSPATR(HI)
     A N24N90N91                            DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
417a AO 91                                  DSPATR(PR)
     A                                 13  4'Vendor name  . . . . .'
     A            W1VNAM        40A  B 13 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Vendor item  . . . . .'
     A            W1VITM        15A  B 14 28
     A  27N90                               DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aAA                                 16  4'UOM  . . . . . . . . .'
700aAA            W1UOM          2A  B 16 28
700aAA  26                                  DSPATR(RI)
700aAA N26N90                               DSPATR(UL)
700aAA  06                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
510aMA                                 23  2'F5=Refresh  F4=Prompt  F12=Cancel-
510aMA                                         F20=Ovrrid'
510aDA*                                23  2'F5=Refresh  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
