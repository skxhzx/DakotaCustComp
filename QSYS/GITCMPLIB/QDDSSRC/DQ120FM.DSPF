     A*%%TS  SD  20010727  082338  DAVE        REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  DQ120FM   Work with daily quantities - Items
     A*  28 June 2001
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
415 AA*    06/28/01  DAS  4.15
     A*      - Created.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010628  094843  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'DQ120'
     A                                  1 26'Work with Daily Quantity Items'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Run:'
     A                                      DSPATR(HI)
     A            WORDTE         6Y 0O  2 35EDTCDE(Y)
     A                                      DSPATR(HI)
     A            WORTIM         6Y 0O  2 45EDTWRD(' 0:  :  ')
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20010706  080640  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A                                  2 43'Change to warehouse .'
     A            PTINP1         3Y 0B  2 65EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3 43'Position to item  . .'
     A            PTINP2        15A  B  3 65
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
414 DA* 85                                  SFLNXTCHG
     A          R DSPSFL                    SFL
     A*%%TS  SD  20010727  081041  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            DQITEM    R        O 11  6REFFLD(DQREC/DQITEM *LIBL/DLYQTY)
     A            ITTYPE    R        O 11 23REFFLD(ITREC/ITTYPE *LIBL/PIRITEM)
     A            W1ACTV         3A  O 11 58
     A            W1CDIF         3A  O 11 63
     A            W1SDIF         3A  O 11 68
     A            W1SSB          1A  O 11 28
     A            W1DESC        25   O 11 31
     A            W1INV          3   O 11 72
     A            W1RCLC         3   O 11 77
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20010727  081041  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Description              '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 57'Actv'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 62'Diff'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 62'Calc'
     A                                      DSPATR(HI)
     A                                 10 27'SSB'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 67'Slot'
     A                                      DSPATR(HI)
     A                                 10 67'Diff'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Inv'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 76'Rclc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 76'Qty'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            DQITEM    R        O 11  6REFFLD(DQREC/DQITEM *LIBL/DLYQTY)
     A            W2SDTE         6Y 0O 11 23EDTCDE(Y)
     A            DQSTIM    R        O 11 32REFFLD(DQREC/DQSTIM *LIBL/DLYQTY)
     A                                      EDTWRD(' 0:  :  ')
     A            W2EDTE         6Y 0O 11 42EDTCDE(Y)
     A            DQETIM    R        O 11 51REFFLD(DQREC/DQETIM *LIBL/DLYQTY)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 23'----- Start -----'
     A                                      DSPATR(HI)
     A                                 10 23'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 42'------ End ------'
     A                                      DSPATR(HI)
     A                                 10 42'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            DQITEM    R        O 11  6REFFLD(DQREC/DQITEM *LIBL/DLYQTY)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SUBSET
     A*%%TS  SD  20010727  082338  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(6 20 14 40)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  1 16'Subset List'
     A                                      DSPATR(HI)
     A                                  3  1'Items with activity  . . . . . .'
     A            W2ACTV         1A  B  3 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A                                  3 36'Y/N/B'
     A                                  5  1'Items with calc differences  . .'
     A            W2CDIF         1A  B  5 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A                                  5 36'Y/N/B'
     A                                  7  1'Items with corresponding SSB . .'
     A            W2SSB          1A  B  7 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A                                  7 36'Y/N/B'
     A                                  9  1'Items with slot differences  . .'
     A            W2SDIF         1A  B  9 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A                                  9 36'Y/N/B'
     A                                 11  1'Inventoried items  . . . . . . .'
     A                                 13  1'Recalculated items . . . . . . .'
     A            W2INV          1   B 11 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A            W2RCLC         1   B 13 34VALUES('Y' 'N' 'B')
     A                                      DSPATR(HI)
     A                                 11 36'Y/N/B'
     A                                 13 36'Y/N/B'
