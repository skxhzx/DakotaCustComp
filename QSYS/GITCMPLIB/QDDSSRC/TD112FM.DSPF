     A*%%TS  SD  20080923  144434  BFC         REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TD112FM   Task standards maintenance - Display
     A*  22 December, 1995
     A*  Hemant Kapadia
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*  11/20/00  HNK  4.15
     A*    - Trans/Hr added
     A*
     A*  08/23/09  GJA  5.30
     A*    - Dept heading changed to Dept/Zone
     A*
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20080923  144434  BFC         REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            TDWHSE    R        O 11  2REFFLD(TDREC/TDWHSE *LIBL/TASKDET)
     A            TDWHDP    R        O 11  9REFFLD(TDREC/TDWHDP *LIBL/TASKDET)
     A            W1STDP         5Y 0B 11 25TEXT('Pieces/hour')
     A                                      EDTCDE(3)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A N90                                  DSPATR(PC)
     A            W1STPH         5Y 0B 11 35TEXT('Trans/Hr')
     A                                      EDTCDE(3)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A N90                                  DSPATR(PC)
     A            TDSTYP    R        O 11 20REFFLD(TDREC/TDSTYP *LIBL/TASKDET)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20080923  144434  BFC         REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'TD112.01'
     A                                  1 28'Task Standards Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  2'Task:'
     A            $PCODE         6A  O  7  8
     A                                  7 15'-'
     A            TKDESC        30A  O  7 17
     A                                 10 25'Pcs/Hr'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 34'Trans/Hr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
530aMA                                 10  8'Dept/Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 19'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  2'Whse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
