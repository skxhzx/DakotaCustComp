     A*%%TS  SD  19991201  112709  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS170FM   Truck Build split stops. - Display screen
     A*  01 December 1999
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    01/23/09  CWM  5.20
     A*      Enh: Added support for 3 digit stop numbers.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PS170.01'
     A                                  1 31'Work with Split Items'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Truck Detail'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Warehouse:'
     A            WOWHSE        35A  O  1 13
     A                                  1 52'Route:'
     A            WORTE          5A  O  1 59
     A                                  2 52'Position to Compartment '
     A            PTINP1         1A  B  2 77
     A                                  3 52'Position to Stop . . . .'
520 DA*           PTINP2         2Y 0B  3 77EDTCDE(4)
520 MA            PTINP2         3Y 0B  3 77EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1COMP         1A  O 11  8
520 DA*           W1STOP         3Y 0O 11 11EDTCDE(4)
520 CA            W1STOP         3Y 0H
520 AA            X1STOP         3Y 0O 11 11EDTCDE(4)
     A            W1ORD          7Y 0O 11 15EDTCDE(4)
     A            W1ODSQ         5Y 0O 11 23EDTCDE(4)
     A            W1ITEM        15A  O 11 29
     A            W1ROW          2Y 0O 11 46EDTCDE(4)
     A            W1COL          2Y 0O 11 50EDTCDE(4)
     A            W1UNLD         2Y 0O 11 54EDTCDE(4)
     A            W1PCS          5Y 0O 11 58EDTCDE(3)
     A            W1CUBE         9Y 3O 11 65EDTCDE(3)
     A            W1BULK         1A  O 11 77
     A                                 12 45'Total weight:'
     A            W1SWGT         9Y 2O 12 59EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A                                      CF11
     A*                                     ROLLDOWN
     A*                                     ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A  01                                  SFLEND(*MORE)
     A N01                                  SFLCLR
     A  44                                  SFLDROP(CF11)
     A N44                                  SFLFOLD(CF11)
     A                                      SFLMODE(&SFLMOD)
     A                                      SFLCSRRRN(&RTNRRN)
     A            SFLMOD         1A  H      TEXT('Hidden: Subfile mode')
     A            RTNRRN         5S 0H      TEXT('Hidden: Subfile RRN')
     A                                  9 53'Unld'
     A                                      DSPATR(HI)
     A                                  9 58'Total'
     A                                      DSPATR(HI)
     A                                  9 65'Total'
     A                                      DSPATR(HI)
     A                                  9 76'Incl'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Comp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 11'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Order#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Seq# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 45'Row'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 49'Col'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Seq '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Cube      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Bulk'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
