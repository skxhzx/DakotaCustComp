     A*%%TS  SD  20200819  160220  KENNETHE    REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP230FM   PIR Feature options
     A*  16 May 2000
     A*  Max Blacknik
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
510aAA*    10/25/06  RH   5.10a
     A*      - Add Stand Alone to screen option.
640aAA*    05/10/11  DAS  6.40a
     A*      - Changed to OP23001.
     A*      - Changed Locator to InBound, Standalone to Truck Builder.
     A*      - Added Truck Builder Full/Light code.
730aAA*    04/03/19  DAS  7.30a
     A*      - Added W1TBTYPE, W1TBCOL
740 AA*    08/19/20  KDE  7.40
     A*      - Added W1pltdl
750aAA*    02/17/21  DAS  7.50a
     A*      - Changed 'Truck Builder' to 'TruckBuilder(r)'
     A*      - Changed 'TruckBuilder' to 'TruckBuilder(r)'
     A*      - Changed 'Advanced' to 'Prime'
     A*      - Because only text changed, modified existing OP23003.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20200819  160220  KENNETHE    REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      CF04
     A                                      OVERLAY
     A                                  1  2'OP230.01'
     A                                  1 32'PIR Feature Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'FS-WMS installed feature . . . . .-
     A                                       . .'
     A            W1FEAT         1A  B  7 44
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 49'1=InBound'
     A                                  8 49'2=Full system'
     A                                  9 49'3=TruckBuilder(r)'
     A                                 11  4'TruckBuilder(r) Full/Light code  .-
     A                                       . .'
     A            W1LGHT         3A  B 11 44CHECK(LC)
     A                                 12  6'- Only used with feature 3'
     A                                 13  6'- One code will turn on Full'
     A                                 14  6'- All other codes will turn on Lig-
     A                                      ht'
     A                                 16  4'TruckBuilder(r) type . . . . . . .-
     A                                       . .'
     A            W1TBTYPE       1A  B 16 44
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 49'S=Std E=Enhanced(Prime) B=Both'
     A                                 17  4'TruckBuilder(r) Prime 3+ columns .-
     A                                       . .'
     A            W1TBCOL        1A  B 17 44
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 49'N=Std 2 col, Y=Allow 3+ col'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A                                 18  4'TruckBuilder(r) Pallet Building Le-
     A                                      vel.'
     A            W1PLTBL        1A  B 18 44
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 49'1=Std  2=Level 2  3=Level 3'
     A                                 20  4'TruckBuilder(r) Pallet Types. . . -
     A                                       . .'
     A  40                                  DSPATR(ND)
     A            W1PLTYPS      10A  B 20 44
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 40                                  DSPATR(PR)
     A  40                                  DSPATR(ND)
     A                                 20 57'Codes from GLOZONTYP'
     A  40                                  DSPATR(ND)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
