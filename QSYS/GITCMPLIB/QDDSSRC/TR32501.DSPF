     A*%%TS  SD  20111129  103247  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR325FM    TRAX stop detail - Display
     A*  Ginger Andrews
     A*
     A*  Revisions
     A*
640a A*    10/11/11  GJA  6.40a
     A*      - Created.
640bAA*    11/28/11  RBD  6.40b
     A*      - Reduced some hdgs to make more room.
     A*      - Added customer name.
     A*      - Moved memo to second view.
     A*      - Fixed end of subfile indicator.
     A*
640cAA*    02/17/12  RBD  6.40c
     A*      - Increased sfl page from 12 to 14 to be
     A*        consistent with other Trax view routes programs.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20111128  153854  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTPSTP    R        O  8  6REFFLD(TTPREC/TTPSTP *LIBL/TTSTP)
640bAA            W1CNAM        38A  O  8 10
     A            W1PDLV         1A  O  8 50
     A            W1QDLV         6Y 0O  8 53EDTCDE(4)
     A            W1QDLN         6Y 0O  8 60EDTCDE(4)
     A            W1QCRT         6Y 0O  8 67EDTCDE(4)
     A            W1QPRT         6Y 0O  8 74EDTCDE(4)
640bDA**          TTPMEM    R        O  8 50REFFLD(TTPREC/TTPMEM *LIBL/TTSTP)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20111129  103247  RETHA       REL-V6R1M0  5761-WDS
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
640bDA**90                                  SFLEND(*MORE)
640bMA  84                                  SFLEND(*MORE)
     A                                      SFLSIZ(0034)
640cDA**                                    SFLPAG(0012)
640cMA                                      SFLPAG(0014)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Trax Stop Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 54'Pcs'
     A                                      DSPATR(HI)
     A                                  6 60'Pcs'
     A                                      DSPATR(HI)
     A                                  6 69'Curr / Prev'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                  7  5'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 10'Name                              -
     A                                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'Dlv'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 60'NonScn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 68'Pcs Returned'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 54'Dlvrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20111128  152500  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTPSTP    R        O  8  6REFFLD(TTPREC/TTPSTP *LIBL/TTSTP)
     A            W1PSCN         1A  O  8 11
     A            W1STRD         6  0O  8 14EDTWRD('  /  /  ')
     A            TTPSRT    R        O  8 24REFFLD(TTPREC/TTPSRT *LIBL/TTSTP)
     A                                      EDTWRD('  :  :  ')
     A            W1FIND         6Y 0O  8 33EDTWRD('  /  /  ')
     A            TTPFIT    R        O  8 43REFFLD(TTPREC/TTPFIT *LIBL/TTSTP)
     A                                      EDTWRD('  :  :  ')
640bAA            W1MEM         28A  O  8 52
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20111129  103247  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
640cDA**                                    SFLPAG(0012)
640cMA                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
640bDA**90                                  SFLEND(*MORE)
640bMA  84                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 10'Scn'
     A                                      DSPATR(HI)
     A                                  6 19'Delivery'
     A                                      DSPATR(HI)
     A                                  6 38'Delivery'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 10'Dlv'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 14'  Start Date/Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 52'Memo                        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 33' Finish Date/Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 25'View Trax Stop Detail'
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 22  2'F3=Exit  F5=Refresh  F10=Prev view-
     A                                        F11=Next view  F12=Cancel  -
     A                                      F21=Print'
     A                                      COLOR(BLU)
     A          R MKEY1
     A                                      OVERLAY
     A            W1MSG1        75A  O 24  3
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A          R WINDOW1
     A*%%TS  SD  20111128  154730  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order  . . . . . .'
     A            W1SORT         1A  B  3 20DSPATR(HI)
     A                                  3 25'Ascending, Descending'
     A                                  4  1'Order by . . . . .'
     A            W1OBY          1A  B  4 20DSPATR(HI)
     A                                  7  1'Warehouse  . . . .'
     A            W1WHSE         3Y 0B  7 20DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  8  1'Route Id . . . . .'
     A            W1RTE          5A  B  8 20DSPATR(HI)
     A                                  8 32'*ALL'
     A                                  9  1'Delivery date  . .'
     A            W1DLVD         6Y 0B  9 20DSPATR(HI)
     A                                  9 32'0=All'
     A                                 10  1'Driver . . . . . .'
     A            W1USR         10A  B 10 20DSPATR(HI)
     A                                 10 32'*ALL'
     A                                 11  1'Non-scanned       '
     A                                 12  3'items only . . .'
     A            W1NSCN         1A  B 12 20DSPATR(HI)
     A                                 12 32'Y=yes'
     A                                 13  1'Returns only . . .'
     A            W1RETN         1A  B 13 20DSPATR(HI)
     A                                 13 32'Y=yes'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20111129  103247  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
