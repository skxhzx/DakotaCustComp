     A*%%TS  SD  20070918  061945  JEFF        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*  Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*  BFC Software, Inc.
     A*  245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*  West Chicago, IL  60185
     A*  (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UI110FM   UCI Catch Weight - Display
     A*  September 17, 2007
     A*  Jeffrey Jahnke
     A*
     A*  Revisions:
     A*
510 AA*  10-17-2007 JCJ 5.10
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070917  182529  JEFF        REL-V5R4M0  5722-WDS
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'CE130.01'
     A                                  1 32'Catch Weight Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Warehouse :'
     A            W1WHSE         3Y 0B  4 14EDTCDE(Z)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WOWHSE        40A  O  4 18
     A                                  6  2'Scan Tran#:'
     A            W1TRAN         7Y 0B  6 14EDTCDE(Z)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A            W1MSG         40A  O  7  1DSPATR(HI)
     A*****************************************************************
     A          R TITLE
     A*%%TS  SD  20070917  215314  JEFF        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'CE130.02'
     A                                      DSPATR(HI)
     A                                  1 32'Catch Weight Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Label Selection'
     A                                      DSPATR(HI)
     A                                  3  2'Scan BFC Label'
     A            W2SCNL         7Y 0B  3 17EDTCDE(Z)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20070917  214348  JEFF        REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(05)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070917  214348  JEFF        REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W2OPT          2A  B  9  2
     A            W2ITEM        10A  O  9  6
     A            W2DESC         9A  O  9 18
     A            W2QRMN         5Y 0O  9 29EDTCDE(3)
     A            W2LBL#         7Y 0O  9 36EDTCDE(3)
     A            RECNOX         3S 0O  9 45
     A*
     A*****************************************************************
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070917  214348  JEFF        REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A N84                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  8  6'Item      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Desc     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 29' Qty '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 36'Bfc Lbl'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A                                      DSPATR(HI)
     A            W2MSG         40A     2  2
     A                                      DSPATR(HI)
     A*****************************************************************
     A          R TITLE3
     A*%%TS  SD  20070918  061945  JEFF        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'CE130.03'
     A                                      DSPATR(HI)
     A                                  1 32'Catch Weight Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 32'UCI Selection'
     A                                      DSPATR(HI)
     A                                  2 52'Quantity Picked:'
     A            W3QPCK         5Y 0O  2 69EDTCDE(Z)
     A                                  3 52'UCI Count......:'
     A            W3UCNT         5Y 0O  3 69EDTCDE(Z)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC3
     A*%%TS  SD  20070917  225135  JEFF        REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(04)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN3        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL3                   SFL
     A*%%TS  SD  20070918  061945  JEFF        REL-V5R4M0  5722-WDS
     A  95                                  SFLNXTCHG
     A            W3OPT          2A  B  8  2
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A            W3INBR        10A  O  8  5
     A            W3IWGT         7Y 2O  8 16EDTCDE(3)
     A            W3ISTS         1A  O  8 30
     A            W3IPCK         1A  O  8 36
     A            W3TRAN         7Y 0O  8 42EDTCDE(Z)
     A            W3LBL#         7Y 0O  8 51EDTCDE(Z)
     A*
     A*****************************************************************
     A          R DSPSFC3                   SFLCTL(DSPSFL3)
     A*%%TS  SD  20070917  225633  JEFF        REL-V5R4M0  5722-WDS
     A                                      CF08
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A N94                                  SFLEND(*MORE)
     A                                      SFLSIZ(0130)
     A                                      SFLPAG(0013)
     A            RECN2          4S 0H      SFLRCDNBR
     A                                  6 29'Stock'
     A                                      DSPATR(HI)
     A                                  6 35'Pick'
     A                                      DSPATR(HI)
     A                                  7  5'UCI Number'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 16' Weight '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 29'Flag'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 35'Flag'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 42' Tran# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 51' Label#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A          R CMDREC3
     A*%%TS  SD  20070917  160945  JEFF        REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN3        79   O  1  2COLOR(BLU)
     A                                      DSPATR(HI)
     A            W3MSG         40A  O  2  2DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
