     A****************************************************************
     A* LOAD60DPR  Verified Returned Trailer Equipment - Drvr Invoices.
     A* December 11, 2008
     A* Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/10/11  JGD  6.40
     F*      - Created
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*----------------------------------------------------------------
     A          R HEAD1
     A*----------------------------------------------------------------
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    31
     A                                      DFT('(Driver Invoice) Returne-
     A                                      d Trailer Equipment')
     A            FRMDTE         6S 0O    79EDTCDE(Y)
     A                                    89
     A                                      DFT('Thru')
     A            TODTE          6S 0O    95EDTCDE(Y)
     A                                   119
     A                                      DFT('Page')
     A            PAGE           4S 0O   123
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    37
     A                                   119
     A                                      DFT('LOADR60D')
     A                                      SPACEA(002)
     A* Print column headings
     A*----------------------------------------------------------------
     A          R COLUMNS
     A*----------------------------------------------------------------
      *
     A                                     1'Equipment'
      *
     A                                    28'Quantity'
      *
     A                                    41'Driver'
      *
     A                                    61'Bay II'
      *
     A                                    83'Load'
      *
     A                                   103'Return'
      *
     A                                      SPACEA(001)
      *
     A                                     1'Description'
     A                                      UNDERLINE
     A
     A                                    17'Load'
     A                                      UNDERLINE
      *
     A                                    22'Rtrn'
     A                                      UNDERLINE
      *
     A                                    27'Short'
     A                                      UNDERLINE
      *
     A                                    33'Over'
     A                                      UNDERLINE
      *
     A                                    41'Name           '
     A                                      UNDERLINE
      *
     A                                    61'Name           '
     A                                      UNDERLINE
      *
     A                                    83'Date    '
     A                                      UNDERLINE
      *
     A                                   103'Date    '
     A                                      UNDERLINE
      *
     A                                   113'Door'
     A                                      UNDERLINE
      *
     A                                   123'Rte#'
     A                                      UNDERLINE
      *----------------------------------------------------------------
     A          R DETAIL1
     A*----------------------------------------------------------------
      *
     A                                      SPACEB(002)
     A            W1DESC        15A        1
      *
     A            W1LOAD         4S 0     17EDTWRD('  0 ')
      *
     A            W1UNLD         4S 0     22EDTWRD('  0 ')
      *
     A            W1SHRT         4S 0     27EDTWRD('  0 ')
      *
     A            W1OVER         4S 0     33EDTWRD('  0 ')
      *
     A            W1DNAM        22A  O    41
      *
     A            W1RUSR        22A       61
      *
     A            W1DATE         6  0O    83
     A                                      EDTCDE(Y)
      *
     A            W1RDTE         6  0O   103
     A                                      EDTCDE(Y)
      *
     A            W1DOOR         3A  O   113
      *
     A            W1RTE#         5A  O   124
     A*----------------------------------------------------------------
     A          R TOTAL1
     A*----------------------------------------------------------------
     A                                      SPACEB(002)
      *
     A            W1EMP#         5S 0O    30
     A                                      EDTCDE(Z)
      *
     A                                    78
     A                                      DFT('Total Exceptions:')
     A            W1EXCP         5S 0O    95
     A                                      EDTCDE(Z)
      *
     A                                     1
     A                                      DFT('_____________________')
      *
     A                                    30
     A                                      DFT('_____')
      *
     A                                    49
     A                                      DFT('____________________')
      *
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('Driver''s Signature')
      *
     A                                    30
     A                                      DFT('Drv #')
      *
     A                                    49
     A                                      DFT('Manager''s Signature')
     A*
     A* Skip 1 Line
     A*----------------------------------------------------------------
     A          R SKIP1
     A*----------------------------------------------------------------
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*
     A* Skip to next page
     A*----------------------------------------------------------------
     A          R SKIPAG
     A*----------------------------------------------------------------
     A                                      TEXT('SKIP TO NEXT PAGE')
     A                                      SKIPB(006)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*----------------------------------------------------------------
     A          R FOOT1
     A*----------------------------------------------------------------
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('LOADR60D: End of Report   ')
     A*
