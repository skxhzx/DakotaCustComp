     A*%%TS  SD  20060725  171856  MLBLACKNIK  REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*  Revisions
     A*
510 AA*    07/25/06  MLB  5.10
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT190.01'
     A                                  1 26'Change Zone Details for Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse . . . . . . . . . . . . -
     A                                      . .'
     A            W1WHSE         3Y 0B  8 43EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  8 60'1 - 999'
     A                                  9  4'Truck . . . . . . . . . . . . . . -
     A                                      . .'
     A            W1TRUK        10A  B  9 43
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  9 60'*ALL, Code'
     A                                 10  4'Zone  . . . . . . . . . . . . . . -
     A                                      . .'
     A            W1ZONE         6A  B 10 43
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                 10 60'*ALL, Code'
     A                                 12  4'New qty level for combined labels -
     A                                      . . '
     A            W1QTY          3A  B 12 43
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AO 24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Print combined label warning  . . -
     A                                      . .'
     A                                 13 43'Before:'
     A            W1CLB          1A  B 13 51
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05
     AO 25                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 54'After:'
     A            W1CLA          1A  B 13 61
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06
     AO 26                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Print Unit of Measure warning . . -
     A                                      . .'
     A                                 14 43'Before:'
     A            W1UMB          1A  B 14 51
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07
     AO 27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 54'After:'
     A            W1UMA          1A  B 14 61
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08
     AO 28                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'New discrepancy label ratio . . . -
     A                                      . .'
     A            W1DSL          2A  B 15 43
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09
     AO 29                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 48'1 label for every x pick labels'
     A                                 16  4'New catchweight label ratio . . . -
     A                                      . .'
     A            W1CWL          2A  B 16 43
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  13
     AO 30                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 48'1 label for every x c/wgt labels'
     A                                 19  7'Note: Fields will only be updated -
     A                                      if a value is entered (not blank)'
     A                                      COLOR(YLW)
     A                                      DSPATR(RI)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT190.02'
     A                                  1 26'Change Zone Details for Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  8 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  9  4'Truck  . . . . . . . . . . .'
     A            W1TRUK    R        O  9 34REFFLD(W1TRUK *SRC)
     A                                 10  4'Zone . . . . . . . . . . . .'
     A            W1ZONE    R        O 10 34REFFLD(W1ZONE *SRC)
     A                                 12  4'New qty level for combined labels -
     A                                      . . '
     A            W1QTY     R        O 12 43REFFLD(W1QTY *SRC)
     A                                 13  4'Print combined label warning  . . -
     A                                      . .'
     A                                 13 43'Before:'
     A            W1CLB     R        O 13 51REFFLD(W1CLB *SRC)
     A                                 13 54'After:'
     A            W1CLA     R        O 13 61REFFLD(W1CLA *SRC)
     A                                 14  4'Print Unit of Measure warning . . -
     A                                      . .'
     A                                 14 43'Before:'
     A            W1UMB     R        O 14 51REFFLD(W1UMB *SRC)
     A                                 14 54'After:'
     A            W1UMA     R        O 14 61REFFLD(W1UMA *SRC)
     A                                 15  4'New discrepancy label ratio . . . -
     A                                      . .'
     A            W1DSL     R        O 15 43REFFLD(W1DSL *SRC)
     A                                 15 48'1 label for every x pick labels'
     A                                 16  4'New catchweight label ratio . . . -
     A                                      . .'
     A            W1CWL     R        O 16 43REFFLD(W1CWL *SRC)
     A                                 16 48'1 label for every x c/wgt labels'
     A                                 18 16'You are about to change the zone d-
     A                                      etails in all of the'
     A                                 19 16'zones for all of the trucks select-
     A                                      ed above'
     A                                 20 16'Please make sure you know what you-
     A                                       are doing.'
     A                                 21 31'Press F10 to continue.'
     A                                 23  2'F12=Cancel'
     A          R SCREEN3
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT110DS.03'
     A                                  1 26'Change Label Program for Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                  6 13'to cancel your request.'
     A                                  9 17'If you are sure, press F22 to chan-
     A                                      ge the trucks.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
    DA*                                     SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
