     A*%%TS  SD  20050329  183226  DAVE        REL-V5R3M0  5722-WDS
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A**************************************************************************
     A*  Revisions:
     A*
500 AA*    03/29/05  DAS  5.00
     A*     - Added slot position to screen.
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20050329  183226  DAVE        REL-V5R3M0  5722-WDS
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT135.01'
     A                                  1 30'Item Slot Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Warehouse :'
     A            W1WHSE        40A  O  3 14
     A                                  4  2'Department:'
     A            W1WHDP         5A  O  4 14
     A                                  5  2'Section ..:'
     A            W1SECT         5A  O  5 14
     A                                  6  2'Slot .....:'
     A            W1DISP        12A  O  6 14
     A            W1SLP          8   O  6 28
     A                                  7  2'Item .....:'
     A            W1ITEM        65A  O  7 14
     A                                  9  2'Pick slot ............:'
     A            W1PICK         3A  O  9 26
     A                                 10  2'Reserved slot ........:'
     A            W1RSRV         3A  O 10 26
     A                                 12  2'Slot status ..........:'
     A            W1SLST         2A  O 12 26
     A            W1SLDS        30A  O 12 31
     A                                 13  2'Slot date/time .......:'
     A            W1SSDT         6Y 0O 13 26EDTWRD('  /  / 0')
     A            W1STIM         6Y 0O 13 36EDTWRD('  :  :  ')
     A                                 14  2'Date item entered slot:'
     A            W1SDTE         6Y 0O 14 26EDTWRD('  /  / 0')
     A                                 15  2'Expiration date ......:'
     A            W1EXDT         6Y 0O 15 26EDTWRD('  /  / 0')
     A                                 17  2'Slot definition ......:'
     A            W1SDEF         2A  O 17 26
     A            W1SDES        30A  O 17 30
     A                                 18  2'Slot tie / high ......:'
     A            W1TIE          4Y 0O 18 26EDTCDE(4)
     A                                 18 31'/'
     A            W1HIGH         2Y 0O 18 34EDTCDE(4)
     A                                 18 38'Preferred:'
     A            W1PFER         3A  O 18 50
     A                                 19  2'Designation ..........:'
     A            W1DESG         5A  O 19 26
     A            W1DESD        30A  O 19 32
     A                                 20  2'Activity zone ........:'
     A            W1ACTV         2A  O 20 26
     A            W1DESA        30A  O 20 30
     A                                 21  2'Build code ...........:'
     A            W1BLD          2A  O 21 26
     A            W1DESB        30A  O 21 30
     A                                 23  2'F5=Refresh  F9=View slot quantitie-
     A                                      s  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
