     A*%%TS  SD  20060830  115713  ROCKY       REL-V5R3M0  5722-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
416 AA* ***************************************************************
416 AA* *****  If the position of any slot entry field is changed *****
416 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
416 AA* *****  program.                                           *****
416 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UI330FM   UCI Inventory entry for item -  RF Mini Screen
     A*  30 August, 2006
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
510aAA*    08/30/06  RH   5.10a
     A*      - Created using IV330
     A*      - Uses spanish screens
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'UI330.01'
     A                                  1 13'UCI Inv Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Team'
     A            W1TEAM        10A  B  4  7
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04
     AON20                                  DSPATR(PC)
     A* 04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A                                  5  2'Date'
     A            W1IDTE         6Y 0B  5  7EDTCDE(4)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  7  2'F3/F12=Exit '
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'UI330.02 '
     A                                  1 13'UCI Inv Entry'
     A                                  3  2'Item.'
     A            W2ITEM        15A  B  3  8
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                  7  2'F3=Exit '
     A                                      COLOR(BLU)
     A          R SCREEN3
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'UI330.03 '
     A                                  1 13'UCI Inv Entry'
     A                                  2  2'TO:'
     A            W3TOSL        12A  O  2  6
     A                                  3  2'Desc '
     A            W3ITM1        15A  O  3  8
     A            W3ITM2        15A  O  4  8
     A            W3ITM3        15A  O  5  8
     A                                  6  2'Slot '
     A            W3DISP        12A  B  6  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  7  2'         '
     A                                      COLOR(BLU)
     A          R SCREEN4
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI330.04 '
     A                                  1 13'UCI Inv Entry'
     A                                  3  1'UCI:'
     A            W4UCI         10A  B  3  6
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A                                  4  6'-OR-'
     A                                  5  1'LCN:'
     A            W4LCN         15A  B  5  6
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A                                  7  2' F8=SltDone'
     A                                      COLOR(BLU)
     A          R SCREEN5
     A*%%TS  SD  20060830  115713  ROCKY       REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI330.05 '
     A                                  1 13'UCI Inv Entry'
     A                                  3  1'Item complete'
     A                                  4  1'All Slots entered'
     A                                  6  6'PRESS ENTER'
     A                                  7  2'         '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'UI330.01'
     A                                  1 13'Incorp.UCI Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Almacn'
     A            W1WHSE         3Y 0B  3  9
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Equipo'
     A            W1TEAM        10A  B  4  9
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04
     AON20                                  DSPATR(PC)
     A* 04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A                                  5  2'Fecha'
     A            W1IDTE         6Y 0B  5  9EDTCDE(4)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  7  2'F3/F12=Salir '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI330.02 '
     A                                  1 13'Incorp.UCI Inv'
     A                                  3  2'Prdc.'
     A            W2ITEM        15A  B  3  8
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                  7  2'F3/F12=Salir '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
416 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
416 AA            WOROW          3  0H
416 AA            WOCOL          3  0H
     A                                  1  2'UI330.03 '
     A                                  1 13'Incorp. UCI Inv'
     A                                  3  2'Desc.'
     A            W3ITM1        15A  O  3  8
     A            W3ITM2        15A  O  4  8
     A            W3ITM3        15A  O  5  8
     A                                  6  2'Slot '
     A            W3DISP        12A  B  6  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  7  2'  '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN4SP
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI330.04 '
     A                                  1 13'Incorp.UCI Inv'
     A                                  3  1'UCI:'
     A            W4UCI         10   B  3  6
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A                                  4  6'-OR-'
     A                                  5  1'LCN:'
     A            W4LNC         15  0B  5  6
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A                                  7  2'F8=SltHace'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
     A          R SCRN5SP
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI330.05 '
     A                                  1 13'Incorp.UCI Inv'
     A                                  3  1'Prdc acabado'
     A                                  4  6'Todos slots incorp'
     A                                  6  6'OPRIMA ENTER'
     A                                  7  2'         '
     A                                      COLOR(BLU)
