     A*%%TS  SD  19970725  184247  DAVE3       REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970725  165203  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'IT271.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Replenishment Slots'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCANREC
     A*%%TS  SD  19970725  184247  DAVE3       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  1 41'Scan slot  . . . .'
     A            W1DISP        12A  B  1 60
     A                                      CHECK(ER)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(04)
     A                                  1  2'Warehouse        :'
     A            WOWHSE        35A  O  1 21
     A                                  2  2'Department       :'
     A            WDCODE         5A  O  2 21
     A            WDDESC        30A  O  2 27
     A                                  3  2'Designation area :'
     A            DSCODE         5A  O  3 21
     A            DSDESC        30A  O  3 27
     A                                  4  2'Item number      :'
     A            W1ITEM        50A  O  4 21
216 AA                                  5  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940916  091756  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 14  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLSTAT    R        O 14  7REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O 14 13
     A            W1PRTY    R        O 14 21REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O 14 28EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O 14 38EDTWRD('  /  /  ')
     A            SLDISP        12A  O 14 49
     A            W1QTY         10A  O 14 63
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19941019  162435  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A**                                    CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 13  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 49'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 18'Priority'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 38'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 18'Picking'
     A                                      DSPATR(HI)
     A                                 12 38'Entered'
     A                                      DSPATR(HI)
     A                                 13 28'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 28'Expire'
     A                                      DSPATR(HI)
     A                                 13 12'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13  6'Stat'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 63'Available'
     A                                      DSPATR(HI)
     A                                 13 63'Quantity '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
409 AA*
409 AA*    VERIFY SLOT ZERO
409 AA*
409 AA          R ZROREC
409 AA                                      LOCK
409 AA                                      OVERLAY
409 AA                                      SLNO(04)
409 AA                                  1 41'Slot zero (Y,N). .'
409 AA            W3ZERO         1A  B  1 60
409 AA                                      CHECK(ER)
409 AA  23                                  DSPATR(RI)
409 AA N23N90                               DSPATR(UL)
409 AA  03                                  DSPATR(PC)
409 AA  90                                  DSPATR(PR)
411 AA*
411 AA*    QUANTITY TO TAKE
411 AA*
     A          R QTYREC
     A*%%TS  SD  19990618  143432  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  1 41'Replenish Qty. . .'
     A            W4RQTY         5Y 0B  1 60EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
409 AA*    CLEAR ALL LINES IN SUBFILE LIST
     A*
409 AA          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
