     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. EQ410FM COPY AND NAME EQ41001
     A*----------------------------------------------------------------
     A*
     A*   Revisions:
     A*
CBI AA*    03/30/04   RH   CBI
CBI  A*      - Changed Check In/Out to Return In/Take Out
417B A*    04/29/04   RH   417b
     A*      - Increase EQ ID from 6 to 8
500a A*    02/10/05   RH   500a
     A*      - Add F7 to screen1 to print test label for belt printer
500b A*    09/12/05   RH   500b
     A*      - ENH: Add screen3 to enter meter hours.
520a A*    01/27/09   RH   520a
     A*      - ENH: Add window to enter begin / end day.
640aAA*    10/25/12   JCJ  640a
     A*      - ENH: Removed F9 keyword.  Renamed to EQ41001.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*                                     CA03
     A                                      CA05
     A*                                     CA12
     A          R SCREEN1
     A*%%TS  SD  19940926  140505  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CF03
     A                                      CF04
500a A                                      CF07
640aD ***                                   CF09
     A                                      CF12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'EQ410.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 12'Equipment Check In/Out'
     A                                      DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
500a A                                  7 25'F7=Print'
500a A N89                                  DSPATR(ND)
500a A                                      COLOR(BLU)
     A                                  2  2'Warehouse:  '
     A            W1WHSE         3Y 0B  2 15
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  3  2'Employee #: '
     A            W1EMP#    R     Y00B  3 15REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                  4  2'Equip. ID:  '
417bDA*           W1EQIP         6A  B  4 15
417b A            W1EQIP         8A  B  4 15
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
CBI DA*                                 5  2'Check In/Out'
CBI DA*           W1IO           1A  B  5 15
CBI  A                                  5  2'Return In/Take Out'
CBI  A            W1IO           1A  B  5 21
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A          R SCREEN2
     A*%%TS  SD  19940926  140505  HEMANT      REL-V2R1M1  5738-PW1
640aD ***                                   CF09
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'EQ410.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 12'Equipment Check In/Out'
     A                                      DSPATR(HI)
     A*                                 7  2'F3/F12=Exit F10=Accept '
     A                                  7  2'F10=Accept             '
     A                                      COLOR(BLU)
     A                                  2  2'Eq ID'
417bDA*           W2EQIP         6A  O  2  8
417b A            W2EQIP         8A  O  2  8
     A                                  3  2'Answer(Y/N) & press Enter'
     A                                      COLOR(BLU)
     A            W2QSTN        30A  O  5  2
     A            W2ANSR         1A  B  5 34
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25                                  DSPATR(HI)
500b A          R SCREEN3
500b A*%%TS  SD  19940926  140505  HEMANT      REL-V2R1M1  5738-PW1
500b A                                      CF10
500b A  98                                  ALARM
500b A                                      CLRL(07)
500b A                                      OVERLAY
500b A                                  1  2'EQ410.03'
500b A                                  1 72DATE
500b A                                      EDTCDE(Y)
500b A                                  1 12'Equipment Check In/Out'
500b A                                      DSPATR(HI)
500b A*                                 7  2'F10=Accept             '
500b A*                                     COLOR(BLU)
500b A                                  2  2'Eq ID'
500b A            W3EQIP         8A  O  2  8
500b A                                  3  2'Type number & press Enter'
500b A                                      COLOR(BLU)
500b A            W3QSTN        30A  O  5  2
500b A            W3METR         7Y 0B  5 34EDTCDE(4)
500b A  26                                  DSPATR(RI)
500b A  06                                  DSPATR(PC)
500b A N26                                  DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
520aAA          R WBEDAY
520aAA***                                   WINDOW(5 15 7 20)
520aAA                                      WINDOW(2 5 4 20)
520aAA                                      WDWTITLE((*TEXT 'Start-End +
520aAA                                        Day') *LEFT)
520aAA            W4OUT          9A  O  1  2
520aAA            W4DAY          1A  B  1 12
520aAA  28                              3  2'Enter Y or N'
520aAA                                      DSPATR(RI)
520aAA*         R WDUMMY                     ASSUME
520aAA*                                 1  2' '
