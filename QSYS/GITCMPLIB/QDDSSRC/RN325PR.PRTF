     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*   RN325PR  Print Customer Returns Analysis
     A*   18 September 2002
     A*   Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Compile options:
     A*
     A*  PAGESIZE(66 198)
     A*  CPI(15)
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
416 AA*  09/18/02  MLB  4.16
     A*    - Created.
416aAA*  10/02/02  MLB  4.16a
     A*    - Revised prtf to add TOTAL1 format to print total
     A*      pieces returned for each return code.
500aAA*  10/13/05  RH   5.00a
     A*    - Revised prtf to add DETAIL2 to print customer name & #.
510 AA*  03/22/07  RBD  5.10
     A*    - Revised prtf to add TOTALLR format to print LR total
     A*      pieces returned.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(2)
     A*
     A            RPTDATE        6S 0      3
     A                                      EDTCDE(Y)
     A                                    54'Customer Returns Anaylsis'
     A                                   122'Page'
     A            PAGE           4S 0    127EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A            RPTTIM         6S 0      3EDTWRD('  :  :  ')
     A*
     A            WOWHSE        60A       37
     A                                   125'RN325'
     A                                      SPACEA(1)
     A            DTERNG        24A       56
     A                                      SPACEA(2)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     8'Emp'
     A                                    35'Return'
     A                                    44'Returned'
     A                                    55'Ordered'
     A                                   115'Shipped'
     A                                   175'Ship'
     A                                   184'Qty'
     A                                      SPACEA(1)
     A*
     A                                     1'Crew '
     A                                      UNDERLINE
     A                                     8'Nbr  '
     A                                      UNDERLINE
     A                                    14'Name                '
     A                                      UNDERLINE
     A                                    35'Date   '
     A                                      UNDERLINE
     A                                    44'By        '
     A                                      UNDERLINE
     A                                    55'Item           '
     A                                      UNDERLINE
     A                                    71'Description                   '
     A                                      UNDERLINE
     A                                   102'Slot        '
     A                                      UNDERLINE
     A                                   115'Item           '
     A                                      UNDERLINE
     A                                   131'Description                   '
     A                                      UNDERLINE
     A                                   162'Slot        '
     A                                      UNDERLINE
     A                                   175'Date  '
     A                                      UNDERLINE
     A                                   184'Returned  '
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A*
     A* Print return code info line
     A*
     A          R LEVEL1
     A                                      SPACEB(1)
     A                                     1'**'
     A            ORRCOD    R             +2REFFLD(ORRCOD ORRTN  )
     A            RNDESC    R             +2REFFLD(RNDESC RTNCODE)
     A                                      SPACEA(2)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A            PTCREW    R              1REFFLD(PTCREW PIRTRAN)
     A            PTEMP#    R             +1REFFLD(PTEMP# PIRTRAN)
     A                                      EDTCDE(4)
     A            EMPNAM        20A       +1
     A            RTNDTE         6S 0     +1
     A                                      EDTCDE(Y)
     A            ORRUSR    R             +1REFFLD(ORRUSR ORRTN  )
     A            ORITEM    R             +1REFFLD(ORITEM ORRTN  )
     A            DESC01    R             +1REFFLD(ITDESC PIRITEM)
     A            SLOT01    R             +1REFFLD(SLDISP SLOT   )
     A            ORCITM    R             +1REFFLD(ORCITM ORRTN  )
     A            DESC02    R             +1REFFLD(ITDESC PIRITEM)
     A            SLOT02    R             +1REFFLD(SLDISP SLOT   )
     A            SHPDTE         6S 0     +1
     A                                      EDTCDE(Y)
     A            RTNQTY        15A       +1
     A                                      SPACEA(1)
     A*
500a A          R DETAIL2
500a A            OHCUST    R             44REFFLD(OHCUST ORDH)
500a A                                      EDTCDE(4)
500a A            OHCNAM    R             +1REFFLD(OHCNAM ORDH)
500a A                                      SPACEA(1)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1                     TEXT('Skip One Line')
     A                                     1' '
     A                                      SPACEA(1)
416aAA*
416aAA* Total Line 1
416aAA*
416aAA          R TOTAL1                    TEXT('Total Line 1')
416aAA                                      SPACEB(1)
416aAA                                   159'* Total pieces returned:'
416aAA            TLRTNQ        15A      184
416aAA                                      SPACEA(1)
     A*
510 AA*
510 AA* Total Line LR
510 AA*
510 AA          R TOTALLR                   TEXT('Total Line LR')
510 AA                                      SPACEB(1)
510 AA                                   153'* Grand Total pieces returned:'
510 AA            LRRTNQ        15A      184
510 AA                                      SPACEA(1)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                     1'RN325: End of Report'
     A                                      SPACEA(1)
     A*
