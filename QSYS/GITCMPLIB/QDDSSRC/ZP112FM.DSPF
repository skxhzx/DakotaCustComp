     A*%%TS  SD  20070109  133940  JEFF        REL-V5R3M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------
     A*
     A*  ZP110FM    Device Group maintenance - Display List
     A*  03 January 2007
     A*  Jeffrey Jahnke
     A*
     A*  ZP112FM - 5.10
     A*
     A*  Revisions
     A*
510 AA*  xx/xx/xx  xxx  x.xx
     A*    - xxx: xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A*%%TS  SD  20070105  161820  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'ZP112.01 '
     A                                  1 28'Device Group Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Detail Entry'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20070105  111434  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            PTDMY1        78A  O  1  2
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070108  193402  JEFF        REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1DSEQ         3Y 0O 15  3EDTCDE(Z)
     A            W1DDEV    R        B 15  7REFFLD(DVREC/DVHGRP *LIBL/DEVGRPH)
     A  25                                  DSPATR(PC)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070109  133940  JEFF        REL-V5R3M0  5722-WDS
     A  *DS3                                SFLLIN(0003)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  8  5'Group . . . . . . . .'
     A            W1HGRP        10A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  8 41'Must enclose with <> (i.e. <GROUP1-
     A                                      >'
     A                                      DSPATR(HI)
     A                                  9  5'Description . . . . .'
     A            W1HDSC        30A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  5'Printer devices . . .'
     A                                      DSPATR(HI)
     A                                 13  5'   Device   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 22'   Device   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 39'   Device   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 56'   Device   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WDDEV         10A  B 14  7
     A  23                                  DSPATR(PC)
     A N90                                  DSPATR(UL)
     A  03                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20070105  161927  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
