     A*%%TS  SD  20190826  095557  LYNNM       REL-V7R2M0  5770-WDS
     A********************************************************************
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  OR402FM      RF UCI Order Selection Changes
     A*  26 August 2019
     A*  Lynn McMahon
     A*  Created using OR40023
     A********************************************************************
     A*  Compile Options:
     A*
     A*    RSTDSP(*YES)
     A*
     A********************************************************************
     A*
     A*  Revisions
     A* xx/xx/xx  xx   x.xx
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20190826  095557  LYNNM       REL-V7R2M0  5770-WDS
720cMA                                      CF03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.01'
     A                                  1 11'UCI Change Order Pick'
     A            W1WHSE         3Y 0B  2  2EDTCDE(4)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  3  2'Label:'
     A            W1LBL#B       10A  B  3 10CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  4  2'UCI:'
     A            W1UCI        100A  B  4 10CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(TRQ)
     A          R SCREEN2A
     A*%%TS  SD  20000628  161057  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA04
     A                                      CA06
     A*                                     CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.02A'
     A                                  1 11'UCI Change Order Pick'
     A            SDISP         12A  O  2  2
     A            WOITEM        65A  O  2 15
     A  45                              3  2'Stop:'
     A  45        W6STOP         5Y 0O  3  8EDTCDE(3)
     A            W6CNT1         5Y 0O  4  2EDTCDE(3)
     A                                  4  8'of'
     A            W6CNT2         5Y 0O  4 11EDTCDE(3)
     A            W6UOM          5   O  4 17
     A                                  5  2'UCI picked:'
     A            W6UCI        100   B  5 14CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  1'F4=Dlt F6=Done'
     A                                      COLOR(TRQ)
     A          R SCREEN2B
     A*%%TS  SD  20190809  065357  LYNNM       REL-V7R2M0  5770-WDS
     A                                      CA04
     A                                      CA06
     A*                                     CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.02B'
     A                                  1 11'UCI Change Order Pick'
     A            SDISP         12A  O  2  2
     A            WOITEM        65A  O  2 15
     A  45                              3  2'Stop:'
     A  45        W6STOP         5Y 0O  3  8EDTCDE(3)
     A            W6CNT1         5Y 0H
     A            W6CNT2         5Y 0H
     A            W6WGT1         6Y 2O  4  2EDTCDE(3)
     A                                  4 10'of'
     A            W6WGT2         6Y 2O  4 13EDTCDE(3)
     A                                  4 21'Lbs'
     A                                  5  2'WGT picked:'
     A            W6UCI        100A  B  5 14CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  1'F4=Dlt F6=Done'
     A                                      COLOR(TRQ)
     A          R SCREEN2C
     A*%%TS  SD  20190809  065357  LYNNM       REL-V7R2M0  5770-WDS
     A                                      CA06
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.02C'
     A                                  1 12'DELETING Order Pick'
     A                                      COLOR(RED)
     A            SDISP         12A  O  2  2
     A            WOITEM        65A  O  2 15
     A  45                              3  2'Stop:'
     A  45        W6STOP         5Y 0O  3  8EDTCDE(3)
     A            W6CNT1         5Y 0H
     A            W6CNT2         5Y 0H
     A            W6WGT1         6Y 2O  4  2EDTCDE(3)
     A                                  4 10'of'
     A            W6WGT2         6Y 2O  4 13EDTCDE(3)
     A                                  4 21'Lbs'
     A                                  5  2'WGT DELETE:'
     A                                      COLOR(RED)
     A            W6UCI        100A  B  5 14CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  1'F6=Done  F12=Cancel'
     A                                      COLOR(TRQ)
     A          R SCREEN2D
     A*%%TS  SD  20190826  095557  LYNNM       REL-V7R2M0  5770-WDS
     A                                      CA06
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.02D'
     A                                  1 12'DELETING Order Pick'
     A                                      COLOR(RED)
     A            SDISP         12A  O  2  2
     A            WOITEM        65A  O  2 15
     A  45                              3  2'Stop:'
     A  45        W6STOP         5Y 0O  3  8EDTCDE(3)
     A            W6CNT1         5Y 0O  4  2EDTCDE(3)
     A                                  4  8'of'
     A            W6CNT2         5Y 0O  4 11EDTCDE(3)
     A            W6UOM          5A  O  4 17
     A                                  5  2'UCI Delete:'
     A            W6UCI        100A  B  5 14CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  1'F6=Done  F12=Cancel'
     A                                      COLOR(TRQ)
     A          R SCREEN3
     A*%%TS  SD  20060519  102907  DAVE        REL-V5R3M0  5722-WDS
     A                                      CF03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR402.3'
     A                                  1 11'Order Pick'
     A                                  3  2'User'
     A                                  4  2'Does Not Exist in Dakota'
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(TRQ)
     A            #USER         10   O  3  7
     A                                  5  2'Press Enter to Exit'
     A          R SCREEN4
     A*%%TS  SD  20160913  080257  MIKER       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR402.4'
     A                                  1 11'UCI Change Order Pick'
     A                                  3  2'Confirm Done'
     A                                  4  2'Are you sure Y/N?'
     A            CDONE          1A  B  4 22COLOR(WHT)
     A                                      CHECK(ER)
     A                                  5  2'Press Enter'
     A                                      COLOR(TRQ)
     A*
     A*
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
