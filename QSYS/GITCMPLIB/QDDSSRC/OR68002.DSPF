500bAA*----------------------------------------------------------------
500bAA*   Multilingual Version
500bAA*   Every screen has a corresponding Spanish screen.
500bAA*   All Spanish screens are at the end.
500bAA*   Any changes must be made to both screens.
500bAA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************

     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL, 60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  OR680FM      RF Select Bulk Picks
     A*  03 October 2002
     A*  Max Blacknik
417 AA*
417 AA********************************************************************
417 AA*  Compile Options:
417 AA*
417  A*    RSTDSP(*YES)
     A*
     A********************************************************************
     A*  Revisions
     A*
416 AA*    10/03/02  MLB  4.16
     A*      - Created.
     A*
417 AA*    05/01/03  MLB  4.17
     A*      - Enh: Added new screen SCREEN 5 to allow bulk pick qty to
     A*        be selected from more than one location. Revised SCREEN 3
     A*        to allow quantity to be revised.
500aAA*    03/29/06  RH   5.00a
     A*      - Enh: Change F10 to F9 on SCREEN 4.
     A*      - Add F10=Short to SCREEN 5.
     A*
500bAA*   06/02/06  JCJ  5.00b
     A*     - Merged Spanish screens from PIRS.
     A*
640aAA*  05/23/12  JCJ  6.40a
     A*    - Enh: Renamed OR680FM to OR68001.
     A*      Increase label to 10 for TRAX processing.
710aAA*  06/14/17  RH   7.10a
     A*    - Enh: Renamed OR68001 to OR68002, add license
     A*
     A********************************************************************
     A*  Client Custom Revisions
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR680.01'
     A                                  1 11'RF Bulk Pick'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse:'
     A            W1WHSE         3Y 0B  3 13EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept.    :'
     A            W1WHDP         5A  B  4 13
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Label#   :'
640aD ***         W1LBL#         7Y 0B  5 13CHECK(ER)
640aMA            W1LBL#B       10   B  5 13CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aD ***                                   EDTCDE(4)
     A                                  6  2'Or'
     A                                      DSPATR(HI)
     A                                  6  5'Tran# :'
     A            W1TRN#         7Y 0B  6 13CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  7 11'F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A                                      CA03
     A                                      CA08
     A                                      CA12
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR680.03'
     A                                  1 11'RF Bulk Pick'
     A                                      DSPATR(HI)
     A            W3ITEM        65A  O  2  1
     A                                  3  1'From:'
     A            W3FMSL        12A  O  3  7
710aAA                                  3 20'Lcn'
710aAA            W3LCNS        15A  O  3 24
     A                                  4  3'Qty:'
417 DA*           W3QTY          7Y 0O  4  8EDTCDE(4)
417 MA            W3NQTY         7Y 0B  4  8EDTCDE(4)
417 AA  26                                  DSPATR(RI)
417 AA N26N90                               DSPATR(UL)
417 AA  06                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
     A            W3UMSR         5   O  4 16
     A                                  6  1'Scan:'
     A            W3SCAN        12A  B  6  7CHECK(ER)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
417 DA* 05                                  DSPATR(PC)
417 MA                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  1'F8=List'
     A                                      COLOR(BLU)
     A                                  7  9'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR680.04'
     A                                  1 12'RF Bulk Pick'
     A                                      DSPATR(HI)
     A                                  3  1'Take product to dock'
     A                                  4  1'Press F9=Done when'
     A                                  5  1'product at loading'
     A                                  6  1'area.'
     A                                  7  1'F9=Done'
     A                                      COLOR(BLU)
     A                                  7 10'F12=Cancel'
     A                                      COLOR(BLU)
     A*
417 AA*                                     CA03
417 AA          R SCREEN5
417 AA                                      CA08
500A A                                      CA10
417 AA  98                                  ALARM
417 AA                                      CSRLOC(WOROW      WOCOL)
417 AA                                      OVERLAY
417 AA            WOROW          3S 0H
417 AA            WOCOL          3S 0H
417 AA                                  1  2'OR680.05'
417 AA                                  1 11'RF Bulk Pick'
417 AA                                      DSPATR(HI)
417 AA            W5ITEM        65A  O  2  1
417 AA                                  3  2'Slot:'
417 AA            W5DISP        12A  O  3  8
417 AA                                  4  3'Qty:'
417 AA            W5OQTY         7Y 0O  4  8EDTCDE(4)
417 AA            W5UMSR         5A  O  4 16
417 AA                                  5  3'Bal:'
417 AA            W5BALQ         7Y 0O  5  8EDTCDE(4)
417 AA                                  7  1'F8=List'
417 AA                                      COLOR(BLU)
500aAA                                  7  9'F10=Short'
500aAA                                      COLOR(BLU)
417 AA*                                 7  9'F12=Cancel'
417 AA*                                     COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPT2           1A  B  3  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2RTE     R        O  3  4REFFLD(RHREC/RHRTE RTEHED)
     A            LBDISP    R        O  3 10REFFLD(LBREC/LBDISP LABEL)
     A            LBQPCK    R        O  3 23REFFLD(LBREC/LBQPCK LABEL)
     A            W2UNMR    R        O  3 29REFFLD(UMREC/UMSHRT UNMESR)
     A            W2ITEM        45A  O  3 35
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0006)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  1  2'OR680.02'
     A                                  1 11'Open Bulk Picks'
     A                                      DSPATR(HI)
     A                                  1 27'1=Select'
     A                                      COLOR(BLU)
     A                                  2  4'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 10'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 23'Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 29'U/M  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 35'Item                              -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPT2           1A  B  3  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2RTE     R        O  3  4REFFLD(RHREC/RHRTE RTEHED)
     A            LBDISP    R        O  3 10REFFLD(LBREC/LBDISP LABEL)
     A            LBQPCK    R        O  3 23REFFLD(LBREC/LBQPCK LABEL)
     A            W2UNMR    R        O  3 29REFFLD(UMREC/UMSHRT UNMESR)
     A            W2ITEM        45A  O  3 35
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  1  2'OR680.02'
     A                                  1 11'Open Bulk Picks'
     A                                      DSPATR(HI)
     A                                  1 27'1=Select'
     A                                      COLOR(BLU)
     A                                  2  4'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 10'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 23'Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 29'U/M  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 35'Item                              -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
500bAA*
500bAA*****************************************************************
500bAA* Spanish Screens
500bAA*****************************************************************
500bAA*----------------------------------------------------------------
     A          R SCRN1SP
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR680.01'
     A                                  1 11'Pick de Bulto de RF'
     A                                      DSPATR(HI)
     A                                  3  2'Almacen  :'
     A            W1WHSE         3Y 0B  3 13EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept.    :'
     A            W1WHDP         5A  B  4 13
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'# rotulo :'
640aD ***         W1LBL#         7Y 0B  5 13CHECK(ER)
640aMA            W1LBL#B       10   B 12 31CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aD ***                                   EDTCDE(4)
     A                                  6  2'O '
     A                                      DSPATR(HI)
     A                                  6  5'# Tran:'
     A            W1TRN#         7Y 0B  6 13CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  2'F3=Salir'
     A                                      COLOR(BLU)
     A                                  7 12'F4=Inicitar'
     A                                      COLOR(BLU)
500bAA*----------------------------------------------------------------
500bAA          R SCRN3SP
     A                                      CA03
     A                                      CA08
     A                                      CA12
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR680.03'
     A                                  1 11'Pick de Bulto de RF'
     A                                      DSPATR(HI)
     A            W3ITEM        65A  O  2  1
     A                                  3  2'De  :'
     A            W3FMSL        12A  O  3  8
     A                                  4  3'Ctd:'
417 DA*           W3QTY          7Y 0O  4  8EDTCDE(4)
417 MA            W3NQTY         7Y 0B  4  8EDTCDE(4)
417 AA  26                                  DSPATR(RI)
417 AA N26N90                               DSPATR(UL)
417 AA  06                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
     A            W3UMSR         5   O  4 16
     A                                  6  1'Escn:'
     A            W3SCAN        12A  B  6  7CHECK(ER)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
417 DA* 05                                  DSPATR(PC)
417 MA                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  1'F8=Lista'
     A                                      COLOR(BLU)
     A                                  7 10'F12=Cancelar'
     A                                      COLOR(BLU)
500bAA*----------------------------------------------------------------
500bAA          R SCRN4SP
     A                                      CA03
     A                                      CA10
     A                                      CA12
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'OR680.04'
     A                                  1 12'Pick de Bulto de RF'
     A                                      DSPATR(HI)
     A                                  3  1'Tome producto al muelle'
     A                                  4  1'Oprima F10=Terminar cuando'
     A                                  5  1'producto esta a la area'
     A                                  6  1'de cargar.'
     A                                  7  1'F10=Terminar'
     A                                      COLOR(BLU)
     A                                  7 15'F12=Cancelar'
     A                                      COLOR(BLU)
     A*
417 AA*                                     CA03
500bAA*----------------------------------------------------------------
500bAA          R SCRN5SP
417 AA                                      CA08
500A A                                      CA10
417 AA  98                                  ALARM
417 AA                                      CSRLOC(WOROW      WOCOL)
417 AA                                      OVERLAY
417 AA            WOROW          3S 0H
417 AA            WOCOL          3S 0H
417 AA                                  1  2'OR680.05'
417 AA                                  1 11'Pick de Bulto de RF'
417 AA                                      DSPATR(HI)
417 AA            W5ITEM        65A  O  2  1
417 AA                                  3  2'Slot:'
417 AA            W5DISP        12A  O  3  8
417 AA                                  4  3'Ctd:'
417 AA            W5OQTY         7Y 0O  4  8EDTCDE(4)
417 AA            W5UMSR         5A  O  4 16
417 AA                                  5  3'Bal:'
417 AA            W5BALQ         7Y 0O  5  8EDTCDE(4)
417 AA                                  7  1'F8=Lista'
417 AA                                      COLOR(BLU)
500aAA                                  7  9'F10=Corto'
500aAA                                      COLOR(BLU)
417 AA*                                 7  9'F12=Cancelar'
417 AA*                                     COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
500bAA*----------------------------------------------------------------
500bAA          R DSPSFLSP                  SFL
     A  21                                  SFLNXTCHG
     A            OPT2           1A  B  3  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2RTE     R        O  3  4REFFLD(RHREC/RHRTE RTEHED)
     A            LBDISP    R        O  3 10REFFLD(LBREC/LBDISP LABEL)
     A            LBQPCK    R        O  3 23REFFLD(LBREC/LBQPCK LABEL)
     A            W2UNMR    R        O  3 29REFFLD(UMREC/UMSHRT UNMESR)
     A            W2ITEM        45A  O  3 35
     A*
500bAA*----------------------------------------------------------------
500bAA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0006)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  1  2'OR680.02'
     A                                  1 11'Abre Picks de Bulto'
     A                                      DSPATR(HI)
     A                                  1 32'1=Seleccionar'
     A                                      COLOR(BLU)
     A                                  2  4'Ruta '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 10'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 23'Ctd  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 29'U/M  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 35'Producto                          -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
500bAA*----------------------------------------------------------------
500bAA          R D16SFLSP                  SFL
     A  21                                  SFLNXTCHG
     A            OPT2           1A  B  3  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2RTE     R        O  3  4REFFLD(RHREC/RHRTE RTEHED)
     A            LBDISP    R        O  3 10REFFLD(LBREC/LBDISP LABEL)
     A            LBQPCK    R        O  3 23REFFLD(LBREC/LBQPCK LABEL)
     A            W2UNMR    R        O  3 29REFFLD(UMREC/UMSHRT UNMESR)
     A            W2ITEM        45A  O  3 35
     A*
500bAA*----------------------------------------------------------------
500bAA          R D16SFCSP                  SFLCTL(D16SFLSP)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  1  2'OR680.02'
     A                                  1 11'Abre Picks de Bulto'
     A                                      DSPATR(HI)
     A                                  1 32'1=Seleccionar'
     A                                      COLOR(BLU)
     A                                  2  4'Ruta '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 10'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 23'Ctd  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 29'U/M  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 35'Producto                          -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
