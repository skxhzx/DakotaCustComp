     A*%%TS  SD  20010209  163724  BFC         REL-V4R4M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT141FM   Item maintenance - Change screen
     A*  26 September 1990
     A*  Hemant Kappadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*    02/09/01  MLB  4.15a
     A*      - Added item department/section to SCREEN1.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010209  163724  BFC         REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF11
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT141.01'
     A                                  1 28'Item Tie/High Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
     A            ITWHSE         3S 0O  4 19
     A            WHDESC        30A  O  4 24
     A                                  5  2'Item           :'
     A            W1HEAD        50A  O  5 19
415aAA                                  6  2'Dept/Section   :'
415aAA            ITWHDP         5   O  6 19
415aAA                                  6 25'/'
415aAA            WOSTYP         5   O  6 28
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10 36'Preferred'
     A                                 11  4'Slot type'
     A                                      DSPATR(UL)
     A                                 11 18'Tie   /   High'
     A                                      DSPATR(UL)
     A                                 11 36' (Y,'' '') '
     A                                      DSPATR(UL)
     A            W1ST1          2A  B 12  7
     A  21                                  DSPATR(RI)
     A  01
     AO 21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1TIE1         4Y 0B 12 18EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  06
     AO 26                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 24'/'
     A            W1HI1          2Y 0B 12 29COMP(GE 0)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11
     AO 31                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1PRF1         1A  B 12 40
     A  61                                  DSPATR(RI)
     A  66
     AO 61                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ST2          2A  B 13  7
     A  22                                  DSPATR(RI)
     A  02
     AO 22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1TIE2         4Y 0B 13 18EDTCDE(4)
     A  27                                  DSPATR(RI)
     A  07
     AO 27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 24'/'
     A            W1HI2          2Y 0B 13 29COMP(GE 0)
     A                                      EDTCDE(4)
     A  32                                  DSPATR(RI)
     A  12
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1PRF2         1A  B 13 40
     A  62                                  DSPATR(RI)
     A  67
     AO 62                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ST3          2A  B 14  7
     A  23                                  DSPATR(RI)
     A  03
     AO 23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1TIE3         4Y 0B 14 18EDTCDE(4)
     A  28                                  DSPATR(RI)
     A  08
     AO 28                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 24'/'
     A            W1HI3          2Y 0B 14 29COMP(GE 0)
     A                                      EDTCDE(4)
     A  33                                  DSPATR(RI)
     A  13
     AO 33                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1PRF3         1A  B 14 40
     A  63                                  DSPATR(RI)
     A  68
     AO 63                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ST4          2A  B 15  7
     A  24                                  DSPATR(RI)
     A  04
     AO 24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1TIE4         4Y 0B 15 18EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09
     AO 29                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 24'/'
     A            W1HI4          2Y 0B 15 29COMP(GE 0)
     A                                      EDTCDE(4)
     A  34                                  DSPATR(RI)
     A  14
     AO 34                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1PRF4         1A  B 15 40
     A  64                                  DSPATR(RI)
     A  69
     AO 64                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ST5          2A  B 16  7
     A  25                                  DSPATR(RI)
     A  05
     AO 25                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1TIE5         4Y 0B 16 18EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10
     AO 30                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 24'/'
     A            W1HI5          2Y 0B 16 29EDTCDE(4)
     A  35                                  DSPATR(RI)
     A  15
     AO 35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1PRF5         1A  B 16 40
     A  65                                  DSPATR(RI)
     A  70
     AO 65                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F4=Prompt  F5=Refresh  F9=Pattern -
     A                                       F11=Select tie/high  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
