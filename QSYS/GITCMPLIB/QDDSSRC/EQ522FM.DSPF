     A*%%TS  SD  20020424  111747  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EQ522FM   Equipment Work Order Detail - Change screen
     A*  02 July, 2003
     A*  Roxanne Harrison
     A*
     A*  Revisions
     A*
417 AA*    07/02/03  RH   4.17
     A*      - Created.
417aAA*    02/18/04  RH   4.17a
     A*      - Change to use WOWRK#, was causing problem in EQ522.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020424  111747  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      RTNCSRLOC(&RTNRCD &RTNFLD)
     A  98                                  ALARM
     A            RTNRCD        10A  H      TEXT('Return rcdfmt cursor')
     A            RTNFLD        10A  H      TEXT('Return rcdfld cursor')
     A                                  1  2'EQ522.01'
     A                                  1 30'Equipment Work Order Maint.'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Work Order:'
417a A*           $PWRK#         7Y 0O  5 14EDTCDE(3)
417aMA            WOWRK#         7Y 0O  5 14EDTCDE(3)
     A            W1QSTN        30   O  7  4
     A            W1ANSR         1   O  7 37
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11  4'Employee # . . . . . . .'
     A            W1EMP#    R     Y00B 11 29REFFLD(USEMP# *LIBL/PIRUSER)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 12  4'Memo . . . . . . . . . .'
     A            W1MEMO        30   B 12 29
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                 13  4'Repair complete Y/N. . .'
     A            W1CMPL         1   B 13 29
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
