     A*%%TS  SD  19961008  141346  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO812FM   PO history header - Change screen
     A*  14 July, 1997
     A*  Roxanne Harrison
     A*
     A*  Revisions
     A*
510a A*   10/30/06  RAH  5.10
     A*      - Enh: Add Shipper name to screen SCREEN1.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PO812.01'
     A                                  1 28'PO History Header File'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'  Detail   '
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'PO number. . . . . .'
     A            W1PO           9   B  8 25
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Vendor number  . . .'
     A            W1VEN         11Y 0B  9 25EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Vendor name  . . . .'
     A            W1VNAM        40A  B 10 25CHECK(LC)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(UL)
     A                                 11  4'Address line 1 . . .'
     A            W1VAD1        40A  B 11 25CHECK(LC)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(UL)
     A                                 12  4'Address line 2 . . .'
     A            W1VAD2        40A  B 12 25CHECK(LC)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N25N90                               DSPATR(UL)
     A                                 13  4'City . . . . . . . .'
     A            W1VCTY        20A  B 13 25CHECK(LC)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N26N90                               DSPATR(UL)
     A                                 13 47'State . .'
     A            W1VST          2A  B 13 57
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N27N90                               DSPATR(UL)
     A                                 13 61'Zip . .'
     A            W1VZP1        10A  B 13 69
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(UL)
510a A                                 15  4'Shipper Name . . . .'
510a A            W1SHPR        40A  B 15 25CHECK(LC)
510a A  29                                  DSPATR(RI)
510a A  09                                  DSPATR(PC)
510a A  90                                  DSPATR(PR)
510a A N29N90                               DSPATR(UL)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
