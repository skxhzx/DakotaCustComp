     A*%%TS  SD  19940822  174237  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI112FM   Import order header maint - Change screen
     A*  29 July 1994
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*    08/17/94  DAS
     A*      - Increased customer number to 11 digits from 10.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940822  174237  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OI112.01'
     A                                  1 23'Import Order Header File Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Order number . . . .'
     A            W1ORD          7Y 0B  8 25EDTCDE(3)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Order type . . . . .'
     A            W1TYPE         1A  B  9 25
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(UL)
     A                                  9 51''' '' = Normal  ''T'' = Transfer'
     A                                 10  4'Customer PO# . . . .'
     A            W1CPO         15A  B 10 25
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(UL)
     A                                 11  4'Customer number  . .'
     A            W1CUS         11Y 0B 11 25EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(UL)
     A                                 12  4'Customer name  . . .'
     A            W1CNM         40A  B 12 25CHECK(LC)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N25N90                               DSPATR(UL)
     A                                 13  4'Address line 1 . . .'
     A            W1CA1         40A  B 13 25CHECK(LC)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N26N90                               DSPATR(UL)
     A                                 14  4'Address line 2 . . .'
     A            W1CA2         40A  B 14 25CHECK(LC)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N27N90                               DSPATR(UL)
     A                                 15  4'City . . . . . . . .'
     A            W1CCT         20A  B 15 25CHECK(LC)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(UL)
     A                                 16  4'State  . . . . . . .'
     A            W1CST          2A  B 16 25
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(UL)
     A                                 17  4'Zip  . . . . . . . .'
     A            W1CZIP        10A  B 17 25
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N30N90                               DSPATR(UL)
     A                                 18  4'Route  . . . . . . .'
     A            W1RTE          5A  B 18 25
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N31N90                               DSPATR(UL)
     A                                 19  4'Stop . . . . . . . .'
     A            W1STP          2Y 0B 19 25EDTCDE(4)
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N32N90                               DSPATR(UL)
     A                                 20  4'Truck  . . . . . . .'
     A            W1TRK         10A  B 20 25
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N33N90                               DSPATR(UL)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 21  4'Miscellaneous  . . .'
     A            W1MISC        20A  B 21 25
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
