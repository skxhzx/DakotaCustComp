     A*%%TS  SD  20020524  101259  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RN246FM    License entry Cust. returns VIRTUAL SLOT
     A*  23 September, 1999
     A*  Roxanne Harrison
     A*    Created using RN146
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
416 AA*    05/23/02  RRH  4.16
     A*      - Increase W1LCNS to 15.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  19990513  114117  QSECOFR     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1UMT          1   H
416 MA            W1QTY          5Y 0B 12  4EDTCDE(4)
416 DA*           W1QTY          5Y 0B 12  4EDTCDE(3)
     A  32                                  DSPATR(RI)
     A N32N69                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  69                                  DSPATR(PR)
     A            W1UOM          5A  O 12 11
416 DA*           W1LCNS         7Y 0B 12 27EDTCDE(4)
416 MA            W1LCNS        15A  B 12 20
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*           W1TRN#         7Y 0O 12  2DSPATR(ND)
     A*
     A*                                     SFLSIZ(0011)
     A*                                     SFLPAG(0010)
416 DA* *DS3                                SFLLIN(0013)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20020524  101259  ROCKY       REL-V4R2M0  5769-PW1
     A  *DS3                                SFLLIN(0010)
     A                                      SFLSIZ(0072)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'RN246.01'
     A                                  1 35'Customer Returns'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'License Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Order number.'
     A            W1ORD          7Y 0O  4 16EDTCDE(4)
     A                                  5  2'Warehouse . .'
     A            $PWHSE         3Y 0O  5 16EDTCDE(4)
     A                                  5 20'-'
     A            WHDESC        30A  O  5 22
     A                                  6  2'Item number .'
     A            WOITEM        65A  O  6 16
     A            WODSC1        40A  O  7 16
     A            WODSC2        40A  O  8 16
     A                                  9  2'Quantity received . . '
     A            W1QRC1         5Y 0O  9 25EDTCDE(4)
     A            W1UM1          5A  O  9 32
     A            W1QRC2         5Y 0O  9 39EDTCDE(4)
     A            W1UM2          5A  O  9 46
     A            W1QRC3         5Y 0O  9 53EDTCDE(4)
     A            W1UM3          5A  O  9 60
     A                                 11  3'Quantity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 20'License'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 45'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 61'License'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 35'Manual Put Away'
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh    F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
