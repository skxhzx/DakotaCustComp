     A****************************************************************
     A*
     A* ER231PR   Thru-put and number of cases selected
     A* 15 January, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER231'
     A*
     A            SUBTTL        40A  O    47
     A                                      SPACEB(001)
     A*
     A* Print week begining date
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A                                     1'Week'
     A            WKWNUM    R             +1REFFLD(WKWNUM ER231)
     A                                      EDTCDE(Z)
     A                                    +1':'
     A                                    +2'('
     A            BGDATE         6S 0     +1EDTCDE(Y)
     A                                    +1' TO '
     A            ENDATE         6S 0     +1EDTCDE(Y)
     A                                    +1')'
     A  92                                10
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     1'Emp #'
     A                                      UNDERLINE
     A                                     7'Name'
     A                                      UNDERLINE
     A                                    41'Sunday'
     A                                      UNDERLINE
     A                                    52'Monday'
     A                                      UNDERLINE
     A                                    62'Tuesday'
     A                                      UNDERLINE
     A                                    71'Wednesday'
     A                                      UNDERLINE
     A                                    83'Thursday'
     A                                      UNDERLINE
     A                                    96'Friday'
     A                                      UNDERLINE
     A                                   105'Saturday'
     A                                      UNDERLINE
     A                                   114'Week Total'
     A                                      UNDERLINE
     A                                   127'Pcs/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKEMP#    R              1REFFLD(PTEMP# PIRTRAN)
     A                                      EDTCDE(Z)
     A            EMNAME        30A        7
     A            T#SUNT         7S 0     38EDTCDE(1)
     A            T#MONT         7S 0     49EDTCDE(1)
     A            T#TUET         7S 0     60EDTCDE(1)
     A            T#WEDT         7S 0     71EDTCDE(1)
     A            T#THUT         7S 0     82EDTCDE(1)
     A            T#FRIT         7S 0     93EDTCDE(1)
     A            T#SATT         7S 0    104EDTCDE(1)
     A            T#WIKT         9S 0    113EDTCDE(1)
     A            T#PCPH         7S 2    124EDTCDE(1)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                    13'Total pieces:'
     A            T#SUNT         7S 0     38EDTCDE(1)
     A            T#MONT         7S 0     49EDTCDE(1)
     A            T#TUET         7S 0     60EDTCDE(1)
     A            T#WEDT         7S 0     71EDTCDE(1)
     A            T#THUT         7S 0     82EDTCDE(1)
     A            T#FRIT         7S 0     93EDTCDE(1)
     A            T#SATT         7S 0    104EDTCDE(1)
     A            T#WIKT         9S 0    113EDTCDE(1)
     A            T#PCPH         7S 2    124EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD          25A       10
     A            T#SUPH         7S 2     38EDTCDE(1)
     A            T#MOPH         7S 2     49EDTCDE(1)
     A            T#TUPH         7S 2     60EDTCDE(1)
     A            T#WEPH         7S 2     71EDTCDE(1)
     A            T#THPH         7S 2     82EDTCDE(1)
     A            T#FRPH         7S 2     93EDTCDE(1)
     A            T#SAPH         7S 2    104EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER231: End of Report '
     A*
