     A*%%TS  SD  20000826  114144  HEMANT      REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  RH390FM  Pallet lookup by route
     A*  31 July 2000
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    01/28/09 CWM  5.20
     A*      - Enh: Add support for 3 digit stops.
530aAA*    09/21/10 RH   5.30a
     A*      - Enh: To include quantity picked from LABELDO
640a A*    03/08/11 GJA  6.40a
     A*      - Enh: Add '*' to denote when voice picked
640b A*    09/01/11 GJA  6.40b
     A*      - Enh: Add LABELDO date/time to screen3
710a A*    02/13/17 GJA  7.10a
     A*      - Enh: Add customer info to screen2, moved Dakota order
     A*             to screen 3
710b A*    12/08/17 LMC  7.10b
     A*      - Enh: Add lblbl# and lbord as hidden fields to the
     A*             subfile views, that don't have those fields already
     A*----------------------------------------------------------------
     A*  Client Custom Revisions - Southwest Traders
     A*
SWTa A*    06/06/19 NLK  SWTa
     A*      - ADD:  CORD is 8 long from ORDHEXA
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH392 '
     A                                  1 31'Pallet Lookup by Route'
     A                                      DSPATR(HI)
     A                                  2 35'Item Detail'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20000826  114144  HEMANT      REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 47'Position to label . . . .'
     A            PTINP2         7Y 0B  3 74EDTCDE(4)
     A            PTDMY1        78A  O  1  2
     A                                  2 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
710BaA            LBORD          7Y 0H
     A            LBLBL#         7Y 0O 11  7EDTCDE(3)
     A            LBITEM        15   O 11 15
530aDA*           W1DESC        25   O 11 31
530aMA            W1DESC        23   O 11 31
530aDA*           LBDISP        12   O 11 57
530aMA            LBDISP        12   O 11 55
640aDA*****       LBOQTY         5Y 0O 11 69EDTCDE(3)
640aAA            VYN            1   O 11 68
640aMA            LBOQTY         5Y 0O 11 70EDTCDE(3)
530aDA*           LBQPCK         5Y 0O 11 71EDTCDE(3)
530aMA            LBQPCK         5Y 0O 11 76EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
530aDA*                                10 31'Description              '
530aMA                                 10 31'Description            '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
530aDA*                                10 57'Slot        '
530aMA                                 10 55'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 15'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
530aAA                                  9 68'Voice'
530aAA                                      DSPATR(HI)
530aAA                                 10 68'Select '
530aAA                                      DSPATR(HI)
530aAA                                      DSPATR(UL)
530aDA*                                 9 70'Qty'
640aDA*****                             9 75'Qty'
640aMA                                  9 76'Qty'
     A                                      DSPATR(HI)
530aDA*                                10 70'Picked'
640aDA*****                            10 75'Picked'
640aMA                                 10 76'Pick '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20000803  123603  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
710bAA            LBORD          7Y 0H
710bAA            LBLBL#         7Y 0H
     A            LBITEM        15A  O 11  6
     A            LBCUBE         9Y 3O 11 22EDTCDE(3)
     A            LBSWGT         9Y 2O 11 33EDTCDE(3)
520 DA*           LBSTOP         2Y 0O 11 47EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O 11 44EDTCDE(3)
SWTaMA            W1CORD         8A  O 11 48
SWTaDA*          *OHCORD         7Y 0O 11 48EDTCDE(3)
SWTaMA            W1CUST        11A  O 11 57
SWTaDA*          *OHCUST        11Y 0O 11 56EDTCDE(3)
SWTaMA            W1CNAM        12A  O 11 69
SWTaMA*          *W1CNAM        12A  O 11 68
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20000803  123603  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 22'     Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33' Weight'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 44'Stp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Order'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 57'Cust#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69'Customer'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
710bAA            LBLBL#         7Y 0H
     A            LBITEM        15   O 11  7
640bAA            LDODTE        10   O 11 25
640bAA            LDOTIM         8   O 11 37
     A            LBORD          7Y 0O 11 51EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640bAA                                 10 25'Voice Date'
640bAA                                      DSPATR(HI)
640bAA                                      DSPATR(UL)
640bAA                                 10 37'Voice Time'
640bAA                                      DSPATR(HI)
640bAA                                      DSPATR(UL)
     A                                 10 51' Order '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
