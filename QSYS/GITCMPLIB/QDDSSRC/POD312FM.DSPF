     A*%%TS  SD  20080906  083225  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  POD312FM   View POD log - View record
     A*  04 September 2008
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    09/04/08  DAS  5.20
     A*      - Created.
520aAA*    09/23/09  MLB  5.20a
     A*      - Enh: Revised REFFLD statements that have part of file
     A*        name on 2nd line.  Was causing problems for Webfacing
     A*        conversion program.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA09
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080906  083225  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'POD312.01'
     A                                  1 35'View POD Log'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Try  . . . . . . . . .'
     A            PODTRY    R        O  3 27REFFLD(PODREC/PODTRY *LIBL/PODLOG)
     A                                      EDTCDE(3)
     A                                  4  4'Status . . . . . . . .'
520aDA*           PODSTATUS R        O  4 27REFFLD(PODREC/PODSTATUS *LIBL/PODLO-
520aMA            PODSTATUS R        O  4 27REFFLD(PODREC/PODSTATUS PODLOG)
520aDA*                                     G)
     A                                  5  4'Code . . . . . . . . .'
520aDA*           PODERRCODER        O  5 27REFFLD(PODREC/PODERRCODE *LIBL/PODL-
520aMA            PODERRCODER        O  5 27REFFLD(PODREC/PODERRCODE PODLOG)
520aDA*                                     OG)
     A                                  6  4'Message  . . . . . . .'
520aDA*           PODERRMSG R        O  6 27REFFLD(PODREC/PODERRMSG *LIBL/PODLO-
520aMA            PODERRMSG R        O  6 27REFFLD(PODREC/PODERRMSG PODLOG)
520aDA*                                     G)
     A                                  8  4'Warehouse  . . . . . .'
     A            PODWHSE   R        O  8 27REFFLD(PODREC/PODWHSE *LIBL/PODLOG)
     A                                      EDTCDE(3)
     A                                  9  4'Route / Id . . . . . .'
     A            PODRTE    R        O  9 27REFFLD(PODREC/PODRTE *LIBL/PODLOG)
     A                                  9 33'/'
     A            PODRTID   R        O  9 35REFFLD(PODREC/PODRTID *LIBL/PODLOG)
     A                                 10  4'Stop . . . . . . . . .'
     A            PODSTOP   R        O 10 27REFFLD(PODREC/PODSTOP *LIBL/PODLOG)
     A                                      EDTCDE(3)
     A                                 11  4'Order / Id . . . . . .'
     A            PODCORD   R        O 11 27REFFLD(PODREC/PODCORD *LIBL/PODLOG)
     A                                      EDTCDE(3)
     A                                 11 39'/'
     A            PODORD    R        O 11 41REFFLD(PODREC/PODORD *LIBL/PODLOG)
     A                                      EDTCDE(3)
     A                                 12  4'Sent file name . . . .'
520aDA*           PODFILESNDR        O 12 27REFFLD(PODREC/PODFILESND *LIBL/PODL-
520aMA            PODFILESNDR        O 12 27REFFLD(PODREC/PODFILESND PODLOG)
520aDA*                                     OG)
     A                                 13  4'Response file name . .'
520aDA*           PODFILETRYR        O 13 27REFFLD(PODREC/PODFILETRY *LIBL/PODL-
520aMA            PODFILETRYR        O 13 27REFFLD(PODREC/PODFILETRY PODLOG)
520aDA*                                     OG)
     A                                 14  4'File Start (Sys/UC)  .'
     A            W1TS1         26A  O 14 27
     A            W1UC1         26A  O 14 55
     A                                 15  4'File End (Sys/UC)  . .'
     A            W1TS2         26A  O 15 27
     A            W1UC2         26A  O 15 55
     A                                 16  4'File user/job  . . . .'
     A            PODUSER1  R        O 16 27REFFLD(PODREC/PODUSER1 *LIBL/PODLOG)
     A                                 16 38'/'
     A            PODJOB1   R        O 16 40REFFLD(PODREC/PODJOB1 *LIBL/PODLOG)
     A                                 16 51'/'
     A            PODNBR1   R        O 16 53REFFLD(PODREC/PODNBR1 *LIBL/PODLOG)
     A                                 17  4'File program . . . . .'
     A            PODCALL1  R        O 17 27REFFLD(PODREC/PODCALL1 *LIBL/PODLOG)
     A                                 17 38'/'
     A            PODPGM1   R        O 17 40REFFLD(PODREC/PODPGM1 *LIBL/PODLOG)
     A                                 18  4'Send Start (Sys/UC)  .'
     A            W1TS3         26A  O 18 27
     A            W1UC3         26A  O 18 55
     A                                 19  4'Send End (Sys/UC)  . .'
     A            W1TS4         26A  O 19 27
     A            W1UC4         26A  O 19 55
     A                                 20  4'Send user/job  . . . .'
     A            PODUSER3  R        O 20 27REFFLD(PODREC/PODUSER3 *LIBL/PODLOG)
     A                                 20 38'/'
     A            PODJOB3   R        O 20 40REFFLD(PODREC/PODJOB3 *LIBL/PODLOG)
     A                                 20 51'/'
     A            PODNBR3   R        O 20 53REFFLD(PODREC/PODNBR3 *LIBL/PODLOG)
     A                                 21  4'Send program . . . . .'
     A            PODCALL3  R        O 21 27REFFLD(PODREC/PODCALL3 *LIBL/PODLOG)
     A                                 21 38'/'
     A            PODPGM3   R        O 21 40REFFLD(PODREC/PODPGM3 *LIBL/PODLOG)
     A                                 22  4'Status (Sys/UC)  . . .'
     A            W1TS9         26A  O 22 27
     A            W1UC9         26A  O 22 55
     A                                 23  2'F3=Exit  F5=Refresh  F7=View sent -
     A                                      file  F9=View response file  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
