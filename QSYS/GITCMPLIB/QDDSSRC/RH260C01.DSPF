     A*%%TS  SD  20100903  145306  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  RH260CFM
     A*  August 6, 2015
     A*  Jose G. Duque
     A*
     A*  Revisions:
     A*
412 AA*  08/23/99  DAS  4.12
     A*   - Customer order number added
     A*
414 AA*    06/30/00  DAS  4.14
     A*      - Changed indicator for SFLNXTCHG from 85 to 21.
     A*        Re: Program never changed to use 85 instead of 21.
     A*
530  A*    09/03/10  RBD  5.30
     A*      - Renamed to RH160A01.
     A*      - Added display of driver. Shifted exported date and
     A*        time, status date and time, status, and route id to
     A*        the right to make room for driver.
     A*
640aAA*    06/03/11  JCJ  6.40a
640aAA*      - Renamed to RH260A02.
     A*
     *----------------------------------------------------------------
     *  Client Custom Revisions: European Imports
     *
EIa  A*    03/01/06  HNK  EIa
     A*      - Remove order number.  They have more than one Ord for
     A*        a Willcall route.
     F*
CBI AF*    03/11/09  CBI-JGD
     F*      Add function key "F9" to be able to display
     F*      exported orders by Route or Order #.
     F*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
CBI  A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970828  062531  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH260A'
     A                                  1 23'Work with Exported Will Call Route-
     A                                      s'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Position to route . . . .'
     A            PTINP2         5A  B  3 75
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990823  205411  DAVE        REL-V4R2M0  5769-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
417  A            OHCORD    R        O 11  7REFFLD(OHREC/OHCORD *LIBL/ORDH)
     A            W1STAT        10A  O 11 16
     A            RHRTE     R        O 11 28REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHTRUK    R        O 11 36REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1CUBE         9Y 3O 11 48EDTCDE(3)
     A            W1SWGT         9Y 2O 11 60EDTCDE(3)
     A            W1PCS          5Y 0O 11 72EDTCDE(3)
     A            W1CORD         7Y 0O 11 26EDTCDE(Z)
EIa  A  75                                  DSPATR(ND)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990823  205411  DAVE        REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *---------NEW--FROM HERE-----------------------------------------
     A                                 10  7'Order #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 36'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 60'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 72'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
      *---------TO --HERE----------------------------------------------
     A*           FLD001         1A  O  9  3
     A*                                10 26'Order #'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
EIa  A* 75                                  DSPATR(ND)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OHCORD    R        O 11  7REFFLD(OHREC/OHCORD *LIBL/ORDH)
     A            RHIMPT    R        O 11 34REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHBLDT    R        O 11 53REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHPRTT    R        O 11 72REFFLD(RHREC/RHPRTT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2RPTD         6Y 0O 11 15EDTCDE(Y)
     A            W2IMPD         6Y 0O 11 25EDTCDE(Y)
     A            W2BLDD         6Y 0O 11 44EDTCDE(Y)
     A            W2PRTD         6Y 0O 11 63EDTCDE(Y)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Order#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 25'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 25'---- Imported ---'
     A                                      DSPATR(HI)
     A                                 10 44'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 44'----- Built -----'
     A                                      DSPATR(HI)
     A                                 10 63'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 63'---- Printed ----'
     A                                      DSPATR(HI)
     A                                 10 15'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'Report'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
414 DA* 85                                  SFLNXTCHG
     A          R VW3SFL                    SFL
     A*%%TS  SD  20100903  145306  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
530 AA            TTRUSR    R        O 11 13REFFLD(TTRREC/TTRUSR *LIBL/TTRTE)
     A            W3EXPD         6Y 0O 11 24EDTCDE(Y)
     A            RHEXPT    R        O 11 33REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3STSD         6Y 0O 11 43EDTCDE(Y)
     A            RHSTST    R        O 11 52REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3STAT        10A  O 11 61
     A            RHRTID    R        O 11 74REFFLD(RHREC/RHRTID *LIBL/RTEHED)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20100903  145306  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 24'---- Exported ---'
     A                                      DSPATR(HI)
     A                                  9 43'---- Status -----'
     A                                      DSPATR(HI)
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
530 AA                                 10 13'Driver    '
530 AA                                      DSPATR(HI)
530 AA                                      DSPATR(UL)
     A                                 10 24'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
