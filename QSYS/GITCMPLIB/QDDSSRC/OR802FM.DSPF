     A*%%TS  SD  19991108  130118  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR802FM   Allocation/Picking Log Narrative - Display
     A*  09 November 1999
     A*  Max Blacknik
     A*
     A*  OR802FM - 4.13
     A*
     A*  Revisions
     A*
416 AA*    03/28/02  MLB  4.16
     A*      - Revised subfile, added new field W1MGSQ to display combined
     A*        message id and seq#.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'OR802.01'
     A                                  1 31'Route Narrative'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40   O  2 19DSPATR(HI)
     A  45                              2 64'* Summary Mode *'
     A                                      DSPATR(RI)
     A N45                              2 64'* Detail Mode  *'
     A                                      DSPATR(RI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
416 AA            W1SQNR         5Y 0H      TEXT('Hidden Field: Seq#')
     A            W1SUBR        10A  O  5  2
     A            W1TEXT        68A  O  5 13
     A                                  6  3'Msgid:'
416 DA**          W1MSGN         4A  O  6 10
416 DA**          W1SQNR         5Y 0O  6 15EDTCDE(3)
416 AA            W1MGSQ        10A  O  6 10
     A                                  6 22'Order#:'
     A            W1ORD          7Y 0O  6 30EDTCDE(L)
     A                                  6 40'Cust#:'
     A            W1CUST        11Y 0O  6 47EDTCDE(L)
     A                                  6 60'Item:'
     A            W1ITEM        15A  O  6 66
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      CF09
     A                                      CF11
     A                                      SFLSIZ(0512)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  44                                  SFLDROP(CF11)
     A N44                                  SFLFOLD(CF11)
     A                                      SFLMODE(&SFLMOD)
     A                                      SFLCSRRRN(&RTNRRN)
     A            SFLMOD         1A  H      TEXT('Hidden: Subfile mode')
     A            RTNRRN         5S 0H      TEXT('Hidden: Subfile RRN')
     A            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  4  2'Routine'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Tracking Message Text'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
