     A*%%TS  SD  20020424  111747  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  DH122FM   Dock Matrix Maintenance - Change screen
     A*  16 April 2002
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
416 AA*    04/16/02  MLB  4.16
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020424  111747  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      RTNCSRLOC(&RTNRCD &RTNFLD)
     A  98                                  ALARM
     A            RTNRCD        10A  H      TEXT('Return rcdfmt cursor')
     A            RTNFLD        10A  H      TEXT('Return rcdfld cursor')
     A                                  1  2'DH122.01'
     A                                  1 30'Dock Matrix Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Pass . . . . . . . . .'
     A            W1PASS         3Y 0O  9 28EDTCDE(3)
     A                                 10  4'Dock door status . . .'
     A            W1STAT         1A  O 10 28
     A            W1STDS        30A  O 10 30
     A                                 11  4'Dock door overflow . .'
     A            W1OVRF         1A  O 11 28
     A                                 11 30'Overflow door:'
     A N41                                  DSPATR(ND)
     A            W1OFDR         3Y 0O 11 45EDTCDE(3)
     A N41                                  DSPATR(ND)
     A                                 11 50'Overflow cube:'
     A N41                                  DSPATR(ND)
     A            W1OFCB         9Y 3O 11 65EDTCDE(3)
     A N41                                  DSPATR(ND)
     A                                 12  4'Dock door skip reason.'
     A N42                                  DSPATR(ND)
     A            W1SKPR         1A  O 12 28
     A N42                                  DSPATR(ND)
     A            W1SKDS        50A  O 12 30
     A N42                                  DSPATR(ND)
     A                                 13  4'How assigned . . . . .'
     A            W1ADSC        10A  O 13 28
     A                                 14  5'Assigned by . . . . .'
     A            W1ABY         10A  O 14 28
     A                                 15  6'Assigned date/time .'
     A            W1ADTE         6Y 0O 15 28EDTCDE(Y)
     A                                 15 37'/'
     A            W1ATIM         6Y 0O 15 39EDTWRD(' 0:  :  ')
     A                                 17  4'Route assigned . . . .'
     A            W1RTE          5A  O 17 28
     A                                 18  4'Dock door max cube . .'
     A            W1MAXC         5Y 0O 18 29EDTCDE(3)
     A                                 19  4'Route cube . . . . . .'
     A            W1RTEC         9Y 3O 19 28EDTCDE(L)
     A                                 20  4'Dock door department .'
     A            W1WHDP         5A  O 20 28
     A                                 21  4'Dock door number . . .'
     A            W1DOOR         3Y 0B 21 28EDTCDE(3)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
