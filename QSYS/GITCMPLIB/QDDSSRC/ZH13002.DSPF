     A*%%TS  SD  20091104  135251  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. ZH13001 COPY AND NAME ZH13002
     A*----------------------------------------------------------------
     A*
     A*  ZH130FM  Zone detail maintenance
     A*  11 June 1997
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
     A*    05/26/06  DAS  5.00
     A*      - Moved POSTNREC/OPTREC into DSPSFC.
     A*        Re: Webfacing had problem with double read.
     A*
520aAA*    02/27/09  DAS  P#00150
     A*      - Renamed to ZH13001
     A*      - Added fields designation, category, uom type,
     A*        side, handstack range.
     A*
530 AA*    11/04/07  JCJ  5.30
     A*      - Added Template Code / Template in upper right corner
     A*        of Screen Title.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'ZH130 '
     A                                  1 29'Zone Detail Maintenance'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
530 AA            W1ZTMP        10O     2 70
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTINP1         5A  B  1 74
     A                                  1 46'Position to department . .'
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A            W1TRUK        40A  O  2  2
     A            W1ZONE        40   O  3  2
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20090228  063748  DAVE        REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ZDWHDP    R        O 11  7REFFLD(ZDREC/ZDWHDP *LIBL/ZONED)
     A            ZDSTYP    R        O 11 15REFFLD(ZDREC/ZDSTYP *LIBL/ZONED)
     A            ZDAISL    R        O 11 19REFFLD(ZDREC/ZDAISL *LIBL/ZONED)
     A            ZDRLVL    R        O 11 26REFFLD(ZDREC/ZDRLVL *LIBL/ZONED)
     A                                      EDTCDE(3)
     A            ZDLOC1    R        O 11 31REFFLD(ZDREC/ZDLOC1 *LIBL/ZONED)
     A                                      EDTCDE(3)
     A            ZDLOC2    R        O 11 36REFFLD(ZDREC/ZDLOC2 *LIBL/ZONED)
     A                                      EDTCDE(3)
     A            ZDHND1    R        O 11 41REFFLD(ZDREC/ZDHND1 *LIBL/ZONED)
     A            ZDHND2    R        O 11 46REFFLD(ZDREC/ZDHND2 *LIBL/ZONED)
     A            ZDDESG    R        O 11 50REFFLD(ZDREC/ZDDESG *LIBL/ZONED)
     A            ZDCAT     R        O 11 56REFFLD(ZDREC/ZDCAT *LIBL/ZONED)
     A            ZDUTYP    R        O 11 69REFFLD(ZDREC/ZDUTYP *LIBL/ZONED)
     A            ZDSIDE    R        O 11 74REFFLD(ZDREC/ZDSIDE *LIBL/ZONED)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20090227  214055  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO          4S 0H      SFLRCDNBR
     A            PTINP1         5A  B  3 74
     A                                  3 46'Position to department . .'
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A            W1TRUK        40A  O  4  2
     A            W1ZONE        40   O  5  2
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A            FLD001         1A  O  6  3
     A                                  9 25'Rack'
     A                                      DSPATR(HI)
     A                                  9 31'From'
     A                                      DSPATR(HI)
     A                                  9 36'To'
     A                                      DSPATR(HI)
     A                                  9 40'From'
     A                                      DSPATR(HI)
     A                                  9 45'To'
     A                                      DSPATR(HI)
     A                                  9 67'UOM'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 13'Sectn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 19'Aisle'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'Level'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Loc '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Loc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 40'Hand'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 45'Hand'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 50'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 56'Category  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Side'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
