500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  Revisions
416aAF*    05/23/02  RRH  4.16a
     F*      - Increase LILCNS to 15.
     A*
500 AA*   06/06/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000720  100735  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'LP145.01'
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  1 12'License Transactions'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A*         R SCANREC
     A*                                     LOCK
     A*                                     SLNO(01)
     A*                                     OVERLAY
     A*                                 1 16'Scan slot:'
     A*           W1DISP        12A  B  1 27CHECK(ER)
     A*                                 1  2'IT276.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20000720  100735  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'License:'
416aMA            LILCNS        15A  O  1 11
416aDA*           LILCNS         7Y 0O  1 11EDTCDE(4)
     A                                  1 29'1=Select'
     A                                  2  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000719  125233  ROCKY       REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LITRN#    R        O  5  6REFFLD(LIREC/LITRN# *LIBL/LICENSE)
     A                                      EDTCDE(4)
     A            W1TASK         6A  O  5 15
     A                               O  5 22'/'
     A            W1CAT          6A  O  5 24
     A            W1TRST        10A  O  5 33
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000719  125233  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Trans #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 15'Trans. Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 28'Trans. Status'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
414 DA*                                     SLNO(22)
414 DA*           CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    CLEAR ALL LINES IN SUBFILE LIST
     A*
     A          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R TITLESP
     A*%%TS  SD  20000720  100735  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'LP145.01'
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  1 12'Transacciones de Lic'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
500 AA*----------------------------------------------------------------
500 AA*         R SCANRESP
     A*                                     LOCK
     A*                                     SLNO(01)
     A*                                     OVERLAY
     A*                                 1 16'Escn slot:'
     A*           W1DISP        12A  B  1 27CHECK(ER)
     A*                                 1  2'IT276.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
500 AA*----------------------------------------------------------------
500 AA          R POSTNRSP
     A*%%TS  SD  20000720  100735  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Licncia:'
416aMA            LILCNS        15A  O  1 11
416aDA*           LILCNS         7Y 0O  1 11EDTCDE(4)
     A                                  1 29'1=Seleccnr'
     A                                  2  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A*%%TS  SD  20000719  125233  ROCKY       REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LITRN#    R        O  5  6REFFLD(LIREC/LITRN# *LIBL/LICENSE)
     A                                      EDTCDE(4)
     A            W1TASK         6A  O  5 15
     A                               O  5 22'/'
     A            W1CAT          6A  O  5 24
     A            W1TRST        10A  O  5 33
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  20000719  125233  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'# Trans'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 15'TipoDeTrans'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 28'Estado Trans'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R OPTRECSP
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Escriba opciones, oprima Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R CMDRECSP
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  2  2'F5=Refrescar  F12=Cancelar'
     A                                      COLOR(BLU)
414 DA*                                     SLNO(22)
414 DA*           CMDLN1        79   O  2  2COLOR(BLU)
     A*****************************************************************
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
500 AA*----------------------------------------------------------------
500 AA          R DELRECSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Oprima Enter para confirmar-
     A                                       F4=Borrar.'
     A                                      COLOR(BLU)
     A                                  4  2'Oprima F12 para volver a cambiar-
     A                                       seleccion.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*    COMMAND LINES
     A*
     A          R CMDDELSP
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancelar'
     A                                      COLOR(BLU)
