     A*%%TS  SD  20100618  162454  ROCKY       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UTMNUSFM  PIR UTILITY MENUS
     A*  18 JUNE, 2010
     A*  Roxanne Harrison
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
530 AA*    06/18/10  RH   5.30
     A*      - Created.
640 AA*    08/27/12  RH   6.40
     A*      - add option 5 BFC Tomcat Server Utility Menu'
     A*
650 AA*    02/27/14  MLB  6.50
     A*      - Fix: Corrected spelling for opt# 3 Transaction.
     A*      - Created new dspf,UTMNUS02 for menu.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20100618  162454  ROCKY       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'        '
     A                                  1 34'PIR Utility Menus'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4' 1. PIR Utility Menu             '
     A                                  8  4' 2. Voice Utility Menu           '
650 MA                                  9  4' 3. Transaction Utility Menu     '
     A                                 10  4' 4. PIR Zebra Utility Menu       '
640aAA                                 11  4' 5. BFC Tomcat Server Utility Menu'
     A                                 18  4'90. Sign Off                     '
     A                                 20  4'Option: '
     A            W1OPT          2Y 0B 20 15EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
