     A*%%TS  SD  19940711  163703  HEMANT      REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TC112FM   Task Category Table maintenance - Change screen
     A*  14 December, 1995
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940711  163703  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TC112.01'
     A                                  1 26'Task Category Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Code . . . . . . . . .'
     A            W1CODE         6A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Description  . . . . .'
     A            W1DESC        30A  B 11 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Sub category . . . . .'
     A            W1COD1         6A  B 12 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES1        30A  B 12 36CHECK(LC)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD2         6A  B 13 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES2        30A  B 13 36CHECK(LC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD3         6A  B 14 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES3        30A  B 14 36CHECK(LC)
     A N25N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD4         6A  B 15 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES4        30A  B 15 36CHECK(LC)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD5         6A  B 16 28
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES5        30A  B 16 36CHECK(LC)
     A N27N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD6         6A  B 17 28
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES6        30A  B 17 36CHECK(LC)
     A N28N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD7         6A  B 18 28
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES7        30A  B 18 36CHECK(LC)
     A N29N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD8         6A  B 19 28
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES8        30A  B 19 36CHECK(LC)
     A N30N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD9         6A  B 20 28
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES9        30A  B 20 36CHECK(LC)
     A N31N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*
     A            W1COD0         6A  B 21 28
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DES0        30A  B 21 36CHECK(LC)
     A N32N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
