     A*%%TS  SD  19960320  164523  DAVE3       REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  IT322FM   View warehouse book by item.
     A*  11 December 1993
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions:
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19960320  151215  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 35'Slot Listing'
     A                                      DSPATR(HI)
     A                                  1  2'SL332'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19960320  164523  DAVE3       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WDCODE         5A  O  2  2
     A            WOAISL         3A  O  3  2
     A            WHDESC        30A  O  1 10
     A            WDDESC        30A  O  2 10
     A                                  1 41'Change to warehouse   .'
     A                                  2 41'Change to department  .'
     A                                  1  8'-'
     A                                  2  8'-'
     A                                  3 41'Change to aisle . . . .'
     A            PTINP3         3A  B  3 66
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A            PTINP2         5A  B  2 66
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A            PTINP1         3Y 0B  1 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WOWHSE         3A  O  1  2
     A            PTTXT4        23   O  4 41DSPATR(ND)
     A            PTINP4         5Y 0B  4 66EDTCDE(4)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960320  162611  DAVE3       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            SLDISP        12A  O 10  4
     A            SLPSEQ    R        O 10 20REFFLD(SLREC/SLPSEQ *LIBL/SLOT)
     A            SLSDEF    R        O 10 28REFFLD(SLREC/SLSDEF *LIBL/SLOT)
     A            SLPICK    R        O 10 34REFFLD(SLREC/SLPICK *LIBL/SLOT)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960320  162611  DAVE3       REL-V3R1M0  5763-PW1
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0072)
     A                                      SFLPAG(0024)
     A                                      SFLLIN(0010)
     A                                  9  4'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 18'Pick Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 28'Def'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 44'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 58'Pick Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 68'Def'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 73'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
