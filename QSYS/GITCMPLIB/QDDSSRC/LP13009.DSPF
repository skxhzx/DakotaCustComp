     A*%%TS  SD  20150804  085212  ROCKY       REL-V6R1M0  5761-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LP130FM   Pallet License Plate - View RF (Full Screen)
     A*  08 September, 1999
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*  09/08/99  HNK 4.12
     A*   - Created
     A*  07/13/00  RH  4.14a
     A*   - Add transaction type(task code and task category code).
414dAA*    09/13/00  HNK  4.14d
     A*      - Add pick slot on screen 2
     A*
414eAA*  10/13/00  DAS 4.14e
     A*   - Rearranged fields.
     A*   - Added receiving fields W2RDTE, W2RTIM, W2RUSR, W2RDEV,
     A*     W2RPO, W2RACT.
     A*   - Added generated date/time fields W2GDTE, W2GTIM.
     A*   - Remove W2PDTE.
416 AA*  08/16/01  MLB 4.16
     A*   - Added receiving fields W2EXPD, W2MFRD TO SCREEN2.
416aAA*    05/23/02  RRH  4.16a
     A*      - Increase W1LCNS and W2LCNS to 15.
416bAA*    08/06/02  MLB  4.16b
     A*      - Remove overlay from screen 1 & 2.
     A*      - Add dummy i/o field to screen 2.
417aAA*    05/20/04  RH   4.17a
     A*      - Enh: Add screen 3 for selection information.
     A*
500 AA*   06/06/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   10/17/06  RH   5.10a
     A*     - Add F8=Date info to Screen 2.
600aAA*   05/17/08  DAS  6.00a
     A*     - Added lot code to screen 2
     A*     - 4.17 change from Jordano lic hist programs.
600bAA*   03/05/09  DAS  6.00b
     A*     - Revised to work with new license tracking.
     A*     - Renamed to LP13001
600cAA*   06/09/09  DAS  6.00c
     A*     - Added tran# back into screen 1.
     A*     - Changed screen3 to just display tran info.
610aAA*   02/16/10  DAS  6.10a
     A*     - Added receiving tran# to screen 2
640aAA*   05/31/12  RH   6.40a
     A*     - Renamed to LP13003 to re-instate $8=Dates
650aAA*   12/28/12  RH   6.50a
     A*     - Renamed to LP13004 to re-instate Screen3 pick qty
650bAA*   08/05/13  lmc  6.50b
     A*     - Renamed to LP13005 to add f9 for catch weight override.
     A*     - Add Received weight to screen2.
650cAA*   01/28/14  lmc  6.50c
     A*     - Renamed to LP13006 to add brand.
650dAA*   02/18/14  lmc  6.50d
     A*     - Renamed to LP13007 to add f6 for weight details.
700aAA*   08/04/15  RH   7.00a
     A*     - Renamed to LP13008 to add Licnese dept and item dept
700bAA*   04/05/16  LMC  7.00b
     A*     - Renamed to LP13009 to display Item Maintenance UOM on
     A*       the received weight field.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
416bDA*                                     OVERLAY
     A          R SCREEN1
     A*%%TS  SD  20090610  115845  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130.01'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A                                  7  9'Warehouse . .'
     A            W1WHSE         3Y 0B  7 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9  9'License . . .'
416aDA*           W1LCNS         7Y 0B 10 24CHECK(ER)
     A            W1LCNS        15A  B  9 24CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 11 11'- OR -'
416aDA*                                     EDTCDE(4)
     A                                 13  9'Tran# . . . .'
     A            W1TRN#         7Y 0B 13 24CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
416bDA*                                     OVERLAY
     A          R SCREEN2
     A*%%TS  SD  20150804  085212  ROCKY       REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A                                      CF07
     A                                      CF08
     A  31                                  CF09
     A  33                                  CF10
     A  98                                  ALARM
     A                                  1  2'LP130.02'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  4  5'License . . . . . . .'
     A            W2LCNS        15A  O  4 28
     A                                  5  5'Original license  . .'
     A            W2OLCNS       15A  O  5 28
     A                                  6  5'Item  . . . . . . . .'
     A            W2ITEM        50A  O  6 28
     A                                  7  5'Pick slot . . . . . .'
     A            W2PSLT        35A  O  7 28
700a A                                  7 65'Item Dept:'
700a A            W2IWHD         5A  O  7 76
     A                                  9  5'Current location  . .'
     A                                      DSPATR(HI)
     A            W2DISP        25A  O  9 28DSPATR(HI)
700a A  36                              9 65'Slot Dept:'
700a A  36        W2SWHD         5A  O  9 76
     A                                 10  5'Quantity  . . . . . .'
     A                                      DSPATR(HI)
     A            W2QTY         10A  O 10 28DSPATR(HI)
     A                                 11  5'Lcns entered date . .'
     A                                      DSPATR(HI)
     A            W2ENTD         6Y 0O 11 28DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                 12  5'Lcns exp date . . . .'
     A                                      DSPATR(HI)
     A            W2EXPD         8A  O 12 28DSPATR(HI)
     A                                 14  5'Received  . . . . . .'
     A            W2RDTE         6Y 0O 14 28EDTCDE(Y)
     A            W2RTIM         6Y 0O 14 38EDTWRD(' 0:  :  ')
     A                                 15  5'Purchase order  . . .'
     A            W2RPO          9A  O 15 28
     A                                 16  5'Received item . . . .'
     A            W2RITEM       50A  O 16 28
     A                                 17  5'Date entered  . . . .'
     A            W2RENTDTE      8A  O 17 28
     A            W2DTYP        20A  O 17 38
     A                                 18  5'Date calculated . . .'
     A            W2RCALCDTE     8A  O 18 28
     A                                 19  5'Lot code  . . . . . .'
     A            W2LOT         30A  O 19 28
     A  32                             20  5'Received Weight . . .'
     A  32        W2LNTTLLBS     9Y 2O 20 28EDTCDE(J)
700bAA  32        IMUOM          3A  O 20 42
     A                                 21  5'Brand . . . . . . . .'
     A            W2BRAND       10A  O 21 28
650bDA*                                23  2'F3=Exit  F7=History  F8=Dates +
     A                                 23  2'F3=Exit  F7=History  F8=Dates'
     A                                      COLOR(BLU)
650bDA*                                       F12=Cancel'
650dDA*N31                             23 33'F12=Cancel'
650DDA*                                     COLOR(BLU)
650dDA* 31                             23 33'F9=Weights  F12=Cancel'
     A  31                             23 33'F9=Weights'
     A                                      COLOR(BLU)
     A  33                             23 45'F10=Rcvd Weights'
     A                                      COLOR(BLU)
     A                                 23 63'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 14 49'Trn#'
     A            W2TRN#         7Y 0O 14 54EDTCDE(4)
     A          R SCREEN3
     A*%%TS  SD  20090610  115845  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A                                      CF07
     A  98                                  ALARM
     A                                  1  2'LP130.03'
     A                                  1 29'Transaction Information'
     A                                      DSPATR(HI)
     A            FLD03          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  4  3'General Information:'
     A                                      DSPATR(HI)
     A                                  6  5'Transaction . . . . .'
     A            W3TRN#         7Y 0O  6 28EDTCDE(3)
     A            W3TASK         6A  O  6 38
     A                                  6 45'/'
     A            W3TCAT         6A  O  6 47
     A                                  7  5'Status  . . . . . . .'
     A            W3TRST        10A  O  7 28
650aAA                                  8  5'Quantity  . . . . . .'
650aAA            W3PQTY         5Y 0O  8 28EDTCDE(4)
     A                                  9  5'Employee  . . . . . .'
     A            W3EMP#         5Y 0O  9 28EDTCDE(3)
     A            W3ENAM        30A  O  9 35
     A                                 11  5'Generated . . . . . .'
     A            W3GDTE         6Y 0O 11 28EDTCDE(Y)
     A            W3GTIM         6Y 0O 11 38EDTWRD(' 0:  :  ')
     A                                 12  5'Started . . . . . . .'
     A            W3SDTE         6Y 0O 12 28EDTCDE(Y)
     A            W3STIM         6Y 0O 12 38EDTWRD('  :  :  ')
     A                                 13  5'Ended . . . . . . . .'
     A            W3EDTE         6Y 0O 13 28EDTCDE(Y)
     A            W3ETIM         6Y 0O 13 38EDTWRD('  :  :  ')
     A                                 14  5'Closed  . . . . . . .'
     A            W3CDTE         6Y 0O 14 28EDTCDE(Y)
     A            W3CTIM         6Y 0O 14 38EDTWRD('  :  :  ')
     A                                 23  2'F3=Exit  F7=History  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
416bDA*                                     OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130.01'
     A                                  1 13'Info de Licencia/Transaccion'
     A                                      DSPATR(HI)
     A                                  7 16'Almc'
     A            W1WHSE         3Y 0B  7 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8 12'# Trans.'
     A            W1TRN#         7Y 0B  8 24CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 15'-O-'
     A                                 10 12'Licencia'
416aDA*           W1LCNS         7Y 0B 10 24CHECK(ER)
416aMA            W1LCNS        15A  B 10 24CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                 23  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A*         R SCREEN2
     A          R SCRN2SP
     A*%%TS  SD  20090307  072144  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A                                      CF07
650bAA  31                                  CF09
     A  98                                  ALARM
     A                                  1  2'LP130.02'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  4  5'Licencia  . . . . . .'
     A            W2LCNS        15A  O  4 28
     A                                  5  5'Original licencia . .'
     A            W2OLCNS       15A  O  5 28
     A                                  6  5'Producto  . . . . . .'
     A            W2ITEM        50A  O  6 28
     A                                  7  5'Pick slot . . . . . .'
     A            W2PSLT        35A  O  7 28
     A                                  9  5'Current location  . .'
     A                                      DSPATR(HI)
     A            W2DISP        25A  O  9 28DSPATR(HI)
     A                                 10  5'Cantidad  . . . . . .'
     A                                      DSPATR(HI)
     A            W2QTY         10A  O 10 28DSPATR(HI)
     A                                 11  5'Entered date  . . . .'
     A                                      DSPATR(HI)
     A            W2ENTD         6S 0O 11 28DSPATR(HI)
     A                                 12  5'Slot exp date . . . .'
     A                                      DSPATR(HI)
     A            W2EXPD         8A  O 12 28DSPATR(HI)
     A                                 14  5'Recivido  . . . . . .'
     A            W2RDTE         6Y 0O 14 28EDTCDE(Y)
     A            W2RTIM         6Y 0O 14 38EDTWRD(' 0:  :  ')
     A                                 15  5'Purchase order  . . .'
     A            W2RPO          9A  O 15 28
     A                                 16  5'Received'
     A                                 16 14'item'
     A                                 16 19'.'
     A                                 16 21'.'
     A                                 16 23'.'
     A                                 16 25'.'
     A            W2RITEM       50A  O 16 28
     A                                 17  5'Date entered  . . . .'
     A            W2RENTDTE      8A  O 17 28
     A            W2DTYP        20A  O 17 38
     A                                 18  5'Date calculated . . .'
     A            W2RCALCDTE     8A  O 18 28
     A                                 19  5'Lot code  . . . . . .'
     A            W2LOT         30A  O 19 28
650bAA  32                             20  5'Received Weight . . .'
650bAA  32        W2LNTTLLBS     9Y 2O 20 28EDTCDE(J)
700bAA  32        IMUOM          3A  O 20 42
650bDA*                                23  2'F3=Exit  F7=History  F12=Cancel'
650bMA                                 23  2'F3=Exit  F7=History  F8=Dates'
     A                                      COLOR(BLU)
650bMA N31                             23 33'F12=Cancel'
650BAA                                      COLOR(BLU)
650bAA  31                             23 33'F9=Weights  F12=Cancel'
650bAA                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130.03'
     A                                  1 31'Licencia/Info de Transaccion'
     A                                      DSPATR(HI)
     A            FLD03          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'Informacion General:'
     A                                      DSPATR(HI)
     A                                  4  5'Licencia. . . . . . .'
     A            W3LCNS        15A  O  4 28
     A                                  5  5'Transaccion . . . . .'
     A            W3TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  5 37'Estado:'
     A            W3TRST        10A  O  5 45
     A                                  5 57'Tipo:'
     A            W3TASK         6A  O  5 63
     A                                  5 70'/'
     A            W3TCAT         6A  O  5 72
     A                                  6  5'Generado . . . . . .'
     A            W3GDTE         6Y 0O  6 28EDTCDE(Y)
     A            W3GTIM         6Y 0O  6 38EDTWRD(' 0:  :  ')
     A                                  7  5'Route . . . . . . . .'
     A            W3RTE          5A  O  7 28
     A                                  8  5'ID de ruta . . . . .'
     A            W3RTID         5A  O  8 28
     A                                  9  5'Cantidad  . . . . . .'
     A            W3PQTY         5Y 0O  9 28EDTCDE(4)
     A                                 15  3'Info de Transaccion  :'
     A                                      DSPATR(HI)
     A                                 17  5'Empleado  . . . . . .'
     A            W3EMP#         5Y 0O 17 28EDTCDE(3)
     A            W3ENAM        30A  O 17 35
     A                                 18  5'Empezado. . . . . . .'
     A            W3SDTE         6Y 0O 18 28EDTCDE(Y)
     A            W3STIM         6Y 0O 18 38EDTWRD('  :  :  ')
     A                                 19  5'Terminado . . . . . .'
     A            W3EDTE         6Y 0O 19 28EDTCDE(Y)
     A            W3ETIM         6Y 0O 19 38EDTWRD('  :  :  ')
     A                                 20  5'Cerrado . . . . . . .'
     A            W3CDTE         6Y 0O 20 28EDTCDE(Y)
     A            W3CTIM         6Y 0O 20 38EDTWRD('  :  :  ')
     A                                 21  5'Estado de rotulo. . .'
     A            W3LBST        10A  O 21 28
     A                                 23  2'F3=Salir   F12=Cancelar'
     A                                      COLOR(BLU)
