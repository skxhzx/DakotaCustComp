     A*%%TS  SD  20110421  204642  ROCKY       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  PO838FM   View PORCV records Full PO
     A*  19 April, 2011
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
640 AA*    04/19/11  RH   6.40
     A*      - Created using PO138
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20001012  134849  DAVE3       REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'PO838.01'
     A                                  1 30'View PO Receiving History'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1 14EDTCDE(3)
     A                                  1 18'-'
     A            WHDESC        30A  O  1 20
     A                                  2  2'PO Number:'
     A            $KPO           9A  O  2 14
     A**                                3  7'Item:'
     A**          W1ITEM        50A  O  3 14
     A                                  1  2'Warehouse:'
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20110421  204642  ROCKY       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1DATE         6Y 0O 11  7EDTCDE(Y)
     A            PORTIM    R        O 11 17REFFLD(PORREC/PORTIM *LIBL/PORCV)
     A                                      EDTWRD(' 0:  :  ')
     A            W1ACT          8A  O 11 27
     A            W1QTY          5Y 0O 11 37EDTCDE(4)
     A            W1UOM          2A  O 11 43
     A            W1ITEM        15A  O 11 47
     A            PORLIC    R        O 11 64REFFLD(PORREC/PORLIC *LIBL/PORCV)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20110421  204642  ROCKY       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Action  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'License'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20001012  171115  DAVE3       REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1DATE         6Y 0O 11  7EDTCDE(Y)
     A            PORTIM    R        O 11 17REFFLD(PORREC/PORTIM *LIBL/PORCV)
     A                                      EDTWRD(' 0:  :  ')
     A            W1ACT          8A  O 11 27
     A            W1QTY          5Y 0O 11 37EDTCDE(4)
     A            W1UOM          2A  O 11 43
     A            PORUSR    R        O 11 47REFFLD(PORREC/PORUSR *LIBL/PORCV)
     A            POREMP    R        O 11 58REFFLD(PORREC/POREMP *LIBL/PORCV)
     A                                      EDTCDE(4)
     A            PORTRN    R        O 11 64REFFLD(PORREC/PORTRN *LIBL/PORCV)
     A                                      EDTCDE(4)
     A            PORLBL    R        O 11 73REFFLD(PORREC/PORLBL *LIBL/PORCV)
     A                                      EDTCDE(4)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20001012  171115  DAVE3       REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD002         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Action  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Empl#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Label #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20001012  142333  DAVE3       REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1DATE         6Y 0O 11  7EDTCDE(Y)
     A            PORTIM    R        O 11 17REFFLD(PORREC/PORTIM *LIBL/PORCV)
     A                                      EDTWRD(' 0:  :  ')
     A            W1ACT          8A  O 11 27
     A            W1QTY          5Y 0O 11 37EDTCDE(4)
     A            W1UOM          2A  O 11 43
     A            PORUSR    R        O 11 47REFFLD(PORREC/PORUSR *LIBL/PORCV)
     A            PORDEV    R        O 11 59REFFLD(PORREC/PORDEV *LIBL/PORCV)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD003         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Action  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 59'Device    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
