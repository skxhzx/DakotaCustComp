     A*%%TS  SD  20020713  065953  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A********************************************************************
     A*
415 AA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS IF NECESSARY.
     A*
     A*
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A**************************************************************************
     A*  SU252FM   RF Date StuffIt - Change
     A*
     A*  Revisions:
     A*
416 AA*    07/13/02  DAS  4.16
     A*      - Created.
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020713  065953  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SU252.01'
     A                                  1 12'Date StuffIt'
     A                                      DSPATR(HI)
     A            SLDISP        12A  O  2  1
     A            WOITEM        65A  O  3  1
     A                                  4  1'Exp:'
     A            WOEXPD         6Y 0O  4  6EDTCDE(Y)
     A            WODTE          8A  O  5  1
     A            W1DTE          6Y 0B  5 10
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N21                                  DSPATR(HI)
     A                                  5 17'(MDY)'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRBTM
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(16)
     A          R CLRSCR
     A                                      CLRL(*ALL)
415 AA*****************************************************************
415 AA*
415 AA* 16-line screens
415 AA*
     A          R SCREEN61
     A*%%TS  SD  20020713  065953  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'SU252.61'
     A                                  1 12'Date StuffIt'
     A                                      DSPATR(HI)
     A                                  3  1'Slt:'
     A            SLDISP        12A  O  3  6
     A                                  5  1'Itm:'
     A            ITITEM        15A  O  5  6
     A                                  6  1'Dsc:'
     A            ITDESC        30A  O  6  6
     A                                  7  1'Pck:'
     A            ITPDSC        15A  O  7  6
     A                                  8  1'Exp:'
     A            WOEXPD         6Y 0O  8  6EDTCDE(Y)
     A            WODTE          8A  O 11  1
     A            W1DTE          6Y 0B 11 10
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N21                                  DSPATR(HI)
     A                                 11 17'(MDY)'
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
415 AA*****************************************************************
415 AA*
415 AA* Error Message Subfile Records for 16-line screens
415 AA*
415 AA*    Error Message Subfile Record
415 AA*
415 AA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415 AA*
415 AA*    Error Message Subfile Control Record
415 AA*
415 AA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415 AA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
