     A*%%TS  SD  19960612  165722  ROCKY       REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  IT317AFM  View inventory adjustment report.
     A*  11 June 1996
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*  Roxanne Harrison
     A*
     A*  Revisions:
410 AF*    12/07/98  RH   4.10
     F*      - Add Adjustment code PARM to screen.
415aAF*    06/16/01  RH   4.15a
     F*      - Add WOLINK to show if item is linked.
415bAF*    06/14/01  RH   4.15b
     F*      - Add WOTOT total gain/loss to screen.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19941109  162934  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Inventory  Adjustments'
     A                                      DSPATR(HI)
     A                                  1  2'IT317A'
     A                                  2 30'  By Item/Date/Slot   '
     A                                      DSPATR(HI)
     A            W1FMDT         6Y 0O  3 30EDTCDE(Y)
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0O  3 44EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  3 40'to'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19960612  144124  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(04)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A            W1ITEM        15A  O  2  8
415aAA            WOLINK         6A  O  2 24
415aMA                                  2 35'Slot:'
415aMA            W1DISP        12A  O  2 41
415aMA                                  2 55'Adjustment code:'
415aMA            W1CODE         4A  O  2 72
415aDA*                                 2 30'Slot:'
415aDA*           W1DISP        12A  O  2 36
410 DA*                                 2 50'Adjustment code:'
410 DA*           W1CODE         4A  O  2 67
     A                                  3 41'Position to item  . . .'
     A            PTINP1        15A  B  3 65
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  2  2'Item:'
415b A                                  3  2'Total gain/loss:'
415b A            WOTOT         10A  O  3 20
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960612  163957  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            IAITEM    R        O  9  2REFFLD(OQREC/IAITEM *LIBL/IT316A)
     A            W1DATE         6Y 0O  9 18EDTCDE(Y)
     A            W1TIME         4Y 0O  9 27EDTWRD('  :  ')
     A            IADISP    R        O  9 33REFFLD(OQREC/IADISP *LIBL/IT316A)
     A            IABY      R        O  9 46REFFLD(OQREC/IABY *LIBL/IT316A)
     A            QTY           10A  O  9 58
     A            W1CWTA         7Y 2O  9 71EDTCDE(M)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960612  163957  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 58'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 27'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 46'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 71'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 33'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19960612  131110  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            QTY           10A  O  9 60
     A            W1DATE         6Y 0O  9 35EDTCDE(Y)
     A            IAITEM    R        O  9  2REFFLD(OQREC/IAITEM *LIBL/IT316A)
     A            IADISP    R        O  9 44REFFLD(OQREC/IADISP *LIBL/IT316A)
     A            W1CWTA         7Y 2O  9 71EDTCDE(M)
     A            IACODE    R        O  9 57REFFLD(OQREC/IACODE *LIBL/IT316A)
     A            W1DESC        16A  O  9 18
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19960612  131110  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 60'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 18'Description     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 55'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 71'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 35'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 44'Slot      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19960612  164728  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            IAITEM    R        O  9  2REFFLD(OQREC/IAITEM *LIBL/IT316A)
     A            W1DATE         6Y 0O  9 18EDTCDE(Y)
     A            W1TIME         4Y 0O  9 27EDTWRD('  :  ')
     A            IADISP    R        O  9 33REFFLD(OQREC/IADISP *LIBL/IT316A)
     A            IACODE    R        O  9 48REFFLD(OQREC/IACODE *LIBL/IT316A)
     A            IAMEMO    R        O  9 51REFFLD(OQREC/IAMEMO *LIBL/IT316A)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19960612  165722  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO3         4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 27'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 51'Memo                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 46'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 33'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
