     A*%%TS  SD  20000613  154047  HEMANT      REL-V4R2M0  5769-PW1
     A*
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LU121FM   Transaction lookup by user/start date.
     A*  28 July 1999
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
412 AA*  07/29/99  HNK  4.12
     A*  - Avg percentage disabled.  It was a misleading average.
     A*
520 AA*  03/07/08  RBD  5.10
     A*  - Added following text to optrec for clarity :
     A*    " FIXED Pieces Include Merge Pickup Pieces ".
     A*    Some pgms, ie ER382, do not include merge pieces.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA10
     A                                      CA11
     A                                      CA12
     A                                      CA17
     A                                      CA18
     A                                      CA20
     A          R TITLE
     A*%%TS  SD  19981228  111032  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1  3'LU121.01 '
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 27'Transactions Lookup by User'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19981229  113212  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 45'Position to start date . .'
     A            PTDMY1        78A  O  1  2
     A            PTDMY2        78A  O  3  2
     A            PTINP1         6Y 0B  2 73EDTCDE(Z)
     A                                  2  3'User:'
     A            W1UNAM        30A  O  2  9
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20000613  154047  HEMANT      REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
520 DA*                                 1  2'Press F3 to exit.'
520 MA                                  1  2'Previous task includes idle time'
520 MA                                      COLOR(WHT)
520 MA                                  1 38' FIXED Pieces Include Merge Pick-
520 MA                                      up Pieces '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A*           OPTLN2        75A  O  3  2COLOR(BLU)
520 MA            OPTLN2        75A  O  3  4COLOR(BLU)
     A*           OPTLN1        75A  O  2  2COLOR(BLU)
520 MA            OPTLN1        75A  O  2  1COLOR(BLU)
     A*           W1APCN         5Y 2O  1 69EDTCDE(4)
520 MA            W1APCN         5Y 2O  2 76EDTCDE(4)
     A                                      DSPATR(ND)
     A                                  1 26'Idle time included in previous tas-
     A                                      k.'
     A                                      COLOR(WHT)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960219  164626  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            WTTASK    R        O 11  2REFFLD(WTREC/WTTASK *LIBL/WRKTRAN)
     A            W1SDTE         6Y 0O 11 10EDTWRD('  /  /  ')
     A            W1EDTE         6Y 0O 11 30EDTWRD('  /  /  ')
     A            W1HRS          2Y 0O 11 52EDTCDE(3)
     A            W1MIN          2Y 0O 11 56EDTCDE(3)
     A            WTSTIM    R        O 11 20REFFLD(WTREC/WTSTIM *LIBL/WRKTRAN)
     A                                      EDTWRD('  :  :  ')
     A            WTETIM    R        O 11 40REFFLD(WTREC/WTETIM *LIBL/WRKTRAN)
     A                                      EDTWRD('  :  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960219  164626  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'  Date  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 10'<------Start----->'
     A                                      DSPATR(HI)
     A                                  9 30'<-------End------>'
     A                                      DSPATR(HI)
     A                                 10 30'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 40'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'Hrs Min'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 52' Time  '
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19981228  111032  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            WTTASK    R        O 11  2REFFLD(WTREC/WTTASK *LIBL/WRKTRAN)
     A            WTCUBE    R        O 11 10REFFLD(WTREC/WTCUBE *LIBL/WRKTRAN)
     A                                      EDTCDE(3)
     A            WTSWGT    R        O 11 22REFFLD(WTREC/WTSWGT *LIBL/WRKTRAN)
     A                                      EDTCDE(3)
     A            WTPCS     R        O 11 35REFFLD(WTREC/WTPCS *LIBL/WRKTRAN)
     A                                      EDTCDE(4)
     A            W1STDP         5Y 0O 11 53EDTCDE(4)
     A            W1PCNT         5Y 2O 11 60EDTCDE(4)
     A            W1HRS          2Y 0O 11 44EDTCDE(3)
     A            W1MIN          2Y 0O 11 48EDTCDE(3)
     A            W1STPH         5  0O 11 68EDTCDE(4)
     A            W1TPCN         5  2O 11 75EDTCDE(4)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19981228  111032  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'Total cube'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 22'     Total'
     A                                      DSPATR(HI)
     A                                 10 22'    Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34' Total'
     A                                      DSPATR(HI)
     A                                 10 34'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 52'Std pcs'
     A                                      DSPATR(HI)
     A                                 10 52' per Hr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 60'  % of'
     A                                      DSPATR(HI)
     A                                 10 60'   Std'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'Hrs Min'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'  Time '
     A                                      DSPATR(HI)
     A                                  9 69'Std'
     A                                      DSPATR(HI)
     A                                 10 69'TPH'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 76'% of'
     A                                      DSPATR(HI)
     A                                 10 75'StdTPH'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
    DA* SUBFILE FOR LIST DISPLAY - VIEW 3
    DA*
    DA*         R VW3SFL                    SFL
    DA*
    DA* 21                                  SFLNXTCHG
    DA*           OPTION         2A  B 11  2
    DA* 20                                  DSPATR(PC)
    DA*N21N90                               DSPATR(UL)
    DA* 21                                  DSPATR(RI)
    DA* 90                                  DSPATR(PR)
    DA*           PTTASK    R        O 11  6REFFLD(PTREC/PTTASK *LIBL/PIRTRAN)
    DA*
    DA*         R VW3SFC                    SFLCTL(VW3SFL)
    DA*                                     SFLSIZ(0010)
    DA*                                     SFLPAG(0010)
    DA*                                     ROLLDOWN
    DA*                                     ROLLUP
    DA*                                     OVERLAY
    DA* 01                                  SFLDSP
    DA* 01                                  SFLDSPCTL
    DA*N01                                  SFLCLR
    DA*                                10  2'Opt'
    DA*                                     DSPATR(HI)
    DA*                                     DSPATR(UL)
    DA*                                10  6'Task  '
    DA*                                     DSPATR(UL)
    DA*                                     DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
