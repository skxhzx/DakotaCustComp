     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG120FM    Staging Import View Import Item Updates
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IMPSITMN)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            IUUTYP    R        O  8 10
     A            IUWHDP    R        O  8 13
     A            IUITEM    R        O  8 19
     A            IUTYPE    R        O  8 36
     A            IUSTYP    R        O  8 40
     A            IUDESG    R        O  8 43
     A            S1DESC        30A  O  8 49
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 30'View Item Import Data'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'A/C'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 13'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19'     Item      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 35'Typ'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 39'Sec'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 43'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'             Desc             '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A  98                                  ALARM
     A                                  1  2'SG120.02'
     A                                  1 30'View Item Import Data'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 26'Full Item Information - Page 1'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse . . .'
     A            IUWHSE    R        O  4 18DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                  4 34'Added Timestamp . .'
     A            IUADDTS   R        O  4 54DSPATR(HI)
     A                                  5  2'Batch ID  . . .'
     A            IUBATID   R        O  5 18DSPATR(HI)
     A                                  5 34'Status Timestamp  .'
     A            IUSTSTS   R        O  5 54DSPATR(HI)
     A                                  7  2'Status  . . . .'
     A            IUSTATUS  R        O  7 18DSPATR(HI)
     A            IUTEXT    R        O  7 24DSPATR(HI)
     A                                 11  2'Update Type . .'
     A            IUUTYP    R        O 11 18REFFLD(IUREC/IUUTYP *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 11 34'Department  . .'
     A            IUWHDP    R        O 11 50DSPATR(HI)
     A                                 12  2'Item  . . . . .'
     A            IUITEM    R        O 12 18REFFLD(IUREC/IUITEM *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 12 34'Item Type . . .'
     A            IUTYPE    R        O 12 50REFFLD(IUREC/IUTYPE *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 13  2'Item Desc . . .'
     A            IUDESC    R        O 13 18REFFLD(IUREC/IUDESC *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 14 34'Pack Desc . . .'
     A            IUPDSC    R        O 14 50REFFLD(IUREC/IUPDSC *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 15  2'Section Type  .'
     A            IUSTYP    R        O 15 18REFFLD(IUREC/IUSTYP *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 15 34'Activity  . . .'
     A            IUACTV    R        O 15 50REFFLD(IUREC/IUACTV *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 16  2'Designation . .'
     A            IUDESG    R        O 16 18REFFLD(IUREC/IUDESG *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 16 34'Build Code  . .'
     A            IUBLD     R        O 16 50REFFLD(IUREC/IUBLD *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 17  2'Vendor Number .'
     A            IUVEN     R        O 17 18REFFLD(IUREC/IUVEN *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 17 34'Vendor Item . .'
     A            IUVITM    R        O 17 50REFFLD(IUREC/IUVITM *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 19  2'Normal UOM  . .'
     A            IUUM1     R        O 19 18REFFLD(IUREC/IUUM1 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 19 34'Dimension Len .'
     A            IUULEN    R        O 19 50REFFLD(IUREC/IUULEN *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 19 57'Wid'
     A            IUUWDT    R        O 19 61REFFLD(IUREC/IUUWDT *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 19 68'Hgt'
     A            IUUHGT    R        O 19 72REFFLD(IUREC/IUUHGT *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20  2'Cube  . . . . .'
     A            IUCUBE    R        O 20 18REFFLD(IUREC/IUCUBE *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 20 34'Breakdown 1 UOM'
     A            IUUM2     R        O 20 50REFFLD(IUREC/IUUM2 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 20 54'Qty'
     A            IUUMQ2    R        O 20 58REFFLD(IUREC/IUUMQ2 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 20 63'Flg'
     A            IUFLG1    R        O 20 67REFFLD(IUREC/IUFLG1 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 21  2'Shipping Weight'
     A            IUSWGT    R        O 21 18REFFLD(IUREC/IUSWGT *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 21 34'Breakdown 2 UOM'
     A            IUUM3     R        O 21 50REFFLD(IUREC/IUUM3 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 21 54'Qty'
     A            IUUMQ3    R        O 21 58REFFLD(IUREC/IUUMQ3 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 21 63'Flg'
     A            IUFLG2    R        O 21 67REFFLD(IUREC/IUFLG2 *LIBL/IMPSITMN)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Page'
     A                                      COLOR(BLU)
