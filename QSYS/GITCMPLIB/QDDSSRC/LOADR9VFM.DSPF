     A********************************************************************
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  LOADR9VFM    VIP - RF Pallet Loading.
     A*  March 19, 2013
     A*  Retha Davis
     A*
     A********************************************************************
     A*  Revisions:
     A*
650aAA* 03/19/13  RBD   6.50a
     A*   - Created for VIP.
     A*   - Removed pallet count from display.
     A*   - User will not scan trailer id for each pallet.  Instead, he
     A*     will scan the trailer id on the last merged pallet of a group.
     A*
     A********************************************************************
     A* Client Custom Revisions:
     A*
     A*
     A********************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA09
     A********************************************************************
     A*         Enter Route # Screen
     A********************************************************************
     A          R SCREEN1
     A                                      CA03
     A                                      CA11
     A                                      CA01
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9V.01'
     A                                  1 12'Plt Load'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  3'Route Id:'
     A            W1RTID         5A  B  5 14
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                  7  3'Route #:'
     A            W1RTE#         5A  B  7 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A N27                             10  1'F3=Exit/Signoff'
     A                                      COLOR(BLU)
     A  27                             10  1'F3=Exit F11=CloseRte'
     A                                      COLOR(BLU)
     A*
     A                                 11  1'F1=Unload Plt'
     A                                      COLOR(BLU)
     A*
     A********************************************************************
     A*         Enter Trailer # Screen (if route no assigned)
     A********************************************************************
     A          R SCREEN2
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A  98                                  ALARM
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(11)
     A                                      OVERLAY
      *
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
      *
     A                                  1  2'LOADR9V.02'
     A                                  4  2'Route:'
     A            W2RTE#         5A  O  4  9
     A                                  5  2'Door #:'
     A            W2DOOR         3A  O  5 10
     A                                  9  2'Trl Id:'
     A            W2TRL#         8A  B  9 10DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  1'F3=Exit  F5=Refresh F4=Prompt'
     A                                      COLOR(BLU)
     A*
     A********************************************************************
     A*         Pallet Loading Screen
     A********************************************************************
     A          R SCREEN3
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9V.03'
     A                                  2  2'Plt Load'
     A                                      DSPATR(HI)
     A                                  4  2'Route:'
     A            W3RTE#         5A  O  4  9
     A                                  5  2'Door #:'
     A            W3DOOR         3A  O  5 10
     A                                  8  2'Plt Id:'
     A            W3PLT#         7Y 0B  8 10DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
     A                                  9  2'Trl Id:'
     A            W3TRL#         8A  B  9 10DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*         Unload Pallet Screen
     A********************************************************************
     A          R SCREEN4
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9V.04'
     A                                  2  2'** UNLOAD PALLET **'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  4  2'Whse:'
     A            W4WHSE         3Y 0B  4 10EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Rte Id:'
     A            W4RTID         5A  B  5 10DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22
     AON21N23N24                            DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  2'Plt Id:'
     A            W4PLT#         7Y 0B  6 10DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                  7  2'Trl Id:'
     A            W4TRL#         8A  B  7 10DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(12)
     A                                      CLRL(1)
