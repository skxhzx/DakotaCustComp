     A*%%TS  SD  20020524  102226  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO236FM    VS License entry P.O. receiving
     A*  16 February, 1998
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Notes:
     A*    Created using PO136 - previous revisions removed.
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    04/27/98  RH   4.09
     A*      - Added W1DISP as a hidden field, replaced W1TRN#.
     A*      - Added W1UMT UOM type (N, 1, 2) as a hidden field.
     A*      - Added W1UOM UOM .
412 AA*    09/07/99  RH   4.12
     A*      - Add "Manual Put away" to screen.
     A*
414 AA*    10/02/00  DAS  4.14
     A*     - Moved lines down to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
415 AA*    03/10/01  DAS  4.15
     A*     - Revised to allow us by PO200 RF small screen receiving.
     A*     - Added SCREEN2.
     A*     - Added MSGREC2, MSGCTL2 and MSGCLR2 to be used with SC2.
     A*
415bAA*    08/22/01  DAS  4.15b
     A*      - Revise lines displaying the item numbers.
416 AA*    05/23/02  RRH  4.16
     A*      - Increase W1LCNS and W2LCNS to 15.
417aAA*    06/18/03  RH   4.17a
     A*      - Add edit code CHECK(ER) to W1LCNS to advance after scan
417bAA*    07/29/03  HNK  4.17b
     A*      - Fix - Add *MORE for subfile SFLEND keyword.
417cAA*    03/01/04  RH   4.17c
     A*      - Fix - Change edit code to not display 0 for qty.
417dAA*    06/04/04  DAS  4.17d
     A*      - Fix - Revised subfile to retrieve record number so
     A*              cursor could be repositioned to a specific
     A*              subfile line.
600aAA*    02/02/09  GJA  6.00a
     A*      - Enh - Add window to select how many labels to print
     A*              for external storage
600bAA*    06/03/09  GJA  6.00b
     A*      - Enh - Add indicatior to site field
610bAA*    08/13/09  GJA  6.10b
     A*      - Enh - Add window to print produce labels
610cAA*    10/21/09  GJA  6.10c
     A*      - Enh - Add error for blank OSS staging field
640aAA*    04/19/11  JGD & RBD 6.40a
     A*      - Enh - Changed format name to PO23601.
     A*      - Enh - Added special status code and F4 to prompt it.
650aAA*    06/05/14  LMC  6.50a
     A*      - Enh - Changed format name to PO23602.
     A*        Added notifiation message field to head1.
     A*        IN96 used to disable F9 offsite storage.
700aAA*    12/23/15  LMC  7.00a
     A*      - Enh - Added indicator 70 to protect license field
     A*        when receiving option Use Generic License = 'N'
     A*        copied display file from PO23602 renamed to PO23603.
710aAA*    04/04/17  RH   7.10a
     A*      - Enh - Added slot definition to display.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: VIP Foodservice
     A*
VIP AA*    04/29/16  MLB  VIP
     A*      - Added W1MSG as 78 byte message field. Display
     A*        warning message for Allergen flagged items.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: SGC
SGCaAA*    04/04/17  RH   SGCa
     A*      - Add field to display 'SPIN' if pallet needs to spin
     A*        based on slot definition length.
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Chef's Warehouse
DRYaAA*    10/30/17  LMC  DRYa
     A*      - protect the w1stat field - client does not want it to
     A*        be changed at time of receipt.
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
640aAA                                      CA04
     A                                      CA05
     A                                      CA12
650aDA*                                     CA09
650aMA N96                                  CA09
610bAA                                      CA08
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  19970917  093509  DAVE3       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            W1DISP        12   H
     A            W1UMT          1   H
417dMA            W1QTY          5Y 0B 13  4EDTCDE(4)
417d A*           W1QTY          5Y 0B 13  4EDTCDE(3)
     A  32                                  DSPATR(RI)
     A N32N69                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  69                                  DSPATR(PR)
     A            W1UOM          5A  O 13 11
     A            W1XDK          1   O 13 20
416 DA*           W1LCNS         7Y 0B 13 27EDTCDE(4)
416  A            W1LCNS        15A  B 13 25
     A  33                                  DSPATR(RI)
     A*N69
     A N33N68N69                            DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  68                                  DSPATR(ND)
700aAA  70
700aMAO 69                                  DSPATR(PR)
700aDA* 69                                  DSPATR(PR)
417a A                                      CHECK(ER)
     A*
     A* *DS3                                SFLLIN(0010)
     A* *DS3                                SFLLIN(0006)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20020524  102226  ROCKY       REL-V4R2M0  5769-PW1
     A  *DS3                                SFLLIN(0005)
     A                                      SFLSIZ(0072)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
417bMA  84                                  SFLEND(*MORE)
417dAA                                      SFLCSRRRN(&RTNRRN)
      *
640aAA                                      RTNCSRLOC(&CSRRCD &CSRFLD)
640aAA            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
640aAA            CSRFLD        10A  H      TEXT('Cursor field name')
      *
417dAA            RTNRRN         5S 0H      TEXT('Hidden: Subfile RRN')
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PO236.01'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'License Entry'
     A                                      DSPATR(HI)
     A                                  4  2'PO number . .'
     A            W1PO           9A  O  4 16
     A                                  4 60'Manual Put away'
     A                                  5  2'Warehouse . .'
     A            $PWHSE         3Y 0O  5 16EDTCDE(4)
     A                                  5 20'-'
     A            WHDESC        30A  O  5 22
650aAA            W1NOTE        15A  O  5 60DSPATR(HI)
     A            WOLBL         13A  O  6  2
     A            WOITEM        65A  O  6 16
     A            WOLBLS        13A  O  7  2
     A            WOSITM        65A  O  7 16
     A            WODSC1        40A  O  8 16
     A            WODSC2        40A  O  9 16
710aAA                                  9 60'Slot type:'
710aAA            WOSDEF         2A  O  9 71
SGCaAA            WOSPIN         4A  O  9 75
     A                                 10  2'Quantity received . . '
     A            W1QRC1         5Y 0O 10 25EDTCDE(4)
640aDA**          W1UM1          5A  O 10 32
640aMA            W1UM1          5A  O 10 31
640aDA**          W1QRC2         5Y 0O 10 39EDTCDE(4)
640aMA            W1QRC2         5Y 0O 10 38EDTCDE(4)
640aDA**          W1UM2          5A  O 10 46
640aMA            W1UM2          5A  O 10 44
640aDA**          W1QRC3         5Y 0O 10 53EDTCDE(4)
640aMA            W1QRC3         5Y 0O 10 51EDTCDE(4)
640aDA**          W1UM3          5A  O 10 60
640aMA            W1UM3          5A  O 10 57
640aAA                                 10 64'Status:'
640aAA            W1STAT         2A  B 10 72
640aAA N20N69                               DSPATR(HI)
640aAA  20                                  DSPATR(RI)
640aAA N20N69                               DSPATR(UL)
640aAA  02                                  DSPATR(PC)
DRYaAA  67
DRYaDA* 69                                  DSPATR(PR)
DRYaMAO 69                                  DSPATR(PR)
640aAA                                 10 75'(opt)'
VIP AA            W1MSG         78A    11  2
VIP AA  45                                  DSPATR(RI)
VIP AA N45                                  DSPATR(ND)
VIP AA  45                                  COLOR(RED)
     A                                 12  3'Quantity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 18'XDock'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 25'License'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 45'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 60'XDock'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 67'License'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R SCREEN2
     A*%%TS  SD  20010307  161902  DAVE3       REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CF07
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'PO236.02'
     A                                  1 12'RF Recv'
     A                                  2  1'P.O.'
     A            W2PO           9A  O  2  6
     A            W2LINE         5Y 0O  2 16EDTCDE(4)
     A            W2ITEM        65A  O  3  1
     A            W2PCTR         4Y 0O  4  1EDTCDE(4)
     A                                  4  6'of'
     A            W2PAL          4Y 0O  4  9EDTCDE(4)
     A                                  4 14'pallets'
     A                                  5  1'Qty'
     A            W2QTY          5Y 0B  5  5EDTCDE(4)
     A N32N69                               DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N69                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  69                                  DSPATR(PR)
     A            W2UM           2   O  5 11
     A            W2TIHI         7   O  5 14
     A                                  6  1'Lic'
416 DA*           W2LCNS         7Y 0B  6  5EDTCDE(4)
416  A            W2LCNS        15A  B  6  5
     A  33                                  DSPATR(RI)
     A N33N68N69                            DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  68                                  DSPATR(ND)
     A  69                                  DSPATR(PR)
     A                                      CHECK(ER)
     A N33N68N69                            DSPATR(HI)
416 DA*           W2XDK          3   O  6 14
416  A            W2XDK          3   O  6 22
     A                                  7  1'F7=Prev F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
650aDA*                                 1  2'F5=Refresh F8=Produce Labels +
650aDA*                                      F9=Off Site F12=Cancel'
650aMA N96                              1  2'F5=Refresh F8=Produce Labels +
650aMA                                       F9=Off Site F12=Cancel'
650aAA  96                              1  2'F5=Refresh F8=Produce Labels +
650aAA                                                   F12=Cancel'
     A                                      COLOR(BLU)
600aDA*                                 1  2'F5=Refresh  F12=Cancel'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS for SCREEN2
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC2                   SFL
     A*%%TS  SD  20010313  080513  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(08)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL2                   SFLCTL(MSGREC2)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*                                     OVERLAY
     A          R MSGCLR2
     A*%%TS  SD  20010313  080513  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(08)
     A                                      CLRL(01)
     A*****************************************************************
600aAA          R WEXTSTG
600aAA                                      WINDOW(5 15 8 26)
600aAA                                      WDWTITLE((*TEXT 'Off Site  +
600aAA                                        Storage') *LEFT)
600aAA                                  2  2'Storage'
600aAA            W3STG          1A  B  2 15
600bAA  95                                  DSPATR(PR)
600aAA                                  3  2'# of plates'
600aAA            W3PAL          4Y 0B  3 15EDTCDE(4)
600aAA                                  4  2'Printer'
600aAA            W3DEV         10A  B  4 15
600aAA  92                              6  2'too many pallets'
600aAA                                      DSPATR(RI)
600aAA  93                              6  2'Can not be zero'
600aAA                                      DSPATR(RI)
610cAA  94                              6  2'Storage is blank'
610cAA                                      DSPATR(RI)
600aAA                                  7  2'F12=Cancel'
600aAA                                      DSPATR(HI)
600aAA          R WDUMMY                     ASSUME
600aAA                                  1  2' '
     A*****************************************************************
610bAA          R WPRDLBL
610bAA                                      WINDOW(5 15 8 26)
610bAA                                      WDWTITLE((*TEXT 'Produce  +
610bAA                                        Label') *LEFT)
610bAA                                  2  2'# of labels'
610bAA            W4LQTY         4Y 0B  2 15EDTCDE(4)
610bAA                                  3  2'Printer'
610bAA            W4DEV         10A  B  3 15
610bAA  94                              6  2'Can not be zero'
610bAA                                      DSPATR(RI)
610bAA                                  7  2'F12=Cancel'
610bAA                                      DSPATR(HI)
