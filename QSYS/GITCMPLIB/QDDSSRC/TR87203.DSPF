     A*%%TS  SD  20141120  130753  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR87201   Trax Merged Routes maintenance - Change screen
     A*  29 December, 2011
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*    12/29/11  RBD  6.40
     A*      - Created.
     A*
650 AA*    09/11/14  RBD  6.50
     A*      - Changed screen format to TR87201.
     A*      - Added route lookup and selection.
     A*
650aAA*    11/18/14  RBD  6.50a
     A*      - Changed screen format to TR87202.
     A*      - Added multi route group Id for parent.
     A*      - Added multi route flag for children.
     A*
720aAA*    01/23/18  RBD  7.20a
     A*      - Changed screen format to TR87203.
     A*      - Commented out multi route group Id for parent
     A*        and multi route flag for children.
     A*
730 AA*    12/21/19  MLB  7.30
     A*      - Enh: Revised pgm to allow lowercase entry for route
     A*        number.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20141120  130753  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TR872.01'
     A                                  1 25'Trax Merged Routes Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse  . . . . . .'
     A            $PWHSE         3Y 0O  4 25EDTCDE(3)
     A                                  4 29'-'
     A            WHDESC        30A  O  4 31
     A                                  5  2'Parent Route . . . . .'
     A            W1PRTE         5A  O  5 25
     A                                  6  2'Parent Route Id  . . .'
     A            W1PRTI         5   O  6 25
720aDA**                                7  2'Multi Route Group Id .'
720aDA**          W1GGID         5Y 0O  7 25EDTCDE(4)
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11  4'Child Route  . . . . .'
     A            W1CRTE         5A  B 11 28
730 AA                                      CHECK(LC)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 12  4'Child Route Id . . . .'
     A            W1CRTI         5A  B 12 28
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
720aDA*           W1MFLG         1A  B 13 28
720aDA* 23                                  DSPATR(RI)
720aDA*N23N90                               DSPATR(HI)
720aDA*N23N90                               DSPATR(UL)
720aDA* 03                                  DSPATR(PC)
720aDA* 90                                  DSPATR(PR)
     A                                 23  2'F8=Rte Lookup  F12=Cancel'
     A                                      COLOR(BLU)
720aDA*                                13  4'Multi Route Y or N . .'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
