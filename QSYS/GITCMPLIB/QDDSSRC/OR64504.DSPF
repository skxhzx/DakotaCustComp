     A*%%TS  SD  20151029  020937  JEFF        REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  OR645FM      Reprint Route - Traffic Cop.
     A*  26 September 1990
     A*  Hemant Kapadia
     A*
     A********************************************************************
     A*
     A*  Revisions
     A*
     A*    09/26/90  HNK
     A*      - Created.
     A*
417 AA*    04/12/04  MLB  4.17
     A*      - Enh: Added new option to call Print Trans-
     A*        action audit report.
     A*        Printer device/Form type text and entry fields
     A*        moved down two lines.
     A*
510 AA*    03/31/07  DAS  5.10
     A*      - Enh: Added new option to call cw summary
     A*
520 AA*    03/26/09  CWM  5.20
     A*      - Enh: Added new functionality for Override Print file
     A*             processing.
640aAA*    03/02/11  JCJ  6.40a
     A*      - Enh: Renamed from OR645FM to OR64501 per GUI standards.
     A*             Added reprint by Transaction and Label Numbers.
     A*
640bAA*    06/29/11  JCJ  6.40b
     A*      - Enh: Renamed from OR64501 to OR64502 per GUI standards.
     A*             Added label range for reprinting by Label Number.
     A*
700aAA*    10/29/15  JCJ  7.00a
     A*      - Enh: Renamed from OR64502 to OR64503 per GUI standards.
     A*             Added pallet reprint options.
     A*
710aAA*    08/25/17  NLK  7.10a
     A*      - FIX: Renamed from OR64503 to OR64504 per GUI standards.
     A*         Shift middle columns to allow lbl/trax 10 long
     A*         Col 38 now 35; 48 now 47; rmv 35 dots all on Screen1
     A*         fields w1zone and w1zon2 was 7 long now 10
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20151029  011602  JEFF        REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OR645.01'
     A                                  1 34'Reprint Route'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'Warehouse  . . . . . . . . . .'
710aD ***         W1WHSE         3Y 0O  4 38EDTCDE(Z)
710aMM            W1WHSE         3Y 0O  4 35EDTCDE(Z)
     A                                  4 42'-'
     A            WHDESC        30A  O  4 44
     A                                  5  4'Route  . . . . . . . . . . . .'
710aD ***         W1RTE          5A  O  5 38
710aMA            W1RTE          5A  O  5 35
     A                                  5 47'Print'
     A                                      DSPATR(HI)
     A                                  6 35'Reprint'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA***                               7 48'Print Override'
     A                                  6 47'Override'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  4'Labels . . . . . . . . . . . .'
710aD ***         W1LBLS         1A  B  7 38
710aMA            W1LBLS         1A  B  7 35
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***                               8 65'Y=Yes, N=No'
     A                                  7 58'Y=Yes, N=No'
     A                                  8  6'Reprint Type . . . . . . . .'
710aD ***         W1TYPE         1A  B  8 38
710aMA            W1TYPE         1A  B  8 35
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Z=Zone, T=Tran, L=Label'
640aDA***                               9  6'Zone . . . . . . . . . . . .'
     A                                  9  6'Reprint Value  . . . . . . .'
640aDA***         W1ZONE         6A  B  9 38
710aD ***         W1ZONE         7A  B  9 38
710aMA            W1ZONE        10A  B  9 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***                               9 66'*ALL, Code'
710aD ***         W1ZON2         7A  B  9 48
710aMA            W1ZON2        10A  B  9 47
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640bDA***                              10 58'*ALL, Code, Trn#, Lbl#'
     A                                  9 58'*ALL, Zone, Trn#, Lbl#'
640bDA***                              11 59'F4=Prompt'
     A                                 10 59'range. F4=Prompt'
700aAA                                 11  4'Pallet Label Reprint Type. . .'
710aD ***         W1PLT          1   B 11 38
710aMA            W1PLT          1   B 11 35
700aAA N33N90                               DSPATR(HI)
700aAA  33                                  DSPATR(RI)
700aAA N33N90                               DSPATR(UL)
700aAA  13                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                 11 58'Z=Zone, T=Tran, P=Plt#'
700aAA                                 12  6'Reprint Value. . . . . . . .'
710aD ***         W1PTYP         7   B 12 38
710aMA            W1PTYP         7   B 12 35
700aAA N34N90                               DSPATR(HI)
700aAA  34                                  DSPATR(RI)
700aAA N34N90                               DSPATR(UL)
700aAA  14                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                 12 58'*All, F4=Prompt'
640aDA***                              11  4'Truck summary  . . . . . . . .'
     A                                 14  4'Truck summary  . . . . . . . .'
640aDA***         W1TRK          1A  B 11 38
710aD ***         W1TRK          1A  B 14 38
710aMA            W1TRK          1A  B 14 35
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W2TRK          1A  B 11 49DSPATR(HI)
710aD ***         W2TRK          1A  B 14 48DSPATR(HI)
710aMA            W2TRK          1A  B 14 47DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
640aDA***                              11 65'Y=Yes, N=No'
     A                                 14 58'Y=Yes, N=No'
640aDA***                              13  4'Truck pallets  . . . . . . . .'
     A                                 15  4'Truck pallets  . . . . . . . .'
640aDA***         W1TRPL         1A  B 13 38
710aD ***         W1TRPL         1A  B 15 38
710aMA            W1TRPL         1A  B 15 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W2TRPL         1A  B 13 49DSPATR(HI)
710aD ***         W2TRPL         1A  B 15 48DSPATR(HI)
710aMA            W2TRPL         1A  B 15 47DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  94                                  DSPATR(ND)
     A  94                                  DSPATR(PR)
640aDA***                              13 65'Y=Yes, N=No'
     A                                 15 58'Y=Yes, N=No'
640aDA***                              14  6'Detail or Summary  . . . . .'
     A                                 16  6'Detail or Summary  . . . . .'
640aDA***         W1DS           1A  B 14 38
710aD ***         W1DS           1A  B 16 38
710aMA            W1DS           1A  B 16 35
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***                              14 58'D=Detail, S=Summary'
     A                                 16 58'D=Detail, S=Summary'
     A                                 17 59'B=Both'
640aDA***                              16  4'Transaction audit  . . . . . .'
     A                                 18  4'Transaction audit  . . . . . .'
640aDA***         W1AUDT         1A  B 16 38
710aD ***         W1AUDT         1A  B 18 38
710aMA            W1AUDT         1A  B 18 35
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W2AUDT         1A  B 16 49DSPATR(HI)
710aD ***         W2AUDT         1A  B 18 48DSPATR(HI)
710aMA            W2AUDT         1A  B 18 47DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96                                  DSPATR(PR)
640aDA***                              16 65'Y=Yes, N=No'
     A                                 18 58'Y=Yes, N=No'
640aDA***                              18  4'Catchweight summary  . . . . .'
     A                                 19  4'Catchweight summary  . . . . .'
640aDA***         W1CSUM         1A  B 18 38
710aD ***         W1CSUM         1A  B 19 38
710aMA            W1CSUM         1A  B 19 35
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W2CSUM         1A  B 18 49DSPATR(HI)
710aD ***         W2CSUM         1A  B 19 48DSPATR(HI)
710aMA            W2CSUM         1A  B 19 47DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95                                  DSPATR(PR)
640aDA***                              18 65'Y=Yes, N=No'
     A                                 19 58'Y=Yes, N=No'
640aDA***                              20  4'Printer device . . . . . . . .'
     A                                 21  4'Printer device . . . . . . . .'
     A N59                                  DSPATR(ND)
640aDA***         W1DEV         10A  B 20 38
710aD ***         W1DEV         10A  B 21 38
710aMA            W1DEV         10A  B 21 35
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N59                                  DSPATR(ND)
640aDA***                              21  4'Form type  . . . . . . . . . .'
     A                                 22  4'Form type  . . . . . . . . . .'
     A N59                                  DSPATR(ND)
640aDA***         W1FORM        10A  B 21 38
710aD ***         W1FORM        10A  B 22 38
710aMA            W1FORM        10A  B 22 35
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N59                                  DSPATR(ND)
640aDA***                              23  2'F3=Exit  F4=Select  F5= Refresh  F-
640aDA***                                   12=Cancel'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
