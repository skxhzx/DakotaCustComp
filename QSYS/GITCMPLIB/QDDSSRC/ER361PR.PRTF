     A****************************************************************
     A*
     A* ER361PR   Management report for a selector (Detail)
     A* 16 March, 2000
     A* Hemant Kapadia
     A*
     A* 03.16.00  HNK  4.13
     A*  Created
     A*
     A* Note: Replaces ER271PR
     A*
     A*       Compile with 145  cpi
     A*
     A* Revisions:
     A*
     A* 10/09/09   GJA   5.30
     A* -Enh: cleaned up and revamped for suspended transactions
     A*
530a A* 08/24/10   GJA   5.30a
     A* -Enh: add IDLE time
     A*
530b A* 08/12/10   RH    5.30b
     A* -Enh: Add route and route id to report
     A****************************************************************
     A*
     A* Custom Client Revisions:  Crumbley
     A*
CPCa A* 05/04/11   GJA   CPCa
     A*   Enh:  Add goaltime to report
     A****************************************************************
     A*
     A* Custom Client Revisions:  Eastland
     A*
EFCa A* 11/18/11   GJA   EFCa
     A*   Enh:  Remove dept/zone column
     A*         Add order number and customer
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER361'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(001)
     A            TMERNG        22A  O    56
530aAA                                      SPACEA(003)
530aMA            MRGTXT        45A  O     2
     A*
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(PTEMP# ER361)
     A                                      EDTCDE(Z)
     A            WKNAME        40A       +1
     A  93                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     1'Dept/'
     A                                    54'Short'
     A                                    63'Start'
     A                                    73'Start'
     A                                    83'End '
     A                                    93'End '
     A                                   103'Direct'
530aAA                                   115'Idle'
     A                                      SPACEA(001)
     A*
     A                                     1'Zone '
     A                                      UNDERLINE
     A                                     8'Task  '
     A                                      UNDERLINE
     A                                    21'Transaction'
     A                                      UNDERLINE
     A                                    34'Pieces'
     A                                      UNDERLINE
     A                                    44'Cntr'
     A                                      UNDERLINE
     A                                    54'Cntr'
     A                                      UNDERLINE
     A                                    63'Date'
     A                                      UNDERLINE
     A                                    73'Time'
     A                                      UNDERLINE
     A                                    83'Date'
     A                                      UNDERLINE
     A                                    93'Time'
     A                                      UNDERLINE
     A                                   103'Min'
     A                                      UNDERLINE
     A                                   108'Sec'
     A                                      UNDERLINE
530aAA                                   115'Min'
530aAA                                      UNDERLINE
530aAA                                   120'Sec'
530aAA                                      UNDERLINE
530aAA                                   126'Route'
530aAA                                      UNDERLINE
530aAA                                   134'RteID'
530aAA                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A*
EFCaAA* Print column headings for Eastland
EFCaAA*
EFCaAA          R COLUMNSEFC
     A                                    60'Total'
     A                                    94'Short'
     A                                   103'Start'
     A                                   113'Start'
     A                                   123'End '
     A                                   133'End '
     A                                   143'Direct'
     A                                   155'Idle'
     A                                      SPACEA(001)
     A*
     A                                     1'Transaction'
     A                                      UNDERLINE
     A                                     9'Customer'
     A                                      UNDERLINE
     A                                    50'Order'
     A                                      UNDERLINE
     A                                    60'Order'
     A                                      UNDERLINE
     A                                    74'Pieces'
     A                                      UNDERLINE
     A                                    84'Cntr'
     A                                      UNDERLINE
     A                                    94'Cntr'
     A                                      UNDERLINE
     A                                   103'Date'
     A                                      UNDERLINE
     A                                   113'Time'
     A                                      UNDERLINE
     A                                   123'Date'
     A                                      UNDERLINE
     A                                   133'Time'
     A                                      UNDERLINE
     A                                   143'Min'
     A                                      UNDERLINE
     A                                   148'Sec'
     A                                      UNDERLINE
     A                                   155'Min'
     A                                      UNDERLINE
     A                                   160'Sec'
     A                                      UNDERLINE
     A                                   170'Route'
     A                                      UNDERLINE
     A                                   180'RteID'
     A                                      UNDERLINE
     A                                      SPACEA(002)
EFCaAA*
EFCaAA* Print detail line
EFCaAA*
EFCaAA          R DETAILEFC
     A                                      SPACEA(001)
     A            WKTRN#    R              1REFFLD(PTTRN# ER361)
     A                                      EDTCDE(Z)
     A            OHCNAM        40A        9
     A            OHCORD    R             50REFFLD(OHCORD ORDH)
     A            ORDPCS         6S 0     60EDTCDE(Z)
     A            W1PTOT    R             74REFFLD(PTPCS  ER361)
     A                                      EDTCDE(1)
     A            W1RECK         7S 0     81
     A                                      EDTCDE(3)
     A            W1QTYSA        7S 0     92
     A                                      EDTCDE(3)
     A            W1SDTE         6S 0    103
     A                                      EDTWRD('  /  /  ')
     A            W1STIM         6S 0    113
     A                                      EDTWRD('  :  :  ')
     A            W1EDTE         6S 0    123
     A                                      EDTWRD('  /  /  ')
     A            W1ETIM         6S 0    133
     A                                      EDTWRD('  :  :  ')
     A            W1MULT         1A      141
     A            W1DMIN         4S 0    143
     A                                      EDTCDE(4)
     A            W1DSEC         4S 0    148
     A                                      EDTCDE(4)
     A            W1IMIN         4S 0    155
     A                                      EDTCDE(4)
     A            W1ISEC         4S 0    160
     A                                      EDTCDE(4)
     A            W1RTE          5A      170
     A            W1RTID         5A      180
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKWHDP    R              1REFFLD(PTWHDP ER361)
     A            WKTASK    R              8REFFLD(PTTASK ER361)
     A            WKTRN#    R             21REFFLD(PTTRN# ER361)
     A                                      EDTCDE(Z)
     A            W1PTOT    R             34REFFLD(PTPCS  ER361)
     A                                      EDTCDE(1)
     A            W1RECK         7S 0     41
     A                                      EDTCDE(3)
     A            W1QTYSA        7S 0     52
     A                                      EDTCDE(3)
     A            W1SDTE         6S 0     63
     A                                      EDTWRD('  /  /  ')
     A            W1STIM         6S 0     73
     A                                      EDTWRD('  :  :  ')
     A            W1EDTE         6S 0     83
     A                                      EDTWRD('  /  /  ')
     A            W1ETIM         6S 0     93
     A                                      EDTWRD('  :  :  ')
     A            W1MULT         1A      101
     A            W1DMIN         4S 0    103
     A                                      EDTCDE(4)
     A            W1DSEC         4S 0    108
     A                                      EDTCDE(4)
530aAA            W1IMIN         4S 0    115
530aAA                                      EDTCDE(4)
530aAA            W1ISEC         4S 0    120
530aAA                                      EDTCDE(4)
530bAA            W1RTE          5A      126
530bAA            W1RTID         5A      134
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            T#PTOT         7S 0     31EDTCDE(1)
     A            T#CTOT         7S 0     39EDTCDE(1)
     A            T#STOT         7S 0     50EDTCDE(1)
     A                                    93'Hours'
     A                                      UNDERLINE
     A            T#DTOT         7S 2     98EDTCDE(1)
530aAA            T#ITOT         7S 2    111EDTCDE(1)
     A                                      SPACEA(001)
EFCaAA*
     A* Print totals
     A*
     A          R TOTAL1EFC
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            T#PTOT         7S 0     71EDTCDE(1)
     A            T#CTOT         7S 0     79EDTCDE(1)
     A            T#STOT         7S 0     90EDTCDE(1)
     A                                   135'Hours'
     A                                      UNDERLINE
     A            T#DTOT         7S 2    143EDTCDE(1)
     A            T#ITOT         7S 2    155EDTCDE(1)
     A                                      SPACEA(001)
CPCa A* Print column headings
     A*
     A          R COLUMNSC
     A                                     1'Dept/'
     A                                    54'Short'
     A                                    63'Start'
     A                                    73'Start'
     A                                    83'End '
     A                                    93'End '
     A                                   103'Direct'
530aAA                                   115'Idle'
     A                                      SPACEA(001)
     A*
     A                                     1'Zone '
     A                                      UNDERLINE
     A                                     8'Task  '
     A                                      UNDERLINE
     A                                    21'Transaction'
     A                                      UNDERLINE
     A                                    34'Pieces'
     A                                      UNDERLINE
     A                                    44'Cntr'
     A                                      UNDERLINE
     A                                    54'Cntr'
     A                                      UNDERLINE
     A                                    63'Date'
     A                                      UNDERLINE
     A                                    73'Time'
     A                                      UNDERLINE
     A                                    83'Date'
     A                                      UNDERLINE
     A                                    93'Time'
     A                                      UNDERLINE
     A                                   103'Min'
     A                                      UNDERLINE
     A                                   108'Sec'
     A                                      UNDERLINE
530aAA                                   115'Min'
530aAA                                      UNDERLINE
530aAA                                   120'Sec'
530aAA                                      UNDERLINE
     A                                   134'Goal'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1C
     A                                      SPACEA(001)
     A            WKWHDP    R              1REFFLD(PTWHDP ER361)
     A            WKTASK    R              8REFFLD(PTTASK ER361)
     A            WKTRN#    R             21REFFLD(PTTRN# ER361)
     A                                      EDTCDE(Z)
     A            W1PTOT    R             34REFFLD(PTPCS  ER361)
     A                                      EDTCDE(1)
     A            W1RECK         7S 0     41
     A                                      EDTCDE(3)
     A            W1QTYSA        7S 0     52
     A                                      EDTCDE(3)
     A            W1SDTE         6S 0     63
     A                                      EDTWRD('  /  /  ')
     A            W1STIM         6S 0     73
     A                                      EDTWRD('  :  :  ')
     A            W1EDTE         6S 0     83
     A                                      EDTWRD('  /  /  ')
     A            W1ETIM         6S 0     93
     A                                      EDTWRD('  :  :  ')
     A            W1MULT         1A      101
     A            W1DMIN         4S 0    103
     A                                      EDTCDE(4)
     A            W1DSEC         4S 0    108
     A                                      EDTCDE(4)
530aAA            W1IMIN         4S 0    115
530aAA                                      EDTCDE(4)
530aAA            W1ISEC         4S 0    120
530aAA                                      EDTCDE(4)
     A            W1GLTM         5S 3    134
     A                                      EDTCDE(4)
     A*
     A* Print totals
     A*
     A          R TOTAL1C
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            T#PTOT         7S 0     31EDTCDE(1)
     A            T#CTOT         7S 0     39EDTCDE(1)
     A            T#STOT         7S 0     50EDTCDE(1)
     A                                    93'Hours'
     A                                      UNDERLINE
     A            T#DTOT         7S 2     98EDTCDE(1)
530aAA            T#ITOT         7S 2    111EDTCDE(1)
     A            T#GLTOT        7S 3    134EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER361: End of Report '
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER361: No records found'
     A*
