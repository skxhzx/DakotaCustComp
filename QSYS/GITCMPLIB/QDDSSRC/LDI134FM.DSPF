     A*%%TS  SD  19990726  052348  DAVE        REL-V4R2M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SCREEN1
     A*%%TS  SD  19990726  052348  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LDI134.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 10 35'Slot:'
     A            W1SLOT        12A  O 10 41
     A                                  1 22'Delete Slots Not Found In LAYSLOT -
     A                                      File'
     A                                      DSPATR(HI)
     A                                 12 34'Found:'
     A            W1FND          5Y 0O 12 41EDTCDE(Z)
     A                                  9 35'Dept:'
     A            W1WHDP         5A  O  9 41
     A                                 13 32'Deleted:'
     A            W1DEL          5Y 0O 13 41EDTCDE(Z)
     A                                 14 32'Skipped:'
     A            W1SKP          5Y 0O 14 41EDTCDE(Z)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
