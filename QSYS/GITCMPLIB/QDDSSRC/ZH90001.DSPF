     A*%%TS  SD  20091109  103306  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. ZH19001 COPY AND NAME ZH19002
     A*----------------------------------------------------------------
     A*
     A*  ZH190FM  Zone Lookup
     A*  09 November 2009
     A*  Jeffrey Jahnke
     A*
     A*  Revisions:
     A*
530 AA*    11/09/09  JCJ  5.30
     A*      - Moved POSTNREC/OPTREC into DSPSFC.
     A*        Re: Webfacing had problem with double read.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970611  082506  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'ZH900.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 35'Zone Lookup'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19970611  082506  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  2 48'Position to zone . . . .'
     A            PTINP1         6A  B  2 74
     A            WHCODE         3Y 0O  2  3EDTCDE(4)
     A                                  2  7'-'
     A            WHDESC        30A  O  2  9
     A            FLD005         1A  O  1  2
     A            WOTRUK        43   O  3  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970611  082506  DAVE        REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ZHZONE    R        O 11  7REFFLD(ZHREC/ZHZONE *LIBL/ZONEH)
     A            ZHDESC    R        O 11 15REFFLD(ZHREC/ZHDESC *LIBL/ZONEH)
     A            ZHCOMP    R        O 11 52REFFLD(ZHREC/ZHCOMP *LIBL/ZONEH)
     A            ZHPZON    R        O 11 60REFFLD(ZHREC/ZHPZON *LIBL/ZONEH)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970611  082506  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            FLD005         1A  O  2  2
     A            WHCODE         3Y 0O  4  3EDTCDE(4)
     A                                  4  7'-'
     A            WHDESC        30A  O  4  9
     A                                  4 48'Position to zone . . . .'
     A            PTINP1         6A  B  4 74
     A            WOTRUK        43   O  5  3
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10 15'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Zone  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Compartment'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 47'Truck'
     A                                      DSPATR(HI)
     A                                 10 60'Zone  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 60'Parent'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
