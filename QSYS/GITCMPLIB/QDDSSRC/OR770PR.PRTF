     A****************************************************************
     A*
     A* OR770PR   Host pick Will Call Exception report.
     A* 04 April 2000
     A* Max Blacknik
     A*
     A****************************************************************
     A*  Revisions:
     A*
520 AA*  01/29/09  CWM  5.20
     A*    - Enh: Changed stop to 3 digit from 2 digit.
     A*
650 AA*    05/29/13  RBD  6.50
     A*      - Enh: Changed stop from 3 num to 9 char stop/drop display.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            RDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            TITLE         40A  O    21
     A*                                                                )
     A                                    72
     A                                      'Page'
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    26
     A                                    76
     A                                      'OR770'
     A*
     A            BATCH         30A  O    26
     A                                      SPACEB(001)
     A            $RTYPE         8A  O    73
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(002)
     A                                    60
     A                                      'Quantity'
     A                                    71
     A                                      'Quantity'
     A                                      SPACEA(001)
     A                                     1
     A                                      'Stop'
650 DA*                                    7
650 MA                                    11
     A                                      'Customer'
650 DA*                                   19
650 MA                                    23
     A                                      'Name'
     A                                    60
     A                                      'Ordered'
     A                                    71
     A                                      'Picked'
     A*
     A* Print detail line  (For Detail Report only)
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
520 DA*           OHSTOP    R        O     1
520 DA*                                     REFFLD(OHSTOP ORDH)
650 DA*           DSPSTP         3S 0O     1
650 MA            DSPSTP         9A  O     1
650 DA*                                     EDTCDE(Z)
650 DA*           CUST#         11A  O     7
650 MA            CUST#         11A  O    11
650 DA*           CSNAME        25A  O    19
650 MA            CSNAME        25A  O    23
     A*
     A*  Print item detail (For Detail Report only)
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            ODITEM    R        O    12
     A                                      REFFLD(ODITEM ORDD)
     A            ITDESC    R        O    28
     A                                      REFFLD(ITDESC PIRITEM)
     A            P1QORD        10A  O    60
     A            P1QPCK        10A  O    71
     A*
     A*  Print item pack description (For Detail Report only)
     A*
     A          R DETAIL3
     A                                      SPACEB(001)
     A            ITPDSC    R        O    28
     A                                      REFFLD(ITPDSC PIRITEM)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                     1
     A                                      'OR770: End of Report '
     A*
