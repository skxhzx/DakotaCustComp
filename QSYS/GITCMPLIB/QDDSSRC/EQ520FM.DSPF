     A*%%TS  SD  20040218  141441  BFC2        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EQ520FM   Equipment work order detail (one whse) - Display
     A*  01 July, 2003
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
417c A*    07/24/03  RH   4.17c
     A*      - Put Eq ID on screen.
417d A*    02/18/04  RH   4.17d
     A*      - Add Original work order to display.
417e A*    04/29/04  RH   4.17e
     A*      - Increase EQ ID code from 6 to 8.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*                                     CA04
     A          R TITLE
     A*%%TS  SD  19940706  172535  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'EQ520.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 25'Equipment Work Order Detail     '
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940711  163426  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            PTDMY1        78A  O  1  2
     A            $KWHSE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*                                 2 48'Change to warehouse . . .'
     A*           PTINP1         3Y 0B  2 75EDTCDE(4)
     A* 21                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A                                  3  2'WORK ORDER #:'
417d A*           WOWRK#         3Y 0O  3 17EDTCDE(3)
417d A            WOWRK#         7Y 0O  3 17EDTCDE(3)
417c A                                  3 50'Equipment ID:'
417eDA*           WOCODE         6A  O  3 65
417e A            WOCODE         8A  O  3 65
     A*                                 3 48'Position to work order. .'
     A*           PTINP2         6A  B  3 75
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20040218  141441  BFC2        REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*           EWWRK#    R        O 11  7REFFLD(EWREC/EWWRK# *LIBL/EQUIPW)
     A*                                     EDTCDE(3)
     A*           W1GDTE         8A  O 11 24
     A*           EWETRN    R        O 11 35REFFLD(EWREC/EWETRN *LIBL/EQUIPW)
     A*                                     EDTCDE(3)
     A**          EWPDTE    R        O 11 50REFFLD(EWREC/EWPDTE *LIBL/EQUIPW)
     A            W1QSTN    R        O 11  7REFFLD(EAQSTN EQUIPA)
     A            W1ANSR    R        O 11 39REFFLD(EAANSR EQUIPA)
     A            W1BY          20A  O 11 42
     A*           W1CDTE         6Y 0O 11 68EDTCDE(Y)
     A            W1CDTE         8A  O 11 64
     A*           W1CDTE         6Y 0O 11 68EDTWRD('  /  /  ')
     A*           W1CDTE         6Y 0O 11 68
417d A            W1OWK#    R        O 11 74REFFLD(EAWRK# EQUIPA)
417d A                                      EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20040218  141441  BFC2        REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Exception Questions      / Answer'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Completed by        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417d A                                 10 74'Org WK#'
417d A                                      DSPATR(HI)
417d A                                      DSPATR(UL)
417a A*                                10 63'Complete'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
     A            FLD001        78A  O  9  2
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F9=Print All   F12=Cancel'
     A                                      COLOR(BLU)
