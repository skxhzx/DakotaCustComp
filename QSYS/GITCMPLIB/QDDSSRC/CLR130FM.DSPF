     A*%%TS  SD  20010207  163752  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI210FM    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010207  163752  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'CLR130.01'
     A                                  1 27'Clear Files for Warehouse 1'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'*** USE WITH EXTREME CAUTION ***'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Y=Yes, N=No'
     A                                 12 11'This program will clear warehouse -
     A                                      1 records from the FS-WMS'
     A                                 13 11'files in which records are created-
     A                                       during processes such as'
     A                                 14 11'receiving and selection. Examples -
     A                                      of these files are LABEL,'
     A                                 15 11'PIRTRAN, ORDH, ORDD, etc ... Files-
     A                                       that are NOT cleared are'
     A                                 16 11'the ones that contain more static -
     A                                      type information such as'
     A                                 17 11'PIRITEM, ITEMQTY, SLOT, etc ... Al-
     A                                      so, sequence numbers used'
     A                                 18 11'internally are set to 1. These wou-
     A                                      ld include Label #,'
     A                                 19 11'Transaction #, etc ...'
     A                                 20 11'See program CLR135 for the actual -
     A                                      files and sequence numbers.'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
