     A*%%TS  SD  19940916  162131  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP010FM   PIR options - Select warehouse
     A*  13 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940916  162131  DAVE        REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTADJ.01'
     A                                  1 31'Test ADJQTY Program'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Command  . . . . . . .'
     A            W1OPT          1A  B  5 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(HI)
     A                                  5 53'1 = *STOCK'
     A                                  5 66'2 = *UNSTOCK'
     A                                  6 53'3 = *ALLOC'
     A                                  6 66'4 = *UNALLOC'
     A                                  8 53'7 = *PICK'
     A                                  9 53'8 = *AVAIL'
     A                                 12  4'Type . . . . . . . . .'
     A            W1TYPE         1A  B 12 28
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                 12 53'1 = *INTER, 2=*BATCH'
     A                                 13  4'Warehouse  . . . . . .'
     A            $PWHSE         3Y 0B 13 28EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                 14  4'Item . . . . . . . . .'
     A            $PITEM        15A  B 14 28
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 15  4'Normal qty . . . . . .'
     A            $PQTY1         7Y 0B 15 28EDTCDE(P)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A                                 16  4'Breakdown 1 qty  . . .'
     A            $PQTY2         7Y 0B 16 28EDTCDE(P)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A                                 17  4'Breakdown 2 qty  . . .'
     A            $PQTY3         7Y 0B 17 28EDTCDE(P)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A                                 18  4'Return normal qty  . .'
     A            $PRTQ1         7Y 0O 18 28EDTCDE(P)
     A                                 19  4'Return brk1 qty  . . .'
     A            $PRTQ2         7Y 0O 19 28EDTCDE(P)
     A                                 20  4'Return brk2 qty  . . .'
     A            $PRTQ3         7Y 0O 20 28EDTCDE(P)
     A                                 21  4'Return code  . . . . .'
     A            $PRTN          8A  O 21 28
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =View quantities  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 53'5 = *HOLD'
     A                                  7 66'6 = *RELEASE'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
