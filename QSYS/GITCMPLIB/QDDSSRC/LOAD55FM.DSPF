     A*%%TS  SD  20090128  101429  JOSED       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  LOAD55FM  Loader Module: Close Open Trailer Equipment
     A*  October 29, 2008
     A*  Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/15/11  JGD  6.40
     F*      - Created
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA09
     A                                      CA12
     A                                      CA21
     A          R TITLE
     A*%%TS  SD  20090128  101429  JOSED       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  1  3'Loadr55.01'
     A                                      DSPATR(HI)
     A                                  1 22'Adjust/Closed Trailer Equipment Re-
     A                                      turns'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20081029  163400  JOSED       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                      DSPATR(HI)
     A                                  2  6'-'
     A                                      DSPATR(HI)
     A            WHDESC        30A  O  2  8DSPATR(HI)
     A                                  3 42'Position to trailer # . .'
     A            PTINP1         6   B  3 68DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      CHECK(ER)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20081106  152949  JOSED       REL-V5R4M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A            W1TRL#         8A  O 11  7
     A            W1RTE#         5A  O 11 17
     A            W1RTEI         5A  O 11 24
     A            W1STAT         4A  O 11 31
     A            W1LDTE         6Y 0O 11 37EDTWRD('  /  /  ')
     A            W1LTIM         6Y 0O 11 47EDTWRD('  :  :  ')
     A            W1UDTE         6  0O 11 57EDTWRD('  /  /  ')
     A            W1UTIM         6  0O 11 67EDTWRD('  :  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20081106  152332  JOSED       REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 39'Load'
     A                                      DSPATR(HI)
     A                                  9 49'Load'
     A                                      DSPATR(HI)
     A                                  9 58'Return'
     A                                      DSPATR(HI)
     A                                  9 68'Return'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'RteId'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Trailer#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Stat'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 57'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
