     A*%%TS  SD  20160927  082004  THOMAS      REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
610bAA* *******************************************
610bAA* *****  RSTDSP value must be set *YES  *****
610bAA* *******************************************
     A*
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A********************************************************************
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  LT210FM      RF License Tracking
     A*  11 December 2003
     A*  David Sommerville
     A*
     A*  Revisions
     A*
417 AA*    12/11/03  DAS  4.17
     A*      - Created.
     A*
500 AA*    08/28/04  DAS  5.00
     A*      - Made revisions to screens for pulling.
     A*
500aAA*    01/11/05  DAS  5.00a
     A*      - Added SCREEN29.
     A*
500bAA*    02/15/05  DAS  5.00b
     A*      - Added SCREEN30.
     A*
500cAA*    03/25/05  DAS  5.00c
     A*      - Added F9=Skip to SCREEN01
     A*      - Added CA10 to SCREEN02D
     A*
500dAA*    04/13/05  DAS  5.00d
     A*      - Revised SCREEN02D (Search other pallets). Changed it
     A*        from asking for pallet position to just asking a Y/N
     A*        question.
     A*      - Because of above change, removed F10=Excpetion from
     A*        SCREEN02D. Answering No will create the exception.
     A*
610aAA*    08/27/09  DAS  6.10a
     A*      - Added F9 to screen 3.
     A*
610bAA*    11/01/10  DAS  6.10b
     A*      - Discovered RSTDSP field must be set to *YES.
     A*        Re: Running fill screen transfer would blow up when
     A*        trying to display screen 21, but only using iSeries access.
650AAA*    01/18/13  RH   6.50A
     A*      - FIX: LT210.21 LICENSE SCANN DIDN'T HAVE CHECK ER
700aAA*    06/29/16  LMC  7.00a
     A*      - RESERVED
     A*
700bAA*    09/15/16  TAB  7.00b
     A*      - Added new screen for F8 override SCREEN03B
     A*      - Added F8 key to SCREEN03
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN01
     A*%%TS  SD  20160916  080039  THOMAS      REL-V7R2M0  5770-WDS
     A                                      CA12
700bAA  60                                  CA08
     A                                      CA09
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.01'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A            W1PLOC        10A  O  3 28DSPATR(ND)
     A            W1PPOS         2S 0O  3 39DSPATR(ND)
     A            WRNTXT        40A  O  4  1DSPATR(HI)
     A  91                                  DSPATR(ND)
     A                                  5  1'Pull'
     A            W1PULL        25A  O  5  6
     A                                  6  1'Press Enter to Continue'
500cDA**                                7  1'F12=Cancel'
500cMA                                  7  1'F9=Skip F12=Cancel'
     A                                      COLOR(BLU)
700bAA                                  7 20'F8=Chg Lcns'
700bAA                                      COLOR(BLU)
700bAA N60                                  DSPATR(ND)
     A          R SCREEN02A
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.02A'
     A            HEAD1         20   O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Is item on pallet?'
     A            W2OPT          1   B  5 21
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  5 24'(Y,N)'
     A                                  6  1'UPC:'
415aMA            W2UPC         50A  B  6  6
218 AA                                      CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN02B
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.02B'
     A            HEAD1         20   O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  3 29'Item pos:'
     A            W1IPOS         2  0O  3 39
     A                                  5  1'Is item on'
     A            W1ISON        16   O  5 12
     A            W2OPT          1   B  5 30
     A                                      CHECK(ER)
     A                                  5 33'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN02C
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.02C'
     A            HEAD1         20   O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  3 29'Item pos:'
     A            W1IPOS         2  0O  3 39
     A                                  5  1'Is item on a previous pallet?'
     A            W2OPT          1   B  5 32
     A                                      CHECK(ER)
     A                                  5 35'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN02D
     A*%%TS  SD  20040831  172117  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
500dDA**                                    CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.02D'
     A            HEAD1         20A  O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  4  1'Search other pallets for item.'
500dDA**                                5  1'If found, enter pallet position ..'
500dMA                                  5  1'Was item found?'
500dDA**          W2DPOS         2Y 0B  5 36EDTCDE(4)
500dMA            W2DOPT         1   B  5 17
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      DSPATR(HI)
500dDA**                                6  1'If not found, press F10.'
500dDA**                                7  1'F12=Cancel F10=Exception'
500dMA                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN03
     A*%%TS  SD  20160927  082004  THOMAS      REL-V7R2M0  5770-WDS
     A                                      CA12
     A                                      CA09
700bAA  60                                  CA08
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.03'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W3SLOT        15A  O  3  7
     A            W3PLOC        10A  O  3 28DSPATR(ND)
     A            W3PPOS         2S 0O  3 39DSPATR(ND)
     A            WRNTXT        40A  O  4  1DSPATR(HI)
     A  91                                  DSPATR(ND)
     A                                  5  1'Pull license'
     A            W3LDSP        15A  O  5 14
     A                                  6  1'Scan to confirm:'
     A            W3LIC         15A  B  6 18CHECK(ER)
     A                                  7  1'F12=Cancel F10=Excp F9=Skip'
     A                                      COLOR(BLU)
700bAA                                  7 29'F8=Chg Lcns'
700bAA                                      COLOR(BLU)
700bAA N60                                  DSPATR(ND)
     A          R SCREEN03A
     A*%%TS  SD  20040831  161030  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.03A'
     A            HEAD1         20A  O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  5  1'Is item on pallet?'
     A            W3OPT          1A  B  5 21
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  5 24'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
700bAA          R SCREEN03B
700bAA*%%TS  SD  20160916  122720  THOMAS      REL-V7R2M0  5770-WDS
700bAA                                      CA12
700bAA  98                                  ALARM
700bAA                                      CLRL(07)
700bAA                                      CSRLOC(WOROW      WOCOL)
700bAA                                      OVERLAY
700bAA            WOROW          3S 0H
700bAA            WOCOL          3S 0H
700bAA                                  1  2'LT210.03'
700bAA            HEAD1         20A  O  1 11
700bAA            HEAD2         65A  O  2  1
700bAA            W3PLOC        10A  O  3 28DSPATR(ND)
700bAA            W3PPOS         2S 0O  3 39DSPATR(ND)
700bAA            WRNTXT        40A  O  4  1DSPATR(HI)
700bAA  91                                  DSPATR(ND)
700bAA                                  6  1'Scan Chg Lcns:'
700bAA            W3BLIC        15A  B  6 16CHECK(ER)
700bAA                                  7  1'F12=Cancel'
700bAA                                      COLOR(BLU)
     A          R SCREEN04
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.04'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'No more pallet positions.'
     A                                  6  1'Press F10 to log exception.'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 12'F10=Excp'
     A                                      COLOR(BLU)
     A          R SCREEN05
     A*%%TS  SD  20090227  055736  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.05'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  3 31'Pulled:'
     A                                      DSPATR(ND)
     A            W4PULL         2   O  3 39DSPATR(ND)
     A                                  5  1'Any pallets to put back?'
     A            W4OPT          1A  B  5 27CHECK(ER)
     A                                  5 31'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN06
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.06'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Is slot empty?'
     A            W4OPT          1   B  5 17
     A                                      CHECK(ER)
     A                                  5 21'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN07
     A*%%TS  SD  20090227  055736  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.07'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  3 31'Pulled:'
     A                                      DSPATR(ND)
     A            W4PULL         2   O  3 39DSPATR(ND)
     A                                  5  1'Put pallets back into slot.'
     A                                  6  1'Press Enter to continue.'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN08
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.08'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Scan a license on the pallet being'
     A                                  6  1'put back into slot:'
     A            W3LIC         15   B  6 21
     A                                      CHECK(ER)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 12'F10=Excp'
     A                                      DSPATR(BL)
     A                                      COLOR(BLU)
     A          R SCREEN09
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.09'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Confirm item not found?'
     A            W4OPT          1   B  5 26
     A                                      CHECK(ER)
     A                                  5 30'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN21
     A*%%TS  SD  20090227  060548  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA12
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.21'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A            W21DESC       40A  O  4  1
     A                                  5  1'Assign new license'
     A            W21LIC        15A  B  5 20
     A                                      CHECK(ER)
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  6  1'quantity going into slot.'
     A***                               6  1'License is for:'
     A            W21PSPLIT      1A  B  1 22DSPATR(ND)
     A                                      CHECK(ER)
     A  92                                  DSPATR(PR)
     A N22                                  DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  22                                  DSPATR(RI)
     A****                              6 20'1=Slot 2=Leftover'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 12'F10=Excp'
     A                                      DSPATR(BL)
     A                                      COLOR(BLU)
     A                                  5 36'to'
     A          R SCREEN22
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.22'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Merge item onto existing pallet?'
     A            W22OPT         1   B  5 34
     A                                      CHECK(ER)
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  5 36'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN23
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.23'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Put pallet into slot.'
     A                                  6  1'Press Enter to continue.'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN24
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.24'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Pull'
     A            W24PULL       25   O  5  6
     A                                  6  1'Press Enter to Continue'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN25
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.25'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Merging item onto'
     A            W25POS        13   O  5 19
     A            W25OPT         1   B  5 34
     A                                      CHECK(ER)
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  5 36'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN26
     A*%%TS  SD  20090227  055736  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.26'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  3 31'Pulled:'
     A                                      DSPATR(ND)
     A            W4PULL         2   O  3 39DSPATR(ND)
     A                                  5  1'Put pallets back into slot.'
     A                                  6  1'Press Enter to continue.'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN27
     A*%%TS  SD  20031212  111703  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.27'
     A            HEAD1         20   O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15   O  3  7
     A                                  5  1'Scan a license on the pallet being'
     A                                  6  1'put back into slot:'
     A            W27LIC        15   B  6 21
     A                                      CHECK(ER)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 12'F10=Excp'
     A                                      DSPATR(BL)
     A                                      COLOR(BLU)
     A          R SCREEN28
     A*%%TS  SD  20031213  075655  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.28'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  5  1'No more pallet positions.'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN29
     A*%%TS  SD  20050111  075017  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.029'
     A            HEAD1         20A  O  1 12
     A            HEAD2         65A  O  2  1
     A                                  3  1'Slot:'
     A            W1SLOT        15A  O  3  7
     A                                  5  1'Pallet position:'
     A            W29POS         2Y 0B  5 18EDTCDE(4)
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      DSPATR(HI)
     A                                  6  1'(1=Front position)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
500bAA          R SCREEN30
     A*%%TS  SD  20050215  155612  DAVE        REL-V5R2M0  5722-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT210.30'
     A            HEAD1         20A  O  1 11
     A            HEAD2         65A  O  2  1
     A                                  4  1'Scan license to assign to'
     A                                  5  1'pulled product:'
     A            W30LIC        15A  B  5 17
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A**                                    CLRL(1)
     A                                      CLRL(17)
