     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR255PR    FTP Import Repack Item Demand - Exceptions
     A*  16 September 2010
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
530 AA*    09/16/10  MLB  5.30
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A            RDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A            RTIME          6S 0O     1
     A                                      EDTWRD('0  :  :  ')
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'WR255'
     A                                      SPACEB(002)
     A                                     2
     A                                      'Item           '
     A                                    19
     A                                      'Pack Size'
     A                                    35
     A                                      'Description'
     A                                    66
     A                                      'Demand'
     A                                    90
     A                                      'Message'
     A                                      SPACEB(001)
     A                                     2
     A                                      ' '
     A*
     A*%%***********************************************************************
     A          R DETAIL
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            RDIITM    R        O     2
     A                                      REFFLD(RDIITM *LIBL/IMPFRPKDM)
     A            ITPDSC    R        O    19
     A                                      REFFLD(ITPDSC *LIBL/PIRITEM)
     A            ITDESC    R        O    35
     A                                      REFFLD(ITDESC *LIBL/PIRITEM)
     A            RDIRPD    R        O    67
     A                                      REFFLD(RDIRPD *LIBL/IMPFRPKDM)
     A                                      EDTCDE(L)
     A            DETMSG        40A  O    90
     A*
     A*%%***********************************************************************
     A          R GRNDTTL
     A*%%***********************************************************************
     A                                     1
     A                                      SPACEB(002)
     A*
     A                                      'Grand Totals'
     A                                      UNDERLINE
     A                                     4
     A*
     A                                      SPACEB(002)
     A                                      'Total items accepted  :'
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Invalid item numbers  :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Item not a repack item:'
     A                                      SPACEB(001)
     A            DEMERR         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Repack demand <= zero :'
     A                                      SPACEB(001)
     A            NOTRPK         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A*%%***********************************************************************
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('WR255: End of Report ')
     A*
