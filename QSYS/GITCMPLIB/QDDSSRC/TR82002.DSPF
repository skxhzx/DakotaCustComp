     A*%%TS  SD  20111102  152344  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  TR820FM  Tracker - Returns (One whse) - Display
     A*  02 Feb 2010
     A*  Retha Davis
     A*
     A*  Revisions:
     A*
530 AA*    02/02/10  RBD  5.30
     A*      - Created.
     A*
640 AA*    11/02/11  RBD  6.40
     A*      - Added display of upload date.
     A*
640aAA*    06/28/12  RBD  6.40a
     A*      - Fix: Changed format name to TR82001, should have
     A*        done so at 640 when I changed the display.
     A*
740a A*    09/15/20  GJA  7.40a
     A*      - Enh: Add route start date to TR82002   ould have
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970617  143050  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'TR820'
     A                                  1 29'Work with Trax Returns'
     A                                      DSPATR(HI)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Position to route . . . .'
     A            PTINP2         5A  B  3 75
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20111102  152344  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHTRUK    R        O 11 14REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1USER        10A  O 11 26
     A            W1PCS          6Y 0O 11 39EDTCDE(3)
740aAA            W1SDTE         6Y 0O 11 48EDTCDE(Y)
740aDA***         W1UDTE         6Y 0O 11 48EDTCDE(Y)
     A            TTRTID    R        O 11 58REFFLD(RHREC/RHRTID *LIBL/RTEHED)
740aMA            W1UDTE         6Y 0O 11 67EDTCDE(Y)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20111102  152344  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
     A                                  9 39'Pieces'
     A                                      DSPATR(HI)
740aAA                                  9 50'Date'
740aAA                                      DSPATR(HI)
740aDA***                               9 50'Date'
740aMA                                  9 69'Date'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Driver    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 38'Uploaded'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
740ADA***                              10 48'Uploaded'
740AMA                                 10 48'Delivery'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
740AMA                                 10 67'Uploaded'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press F10 to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
