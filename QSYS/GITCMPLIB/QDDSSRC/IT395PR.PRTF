     A*%%***********************************************************************
     A*
     A*   IT395PR  Expired items report
     A*   19 October, 2011
     A*   Ginger Andrews
     A*
     A*  Revisions:
     A*    10/19/11  GJA  GFC
     A*      - Created.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +46
     A                                      DFT('Expired Items Report')
     A                                   142
     A                                      DFT('Page')
     A            PAGE           4S 0O   146
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   145
     A                                      DFT('IT395')
     A                                      SPACEA(1)
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                   113
     A                                      DFT('Rotation')
     A                                   123
     A                                      DFT('Usr')
     A                                   129
     A                                      DFT('Sell by')
     A                                   143
     A                                      DFT('Exp')
     A                                   167
     A                                      DFT('Expire')
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Buy')
     A                                      UNDERLINE
     A                                     5
     A                                      DFT('Vend')
     A                                      UNDERLINE
     A                                    21
     A                                      DFT('Prod#')
     A                                      UNDERLINE
     A                                    37
     A                                      DFT('Typ')
     A                                      UNDERLINE
     A                                    41
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    71
     A                                      DFT('Unit')
     A                                      UNDERLINE
     A                                    76
     A                                      DFT('Size')
     A                                      UNDERLINE
     A                                    91
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                   102
     A                                      DFT('LotQty')
     A                                      UNDERLINE
     A                                   117
     A                                      DFT('Date')
     A                                      UNDERLINE
     A                                   123
     A                                      DFT('Day')
     A                                      UNDERLINE
     A                                   130
     A                                      DFT('Date')
     A                                      UNDERLINE
     A                                   136
     A                                      DFT('Days')
     A                                      UNDERLINE
     A                                   145
     A                                      DFT('Days')
     A                                      UNDERLINE
     A                                   151
     A                                      DFT('License')
     A                                      UNDERLINE
     A                                   167
     A                                      DFT('Date    ')
     A                                      UNDERLINE
     A                                   178
     A                                      DFT('Pick Slot')
     A                                      UNDERLINE
     A                                   192
     A                                      DFT('Lot Code')
     A                                      UNDERLINE
     A                                   223
     A                                      DFT('Weekly Movement')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            IMBNBR    R        O     1REFFLD(IMBNBR ITEMMSC)
     A                                      EDTCDE(Z)
     A            ITVEN#    R        O     5REFFLD(ITVEN# PIRITEM)
     A                                      EDTCDE(Z)
     A            LAITEM    R        O    21REFFLD(LAITEM LICACTIVE)
     A            PICKTYP        3A  O    37
     A            ITDESC    R        O    41REFFLD(ITDESC PIRITEM)
     A            ITUM1     R        O    72REFFLD(ITUM1 PIRITEM)
     A            ITPDSC    R        O    75REFFLD(ITPDSC PIRITEM)
     A            LADISP    R        O    91REFFLD(LADISP LICACTIVE)
     A            QTAVL         10A  O   104
     A            W1EXPD         6S 0O   115
     A                                      EDTWRD('  /  /  ')
     A            IMUDAY    R        O   123REFFLD(IMUDAY ITEMMSC)
     A                                      EDTCDE(Z)
     A            W1SELLD        6S 0O   128
     A                                      EDTWRD('  /  /  ')
     A            SELDYS         3  0O   138EDTCDE(J)
     A            EXPDYS         3  0O   145EDTCDE(J)
     A            LALCNS    R        O   151REFFLD(LALCNS LICACTIVE)
     A            W1RDTA         6S 0O   167
     A                                      EDTWRD('  /  /  ')
     A            SLDISP    R        O   178REFFLD(SLDISP SLOT)
     A            W1LOT         30A  O   192
     A***************************************************************  *********
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT395: End of Report')
