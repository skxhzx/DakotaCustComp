     A*%%TS  SD  20160324  132718  MIKER       REL-V7R2M0  5770-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
416 AA* ***************************************************************
416 AA* *****  If the position of any slot entry field is changed *****
416 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
416 AA* *****  program.                                           *****
416 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IV330FM   Inventory entry by slot -  RF Mini Screen
     A*  07 April, 1999
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
414aAA*    06/20/00  RH   4.14d
     A*      - Add W3ITM3 field to display pack size on screen 3.
     A*
416 AA*    12/13/02  MLB  4.16
     A*      - Enh: Added hidden fields WOROW and WOCOL.
     A*        Added function CSRLOC to SCREEN 2.
     A*        Both of the above added for api program APICVTSLT
     A*
500 AA*   05/31/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
500aAA*    05/25/06  RH   5.00a
     A*      - Enh: Added F8 Override SC2 to allow re-entry of slot.
500bAA*    05/30/06  RH   5.00b
     A*      - Enh: Added SC5 catch weight entry.
510aAA*    11/15/06  RH   5.10a
     A*      - Enh: Add UPC scan to screen 4.
510bAA*    11/21/06  RH   5.10b
     A*      - Enh: Add F6 = Add item to Screen2 and SCRN2SP
520bAA*    05/12/08  RH   5.20
     A*      - Enh: Create Screen 6, scan License, UCI or UPC to verify
     A*        item being counted. Remove UPC from screen 4.
610aAA*    09/16/09  DAS  610a
     A*      - Added Lcns to item info screen 3.
JOR AA*    06/19/07  RH   JORa
     A*      - Enh: Change EXCP to ERROR
700aAA*    03/28/16  MR   700a
700aAA*      - Enh: Add Kilogram/Pounds Toggle
     A*
720aAA*    01/09/17  LMC  720a
720aAA*      - Enh: Show current inventory - based on options.
720aAA*        Renamed display file IV33001 to IV33002.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IV330.01'
     A                                  1 12'Inv. Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Team'
     A            W1TEAM        10A  B  4  7
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04
     AON20                                  DSPATR(PC)
     A* 04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A                                  5  2'Date'
     A            W1IDTE         6Y 0B  5  7EDTCDE(4)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
JORa A                                  7  2'F3/F12=Exit F10=Error.'
JORa A*                                 7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      CF04
510b A                                      CF06
500a A                                      CF08
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
416 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
416 AA            WOROW          3  0H
416 AA            WOCOL          3  0H
     A                                  1  2'IV330.02 '
     A                                  1 12'Inv. Entry'
     A                                  3  2'Dept.'
     A            W2WHDP         5A  B  3  8
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                  4  2'Slot '
     A            W2DISP        12A  B  4  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
JORa A                                  7  2'F6=Add F8=Ovrd F10=Error. '
510b A*                                 7  2'F6=Add F8=Ovrd F10=Excpt. '
510b A*                                 7  2'F3=Exit F8=Ovrd F10=Excpt.'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV330.03 '
     A                                  1 12'Inv. Entry'
610aAA                                  2  2'Lcns:'
610aAA            W3LCNS        15A  O  2  8
610aDA**                                3  2'Item.'
610aMA                                  3  2'Item:'
     A            W3ITEM        15A  O  3  8
     A*N25                                  DSPATR(HI)
     A* 25                                  DSPATR(RI)
     A* 05                                  DSPATR(PC)
610aDA**                                4  2'Desc '
610aMA                                  4  2'Desc:'
     A            W3ITM1        15A  O  4  8
     A            W3ITM2        15A  O  5  8
414aAA            W3ITM3        15A  O  6  8
JORa A                                  7  2'F3/F12=Exit F10=Error.'
JORa A*                                 7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV330.04 '
     A                                  1 12'Inv. Entry'
     A                                  3  1'Qty:'
     A            W4QT01         5Y 0B  3  6EDTCDE(M)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A            W4UM1          5A  O  3 14
720aAA            W4STK1         6A  O  3 20
720aAA                                      DSPATR(HI)
     A            W4QT02         3Y 0B  4  6EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W4UM2          5A  O  4 14
     A  92                                  DSPATR(ND)
720aAA            W4STK2         4A  O  4 20
720aAA                                      DSPATR(HI)
720aAA  92                                  DSPATR(ND)
     A            W4QT03         3Y 0B  5  6EDTCDE(M)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W4UM3          5A  O  5 14
     A  93                                  DSPATR(ND)
720aAA            W4STK3         4A  O  5 20
720aAA                                      DSPATR(HI)
720aAA  93                                  DSPATR(ND)
520b A*                                 6  1'UPC'
520b A* 94                                  DSPATR(ND)
520b A*           W4UPCD        50A  B  6  5CHECK(ER)
520b A* 30                                  DSPATR(RI)
520b A* 94                                  DSPATR(ND)
JORa A                                  7  2'F3/F12=Exit F10=Error.'
JORa *                                  7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
     A          R SCREEN5
     A*%%TS  SD  20160324  132718  MIKER       REL-V7R2M0  5770-WDS
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(07)
700aAA                                      CA07
     A                                      OVERLAY
     A                                  1  2'IV330.05 '
     A                                  1 12'Inv. Entry'
     A                                  3  1'CtWt:'
     A            W5CWGT         7Y 2B  3  7EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29                                  DSPATR(UL)
700aAA            W5UM1          5A  O  3 17
700aAA N31                              7  2'F7=Pounds'
700aAA                                      COLOR(BLU)
700aAA N31                              7 12'F8=Ovrrid'
700aAA                                      COLOR(BLU)
700aAA  31                              7  2'F7=Kilograms'
700aAA                                      COLOR(BLU)
700aMA  31                              7 15'F8=Ovrrid'
700aMA                                      COLOR(BLU)
520b A          R SCREEN6
520b A                                      CF10
520b A  98                                  ALARM
520b A                                      CLRL(07)
520b A                                      OVERLAY
520b A                                  1  2'IV330.06 '
520b A                                  1 12'Inv. Entry'
520b A                                  3  2'Item.'
520b A            W3ITEM        15A  O  3  8
520b A                                  4  1'Lic:'
520b A            W6LCN         15A  B  4  6CHECK(ER)
520b A  26                                  DSPATR(RI)
520b A  06                                  DSPATR(PC)
520b A N26                                  DSPATR(UL)
520b A                                  5  1'UCI:'
520b A            W6UCI         10A  B  5  6CHECK(ER)
520b A  27                                  DSPATR(RI)
520b A  07                                  DSPATR(PC)
520b A N27                                  DSPATR(UL)
520b A* 94                                  DSPATR(ND)
520b A* 94                                  DSPATR(PR)
520b A                                  6  1'UPC'
520b A            W6UPCD        50A  B  6  5CHECK(ER)
520b A  30                                  DSPATR(RI)
520b A  10                                  DSPATR(PC)
520b A N30                                  DSPATR(UL)
     A                                  7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IV330.01'
     A                                  1 12'Incorp. Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Almacn'
     A            W1WHSE         3Y 0B  3  9
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Equipo'
     A            W1TEAM        10A  B  4  9
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04
     AON20                                  DSPATR(PC)
     A* 04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A                                  5  2'Fecha'
     A            W1IDTE         6Y 0B  5  9EDTCDE(4)
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A                                      CF04
510b A                                      CF06
500a A                                      CF08
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
416 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
416 AA            WOROW          3  0H
416 AA            WOCOL          3  0H
     A                                  1  2'IV330.02 '
     A                                  1 12'Incorp. Inv'
     A                                  3  2'Dept.'
     A            W2WHDP         5A  B  3  8
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                  4  2'Slot '
     A            W2DISP        12A  B  4  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
510bMA                                  7  2'F6=Add F8=Ovrd F10=Excpt.'
510bDA*                                 7  2'F3=Salir F8=Ovrd F10=Excpt.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV330.03 '
     A                                  1 12'Incorp. Inv'
610aAA                                  2  2'Lcns:'
610aAA            W3LCNS        15A  O  2  8
     A                                  3  2'Prdc:'
     A            W3ITEM        15A  O  3  8
     A*N25                                  DSPATR(HI)
     A* 25                                  DSPATR(RI)
     A* 05                                  DSPATR(PC)
     A                                  4  2'Desc:'
     A            W3ITM1        15A  O  4  8
     A            W3ITM2        15A  O  5  8
414aAA            W3ITM3        15A  O  6  8
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN4SP
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV330.04 '
     A                                  1 12'Incorp. Inv'
     A                                  3  1'Ctd:'
     A            W4QT01         5Y 0B  3  6EDTCDE(M)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A            W4UM1          5A  O  3 14
720aAA            W4STK1         6A  O  3 20
720aAA                                      DSPATR(HI)
     A            W4QT02         3Y 0B  4  6EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W4UM2          5A  O  4 14
     A  92                                  DSPATR(ND)
720aAA            W4STK2         4A  O  4 20
720aAA                                      DSPATR(HI)
720aAA  92                                  DSPATR(ND)
     A            W4QT03         3Y 0B  5  6EDTCDE(M)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W4UM3          5A  O  5 14
     A  93                                  DSPATR(ND)
720aAA            W4STK3         4A  O  5 20
720aAA                                      DSPATR(HI)
720aAA  93                                  DSPATR(ND)
510a A                                  6  1'UPC'
510a A            W4UPCD        50A  B  6  5CHECK(ER)
510a A  30                                  DSPATR(RI)
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN5SP
500b A                                      CF08
700aAA                                      CA07
500b A  98                                  ALARM
500b A                                      CLRL(07)
500b A                                      OVERLAY
500b A                                  1  2'IV330.05 '
500b A                                  1 12'Inv. Entry'
500b A                                  3  1'CtWt:'
500b A            W5CWGT         7Y 2B  3  7EDTCDE(M)
500b A  29                                  DSPATR(RI)
500b A  09                                  DSPATR(PC)
500b A N29                                  DSPATR(UL)
700aAA            W5UM1          5A  O  3 17
700aAA N31                              7  2'F7=Libras'
700aAA                                      COLOR(BLU)
700aAA N31                              7 12'F8=Ovrrid'
700aAA                                      COLOR(BLU)
700aAA  31                              7  2'F7=Kilogramos'
700aAA                                      COLOR(BLU)
700AMA  31                              7 16'F8=Ovrrid             '
     A                                      COLOR(BLU)
520b A*----------------------------------------------------------------
520b A          R SCRN6SP
520b A                                      CF10
520b A  98                                  ALARM
520b A                                      CLRL(07)
520b A                                      OVERLAY
520b A                                  1  2'IV330.06 '
520b A                                  1 12'Incorp. Inv'
520b A                                  3  2'Prdc.'
520b A            W3ITEM        15A  O  3  8
520b A                                  4  1'Lic:'
520b A            W6LCN         15A  B  4  6CHECK(ER)
520b A  26                                  DSPATR(RI)
520b A  06                                  DSPATR(PC)
520b A N26                                  DSPATR(UL)
520b A                                  5  1'UCI:'
520b A            W6UCI         10A  B  5  6CHECK(ER)
520b A  27                                  DSPATR(RI)
520b A  07                                  DSPATR(PC)
520b A N27                                  DSPATR(UL)
520b A* 94                                  DSPATR(ND)
520b A* 94                                  DSPATR(PR)
520b A                                  6  1'UPC'
520b A            W6UPCD        50A  B  6  5CHECK(ER)
520b A  30                                  DSPATR(RI)
520b A  10                                  DSPATR(PC)
520b A N30                                  DSPATR(UL)
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
720aAA          R SCREEN7
720aAA                                      CF22
720aAA  98                                  ALARM
720aAA                                      OVERLAY
720aAA                                  1  2'IV330.07'
720aAA                                  2  1'Count'
720aAA            W4QT01         5Y 0   2  7EDTCDE(3)
720aAA            #TOITUM1       2A  O  2 13
720aAA            W4QT02         3Y 0   2 16EDTCDE(3)
720aAA  92                                  DSPATR(ND)
720aAA            #TOITUM2       2A  O  2 20
720aAA  92                                  DSPATR(ND)
720aAA            W4QT03         3Y 0   2 23EDTCDE(3)
720aAA  93                                  DSPATR(ND)
720aAA            #TOITUM3       2A  O  2 27
720aAA  93                                  DSPATR(ND)
720aAA                                  3  1'Scans'
720aAA            UCIQTY1        5Y 0   3  7EDTCDE(3)
720aAA            UCIQTY2        3Y 0   3 16EDTCDE(3)
720aAA  92                                  DSPATR(ND)
720aAA            UCIQTY3        3Y 0   3 23EDTCDE(3)
720aAA  93                                  DSPATR(ND)
720aAA                                  4  1'Scan UCI'
720aAA            W7UCI        100A  B  4 10
720aAA                                      CHECK(ER)
720aAA N21                                  DSPATR(HI)
720aAA N21                                  DSPATR(UL)
720aAA  01                                  DSPATR(PC)
720aAA  21                                  DSPATR(RI)
720aMA                                  7  1'F12=Cancel  F22=Done'
720aAA                                      COLOR(BLU)
720aAA          R SCRN7SP
720aAA                                      CF22
720aAA  98                                  ALARM
720aAA                                      OVERLAY
720aAA                                  1  2'IV330.07'
720aAA                                  4  1'Scan UCI'
720aAA            W7UCI        100A  B  4 10
720aAA                                      CHECK(ER)
720aAA N21                                  DSPATR(HI)
720aAA N21                                  DSPATR(UL)
720aAA  01                                  DSPATR(PC)
720aAA  21                                  DSPATR(RI)
720aMA                                  7  1'F12=Cancel  F22=Done'
720aAA                                      COLOR(BLU)
