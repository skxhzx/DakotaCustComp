     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IV220FM   Inv Sheet Item Lookup -
     A*  09 May, 2002
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IV220.01'
     A                                  1 11'Inv Sheet Item Lookup'
     A                                      DSPATR(HI)
     A                                  3  1'Whse'
     A            W1WHSE         3Y 0B  3  6
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  1'Item'
     A            W1ITEM        15A  B  4  6
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  1'UPC:'
     A            W1UPC         20A  B  6  1CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
