     A*%%TS  SD  19960312  104007  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*  Revisions
     A*    09/24/06  RH   5.10
     A*      - Add select for Aisle or All.
     A*
     A*----------------------------------------------------------------
     A*
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960312  104007  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(23)
     A                                      OVERLAY
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A                                  1  2'IV170.01'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 49'1-999'
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 49'Name, *ALL'
     A                                 10  4'Section  . . . . . . . . . .'
     A            W1STYP         1A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'P=PIR, F=Fixed, B=Both'
416 MA                                 12  4'Item # or *ALL . . . . . . .'
     A            W1ITEM        15A  B 12 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 AA                                 13  4' - OR -'
510 AA                                 14  4'Aisle or *ALL  . . . . . . .'
510  A            W1ASLE         4A  B 14 34
510  A N28N90                               DSPATR(HI)
510  A  28                                  DSPATR(RI)
510  A N28N90                               DSPATR(UL)
510  A  08                                  DSPATR(PC)
510  A  90                                  DSPATR(PR)
416  A                                 11  4'Minumum variance . . . . . .'
     A            W1MVAR         5Y 0B 11 34EDTCDE(4)
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 16 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 49'Y=Yes, N=No'
     A                                 17  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 17 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 49'Name, *SYSVAL, *JOB'
     A                                 18  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 18 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 49'Character value, *STD'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
