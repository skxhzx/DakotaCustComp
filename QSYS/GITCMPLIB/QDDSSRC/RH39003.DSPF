     A*%%TS  SD  20151001  164819  LYNNM       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  RH390FM  Pallet lookup by route
     A*  31 July 2000
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
416aAA*    02/19/02  RH   4.16a
     A*     - Change position to'route' to 'route id'.
     A*
530a A*    02/03/10  GJA  5.30a
     A*     - Enh:  Add pallet count to screen 2.
     A*
530b A*    03/22/10  GJA  5.30b
     A*     - Enh:  Add trns and label count to screen 2.
530c A*    08/11/10  RH   5.30c
     A*     - Enh:  Add F9 to call RH380 with sort criteria.
650aAA*    03/07/14  RH   6.50a
     A*     - Enh: Add position to date, can enter route id OR date
     A*       Sometimes users don't have route id
     A*
700 AA*    05/19/15  MLB  7.00  P#01368
     A*     - Enh: Add position to route number along with date.
     A*     - Fields on OPTREC moved down 1 line to make room for
     A*       route number position to field.
     A*
700a A*    01/14/16  GJA  7.00a
     A*     - Fix: Lost options with 7.00 mod.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000731  095337  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH390 '
     A                                  1 31'Pallet Lookup by Route'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
650aDA**                                3 48'Position to route id  . .'
650aMA                                  3  2'Position to route id  . .'
650aDA**          PTINP2         5A  B  3 75
650aMA            PTINP2         5A  B  3 29
650aAA                                  3 39'-OR-'
700 AA                                  3 48'Position to route nbr . .'
700 AA            PTINP4         5A  B  3 75
700 MA                                  4 58'Date (MMDDYY) .'
700 MA            PTINP3         6Y 0B  4 75EDTCDE(4)
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20151001  164819  LYNNM       REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  6REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT        10A  O 11 29
     A            RHTRUK    R        O 11 18REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            RHRTID         5A  O 11 12
     A            W1PRTD         6Y 0O 11 41EDTCDE(Y)
     A            RHPRTT         6Y 0O 11 52EDTWRD('  :  :  ')
     A            W1TRN          5Y 0O 11 62EDTCDE(3)
     A            W1LBL          5Y 0O 11 68EDTCDE(3)
     A            W3PCS          5Y 0O 11 74EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20151001  164819  LYNNM       REL-V6R1M0  5761-WDS
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 29'Status    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 40'Print Date'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 51'Print Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 12'RteID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 18'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Trans'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Label'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20000731  095337  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHIMPT    R        O 11 33REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHBLDT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2RPTD         6Y 0O 11 14EDTCDE(Y)
     A            W2IMPD         6Y 0O 11 24EDTCDE(Y)
     A            W2BLDD         6Y 0O 11 43EDTCDE(Y)
530aAA            W2PLTS         6  0O 11 62EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20000731  095337  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 24'---- Imported ---'
     A                                      DSPATR(HI)
     A                                 10 43'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 43'----- Built -----'
     A                                      DSPATR(HI)
     A                                 10 14'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 14'Report'
     A                                      DSPATR(HI)
530aAA                                 10 62'Pallets'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
700 DA*                                     SLNO(06)
700 MA                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
700aDA*           OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
