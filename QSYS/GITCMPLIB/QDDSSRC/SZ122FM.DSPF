     A*%%TS  SD  20000627  164435  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SZ122FM   Stage Zone maintenance - Change screen
     A*  14 September 1999
     A*  Roxanne Harrison   4.12
     A*
     A*  Revisions
     A*
     A*    09/12/97  DAS  4.05
     A*      - Added 'Screen _ of _' to each screen.
     A*      - Added 'Enter=' on command line of each screen.
     A*
414 AA*    06/27/00  DAS  4.14
     A*      - Added sequence number input field to SCREEN1
     A*      - Removed unneeded header fields.
     A*      - Removed unneeded command keys.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000627  164435  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SZ122.01'
     A                                  1 30'Stage Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Zone  . . . . . . . . . . . . . .'
     A            W1ZONE         6A  B  9 39
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                 10  4'Sequence  . . . . . . . . . . . .'
     A            W1ZSEQ         3Y 0B 10 39EDTCDE(L)
     A                                 11  4'Description . . . . . . . . . . .'
     A            W1DESC        30A  B 11 39
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19970912  091258  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SZ122.02'
     A                                  1 33'Stage Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'2 of 3'
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 65'Picking options'
     A                                      DSPATR(HI)
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Next screen'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  19970912  091258  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SZ122.03'
     A                                  1 33'Stage Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'3 of 3'
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Picking Label Options'
     A                                      DSPATR(HI)
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Save and exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
