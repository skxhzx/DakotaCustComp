     A*%%TS  SD  20000929  141729  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS132FM   Truck full pallet move
     A*  29 September 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
414 AA*    09/29/00  DAS  4.14
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000929  141729  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'PS132.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Full Pallet Move'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
     A            $PWHSE         3Y 0O  4 19EDTCDE(3)
     A                                  4 23'-'
     A            WHDESC        30A  O  4 25
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  7 19EDTCDE(4)
     A                                  7 23'Row:'
     A            WOROW          2S 0O  7 28
     A                                  7 32'Column:'
     A            WOCOL          2S 0O  7 40
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11  3'Total pieces  . .'
     A            W1PCS          5Y 0O 11 21EDTCDE(4)
     A                                 12  3'Total cube  . . .'
     A            W1CUBE         7Y 3O 12 21EDTCDE(4)
     A                                 13  3'Total weight  . .'
     A            W1WGHT         9Y 2O 13 21EDTCDE(4)
     A                                 14  3'Bulk picks  . . .'
     A            W1BCNT         1Y 0O 14 21EDTCDE(3)
     A                                 16  3'Move to Row . . .'
     A            W1ROW          2Y 0B 16 21EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 28'Column  . . .'
     A            W1COL          1Y 0B 16 42EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel  F20=Accept-
     A                                       warning'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PS142.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Pallet Detail'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
413aMA            $PWHSE         3Y 0O  4 19EDTCDE(3)
413aMA                                  4 23'-'
413aMA            WHDESC        30A  O  4 25
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  7 19EDTCDE(4)
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
