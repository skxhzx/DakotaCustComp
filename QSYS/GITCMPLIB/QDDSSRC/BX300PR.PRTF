     A**************************************************************************
     A*
     A*   BX300PR - AMP Customer Orders Needing Reduced
     A*   11 JULY, 2016
     A*   Nancy Koelz
     A*
     A*****************************************************************
     A*   Revisions:
     A*    07/11/16  NLK  7.00
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A**************************************************************************
     A*
     A          R HEADER                    SKIPB(002)
     A                                      SPACEA(001)
     A                                      TEXT('REPORT HEADING')
     A*
     A            RDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +42
     A                                      DFT('AMP Reduced Item Qty')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            RTIME          6S 0O    13
     A                                      EDTWRD('0  :  :  ')
     A            ROUTE          5   O    33
     A                                   115
     A                                      DFT('BX300')
     A*
     A          R HEADTL                    SPACEB(001)
     A                                      SPACEA(001)
     A                                     2'Whse'
     A                                     7'Customer'
     A                                    48'CustOrd'
     A                                    56'Box #'
     A                                    62'BxQty'
     A                                    68'OrdQty'
     A                                    75'Item Description'
     A                                   120'SLOT'
     A*
     A          R DETAIL                    SPACEB(001)
     A            AXWHSE         3  0      3EDTCDE(Z)
     A            OHCNAM        40         7
     A            AXCORD         7  0     48
     A            ABXBOX#        5  0     56EDTCDE(Z)
     A            LBQPCK         5  0     62EDTCDE(Z)
     A            ODQOR1         5  0     68EDTCDE(Z)
     A            AXITEM        15        74
     A            IXDESC        30        89
     A            AXADISP       12       120
