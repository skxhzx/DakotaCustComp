     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR329FM    TRAX stop item trax detail - Display
     A*  Ginger Andrews
     A*
     A*  Revisions
     A*
640a A*    10/11/11  GJA  6.40a
     A*      - Created.
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF21
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            TTXTRX    R        O  8  5REFFLD(TTXREC/TTXTRX *LIBL/TTTRX)
     A            TTXUM     R        O  8 15REFFLD(TTXREC/TTXUM  *LIBL/TTTRX)
     A            TTXOPF    R        O  8 21REFFLD(TTXREC/TTXOPF *LIBL/TTTRX)
     A            W1QDLV         6Y 0O  8 26EDTCDE(4)
     A            W1QDLN         6Y 0O  8 37EDTCDE(4)
     A            W1QCRT         6Y 0O  8 46EDTCDE(4)
     A            W1QPRT         6Y 0O  8 53EDTCDE(4)
     A            W1SRD          6  0O  8 61EDTWRD('  /  /  ')
     A            TTXSRT    R        O  8 70REFFLD(TTXREC/TTXSRT *LIBL/TTTRX)
     A                                      EDTWRD('  :  :  ')
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 22'View Trax Routes Trax Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 20'Opr'
     A                                      DSPATR(HI)
     A                                  6 24'Pieces'
     A                                      DSPATR(HI)
     A                                  6 34'Pieces'
     A                                      DSPATR(HI)
     A                                  6 46'Curr / Prev'
     A                                      DSPATR(HI)
     A                                  6 61'Scanned'
     A                                      DSPATR(HI)
     A                                  7  5'TRAX#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 14'UOM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 20'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 24'Delivered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 34'NonScanned'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'Pcs Returned '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 61'Date/Time        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F21=Print'
     A                                      COLOR(BLU)
     A          R MKEY1
     A                                      OVERLAY
     A            W1MSG1        75A  O 24  3
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A          R WINDOW1
     A                                      WINDOW(6 17 16 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  2  1'Order  . . . . . .'
     A            W1SORT         1A  B  2 20DSPATR(HI)
     A                                  2 25'Ascending, Descending'
     A                                  3  1'Order by . . . . .'
     A            W1OBY          1A  B  3 20DSPATR(HI)
     A                                  4 25'2 = Delivery Date'
     A                                  5  1'Warehouse  . . . .'
     A            W1WHSE         3Y 0B  5 20DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  6  1'Route  . . . . . .'
     A            W1RTE          5A  B  6 20DSPATR(HI)
     A                                  6 32'*ALL'
     A                                  7  1'Stop   . . . . . .'
     A            W1STP          2  0B  7 20DSPATR(HI)
     A                                  7 32'*ALL'
     A                                  8  1'Item . . . . . . .'
     A            W1ITM         15A  B  8 20DSPATR(HI)
     A                                  8 32'*ALL'
     A                                  9  1'Trax#  . . . . . .'
     A            W1TRX          5  0B  9 20DSPATR(HI)
     A                                 10  1'Delivery date  . .'
     A            W1DLVD         6Y 0B 10 20DSPATR(HI)
     A                                 10 32'0=All'
     A                                 11  1'Non-scanned       '
     A                                 12  3'items only . . .'
     A            W1NSCN         1A  B 12 20DSPATR(HI)
     A                                 12 32'Y=yes'
     A                                 13  1'Returns only . . .'
     A            W1RETN         1A  B 13 20DSPATR(HI)
     A                                 13 32'Y=yes'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
