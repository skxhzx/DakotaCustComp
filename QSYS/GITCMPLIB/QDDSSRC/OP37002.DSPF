     A*%%TS  SD  20080604  114355  DAVE        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP370FM   PIR Options - Check digits
     A*  11 June 2007
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    06/11/07  DAS  5.10
     A*      - Created.
     A*
520aAA*    06/04/07  DAS  5.20a  P#00094
     A*      - Added option W1READ to screen.
     A*      - Rearranged screen to get everything to fit.
640a A*    06/13/12  GJA  6.40a
     A*      - Enh:  Add option to handle 5 numeric check digit
640b A*    06/27/12  LMC  6.40b
     A*      - Enh:  Changed the new check digit field to accomodate
     A*        a 9 position check digit, this was need because ICB
     A*        barcoded their 3-3 position check digits into one 9 pos
     A*        barcode
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080604  114355  DAVE        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'OP370.01'
     A                                  1 31'Check Digit Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Number of codes being used . . . .'
     A            W1COD#         1Y 0B  5 40
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                  5 54'1, 2, 3'
     A                                  7  7'Identifier for code 1  . . . . . .'
     A            W1IDC1         6A  B  7 43
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  7 54'Examples: Top,    Red'
     A                                  8  7'Identifier for code 2  . . . . . .'
     A            W1IDC2         6A  B  8 43
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8 54'Examples: Middle, Yellow'
     A                                  9  7'Identifier for code 3  . . . . . .'
     A            W1IDC3         6A  B  9 43
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  9 54'Examples: Bottom, Green'
     A                                 11  4'Max number of digits per code  . .'
     A            W1DGT#         1Y 0B 11 40
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 11 54'2, 3'
     A                                 13  7'Identifier for digit 1 . . . . . .'
     A            W1IDD1         6A  B 13 43
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 13 54'Examples: Left,   1'
     A                                 14  7'Identifier for digit 2 . . . . . .'
     A            W1IDD2         6A  B 14 43
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 14 54'Examples: Center, 2'
     A                                 15  7'Identifier for digit 3 . . . . . .'
     A            W1IDD3         6A  B 15 43
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 15 54'Examples: Right,  3'
     A                                 17  4'Number of digits user reads when'
     A                                 18  6'code is less than 100  . . . . .'
     A            W1READ         1Y 0B 18 40
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 18 54'2, 3'
640bDA*                                20  6'5 numeric check digit  . . . . .'
640bMA                                 20  6'Barcode Size . . . . . . . . . .'
     A            W15NUM         1A  B 20 40
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640bDA*                                20 54'Y=Yes, N=No'
640bMA                                 20 54'2,3,5,or 9'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
