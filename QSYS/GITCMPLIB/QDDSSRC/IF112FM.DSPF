     A*%%TS  SD  20041113  055855  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT112FM   Item maintenance - Change screen
     A*  09 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*    04/25/96  HNK  2.12
     A*      - Command key CF09 added on screen 3.
409  A*
     A*    05/18/98  HNK  4.09
     A*      - Manufacturer's date and shelf life info added on screen1
     A*      - Manufacturer's Tie/High added on screen 3.
412  A*    08/04/99  RH   4.12
     A*      - Add Item Catch Weight Low variance % to screen 2.
     A*      - Add Item Catch Weight High variance % to screen 2.
412  A*    08/24/99  RH   4.12
     A*      - Make UPC fields non display -
     A*        Future change, display 1st record from UPC file and
     A*        allow F4 to display a list of all the UPC records.
     A*    08/25/99  MLB  4.12
     A*      - Number of pick slots for item W1NRPK added on SCREEN1.
413  A*    02/15/00  DAS  4.13
     A*      - Change W1RPKG field so it is only protected when
     A*        indicator 90 is on.
     A*      - Change W1ITM2 field so it is only protected when
     A*        indicator 90 is on.
415  A*    12/15/00  HNK  4.15
     A*      - Change label "Vendor item number" to "Mfg code/Vendor
     A*        item #"
416  A*    11/09/01  HNK  4.16
     A*      - Combine SC1 and SC4.
417a A*    04/01/03  RH   4.17a
     A*      - Move Allow bulk pick field to add # of Days out field.
     A*
417b A*    05/14/03  DAS  4.17b
     A*      - Rearranged fields on screen 1 to group them better.
     A*      - Added license tracking field (W1LTRK) to SCREEN1.
     A*      - Added lot code field (W2LOT) to SCREEN2.
     A*
500  A*    11/13/04  DAS  5.00
     A*      - Changed "Track by License" to just "Tracking".
     A*      - Changed options for "Tracking" from "(H,L,N)" to
     A*        "(Low,Med,High)".
     A*
500aAA*    11/03/05  DAS  5.00a
     A*      - Combined Department / Section entry fields onto 1 line.
     A*      - Added Catgory field, where Section used to be.
     A*
510 AA*    01/24/07  TLJ  5.10
     A*      - Enh: For new item type Repack, remove several fields
     A*      from screen IT112.01 and .02.  Add 3 new fields and make
     A*      Full Case Item mandatory.
     A*
510aAA*    05/24/07  RH   5.10a
     A*      - Enh: Add LxWxH to screen for dimentions.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20041113  055855  DAVE        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                  1  2'IT112.01'
     A                                  1 33'Item Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            HDTYPE        40A  O  2 21
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 60'General information'
     A                                      DSPATR(HI)
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  1'Item  . . . . . . . . . .'
     A            W1ITEM        15A  B  8 28
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                  9  1'Description . . . . . . .'
     A            W1DESC        30A  B  9 28
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
510 AA  81
510 DA**90                                  DSPATR(PR)
510 MAO 90                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A                                      CHECK(LC)
     A            W1DSC2        80A  B 10  1
     A  82                                  DSPATR(ND)
500aDA**                               11  2'Department . . . . . . .'
500aMA                                 11  2'Department / Section . .'
     A            W1WHDP         5A  B 11 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 11 50'Designation area . . . .'
     A            W1DESG         5A  B 11 75
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500aDA**                               12  2'Section  . . . . . . . .'
500aDA**          W1STYP         1A  B 12 28
500aAA                                 11 34'/'
500aMA            W1STYP         1A  B 11 36
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
500aAA                                 12  2'Category . . . . . . . .'
500aAA            W1CAT         10A  B 12 28
     A  44                                  DSPATR(RI)
     A N44N90                               DSPATR(UL)
     A  44                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 50'Activity zone  . . . . .'
     A            W1ACTV         2A  B 12 75
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               13  2'Broken case item . . . .'
510 MA  45                             13  2'Broken case item . . . .'
     A  96                                  DSPATR(ND)
510 DA**          W1RPKG         1A  B 13 28
510 MA  45        W1RPKG         1A  B 13 28
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                 14  4'Full case item . . . .'
     A  96                                  DSPATR(ND)
     A            W1ITM2        15A  B 14 28
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                 14 50'Date sensitive (Y,N) . .'
     A            W1FLGD         1A  B 14 75
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               15 50'Number of days out . . .'
510 MA  45                             15 50'Number of days out . . .'
510 DA**          W1ODAY         3Y 0B 15 75EDTCDE(4)
510 MA  45        W1ODAY         3Y 0B 15 75EDTCDE(4)
     A  42                                  DSPATR(RI)
     A N42N90                               DSPATR(UL)
     A  42                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  2'Allow bulk picks . . . .'
417A A**          W1FLGB         1A  B 16 28
     A            W1FLGB         1A  B 16 28
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               16 50'Mfg date (Y,N) . . . . .'
510 MA  45                             16 50'Mfg date (Y,N) . . . . .'
510 DA**          W1MFLG         1A  B 16 75
510 MA  45        W1MFLG         1A  B 16 75
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  2'Tracking . . . . . . . .'
     A            W1LTRK         1A  B 17 28
     A  43                                  DSPATR(RI)
     A N43N90                               DSPATR(UL)
     A  43                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               17 50'Mfg shelf life days. . .'
510 MA  45                             17 50'Mfg shelf life days. . .'
510 DA**          W1MDAY         3Y 0B 17 75EDTCDE(4)
510 MA  45        W1MDAY         3Y 0B 17 75EDTCDE(4)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               18 50'Dist days to sell. . . .'
510 MA  45                             18 50'Dist days to sell. . . .'
510 DA**          W1DDAY         3Y 0B 18 75EDTCDE(4)
510 MA  45        W1DDAY         3Y 0B 18 75EDTCDE(4)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               19  2'Vendor number  . . . . .'
510 MA  45                             19  2'Vendor number  . . . . .'
510 DA**          W1VEN#        10Y 0B 19 28EDTCDE(4)
510 MA  45        W1VEN#        10Y 0B 19 28EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               19 50'User days to sell. . . .'
510 MA  45                             19 50'User days to sell. . . .'
510 DA**          W1UDAY         3Y 0B 19 75EDTCDE(4)
510 MA  45        W1UDAY         3Y 0B 19 75EDTCDE(4)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               20  2'Mfg code/Vendor item # .'
510 MA  45                             20  2'Mfg code/Vendor item # .'
510 DA**          W1VIT#        15A  B 20 28
510 MA  45        W1VIT#        15A  B 20 28
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               21  2'Replenisment level . . .'
510 MA                                 21  2'Replenishment level. . .'
     A            W1REPL         5Y 0B 21 28EDTCDE(4)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21 34'normal units'
     A                                 21 50'Number of pick slots . .'
     A  41                                  DSPATR(ND)
     A            W1NRPK         3Y 0B 21 75EDTCDE(3)
     A  40                                  DSPATR(RI)
     A N40N90                               DSPATR(UL)
     A  41                                  DSPATR(ND)
     A  20                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UPC         15A  B 22 56
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =View warehouse quantities  F12=Can-
     A                                      cel'
     A                                      COLOR(BLU)
     A                                 17 30'(Low,Med,High)'
     A                                 16 30'(Y,N)'
     A          R SCREEN2
     A*%%TS  SD  20030514  120943  DAVE        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'IT112.02'
     A                                  1 33'Item Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            HDTYPE        40A  O  2 23
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Physical information'
     A                                      DSPATR(HI)
     A                                  5  2'Item     :'
     A            WOITEM        50A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Pack description  . . . . . . . .'
     A            W2PDSC        15A  B  9 39
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 10  4'Normal unit of measure. . . . . .'
     A            W2UM1          2A  B 10 39
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 AA** LINES 11 & 12 FOR NON-REPACK ITEMS          **
510 DA**                               11  4'Breakdown 1 unit of measure . . .'
510 MA  45                             11  4'Breakdown 1 unit of measure . . .'
510 DA**          W2UM2          2A  B 11 39
510 MA  45        W2UM2          2A  B 11 39
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               11 43'Quantity:'
510 MA  45                             11 43'Quantity:'
510 DA**          W2UMQ2         3Y 0B 11 53EDTCDE(4)
510 MA  45        W2UMQ2         3Y 0B 11 53EDTCDE(4)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               11 58'Allow entry:'
510 MA  45                             11 58'Allow entry:'
510 DA**          W2FLG1         1A  B 11 71
510 MA  45        W2FLG1         1A  B 11 71
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               12  4'Breakdown 2 unit of measure . . .'
510 MA  45                             12  4'Breakdown 2 unit of measure . . .'
510 DA**          W2UM3          2A  B 12 39
510 AA  45        W2UM3          2A  B 12 39
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               12 43'Quantity:'
510 MA  45                             12 43'Quantity:'
510 DA**          W2UMQ3         3Y 0B 12 53EDTCDE(4)
510 MA  45        W2UMQ3         3Y 0B 12 53EDTCDE(4)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**                               12 58'Allow entry:'
510 MA  45                             12 58'Allow entry:'
510 DA**          W2FLG2         1A  B 12 71
510 MA  45        W2FLG2         1A  B 12 71
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 AA** LINES 11 & 12 FOR REPACK ITEMS **
510 AA N45                             11  4'Parent Quantity Needed. . . . . .'
510 AA N45        W2RQTY1        3Y 0B 11 39EDTCDE(4)
510 AA  23                                  DSPATR(RI)
510 AA N23N90                               DSPATR(UL)
510 AA  03                                  DSPATR(PC)
510 AA  90                                  DSPATR(PR)
510 AA N45        W2RUM1         2A  O 11 43
510 AA N45        W2RQTY2        3Y 0B 11 46EDTCDE(4)
510 AA  32                                  DSPATR(RI)
510 AA N32N90                               DSPATR(UL)
510 AA  12                                  DSPATR(PC)
510 AA  92
510 AAO 90                                  DSPATR(PR)
510 AA  92                                  DSPATR(ND)
510 AA N45        W2RUM2         2A  O 11 50
510 AA  92                                  DSPATR(ND)
510 AA N45        W2RQTY3        3Y 0B 11 53EDTCDE(4)
510 AA  36                                  DSPATR(RI)
510 AA N36N90                               DSPATR(UL)
510 AA  16                                  DSPATR(PC)
510 AA  93
510 AAO 90                                  DSPATR(PR)
510 AA  93                                  DSPATR(ND)
510 AA N45        W2RUM3         2A  O 11 57
510 AA  93                                  DSPATR(ND)
510 AA**
510aAA                                 13  4'Packaged dimensions (LxWxH) . . .'
     A            W2ULEN         5Y 2B 13 39EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 46'x'
     A            W2UWDT         5Y 2B 13 48
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 13 55'x'
     A            W2UHGT         5Y 2B 13 57
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            OPDUM         15A  O 13 64
     A                                 14  4'Cube per unit . . . . . . . . . .'
     A            W2CUBE         7Y 3B 14 39
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            OPCUM         20A  O 14 48
     A                                 15  4'Shipping weight . . . . . . . . .'
     A            W2SWGT         7Y 2B 15 39
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            OPWUM         15A  O 15 48
     A                                 16  4'Catch weight item . . . . . . . .'
     A            W2CWGT         1A  B 16 39
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  4'Catch weight Low/High variance %.'
     A            W2RG1C         3Y 1B 17 39EDTCDE(3)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 44'%'
     A            W2RG2C         3Y 1B 17 46EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 51'%'
     A                                 18  4'Build code  . . . . . . . . . . .'
     A            W2BLD          2A  B 18 39
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Shelf life (days) . . . . . . . .'
     A            W2SLIF         3Y 0B 19 39EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 44'Notify (days):'
     A            W2NDAY         3Y 0B 19 59EDTCDE(4)
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20  4'Lot code item . . . . . . . . . .'
     A            W2LOT          1   B 20 39
     A  40                                  DSPATR(RI)
     A N40N90                               DSPATR(UL)
     A  40                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 22  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =View warehouse qty  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23  2'F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  19990620  205421  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                  1  2'IT112.01E'
     A                                  1 33'Item Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 60'General information'
     A                                      DSPATR(HI)
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  B  8 31
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                  9  4'Description . . . . . . .'
     A            W1DSC1        40A  B  9 31
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
416 DA***         W1DSC2        40A  B 10 31
416 DA* 22                                  DSPATR(RI)
416 DA*N22N90                               DSPATR(UL)
416 DA* 02                                  DSPATR(PC)
416 DA* 90                                  DSPATR(PR)
416 DA*                                     CHECK(LC)
     A                                 11  4'Section . . . . . . . . .'
     A            W1STYP         1A  B 11 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                 12  4'Department  . . . . . . .'
     A            W1WHDP         5A  B 12 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 13  4'Designation area  . . . .'
     A            W1DESG         5A  B 13 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Activity zone . . . . . .'
     A            W1ACTV         2A  B 14 31
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Broken case item  . . . .'
     A            W1RPKG         1A  B 15 31
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 41'Full case item . . . .'
     A            W1ITM2        15A  B 15 65
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  4'Date sensitive  . . . . .'
     A            W1FLGD         1A  B 16 31
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  4'Allow bulk picks  . . . .'
     A            W1FLGB         1A  B 17 31
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  4'UPC code  . . . . . . . .'
     A            W1UPC         15A  B 18 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Vendor number . . . . . .'
     A            W1VEN#        10Y 0B 19 31EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20  4'Vendor item number  . . .'
     A            W1VIT#        15A  B 20 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21  4'Replenisment level  . . .'
     A            W1REPL         5Y 0B 21 31EDTCDE(4)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21 37'normal units'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =View warehouse quantities  F12=Can-
     A                                      cel'
     A                                      COLOR(BLU)
     A            HDTYPE        40   O  2 24
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  19990620  205421  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF09
     A                                      CF11
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'IT112.03'
     A                                  1 33'Item Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Tie/High information'
     A                                      DSPATR(HI)
     A                                  5  2'Item     :'
     A            WOITEM        50A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  6'Manufacturers Tie/High . . .'
     A            W3MTIE         4Y 0B  9 36EDTCDE(4)
     A  36                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 41'/'
     A            W3MHI          2Y 0B  9 43EDTCDE(4)
     A  37                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 38'Preferred'
     A                                 12  6'Slot type'
     A                                      DSPATR(UL)
     A                                 12 20'Tie   /   High'
     A                                      DSPATR(UL)
     A                                 12 38' (Y,'' '') '
     A                                      DSPATR(UL)
     A            W3ST1          2A  B 13  9
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3TIE1         4Y 0B 13 20EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                 13 26'/'
     A            W3HI1          2Y 0B 13 31EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            W3PRF1         1A  B 13 41
     A  61                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3ST2          2A  B 14  9
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3TIE2         4Y 0B 14 20EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A                                 14 26'/'
     A            W3HI2          2Y 0B 14 31EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A            W3PRF2         1A  B 14 41
     A  62                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3ST3          2A  B 15  9
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3TIE3         4Y 0B 15 20EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                 15 26'/'
     A            W3HI3          2Y 0B 15 31EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A            W3PRF3         1A  B 15 41
     A  63                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3ST4          2A  B 16  9
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3TIE4         4Y 0B 16 20EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                 16 26'/'
     A            W3HI4          2Y 0B 16 31EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A            W3PRF4         1A  B 16 41
     A  64                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3ST5          2A  B 17  9
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3TIE5         4Y 0B 17 20EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                 17 26'/'
     A            W3HI5          2Y 0B 17 31EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A            W3PRF5         1A  B 17 41
     A  65                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =Pattern  F11=Select tie/high  F12=-
     A                                      Cancel'
     A                                      COLOR(BLU)
     A            HDTYPE        40   O  2 23
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
