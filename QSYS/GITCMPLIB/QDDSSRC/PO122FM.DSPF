     A*%%TS  SD  19970729  135426  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PO122FM   PO detail maint - Change screen
     A*  08 October, 1996
     A*  Hemant Kapadia
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
415 AA*    04/25/01  HNK  4.15
     A*      - Changed to enable qty receive fields if the item has '*'
     A*        in right most position of designation code.
416aAA*    10/18/01  RH   4.16a
     A*      - Fix - increase catch weight to 9.2
     A*
500 AA*    11/10/05  MLB  5.00
     A*      - Enh - Added buyer number/name to display.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO122.01'
     A                                  1 28'PO Detail File Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'PO Number:'
     A            $PPO           9A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Sequence number  . . . . .'
     A            W1SEQ          5Y 0B  9 32
     A N21N90N91                            DSPATR(UL)
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 10  4'Item . . . . . . . . . . .'
     A            W1ITM         15A  B 10 32
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                 11  4'Normal qty ordered . . . .'
     A            W1QOR1         5Y 0B 11 32EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Breakdown 1 qty ordered  .'
     A            W1QOR2         5Y 0B 12 32EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Breakdown 2 qty ordered  .'
     A            W1QOR3         5Y 0B 13 32EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Catch weight received  . .'
416aDA**          W1DWGT         7Y 2B 14 32
416a A            W1DWGT         9Y 2B 14 32
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 14 46'Average (Y/N)  . . . . . .'
     A            W1AVG          1A  B 14 73
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
500 AA                                 15  4'Buyer number/name  . . . .'
500 AA            W1BNBR         3Y 0O 15 32
500 AA                                      EDTCDE(3)
500 AA            W1BNAM        40A  O 15 36
     A                                 11 46'Normal qty received  . . .'
415 DA*           W1QRC1         5  0O 11 73
415 AA            W1QRC1         5Y 0B 11 73
     A                                      EDTCDE(4)
415 AA  33                                  DSPATR(UL)
415 AA N33                                  DSPATR(PR)
     A                                 12 46'Breakdown 1 qty received .'
415 DA*           W1QRC2         5  0O 12 73
415 AA            W1QRC2         5Y 0B 12 73
     A                                      EDTCDE(4)
415 AA  34                                  DSPATR(UL)
415 AA N34                                  DSPATR(PR)
     A                                 13 46'Breakdown 2 qty received .'
415 DA*           W1QRC3         5  0O 13 73
415 AA            W1QRC3         5Y 0B 13 73
     A                                      EDTCDE(4)
415 AA  35                                  DSPATR(UL)
415 AA N35                                  DSPATR(PR)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel -
     A                                      F21=Add FS-WMS item'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
