     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  ER610FM
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
     A*
416 AA*    09/17/01  HNK  4.16
     A*      - Created
416aAA*    09/25/02  MLB  4.16a
     A*      - Added time range to screen 1.
     A*
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010807  152539  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ER610.01'
     A                                  1 27'Task Summary by User'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
415 AA                                  8  4'Warehouse . . . . . . . . . .'
415 AA            W1WHSE         3Y 0B  8 35
415 AA N24N90                               DSPATR(HI)
415 AA  24                                  DSPATR(RI)
415 AA N24N90                               DSPATR(UL)
415 AA  04
415 AAO 24                                  DSPATR(PC)
415 AA  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9  4'Employee number  . . . . . .'
     A            W1EMP#    R     Y00B  9 34REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 14'Or'
     A                                 11  4'Crew . . . . . . . . . . . .'
     A            W1CREW         6   B 11 34
415 AA N25N90                               DSPATR(HI)
415 AA  25                                  DSPATR(RI)
415 AA N25N90                               DSPATR(UL)
415 AA  05
415 AAO 25                                  DSPATR(PC)
415 AA  90                                  DSPATR(PR)
416aDA*                                13  4'Start date . . . . . . . . .'
416aMA                                 13  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B 13 34EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416aAA                                 13 41'to'
416aAA            W1TODT         6Y 0B 13 44EDTCDE(4)
416aAA N23N90                               DSPATR(HI)
416aAA  23                                  DSPATR(RI)
416aAA N23N90                               DSPATR(UL)
416aAA  03                                  DSPATR(PC)
416aAA  90                                  DSPATR(PR)
     A                                 13 59'Date (MMDDYY), Blank'
416aDA*                                14  4'End date . . . . . . . . . .'
416aAA                                 14  4'Time range . . . . . . . . .'
416aDA*           W1TODT         6Y 0B 14 34EDTCDE(4)
416aAA            W1FMTM         4Y 0B 14 34EDTCDE(4)
416aAA N26N90                               DSPATR(HI)
416aAA  26                                  DSPATR(RI)
416aAA N26N90                               DSPATR(UL)
416aAA  06                                  DSPATR(PC)
416aAA  90                                  DSPATR(PR)
416aAA                                 14 41'to'
416aAA            W1TOTM         4Y 0B 14 44EDTCDE(4)
416aAA N27N90                               DSPATR(HI)
416aAA  27                                  DSPATR(RI)
416aAA N27N90                               DSPATR(UL)
416aAA  07                                  DSPATR(PC)
416aAA  90                                  DSPATR(PR)
416aAA                                 14 59'Time (HHMM), Blank'
     A                                 16  4'Include merge pieces . . . .'
     A            W1MRG          1A  B 16 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 59'Y=Yes, N=No'
     A                                 23  2'F3=Exit   F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
