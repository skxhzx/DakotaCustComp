     A****************************************************************
     A*
     A* OR652FM   WC RF ITEM SELECTION - DISPLAY
     A*
     A*---------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    01/26/09  CWM  5.20
     A*      - Enh: Changed stop from 2 to 3 character.
     A*
     A****************************************************************
     A*%%TS  SD  19980904  154229  ROCKY       REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*                                     CA03
     A*                                     CA05
     A*                                     CA12
     A          R SCREEN1
     A*%%TS  SD  19970725  170555  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A                                      CF05
     A                                      CF12
     A  98                                  ALARM
     A                                  1  2'OR652.01'
     A                                  1 31'RF Item Selection'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Warehouse:'
     A            W1WHSE         3Y 0O  7 14EDTCDE(4)
     A                                  8  2'Department:'
     A            W1WHDP         5A  O  8 14
     A                                  9  3'  Trans #:'
     A            W1TRN#         7Y 0O  9 14EDTCDE(3)
     A                                 10  3'Pick slot:'
     A            W1TDIS        12A  O 10 14
     A                                 11  4'    Item:'
     A            W1ITEM        65A  O 11 14
     A                                 12  4'Quantity:'
     A            W1QTY          7Y 0B 12 14EDTCDE(4)
     A                                 14  4'    Scan:'
     A            W1SCAN        12A  B 14 14CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  3'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A          R DETAIL2                   SFL
     A*%%TS  SD  19941108  194002  DAVE        REL-V2R1M1  5738-PW1
     A  85                                  SFLNXTCHG
     A            W2LINE         3Y 0O 15  5EDTCDE(3)
     A            W2CWGT         7Y 2B 15 14
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A          R HEAD2                     SFLCTL(DETAIL2)
     A*%%TS  SD  19980415  122451  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
410 AA  98                                  ALARM
410 AA                                      CF20
     A                                      CF05
     A                                      CF12
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'OR652.02'
     A                                  1 31'RF Item Selection'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Catch Weight'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 30
     A                                  6  2'Route/Stop  . . .'
     A            W2RTE          5A  O  6 21
     A                                  6 27'/'
520 DA*           LBSTOP         2Y 0O  6 29EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  6 29EDTCDE(3)
     A                                  7  2'Dept./Section . .'
     A            LBWHDP         5A  O  7 21
     A            WDDESC        30A  O  7 30
     A            W2SECT         5A  O  7 65
     A                                  8  2'Order/Sequence# .'
     A            LBSEQ     R        O  8 31REFFLD(LBREC/LBSEQ *LIBL/LABEL)
     A                                      EDTCDE(3)
     A            LBORD          7Y 0O  8 21EDTCDE(3)
     A                                  8 29'/'
     A                                  9  2'Item  . . . . . .'
     A            LBITEM        15A  O  9 21
     A            ITDESC        30A  O  9 38
409 AA                                 10  2'Quantity  . . . .'
409 AA            LBQPCK         5Y 0O 10 21EDTCDE(3)
409 AA            UMDESC        30A  O 10 28
     A                                 12  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 14  5'Line'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 13'Catch Weight'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R DETAIL3                   SFL
     A*%%TS  SD  19941108  194002  DAVE        REL-V2R1M1  5738-PW1
     A  85                                  SFLNXTCHG
     A            LBWHDP    R        O 11  2REFFLD(LJREC32/LBWHDP *LIBL/LABEL32)
     A            LBQPCK    R        O 11 10REFFLD(LJREC32/LBQPCK *LIBL/LABEL32)
     A                                      EDTCDE(3)
     A            LBITEM    R        O 11 20REFFLD(LJREC32/LBITEM *LIBL/LABEL32)
     A            ITDESC        30A  O 11 37
     A            LBORD     R        O 11 71REFFLD(LJREC32/LBORD *LIBL/LABEL32)
     A                                      EDTCDE(3)
     A*
     A          R HEAD3                     SFLCTL(DETAIL3)
     A*%%TS  SD  19980904  154229  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF10
     A*                                     CF12
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'OR652.03'
     A                                  1 31'RF Item Selection'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Merge Pickup'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 30
     A                                  6  2'Route/Stop  . . .'
     A            W3RTE          5A  O  6 21
     A                                  6 27'/'
520 DA*           LBSTOP         2Y 0O  6 29EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  6 29EDTCDE(3)
     A                                 10  2'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 10'Quantity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Item #         '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 37'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 71'Order #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh  F12=Cancel  F20=Accept-
410 AA                                        warning'
     A                                      COLOR(BLU)
     A*
     A          R CMDRC3
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F10=Continue  '
     A                                      COLOR(BLU)
