     A*%%TS  SD  20000503  110741  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UT760FM   Remove records from ALL 'transaction' files.
     A*  01 May 2000
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*
     A*
     A*
     A*
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT760.01'
     A                                  1 20'Remove Records from ALL Transactio-
     A                                      n Files'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Press F10=Continue to continue or '
     A                                      COLOR(BLU)
     A                                  5  2'Press F3=Exit or F12=Cancel to exi-
     A                                      t this job.'
     A                                      COLOR(BLU)
     A                                 12 12'Enter password  . . . . .'
     A            W1PSWD        10   B 12 39DSPATR(HI)
     A                                      DSPATR(ND)
     A                                      DSPATR(UL)
     A                                 23  2'F3=Exit   F5=Refresh   F10=Continu-
     A                                      e  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT760.02'
     A                                  1 20'Remove Records from ALL Transactio-
     A                                      n Files'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2' '
     A                                      DSPATR(PC)
     A                                  4  2'Press F10=Continue to set job para-
     A                                      meters or '
     A                                      COLOR(BLU)
     A                                  5  2'Press F3=Exit or F12=Cancel to exi-
     A                                      t this job.'
     A                                      COLOR(BLU)
     A                                  7  4'**** CAUTION **** CAUTION **** CAU-
     A                                      TION **** CAUTION **** CAUTION ****'
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A                                  8  4'****                              -
     A                                                                     ****'
     A                                      COLOR(RED)
     A                                  9  4'**** Before running this job, make-
     A                                       sure that all transactions    ****'
     A                                      COLOR(RED)
     A                                 10  4'**** have been closed and complete-
     A                                      d and ALL information has been ****'
     A                                      COLOR(RED)
     A                                 11  4'**** exported back to the host sys-
     A                                      tem.  This includes all re-    ****'
     A                                      COLOR(RED)
     A                                 12  4'**** ceiving, picking, inventory a-
     A                                      djustments, replenishments,    ****'
     A                                      COLOR(RED)
     A                                 13  4'**** customer returns and will cal-
     A                                      l orders have been processed   ****'
     A                                      COLOR(RED)
     A                                 14  4'**** to completion.               -
     A                                                                     ****'
     A                                      COLOR(RED)
     A                                 15  4'**** All records are DELETED from -
     A                                      the files. A complete backup   ****'
     A                                      COLOR(RED)
     A                                 16  4'**** of the FS-WMS data files libr-
     A                                      ary is HIGHLY recommended be-  ****'
     A                                      COLOR(RED)
     A                                 17  4'**** fore running this job.       -
     A                                                                     ****'
     A                                      COLOR(RED)
     A                                 18  4'****                              -
     A                                                                     ****'
     A                                      COLOR(RED)
     A                                 19  4'**** CAUTION **** CAUTION **** CAU-
     A                                      TION **** CAUTION **** CAUTION ****'
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A                                 23  2'F3=Exit   F5=Refresh   F10=Continu-
     A                                      e  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A                                      CF04
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT760.03'
     A                                  1 20'Remove Records from ALL Transactio-
     A                                      n Files'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W3WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*                                 9  4'Before date  . . . . . . . .'
     A*           W3DATE         6Y 0B  9 34EDTCDE(4)
     A*N22N90                               DSPATR(HI)
     A* 22                                  DSPATR(RI)
     A*N22N90                               DSPATR(UL)
     A*                                     DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A*                                 9 53'(MMDDYY)'
     A                                 10  4'Place on job queue ? . . . .'
     A            W3JOBQ         1A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 53'(Y=Yes, N=No)'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F10=Continue  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN4
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT760.04'
     A                                  1 20'Remove Records from ALL Transactio-
     A                                      n Files'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2' '
     A                                      DSPATR(PC)
     A                                  5  2'Press F20=Accept to confirm your c-
     A                                      hoice for Remove Rcds from All Tran-
     A                                      s. Files.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A                                 10  4'Warehouse  . . . . . . . . .'
     A            W3WHSE         3Y 0O 10 34EDTCDE(4)
     A                                 11  4'Before date  . . . . . . . .'
     A            W3DATE         6Y 0O 11 34EDTCDE(4)
     A                                 11 53'(MMDDYY)'
     A                                 12  4'Place on job queue ? . . . .'
     A            W3JOBQ         1A  O 12 34
     A                                 12 53'(Y=Yes, N=No)'
     A                                 15  4'**********************************-
     A                                      ***********************************-
     A                                      ******'
     A                                      COLOR(RED)
     A                                 16  4'****                              -
     A                                                                         -
     A                                        ****'
     A                                      COLOR(RED)
     A                                 17  4'****  This is your FINAL chance to-
     A                                       cancel this job.  If you are unsur-
     A                                      e ****'
     A                                      COLOR(RED)
     A                                 18  4'****  about running this job, pres-
     A                                      s F3=Exit or F12=Cancel.           -
     A                                        ****'
     A                                      COLOR(RED)
     A                                 19  4'****                              -
     A                                                                         -
     A                                        ****'
     A                                      COLOR(RED)
     A                                 20  4'**********************************-
     A                                      ***********************************-
     A                                      ******'
     A                                      COLOR(RED)
     A                                 23  2'F3=Exit  F20=Accept  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
