     A*%%TS  SD  20030411  151346  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PO245FM   Prompt for Retail UPC
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*
     A*  Compile Options:
     A*   RSTDSP(*YES)
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*   - This is not a standard screen entry file. This file
     A*     uses the WINDOW keyword. The standard panel and message
     A*     subfile are linked to the WINDOW keyword.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
417 AA*  04/11/03  MLB  4.17
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA10
     A                                      CA12
     A*
     A*
     A* Define Window format.
     A*
     A          R SCREEN1W
     A                                      WINDOW(&WROW &WCOL 4 50 *NOMSGLIN)
     A                                      USRRSTDSP
     A                                      WDWTITLE((*TEXT 'Prompt for Retail -
     A                                      UPC') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit F9=Skip F1-
     A                                      0=Override warning') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      OVERLAY
     A            WROW           3S 0P
     A            WCOL           3S 0P
     A*
     A* Define Window layout.
     A*
     A          R SCREEN1
     A                                      WINDOW(SCREEN1W)
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      RTNCSRLOC(&CRCD &CFLD &CPOS)
     A                                      OVERLAY
     A            CRCD          10A  H      TEXT('Hidden: Cursor rcdfmt')
     A            CFLD          10A  H      TEXT('Hidden: Cursor field')
     A            CPOS           4S 0H      TEXT('Hidden: Cursor fld pos')
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  2  1'Scan Retail UPC:'
     A            W1SCAN        20A  B  3  1
     A                                      CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Control Record - Window
     A*
     A*
     A*   Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(04)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A                                      WINDOW(SCREEN1W)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      WINDOW(SCREEN1W)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(04)
     A                                      CLRL(1)
     A*
     A          R DUMMY
     A                                      KEEP
     A                                      ASSUME
     A                                  1  2' '
