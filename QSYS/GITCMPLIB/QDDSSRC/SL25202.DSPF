     A*%%TS  SD  20000128  153211  DAVE        REL-V4R4M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/25/99  DAS  4.13
     A*     - Moved lines down to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
530aAA*   10/22/09  RH   5.30a
     A*     - Renamed to SL25201.
     A*     - Enh: Add cw field and screen 3 if one of the items is cw.
640aAA*   06/22/12  LMC  6.40a
     A*     - Renamed to SL25202.
     A*     - Enh: Added W1MEMO field to be stored in ITEMADJ file.
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000128  153211  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL252.01'
     A                                  1 23'Change Existing Item Number in Slo-
     A                                      t'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72'Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse  :'
     A            WOWHSE         3A  O  3 15
     A            WHDESC        30A  O  3 22
     A                                  4  2'Slot Dept. :'
     A            WDCODE         5A  O  4 15
     A            WDDESC        30A  O  4 22
     A                                  5  2'Slot       :'
     A            FMDISP        12A  O  5 15
     A                                  6  2'Slot type  :'
     A            W1STYP         1A  O  6 15
     A                                  7  2'Status     :'
     A            SSDESC        30A  O  7 15
     A                                  8  2'Item Dept. :'
     A            W1WHDP         5A  O  8 15
     A                                  9  2'Item       :'
     A            WOITEM        55A  O  9 15
     A            W1LBLS        12A  O 10  2DSPATR(HI)
     A            W1ITMS        55A  O 10 15DSPATR(HI)
     A                                 11  2'Quantity   :'
     A            W1CURQ        30A  O 11 15
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 17  4'New item number . . .'
     A            W1ITEM        15A  B 17 27
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
640aAA                                 19  4'Memo. . . . . . . . .'
640aAA            W1MEMO        11A  B 19 27
640aAA                                      DSPATR(UL)
     A                                 22  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23  2'F8=View slot info  F9=View slot qt-
     A                                      ys  F21=Tie/High maintenance'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000128  091837  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL252.02'
     A                                  1 23'Change Existing Item Number in Slo-
     A                                      t'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72'Verify'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse  :'
     A            WOWHSE         3A  O  3 15
     A            WHDESC        30A  O  3 22
     A                                  4  2'Slot Dept. :'
     A            WDCODE         5A  O  4 15
     A            WDDESC        30A  O  4 22
     A                                  5  2'Slot       :'
     A            FMDISP        12A  O  5 15
     A                                  6  2'Slot type  :'
     A            W1STYP         1A  O  6 15
     A                                  7  2'Status     :'
     A            SSDESC        30A  O  7 15
     A                                  8  2'Quantity   :'
     A            W1CURQ        30A  O  8 15
     A                                 11  2'Verify changes, press F10.'
     A                                      COLOR(BLU)
     A                                 13 22'From Information'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 55'To Information'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 15  2'Item:'
     A            W2FITM        15   O 15 16
     A            W2TITM        15   O 15 49
     A                                 16  2'Description:'
     A            W2FDSC        30   O 16 16
     A            W2TDSC        30   O 16 49
     A                                 17  2'Department:'
     A            W2FWDP         5   O 17 16
     A  21                                  DSPATR(RI)
     A            W2TWHD         5   O 17 49
     A  21                                  DSPATR(RI)
     A                                 18  2'Section:'
     A            W2FSTP         1   O 18 16
     A  22                                  DSPATR(RI)
     A            W2TSTP         1   O 18 49
     A  22                                  DSPATR(RI)
     A                                 19  2'Designation:'
     A            W2FDSG         5   O 19 16
     A  23                                  DSPATR(RI)
     A            W2TDSG         5   O 19 49
     A  23                                  DSPATR(RI)
640aAA                                 20  2'Memo:'
640aAA            W2MEMO        11   O 20 16
     A                                 23  2'F10=Change item  F12=Cancel'
     A                                      COLOR(BLU)
530aAA          R SCREEN3
530aAA*%%TS  SD  20000128  153211  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A*                                     CF08
     A*                                     CF09
     A*                                     CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL252.01A'
     A                                  1 23'Change Existing Item Number in Slo-
     A                                      t'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72'Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse  :'
     A            WOWHSE         3A  O  3 15
     A            WHDESC        30A  O  3 22
     A                                  4  2'Slot Dept. :'
     A            WDCODE         5A  O  4 15
     A            WDDESC        30A  O  4 22
     A                                  5  2'Slot       :'
     A            FMDISP        12A  O  5 15
     A                                  6  2'Slot type  :'
     A            W1STYP         1A  O  6 15
     A                                  7  2'Status     :'
     A            SSDESC        30A  O  7 15
     A                                  8  2'Item Dept. :'
     A            W1WHDP         5A  O  8 15
     A                                  9  2'Item       :'
     A            WOITEM        55A  O  9 15
     A            W1LBLS        12A  O 10  2DSPATR(HI)
     A            W1ITMS        55A  O 10 15DSPATR(HI)
     A                                 11  2'Quantity   :'
     A            W1CURQ        30A  O 11 15
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 17  4'New item number . . .'
     A            W1ITEM        15A  O 17 27
530a A                                 18  4'Catch Wt. . . . . . .'
530a A            W3CWT          9Y 2B 18 27DSPATR(UL)
530a A                                      EDTCDE(4)
640aAA                                 19  4'Memo. . . . . . . . .'
640aAA            W1MEMO        11A  B 19 27
640aAA                                      DSPATR(UL)
     A                                 22  2'F4=Prompt  F5=Refresh  F12=Cancel'
530aAA                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
