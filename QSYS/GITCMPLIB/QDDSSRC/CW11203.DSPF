     A*%%TS  SD  20111115  213408  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CW112FM   Catch weiight maint.  (one whse) - Display
     A*  21 July 1999
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
412aAA*  08/12/99  HNK  4.12a
     A*    - Quantity and Stop number added on HEAD1 record.
     A*
412dAA*  08/30/99  HNK  4.12d
     A*    - Item description added.
     A*
413 AA*  10/25/99  DAS  4.13
     A*    - Conditioned CA12 with indicator 92.
     A*    - Added comment next to entry field about assumed
     A*      decimal point.
416 AA*  01/21/02  RH   4.16
     A*    - Added 'CLOSING TRANS' to display if called from PT132.
416aAA*  07/29/02  MLB  4.16a
     A*    - FIX: Changed definition of W1SEQ to 3,0 from 2,0.
     A*           Changed SFLLIN to 2 from 3.
417aAA*  08/28/03  RH   4.17a
     A*    - ENH: Add route to screen 3.
CBIaAA*  08/14/03  RH   CBIa
     A*    - ENH: Changed to not scan catchweight so field exit will work
500aAA*  07/11/05  RH   500a
     A*    - FIX: Reversed CBIa change, field exit was not working.
     A*
520 AA*  01/24/09  CWM  5.20
     A*    - Enh: Changed stop from 2 to 3 for host stop mods.
     A*
640aAA*  04/14/11  JCJ  6.40a
     A*    - Enh: Renamed display file from CW112FM to CM11201 per
     A*           GUI standards for screen changes.
     A*    - Enh: Added UOM and Average Catch Weight to screen.
     A*
640BAA*  11/15/11  JCJ  6.40B
     A*    - Enh: Increase item description length.
     A*
700aAA*  03/24/16  LMC  7.00a
     A*    - Renamed from CW11202 to CW11203
     A*    - Enh: Adding logic for Metric.
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A N92                                  CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  19990802  105248  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
416aDA*           W1SEQ          2Y 0O  9  3EDTCDE(3)
416aMA            W1SEQ          3Y 0O  9  2EDTCDE(3)
     A            W1CWGT         7Y 0B  9  6EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*           W1RGHT         5A  B  9 30
     A* 40                                  DSPATR(RI)
     A*N40N90                               DSPATR(UL)
     A* 20                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A*
     A***DS3                                SFLLIN(0010)
416aDA* *DS3                                SFLLIN(0003)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20111115  213408  JEFF        REL-V6R1M0  5761-WDS
     A  *DS3                                SFLLIN(0002)
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0080)
     A                                      OVERLAY
     A  98                                  ALARM
700aAA                                      CF07
     A                                      CF20
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'CW112.03'
     A                                  1 24'Catch Weight Entry Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
640aDA***                               2 60'Route:'
640aDA***         WORTE          5A  O  2 68
640aDA***                               2 46'Route:'
640aDA***         WORTE          5A  O  2 53
     A                                  2 48'Route:'
     A            WORTE          5A  O  2 55
640bDA***                               2 64'UOM:'
640bDA***         WOUCOD         2A  O  2 69
     A                                  2 65'UOM:'
     A            WOUCOD         2A  O  2 70
     A                                  3  2'Item:'
     A            $PITEM        15A  O  3  8
640bDA***         WODESC        15A  O  3 24
     A            WODESC        25A  O  3 24
640bDA***                               3 48'Qty:'
     A                                  3 50'Qty:'
640bDA***         WOPQTY         5Y 0O  3 53EDTCDE(3)
     A            WOPQTY         5Y 0O  3 55EDTCDE(3)
640bDA***                               3 63'Stop:'
     A                                  3 64'Stop:'
520 DA*           WOSTOP         2Y 0O  3 68EDTCDE(3)
640bDA***         WOSTOP         3Y 0O  3 69EDTCDE(3)
     A            WOSTOP         3Y 0O  3 70EDTCDE(3)
     A                                  4  2'Tran:'
     A            $PTRN#         7Y 0O  4  8EDTCDE(3)
     A                                  4 18'AvgWgt:'
     A            WOUWGT         7Y 2O  4 26EDTCDE(3)
640aDA***                               4 26'Catch Weights Entered:'
640bDA***                               4 36'Weights Entered:'
     A                                  4 38'Weights Entered:'
640bDA***         WOCW#          7Y 0O  4 53EDTCDE(3)
     A            WOCW#          7Y 0O  4 55EDTCDE(3)
640bDA***                               4 62'Total:'
     A                                  4 63'Total:'
640bDA***         WOTCW          9Y 2O  4 69EDTCDE(3)
     A            WOTCW          9Y 2O  4 70EDTCDE(3)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5 50'*CLOSING TRANSACTION*'
     A N92                                  DSPATR(ND)
     A                                  7 58'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7  6'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 32'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 45'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 71'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aAA            W1KGLB         3A  O  8  1
     A            W1ADDW         7Y 0B  8  6EDTCDE(4)
500a A                                      CHECK(ER)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 14'(assumed decimal point with 2 deci-
     A                                      mal places)'
     A                                      COLOR(BLU)
412 DA*           W1ADDR         5A  B  8 30
     A* 38                                  DSPATR(RI)
     A*N38N90                               DSPATR(UL)
     A* 18                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
700aAA            CMDLN1        79A  O  1  2COLOR(BLU)
700aDA*                                 1  2'F5=Refresh  F12=Cancel  F20=+
700aDA*                                      Accept warning'
700aDA*                                     COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
