     A*%%TS  SD  20050223  064034  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  SL270FM   Create virtual slot.
     A*  22 February 05
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
500 AA*    02/22/05  DAS  5.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20050223  064034  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SL270.01'
     A                                  1 12'Create VPick Slot'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Dept'
     A            W1WHDP         5A  B  4  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02
     AO 20                                  DSPATR(PC)
     A                                  5  2'Slot'
     A            W1DISP        12A  B  5  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  6  2'Item'
     A            W1ITEM        15A  B  6  7
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
