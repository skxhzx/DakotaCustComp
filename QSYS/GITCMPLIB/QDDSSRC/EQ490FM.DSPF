     A********************************************************************
     A*   Copyright (C) 2017 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  EQ490FM  RF Route Equipment - Return w/o Route Id
     A*  Feb 9, 2017
     A*  Retha Davis
     A*
     A********************************************************************
     A*  Revisions:
     A*
710 AA*    02/09/17  RBD   7.10
     A*      - Created.
     A*
     A********************************************************************
     A*  Client Custom Revisions:
     A*
     A*
     A********************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A********************************************************************
     A*         Scan equipment code and enter qty
     A********************************************************************
     A          R SCREEN1
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'EQ490.01'
     A                                  2  2'Return Equip'
     A                                      DSPATR(HI)
     A                                  3  2' w/o Route Id'
     A                                      DSPATR(HI)
     A                                  5  2' Whse:'
     A            W1WHSE         3Y 0B  5  9EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'Equip:'
     A            W1EQ           8A  B  7  9DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 10  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(12)
     A                                      CLRL(1)
