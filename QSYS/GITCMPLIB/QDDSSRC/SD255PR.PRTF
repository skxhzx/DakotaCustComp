     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  SD255PR    FTP Import Slot Check Digit Updates - Post
     A*  07 January 2010
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*    01/07/10  MLB  5.30
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'SD255'
     A                                      SPACEA(002)
     A                                     2
     A                                      'Type'
     A                                     8
     A                                      'Dept '
     A                                    15
     A                                      'Slot        '
     A                                    29
     A                                      'Ck Digit 1'
     A                                    41
     A                                      'Ck Digit 2'
     A                                    53
     A                                      'Ck Digit 3'
     A                                   100
     A                                      'Message'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            ICDTYP    R        O     3
     A                                      REFFLD(ICDREC/ICDTYP IMPFCKDGT)
     A            ICDWHD    R        O     8
     A                                      REFFLD(ICDREC/ICDWHD IMPFCKDGT)
     A            ICDDSP    R        O    15
     A                                      REFFLD(ICDREC/ICDDSP IMPFCKDGT)
     A            ICDDG1    R        O    32
     A                                      REFFLD(ICDREC/ICDDG1 IMPFCKDGT)
     A                                      EDTCDE(J)
     A            ICDDG2    R        O    44
     A                                      REFFLD(ICDREC/ICDDG2 IMPFCKDGT)
     A                                      EDTCDE(J)
     A            ICDDG3    R        O    56
     A                                      REFFLD(ICDREC/ICDDG3 IMPFCKDGT)
     A                                      EDTCDE(J)
     A            DETMSG        30A  O   100
     A*
     A          R GRNDTTL
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Import records :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Invalid slots        :'
     A                                      SPACEB(001)
     A            NOSLOT         7S 0O    27
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Invalid check digits :'
     A                                      SPACEB(001)
     A            BADDGT         7S 0O    27
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Too many codes sent  :'
     A                                      SPACEB(001)
     A            MAXSNT         7S 0O    27
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Max digits exceeded  :'
     A                                      SPACEB(001)
     A            MAXDGT         7S 0O    27
     A                                      EDTCDE(J)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SD255: End of Report')
     A*
