     A*%%TS  SD  20080924  104256  BFC         REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LU110FM
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
     A*
     A*    08/09/01  HNK  4.15
     A*      - Add warehouse and Crew code.
     A*
     A*    09/24/08  GJA  6.00
     A*      - Cleaned up and revamped for suspended transactions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080924  104256  BFC         REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LU110.01'
     A                                  1 27'Transactions Lookup by User'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  5 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AO 24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  4'Employee number  . . . . . .'
     A            W1EMP#    R     Y00B  7 34REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 14'Or'
     A                                  9  4'Crew . . . . . . . . . . . .'
     A            W1CREW         6A  B  9 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05
     AO 25                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B 11 34EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 59'Date (MMDDYY)'
     A                                 11 41'to'
     A            W1TODT         6Y 0B 11 44
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 12  4'Time range . . . . . . . . .'
     A            W1FMTM         6  0B 12 34EDTCDE(4)
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 59'Time (HHMMSS)'
     A                                 12 41'to'
     A            W1TOTM         6  0B 12 44
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Exit   F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                  2 72TIME
     A                                 15  4'Task . . . . . . . . . . . .'
     A            W1TASK         6A  B 15 34
     A                                 15 59'*ALL'
     A                                 16  4'Zone/Department. . . . . . .'
     A            W1WHDP         5A  B 16 34
     A                                 16 59'*ALL'
     A                                 17  4'Category . . . . . . . . . .'
     A            W1CAT          6A  B 17 34
     A                                 17 59'*ALL'
     A                                 18  4'Sub Category . . . . . . . .'
     A            W1SCAT         6A  B 18 34
     A                                 18 59'*ALL'
     A                                 19  4'Print. . . . . . . . . . . .'
     A            W1PRT          1A  B 19 34
     A                                 19 59'Y=Yes   N=No'
     A                                 20  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 20 34
     A                                 21  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 21 34
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
