     A*%%TS  SD  20011016  125320  HEMANT      REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  RH394FM  Item detail - Display
     A*  30 August, 2000
     A*  Hemant Kapadia
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
     A*    08/30/00  HNK  4.14
     A*      - Created.
     A*
520 AA*    01/28/09 CWM  5.20
     A*      - Enh: Add support for 3 digit stops.
     A*
530aAA*    12/20/09 GJA  5.30a
     A*      - Enh: To include quantity picked from LABELDO
     A*
640a A*    03/08/11 GJA  6.40a
     A*      - Enh: Add '*' to denote when voice picked
     A*
640b A*    09/01/11 GJA  6.40b
     A*      - Enh: Add LABELDO date and time to RH394.03
650a A*    02/05/14 RH   6.50a
     A*      - Enh: Add Pallet id pallet # to view 3.
     A*        Rename format to RH39404
700a A*    06/08/15 GJA  7.00a
     A*      - Fix:  Not sending correct order number from subfile 1
710a A*    12/22/17 LMC  7.10a
     A*      - Fix:  Not sending correct order number and label number
     A*        from all the subfile views.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions - Southwest Traders
     A*
SWTa A*    06/06/19 NLK  SWTa
     A*      - ADD:  CORD is 8 long from ORDHEXA
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH394 '
     A                                  1 31'Pallet Lookup by Route'
     A                                      DSPATR(HI)
     A                                  2 35'Item Detail'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20000831  172056  HEMANT      REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  1 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  1 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2  2'Item:'
     A            W1ITEM        15A  O  2  8
     A                                  2 47'Position to label . . . .'
     A            PTINP2         7Y 0B  2 74EDTCDE(4)
     A            ITDESC        30   O  3  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20011016  125320  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LBLBL#         7Y 0O 11  6EDTCDE(3)
     A            LBTRN#         7Y 0O 11 14EDTCDE(3)
520 DA*           LBSTOP         2Y 0O 11 25EDTCDE(3)
520 MA            LBSTOP         2Y 0H
700aAA            LBORD          7Y 0H
520 AA            DSPSTP         3Y 0O 11 23EDTCDE(3)
     A            PTEMP#         5Y 0O 11 27EDTCDE(3)
     A            W1NAME        25A  O 11 33
640aDA*****       LBOQTY         5Y 0O 11 59EDTCDE(3)
640aMA            LBOQTY         5Y 0O 11 61EDTCDE(3)
640aAA            VYN            1   O 11 59
     A            LBQPCK         5Y 0O 11 69EDTCDE(3)
     A            W1TYPE         5   O 11 76
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20011016  125320  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
530aAA                                  9 59'Voice'
     A                                      DSPATR(HI)
     A                                  9 69'Qty'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  6'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Empl#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'Name                     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
530aAA                                 10 59'Select  '
530aAA                                      DSPATR(HI)
530aAA                                      DSPATR(UL)
     A                                 10 69'Picked'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76' Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 76'Label'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20000901  131928  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LBLBL#         7Y 0O 11  7EDTCDE(3)
     A            LBORD          7Y 0O 11 17EDTCDE(3)
SWTaMA            W1CORD         8A  O 11 26
SWTaDA*          *OHCORD         7Y 0O 11 26EDTCDE(3)
SWTaMA            W1CUST        11A  O 11 35
SWTADA*          *OHCUST        11Y 0O 11 35EDTCDE(3)
     A            W1CNAM        30A  O 11 47
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20000901  131928  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 16'Order ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Order #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35' Customer #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Name                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000901  131928  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
710aAA            LBORD          7Y 0H
     A            LBLBL#         7Y 0O 11  7EDTCDE(3)
     A            LBCUBE         9Y 3O 11 17EDTCDE(3)
     A            LBSWGT         9Y 2O 11 29EDTCDE(3)
640bAA            LDODTE        10   O 11 41
640bAA            LDOTIM         8   O 11 52
650aAA            LBPAL#         7Y 0O 11 63
650aAA            PSPAL#         7Y 0O 11 72EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000901  131928  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 18'     Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'   Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640bAA                                 10 41'Voice Date'
640bAA                                      DSPATR(HI)
640bAA                                      DSPATR(UL)
640bAA                                 10 52'Voice Time'
640bAA                                      DSPATR(HI)
640bAA                                      DSPATR(UL)
650aAA                                 10 63'Pallet ID'
650aAA                                      DSPATR(HI)
650aAA                                      DSPATR(UL)
650aAA                                 10 73'Pallet#'
650aAA                                      DSPATR(HI)
650aAA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A**                                1  2'Type options, press Enter.'
     A**                                    COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
