     A*%%TS  SD  20021231  112820  ROCKY       REL-V4R2M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19930222  104338  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'EQ210.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 25'Equipment Question Maintenance'
     A                                      DSPATR(HI)
     A                                  3  2'Type:'
     A            EYDESC    R        O  3  8REFFLD(EYREC/EYDESC *LIBL/EQUIPY)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20021231  112820  ROCKY       REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            EUSTAT    R        O 11  9REFFLD(EUREC/EUSTAT *LIBL/EQUIPQ)
     A            EUQSTN    R        O 11 15REFFLD(EUREC/EUQSTN *LIBL/EQUIPQ)
     A            EUIO      R        O 11 52REFFLD(EUREC/EUIO *LIBL/EQUIPQ)
     A            EUNORM    R        O 11 62REFFLD(EUREC/EUNORM *LIBL/EQUIPQ)
     A            EUWARN    R        O 11 73REFFLD(EUREC/EUWARN *LIBL/EQUIPQ)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19930221  160307  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Question                      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 50'Check '
     A                                      DSPATR(HI)
     A                                 10 50'In/Out'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 60'Normal'
     A                                      DSPATR(HI)
     A                                 10 60'Answer'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 70'Display'
     A                                      DSPATR(HI)
     A                                 10 70'Warning'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001        48   O  9  2
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19930222  104338  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A*                                     SLNO(03)
     A                                      SLNO(04)
     A                                  1 54'Position to code . . .'
     A            PTINP1         2A  B  1 78
     A            PTDMY1         1A  O  1  2
     A            PTDMY2        78A  O  2  2
     A*                                 2 54'Position to code . . .'
     A*           PTINP1         2A  B  2 78
     A*           PTDMY1        78A  O  1  2
     A*           PTDMY2        78A  O  3  2
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
