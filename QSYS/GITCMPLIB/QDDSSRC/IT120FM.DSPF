     A*%%TS  SD  19940830  143007  DAVE        REL-V2R1M1  5738-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT120FM   Inv. Adjustments
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
416aAA*  02/06/02  RH   4.16a
     A*    - Remove User from screen.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940830  143007  DAVE        REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT120.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 26'Inventory Adjustments by Slot'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  7 28
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
416a A*                                 9  4'User id  . . . . . . .'
416a A*           W1USER        10A  B  9 28
416a A* 24                                  DSPATR(RI)
416a A*N24N90                               DSPATR(UL)
416a A* 04
416a A*N20                                  DSPATR(PC)
416a A*N24                                  DSPATR(HI)
     A                                  9  4'Department . . . . . .'
     A            W1WHDP         5A  B  9 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                 11  4'Slot . . . . . . . . .'
409 MA            W1DISP        12A  B 11 28CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
