     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT320FM    View LICLOG file - Display
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
600 AA*    02/25/09  DAS  6.00  P#00045
     A*      - Created.
     A*
600aAA*    05/20/09  DAS  6.00a
     A*      - Added 4th view
     A*
600bAA*    06/10/09  DAS  6.00b
     A*      - Added w1trn# to window.
     A*
610aAA*    07/08/09  DAS  6.10a
     A*      - Increased sflhide from 100 to 110.
     A*
640aAA*    12/12/12  RBD  6.40a
     A*      - Copied view 1 to new view 5.
     A*      - Changed view 1:
     A*         Whittled Action Qty and Result Qty to 6 char.
     A*         Removed License and Corr License columns.
     A*         Expanded Emp# column to include last and first name.
     A*         Added 'Added' timestamp column.
     A*
650aAA*    02/12/13  LMC  6.50a
     A*      - Renamed display file from LT32001 to LT32002
     A*      - Enh: Change description of ITEM on selection screen to
     A*         License Item.
     A*         Add new field for Slot Item.
     A*         Add from and to date range.
650bAA*    04/02/13  LMC  6.50b
     A*      - Renamed display file from LT32002 to LT32002
     A*      - Enh: Added Action Code to selection screen.
650cAA*    10/10/14  LMC  6.50c
     A*      - Renamed display file from LT32003 to LT32004
     A*      - Enh: Added position cursor error indicators for
     A*        invalid dates.
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20090226  115153  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
610aMA            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
640aDA**          W1AQTY        10A  O  8  9
640aMA            W1AQTY         6A  O  8  9
640aDA**          W1RQTY        10A  O  8 20
640aMA            W1RQTY         6A  O  8 16
640aDA**          LLDISP    R        O  8 31REFFLD(LLREC/LLDISP *LIBL/LICLOG)
640aMA            LLDISP    R        O  8 23REFFLD(LLREC/LLDISP *LIBL/LICLOG)
640aDA**          LLLCNS    R        O  8 44REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
640aDA**          LLFLCNS   R        O  8 60REFFLD(LLREC/LLFLCNS *LIBL/LICLOG)
640aDA**          LLADDEMP  R        O  8 76REFFLD(LLREC/LLADDEMP *LIBL/LICLOG)
640aAA            W1EMP#NAM     24A  O  8 36
640aAA            W1ADDED       19A  O  8 61
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20090227  055426  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
640aDA**                                1 31'View Active Licenses'
640aMA                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6  9'Action'
     A                                      DSPATR(HI)
640aDA**                                6 20'Result'
640aMA                                  6 16'Result'
     A                                      DSPATR(HI)
640aDA**                                6 60'Corresponding'
640aDA**                                    DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA**                                7  9'Quantity  '
640aMA                                  7  9'Qty   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA**                                7 20'Quantity  '
640aMA                                  7 16'Qty   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA**                                7 31'Slot        '
640aMA                                  7 23'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA**                                7 44'License        '
640aDA**                                    DSPATR(HI)
640aDA**                                    DSPATR(UL)
640aDA**                                7 60'License        '
640aDA**                                    DSPATR(HI)
640aDA**                                    DSPATR(UL)
640aDA**                                7 76'Empl#'
640aAA                                  7 36'Employee                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aAA                                  7 61'Added Date and Time'
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20090226  163759  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
610aMA            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            LLLCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            LLOLCNS   R        O  8 25REFFLD(LLREC/LLOLCNS *LIBL/LICLOG)
     A            LLLTRN    R        O  8 41REFFLD(LLREC/LLLTRN *LIBL/LICLOG)
     A            LLTRN#    R        O  8 49REFFLD(LLREC/LLTRN# *LIBL/LICLOG)
     A            LLLBL#    R        O  8 57REFFLD(LLREC/LLLBL# *LIBL/LICLOG)
     A            LLITEM    R        O  8 65REFFLD(LLREC/LLITEM *LIBL/LICLOG)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20090227  055426  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 25'Original'
     A                                      DSPATR(HI)
     A                                  6 41'Log'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 57'Label# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*         R SFL2                      SFL
     A          R SFL3                      SFL
     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
610aMA            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            LLLCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            LLADDEUSR R        O  8 25REFFLD(LLREC/LLADDEUSR *LIBL/LICLOG)
     A            LLADDTS   R        O  8 36REFFLD(LLREC/LLADDTS *LIBL/LICLOG)
     A            LLEXCD    R        O  8 63REFFLD(LLREC/LLEXCD *LIBL/LICLOG)
     A          R SF3CTL                    SFLCTL(SFL3)
     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 63'Info'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'Added                     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 63'Code    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL4                      SFL
     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
610aMA            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            LLLCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            LLITEM    R        O  8 25REFFLD(LLREC/LLITEM *LIBL/LICLOG)
     A            LLPRTY    R        O  8 43REFFLD(LLREC/LLPRTY *LIBL/LICLOG)
     A            W1EXPD         6Y 0O  8 46EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  8 55EDTWRD('  /  /  ')
     A          R SF4CTL                    SFLCTL(SFL4)
     A*%%TS  SD  20090305  102548  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'Prty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'Expires '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'Entered '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
640aAA          R SFL5                      SFL
640aAA  21                                  SFLNXTCHG
640aAA            SFLHIDE      110A  H
640aAA            OPTION         2A  B  8  2
640aAA  20                                  DSPATR(PC)
640aAA N21N90                               DSPATR(UL)
640aAA  21                                  DSPATR(RI)
640aAA  90                                  DSPATR(PR)
640aAA            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
640aAA            W1AQTY         6A  O  8  9
640aAA            W1RQTY         6A  O  8 20
640aAA            LLDISP    R        O  8 31REFFLD(LLREC/LLDISP *LIBL/LICLOG)
640aAA            LLLCNS    R        O  8 44REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
640aAA            LLFLCNS   R        O  8 60REFFLD(LLREC/LLFLCNS *LIBL/LICLOG)
640aAA            LLADDEMP  R        O  8 76REFFLD(LLREC/LLADDEMP *LIBL/LICLOG)
640aAA*
640aAA          R SF5CTL                    SFLCTL(SFL5)
640aAA                                      SFLSIZ(0034)
640aAA                                      SFLPAG(0014)
640aAA                                      ROLLUP
640aAA                                      OVERLAY
640aAA                                      SFLCSRRRN(&CSRRRN)
640aAA N32                                  SFLDSP
640aAA N31                                  SFLDSPCTL
640aAA  31                                  SFLCLR
640aAA  90                                  SFLEND(*MORE)
640aAA            RRN1           4S 0H      SFLRCDNBR(CURSOR)
640aAA            CSRRRN         5S 0H
640aAA            SCRNAME        8A  O  1  2
640aAA                                  1 31'View License History'
640aAA                                      DSPATR(HI)
640aAA                                  1 71DATE
640aAA                                      EDTCDE(Y)
640aAA            SCHEAD        58A  O  2 12
640aAA                                  2 71TIME
640aAA            SCOPT1        75A  O  4  2COLOR(BLU)
640aAA            SCOPT2        75A  O  5  2COLOR(BLU)
640aAA                                  6  9'Action'
640aAA                                      DSPATR(HI)
640aAA                                  6 20'Result'
640aAA                                      DSPATR(HI)
640aAA                                  6 60'Corresponding'
640aAA                                      DSPATR(HI)
640aAA                                  7  1'Opt'
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7  5'Act'
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7  9'Quantity  '
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7 20'Quantity  '
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7 31'Slot        '
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7 44'License        '
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7 60'License        '
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
640aAA                                  7 76'Empl#'
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
     A*
     A          R FKEY1
     A*%%TS  SD  20090226  172828  DAVE        REL-V6R1M0  5761-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F10=Prev view  F11=Next view  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20090226  161952  DAVE        REL-V6R1M0  5761-WDS
650aDA*                                     WINDOW(4 20 18 42)
650aMA                                      WINDOW(2 20 20 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order . . . .'
     A            W1SORT         1A  B  3 15DSPATR(HI)
     A                                  3 20'Ascending, Descending'
     A                                  4  1'Order by  . .'
     A            W1OBY          1A  B  4 15DSPATR(HI)
     A                                  4 20'1 = Lcns / Date'
     A                                  5 20'2 = Date / Lcns'
650bMA                                  6  1'Warehouse . .'
650bMA            W1WHSE         3Y 0B  6 15DSPATR(HI)
650bDA*                                 7  1'Warehouse . .'
650bDA*           W1WHSE         3Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
650bAA                                  7  1'Action. . . .'
650bAA            W1ACTION       3A  B  7 15DSPATR(HI)
650bAA                                  7 32'ALL'
     A                                  8  1'License . . .'
     A            W1LCNS        15A  B  8 15DSPATR(HI)
     A                                  8 32'*ALL'
     A                                  9  1'Dept  . . . .'
     A            W1WHDP        12A  B  9 15DSPATR(HI)
     A                                  9 32'*ALL'
     A                                 10  1'Slot  . . . .'
     A            W1SLOT        12A  B 10 15DSPATR(HI)
     A                                 10 32'*ALL'
     A                                 11  1'Base Slot . .'
     A            W1BASE        12A  B 11 15DSPATR(HI)
     A                                 11 32'*ALL'
650aDA*                                12  1'Item  . . . .'
650aDA*           W1ITEM        15A  B 12 15DSPATR(HI)
650aDA*                                12 32'*ALL'
650aDA*                                13  1'Employee# . .'
650aDA*           W1EMP#         5Y 0B 13 15EDTCDE(3)
650aDA*                                     DSPATR(HI)
650aDA*                                13 32'0=All'
650aDA*                                14  1'Tran# . . . .'
650aDA*           W1TRN#         7Y 0B 14 15EDTCDE(3)
650aDA*                                     DSPATR(HI)
650aDA*                                14 32'0=All'
650aDA*                                15  1'Hide USR  . .'
650aDA*           W1HIDE         1   B 15 15DSPATR(HI)
650aDA*                                15 32'Y,N'
650aDA*                                17  1'F3=Exit  F12=Cancel'
650aDA*                                     COLOR(BLU)
650aMA                                 14  1'License Item.'
650aMA            W1ITEML       15A  B 14 15DSPATR(HI)
650aMA                                 14 32'*ALL'
650aMA                                 15  1'Employee# . .'
650aMA            W1EMP#         5Y 0B 15 15EDTCDE(3)
650aMA                                      DSPATR(HI)
650aMA                                 15 32'0=All'
650aMA                                 16  1'Tran# . . . .'
650aMA            W1TRN#         7Y 0B 16 15EDTCDE(3)
650aMA                                      DSPATR(HI)
650aMA                                 16 32'0=All'
650aMA                                 17  1'Hide USR  . .'
650aMA            W1HIDE         1   B 17 15DSPATR(HI)
650aMA                                 17 32'Y,N'
650aMA                                 19  1'F3=Exit  F12=Cancel'
650aMA                                      COLOR(BLU)
650aAA                                 12  1'Slot Item . .'
650aAA            W1ITEM        15A  B 12 15DSPATR(HI)
650aAA                                 12 32'*ALL'
650aAA                                 13  1'From/To Date.'
650aAA            W1FDAT         6Y 0B 13 15
650aAA                                      DSPATR(HI)
650cAA  22                                  DSPATR(PC)
650aAA            W1TDAT         6Y 0B 13 22
650aAA                                      DSPATR(HI)
650cAA  23                                  DSPATR(PC)
650aAA                                 13 32'0=All'
