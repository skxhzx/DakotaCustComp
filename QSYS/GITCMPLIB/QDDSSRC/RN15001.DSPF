     A*%%TS  SD  20010330  125014  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
530aAA*                  WEBFACING REQUIREMENTS
530aAA*
530aAA* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
530aAA* NUMBER AND MAKE CHANGES TO THAT VERSION.
530aAA*
530aAA* I.E. RN150FM COPY AND NAME RN15001
530aAA*----------------------------------------------------------------
     A*
     A*  RN150FM   Customer Returns Putaway -
     A*  05 May 1999
     A*  Roxanne Harrison
     A*  Created using RN120
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
412 AA*  09/24/99  RH   4.12
     A*    - Add "Directed Put Away" to screen 1 head.
413aAA*    10/14/99  MLB  4.13a
     A*      - Added PF17 key to SCREEN1,SCREEN2 panels View
     A*        receiving log.
415aAA*  02/07/01  RH   4.15a
     A*    - Add Label field to screen 1.
415bAA*  03/30/01  RH   4.15b
     A*    - Add UPC field to screen 1.
416cAA*  11/13/01  RH   4.16c
     A*    - Add Dispositon, Impact inventory, Use label for putaway,
     A*    - Change heading position on screen2 like standard look.
530aAA*  05/14/10  JCJ  5.30a
     A*    - Enh:  Increase label to 10 for TRAX processing.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010330  125014  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF17
     A  98                                  ALARM
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  5 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  4'Printer device  . . . . .'
     A            W1DEV         10A  B  6 31
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                  7  4'Printer form  . . . . . .'
     A            W1FORM        10A  B  7 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
     A                                  8  4'User id . . . . . . . . .'
     A            W1USER        10A  B  8 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 10  4'Label # . . . . . . . . .'
530aD ***         W1LBL#         7Y 0B 10 31CHECK(ER)
530aMA            W1LBL#B       10   B 10 31CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
530aD ***                                   EDTCDE(4)
     A                                 11  4'UPC code  . . . . . . . .'
415bAA            W1UPC         50A  B 11 31
415bAA                                      CHECK(ER)
415  A N25                                  DSPATR(HI)
415bAA  25                                  DSPATR(RI)
415bAA N25N90                               DSPATR(UL)
415bAA  05                                  DSPATR(PC)
415bAA  90                                  DSPATR(PR)
     A                                 12  4'Item number . . . . . . .'
     A            W1ITEM        15A  B 12 31
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel  F17=View rcv log'
     A                                      COLOR(BLU)
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A            W1SCRN         8A  O  1  2
     A                                  9  4'-ENTER ONE-'
     A          R SCREEN2
     A*%%TS  SD  19990924  151908  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
413aAA                                      CF17
     A  98                                  ALARM
     A            W2SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Directed Put Away'
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A                                  5  6'User id:'
     A            W1USER        10A  O  5 13
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Quantity returned . . . .'
     A            W1QT01         5Y 0B 10 31EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1UM1          5A  O 10 37
     A            W1QT02         3Y 0B 10 45EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N31N90                               DSPATR(UL)
     A  94                                  DSPATR(ND)
     A  94
     AO 90                                  DSPATR(PR)
     A            W1UM2          5A  O 10 49
     A  94                                  DSPATR(ND)
     A            W1QT03         3Y 0B 10 57EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93
     AO 90                                  DSPATR(PR)
     A            W1UM3          5A  O 10 61
     A  93                                  DSPATR(ND)
     A                                 11  4'Expiration date . . . . .'
     A            W1EXPD         6Y 0B 11 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A N25N90N96                            DSPATR(HI)
     A                                 12  4'Reason code . . . . . . .'
     A            W1CODE         2A  B 12 31
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416b A                                 13  4'Dispositon code . . . . .'
416b A            W1DCDE         5A  B 13 31
416b A N27                                  DSPATR(HI)
416b A  27                                  DSPATR(RI)
416b A N27N90                               DSPATR(UL)
416b A  07                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
416b A                                 14  4'Use label for putaway . .'
416b A            W1FLBL         1A  B 14 31
416b A N28                                  DSPATR(HI)
416b A  28                                  DSPATR(RI)
416b A N28N90                               DSPATR(UL)
416b A  08                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
416b A                                 15  4'Invoice # . . . . . . . .'
416b A            W1INV#         9A  B 15 31
416b A  29                                  DSPATR(RI)
416b A N29N90                               DSPATR(UL)
416b A  09                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
     A                                 17  4'Transaction number  . . .'
     A            W1TRN#         7Y 0B 17 31
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*                                18  4'Corresponding item  . . .'
     A*           W1CITM        15A  B 18 31
     A* 35                                  DSPATR(RI)
     A*N35N90                               DSPATR(UL)
     A* 15                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
413aAA                                      2=Cancel  F17=View rcv log'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19990924  151908  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
413aAA                                      CF17
     A  98                                  ALARM
     A            W3SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Directed Put Away'
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A                                  5  6'User id:'
     A            W1USER        10A  O  5 13
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  3'Memo . . . . . . . . . . .'
     A            W3MEMO        30A  B 10 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 11  3'-ENTER ONE-'
     A                                 12  3'Corresponding item . . . .'
     A            W3CITM        15A  B 12 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  3'Corsp. UPC Code  . . . . .'
     A            W3UPC         50A  B 13 31CHECK(ER)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  3'Cor. Mfg code/Vend Item# .'
     A            W3VMFG        15A  B 14 31
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel  F17=View rcv log'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
