     A*%%TS  SD  20020501  144840  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  DH120FM  Dock Door Assignment for Route - Display
     A*  18 April 2002
     A*  Max Blacknik
     A*
     A*  Revisions:
     A*
416 AA*    04/18/02  MLB  4.16
     A*      - Created.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'DH130.01'
     A                                  1 26'Dock Door Assignment for Route'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  3  2
     A            WHCODE         3Y 0O  1 13EDTCDE(3)
     A                                  1 17'-'
     A            WHDESC        30A  O  1 19
     A                                  1  2'Warehouse:'
     A                                  2  2'Route    :'
     A            $KRTE          5A  O  2 13
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20020501  144840  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            IND41          1A  H      TEXT('Hidden: W1STAT color ind')
     A            OPTION         2A  B 11  2
     A            W1DRDP    R        O 11  7REFFLD(RCREC/RCDRDP *LIBL/RTECSUM)
     A            W1DOOR    R        O 11 14REFFLD(DMREC/DMDOOR *LIBL/DOCMTRX)
     A                                      EDTCDE(3)
     A  41                                  COLOR(RED)
     A            W1OVRF         1A  O 11 20
     A            W1OFDR         3Y 0O 11 23
     A                                      EDTCDE(3)
     A            W1PCKC    R        O 11 30REFFLD(RCREC/RCPCKC *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            W1PCKW    R        O 11 42REFFLD(RCREC/RCPCKW *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            W1PCKP    R        O 11 54REFFLD(RCREC/RCPCKP *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            W1STAT        15A  O 11 62
     A  41                                  COLOR(RED)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20020501  144840  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9  7'Door'
     A                                      DSPATR(HI)
     A                                  9 20'Overflow'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 14'Door'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 20'Y/N Door'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 42'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 54'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Door status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
