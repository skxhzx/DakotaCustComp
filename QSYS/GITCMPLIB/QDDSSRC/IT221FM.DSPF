     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/26/99  DAS  4.13
     A*     - Added SSB field to screen1
     A*
     A**************************************************************************
     A*%%TS  SD  19991126  055032  DAVE        REL-V4R2M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19991126  055032  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
     A                                      CF09
     A                                      CF10
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT221.01'
     A                                  1 12'Slot Info'
     A            W1SDSP        20A  O  3  1
     A                                  4  1'Status:'
     A            W1SLST         2A  O  4  9
     A                                  4 14'Def:'
     A            W1SDEF         2A  O  4 19
     A            W1ITEM        65A  O  5  1
     A                                  6  1'Qty:'
     A            W1QTY         10A  O  6  6
     A                                  7  1'F9=View Qty F10=Adj.'
     A                                      COLOR(BLU)
     A            WOSSB          3   O  6 18DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
