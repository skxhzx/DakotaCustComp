     A****************************************************************
     A*
     A* LT330PR
     A* 10 AUGUST 2009
     A* Chuck Manny
     A*
     A* Revisions:
     A*
610 AA*    08/10/09  CWM  6.10
     A*      - Created
     A*
640a A*    01/20/11  GJA  6.40
     A*      - Enh:  Add customer number to report
640b A*    06/27/12  LMC  6.40b
     A*      - Enh:  Add Expiration Date, also add PO if called from
     A*              item maintenance.
650a A*    05/16/13  RH   6.50a
     A*      - Enh:  Add Original license to printed report
650bAA*    05/16/13  RBD  6.50b
     A*      - Enh:  Added printing of ship date, count total per item,
     A*              and qty total per item. Had to change starting pos
     A*              of several fields to make room for ship date.
700aAA*    02/29/16  RH/LMC   7.00a
     A*      - Fix:  Change to display total item qty for all UOM as
     A*              string.  Did not display breakdown qty.
     A*      - Enh:  Move Grand total item count to align with total
     A*              item count field
700bAA*    07/26/16  NLK      7.00b
     A*      - Fix:  Adjusted pos to fit 2 more char in CustName/w1loc
     A*              which will put us at MAX usage, even eliminating
     A*              a / between MMDD: was 2016/07/26 now 2016/0726.
     A*
     A****************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    31
     A                                      'P.O. Recall Listing'
     A                                    85
     A                                      'Page'
     A            PAGE           4S 0O    89
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            SCHEAD        58A  O    16
     A                                    88
     A                                      'LT330'
     A                                      SPACEA(002)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A  91                                70
     A                                      '(Continued)'
     A*
     A* Print column headings for Date order.
     A*
     A          R COLUMNS1
     A                                      SPACEA(001)
     A
650bMA                                     1
     A                                      'Item           '
     A                                      UNDERLINE
650bMA                                    17
     A                                      'Quantity  '
     A                                      UNDERLINE
650bMA                                    28
640aAA                                      'Customer#  '
640aAA                                      UNDERLINE
650bMA                                    40
     A                                      'Location                      '
     A                                      UNDERLINE
650bMA                                    71
     A                                      'License        '
     A                                      UNDERLINE
650bMA                                    87
640bAA                                      'Expiration'
640bAA                                      UNDERLINE
650bMA  41                                98'PO Number '
640bAA  41                                  UNDERLINE
650bMA                                   108
650aAA                                      'Original Lic   '
650aAA                                      UNDERLINE
650bAA                                   124'Ship Date'
650bAA                                      UNDERLINE
     A*
     A* Print detail line for Date order
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
650bMA            #FITEM        15A  O     1
650bMA            W1RQTY        10A  O    17
650bMA            #FCUST#       11  0O    28EDTCDE(3)
700bMA            W1LOC         32A  O    40
700bMA            #FLCNS        15A  O    73
700bDA*           #FEXPDT        8  0O    87EDTWRD('    /  /  ')
700bAA            #FEXPDT        8  0O    89EDTWRD('    /    ')
700bMA  41        H1PO           9   O    99
700bMA            #FOLCNS       15A  O   109
700bAA            H1SDTE         6  0O   125EDTWRD('  /  /  ')
     A*
     A**************************************************************************
     A* Print totals
     A*
650bAA          R TOTITM
650bAA                                      SPACEA(001)
650bAA                                    12
650bAA                                      'Item Qty:'
700aMA            WKRQTY        10A  O    22
700aDA*           WKIQTY         5  0O    22
700aDA*                                     EDTCDE(Z)
650bAA                                    35'Item Count:'
650bAA            WKICNT         4  0O    47
650bAA                                      EDTCDE(Z)
     A*
     A**************************************************************************
     A          R TOTAL1
     A                                      SPACEB(001)
700aDA*                                    2
700aMA                                    34
     A                                      'Total Count:'
700aDA*           WKCOUNT       10  0O    16
700aMA            WKCOUNT       10  0O    47
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'LT330: End of Report '
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(PIRWORK/LT330PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(*N       132      *N     )
     A*%%***********************************************************************
     A*
