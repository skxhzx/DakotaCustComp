     A*%%TS  SD  20170502  170218  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP135FM   PIR options - Receiving
     A*  24 September 2001
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
416 AA*    09/24/01  DAS  6.14
     A*      - Created
     A*
417 AA*    04/11/03  MLB  4.17
     A*      - Enh: Add 3 new fields to support Retail UPC functions.
     A*        W1RUPC, W1RUPS, W1RUPF.
     A*        Moved up catchweight fields to make room for new fields.
417aAA*    02/01/04  MLB  4.17a
     A*      - Enh: Add 1 new field to allow control over whether
     A*        individual p/o's can be attached to multiple Super p?o's.
710aAA*    05/02/17  LMC  7.10a
     A*      - Enh: Added New Allow Gui Multiple Pallet Receiving
     A*        option.                                                 .
     A*      - Enh: Renamed display file OP135FM to OP13501.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010926  093323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP135.01'
     A                                  1 32'Receiving Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Use generic license . . . . . . . -
     A                                      . . . . .'
     A            W1PLCN         1A  B  6 49
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 62'Y=Yes, N=No'
     A                                  7  4'Program to print receiving labels -
     A                                      . . . . .'
     A            W1LPGM        10A  B  7 49
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 62'*NONE, name'
710aAA                                  8  4'Allow GUI Multiple pallet Receivin-
710aAA                                      g . . . .'
710aAA            W1MPLT         1A  B  8 49
710aAA N31                                  DSPATR(HI)
710aAA  31                                  DSPATR(RI)
710aAA N31N90                               DSPATR(UL)
710aAA  11                                  DSPATR(PC)
710aAA  90                                  DSPATR(PR)
710aAA                                  8 54'Y=Yes, N=No'
     A                                  9  4'Force marking of PO detail lines  -
     A                                      . . . . .'
     A            W1FRCM         1A  B  9 49
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 54'Y=Yes, N=No'
417aAA                                 10  4'Allow single p/o on multiple Super-
417aAA                                       P/O''s  .'
417aAA            W1MSPO         1A  B 10 49
417aAA N30                                  DSPATR(HI)
417aAA  30                                  DSPATR(RI)
417aAA N30N90                               DSPATR(UL)
417aAA  10                                  DSPATR(PC)
417aAA  90                                  DSPATR(PR)
417aAA                                 10 54'Y=Yes, N=No'
     A                                 11  4'When received expiration date come-
     A                                      s earlier'
     A                                 12  6'than overflow expiration date . . -
     A                                      . . . .'
     A            W1EXCK         1A  B 12 49
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 54'1=Allow, 2=Prevent, 3=Warn'
     A                                 14  4'Force catch weight entry  . . . . -
     A                                      . . . . .'
     A            W1CWFR         1   B 14 49
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 54'Y=Yes, N=No'
417 MA                                 15  4'Allow catch weight entry  . . . . -
     A                                      . . . . .'
417 MA            W1CWEN         1A  B 15 49
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
417 MA                                 15 54'1=Every receipt'
417 MA                                 16 54'2=First receipt only'
417 MA                                 17 54'3=Never'
417 MA                                 18  4'Catch weight value to enter . . . -
     A                                      . . . . .'
417 MA            W1CWTP         1A  B 18 49
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
417 MA                                 18 54'1=Choice, 2=Total, 3=Avg'
417 AA*
417 AA                                 19  4'Prompt for Retail UPC . . . . . . -
417 AA                                      . . . . .'
417 MA            W1RUPC         1A  B 19 49
417 AA N27                                  DSPATR(HI)
417 AA  27                                  DSPATR(RI)
417 AA N27N90                               DSPATR(UL)
417 AA  07                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
417 AA                                 19 54'Y=Yes, N=No'
417 AA*
417 AA                                 20  5'Allow user to skip Retail UPC scan-
417 AA                                       . . . .'
417 MA            W1RUPS         1A  B 20 49
417 AA N28                                  DSPATR(HI)
417 AA  28                                  DSPATR(RI)
417 AA N28N90                               DSPATR(UL)
417 AA  08                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
417 AA                                 20 54'Y=Yes, N=No'
417 AA*
417 AA                                 21  5'Require Retail UPC on file before -
417 AA                                      receipt.'
417 MA            W1RUPF         1A  B 21 49
417 AA N29                                  DSPATR(HI)
417 AA  29                                  DSPATR(RI)
417 AA N29N90                               DSPATR(UL)
417 AA  09                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
417 AA                                 21 54'Y=Yes, N=No'
417 AA*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
