     A*%%TS  SD  19961008  160405  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO822FM   PO History detail maint - Change screen
     A*  14 July, 1997
     A*  Roxanne Harrison
     A*
     A*  Created using PO122FM
     A*  Revisions
     A*    02/09/00  MLB  4.13a
     A*      - Revised SCREEN1 to add the following fields: W1DWGT,
     A*        W1AVG,W1QRC1,W1QRC2,W1QRC3.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19961008  160405  HEMANT      REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO822.01'
     A                                  1 28'PO History Detail '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'PO Number:'
     A            $PPO           9A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Sequence number  . . . . .'
     A            W1SEQ          5Y 0B  9 32
     A N21N90N91                            DSPATR(UL)
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 10  4'Item . . . . . . . . . . .'
     A            W1ITM         15A  B 10 32
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                 11  4'Normal qty ordered . . . .'
     A            W1QOR1         5Y 0B 11 32EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Breakdown 1 qty ordered  .'
     A            W1QOR2         5Y 0B 12 32EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Breakdown 2 qty ordered  .'
     A            W1QOR3         5Y 0B 13 32EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
413aAA                                 14  4'Catch weight received  . .'
413aAA            W1DWGT         7Y 2B 14 32
413aAA  26                                  DSPATR(RI)
413aAA N26N90                               DSPATR(UL)
413aAA  06                                  DSPATR(PC)
413aAA  95
413aAAO 90                                  DSPATR(PR)
413aAA  95                                  DSPATR(ND)
413aAA                                      EDTCDE(4)
413aAA                                 14 46'Average (Y/N)  . . . . . .'
413aAA            W1AVG          1A  O 14 73
413aAA                                 11 46'Normal qty received  . . .'
413aAA            W1QRC1         5  0O 11 73
413aAA                                      EDTCDE(4)
413aAA                                 12 46'Breakdown 1 qty received .'
413aAA            W1QRC2         5  0O 12 73
413aAA                                      EDTCDE(4)
413aAA                                 13 46'Breakdown 2 qty received .'
413aAA            W1QRC3         5  0O 13 73
413aAA                                      EDTCDE(4)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
