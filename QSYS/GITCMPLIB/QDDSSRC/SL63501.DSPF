500aAA*----------------------------------------------------------------
500aAA*   Multilingual Version
500aAA*   Every screen has a corresponding Spanish screen.
500aAA*   All Spanish screens are at the end.
500aAA*   Any changes must be made to both screens.
500aAA*   If necessary, contact appropriate person to do translation.
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/26/99  DAS  4.13
     A*     - Added SSB field to screen3
     A*    04/13/06  RH   5.00
     A*     - Enh: Added F10 exception to screen
     A*
500aAA*   06/04/06  JCJ  5.00a
     A*     - Merged Spanish screens from PIRS.
650aAA*   11/11/14  LMC  6.50a
     A*     - Renamed SL635FM to SL63501.
     A*     - Added W2INFO field to display open putaway message.
     A*
     A**************************************************************************
     A*%%TS  SD  19991126  060605  DAVE        REL-V4R2M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF09
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.01'
     A                                  1 12'Slot Qtys'
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  8EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept:'
     A            W1WHDP         5A  B  4  8
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Slot:'
     A            W1DISP        12A  B  5  8
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                  7  1'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19970211  124429  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF09
500  A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.02'
     A                                  1 12'Slot Qtys'
     A                                  2  1'Stk:'
     A            SLSTK1    R        O  2  7REFFLD(SLREC/SLSTK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM1          5A  O  2 15
     A                                  3  1'Alo:'
     A            SLALC1    R        O  3  7REFFLD(SLREC/SLALC1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  4  1'Pck:'
     A            SLPCK1    R        O  4  7REFFLD(SLREC/SLPCK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  5  1'Trn:'
     A            SLTFR1    R        O  5  7REFFLD(SLREC/SLTFR1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  6  1'Rec:'
     A            SLRCV1    R        O  6  7REFFLD(SLREC/SLRCV1 *LIBL/SLOT)
     A                                      EDTCDE(O)
650aAA            W2INFO        15   O  6 15DSPATR(HI)
650aAA                                      COLOR(RED)
650aAA                                      DSPATR(BL)
     A                                  7  1'Avl:'
     A            W2AVL1         7Y 0O  7  7EDTCDE(N)
     A                                  8  1'F3=Exit  F9=Brk Qty  F10=Err'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19991126  060605  DAVE        REL-V4R2M0  5769-PW1
500  A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.03'
     A                                  1 12'Slot Qtys'
     A                                  2  1'Stk:'
     A            SLSTK2    R        O  2  6REFFLD(SLREC/SLSTK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM2          2A  O  2 11
     A            SLSTK3    R        O  2 14REFFLD(SLREC/SLSTK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM3          2A  O  2 19
     A                                  3  1'Alo:'
     A            SLALC2    R        O  3  6REFFLD(SLREC/SLALC2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLALC3    R        O  3 14REFFLD(SLREC/SLALC3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  4  1'Pck:'
     A            SLPCK2    R        O  4  6REFFLD(SLREC/SLPCK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLPCK3    R        O  4 14REFFLD(SLREC/SLPCK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  5  1'Trn:'
     A            SLTFR2    R        O  5  6REFFLD(SLREC/SLTFR2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLTFR3    R        O  5 14REFFLD(SLREC/SLTFR3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  6  1'Rec:'
     A            SLRCV2    R        O  6  6REFFLD(SLREC/SLRCV2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLRCV3    R        O  6 14REFFLD(SLREC/SLRCV3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  7  1'Avl:'
     A            W2AVL2         5Y 0O  7  6EDTCDE(N)
     A            W2AVL3         5Y 0O  7 14EDTCDE(N)
     A                                  8  1'F3/F12=Exit  F10=Err'
     A                                      COLOR(BLU)
     A            WOSSB          3   O  8 18DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500aAA*
500aAA*****************************************************************
500aAA* Spanish Screens
500aAA*****************************************************************
500aAA*----------------------------------------------------------------
500aAA          R SCRN1SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF09
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.01'
     A                                  1 12'Ctds de Slots'
     A                                  3  2'Alm :'
     A            W1WHSE         3Y 0B  3  8EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept:'
     A            W1WHDP         5A  B  4  8
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Slot:'
     A            W1DISP        12A  B  5  8
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                  7  1'F3=Salir  F4=Inicitar'
     A                                      COLOR(BLU)
500aAA*----------------------------------------------------------------
500aAA          R SCRN2SP
     A*%%TS  SD  19970211  124429  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF09
500  A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.02'
     A                                  1 12'Ctds de Slots'
     A                                  2  1'Inv:'
     A            SLSTK1    R        O  2  7REFFLD(SLREC/SLSTK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM1          5A  O  2 15
     A                                  3  1'Asg:'
     A            SLALC1    R        O  3  7REFFLD(SLREC/SLALC1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  4  1'Pck:'
     A            SLPCK1    R        O  4  7REFFLD(SLREC/SLPCK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  5  1'Trn:'
     A            SLTFR1    R        O  5  7REFFLD(SLREC/SLTFR1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  6  1'Rec:'
     A            SLRCV1    R        O  6  7REFFLD(SLREC/SLRCV1 *LIBL/SLOT)
     A                                      EDTCDE(O)
650aAA            W2INFO        15   O  6 15DSPATR(HI)
650aAA                                      COLOR(RED)
650aAA                                      DSPATR(BL)
     A                                  7  1'Dsp:'
     A            W2AVL1         7Y 0O  7  7EDTCDE(N)
     A                                  8  1'F3=Salir  F9=Ctd Rta'
     A                                      COLOR(BLU)
500aAA*----------------------------------------------------------------
500aAA          R SCRN3SP
     A*%%TS  SD  19991126  060605  DAVE        REL-V4R2M0  5769-PW1
500  A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL635.03'
     A                                  1 12'Ctds de Slots'
     A                                  2  1'Inv:'
     A            SLSTK2    R        O  2  6REFFLD(SLREC/SLSTK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM2          2A  O  2 11
     A            SLSTK3    R        O  2 14REFFLD(SLREC/SLSTK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            W2UM3          2A  O  2 19
     A                                  3  1'Asg:'
     A            SLALC2    R        O  3  6REFFLD(SLREC/SLALC2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLALC3    R        O  3 14REFFLD(SLREC/SLALC3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  4  1'Pck:'
     A            SLPCK2    R        O  4  6REFFLD(SLREC/SLPCK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLPCK3    R        O  4 14REFFLD(SLREC/SLPCK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  5  1'Trn:'
     A            SLTFR2    R        O  5  6REFFLD(SLREC/SLTFR2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLTFR3    R        O  5 14REFFLD(SLREC/SLTFR3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  6  1'Rec:'
     A            SLRCV2    R        O  6  6REFFLD(SLREC/SLRCV2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLRCV3    R        O  6 14REFFLD(SLREC/SLRCV3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                  7  1'Dsp:'
     A            W2AVL2         5Y 0O  7  6EDTCDE(N)
     A            W2AVL3         5Y 0O  7 14EDTCDE(N)
     A                                  8  1'F3/F12=Salir'
     A                                      COLOR(BLU)
     A            WOSSB          3   O  8 18DSPATR(HI)
