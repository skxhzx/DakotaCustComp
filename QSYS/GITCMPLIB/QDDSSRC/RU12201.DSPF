     A*%%TS  SD  20040526  224531  BFC2        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  RU122FM   Work with Flag Entries - Change
     A*  26 May 2004
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    05/26/04  MLB  4.17
     A*      - Created.
     A*
500 AA*    07/10/05  MLB  5.00
     A*      - Enh: Added Original qty short field to display.
     A*
520 AA*    01/28/09  CWM  5.20
     A*      - Enh: Add support for 3 digit stops.
750aAA*    09/29/21  RH   7.50a
     A*      - Enh: Rename RU12201.  Add item qty fields.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'RU122.01'
     A                                  1 30'Work with Flag Entries'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Runner trn#/Status . .'
     A            W1TRN#    R        O  9 28REFFLD(LBTRN# LABEL)
     A                                      EDTCDE(3)
     A                                  9 36'/'
     A            W1STAT        10A  O  9 38
     A                                 10  6'Assigned to  . . . .'
     A            W1EMP         50A  O 10 28
     A                                 12  4'Original transaction#.'
     A            W1OTRN    R        O 12 28REFFLD(LBTRN# LABEL)
     A                                 13  4'Route / stop number. .'
     A            W1RTE          5A  O 13 28
     A                                 13 34'/'
520 AA            DSPSTP         5S 0O 13 36
520 DA*           W1STOP         2S 0O 13 36
520 MA            W1STOP         2S 0H
     A                                 14  4'Customer order number.'
     A            W1CORD    R        O 14 28REFFLD(LBTRN# LABEL)
     A                                      EDTCDE(3)
     A            W1CNAM    R        O 14 36REFFLD(OHCNAM ORDH)
     A                                 15  4'Label number . . . . .'
     A            W1LBL#    R        O 15 28REFFLD(LBLBL# LABEL)
     A                                 16  4'Department . . . . . .'
     A            W1WHDP    R        O 16 28REFFLD(LBWHDP LABEL)
     A                                 17  4'Item number  . . . . .'
     A            W1ITEM        50A  O 17 28
     A                                 18  4'Slot . . . . . . . . .'
     A            W1DISP    R        O 18 28REFFLD(LBDISP LABEL)
500 AA                                 19  4'Original qty short . .'
500 AA            W1OQYS    R        O 19 28REFFLD(RUQYFD RUNNER)
500 AA                                      EDTCDE(3)
500 AA            W1UCD3    R        O 19 34REFFLD(LBUCOD LABEL)
     A                                 20  4'Quantity short/out . .'
     A            W1QYSO    R        B 20 28REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UCOD    R        O 20 34REFFLD(LBUCOD LABEL)
     A                                 21  4'Quantity found . . . .'
     A            W1QYFD    R        O 21 28REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(3)
     A            W1UCD2    R        O 21 34REFFLD(LBUCOD LABEL)
750aAA                                 18 43'Stk:'
750aAA                                 19 43'Alo:'
750cMA                                 20 43'Ina:'
750aAA                                 21 43'Avl:'
750aAA            IQSTK1    R        O 18 48REFFLD(IQREC/IQSTK1 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQSTK2    R        O 18 59REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQSTK3    R        O 18 67REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQALC1    R        O 19 48REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQALC2    R        O 19 59REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQALC3    R        O 19 67REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQHLD1    R        O 20 48REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQHLD2    R        O 20 59REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQHLD3    R        O 20 67REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQAVL1    R        O 21 48REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQAVL2    R        O 21 59REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
750aAA            IQAVL3    R        O 21 67REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
750aAA                                      EDTCDE(O)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
