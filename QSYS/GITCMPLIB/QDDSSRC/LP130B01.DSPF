500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LP130B01   Pallet License Plate - View RF (Full Screen)
     A*  08 September, 1999
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*  09/08/99  HNK 4.12
     A*   - Created
     A*  07/13/00  RH  4.14a
     A*   - Add transaction type(task code and task category code).
414dAA*    09/13/00  HNK  4.14d
     A*      - Add pick slot on screen 2
     A*
414eAA*  10/13/00  DAS 4.14e
     A*   - Rearranged fields.
     A*   - Added receiving fields W2RDTE, W2RTIM, W2RUSR, W2RDEV,
     A*     W2RPO, W2RACT.
     A*   - Added generated date/time fields W2GDTE, W2GTIM.
     A*   - Remove W2PDTE.
416 AA*  08/16/01  MLB 4.16
     A*   - Added receiving fields W2EXPD, W2MFRD TO SCREEN2.
416aAA*    05/23/02  RRH  4.16a
     A*      - Increase W1LCNS and W2LCNS to 15.
416bAA*    08/06/02  MLB  4.16b
     A*      - Remove overlay from screen 1 & 2.
     A*      - Add dummy i/o field to screen 2.
417aAA*    05/20/04  RH   4.17a
     A*      - Enh: Add screen 3 for selection information.
     A*
500 AA*   06/06/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   10/17/06  RH   5.10a
     A*     - Add F8=Date info to Screen 2.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
416bDA*                                     OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130B.01'
     A                                  1 13'License/Tran Information'
     A                                      DSPATR(HI)
     A                                  7 16'Whse'
     A            W1WHSE         3Y 0B  7 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8 12'Tran. #.'
     A            W1TRN#         7Y 0B  8 24CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 15'-OR-'
     A                                 10 12'License.'
416aDA*           W1LCNS         7Y 0B 10 24CHECK(ER)
416aMA            W1LCNS        15A  B 10 24CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
416bDA*                                     OVERLAY
     A          R SCREEN2
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
510a A                                      CF08
     A  98                                  ALARM
     A                                  1  2'LP130B.02'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'General Information:'
     A                                      DSPATR(HI)
     A                                  4  5'License . . . . . . .'
     A            W2LCNS        15A  O  4 28
     A                                  5  5'Transaction . . . . .'
     A            W2TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  5 37'Status:'
     A            W2TRST        10A  O  5 45
     A                                  5 57'Type:'
     A            W2TASK         6A  O  5 63
     A                                  5 70'/'
     A            W2TCAT         6A  O  5 72
     A                                  6  5'Generated . . . . . .'
     A            W2GDTE         6Y 0O  6 28EDTCDE(Y)
     A            W2GTIM         6Y 0O  6 38EDTWRD(' 0:  :  ')
     A                                  7  5'Item  . . . . . . . .'
     A            W2ITEM        49A  O  7 28
     A                                  8  5'Quantity  . . . . . .'
     A            W2PQTY         5Y 0O  8 28EDTCDE(4)
     A                                 10  3'Receiving Information:'
     A                                      DSPATR(HI)
     A                                 11  5'Last action . . . . .'
     A            W2RACT         8A  O 11 28
     A            W2RDTE         6Y 0O 11 38EDTCDE(Y)
     A            W2RTIM         6Y 0O 11 48EDTWRD(' 0:  :  ')
     A                                 12  5'Purchase order  . . .'
     A            W2RPO          9A  O 12 28
     A                                 12 54'Exp date:'
     A  40                                  DSPATR(ND)
     A            W2EXPD         6Y 0O 12 64EDTCDE(Y)
     A  40                                  DSPATR(ND)
     A                                 14  5'User/Device . . . . .'
     A            W2RUSR        10A  O 14 28
     A                                 14 39'/'
     A            W2RDEV        10A  O 14 41
     A                                 14 54'Mfr date:'
     A  41                                  DSPATR(ND)
     A            W2MFRD         6Y 0O 14 64EDTCDE(Y)
     A  41                                  DSPATR(ND)
     A                                 15  3'Putaway Information:'
     A                                      DSPATR(HI)
     A                                 16  5'Slot  . . . . . . . .'
     A            W2DISP        12A  O 16 28
     A            W2VRTL        10A  O 16 42
     A                                 16 54'Pick slot:'
     A            W2PSLT        12A  O 16 65
     A                                 17  5'Employee  . . . . . .'
     A            W2EMP#         5Y 0O 17 28EDTCDE(3)
     A            W2ENAM        30A  O 17 35
     A                                 18  5'Started . . . . . . .'
     A            W2SDTE         6Y 0O 18 28EDTCDE(Y)
     A            W2STIM         6Y 0O 18 38EDTWRD('  :  :  ')
     A                                 19  5'Ended . . . . . . . .'
     A            W2EDTE         6Y 0O 19 28EDTCDE(Y)
     A            W2ETIM         6Y 0O 19 38EDTWRD('  :  :  ')
     A                                 20  5'Closed  . . . . . . .'
     A            W2CDTE         6Y 0O 20 28EDTCDE(Y)
     A            W2CTIM         6Y 0O 20 38EDTWRD('  :  :  ')
     A                                 21  5'Label status  . . . .'
     A            W2LBST        10A  O 21 28
510aMA                                 23  2'F3=Exit  F8=Dates  F12=Cancel'
510aDA*                                23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 13  5'Vendor  . . . . . . .'
     A            W2VEND        50   O 13 28
417a A          R SCREEN3
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130B.03'
     A                                  1 31'License/Trans Information'
     A                                      DSPATR(HI)
     A            FLD03          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'General Information:'
     A                                      DSPATR(HI)
     A                                  4  5'License . . . . . . .'
     A            W3LCNS        15A  O  4 28
     A                                  5  5'Transaction . . . . .'
     A            W3TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  5 37'Status:'
     A            W3TRST        10A  O  5 45
     A                                  5 57'Type:'
     A            W3TASK         6A  O  5 63
     A                                  5 70'/'
     A            W3TCAT         6A  O  5 72
     A                                  6  5'Generated . . . . . .'
     A            W3GDTE         6Y 0O  6 28EDTCDE(Y)
     A            W3GTIM         6Y 0O  6 38EDTWRD(' 0:  :  ')
     A                                  7  5'Route . . . . . . . .'
     A            W3RTE          5A  O  7 28
     A                                  8  5'Route ID  . . . . . .'
     A            W3RTID         5A  O  8 28
     A                                  9  5'Quantity  . . . . . .'
     A            W3PQTY         5Y 0O  9 28EDTCDE(4)
     A                                 15  3'Selection Information:'
     A                                      DSPATR(HI)
     A                                 17  5'Employee  . . . . . .'
     A            W3EMP#         5Y 0O 17 28EDTCDE(3)
     A            W3ENAM        30A  O 17 35
     A                                 18  5'Started . . . . . . .'
     A            W3SDTE         6Y 0O 18 28EDTCDE(Y)
     A            W3STIM         6Y 0O 18 38EDTWRD('  :  :  ')
     A                                 19  5'Ended . . . . . . . .'
     A            W3EDTE         6Y 0O 19 28EDTCDE(Y)
     A            W3ETIM         6Y 0O 19 38EDTWRD('  :  :  ')
     A                                 20  5'Closed  . . . . . . .'
     A            W3CDTE         6Y 0O 20 28EDTCDE(Y)
     A            W3CTIM         6Y 0O 20 38EDTWRD('  :  :  ')
     A                                 21  5'Label status  . . . .'
     A            W3LBST        10A  O 21 28
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
416bDA*                                     OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130B.01'
     A                                  1 13'Info de Licencia/Transaccion'
     A                                      DSPATR(HI)
     A                                  7 16'Almc'
     A            W1WHSE         3Y 0B  7 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8 12'# Trans.'
     A            W1TRN#         7Y 0B  8 24CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 15'-O-'
     A                                 10 12'Licencia'
416aDA*           W1LCNS         7Y 0B 10 24CHECK(ER)
416aMA            W1LCNS        15A  B 10 24CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                 23  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
416bDA*                                     OVERLAY
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130B.02'
     A                                  1 31'Informacion de Lic'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'Informacion General:'
     A                                      DSPATR(HI)
     A                                  4  5'Licencia. . . . . . .'
     A            W2LCNS        15A  O  4 28
     A                                  5  5'Transaccion . . . . .'
     A            W2TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  5 37'Estado:'
     A            W2TRST        10A  O  5 45
     A                                  5 57'Tipo:'
     A            W2TASK         6A  O  5 63
     A                                  5 70'/'
     A            W2TCAT         6A  O  5 72
     A                                  6  5'Generado  . . . . . .'
     A            W2GDTE         6Y 0O  6 28EDTCDE(Y)
     A            W2GTIM         6Y 0O  6 38EDTWRD(' 0:  :  ')
     A                                  7  5'Producto. . . . . . .'
     A            W2ITEM        49A  O  7 28
     A                                  8  5'Cantidad  . . . . . .'
     A            W2PQTY         5Y 0O  8 28EDTCDE(4)
     A                                 10  3'Informacion de Recibo:'
     A                                      DSPATR(HI)
     A                                 11  5'Ultima accion . . . .'
     A            W2RACT         8A  O 11 28
     A            W2RDTE         6Y 0O 11 38EDTCDE(Y)
     A            W2RTIM         6Y 0O 11 48EDTWRD(' 0:  :  ')
     A                                 12  5'PO  . . . . . . . . .'
     A            W2RPO          9A  O 12 28
     A                                 12 54'Fcha Exp:'
     A  40                                  DSPATR(ND)
     A            W2EXPD         6Y 0O 12 64EDTCDE(Y)
     A  40                                  DSPATR(ND)
     A                                 14  5'Usuario/aparato . . .'
     A            W2RUSR        10A  O 14 28
     A                                 14 39'/'
     A            W2RDEV        10A  O 14 41
     A                                 14 54'Fcha Mfr:'
     A  41                                  DSPATR(ND)
     A            W2MFRD         6Y 0O 14 64EDTCDE(Y)
     A  41                                  DSPATR(ND)
     A                                 15  3'Informacion de Guardar'
     A                                      DSPATR(HI)
     A                                 16  5'Slot  . . . . . . . .'
     A            W2DISP        12A  O 16 28
     A            W2VRTL        10A  O 16 42
     A                                 16 54'Pick slot:'
     A            W2PSLT        12A  O 16 65
     A                                 17  5'Empleado. . . . . .'
     A            W2EMP#         5Y 0O 17 28EDTCDE(3)
     A            W2ENAM        30A  O 17 35
     A                                 18  5'Empezado. . . . . . .'
     A            W2SDTE         6Y 0O 18 28EDTCDE(Y)
     A            W2STIM         6Y 0O 18 38EDTWRD('  :  :  ')
     A                                 19  5'Terminado . . . . . .'
     A            W2EDTE         6Y 0O 19 28EDTCDE(Y)
     A            W2ETIM         6Y 0O 19 38EDTWRD('  :  :  ')
     A                                 20  5'Cerrado . . . . . . .'
     A            W2CDTE         6Y 0O 20 28EDTCDE(Y)
     A            W2CTIM         6Y 0O 20 38EDTWRD('  :  :  ')
     A                                 21  5'Estado de rotulo. . .'
     A            W2LBST        10A  O 21 28
     A                                 23  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A                                 13  5'Vendedor. . . . . . .'
     A            W2VEND        50   O 13 28
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LP130B.03'
     A                                  1 31'Licencia/Info de Transaccion'
     A                                      DSPATR(HI)
     A            FLD03          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'Informacion General:'
     A                                      DSPATR(HI)
     A                                  4  5'Licencia. . . . . . .'
     A            W3LCNS        15A  O  4 28
     A                                  5  5'Transaccion . . . . .'
     A            W3TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  5 37'Estado:'
     A            W3TRST        10A  O  5 45
     A                                  5 57'Tipo:'
     A            W3TASK         6A  O  5 63
     A                                  5 70'/'
     A            W3TCAT         6A  O  5 72
     A                                  6  5'Generado . . . . . .'
     A            W3GDTE         6Y 0O  6 28EDTCDE(Y)
     A            W3GTIM         6Y 0O  6 38EDTWRD(' 0:  :  ')
     A                                  7  5'Route . . . . . . . .'
     A            W3RTE          5A  O  7 28
     A                                  8  5'ID de ruta . . . . .'
     A            W3RTID         5A  O  8 28
     A                                  9  5'Cantidad  . . . . . .'
     A            W3PQTY         5Y 0O  9 28EDTCDE(4)
     A                                 15  3'Info de Transaccion  :'
     A                                      DSPATR(HI)
     A                                 17  5'Empleado  . . . . . .'
     A            W3EMP#         5Y 0O 17 28EDTCDE(3)
     A            W3ENAM        30A  O 17 35
     A                                 18  5'Empezado. . . . . . .'
     A            W3SDTE         6Y 0O 18 28EDTCDE(Y)
     A            W3STIM         6Y 0O 18 38EDTWRD('  :  :  ')
     A                                 19  5'Terminado . . . . . .'
     A            W3EDTE         6Y 0O 19 28EDTCDE(Y)
     A            W3ETIM         6Y 0O 19 38EDTWRD('  :  :  ')
     A                                 20  5'Cerrado . . . . . . .'
     A            W3CDTE         6Y 0O 20 28EDTCDE(Y)
     A            W3CTIM         6Y 0O 20 38EDTWRD('  :  :  ')
     A                                 21  5'Estado de rotulo. . .'
     A            W3LBST        10A  O 21 28
     A                                 23  2'F3=Salir   F12=Cancelar'
     A                                      COLOR(BLU)
