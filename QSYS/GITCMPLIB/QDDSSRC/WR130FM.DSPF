     A*%%TS  SD  20101124  140536  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR130FM    Repack Item Work Order Entry List
     A*
     A*  Revisions
     A*
530 AA*    08/17/10  JCJ  5.30
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20101124  140536  JEFF        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            H1BUILD        3S 0H
     A            H1BITEM       15A  H
     A            H1WHDP         5A  H
     A            ITITEM        15A  O 11  6
     A            ITDESC        30A  O 11 22
     A            W1RPKD         5Y 0O 11 53EDTCDE(Z)
     A            W1WHSQ         5Y 0O 11 61EDTCDE(Z)
     A            W1BUILD        3Y 0B 11 71EDTCDE(Z)
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20101124  140457  JEFF        REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CF16
     A                                      OVERLAY
     A                                      SFLCSRRRN(&@CSRPO)
     A  26                                  SFLDSP
     A N27                                  SFLDSPCTL
     A  27                                  SFLCLR
     A N28                                  SFLEND(*MORE)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A            @CSRPO         5  0H
     A            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'WR130.01'
     A                                  1 24'Repack Item Work Order Entry List'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            ITWHSE         3Y 0O  4  3EDTCDE(4)
     A                                  4  7'-'
     A            WHDESC        30A  O  4  9
     A                                  4 40'Position to code . . . .'
     A            PTINP1        15A  B  4 65
     A                                  9 61'Repack'
     A                                      DSPATR(HI)
     A                                  9 70'Repack'
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 53'Demand'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 60'Whse Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'Build'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20101101  085759  JEFF        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            ITITEM        15A  O 11  6
     A            ITDESC        30A  O 11 22
     A            W2BUILD        3Y 0O 11 53EDTCDE(Z)
     A            W2WHSQ         5Y 0O 11 61EDTCDE(Z)
     A            W2NEED         3Y 0O 11 73EDTCDE(Z)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20101026  131301  JEFF        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      CF10
     A                                      CF11
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 53'Repack'
     A                                      DSPATR(HI)
     A                                  9 61'Parent'
     A                                      DSPATR(HI)
     A                                  9 72'Parent'
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 53'Build'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 60'Whse Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'Qty Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20101028  145540  JEFF        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            H3BITEM       15   H
     A            H3WHDP         5   H
     A            W3ITEM        15A  O 11  6
     A            W3DESC        30A  O 11 22
     A            W3RPKD         5Y 0O 11 53EDTCDE(Z)
     A            W3WHSQ         5Y 0O 11 61EDTCDE(Z)
     A            W3BUILD        3Y 0B 11 71EDTCDE(Z)
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20101124  140536  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A  36                                  SFLDSP
     A N37                                  SFLDSPCTL
     A  37                                  SFLCLR
     A N38                                  SFLEND(*MORE)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A                                  1  2'WR130.03'
     A                                  1 24'Repack Item Work Order Entry List'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Press Enter to confirm your choice-
     A                                      s to Create Work Orders            -
     A                                                 Press F12 to return to c-
     A                                      hange your choices.'
     A                                      DSPATR(BL)
     A                                      COLOR(BLU)
     A                                  9 61'Repack'
     A                                      DSPATR(HI)
     A                                  9 70'Repack'
     A                                      DSPATR(HI)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 53'Demand'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 60'Whse Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'Build'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  20100817  152821  JEFF        REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A*%%TS  SD  20101016  093620  JEFF        REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Create Work Order'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
