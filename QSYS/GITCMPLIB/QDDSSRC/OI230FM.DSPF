     A*%%TS  SD  20100315  171452  FMSBFC      REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI220FM    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Dierk's Waukesha
     A*
DRK AA*    03/15/10  MLB  DRK
     A*      - Added new screen fmt, SCN1DRK to add route number
     A*        as part of the batch import selection criteria.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970828  065643  DAVE        REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'OI230.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A                                 10  4'Place on job queue . . . . .'
     A                                 11  4'Printer device . . . . . . .'
     A                                 12  4'Form type  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1JOBQ         1A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1DEV         10A  B 11 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1FORM        10A  B 12 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 58'Y=Yes, N=No'
DRK AA*
DRK AA          R SCN1DRK
DRK AA                                      CF04
DRK AA  98                                  ALARM
DRK AA                                      CLRL(23)
DRK AA                                      OVERLAY
DRK AA                                  1  2'OI230.01'
DRK AA            RPTTTL        50A  O  1 16DSPATR(HI)
DRK AA                                  1 72DATE
DRK AA                                      EDTCDE(Y)
DRK AA                                  5  2'Type changes, press Enter.'
DRK AA                                      COLOR(BLU)
DRK AA                                  8  4'Warehouse  . . . . . . . . .'
DRK AA            W1WHSE         3Y 0B  8 34EDTCDE(4)
DRK AA N21N90                               DSPATR(HI)
DRK AA  21                                  DSPATR(RI)
DRK AA N21N90                               DSPATR(UL)
DRK AA  01                                  DSPATR(PC)
DRK AA  90                                  DSPATR(PR)
DRK AA                                  9  4'Route Number . . . . . . . .'
DRK AA            W1RTE          5A  B  9 34
DRK AA N22N90                               DSPATR(HI)
DRK AA  22                                  DSPATR(RI)
DRK AA N22N90                               DSPATR(UL)
DRK AA  02                                  DSPATR(PC)
DRK AA  90                                  DSPATR(PR)
DRK AA                                 10  4'Place on job queue . . . . .'
DRK AA            W1JOBQ         1A  B 10 34
DRK AA N23N90                               DSPATR(HI)
DRK AA  23                                  DSPATR(RI)
DRK AA N23N90                               DSPATR(UL)
DRK AA  03                                  DSPATR(PC)
DRK AA  90                                  DSPATR(PR)
DRK AA                                 10 58'Y=Yes, N=No'
DRK AA                                 11  4'Printer device . . . . . . .'
DRK AA            W1DEV         10A  B 11 34
DRK AA N24N90                               DSPATR(HI)
DRK AA  24                                  DSPATR(RI)
DRK AA N24N90                               DSPATR(UL)
DRK AA  04                                  DSPATR(PC)
DRK AA  90                                  DSPATR(PR)
DRK AA                                 12  4'Form type  . . . . . . . . .'
DRK AA            W1FORM        10A  B 12 34
DRK AA N25N90                               DSPATR(HI)
DRK AA  25                                  DSPATR(RI)
DRK AA N25N90                               DSPATR(UL)
DRK AA  05                                  DSPATR(PC)
DRK AA  90                                  DSPATR(PR)
DRK AA                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
DRK AA                                       F12=Cancel'
DRK AA                                      COLOR(BLU)
DRK AA*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
