500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A********************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  SL930FM      SLOT LOOKUP FOR VERIFIED ZERO SLOTS
     A*  21 September, 1994
     F*  Dave Sommerville
     F*  Hemant Kapadia
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
500 AA*   06/04/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A********************************************************************
     A*%%TS  SD  19970930  144818  HEMANT      REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19940921  164143  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'SL930.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 28'Verified Zero Slot Lookup'
     A                                      DSPATR(HI)
     A            FLD001        75   O  2  2
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940921  113456  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2  2'Warehouse        :'
     A                                  3  2'Department       :'
     A                                  4  2'Designation area :'
     A            WDCODE         5A  O  3 21
     A            DSCODE         5A  O  4 21
     A            DSDESC        30A  O  4 27
     A            WDDESC        30A  O  3 27
     A            WOWHSE        35A  O  2 21
     A                                  5 38'Position to definition type .'
     A                                  6 38'Position to activity zone . .'
     A            PTINP1         2   B  5 69
     A            PTINP2         2   B  6 69
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970930  144818  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 14  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLSDEF    R        O 14 11REFFLD(SLREC/SLSDEF *LIBL/SLOT)
     A            SLACTV    R        O 14 22REFFLD(SLREC/SLACTV *LIBL/SLOT)
     A            SLDISP        12A  O 14 30
     A            SLPICK    R        O 14 47REFFLD(SLREC/SLPICK *LIBL/SLOT)
     A            SLDESG    R        O 14 55REFFLD(SLREC/SLDESG *LIBL/SLOT)
     A            SLSTYP    R        O 14 65REFFLD(SLREC/SLSTYP *LIBL/SLOT)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970930  144818  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0008)
     A                                      CF06
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 13  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 30'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13  7'Definition'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  7'Slot'
     A                                      DSPATR(HI)
     A                                 12 20'Activity'
     A                                      DSPATR(HI)
     A                                 13 20'Zone    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 46'Pick'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 55'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 63'Section'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19940921  130922  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            W1CMD         75A  O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
     A          R TITLESP
     A*%%TS  SD  19940921  164143  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'SL930.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 28'Buscar slot de 0 verificado'
     A                                      DSPATR(HI)
     A            FLD001        75   O  2  2
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
500 AA*----------------------------------------------------------------
500 AA          R POSTNRSP
     A*%%TS  SD  19940921  113456  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2  2'Almacen          :'
     A                                  3  2'Departamento     :'
     A                                  4  2'Area designada   :'
     A            WDCODE         5A  O  3 21
     A            DSCODE         5A  O  4 21
     A            DSDESC        30A  O  4 27
     A            WDDESC        30A  O  3 27
     A            WOWHSE        35A  O  2 21
     A                                  5 38'Poner a tipo de definicion  .'
     A                                  6 38'Poner a zona de actividad . .'
     A            PTINP1         2   B  5 69
     A            PTINP2         2   B  6 69
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A*%%TS  SD  19970930  144818  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 14  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLSDEF    R        O 14 11REFFLD(SLREC/SLSDEF *LIBL/SLOT)
     A            SLACTV    R        O 14 22REFFLD(SLREC/SLACTV *LIBL/SLOT)
     A            SLDISP        12A  O 14 30
     A            SLPICK    R        O 14 47REFFLD(SLREC/SLPICK *LIBL/SLOT)
     A            SLDESG    R        O 14 55REFFLD(SLREC/SLDESG *LIBL/SLOT)
     A            SLSTYP    R        O 14 65REFFLD(SLREC/SLSTYP *LIBL/SLOT)
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  19970930  144818  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0008)
     A                                      CF06
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 13  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 30'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13  7'Definicion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  7'Slot'
     A                                      DSPATR(HI)
     A                                 12 20'Activity'
     A                                      DSPATR(HI)
     A                                 13 20'Zona    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 46'Pick'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 55'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 63'Seccion'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R OPTRECSP
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Escriba opciones, oprima ENTER.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
500 AA*----------------------------------------------------------------
     A          R DELRECSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Oprima ENTER para confirmar las se-
     A                                      lecciones por 4=borrar.'
     A                                      COLOR(BLU)
     A                                  4  2'Oprima F12 para volver a cambiar l-
     A                                       as selecciones.'
     A                                      COLOR(BLU)
