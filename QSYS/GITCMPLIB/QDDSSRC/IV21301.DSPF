     A*%%TS  SD  19980715  165607  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*  Revisions
     A*
412 AA*    09/28/99  DAS  4.12
     A*      - Added error indicators to W1SLOT on SCREEN1.
500aAA*    04/27/05  RH   5.00a
     A*      - Added W2SPOS slot pallet position.
500bAA*    06/06/06  RH   5.00b
     A*      - Added W2CWGT catch wt and F8=Ovrrid
500cAA*    06/07/06  RH   5.00c
     A*      - Change W2CWGT catch wt to 7.2 to allow keyed decimal
500cAA*    06/07/06  RH   5.00c
     A*      - Change W2CWGT catch wt to 7.2 to allow keyed decimal
520aAA*    07/08/08  RH   5.20a
     A*      - Move W2SITM to screen 1 to allow Screen2 CW field to be protected
710aAA*  05/12/17  LMC  7.10a
     A*    - Rename IV213FM to IV21301. using different indicators
     A*      to control the display of breakdown entry fields.
     A*    - Fix w2spos field so the cursor doesn't go to a non display
     A*      field when tabbing to next field for entry.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  19980715  165607  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'IV213.01'
     A                                  1 28'Inventory Sheet Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Virtual Slot'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11  4'Physical slot . . . .'
     A            W1SLOT        12A  B 11 27
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
520aAA                                 13  4'Item  . . . . . . . .'
520aAA            W1SITM        15A  B 13 27
520aAA  40                                  DSPATR(PC)
520aAA  90                                  DSPATR(PR)
520aAA N90                                  DSPATR(UL)
520aAA  40                                  DSPATR(RI)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
    DA*                                     CF04
500b A                                      CF08
     A  98                                  ALARM
     A                                  1  2'IV213.02'
     A                                  1 28'Inventory Sheet Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Virtual Slot'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
520aDA*                                 7  3'Item  . . . . . . . .'
520aDA*           W2SITM        15A  B  7 25
520aDA* 40                                  DSPATR(PC)
520aDA* 90                                  DSPATR(PR)
520aDA*N90                                  DSPATR(UL)
520aDA* 40                                  DSPATR(RI)
     A                                  8  3'Normal quantity . . .'
     A            W2SQT1         5Y 0B  8 25
     A                                      EDTCDE(L)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  9  3'Breakdown Qty 1 . . .'
710aAA  93                                  DSPATR(ND)
     A            W2SQT2         3Y 0B  9 25EDTCDE(L)
710aAA  93                                  DSPATR(ND)
710aAA  93
710aDA* 90                                  DSPATR(PR)
710aMAO 90                                  DSPATR(PR)
710aDA*N90                                  DSPATR(UL)
710aMA N90N93                               DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A                                 10  3'Breakdown Qty 2 . . .'
710aAA  94                                  DSPATR(ND)
     A            W2SQT3         3Y 0B 10 25EDTCDE(L)
710aAA  94                                  DSPATR(ND)
710aAA  94
710aDA* 90                                  DSPATR(PR)
710aMAO 90                                  DSPATR(PR)
710aDA*N90                                  DSPATR(UL)
710aMA N90N94                               DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A                                 11  3'Pallet Position . . .'
     A  45                                  DSPATR(ND)
     A            W2SPOS         3Y 0B 11 25EDTCDE(L)
710aDA* 90 45                               DSPATR(PR)
710aAA  90
710aMAO    45                               DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  45                                  DSPATR(ND)
500b A                                 12  3'Catch wt. . . . . . .'
520aMA  47                                  DSPATR(ND)
500b A            W2CWGT         7Y 2B 12 25EDTCDE(M)
500B A  90                                  DSPATR(PR)
500b A N90                                  DSPATR(UL)
500b A  46                                  DSPATR(RI)
500b A  46                                  DSPATR(PC)
520aMA  47                                  DSPATR(ND)
500b A* 45                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F5=Refresh  F8=Ovrrid -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
