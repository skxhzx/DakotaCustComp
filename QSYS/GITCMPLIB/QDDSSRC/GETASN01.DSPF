     A*%%TS  SD  20120802  091203  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  GETASNFM   Lot Selection for Multiple LPN
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
640 AA*    08/01/12  LMC  6.40
     A*      - Created.
650aAA*    08/30/13  LMC  6.50a
     A*      - Enh: Renamed display file to GETASN01.
     A*      - Enh: Added qty,weight, dates to subfile.
     A*      - Enh: Added summary lines for receiving.
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20120801  142015  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
650aDA*           W1QTY          8  0H
     A            W1MFGDT        6  0H
     A            W1MFGEXP       6  0H
650aDA*           W1WGT         11  3H
     A            W1UPC         20   H
     A            W1LPN         50   H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
650aDA*N21N90                               DSPATR(UL)
650aMA N21N90N91                            DSPATR(UL)
     A  21                                  DSPATR(RI)
650aDA* 90                                  DSPATR(PR)
650aAA  91
650aMAO 90                                  DSPATR(PR)
650aDA*           W1PO           9A  O  8 11
650aMA            W1PO           9A  H
650aAA            W1DSPPO        9A  O  8  5
650aDA*           W1WHSE    R        O  8  6REFFLD(TTRREC/TTRS *LIBL/TTRTE)
650aMA            W1WHSE    R        H      REFFLD(TTRREC/TTRS *LIBL/TTRTE)
650aDA*                                     EDTCDE(4)
650aDA*           W1SEQ          5Y 0O  8 39EDTCDE(4)
650aMA            W1SEQ          5Y 0H
650aDA*           W1ITEM        15A  O  8 22
650aMA            W1ITEM        15A  O  8 15
650aDA*           W1LOT         20A  O  8 47
650aMA            W1LOT         20A  H
650aMA            W1DSPLOT      20A  O  8 31
650aAA            W1QTY          5  0O  8 52EDTCDE(Z)
650aAA            W1WGT          9  2O  8 58EDTWRD('       .  ')
650aAA            W1DATE         6Y 0O  8 69EDTCDE(Y)
650aAA            DATTYPE        1   O  8 78
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20120802  091203  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
650aDA*                                 1 30'Select ASN Lot Number'
650aMA                                  1 30'Select To Receive'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
650aDA*                                 7  5'Whse'
650aDA*                                     DSPATR(HI)
650aDA*                                     DSPATR(UL)
650aDA*                                 7 11'PO Number'
650aMA                                  7  5'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aDA*                                 7 22'Item Number    '
650aMA                                  7 15'Item Number    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aDA*                                 7 39'Seq# '
650aDA*                                     DSPATR(HI)
650aDA*                                     DSPATR(UL)
650aDA*                                 7 47'Lot Number          '
650aMA                                  7 31'Lot Number          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aAA                                  7 52'Qty  '
650aAA                                      DSPATR(HI)
650aAA                                      DSPATR(UL)
650aAA                                  7 58'Weight    '
650aAA                                      DSPATR(HI)
650aAA                                      DSPATR(UL)
650aAA                                  7 69'Date    '
650aAA                                      DSPATR(HI)
650aAA                                      DSPATR(UL)
     A*
     A          R FKEY1
     A*%%TS  SD  20120801  142015  LYNNM       REL-V6R1M0  5761-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel   -
     A                                                                  '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
