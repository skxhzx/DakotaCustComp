     A****************************************************************
     A*
     A* ER211PR   Selectors % of standards by week
     A* 09 January, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER211'
     A*
     A            SUBTTL        40A  O    47
     A                                      SPACEB(001)
     A*
     A* Print week begining date
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A                                     1'Week'
     A            WKWNUM    R             +1REFFLD(WKWNUM ER211)
     A                                      EDTCDE(Z)
     A                                    +1':'
     A                                    +2'('
     A            BGDATE         6S 0     +1EDTCDE(Y)
     A                                    +1' TO '
     A            ENDATE         6S 0     +1EDTCDE(Y)
     A                                    +1')'
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    49'Sunday'
     A                                    57'Monday'
     A                                    65'Tuesday'
     A                                    74'Wednesday'
     A                                    85'Thursday'
     A                                    95'Friday'
     A                                   103'Saturday'
     A                                      SPACEA(001)
     A*
     A                                     1'Emp #'
     A                                      UNDERLINE
     A                                     7'Name'
     A                                      UNDERLINE
     A                                    49'  %   '
     A                                      UNDERLINE
     A                                    57'  %   '
     A                                      UNDERLINE
     A                                    65'  %    '
     A                                      UNDERLINE
     A                                    74'   %     '
     A                                      UNDERLINE
     A                                    85'   %    '
     A                                      UNDERLINE
     A                                    95'  %   '
     A                                      UNDERLINE
     A                                   103'   %    '
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKEMP#    R              1REFFLD(PTEMP# PIRTRAN)
     A                                      EDTCDE(Z)
     A            WKNAME        40A        7
     A            T#SUNT         7S 2     47EDTCDE(3)
     A            T#MONT         7S 2     55EDTCDE(3)
     A            T#TUET         7S 2     64EDTCDE(3)
     A            T#WEDT         7S 2     75EDTCDE(3)
     A            T#THUT         7S 2     85EDTCDE(3)
     A            T#FRIT         7S 2     93EDTCDE(3)
     A            T#SATT         7S 2    103EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#SUNT         7S 2     47EDTCDE(3)
     A            T#MONT         7S 2     55EDTCDE(3)
     A            T#TUET         7S 2     64EDTCDE(3)
     A            T#WEDT         7S 2     75EDTCDE(3)
     A            T#THUT         7S 2     85EDTCDE(3)
     A            T#FRIT         7S 2     93EDTCDE(3)
     A            T#SATT         7S 2    103EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER211: End of Report '
     A*
