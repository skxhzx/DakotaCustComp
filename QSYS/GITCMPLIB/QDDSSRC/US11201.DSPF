     A*%%TS  SD  20210512  071855  KENNETHE    REL-V7R4M0  5770-WDS
500bAA*----------------------------------------------------------------
500bAA*   Multilingual Version
500bAA*   Every screen has a corresponding Spanish screen.
500bAA*   All Spanish screens are at the end.
500bAA*   Any changes must be made to both screens.
500bAA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  US112FM   User Code Table Maintenance - Change screen
     A*  07 July, 1994
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*    11/19/96  RH   2.17
     A*      - If not EPM, fields are non-display and protected.
     A*
500 AA*    03/22/05  DAS  5.00
     A*      - Added Belt Printer option.
     A*
500aAA*    05/24/06  DAS  5.00a
     A*      - Added Language option.
     A*
500bAA*   06/04/06  JCJ  5.00b
     A*     - Merged Spanish screens from PIRS.
     A*
750 AA*   05/12/21  KDE  7.50
     A*     - Allow user option for Bluetooth belt printer
     A*
750aAA*   06/21/21  DAS  7.50a
     A*     - Added Bluetooth/Peel-Off option text.
     A*       Webfacing doesn't care about text changes
     A*       so didn't change format name.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20210512  071855  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'US112.01'
     A                                  1 27'User Code Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'User code  . . . . . .'
     A            W1CODE        10A  B  8 29
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Employee number  . . .'
     A  95                                  DSPATR(ND)
     A            W1EMP#         5Y 0B  9 29EDTCDE(4)
     A  61
     AO 90
     AO 95
     AO 91                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N90N91N61
     A N22                                  DSPATR(HI)
     A N90N91N61
     A N22                                  DSPATR(UL)
     A  95                                  DSPATR(ND)
     A                                 10  4'First name . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1FNAM        15A  B 10 29CHECK(LC)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 11  4'Middle initial . . . .'
     A  95                                  DSPATR(ND)
     A            W1INIT         1A  B 11 29
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 12  4'Last name  . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1LNAM        20A  B 12 29CHECK(LC)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 13  4'Normal warehouse . . .'
     A            W1WHSE         3Y 0B 13 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90                               DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 14  4'Normal department  . .'
     A  95                                  DSPATR(ND)
     A            W1WHDP         5A  B 14 29
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 15  4'Normal section . . . .'
     A  95                                  DSPATR(ND)
     A            W1STYP         1A  B 15 29
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N90                               DSPATR(HI)
     A N28N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 16  4'Shift code . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1SHFT         6A  B 16 29
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N90                               DSPATR(HI)
     A N29N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 17  4'Crew code  . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1CREW         6A  B 17 29
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 18  4'Normal hours/day . . .'
     A  95                                  DSPATR(ND)
     A            W1NHRS         3Y 1B 18 29EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N90                               DSPATR(HI)
     A N31N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 19  4'Belt printer user  . .'
     A  95                                  DSPATR(ND)
     A            W1BPU          1A  B 19 29
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N90                               DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 20  4'Language . . . . . . .'
     A            W1LNG          2A  B 20 29
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N90                               DSPATR(HI)
     A N33N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
750aDA**                               20 59'EN=English, SP=Spanish'
750aMA                                 20 40'EN=English, SP=Spanish'
     A                                 23  2'F4=Prompt F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
750aDA**                               19 59'Y, N, B(Bluetooth)'
750aMA                                 19 40'Y, N, B(Bluetooth)+
750aMA                                       , P(Bluetooth/Peel-Off)'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500bAA*
500bAA*****************************************************************
500bAA* Spanish Screens
500bAA*****************************************************************
500bAA*----------------------------------------------------------------
500bAA          R SCRN1SP
     A*%%TS  SD  20060524  104949  DAVE        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'US112.01'
     A                                  1 27'Maint. de Table de Usuario '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Incorp. Detallada'
     A                                      DSPATR(HI)
     A                                  5  2'Escriba cambios oprima Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Codigo de usuario. . .'
     A            W1CODE        10A  B  8 29
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Numero de empleado . .'
     A  95                                  DSPATR(ND)
     A            W1EMP#         5Y 0B  9 29EDTCDE(4)
     A  61
     AO 90
     AO 95
     AO 91                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N90N91N61
     A N22                                  DSPATR(HI)
     A N90N91N61
     A N22                                  DSPATR(UL)
     A  95                                  DSPATR(ND)
     A                                 10  4'Nombre . . . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1FNAM        15A  B 10 29CHECK(LC)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 11  4'2o nombre (1o letra) .'
     A  95                                  DSPATR(ND)
     A            W1INIT         1A  B 11 29
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 12  4'Apellido . . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1LNAM        20A  B 12 29CHECK(LC)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 13  4'Almacen normal . . . .'
     A            W1WHSE         3Y 0B 13 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90                               DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 14  4'Departamento normal. .'
     A  95                                  DSPATR(ND)
     A            W1WHDP         5A  B 14 29
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 15  4'Seccion normal . . . .'
     A  95                                  DSPATR(ND)
     A            W1STYP         1A  B 15 29
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N90                               DSPATR(HI)
     A N28N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 16  4'Codigo de turno. . . .'
     A  95                                  DSPATR(ND)
     A            W1SHFT         6A  B 16 29
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N90                               DSPATR(HI)
     A N29N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 17  4'Codigo de equipo . . .'
     A  95                                  DSPATR(ND)
     A            W1CREW         6A  B 17 29
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30N90                               DSPATR(HI)
     A N30N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 18  4'horas normales de dia.'
     A  95                                  DSPATR(ND)
     A            W1NHRS         3Y 1B 18 29EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31N90                               DSPATR(HI)
     A N31N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 19  4'Usuario del impresor .'
     A  95                                  DSPATR(ND)
     A            W1BPU          1A  B 19 29
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32N90                               DSPATR(HI)
     A N32N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
750 MA                                 19 59'Y, N, B(Bluetooth)'
     A                                 20  4'Idioma . . . . . . . .'
     A            W1LNG          2   B 20 29
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33N90                               DSPATR(HI)
     A N33N90                               DSPATR(UL)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 20 59'EN=Ingles , SP=Espanol'
     A                                 23  2'F4=Incita F5=Rfresca F12=Cancela'
     A                                      COLOR(BLU)
