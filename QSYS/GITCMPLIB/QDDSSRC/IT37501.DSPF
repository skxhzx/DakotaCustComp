     A*%%TS  SD  19941109  101538  HEMANT      REL-V2R1M1  5738-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT375FM   Item Slot list in Pick order. - Display
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
417 AA*    03/18/03  RH   4.17
     A*      - Add "To Item" for range selection.
     A*
     A*----------------------------------------------------------------
     F*  Client Custom Revisions
     F*
CBI AF*    08/25/08  CBI-JGD
     F*      Added record 'WARNMSG' (Large report process warning).
     F*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941109  101538  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IT375.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 21'Print Item Slot List'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 53'Code'
     A                                  9  4'From Item. . . . . . . . . .'
     A            W1ITEM        15A  B  9 34
     A N29N90                               DSPATR(HI)
     A N29N90                               DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'To Item. . . . . . . . . . .'
     A            W1TITM        15A  B 10 34
     A N28N90                               DSPATR(HI)
     A N28N90                               DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 11 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'Y=Yes, N=No'
     A                                 12  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 12 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 13 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
CBI AA          R WARNMSG
CBI AA                                      LOCK
CBI AA                                      OVERLAY
CBI AA                                      SLNO(3)
CBI AA N21                              5  2'THE NUMBER OF ITEMS TO BE PROCESSE-
CBI AA                                      D WILL CREATE A LARGE REPORT'
CBI AA                                      COLOR(RED)
CBI AA                                      DSPATR(BL)
CBI AA  21                              5  2'THE NUMBER OF ITEMS TO BE PROCESSE-
CBI AA                                      D IS EQUAL OR GREATER THAN 50,'
CBI AA                                      COLOR(RED)
CBI AA                                      DSPATR(BL)
CBI AA  21                              6  2'THE REPORT WILL BE SENT TO OUTQ PR-
CBI AA                                      T01'
CBI AA                                      COLOR(RED)
CBI AA                                      DSPATR(BL)
CBI AA                                  8  2'Press Enter to Continue or F12 to -
CBI AA                                      Cancel'
CBI AA                                      DSPATR(HI)
