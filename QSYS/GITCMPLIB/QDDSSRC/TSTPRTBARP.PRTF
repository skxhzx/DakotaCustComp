     A****************************************************************
     A*
     A* TSTPRTBARP   Test printing BARCODE
     A* 16 September 2014
     A* Dave Sommerville
     A*
     A* 09/16/14  DAS  6/50
     A*  - Created
     A*
     A****************************************************************
     A*
     A*  Compile with following options:
     A*
     A*    DEV(HP1300) --- It looks like this isn't necessary
     A*    DEVTYPE(*AFPDS)
     A*    PAGESIZE(66 80)
     A*
     A*  Printer device used for testing:
     A*
     A*     DEVD        HP1300
     A*     DEVCLS      *VRT
     A*     TYPE        3812
     A*     MODEL       1
     A*     TRANSFORM   *YES
     A*
     A*  Sent spool file to following remote writer:
     A*
     A*     DAVEHP6
     A*
     A****************************************************************
     A*
     A*  URL to IBM BARCODE document (split across multiple lines)
     A*
     A*   http://publib.boulder.ibm.com/infocenter/iadthelp/v6r0
     A*   /index.jsp?topic=/com.ibm.etools.iseries.langref2.doc
     A*   /rzakdmstptbarco.htm
     A*
     A****************************************************************
     A*
     A*  Notes
     A*
     A*    Had problems printing CODE3OF9. It would print the barcode
     A*    but it would also print a second short barcode overlaying
     A*    the bottom right corner.
     A*
     A*    CODE128 is proably a better choice, anyway.
     A*
     A*    Discovered that CODE128 has the same problem when the
     A*    length of the field is increased. The length was originally
     A*    5 without a problem. Increasing it to 8 makes the extra
     A*    garbage print.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
    AA            UDATE          6S 0O     3EDTCDE(Y)
     A                                    30
     A                                      DFT('Barcode Test')
     A                                    70
     A                                      DFT('TSTPRTBAR')
     A                                      SPACEA(001)
     A*
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('Stock1')
     A                                      UNDERLINE
     A                                    22
     A                                      DFT('Barcode')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            SLDISP    R        O     1REFFLD(SLDISP SLOT)
     A            SLSTK1    R        O    14REFFLD(SLSTK1 SLOT)
     A                                    22DFT('CODE3OF9 - ')
     A            VAL39         12A  O    35BARCODE(CODE3OF9 *HRI *NOAST)
     A                                      SPACEA(004)
     A            TXT39         12A  O    35
     A                                      SPACEA(001)
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            SLDISP    R        O     1REFFLD(SLDISP SLOT)
     A            SLSTK1    R        O    14REFFLD(SLSTK1 SLOT)
     A                                    22DFT('CODE128  - ')
     A            VAL128        12A  O    35BARCODE(CODE128 (0.5 *UOM))
     A                                      SPACEA(004)
     A            TXT128        12A  O    35
     A                                      SPACEA(001)
     A*
     A          R DETAIL3
     A                                      SPACEB(001)
     A            SLDISP    R        O     1REFFLD(SLDISP SLOT)
     A            SLSTK1    R        O    14REFFLD(SLSTK1 SLOT)
     A                                    22DFT('PDF417   - ')
     A            VALPDF        30A  O    35BARCODE(PDF417 (*PDF417 5 *MIN 3))
     A                                      SPACEA(004)
     A            TXTPDF        30A  O    35
     A                                      SPACEA(001)
     A*
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('TSTPRTBAR: End of Report ')
     A*
