     A*%%TS  SD  20120504  120702  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  HV112FM    Question File Validation Maintenance - Change
     A*  10 April 21012
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    04/10/12  LMC  6.40a
     A*      - Created
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20120504  120702  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'HV112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Type . . . . . . . . .'
     A            W1TYPE         3A  B  8 33DSPATR(PR)
     A                                      CHECK(ER)
     A            W1CODE        15A  B  9 28DSPATR(PR)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 20'HACCP Question File Validation Mai-
     A                                      ntenance'
     A                                      DSPATR(HI)
     A  13        W1T3VL        30A  B 11 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  14        W1T5VL         9Y 2B 11 28EDTCDE(M)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  15        W1VALU        15Y 0B 11 28EDTCDE(Z)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Valid Answer. . . . .'
     A                                 23 36' '
     A            W1LEVL         4A  B  8 28DSPATR(PR)
     A            W1SEQ#         5S 0B  8 42DSPATR(ND)
     A                                      DSPATR(PR)
     A            W1VSQ#         5S 0B  8 50DSPATR(ND)
     A                                      DSPATR(PR)
     A            W1QSTN        45A  O 10 28DSPATR(HI)
     A  14        W1T5RG         9Y 2B 11 41EDTCDE(M)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  34                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  15        W1VALR        15Y 0B 11 46EDTCDE(Z)
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Description(value Only)'
     A            W1DESC        30A  B 12 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Warning Message-(Range)'
     A            W1WARN        30A  B 13 28
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
