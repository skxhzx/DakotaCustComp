     A****************************************************************
     A*
     A* HC321PR   History HACCP report
     A* 23 April, 2008
     A* Roxanne Harrison
     A*
     A*--------------------------------------------------------------*
     A* 04/23/08  RH   5.20
     A*  - Created using HC311
     A*
530 AA* 01/23/10  MLB  5.30
     A*  - Enh: Added fmt HEAD1A to add line to print from/to
     A*    date range when selected.
     A*
640aAA* 02/14/12  LMC  6.40a
     A*  - Enh: Added Vendor name and number
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    56
530 DA*                                     DFT('Histoy HACCP Information +
530 MA                                      DFT('History HACCP Information +
     A                                      Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('HC321')
530 AA*
530 AA          R HEAD1A
530 AA                                      TEXT('REPORT HEADING 1a')
530 AA                                      SPACEB(001)
530 AA            W1FRMDT        6S 0O    60
530 AA                                      EDTCDE(Y)
530 AA                                    +2
530 AA                                      DFT('thru')
530 AA            W1TODT         6S 0O    +2
530 AA                                      EDTCDE(Y)
530 AA                                      SPACEA(001)
530 AA*
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    64
     A                                      DFT('PROD')
     A                                    70
     A                                      DFT('PROD')
     A                                    76
     A*                                     DFT('Trailer Temp')
     A                                      DFT('Refer Setting')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('PO Number')
     A                                      UNDERLINE
     A                                    11
     A                                      DFT('Item      ')
     A                                      UNDERLINE
     A                                    22
     A                                      DFT('Descriptiont            ')
     A                                      UNDERLINE
     A                                    48
     A                                      DFT('Truck Seal #')
     A                                      UNDERLINE
     A                                    64
     A                                      DFT('Area ')
     A                                      UNDERLINE
     A                                    70
     A                                      DFT('Type ')
     A                                      UNDERLINE
     A                                    76
     A                                      DFT('Reqd')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('Delvd')
     A                                      UNDERLINE
     A                                    95
     A                                      DFT('Load Condition')
     A                                      UNDERLINE
     A                                   116
     A                                      DFT('Inspected By')
     A                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            HAHPO     R        O     1REFFLD(HAHPO   HC321)
640aAA                                    11'Vendor:'
640aAA            PHHVEN    R        O    19REFFLD(PHHVEN POHHDR)
640aAA                                      EDTCDE(Z)
640aAA            PHHVNA    R        O    31REFFLD(PHHVNA POHHDR)
     A            W1ITEM        10A  O    11
640aAA                                      SPACEB(001)
     A            W1DESC        25A  O    22
     A            HAHTKSL   R        O    48REFFLD(HAHTKSL HC321)
     A            HAHPRDA   R        O    64REFFLD(HAHPRDA HC321)
     A            HAHPRDT   R        O    70REFFLD(HAHPRDT HC321)
     A            HAHRSRQ   R        O    76REFFLD(HAHRSRQ HC321)
     A                                      EDTCDE(L)
     A            HAHRSDL   R        O    85REFFLD(HAHRSDL HC321)
     A                                      EDTCDE(L)
     A            W1LDCN        20A  O    95
     A            W1RCBY        15A  O   116
     A*           W1RCBY        20A  O   111
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('TRAILER R/M/T:')
     A            HAHTTRR   R        O    16REFFLD(HAHTTRR HC321)
     A                                      EDTCDE(L)
     A                                    23
     A                                      DFT('/')
     A            HAHTTMD   R        O    25REFFLD(HAHTTMD HC321)
     A                                      EDTCDE(L)
     A                                    32
     A                                      DFT('/')
     A            HAHTTNS   R        O    34REFFLD(HAHTTNS HC321)
     A                                      EDTCDE(L)
     A                                    45
     A                                      DFT('PALLET R/M/T:')
     A            HAHPTRT   R        O    59REFFLD(HAHPTRT HC321)
     A                                      EDTCDE(L)
     A                                    65
     A                                      DFT(':')
     A            HAHPTRM   R        O    67REFFLD(HAHPTRM HC321)
     A                                      EDTCDE(L)
     A                                    73
     A                                      DFT(':')
     A            HAHPTRB   R        O    75REFFLD(HAHPTRB HC321)
     A                                      EDTCDE(L)
     A                                    82
     A                                      DFT('/')
     A            HAHPTMT   R        O    84REFFLD(HAHPTMT HC321)
     A                                      EDTCDE(L)
     A                                    90
     A                                      DFT(':')
     A            HAHPTMM   R        O    92REFFLD(HAHPTMM HC321)
     A                                      EDTCDE(L)
     A                                    99
     A                                      DFT(':')
     A            HAHPTMB   R        O   101REFFLD(HAHPTMB HC321)
     A                                      EDTCDE(L)
     A                                   108
     A                                      DFT('/')
     A            HAHPTNT   R        O   110REFFLD(HAHPTNT HC321)
     A                                      EDTCDE(L)
     A                                   117
     A                                      DFT(':')
     A            HAHPTNM   R        O   119REFFLD(HAHPTNM HC321)
     A                                      EDTCDE(L)
     A                                   126
     A                                      DFT(':')
     A            HAHPTNB   R        O   128REFFLD(HAHPTNB HC321)
     A                                      EDTCDE(L)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('HC321: End of Report ')
     A*
