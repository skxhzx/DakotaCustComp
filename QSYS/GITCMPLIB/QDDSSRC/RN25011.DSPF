     A*%%TS  SD  19990924  151755  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2017 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   P.O. Box 1957
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  RN250FM   Customer Returns Putaway - VIRTUAL SLOT
     A*  23 September 1999
     A*  Roxanne Harrison
     A*  Created using RN150
     A*
     A*  SK112FM - 1.01
     A*
     A*----------------------------------------------------------------
     A*  Revisions
415bAA*  02/07/01  RH   4.15b
     A*    - Add catch weight field to screen.
416bAA*  11/09/01  RH   4.16b
     A*    - Add Dispositon, Impact inventory, Use label for putaway,
     A*      Invoice#, Mfg code, and UPC to screen2.
     A*    - Change heading position on screen1 like standard look.
     A*    - Add screen2.
416dAA*  11/28/01  RH   4.16d
     A*    - Disable F17 Recv. log on screen 2.
416eAA*  02/04/02  RH   4.16e
     A*    - Change Impact inventory to Put Away.
     A*    - Change W1FLBL to W1ULBL use label for putaway.
416fAA*  06/13/02  MLB  4.16f
     A*    - Change edit code from 4 to 3 for catch weight.
416lAF*    09/05/02  RH   4.16l
     F*      - Move manufacturer date to screen 2.
     F*      - Move corresponding item to screen 1.
510 AA*  09/28/06  JCJ  510 A
     A*  - Made the following changes to screen format SCREEN1         "
     A*  - Change field label from "Invoice number" to "Delivery number"
     A*    when client is PACKERS.                                     "
     A*  - Added entry of Billing Document number if client is Packers."
     A*  - Dropped text "-ENTER ONE-" and the three entry fileds that  "
     A*    follow one line to make room for Billing Document Number.   "
530aAA*  02/03/10  RH   530a
     A*  - Fix: Do not underline W1BIL# for ind 17, is only for Packers"
EGF AA*  03/01/10  RH   EGFa
     A*  - Add 58 to protect W2IINV for EGF.  Value set using RNINVF.  "
     A*  - Add 58 to protect & nondisplay W1QT03.                      "
640aAA*  01/31/11  RH   640a
     A*  - Fix: Protect breakdown2 qty W1QT03, was non-display only    "
640bAA*  09/04/12  RH   640b
     A*  - ENH: Rename format member to RN25003, enable F20 override   "
640cAA*  07/18/12  LMC  640c
     A*  - Enh: renamed format for screen change to RN25004.
     A*  - Enh: Add f11 - Mfr Decifer command.
DRYaAA*  01/31/11  RH   DRYa
     A*  - Enh: Protect transaction with indicator 59.                 "
CSPbAA*  09/14/11  RTR  CSP   P#01053
     A*  - Enh: Set default disposition from return code.
650aAA*  11/26/12  LMC  650a
     A*  - Enh: renamed format for screen change to RN25005.
     A*  - Enh: UCI/GS1 scanning
     A*  - Enh: Added Lot code to returns
650bAA*  10/31/13  LMC  650b
     A*  - Enh: renamed format for screen change to RN25006.
     A*  - Enh: PTI code entry field added.
700 AA*  11/07/16  JGD  700
     A*  - Enh: Add "Item Type" field (W1TYPE) to be displayed when
     A*         client is Cheney to identify Item types "BANC or NORM",
     A*         otherwise original field "ORDER #" will be displayed.
     A*  - Enh: renamed format for screen change to RN25007.
700aAA*  05/07/15  RH   7.00A
     A*    - ENH: RENAME TO RN25007
     A*    - ENH: ADDED THE ABILITY TO PROTECT NORMAL QTY ENTRY
     A*
700bAA*  03/29/16  LMC  7.00b
     A*    - Renamed from RN25007 to RN25008
     A*    - Enh: Adding logic for Metric.
     A*
720aAA*  07/26/18  LMC  7.20a
     A*    - Renamed from RN25009 to RN25010
     A*    - Enh: Adding Uom on the mispick item number
740aAA*  08/14/20  LMC  7.40a
     A*    - Renamed from RN25010 to RN25011
     A*    - Enh: Added scan count to screen4
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: SGC Foodservice
SGC AA*  09/07/17  MLB  SGC  P#01801
     A*    - Add new field to screen; W1lookt - Lookup Type (O, I, C)
     A*      Where O=Order# lookup, I=Invoice history lookup and
     A*            C=Pick-up request.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN0
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  5 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  4'Printer device  . . . . .'
     A            W1DEV         10A  B  6 31
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                  7  4'Printer form  . . . . . .'
     A            W1FORM        10A  B  7 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
     A                                  8  4'User id . . . . . . . . .'
     A            W1USER        10A  B  8 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 10  4'Item number . . . . . . .'
     A            W1ITEM        15A  B 10 31
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A            WOHEAD        60   O  1 11DSPATR(HI)
     A            W0SCRN         8   O  1  2
     A          R SCREEN1
     A*%%TS  SD  19990924  151755  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
700bAA                                      CA07
640B A                                      CF20
416H A                                      CF09
     A                                      CF04
     A  98                                  ALARM
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
416  A                                  3  2'Warehouse:'
416  A            WOWHSE        40A  O  3 13
700 MA N38                              5  6'Order:'
700 MA N38        W1ORD          7Y 0O  5 13EDTCDE(4)
700 AA  38                              5  7'TYPE:'
700 AA  38        W1TYPE         5A  O  5 13DSPATR(HI)
416  A                                  4  7'Item:'
416  A            WOITEM        49A  O  4 13
     A**                                5  4'Warehouse . . . . . . . .'
     A**          WOWHSE        40A  O  5 31
     A**                                6  4'User id . . . . . . . . .'
     A**          WOUSER        10A  O  6 31
     A**                                7  4'Printer device  . . . . .'
     A**          WODEV         10A  O  7 31
     A**                                8  4'Printer form  . . . . . .'
     A**          WOFORM        10A  O  8 31
     A**                               10  4'Item number . . . . . . .'
     A**          WOITEM        49A  O 10 31
     A                                  9  4'Quantity returned . . . .'
     A            W1QT01         5Y 0B  9 31EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
700A A  41                                  DSPATR(ND)
700A A  41
     AO 90                                  DSPATR(PR)
     A            W1UM1          5A  O  9 37
700A A  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B  9 45EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N31N90                               DSPATR(UL)
     A  94                                  DSPATR(ND)
     A  94
     AO 90                                  DSPATR(PR)
     A            W1UM2          5A  O  9 49
     A  94                                  DSPATR(ND)
     A            W1QT03         3Y 0B  9 57EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
EGF  A  58
     AO 93                                  DSPATR(ND)
EGF  A  58
640a AO 93
     AO 90                                  DSPATR(PR)
     A            W1UM3          5A  O  9 61
     A  93                                  DSPATR(ND)
     A                                 10  4'Number of pallets . . . .'
     A            W1PLTS         5Y 0B 10 31EDTCDE(4)
     A N36N90                               DSPATR(HI)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
EGF  A  93
     A  90                                  DSPATR(PR)
     A                                 11  4'Catch weight returned . .'
     A            W1CWTR         9Y 2B 11 31
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
416F A                                      EDTCDE(4)
     A N33N90N95                            DSPATR(HI)
700bAA            W1KGLB         3   O 11 42
700bAA  95                                  DSPATR(ND)
     A                                 12  4'Reason code . . . . . . .'
     A            W1CODE         2A  B 12 31
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416b A                                 13  4'Dispositon code . . . . .'
416b A            W1DCDE         5A  B 13 31
CSPbDA*N27                                  DSPATR(HI)
CSPbMA N27N89                               DSPATR(HI)
416b A  27                                  DSPATR(RI)
416b A N27N90                               DSPATR(UL)
416b A  07                                  DSPATR(PC)
CSPbDA* 90                                  DSPATR(PR)
CSPbAA  89
CSPbAAO 90                                  DSPATR(PR)
416b A                                 14  4'Use label for putaway . .'
416b A            W1ULBL         1A  B 14 31
416b A N28                                  DSPATR(HI)
416b A  28                                  DSPATR(RI)
416b A N28N90                               DSPATR(UL)
416b A  08                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
510 DA***                              15  4'Invoice # . . . . . . . .'
510 MA            W1INVH        25A    15  4
416b A            W1INV#         9A  B 15 31
416b A  29                                  DSPATR(RI)
416b A N29N90                               DSPATR(UL)
416b A  09                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
SGC AA*
SGC AA            W1LKTX        11A    15 43
SGC AA            W1LKTP         1A  B 15 55
SGC AA  38                                  DSPATR(RI)
SGC AA N38N90                               DSPATR(UL)
SGC AA  38                                  DSPATR(PC)
SGC AA  90                                  DSPATR(PR)
SGC AA            W1LKCD         5A    15 58
SGC AA*
     A                                 16  4'Transaction number  . . .'
     A            W1TRN#         7Y 0B 16 31
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
DRYa A  59
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(4)
510 AA            W1BILH        25A    17  4
510 AA            W1BIL#        10A  B 17 31
510 AA N37                                  DSPATR(HI)
510 AA  37                                  DSPATR(RI)
530ADA*N37N90                               DSPATR(UL)
530aMA N37N90N17                            DSPATR(UL)
510 AA  17
510 AAO 90                                  DSPATR(PR)
510 A *
510 A * Dropped these fields one line to make room for Billing Document
510 A * Number
510 A *
     A                                 18  3'-ENTER ONE-'
     A                                 19  3'Corresponding item . . . .'
     A            W1CITM        15A  B 19 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
720aAA            W1QM01         5Y 0B 19 47EDTCDE(4)
720aAA N44N90                               DSPATR(HI)
720aAA  44                                  DSPATR(RI)
720aAA  44                                  DSPATR(PC)
720aAA N44N90                               DSPATR(UL)
720aAA  45                                  DSPATR(ND)
720aAA  45
720aAAO 90                                  DSPATR(PR)
720aAA            W1UM1M         5A  O 19 53
720aAA  45                                  DSPATR(ND)
720aAA            W1QM02         3Y 0B 19 59EDTCDE(4)
720aAA  46                                  DSPATR(RI)
720aAA  46                                  DSPATR(PC)
720aAA N46N90                               DSPATR(UL)
720aAA  47                                  DSPATR(ND)
720aAA  47
720aAAO 90                                  DSPATR(PR)
720aAA            W1UM2M         5A  O 19 63
720aAA  47                                  DSPATR(ND)
720aAA            W1QM03         3Y 0B 19 69EDTCDE(4)
720aAA  48                                  DSPATR(RI)
720aAA  48                                  DSPATR(PC)
720aAA N48N90                               DSPATR(UL)
720aAA  58
720aAAO 49                                  DSPATR(ND)
720aAA  58
720aAAO 49
720aAAO 90                                  DSPATR(PR)
720aAA            W1UM3M         5A  O 19 73
720aAA  49                                  DSPATR(ND)
     A                                 20  3'Corsp. UPC Code  . . . . .'
     A            W1UPC         50A  B 20 31CHECK(ER)
     A*N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21  3'Cor. Mfg code/Vend Item# .'
     A            W1VMFG        15A  B 21 31
     A*N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A**                               19  4'Corresponding item  . . .'
     A**          W1CITM        15A  B 19 31
     A* 35                                  DSPATR(RI)
     A*N35N90                               DSPATR(UL)
     A* 15                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
700bAA            CMDLN1        79A  O 23  2
700bDA*                                23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
700bDA*                                     =UOM  F12=Cancel  F20=Accept Warn'
     A                                      COLOR(BLU)
     A            W1SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A                                  2 33'Manual Put Away'
     A          R SCREEN2
     A*%%TS  SD  20011031  092817  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
416dDA*                                     CF17
650bAA                                      CF20
640cAA                                      CF11
     A  98                                  ALARM
     A            W2SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Manual Put Away'
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A                                  5  6'Order:'
     A            W1ORD          7Y 0O  5 13EDTCDE(4)
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
416eDA**                                9  3'Impact Inventory . . . . .'
416eMA                                  9  3'Put Away (Y,N) . . . . . .'
     A            W2IINV         1A  B  9 31
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
EGF  A  58
     AO 90                                  DSPATR(PR)
     A                                 10  3'Memo . . . . . . . . . . .'
     A            W2MEMO        30A  B 10 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 12  4'Expiration date . . . . .'
     A            W2EXPD         6Y 0B 12 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96
650aAAO 83
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A N25N90N96                            DSPATR(HI)
     A                                 13  3'Manufacturing date  . . . .'
     A            W2MFDT         6Y 0B 13 31EDTCDE(4)
     A  32                                  DSPATR(RI)
     A  87                                  DSPATR(ND)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
650aAA  84
650aDA* 87                                  DSPATR(PR)
650aMAO 87                                  DSPATR(PR)
     A N32N90N87                            DSPATR(HI)
650aAA                                 14  3'Lot Code. . . . . . . . . .'
650aAA  88                                  DSPATR(ND)
650aAA            W2LOTC        30   B 14 31
650aAA  88                                  DSPATR(ND)
650aAA  85
650aDA* 88                                  DSPATR(PR)
650aMAO 88                                  DSPATR(PR)
650bAA                                 15  3'PTI Code. . . . . . . . . .'
650bAA  81                                  DSPATR(ND)
650bAA            W2PTI          4Y 0B 15 31
650bAA  81                                  DSPATR(ND)
650bMAO 81                                  DSPATR(PR)
650bAA  41                                  DSPATR(RI)
650bAA  41                                  DSPATR(PC)
650bAA N41N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
640cMA*                                     2=Cancel  '
650bDA*                                     1=Mfr Code  F12=Cancel'
650bMA                                      1=Mfr Code  F12=Cancel  F20=Ovr'
416dDA*                                     2=Cancel  F17=View rcv log'
     A                                      COLOR(BLU)
650aAA          R SCREEN3
650aAA*%%TS  SD  19990924  151755  ROCKY       REL-V4R2M0  5769-PW1
650aAA                                      OVERLAY
650aAA                                      CLRL(23)
650aAA                                      CF20
650aAA                                      CF09
650aAA                                      CF04
650aAA  98                                  ALARM
650aAA                                  1 72DATE
650aAA                                      EDTCDE(Y)
650aAA                                  7  2'Type changes, press Enter.'
650aAA                                      COLOR(BLU)
650aAA                                  3  2'Warehouse:'
650aAA            WOWHSE        40A  O  3 13
650aAA                                  5  6'Order:'
650aAA            W1ORD          7Y 0O  5 13EDTCDE(4)
650aAA                                  4  7'Item:'
650aAA            WOITEM        49A  O  4 13
650aAA                                 09  4'Scan License. . . . . . . '
650aAA  73                                  DSPATR(ND)
650aAA            W3LCNS        15A  B 09 31
650aAA  43                                  DSPATR(RI)
650aAA N43N90                               DSPATR(UL)
650aAA  43                                  DSPATR(PC)
650aAA  90
650aAAO 80
650aAAO 73                                  DSPATR(PR)
650aAA  73                                  DSPATR(ND)
650aAA                                 10  4'Quantity returned . . . .'
650aAA            W1QT01         5Y 0B 10 31EDTCDE(4)
650aAA N24N90                               DSPATR(HI)
650aAA  24                                  DSPATR(RI)
650aAA  04                                  DSPATR(PC)
650aAA N24N90                               DSPATR(UL)
650aAA  82
650aDA* 90                                  DSPATR(PR)
650aMAO 90                                  DSPATR(PR)
650aAA            W1UM1          5A  O 10 37
650aAA            W1QT02         3Y 0B 10 45EDTCDE(4)
650aAA  31                                  DSPATR(RI)
650aAA  11                                  DSPATR(PC)
650aAA N31N90                               DSPATR(UL)
650aAA  94                                  DSPATR(ND)
650aAA  94
650aAAO 82
650aAAO 90                                  DSPATR(PR)
650aAA            W1UM2          5A  O 10 49
650aAA  94                                  DSPATR(ND)
650aAA            W1QT03         3Y 0B 10 57EDTCDE(4)
650aAA  30                                  DSPATR(RI)
650aAA  10                                  DSPATR(PC)
650aAA N30N90                               DSPATR(UL)
650aAA  58
650aAAO 93                                  DSPATR(ND)
650aAA  58
650aAAO 82
650aAAO 93
650aAAO 90                                  DSPATR(PR)
650aAA            W1UM3          5A  O 10 61
650aAA  93                                  DSPATR(ND)
650aAA                                 11  4'Catch weight returned . .'
650aAA            W1CWTR         9Y 2B 11 31
650aAA  33                                  DSPATR(RI)
650aAA N33N90                               DSPATR(UL)
650aAA  33                                  DSPATR(PC)
650aAA  95                                  DSPATR(ND)
650aAA  95
650aAAO 72
650aAAO 90                                  DSPATR(PR)
650aAA                                      EDTCDE(4)
650aAA N33N90N95                            DSPATR(HI)
700bAA            W1KGLB         3   O 11 42
700bAA  95                                  DSPATR(ND)
650aAA                                 12  4'Reason code . . . . . . .'
650aAA            W1CODE         2A  B 12 31
650aAA N22                                  DSPATR(HI)
650aAA  22                                  DSPATR(RI)
650aAA N22N90                               DSPATR(UL)
650aAA  02                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 13  4'Dispositon code . . . . .'
650aAA            W1DCDE         5A  B 13 31
650aAA N27N89                               DSPATR(HI)
650aAA  27                                  DSPATR(RI)
650aAA N27N90                               DSPATR(UL)
650aAA  07                                  DSPATR(PC)
650aAA  89
650aAAO 90                                  DSPATR(PR)
650aAA                                 14  4'Use label for putaway . .'
650aAA            W1ULBL         1A  B 14 31
650aAA N28                                  DSPATR(HI)
650aAA  28                                  DSPATR(RI)
650aAA N28N90                               DSPATR(UL)
650aAA  08                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA            W1INVH        25A    15  4
650aAA            W1INV#         9A  B 15 31
650aAA  29                                  DSPATR(RI)
650aAA N29N90                               DSPATR(UL)
650aAA  09                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 16  4'Transaction number  . . .'
650aAA            W1TRN#         7Y 0B 16 31
650aAA  34                                  DSPATR(RI)
650aAA N34N90                               DSPATR(UL)
650aAA  14                                  DSPATR(PC)
650aAA  59
650aAAO 90                                  DSPATR(PR)
650aAA                                      EDTCDE(4)
650aAA            W1BILH        25A    17  4
650aAA            W1BIL#        10A  B 17 31
650aAA N37                                  DSPATR(HI)
650aAA  37                                  DSPATR(RI)
650aAA N37N90N17                            DSPATR(UL)
650aAA  17
650aAAO 90                                  DSPATR(PR)
650aAA                                 18  3'-ENTER ONE-'
650aAA                                 19  3'Corresponding item . . . .'
650aAA            W1CITM        15A  B 19 31
650aAA  23                                  DSPATR(RI)
650aAA N23N90                               DSPATR(UL)
650aAA  03                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 20  3'Corsp. UPC Code  . . . . .'
650aAA            W1UPC         50A  B 20 31CHECK(ER)
650aAA  25                                  DSPATR(RI)
650aAA N25N90                               DSPATR(UL)
650aAA  05                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 21  3'Cor. Mfg code/Vend Item# .'
650aAA            W1VMFG        15A  B 21 31
650aAA  26                                  DSPATR(RI)
650aAA N26N90                               DSPATR(UL)
650aAA  06                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
650aAA                                      =UOM  F12=Cancel  F20=Accept Warn'
650aAA                                      COLOR(BLU)
650aAA            W3SCRN         8A  O  1  2
650aAA            WOHEAD        60A  O  1 11DSPATR(HI)
650aAA                                  2 33'Manual Put Away'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
650aAA          R SCREEN4
650aAA*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
650aAA                                      OVERLAY
650aAA                                      CLRL(23)
700bAA                                      CA07
650aAA                                      CF20
650aAA                                      CF22
650aAA  98                                  ALARM
650aAA            W4SCRN         8   O  1  2
650aAA            WOHEAD        60   O  1 11DSPATR(HI)
650aAA                                  1 72DATE
650aAA                                      EDTCDE(Y)
650aAA                                  3  2'Warehouse:'
650aAA            WOWHSE        40A  O  3 13
650aAA                                  5  6'Order:'
650aAA            W1ORD          7Y 0O  5 13EDTCDE(4)
740aAA                                  6  6'Scan.:'
740aAA            SCAN           5Y 0O  6 13EDTCDE(4)
650aAA                                  4  7'Item:'
650aAA            WOITEM        49A  O  4 13
650aAA                                 07  4'Scan UCI  . . . . . . . .'
650aAA            W1UCI        100A  B 07 31
650aAA                                      CHECK(ER)
700bDA*N25                                  DSPATR(HI)
700bDA* 25                                  DSPATR(RI)
700bDA*N25N90                               DSPATR(UL)
700bDA* 05                                  DSPATR(PC)
700bAA N55                                  DSPATR(HI)
700bAA  55                                  DSPATR(RI)
700bAA N55N90                               DSPATR(UL)
700bAA  65                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
700bAA                                 11  4'Catch weight returned . .'
700bAA N33
700bAAO 95                                  DSPATR(ND)
700bAA            W4CWTR         9Y 2B 11 31
700bAA  33                                  DSPATR(RI)
700bAA N33N90                               DSPATR(UL)
700bAA  33                                  DSPATR(PC)
700bAA N33
700bAAO 95                                  DSPATR(ND)
700bAA  95
700bAAO 90                                  DSPATR(PR)
700bAA                                      EDTCDE(4)
700bAA N33N90N95                            DSPATR(HI)
700bAA            W1KGLB         3   O 11 42
700bAA N33
700bAAO 95                                  DSPATR(ND)
700bAA                                 12  4'Expiration date . . . . .'
700bAA N25
700bAAO 96                                  DSPATR(ND)
700bAA            W4EXPD         6Y 0B 12 31
700bAA  25                                  DSPATR(RI)
700bAA N25N90                               DSPATR(UL)
700bAA  05                                  DSPATR(PC)
700bAA N25
700bAAO 96                                  DSPATR(ND)
700bAA  96
700bAAO 83
700bAAO 90                                  DSPATR(PR)
700bAA                                      EDTCDE(3)
700bAA N25N90N96                            DSPATR(HI)
700bAA                                 13  3'Manufacturing date  . . . .'
700bAA N32
700bAAO 87                                  DSPATR(ND)
700bAA            W4MFDT         6Y 0B 13 31EDTCDE(4)
700bAA  32                                  DSPATR(RI)
700bAA N32
700bAAO 87                                  DSPATR(ND)
700bAA N32N90                               DSPATR(UL)
700bAA  12                                  DSPATR(PC)
700bAA  84
700bAAO 87                                  DSPATR(PR)
700bAA N32N90N87                            DSPATR(HI)
700bDA*                                23  2'F3=Exit   F12=Cancel  F20=Override-
700bdA*                                       F22=Complete License'
700bAA            CMDLN1        79A  O 23  2
650aAA                                      COLOR(BLU)
650aAA          R SCREEN5
650aAA*%%TS  SD  19990924  151755  ROCKY       REL-V4R2M0  5769-PW1
650aAA                                      OVERLAY
650aAA                                      CLRL(23)
650aAA  98                                  ALARM
650aAA                                  1 72DATE
650aAA                                      EDTCDE(Y)
650aAA                                  7  2'Type changes, press Enter.'
650aAA                                      COLOR(BLU)
650aAA            W5SCRN         8A  O  1  2
650aAA                                  3  2'Warehouse:'
650aAA            WOWHSE        40A  O  3 13
650aAA                                  5  6'Order:'
650aAA            W1ORD          7Y 0O  5 13EDTCDE(4)
650aAA                                  4  7'Item:'
650aAA            WOITEM        49A  O  4 13
650aAA                                  9  4'Quantity returned . . . .'
650aAA            W1QT01         5Y 0B  9 31EDTCDE(4)
650aAA N24N90                               DSPATR(HI)
650aAA  24                                  DSPATR(RI)
650aAA  04                                  DSPATR(PC)
650aAA N24N90                               DSPATR(UL)
650aAA                                      DSPATR(PR)
650aAA            W1UM1          5A  O  9 37
650aAA            W1QT02         3Y 0B  9 45EDTCDE(4)
650aAA  31                                  DSPATR(RI)
650aAA  11                                  DSPATR(PC)
650aAA N31N90                               DSPATR(UL)
650aAA  94                                  DSPATR(ND)
650aAA                                      DSPATR(PR)
650aAA            W1UM2          5A  O  9 49
650aAA  94                                  DSPATR(ND)
650aAA            W1QT03         3Y 0B  9 57EDTCDE(4)
650aAA  30                                  DSPATR(RI)
650aAA  10                                  DSPATR(PC)
650aAA N30N90                               DSPATR(UL)
650aAA  58
650aAAO 93                                  DSPATR(ND)
650aAA                                      DSPATR(PR)
650aAA            W1UM3          5A  O  9 61
650aAA  93                                  DSPATR(ND)
650aAA                                 10  4'Number of pallets . . . .'
650aAA            W1PLTS         5Y 0B 10 31EDTCDE(4)
650aAA N36N90                               DSPATR(HI)
650aAA  36                                  DSPATR(RI)
650aAA N36N90                               DSPATR(UL)
650aAA  16                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 11  4'Catch weight returned . .'
650aAA            W1CWTR         9Y 2B 11 31
650aAA  33                                  DSPATR(RI)
650aAA N33N90                               DSPATR(UL)
650aAA  33                                  DSPATR(PC)
650aAA  95                                  DSPATR(ND)
650aAA                                      DSPATR(PR)
650aAA                                      EDTCDE(4)
650aAA N33N90N95                            DSPATR(HI)
700bAA            IMUOM          3   O 11 42
700bAA  95                                  DSPATR(ND)
650aAA                                 12  4'Reason code . . . . . . .'
650aAA            W1CODE         2A  B 12 31
650aAA N22                                  DSPATR(HI)
650aAA  22                                  DSPATR(RI)
650aAA N22N90                               DSPATR(UL)
650aAA  02                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 13  4'Dispositon code . . . . .'
650aAA            W1DCDE         5A  B 13 31
650aAA N27N89                               DSPATR(HI)
650aAA  27                                  DSPATR(RI)
650aAA N27N90                               DSPATR(UL)
650aAA  07                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 14  4'Use label for putaway . .'
650aAA            W1ULBL         1A  B 14 31
650aAA N28                                  DSPATR(HI)
650aAA  28                                  DSPATR(RI)
650aAA N28N90                               DSPATR(UL)
650aAA  08                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA            W1INVH        25A    15  4
650aAA            W1INV#         9A  B 15 31
650aAA  29                                  DSPATR(RI)
650aAA N29N90                               DSPATR(UL)
650aAA  09                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 16  4'Transaction number  . . .'
650aAA            W1TRN#         7Y 0B 16 31
650aAA  34                                  DSPATR(RI)
650aAA N34N90                               DSPATR(UL)
650aAA  14                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                      EDTCDE(4)
650aAA            W1BILH        25A    17  4
650aAA            W1BIL#        10A  B 17 31
650aAA N37                                  DSPATR(HI)
650aAA  37                                  DSPATR(RI)
650aAA N37N90N17                            DSPATR(UL)
650aAA                                      DSPATR(PR)
650aA *
650aAA                                 18  3'-ENTER ONE-'
650aAA                                 19  3'Corresponding item . . . .'
650aAA            W1CITM        15A  B 19 31
650aAA  23                                  DSPATR(RI)
650aAA N23N90                               DSPATR(UL)
650aAA  03                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 20  3'Corsp. UPC Code  . . . . .'
650aAA            W1UPC         50A  B 20 31CHECK(ER)
650aAA  25                                  DSPATR(RI)
650aAA N25N90                               DSPATR(UL)
650aAA  05                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 21  3'Cor. Mfg code/Vend Item# .'
650aAA            W1VMFG        15A  B 21 31
650aAA  26                                  DSPATR(RI)
650aAA N26N90                               DSPATR(UL)
650aAA  06                                  DSPATR(PC)
650aAA                                      DSPATR(PR)
650aAA                                 23  2'F3=Exit             F5=Refresh    -
650aAA                                            F12=Cancel                 '
650aAA                                      COLOR(BLU)
650aAA            WOHEAD        60A  O  1 11DSPATR(HI)
650aAA                                  2 33'Manual Put Away'
650aAA          R CNLWDW
650aAA*%%TS  SD  20120907  103814  LYNNM       REL-V6R1M0  5761-WDS
650aAA                                      WINDOW(2 2 10 65)
650aAA                                      CF10
650aAA                                  1 23'Confirm Cancel/Delete'
650aAA                                      DSPATR(HI)
650aAA                                  4 30'DELETED'
650aAA                                      COLOR(RED)
650aAA            W3LCNS        15A  O  7 26DSPATR(HI)
650aAA                                  9 17'F10=Delete   F12=Continue Scanning'
650aAA                                      COLOR(BLU)
650aAA                                  5 17'If you wish to do this press F10.'
650aAA                                  6 12'Otherwise press F12 to scan to thi-
650aAA                                      s license'
650aAA                                  3 12'All barcodes scanned to this licen-
650aAA                                      se will be'
