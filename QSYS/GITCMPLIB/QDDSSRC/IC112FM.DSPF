     A*%%TS  SD  20051222  094717  JEFF        REL-V5R3M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20051222  094717  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'IC112.01'
     A                                  1 25'Item Category Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Category Code. . . . .'
     A            W1CODE    R        B  8 27REFFLD(ICREC/ICCODE *LIBL/ITEMCAT)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Category Description .'
     A            W1DESC    R        B  9 27REFFLD(ICREC/ICDESC *LIBL/ITEMCAT)
     A                                      CHECK(LC)
     A  90                                  DSPATR(PR)
     A                                 11  6'Collect P.O. Header HACCP informat-
     A                                      ion. . .'
     A            W1HHDR    R        B 11 49REFFLD(ICREC/ICHHDR *LIBL/ITEMCAT)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'1=No, 2=Optional, 3=Force'
     A                                 13  6'Collect P.O. Detail HACCP informat-
     A                                      ion. . .'
     A            W1HDTL    R        B 13 49REFFLD(ICREC/ICHDTL *LIBL/ITEMCAT)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 53'1=No, 2=Optional, 3=Force'
     A                                 15  6'Collect unique category HACCP info-
     A                                      rmation.'
     A            W1HCAT    R        B 15 49REFFLD(ICREC/ICHCAT *LIBL/ITEMCAT)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 53'1=No, 2=Optional, 3=Force'
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
