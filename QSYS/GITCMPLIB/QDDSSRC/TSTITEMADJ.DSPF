     A*%%TS  SD  20020108  141833  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTITEMADJ  Test DRIITEMADJ interface.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    10/29/01  DAS  4.16
     A*      - Created
     A*
416aAA*    01/08/02  DAS  4.16a
     A*      - Added F8=Whse Qty
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020108  141833  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTITEMADJ.1'
     A                                  1 28'Test DRIITEMADJ Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Adjustment type  . . . .'
     A            W1TYPE         1A  B  7 30
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  7 49'1 = Normal, 2 = Customer order'
     A                                  8  4'Adjustment code  . . . .'
     A            W1CODE         2A  B  8 30
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  9  4'Warehouse  . . . . . . .'
     A            W1WHSE         3Y 0B  9 30
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10  4'Department . . . . . . .'
     A            W1WHDP         5A  B 10 30
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                 11  4'Slot . . . . . . . . . .'
     A            W1SLOT        12A  B 11 30
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                 12  4'Item . . . . . . . . . .'
     A            W1ITEM        15A  B 12 30
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                 13  4'Normal quantity  . . . .'
     A            W1QTY1         5Y 0B 13 30EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A                                 14  4'Brk 1 quantity . . . . .'
     A            W1QTY2         3Y 0B 14 30EDTCDE(M)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                 15  4'Brk 2 quantity . . . . .'
     A            W1QTY3         3Y 0B 15 30EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                 16  4'Catch weight . . . . . .'
     A            W1CWTA         9Y 2B 16 30EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                 17  4'Memo . . . . . . . . . .'
     A            W1MEMO        30A  B 17 30CHECK(LC)
     A                                 19  4'Return code  . . . . . .'
     A            W1RTN         10A  O 19 30
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F8-
     A                                      =Whse Qtys  F9=Slot Qtys  F10=View -
     A                                      prty rpl'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
