     A*%%TS  SD  20070116  094546  BFCTEST1    REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  RU373FM   View Cleared Flags (by Route)
     A*  November 9, 2004
     A*
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA10
     A                                      CA12
     A                                      CA21
     A          R TITLE
     A*%%TS  SD  19941028  115201  JOSED       REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 28'Cleared Flags-by Time Diff.'
     A                                      DSPATR(HI)
     A                                  1  2'RU373'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19941028  115201  JOSED       REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A            WOFROM         6Y 0O  1 46EDTCDE(Y)
     A                                  1 56'Thru'
     A            WOTO           6Y 0O  1 62EDTCDE(Y)
     A            WOPREV        35A  O  2 41
     A*----------------------------------------------------------------
     A* Subfile for list display
     A*
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20050331  101658  BFCTEST1    REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1RTE          4A  O  9  3
     A            W1ITEM         8A  O  9  8
     A            W1DESC        30A  O  9 17
     A            W1QTY          4  0O  9 49EDTCDE(Z)
     A  09                                  DSPATR(BL)
     A  09                                  COLOR(RED)
     A            W1QTYF         4  0O  9 55EDTCDE(3)
     A  09                                  DSPATR(BL)
     A  09                                  COLOR(RED)
     A            W1FTRN         7A  O  9 61
     A            W1WHDP         5A  O  9 69
     A            W1EMP#         5Y 0O  9 75EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20050331  093712  BFCTEST1    REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  7 49'Flag'
     A                                      DSPATR(HI)
     A                                  7 55'Flag'
     A                                      DSPATR(HI)
     A                                  7 61'Orig'
     A                                      DSPATR(HI)
     A                                  8  3'Rte#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8  8'Item  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 17'        Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 49'Qty '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 54'Found'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 61'Tran#  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 69'Dept'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 76'Emp#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R VW2SFL                    SFL
     A*%%TS  SD  20070116  090702  BFCTEST1    REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1RTE          4A  O  9  3
     A            W1ITEM         8A  O  9 10
     A            W1STRT         6Y 0O  9 20EDTWRD('0 :  :  ')
     A            W1CLST         6Y 0O  9 30EDTWRD('0 :  :  ')
     A            W1DIFF         6Y 0O  9 40EDTWRD('0 :  :  ')
     A            W1OTRN         7A  O  9 50
     A            W1WHDP         5A  O  9 59
     A            W1EMP#         5Y 0O  9 66EDTCDE(Z)
     A            W1TYPE         6A  O  9 73
     A*----------------------------------------------------------------
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20070116  094546  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  7 20'Start'
     A                                      DSPATR(HI)
     A                                  7 30'Close'
     A                                      DSPATR(HI)
     A                                  7 50'Flag'
     A                                      DSPATR(HI)
     A                                  7 73'Flag'
     A                                      DSPATR(HI)
     A                                  8  3'Rte#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 10'Item  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 50'Tran#  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 59'Dept'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 67'Emp#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 20'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 30'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 40'Diff    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 73'Type  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
