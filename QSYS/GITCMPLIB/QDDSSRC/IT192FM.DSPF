     A*%%TS  SD  20120926  180909  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  IT192FM    Item Substitutes file - Change
     A*  30 Aug 2012
     A*  Retha Davis
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    08/30/12  RBD  6.40a
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20120926  180909  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'IT192.01'
     A                                  1 26'Item Substitutes Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse . . . . . . .'
     A            WHCODE         3Y 0O  7 29EDTCDE(Z)
     A                                  7 33'-'
     A            WHDESC        30   O  7 35
     A                                  8  4'Item  . . . . . . . . .'
     A  22                                  DSPATR(ND)
     A            W1ITEM        15A  O  8 29
     A  22                                  DSPATR(ND)
     A            W1IDSC        30A  O  9 29
     A                                 11  4'Customer Number . . . .'
     A            W1CUST        11Y 0B 11 29
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 11 47'Optional/Not verfied'
     A  22                                  DSPATR(ND)
     A                                 12  4'Item Substitute . . . .'
     A  22                                  DSPATR(ND)
     A            W1ISUB        15A  B 12 29
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
     AO 22                                  DSPATR(PR)
     A N27N90                               DSPATR(HI)
     A  22                                  DSPATR(ND)
     A                                 12 47'*NOSUB = No subs allowed'
     A  22                                  DSPATR(ND)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
