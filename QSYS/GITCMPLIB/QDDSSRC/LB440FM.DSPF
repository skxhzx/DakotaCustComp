     A*%%TS  SD  20140827  125422  BFC011      REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2021 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LB440FM   Return Pallet License Print
     A*  06 July 2021
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
     A*    07/06/21  LMC  7.50
     A*      - Date Created.
     A*      - Copied from LB34001
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20140827  125422  BFC011      REL-V7R1M0  5770-WDS
     A                                      CF04
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LB440.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'License Prefix..'
     A            W1PREFIX       6A  B  8 21
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                 10  4'Starting number.'
     A            W1START        4Y 0B 10 21
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 10 41'1 - 9999'
     A                                      DSPATR(HI)
     A                                 12  4'Number to print.'
     A            W1COPY         3Y 0B 12 21
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 12 41'1 - 999'
     A                                      DSPATR(HI)
     A                                 14  4'Device . . . . .'
     A            W1DEV         10A  B 14 21
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Form . . . . . .'
     A            W1FORM        10A  B 15 21
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F10=Change starting number   F12=C-
     A                                      ancel'
     A                                      COLOR(BLU)
