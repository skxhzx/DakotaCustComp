     A*%%TS  SD  20040721  114953  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTFTPAPI   Test BFCFTPAPI
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    07/20/04  DAS  4.17
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040721  114953  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTFTPAPI.1'
     A                                  1 34'Test BFCFTPAPI'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Command  .'
     A            W1CMD         10A  B  3 13
     A                                  3 26'*ALL, *CONNECT, *ACTION, *QUIT'
     A                                  4  2'Server . .'
     A            W1SRVR        65A  B  4 13CHECK(LC)
     A                                  5  2'User/Pwd .'
     A            W1USER        30A  B  5 13CHECK(LC)
     A                                  5 44'/'
     A            W1PWD         30A  B  5 46CHECK(LC)
     A                                  7  2'Passive:'
     A            W1PASV         1A  B  7 11VALUES('Y' 'N')
     A                                  7 13'(Y,N)'
     A                                  7 22'Namefmt:'
     A            W1NFMT         1A  B  7 31VALUES('0' '1')
     A                                  7 33'(1,0)'
     A                                  7 42'Binary:'
     A            W1BIN          1A  B  7 50VALUES('Y' 'N')
     A                                  7 52'(Y,N)'
     A                                  7 61'Put/Get:'
     A            W1ACTN         1A  B  7 70VALUES('P' 'G')
     A                                  7 72'(P,G)'
     A                                  9  2'File type.'
     A            W1TYPE         4A  B  9 13
     A                                  9 18'(FILE,SAVF)'
     A                                 11  2'From . . .'
     A                                 11 13'Lib:'
     A            W1FLIB        10A  B 11 18
     A                                 11 29'File:'
     A            W1FFIL        10A  B 11 35
     A                                 11 46'Mbr:'
     A            W1FMBR        10   B 11 51
     A                                 12  2'or string:'
     A            W1FSTR        65A  B 12 13CHECK(LC)
     A                                 14  2'To . . . .'
     A                                 14 13'Lib:'
     A            W1TLIB        10A  B 14 18
     A                                 14 29'File:'
     A            W1TFIL        10A  B 14 35
     A                                 14 46'Mbr:'
     A            W1TMBR        10   B 14 51
     A                                 15  2'or string:'
     A            W1TSTR        65A  B 15 13CHECK(LC)
     A                                 17  2'Namefmt 1 examples:'
     A                                 18 15'iSeries save file:'
     A                                 18 35'/qsys.lib/pirtfr.lib/filename.savf'
     A                                 19 15'IFS (PC) file:'
     A                                 19 35'/home/dave/filename'
     A                                 21  2'Message:'
     A            W1MSG         60A  O 21 13
     A                                 23  2'F3=Exit  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
