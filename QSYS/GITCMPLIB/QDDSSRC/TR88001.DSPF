     A*%%TS  SD  20120209  112213  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR880FM    Trax - reset downloaded or uploaded status so
     A*             the route can be downloaded again.
     A*
     A*  Retha Davis
     A*
     A*  Revisions
     A*
640 AA*    02/03/12  RBD  6.40
     A*      - Created.
     A*
640aAA*    05/25/12  RBD  6.40a
     A*      - Fix: moved reset screen fkeys to reset screen format.
     A*        Pgm was hanging up if user pressed F3 on reset screen.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
640aDA**                                    CA03
     A                                      CA05
640aDA**                                    CA12
     A                                      CF09
640aDA**                                    CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTLRTE    R        O  8  5REFFLD(TTLREC/TTLRTE *LIBL/TTLOG)
     A            TTLRTID   R        O  8 11REFFLD(TTLREC/TTLRTID *LIBL/TTLOG)
     A            W1STAT        10A  O  8 20
     A            TTLTS9    R        O  8 31REFFLD(TTLREC/TTLTS9 *LIBL/TTLOG)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
640aAA                                      CF03
640aAA                                      CF12
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 20'Reset Downloaded or Uploaded Trax +
     A                                       Routes'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        76A  O  2  3
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 11'Route ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 20'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 31'Timestamp                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20120209  112213  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
640aAA                                      CF03
640aAA                                      CF12
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order  . . . . . .'
     A            W1SORT         1A  B  3 20DSPATR(HI)
     A                                  3 25'Ascending, Descending'
     A                                  4  1'Order by . . . . .'
     A            W1OBY          1A  B  4 20DSPATR(HI)
     A                                  4 25'1 = Route'
     A                                  5 25'2 = Dwnld/Upld Date'
     A                                  7  1'Warehouse  . . . .'
     A            W1WHSE         3Y 0B  7 20
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A                                  8  1'Route  . . . . . .'
     A            W1RTE          5A  B  8 20
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  8 32'*ALL'
     A                                  9  1'Dwnld/Upld Date  .'
     A            W1DATE         6Y 0B  9 20
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                  9 32'MMDDYY / 0=All'
     A                                 10  1'Downloaded only  .'
     A            W1DWNL         1A  B 10 20
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 10 32'Y=yes N=no'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R PASWRD
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
     A*
     A          R RESREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
640aAA                                      CF10
640aAA                                      CF12
     A                                  1  2'TR880'
     A                                  1 20'Reset Downloaded or Uploaded Trax +
     A                                       Routes'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Press F10 to confirm your choice-
     A                                       for 4=Reset status.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choice.'
     A                                      COLOR(BLU)
     A                                  6  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 11'Route ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 20'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 31'Timestamp                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TTLRTE    R        O  8  5REFFLD(TTLREC/TTLRTE *LIBL/TTLOG)
     A            TTLRTID   R        O  8 11REFFLD(TTLREC/TTLRTID *LIBL/TTLOG)
     A            W1STAT        10A  O  8 20
     A            TTLTS9    R        O  8 31REFFLD(TTLREC/TTLTS9 *LIBL/TTLOG)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
