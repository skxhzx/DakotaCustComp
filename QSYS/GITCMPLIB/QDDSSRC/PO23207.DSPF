     A*%%TS  SD  20110210  103707  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO232FM   VS P/O Receiving (detail) - Change
     A*  02 October 2000
     A*  Dave Sommerville
     A*
     A*  PO232FM
     A*
     A*  Revisions:
     A*
414 AA*    10/02/00  DAS  4.14
     A*     - Moved lines up to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
415aAA*    04/11/01  MLB  4.15a
     A*      - Add F9 to call catchweight entry program.
     A*      - Reformatted function key text.
     A*
415bAA*    08/22/01  DAS  4.15b
     A*      - Revise lines displaying the item numbers.
     A*
416 AA*    09/26/01  DAS  4.16
     A*      - Changed catch weight desc line to variable.
     A*      - Added indicators 81 & 82 to control cw entry and display
     A*
416aAA*    10/18/01  RH   4.16a
     A*      - Fix - increase catch weight to 9.2
     A*
417 AA*    04/15/03  MLB  4.17
     A*      - Enh - Increased length of UPC entry field to 50.
417aAA*    05/13/03  MLB  4.17a
     A*      - Enh - Add entry field for lot code, W1LOTC.
417bAA*    01/20/04  RH   4.17b
     A*      - Enh:  Add Entered date entry field for MOVE IN po to
     A*              allow for FIFO rotation after product is moved in
     A*
500 AA*    04/02/04  DAS  5.00
     A*      - Enh:  Add *IN65 - Dpt Not Live.
510 AA*    10/01/07  LMC  5.10
     A*      - Enh:  Add F11 to call Date code decipher program
YHA AA*    10/10/07  RH   YHAa
     A*      - Enh:  Add Ordered qty to screen 1.
510aAA*    10/12/07  LMC  5.10a
     A*      - Enh:  Add non display indicator if client doesn't have
     A*      the decipher programs installed.
520aAA*    04/24/08  RH   5.20a
     A*      - Fix:  Decrease breakdown UOM qty fields from 5 to 3.
     A*              PT132 can only process 3 digits.
     A*
640aAA*    02/08/11  RBD  6.40a
     A*      - Enh:  Allow weight to be entered in kilograms ... it
     A*        typically is entered in pounds.  Use F7 to toggle between
     A*        F7=Kilograms and F7=Pounds.
     A*      - Removed function key text from dds and added command lines
     A*        which are formatted within the program.
     A*
640bAA*    08/16/11  RH   6.40b
     A*      - Enh:  Add overflow Y/N field, rename to PO23202
640cAA*    09/19/12  LMC  6.40C
     A*      - Enh:  Added Brand field.
     A*      Created new display file PO23204 to replace PO23203.
650aAA*    10/24/13  LMC  6.50a
     A*      - Enh:  Added PTI field.
     A*      Created new display file PO23205 to replace PO23204.
650bAA*    06/05/14  LMC  6.50b
     A*      - Enh:  Added notifiation message field to screen1.
     A*      Created new display file PO23206 to replace PO23205.
700aAA*    03/29/16  LMC  7.00a
     A*      - Enh:  Need to show w1kglb if item is CW item.
     A*        *in81 should not be used to ND w1kglb.
     A*      Created new display file PO23207 to replace PO23206.
     A*-------------------------------------------------------------------------
     A*  Client Custom Revisions: Finkle
     A*
FKLaAA*    12/18/08  RH   FKLa
     A*      - Enh:  Make tie/hi non display on indicator 86
     A*
     A*-------------------------------------------------------------------------
     A*  Client Custom Revisions: Glazier
     A*
GFCa A*    10/04/11  GJA  GFCa
     A*      - Enh:  Add password window for expiration date process
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  20110210  103707  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(21)
     A                                      CF04
     A                                      CF07
     A                                      CF08
     A N95                                  CF09
     A                                      CF20
     A                                      CF10
     A N66                                  CF11
     A  98                                  ALARM
     A                                  1  2'PO232.01'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'PO number . .'
     A            W1PO           9A  O  6 16
     A                                  6 60'Manual Put away'
     A                                  7  2'Warehouse . .'
     A            W1WHSE         3Y 0O  7 16EDTCDE(4)
     A            W1FDSC        30A  O  7 21
650bAA            W1NOTE        15A  O  7 60DSPATR(HI)
415bDA**                                8  2'Item number .'
     A            WOLBL         13A  O  8  2
     A            WOITEM        65A  O  8 16
415bDA**          WOLBLS        13   O  9  2DSPATR(HI)
     A            WOLBLS        13A  O  9  2
415bDA**          WOSITM        65   O  9 16DSPATR(HI)
     A            WOSITM        65A  O  9 16
     A            WODSC1        40A  O 10 16
     A            WODSC2        40A  O 11 16
     A                                 12  2'Home slot . .'
     A            WOSLOT        12A  O 12 16
     A            WOPDSC        15A  O 12 40
     A            WOTIE          4Y 0O 12 57EDTCDE(4)
     A  86                                  DSPATR(ND)
     A                                 12 62'/'
     A  86                                  DSPATR(ND)
     A            WOHI           2Y 0O 12 64EDTCDE(4)
     A  86                                  DSPATR(ND)
     A                                 13  2'Ordered:'
     A  63                                  DSPATR(ND)
     A            WOQOR         15A  O 13 11
     A  63                                  DSPATR(ND)
     A                                 13 45'DEPT NOT LIVE'
     A  65                                  DSPATR(ND)
     A                                 13 45'NOT A FSWMS ITEM'
     A  64                                  DSPATR(ND)
640bAA                                 13 67'Overflow:'
640bAA            WOOSTK         1   O 13 78
     A                                 14  3'UPC code  . . . . . . . . .'
417 DA*           W1UPC         20A  B 14 31CHECK(ER)
     A            W1UPCD        50A  B 14 31CHECK(ER)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 92                                  DSPATR(PR)
     A                                 15  3'Quantity received . . . . .'
     A            W1QRC1         5Y 0B 15 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 92                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UM1          5A  O 15 37
     A            W1QRC2         3Y 0B 15 44EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1UM2          5A  O 15 50
     A  94                                  DSPATR(ND)
     A            W1QRC3         3Y 0B 15 57EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1UM3          5A  O 15 63
     A  93                                  DSPATR(ND)
     A                                 16  3'Number of pallets . . . . .'
     A            W1PLTS         5Y 0B 16 31EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  3'Expiration date . . . . . .'
     A            W1EXDT         6Y 0B 17 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96
     AO 90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 42'FIFO date:'
     A  89                                  DSPATR(ND)
     A            W1FIFO         6Y 0B 17 53
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  89                                  DSPATR(ND)
     A  89                                  DSPATR(PR)
     A                                      EDTCDE(4)
417bAA*
     A                                 17 71'(MMDDYY)'
     A  96                                  DSPATR(ND)
416 DA**                               18  3'Catch weight received . . .'
     A            WOCRCV        27A  O 18  3
416aDA**          W1DWGT         7Y 2B 18 31
     A            W1DWGT         9Y 2B 18 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  82
     AO 95
     AO 90                                  DSPATR(PR)
     A  82
     AO 95                                  DSPATR(ND)
     A                                      EDTCDE(4)
640aAA            W1KGLB         3A  O 18 42
700aDA* 81
700aDA*O 82
700aMA  82
640aAAO 95                                  DSPATR(ND)
     A                                 18 50'Average (Y/N) . . . . . .'
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A            W1AVG          1A  B 18 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  81
     AO 82
     AO 95
     AO 90                                  DSPATR(PR)
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A                                 19  3'Slot override . . . . . . .'
     A                                      DSPATR(ND)
     A            W1SLOT        12A  B 19 31DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 20  3'Manufacturing date  . . . .'
     A            W1MFDT         6Y 0B 20 31EDTCDE(4)
     A  27                                  DSPATR(RI)
     A  87                                  DSPATR(ND)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  87                                  DSPATR(PR)
     A                                 21  3'Lot code  . . . . . . . . .'
     A  88                                  DSPATR(ND)
     A            W1LOTC        30A  B 21 31CHECK(LC)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  88                                  DSPATR(ND)
     A  88                                  DSPATR(PR)
640cAA                                 21 63'Brand:'
640cAA  74                                  DSPATR(ND)
640cAA            W1BRAND       10A  B 21 70
640cAA  36                                  DSPATR(RI)
640cAA N36N90                               DSPATR(UL)
640cAA  16                                  DSPATR(PC)
640cAA  74                                  DSPATR(ND)
640cAA  74                                  DSPATR(PR)
650aAA                                 20 63'PTI:'
650aAA  73                                  DSPATR(ND)
650aAA            W1PTI          4Y 0B 20 70
650aAA  37                                  DSPATR(RI)
650aAA N37N90                               DSPATR(UL)
650aAA  17                                  DSPATR(PC)
650aAA  73                                  DSPATR(ND)
650aAA  73                                  DSPATR(PR)
417aAA*
415aDA*                                23  2'F5=Refresh  F8=Tie/Hi  F12=Cancel -
415aDA*                                     F20=Accept warning'
640aDA*                                23  2'F5=Refresh'
640aDA*                                     COLOR(BLU)
640aDA*                                23 14'F8=Tie/Hi'
640aDA*                                     COLOR(BLU)
640aDA*                                23 25'F9=Cwgt Entry'
640aDA* 95                                  DSPATR(ND)
640aDA*                                     COLOR(BLU)
640aDA*                                23 40'F12=Cancel'
640aDA*                                     COLOR(BLU)
640aDA*                                23 52'F20=Accept warning'
640aDA*                                     COLOR(BLU)
640aDA*                                22 40'F11=Mfr Code'
640aDA*                                     COLOR(BLU)
640aDA* 66                                  DSPATR(ND)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
640aAA* Command Line
640aAA          R CMDREC
640aAA                                      LOCK
640aAA                                      OVERLAY
640aAA                                      SLNO(22)
640aAA            CMDLN1        79A  O  1  2COLOR(BLU)
640aAA            CMDLN2        79A  O  2  2COLOR(BLU)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
MSG DA*N97                                  SFLCLR
MSG MA                                      SFLDSP
MSG AA                                      SFLDSPCTL
MSG AA                                      SFLINZ
     A  97                                  SFLEND
MSG AA            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R PASWRD
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  I  4  2COLOR(WHT)
     A                                      DSPATR(ND)
