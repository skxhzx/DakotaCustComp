     A*%%TS  SD  20090130  083157  JOSED       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  LOAD58FM  Verified Returned Trailer Equipment - Options
     A*  November 17, 2008
     A*  Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/10/11  JGD  6.40
     F*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20090130  083157  JOSED       REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A*
     A                                  1  2'Loadr58.01'
     A*
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A*
     A                                  6  4'Warehouse  . . . . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 40EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                  7  4'From date  (MMDDYY)  . . . . . . .'
     A            W1FRDT         6Y 0B  7 40EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                  8  4'To date    (MMDDYY)  . . . . . . .'
     A            W1TODT         6Y 0B  8 40EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                  9  4'Order report . . . . . . . . . . .'
     A            W1SORT         1A  B  9 40
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 49'1 = Loader'
     A                                 10 49'2 = Driver'
     A                                 11 49'3 = Equipment'
     A                                 12 49'4 = All of the above'
     A*
     A                                 14  4'Report type  . . . . . . . . . . .'
     A            W1RTYP         1A  B 14 40
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 49'D=Detail S=Summary B=Both'
     A*
     A                                 16  4'Print invoices?  . . . . . . . . .'
     A            W1INVC         1A  B 16 40
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 49'(Y=Yes, N=No)'
     A*
     A                                 17  4'Invoice type . . . . . . . . . . .'
     A            W1ITYP         1A  B 17 40
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 49'(D=Driver, L=Loader, B=Both)'
     A*
     A                                 19  4'Place on job queue?  . . . . . . .'
     A            W1JOBQ         1A  B 19 40
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 19 49'(Y=Yes, N=No)'
     A*
     A                                 20  4'Printer device . . . . . . . . . .'
     A            W1DEV         10A  B 20 40
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 21  4'Form type  . . . . . . . . . . . .'
     A            W1FORM        10A  B 21 40
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 49'Route loading date'
     A                                  1 21'Returned Trailer Equipment Excepti-
     A                                      on Report'
     A                                      DSPATR(HI)
