     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  TB300PR    Palletizer Report ...
     A*                Tote and bundle counts and transactions
     A*
     A*  14 April 2010
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*    04/14/10  RBD  5.30
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                     2
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                    13
     A                                      'Palletizer Report For ROUTE'
     A            TBRTE     R        O    41
     A                                      REFFLD(TBRTE TOTEBUND)
     A                                    48
     A                                      'Pallet'
     A            W1PAL#         7A  O    55
     A                                    67
     A                                      'Stops'
     A            W1STP2         3S 0     73
     A                                      EDTCDE(3)
     A                                    77
     A                                      '-'
     A            W1STP1         3S 0     79
     A                                      EDTCDE(3)
     A                                    90
     A                                      'Transaction'
     A            PSTRN#    R        O   102
     A                                      REFFLD(PSTRN# PLTSUM)
     A                                      EDTCDE(3)
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
      *
     A                                     2
     A                                      TIME
     A            WHHEAD        40A  O    13
     A                                   121
     A                                      'TB300'
     A                                      SPACEA(002)
     A                                     2
     A                                      'Stop  Customer'
     A            Z1HDG          6A  O    43
     A            Z2HDG          6A  O    55
     A            Z3HDG          6A  O    67
     A            Z4HDG          6A  O    79
     A            Z5HDG          6A  O    91
     A                                   103
     A                                      'OTHER       TOTAL'
     A                                      SPACEA(000)
     A          R UNDLIN
     A            UNLINE       124A  O     2
     A                                      SPACEA(000)
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
     A            W1STOP         3S 0      2
     A                                      EDTCDE(3)
     A            OHCUSA        11A  O     8
     A            C1TB          11A  O    43
     A            C2TB          11A  O    55
     A            C3TB          11A  O    67
     A            C4TB          11A  O    79
     A            C5TB          11A  O    91
     A            C6TB          11A  O   103
     A            C7TB          11A  O   115
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            OHCNAM    R        O     2
     A                                      REFFLD(OHCNAM ORDH)
     A            C1TRN          7A  O    43
     A            C2TRN          7A  O    55
     A            C3TRN          7A  O    67
     A            C4TRN          7A  O    79
     A            C5TRN          7A  O    91
     A            C6TRN          7A  O   103
     A*
     A          R GRNDTTL
     A                                      SPACEB(002)
     A                                     2
     A                                      'Total Totes/Bundles'
     A            C1TB_T        11A  O    43
     A            C2TB_T        11A  O    55
     A            C3TB_T        11A  O    67
     A            C4TB_T        11A  O    79
     A            C5TB_T        11A  O    91
     A            C6TB_T        11A  O   103
     A            C7TB_T        11A  O   115
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('TB300: End of Report')
     A*
