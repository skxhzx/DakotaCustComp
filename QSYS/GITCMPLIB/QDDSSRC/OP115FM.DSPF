     A*%%TS  SD  19941008  110904  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*
     A*  OP110FM   PIR options - Picking
     A*  22 August 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941008  110904  DAVE        REL-V2R1M1  5738-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP115.01'
     A                                  1 30'Pick Printing Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  3'Qty level for combined labels . .'
     A            W1CLVL         3Y 0B  9 38
     A  22                                  DSPATR(RI)
     A                                      EDTCDE(3)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 10  3'Print combined label warning  . .'
     A            W1CWRB         1A  B 10 46
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  3'Text for last line of label . . .'
     A            W1TEXT        43A  B 17 38CHECK(LC)
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 10 38'Before:'
     A                                 10 50'After:'
     A            W1CWRA         1   B 10 57
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  3'Label printing program  . . . . .'
     A            W1LPGM        10   B  7 38
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  3'Print unit of measure warning . .'
     A                                 12 38'Before:'
     A                                 12 50'After:'
     A            W1UWRB         1   B 12 46
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UWRA         1   B 12 57
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  3'Discrepancy label ratio . . . . .'
     A                                 15  3'Catch weight label ratio  . . . .'
     A                                 14 38'One label for every'
     A                                 15 38'One label for every'
     A            W1ERR          2Y 0B 14 58EDTCDE(3)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1CWGT         2Y 0B 15 58EDTCDE(3)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 61'pick labels'
     A                                 15 61'catch weight items'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
