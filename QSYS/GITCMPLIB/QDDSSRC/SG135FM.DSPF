     A*%%TS  SD  20140425  053656  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG135FM    Staging Import View Slot Stuffit Updates
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IMPSSTUF)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140425  053656  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SIQTY1    R        H
     A            SIQTY2    R        H
     A            SIQTY3    R        H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            SIWHDP    R        O  8 10
     A            SIDISP    R        O  8 17
     A            SITYPE    R        O  8 32
     A            SIITEM    R        O  8 36
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140425  053656  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 30'View Slot Stuffit Updates'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 10'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 17'    Slot    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 31'Typ'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'     Item      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20140425  053656  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG135.02'
     A                                  1 28'View Slot Stuffit Updates'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 30'Full Slot Information'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse . . .'
     A            SIWHSE    R        O  4 18DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                  4 34'Added Timestamp . .'
     A            SIADDTS   R        O  4 54DSPATR(HI)
     A                                  5  2'Batch ID  . . .'
     A            SIBATID   R        O  5 18DSPATR(HI)
     A                                  5 34'Status Timestamp  .'
     A            SISTSTS   R        O  5 54DSPATR(HI)
     A                                  7  2'Status  . . . .'
     A            SISTATUS  R        O  7 18DSPATR(HI)
     A            SITEXT    R        O  7 24DSPATR(HI)
     A                                 11  2'Update Type . .'
     A            SITYPE    R        O 11 18DSPATR(HI)
     A                                 13  2'Department  . .'
     A            SIWHDP    R        O 13 18DSPATR(HI)
     A                                 14  2'Slot  . . . . .'
     A            SIDISP    R        O 14 18DSPATR(HI)
     A                                 15  2'Item  . . . . .'
     A            SIITEM    R        O 15 18DSPATR(HI)
     A                                 17  2'Slot Qty 1  . .'
     A            SIQTY1    R        O 17 18DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 18  2'Slot Qty 2  . .'
     A            SIQTY2    R        O 18 18DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 19  2'Slot Qty 3  . .'
     A            SIQTY3    R        O 19 18DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
