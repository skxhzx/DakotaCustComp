     A*%%TS  SD  20090919  163247  GINGERA     REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  ER340FM   Print Letdown/Replenishment transactions - Options
     A*  02 July, 1999
     A*  Hemant Kapadia
     A*
     A*  7.2.99  HNK  4.11
     A*  - Created
     A*
     A*  Revisions
     A*  09/18/09  GJA  5.30
     A*  - Cleaned up and revamped for suspended transactions.
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ER340.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  8  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 59'Date (MMDDYY)'
     A                                  8 41'to'
     A            W1TODT         6Y 0B  8 44
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9  4'Time range . . . . . . . . .'
     A            W1FMTM         4Y 0B  9 34EDTCDE(4)
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 59'Time (HHMM)'
     A                                  9 41'to'
     A            W1TOTM         4Y 0B  9 44
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 11  4'Employee number  . . . . . .'
     A                                 18  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 18 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 59'Y=Yes, N=No'
     A                                 19  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 19 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 59'Name, *SYSVAL, *JOB'
     A                                 20  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 20 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20 59'Character value, *STD'
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A            W1EMP#         5A  B 11 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 59'Number, *ALL'
     A                                  2  2USER
     A                                  2 72TIME
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
