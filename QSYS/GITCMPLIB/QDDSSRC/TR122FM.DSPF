     A*%%TS  SD  20141204  174818  RETHA       REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------
     A*
     A*  TR122FM   Trax Options by Customer - add, change, delete
     A*
     A*  04 Dec 2014
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*    Can't combine *TRAX, *TRAX1, and/or *TRAX2 options on the same
     A*    screen because we lock the record when we retrieve it so that it
     A*    can't be changed while being updated ... there are currently no
     A*    logicals for OPTIONS so separating the different opcodes onto
     A*    different screens seemed to be the best way to handle this.
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650 AA*    12/04/14  RBD  6.50
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA10
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20141204  174818  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TR122.01'
     A                                  1 30'Trax Customer Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Whse'
     A            WHCODE         3Y 0O  2 30EDTCDE(3)
     A                                  2 34'-'
     A            WHDESC        30A  O  2 36
     A                                  3 25'Cust'
     A            $PCUST        11Y 0O  3 30EDTCDE(4)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  2'Breakdowns allowed ..............'
     A            W1TBDA         1X  B  7 36
     A N34N90                               DSPATR(UL)
     A  34                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 38'Y/N'
     A                                  7 45'Send data after stop .......'
     A            W1TSDA         1X  B  7 74
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 76'Y/N'
     A                                  8  2'Add return enabled ..............'
     A            W1TARE         1X  B  8 36
     A N49N90                               DSPATR(UL)
     A  49                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 38'Y/N'
     A                                  8 45'Catchweight required .......'
     A            W1TCWR         1X  B  8 74
     A N40N90                               DSPATR(UL)
     A  40                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 76'Y/N'
     A                                  9  2'Generic breakdown allowed .......'
     A            W1TGBD         1X  B  9 36
     A N35N90                               DSPATR(UL)
     A  35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 38'Y/N'
     A                                  9 45'Pallets enabled ............'
     A            W1TPAL         1X  B  9 74
     A N53N90                               DSPATR(UL)
     A  53                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 76'Y/N'
     A                                 10  2'Pre-trip questions ..............'
     A            W1TRTQ         1X  B 10 36
     A N36N90                               DSPATR(UL)
     A  36                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 38'Y/N'
     A                                 10 45'Bulk items enabled .........'
     A            W1TBUL         1X  B 10 74
     A N56N90                               DSPATR(UL)
     A  56                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 76'Y/N'
     A                                 11  2'Post-trip questions .............'
     A            W1TOTQ         1X  B 11 36
     A N37N90                               DSPATR(UL)
     A  37                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 38'Y/N'
     A                                 11 45'GS1 scanning ...............'
     A            W1TGSS         1X  B 11 74
     A N63N90                               DSPATR(UL)
     A  63                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 76'Y/N'
     A                                 12  2'Credit card enabled .............'
     A            W1TCCE         1X  B 12 36
     A N38N90                               DSPATR(UL)
     A  38                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 38'Y/N'
     A                                 12 45'GS1 unique .................'
     A            W1TGSU         1X  B 12 74
     A N64N90                               DSPATR(UL)
     A  64                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 76'Y/N'
     A                                 13  2'Signature enabled ...............'
     A            W1TSIG         1X  B 13 36
     A N39N90                               DSPATR(UL)
     A  39                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 38'Y/N'
     A                                 13 45'Non scan reason prompt .....'
     A            W1TNSR         1X  B 13 74
     A N72N90                               DSPATR(UL)
     A  72                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 76'Y/N'
     A                                 14  2'Print invoice ...................'
     A            W1TPIV         1X  B 14 36
     A N42N90                               DSPATR(UL)
     A  42                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 38'Y/N'
     A                                 15  2'Filter by department ............'
     A            W1TFBD         1X  B 15 36
     A N73N90                               DSPATR(UL)
     A  73                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 38'Y/N'
     A                                 23  2'F3=Exit  F5=Refresh '
     A                                      COLOR(BLU)
     A                                 23 23'F10=Delete  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN5
     A*%%TS  SD  20141204  170717  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TR122.05'
     A                                  1 30'Trax Customer Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Whse'
     A            WHCODE         3Y 0O  2 30EDTCDE(3)
     A                                  2 34'-'
     A            WHDESC        30A  O  2 36
     A                                  3 25'Cust'
     A            $PCUST        11Y 0O  3 30EDTCDE(4)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  2'Email receipt Y/N ...............'
     A            W5TER          1A  B  7 36
     A N74N90                               DSPATR(UL)
     A  74                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  2'Receipt email from address ......'
     A            W5TREF        60A  B  9 20
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 10  2'Receipt email subject ...........'
     A            W5TRES        55A  B 11 20
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R DELWDW
     A                                      WINDOW(2 2 10 65)
     A                                  2 16'    CONFIRM OR CANCEL +
     A                                       REQUEST TO DELETE    '
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                  5 17'Whse'
     A            WHCODE         3Y 0O  5 22EDTCDE(3)
     A                                  5 26'-'
     A            WHDESC        30A  O  5 28
     A                                  6 17'Cust'
     A            $PCUST        11Y 0O  6 22EDTCDE(4)
     A                                  9 17'Enter=Delete   F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
