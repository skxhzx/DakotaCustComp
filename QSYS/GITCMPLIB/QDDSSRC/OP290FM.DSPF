     A*%%TS  SD  20050125  161408  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
500 MA*  OP290FM   PIR options - Item Tracking Options
     A*  21 November 2002
     A*  Max Blacknik
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
417 AA*    11/21/02  MLB  4.17
     A*      - Created.
417aAA*    07/21/03  MLB  4.17a
     A*      - Enh: Added new option W1TKSP - Track push back slot
     A*        positions.
     A*
500 AA*    11/13/04  DAS  5.00
     A*      - Enh: Renamed from "License Tracking Options" to
     A*        "Tracking Options".
     A*      - Enh: Removed the general tracking option. Re: Every item
     A*        will be tracked even if the user won't see it, so a
     A*        "Yes/No" option doesn't make sense.
     A*      - Enh: Moved tracking group options to separate pgm OP295.
     A*      - Enh: Created new "split quantity" option.
     A*
500aAA*    01/13/05  DAS  5.00a
     A*      - Enh: Added Transfer split option (W1TSPL).
     A*      - Enh: Renamed W1SPLT to W1PSPL (Putaway split).
     A*
500bAA*    01/25/05  DAS  5.00b
     A*      - Enh: Added "(future)" to the descriptions for values
     A*        1 & 3 for each option. Re: Initial release will only
     A*        allow value 2.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20050125  161408  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP290.01'
500 DA**                                1 31'License Plate Tracking'
     A                                  1 30'Item Tracking Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'When splitting a pallet quantity,'
     A                                  8  5'what happens with original license-
     A                                       plate . . . . . . . . . .'
     A            W1PSPL         1A  B  8 67
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                  8 72'1,2,3'
     A                                 10 12'1 = It goes with first quantity pu-
     A                                      t into a slot (future)'
     A                                 11 12'2 = It goes with the last quantity-
     A                                       put into a slot'
     A                                 12 12'3 = Ask user (future)'
     A                                 15  3'When doing a partial quantity Tran-
     A                                      sfer/Replenishment'
     A                                 16  5'what happens with original license-
     A                                       plate . . . . . . . . . .'
     A            W1TSPL         1A  B 16 67
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 16 72'1,2,3'
     A                                 18 12'1 = It stays with the remaining qu-
     A                                      antity in the ''From'' slot'
     A                                 19 12'2 = It goes with quantity being tr-
     A                                      ansfered/replenished (future)'
     A                                 20 12'3 = Ask user (future)'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
