     A*%%TS  SD  20011029  173022  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP250FM   PIR options - Customer Returns
     A*  12 October 2001
     A*  Max Blacknik
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
416 AA*    10/12/01  MLB  4.16
     A*      - Created.
416aAA*   10/29/01  RH    4.16a
     A*    - Add force return label entry (Y/N).
416bAA*   11/29/01  RH    4.16b
     A*    - Add Export Quick returns (Y/N).
416cAA*   02/04/02  RH    4.16c
     A*    - Add Use label for putaway (Y/N).
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20011029  173022  ROCKY       REL-V4R2M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP250.01'
     A                                  1 30'Customer Returns Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  6'Allow changes to return quantity d-
     A                                      uring close  . .'
     A            W1RQCH         1A  B  9 58
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 64'Y=Yes, N=No'
     A                                 10  6'Allow changes to quantity if retur-
     A                                      n exported . . .'
     A            W1RQEC         1A  B 10 58
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 64'Y=Yes, N=No'
     A                                 11  6'Force return label entry . . . . .-
     A                                       . . . . . . . .'
     A            W1RFLB         1A  B 11 58
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 64'Y=Yes, N=No'
416b A                                 12  6'Export quick returns . . . . . . .-
416b A                                       . . . . . . . .'
416b A            W1REXQ         1A  B 12 58
416b A N24N90                               DSPATR(HI)
416b A  24                                  DSPATR(RI)
416b A N24N90                               DSPATR(UL)
416b A  04                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
416b A                                 13 64'Y=Yes, N=No'
416c A                                 13  6'Use label for putaway. . . . . . .-
416c A                                       . . . . . . . .'
416c A            W1RULB         1A  B 13 58
416c A N25N90                               DSPATR(HI)
416c A  25                                  DSPATR(RI)
416c A N25N90                               DSPATR(UL)
416c A  05                                  DSPATR(PC)
416c A  90                                  DSPATR(PR)
416c A                                 13 64'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
