     A*%%TS  SD  20081005  170617  DAVE        REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RU312FM      RF - Runner Clear Flags catchweight entry
     A*  20 February 2006
     A*  Jeffrey Jahnke
     A*
     A********************************************************************
     A*  Revisions:
     A*
500 AA*  02/16/06  JCJ  5.00
     A*    - Created.
500aAA*  05/03/06  MLB  5.00a
     A*    - Fix: Removed ind 01 from DSPATR(PC) on screens, 1,61.
520aAA*  10/05/08  DAS  5.20a
     A*    - Fix: Increased w1qty to 5.0
700aAA*  04/05/16  LMC  7.00a
     A*    - Renamed from RU312FM to RU31201
     A*    - Enh: Adding logic for Metric.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A          R SCREEN1
     A*%%TS  SD  20081005  170617  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA07
     A                                      CA12
     A                                      CF10
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'RU312.01'
     A                                  1 11'Single Pick'
     A                                  2  1'Ord:'
     A            WOORD          7Y 0O  2  6EDTCDE(4)
     A            WOLINE         5Y 0O  2 16EDTCDE(4)
     A                                  3  1'Itm:'
     A            WOITM2        70A  O  3  6
     A                                  4  1'Qty:'
     A            WOQTY          5Y 0O  4  6EDTCDE(4)
     A            WOUM           5A  O  4 12
700aDA*                                 4 19'Weight'
700aMA                                  4 19'Wt'
700aAA            W1KGLB         3A  O  4 22DSPATR(HI)
     A            W1CNT          3Y 0O  4 26EDTCDE(4)
     A                                  4 30'of'
     A            W1QTY          5Y 0O  4 33EDTCDE(4)
     A                                  5  1'Pck:'
     A            WOPCK          5Y 0O  5  6EDTCDE(4)
     A                                  5 19'Prv wgt:'
     A            W1LAST         7Y 2O  5 28EDTCDE(4)
     A            W1CW          50A  B  6  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
700aAA            CMDLN1        79A  O  7  1COLOR(BLU)
700aDA*                                 7  1'F7=Prev  F13=No More'
700aDA*                                     COLOR(BLU)
     A                                  2 14'/'
     A          R SCREEN61
     A*%%TS  SD  20081005  170617  DAVE        REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA07
     A                                      CF10
     A                                      CA12
     A                                      CA13
700aAA                                      CA19
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'RU31261'
     A                                  1 10'Single Pick'
     A                                  3  1'Ord:'
     A            WOORD          7Y 0O  3  6EDTCDE(4)
     A                                  4  1'Lin:'
     A            WOLINE         5Y 0O  4  6EDTCDE(4)
     A                                  5  1'Itm:'
     A            WOITEM        20A  O  5  6
     A                                  6  1'Dsc:'
     A            WODESC        30A  O  6  6
700aDA*                                10  1'Weight'
700aMA                                 10  1'Wt'
700aAA            W1KGLB         3A  O 10  4DSPATR(HI)
     A            W1CNT          3Y 0O 10  8EDTCDE(4)
     A                                 10 12'of'
     A            W1QTY          5Y 0O 10 15EDTCDE(4)
     A            WOUM           5A  O 10 22COLOR(BLU)
     A            W1CW          50A  B 11  1
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                      CHECK(ER)
     A                                 12  1'(assumed decimal point with 2 deci-
     A                                      mal places)'
     A                                      COLOR(BLU)
     A                                 13  1'Prv wgt:'
     A            W1LAST         7Y 2O 13 10EDTCDE(4)
700aAA            CMDLN1        79A  O 15  1COLOR(BLU)
700aDA*                                15  1'F7=Prv F10=Accept F13=No More'
700aDA*                                     COLOR(BLU)
      *****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
      *****************************************************************
      *
      * Error Message Subfile Records
      *
      *    Error Message Subfile Record
      *
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
      *
      *    Error Message Subfile Control Record
      *
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
      *****************************************************************
      *
      * Error Message Subfile Records for 16 line screens
      *
      *    Error Message Subfile Record
      *
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
      *
      *    Error Message Subfile Control Record
      *
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
