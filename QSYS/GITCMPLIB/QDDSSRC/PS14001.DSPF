     A*%%TS  SD  19970623  133928  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS140FM   Built truck Pallet detail (one whse) - Display screen
     A*  23 June 1997
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
500 AA*    05/02/06  JCJ  5.00
     A*      Enh: When in view mode display the word VIEW under the
     A*           date in reverse image.
     A*
520 AA*    01/23/09  CWM  5.20
     A*      Enh: Added support for 3 digit stop numbers.
     A*
640 AA*    01/19/11  RH   6.40
     A*      - Enh: Renamed PS140FM to PS14001 for GUI.
     A*      - Enh: Added customized screens for Springfield Grocer
SGC AA*    01/19/11  RH   SGCa
     A*      - Enh: Display '*' for ice cream ICR desg item.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19970617  150737  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'PS140.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Pallet Detail'
     A                                      DSPATR(HI)
500 AA            WMODE          6   O  2 73
500 AA  30                                  DSPATR(RI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19970623  133928  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Warehouse      :'
     A            WOWHSE        35A  O  1 19
     A                                  2  2'Route          :'
     A            WORTE          5A  O  2 19
     A                                  3  2'Compartment    :'
     A            WOCOM         15A  O  3 19
     A                                  3 52'Position to Stop  . . .'
     A            PTINP2         2Y 0B  3 77EDTCDE(4)
     A                                  4  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  4 19EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970623  132955  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
520 DA*           PSSSTP    R        O 11  9REFFLD(PSSREC/PSSSTP *LIBL/PLTSTP1)
520 MA            PSSSTP    R        H      REFFLD(PSSREC/PSSSTP *LIBL/PLTSTP1)
520 AA            DSPSTP         5Y 0O 11  6
     A                                      EDTCDE(4)
     A            PSSBSQ    R        O 11 17REFFLD(PSSREC/PSSBSQ *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A            PSSPCS    R        O 11 20REFFLD(PSSREC/PSSPCS *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A            PSSCUB    R        O 11 29REFFLD(PSSREC/PSSCUB *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A            PSSSWT    R        O 11 41REFFLD(PSSREC/PSSSWT *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970623  132955  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A*                                     CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 13'Bulk'
     A                                      DSPATR(HI)
     A                                  9 20'Total'
     A                                      DSPATR(HI)
     A                                  9 41'Total'
     A                                      DSPATR(HI)
     A                                  9 29'Total'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 DA*                                10  7'Stop'
520 MA                                 10  6' Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 20'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aAA*
640aAA          R SGCSFL                    SFL
     A*%%TS  SD  19970623  132955  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
520 MA            PSSSTP    R        H      REFFLD(PSSREC/PSSSTP *LIBL/PLTSTP1)
520 AA            DSPSTP         5Y 0O 11  6
     A                                      EDTCDE(4)
     A            PSSBSQ    R        O 11 17REFFLD(PSSREC/PSSBSQ *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A            PSSPCS    R        O 11 20REFFLD(PSSREC/PSSPCS *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
SGCaAA            WOMARK         1   O 11 27
     A            PSSCUB    R        O 11 29REFFLD(PSSREC/PSSCUB *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A            PSSSWT    R        O 11 41REFFLD(PSSREC/PSSSWT *LIBL/PLTSTP1)
     A                                      EDTCDE(4)
     A*
640aAA          R SGCSFC                    SFLCTL(SGCSFL)
     A*%%TS  SD  19970623  132955  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A*                                     CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 13'Bulk'
     A                                      DSPATR(HI)
     A                                  9 20'Total'
     A                                      DSPATR(HI)
     A                                  9 41'Total'
     A                                      DSPATR(HI)
     A                                  9 29'Total'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 MA                                 10  6' Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 20'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
222  A*                                     SLNO(06)
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
222  A*           OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
