     A********************************************************************
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  EQ485FM  RF Route Equipment - Return
     A*  April 29, 2016
     A*  Retha Davis
     A*
     A********************************************************************
     A*  Revisions:
     A*
700aAA*    04/29/16  RBD   7.00a
     A*      - Created.
     A*
     A********************************************************************
     A*  Client Custom Revisions:
     A*
     A*
     A********************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A********************************************************************
     A*         Enter Route Id
     A********************************************************************
     A          R SCREEN1
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'EQ485.01'
     A                                  1 11'Return Equip'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  3'Route Id:'
     A            W1RTID         5A  B  5 14
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                 10  1'F3=Exit/Signoff'
     A                                      COLOR(BLU)
     A*
     A********************************************************************
     A*         Scan equipment code and enter qty
     A********************************************************************
     A          R SCREEN2
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'EQ845.02'
     A                                  2  2'Return Equip'
     A                                      DSPATR(HI)
     A                                  4  2'Route:'
     A            W2RTE          5A  O  4  9
     A                                  5  2'Route Id:'
     A            W2RTID         5A  O  5 12
     A                                  7  2'Equip:'
     A            W2EQ           8A  B  7  9DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                  9  2'Qty:'
     A            W2QTY          5Y 0B  9  9DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      CHECK(ER)
     A*
     A                                 11  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(12)
     A                                      CLRL(1)
