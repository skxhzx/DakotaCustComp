     A*%%TS  SD  20080506  113839  JEFF        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS180FM   Built truck Pallet detail (one whse) - Display screen
     A*  04 May  2006
     A*  Jeffrey  Jahnke
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    05/03/06  DAS  5.00
     A*      - Created customized screens for I-Suppy and Harbor.
     A*
520 AA*    05/06/08  JCJ  5.20
     A*      - Added Tote item flag - General Client subfile.
     A*
520bAA*    01/28/09  CWM  5.20b
     A*      - Enh: Added support for entry of 5 CHARACTER host stop.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19990907  190150  DAVE        REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'PS180.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Tote Detail'
     A                                      DSPATR(HI)
     A            WMODE          6   O  2 73
     A  30                                  DSPATR(RI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19990907  190150  DAVE        REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Warehouse      :'
     A            WOWHSE        35A  O  1 19
     A                                  2  2'Route          :'
     A            WORTE          5A  O  2 19
     A                                  2 26'Compartment:'
     A            WOCOM         15A  O  2 39
     A                                  3  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  3 19EDTCDE(4)
     A                                  3 23'Row:'
     A            WOROW          2  0O  3 28
     A                                  3 32'Col:'
     A            WOCOL          2  0O  3 37
     A                                  4  2'Stop           :'
520bDA*           WOSTOP         2  0O  4 19
520bMA            WOSTOP         2  0H
520bAA            HSTSTP         5Y 0O  4 19EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
520 MA            LWITEM    R        O 11  6REFFLD(LWREC/LWITEM *LIBL/WORKLBL)
520 MA            LWUCOD    R        O 11 22REFFLD(LWREC/LWUCOD *LIBL/WORKLBL)
520 MA            LWTYPE    R        O 11 26REFFLD(LWREC/LWTYPE *LIBL/WORKLBL)
520 AA            W1TFLG         1A  O 11 31
520 MA            LWQALC    R        O 11 35REFFLD(LWREC/LWQALC *LIBL/WORKLBL)
     A                                      EDTCDE(4)
520 MA            LWCUBE    R        O 11 41REFFLD(LWREC/LWCUBE *LIBL/WORKLBL)
     A                                      EDTCDE(4)
520 MA            LWSWGT    R        O 11 52REFFLD(LWREC/LWSWGT *LIBL/WORKLBL)
     A                                      EDTCDE(4)
520 MA            LWWHDP         5A  O 11 63
     A            LWDISP        12A  O 11 69
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
520 AA                                  9 29'Tote'
520 AA                                      DSPATR(HI)
520 MA                                  9 34'Total'
     A                                      DSPATR(HI)
520 MA                                  9 41'Total'
     A                                      DSPATR(HI)
520 MA                                  9 52'Total'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 MA                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 D ***                              10 23'UOM'
520 MA                                 10 22'UM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 D ***                              10 27'Type'
520 MA                                 10 25'Typ'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 AA                                 10 29'Item'
520 AA                                      DSPATR(HI)
520 AA                                      DSPATR(UL)
520 MA                                 10 34'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 MA                                 10 41'Cube      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 MA                                 10 52'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
520 MA                                 10 63'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 69'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Custom subfile for Harbor.
     A*
     A          R HARSFL                    SFL
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LWITEM    R        O 11  6REFFLD(LWREC/LWITEM *LIBL/WORKLBL)
     A            LWUCOD    R        O 11 22REFFLD(LWREC/LWUCOD *LIBL/WORKLBL)
     A            LWTYPE    R        O 11 26REFFLD(LWREC/LWTYPE *LIBL/WORKLBL)
     A            W1TFLG         1A  O 11 31
     A            LWQALC    R        O 11 35REFFLD(LWREC/LWQALC *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWCUBE    R        O 11 41REFFLD(LWREC/LWCUBE *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWSWGT    R        O 11 52REFFLD(LWREC/LWSWGT *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWWHDP         5A  O 11 63
     A            LWDISP        12A  O 11 69
     A*
     A          R HARSFC                    SFLCTL(HARSFL)
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 29'Tote'
     A                                      DSPATR(HI)
     A                                  9 34'Total'
     A                                      DSPATR(HI)
     A                                  9 41'Total'
     A                                      DSPATR(HI)
     A                                  9 52'Total'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'UM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'Typ'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Item'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Cube      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 69'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
500 AA*****************************************************************
500 AA*
500 AA* Custom subfile for I-Supply.
500 AA*
     A          R ISPSFL                    SFL
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LWITEM    R        O 11  7REFFLD(LWREC/LWITEM *LIBL/WORKLBL)
     A            WOMARK         1   O 11 23
     A            LWUCOD    R        O 11 26REFFLD(LWREC/LWUCOD *LIBL/WORKLBL)
     A            LWTYPE    R        O 11 30REFFLD(LWREC/LWTYPE *LIBL/WORKLBL)
     A            LWQALC    R        O 11 35REFFLD(LWREC/LWQALC *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWCUBE    R        O 11 41REFFLD(LWREC/LWCUBE *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWSWGT    R        O 11 52REFFLD(LWREC/LWSWGT *LIBL/WORKLBL)
     A                                      EDTCDE(4)
     A            LWWHDP         5A  O 11 63
     A            LWDISP        12A  O 11 69
     A*
     A          R ISPSFC                    SFLCTL(ISPSFL)
     A*%%TS  SD  20060503  115327  BFC         REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 34'Total'
     A                                      DSPATR(HI)
     A                                  9 41'Total'
     A                                      DSPATR(HI)
     A                                  9 52'Total'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'UOM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Cube      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 69'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
222  A*                                     SLNO(06)
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
222  A*           OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
