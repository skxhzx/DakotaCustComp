     A*%%TS  SD  19970515  114644  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LP110FM   Pallet License Plate - View
     A*  31 January, 1997
     A*  Dave Sommerville
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
416 AF*    05/23/02  RRH  4.16
     F*      - Increase W1LCNS and W2LCNS to 15.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970515  114644  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'LP110.01'
     A                                  1 35'Pallet License View'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  5 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  4'Transaction # . . . . . .'
     A            W1TRN#         7Y 0B  7 31CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AON35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  8  4'-OR-'
     A                                  9  4'License # . . . . . . . .'
416 DA*           W1LCNS         7Y 0B  9 31CHECK(ER)
416 MA            W1LCNS        15A  B  9 31CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02
     AO 35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
416 DA*                                     EDTCDE(4)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19970204  083432  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'LP110.02'
     A                                  1 35'Pallet License View'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  7  4'License # . . . . . . . .'
416 DA*           W2LCNS         7Y 0O  7 31EDTCDE(3)
416 MA            W2LCNS        15A  O  7 31
     A                                  8  4'Transaction number. . . .'
     A            W2TRN#         7Y 0O  8 31EDTCDE(3)
     A                                  9  4'Item number . . . . . . .'
     A            W2ITEM        49A  O  9 31
     A                                 10  4'Putaway date  . . . . . .'
     A            W2PDTE         6Y 0O 10 31EDTCDE(Y)
     A                                 11  4'Slot number . . . . . . .'
     A            W2DISP        12A  O 11 31
     A                                 12  4'Quantity  . . . . . . . .'
     A            W2PQTY         5Y 0O 12 31EDTCDE(4)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
