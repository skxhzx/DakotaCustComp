     A*%%TS  SD  20120907  102526  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*  Copyright (C) 2012 BFC Software  Inc. - All Rights Reserved
     A*  BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  IT1900FM    Item Substitutes lookup
     A*  06 Sep 2012
     A*  Retha Davis
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    09/06/12  RBD  6.40a
     A*      - Created
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20120906  153747  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                  1  2'IT1900.01'
     A                                  1 29'Item Substitutes Lookup'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20120907  102526  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD005         1A  O  1  2
     A            WHCODE         3Y 0O  1  5EDTCDE(4)
     A                                  1  9'-'
     A            WHDESC        30A  O  1 11
     A                                  2  4'Cust -'
     A            ISUCUST       11Y 0O  2 11EDTCDE(Z)
     A            FLD002         1A  O  3  2
     A                                  3  4'Item -'
     A            W1ITDS        48   O  3 11
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20120906  164843  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ISUISUB   R        O 11  7REFFLD(ISUREC/ISUISUB *LIBL/ITEMSUB)
     A            W1SDSC        30   O 11 23
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20120906  164843  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Item Substitute                   -
     A                                                  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
