     A****************************************************************
     A*
     A* ER322PR   Employee management report for a shift(RECV) (Summary)
     A* 21 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER322'
     A*
     A* Print shift code and description
     A*
     A          R SHHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'SHIFT:'
     A            WKSHFT    R             +1REFFLD(WKSHFT ER321)
     A            SHDESC    R             +1REFFLD(SHDESC SHIFT)
     A  93                                75
     A                                      '(Continued)'
     A*
     A* Print crew code and description
     A*
     A          R CRHEAD
     A                                      SPACEA(002)
     A                                     1'CREW:'
     A            WKCREW    R             +1REFFLD(WKCREW ER321)
     A            CRDESC    R             +1REFFLD(CRDESC CREW)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    44'Direct'
     A                                    56'Cases'
     A                                    73'Cube'
     A                                    87'Weight'
     A                                      SPACEA(001)
     A*
     A                                     2'  Date  '
     A                                      UNDERLINE
     A                                    11'Employee                       '
     A                                      UNDERLINE
     A                                    44' Hours'
     A                                      UNDERLINE
     A                                    52' Received'
     A                                      UNDERLINE
     A                                    68' Received'
     A                                      UNDERLINE
     A                                    84' Received'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A            GTDATE         6S 0      2EDTCDE(Y)
     A  91                                75
     A                                      '(Continued)'
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKEMP#    R             11REFFLD(WKEMP# ER321)
     A                                       EDTCDE(Z)
     A            WKNAME    R             17REFFLD(WKNAME ER321)
     A            DIRTOT         5S 2     44EDTCDE(3)
     A            WKPTOT    R             52REFFLD(WKPTOT ER321)
     A                                      EDTCDE(1)
     A            WKCTOT    R             63REFFLD(WKCTOT ER321)
     A                                      EDTCDE(1)
     A            WKWTOT    R             79REFFLD(WKWTOT ER321)
     A                                      EDTCDE(1)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#DTOT         7S 2     42EDTCDE(3)
     A            T#PTOT         7S 0     52EDTCDE(1)
     A            T#CTOT        11S 3     63EDTCDE(1)
     A            T#WTOT        11S 2     79EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER322: End of Report '
     A*
