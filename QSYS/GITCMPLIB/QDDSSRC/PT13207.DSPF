     A*%%TS  SD  20140219  142521  ROCKY       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PT132FM   Close transaction
     A*  12 October 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. PT132FM COPY AND NAME PT13201
     A*----------------------------------------------------------------
     A*  Revisions
218 AA*    02/05/97  RH   2.18
     A*      - Protect SC8 W8QRPL field, cannot update.
404 AA*    08/20/97  HNK  4.04
     A*      - Changed to display route instead of route ID on Head6
     A*        and Head7.
409 AA*    04/15/97  RH   4.09
     A*      - Changed to display quantity and unit of measure on head7
410 AA*    09/30/98  RH   4.10
     A*      - Add F20 override warning msg to head7
412 AA*    08/??/99  DAS  4.12
     A*      - ??????
412 AA*    09/02/99  HNK  4.12
     A*      - Add License #, Item and item description on Screen 8
414aAA*    08/03/00  RRH  4.14a
     A*      - Change labels on screen 4, 5, 10 to "From" & "To" slots.
415jAA*    06/26/01   RH  4.15j
     A*      - Add item # to screen 5.
     A*
416 AA*    01/30/02  MLB  4.16
     A*      - Add order# to Screen 2,3,4 & DETAIL 6 as a hidden field.
     A*      - Add item # to Screen 2,3,4 as a hidden field.
416 AA*    05/23/02  RRH  4.16
     A*      - Increase W8LCN# to 15.
     A*
417 AA*    08/07/03  MLB  4.17
     A*      - Fix: Revised DETAIL 6 to reverse image W6DISP for out
     A*        label records.
417aAA*    02/06/04  HNK  4.17a
     A*      - Enh: Revised DETAIL 6 to retrieve record number when
     A*        cursor is positioned to the line and F7 is pressed.
417bAA*    05/12/04  RH   4.17b
     A*      - Fix: Change SLFEND to *MORE, the '+' was writing over
     A*        the merge flag.
417cAA*    04/30/04  RH   4.17c
     A*      - Enh: Add item number, description and license plate to
     A*        screen 9 - Customer returns.
417dAA*    07/07/04  RH   4.17d
     A*      - Enh: Add position to label to screen6.
     A*
520 AA*    01/28/09 CWM  5.20
     A*      - Enh: Add support for 3 digit stops.
520aAA*    04/01/09 RH   5.20a
     A*      - Enh: Add F8 to call substitutes RH182
     A*
610 AA*    11/09/10 MLB  6.10
     A*      - Enh: Revised Screen 6 to add Hidden field
     A*        to contain order seq nbr, 5,0. W6SEQ is 4,0.
640aAA*    06/10/11 RH   6.40a
     A*      - Enh: Add F20 to Screen 6 to allow override
     A*        Renamed to PT13202
640bAA*    06/14/11 RH   6.40b
     A*      - Enh: Add W6SQTY hidden field to save entered qty to set
     A*        error flag.  Renamed to PT13203
640cAA*    06/16/11 DAS  6.40c
     A*      - Enh: Add hidden warning flag W6WARN.
650AAA*    02/19/14 RH/MLB 6.50a
     A*      - Rename format to PT13205, increase w6 seq to 5 long
     A*
700 AA*    12/16/16  MLB  7.00
     A*      - Enh: Renamed format to PT13206.
     A*      - Added 3 new fields to subfile format, DETAIL6. New fields
     A*        needed as part of CBIj mod.
     A*
720 AA*    04/06/18  RBD  7.20
     A*      - Enh: Renamed format to PT13207.
     A*      - On detail6 color item pink if it's been subbed.
     A*      - Added subbed comment to head6.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Avalon Foodservice
     A*
AVL AA*    12/05/11  MLB  AVL
     A*      - Added new field to HEAD6 to display Retalix unit of work#.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941012  110510  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PT132.01'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Transaction number . . . .'
     A            W1TRN#         7Y 0B  8 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19941109  202424  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
412 AA                                      CF10
416 AA            W2ORD          7S 0H      TEXT('Hidden: Order#')
416 AA            W2ITEM        15A  H      TEXT('Hidden: Item #')
     A                                  1  2'PT132.02'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Bulk Pick - Full Pallet'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Route . . . . . .'
     A            LBRTE          5A  O  6 21
     A                                  7  2'Department  . . .'
     A            LBWHDP         5A  O  7 21
     A            WDDESC        30A  O  7 31
     A                                  8  2'Stop  . . . . . .'
520 DA*           LBSTOP         2Y 0O  8 21EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  8 21EDTCDE(3)
     A                                  9  2'Customer  . . . .'
     A            OHCNAM        40A  O  9 21
     A                                 10  2'Slot  . . . . . .'
     A            LBDISP        12A  O 10 21
     A                                 11  2'Quantity  . . . .'
     A            LBQPCK         5Y 0O 11 21EDTCDE(3)
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  4'Quantity picked . . . . .'
     A            W2QTY          5Y 0B 16 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
412 MA                                 23  2'Enter=Save & Return  F3=Exit  F5=R-
412 AA                                      efresh  F10=Save & Close  F12=Cance-
412 AA                                      l'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5   O 11 27
     A            WOUM2          5   O 16 37
     A          R SCREEN3
     A*%%TS  SD  19941109  202424  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
412 AA                                      CF10
416 AA            W3ORD          7S 0H      TEXT('Hidden: Order#')
416 AA            W3ITEM        15A  H      TEXT('Hidden: Item #')
     A                                  1  2'PT132.03'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 28'Bulk Pick - Partial Pallet'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Route . . . . . .'
     A            LBRTE          5A  O  6 21
     A                                  7  2'Department  . . .'
     A            LBWHDP         5A  O  7 21
     A            WDDESC        30A  O  7 31
     A                                  8  2'Stop  . . . . . .'
520 DA*           LBSTOP         2Y 0O  8 21EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  8 21EDTCDE(3)
     A                                  9  2'Customer  . . . .'
     A            OHCNAM        40A  O  9 21
     A                                 10  2'Slot  . . . . . .'
     A            LBDISP        12A  O 10 21
     A                                 11  2'Quantity  . . . .'
     A            LBQPCK         5Y 0O 11 21EDTCDE(3)
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  4'Quantity picked . . . . .'
     A            W3QTY          5Y 0B 16 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
412 MA                                 23  2'Enter=Save & Return  F3=Exit  F5=R-
412 AA                                      efresh  F10=Save & Close  F12=Cance-
412 AA                                      l'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5   O 11 27
     A            WOUM2          5   O 16 37
     A          R SCREEN4
     A*%%TS  SD  19941109  202424  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
412 AA                                      CF10
416 AA            W4ORD          7S 0H      TEXT('Hidden: Order#')
416 AA            W4ITEM        15A  H      TEXT('Hidden: Item #')
     A                                  1  2'PT132.04'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 27'Bulk Pick with Replenishment'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Route . . . . . .'
     A            LBRTE          5A  O  6 21
     A                                  7  2'Department  . . .'
     A            LBWHDP         5A  O  7 21
     A            WDDESC        30A  O  7 31
     A                                  8  2'Stop  . . . . . .'
520 DA*           LBSTOP         2Y 0O  8 21EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  8 21EDTCDE(3)
     A                                  9  2'Customer  . . . .'
     A            OHCNAM        40A  O  9 21
414aDA*                                10  2'Pick slot . . . .'
414aMA                                 10  2'From slot . . . .'
     A            LBDISP        12A  O 10 21
     A                                 11  2'Pick quantity . .'
     A            LBQPCK         5Y 0O 11 21EDTCDE(3)
414aDA*                                12  2'Replenish slot  .'
414aMA                                 12  2'To slot . . . . .'
     A            LBDSP2        12A  O 12 21
     A                                 13  2'Replenish qty . .'
     A            LBQRMN         5Y 0O 13 21EDTCDE(3)
     A                                 16  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 18  4'Quantity picked . . . . .'
     A            W4QPCK         5Y 0B 18 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 19  4'Quantity replenished  . .'
     A            W4QRPL         5Y 0B 19 31
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
412 MA                                 23  2'Enter=Save & Return  F3=Exit  F5=R-
412 AA                                      efresh  F10=Save & Close  F12=Cance-
412 AA                                      l'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5   O 11 27
     A            WOUM2          5   O 13 27
     A            WOUM3          5   O 18 37
     A            WOUM4          5   O 19 37
     A          R SCREEN5
     A*%%TS  SD  19941109  202424  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PT132.05'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 30'Replenishment/Transfer'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Department  . . .'
     A            LBWHDP         5A  O  6 21
     A            WDDESC        30A  O  6 31
414aDA*                                 7  2'Pick slot . . . .'
414aMA                                  7  2'From slot . . . .'
     A            LBDISP        12A  O  7 21
     A                                  8  2'Pick quantity . .'
     A            LBQPCK         5Y 0O  8 21EDTCDE(3)
414aDA*                                 9  2'Replenish slot  .'
414aMA                                  9  2'To slot . . . . .'
     A            LBDSP2        12A  O  9 21
415j A                                 10  2'Item #  . . . . .'
415j A            LBITEM        15A  O 10 21
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 15  4'Replenished quantity . . .'
     A            W5QRPL         5Y 0B 15 32
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5   O  8 27
     A            WOUM2          5   O 15 38
     A*
     A          R DETAIL6                   SFL
     A*%%TS  SD  20140219  144058  BFC         REL-V7R1M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            W6OUT          1A  H
     A            W6LBL#         7S 0H
     A            W6ORD          7S 0H      TEXT('Hidden: Order#')
     A            W6ORSQ         5S 0H      TEXT('Hidden: Seq #')
     A            W6SQTY         5S 0H      TEXT('Hidden: Save Qty')
     A            W6WARN         1A  H      TEXT('Hidden: Warn Flag')
     A            W6GRP1         1A  H      TEXT('Hidden: LBGRP1')
     A            W6GRP2         1A  H      TEXT('Hidden: LBGRP2')
700 AA            W6TYPE         1A  H      TEXT('Hidden: LBTYPE')
700 AA            W6PAL#         7Y 0H      TEXT('Hidden: LBPAL#')
700 AA            W6ASL2         3A  H      TEXT('Hidden: LBASL2')
720 AA            HLDI35         1A  H      TEXT('Hold Ind 35 setting')
     A            W6LINE         3Y 0O 13  2EDTCDE(3)
     A            W6ITEM        15A  O 13  7
720 AA  35                                  COLOR(PNK)
     A            W6SEQ          5Y 0O 13 23EDTCDE(3)
520 DA*           W6STOP         2Y 0O 13 29EDTCDE(3)
     A            W6STOP         2Y 0H
     A            DSPSTP         3Y 0O 13 30EDTCDE(3)
     A            W6DISP        12A  O 13 34
     A  65                                  DSPATR(RI)
     A            W6CWGT         1A  O 13 48
     A            W6QPCK         5Y 0O 13 53EDTCDE(3)
     A            W6STAT         1A  O 13 61
     A            W6QTY          5Y 0B 13 66EDTCDE(3)
     A                                      DSPATR(MDT)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WOUM           5A  O 13 72
     A            W6MRG          1A  O 13 79
     A*
     A          R HEAD6                     SFLCTL(DETAIL6)
     A*%%TS  SD  20140219  142521  ROCKY       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0009)
     A                                      CF07
     A                                      CF08
     A                                      CF10
     A                                      CF20
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&RTNRRN)
     A            RTNRRN         5S 0H      TEXT('Hidden: Subfile RRN')
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PT132.06'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Normal Pick'
     A                                      DSPATR(HI)
     A                                 10  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  2'Transaction . . .'
     A                                  5  2'Warehouse . . . .'
     A                                  6  2'Route . . . . . .'
     A                                  7  2'Zone  . . . . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A            W6UOW#        15A  O  4 30
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 30
     A            W6RTE          5A  O  6 21
     A            PSSZON         6A  O  7 21
     A            W6ZDSC        30A  O  7 30
     A                                  8  2'Pallet type . . .'
     A            W6PTYP         5A  O  8 21
720 AA  36                              9  2'Items which have been subbed'
720 AA  36                                  COLOR(PNK)
     A                                  9 38'Position to label#. . .'
     A            PTINP1         7Y 0B  9 63EDTCDE(4)
417d A* 02                                  DSPATR(PC)
     A                                 12  2'Line'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 29'Stop'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 34'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 66'Picked     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 47'Catch'
     A                                      DSPATR(HI)
     A                                 12 47'WT  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 59'Closed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 53'Qty  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 12 23'Seq# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 23'Item'
     A                                      DSPATR(HI)
     A                                 12 78'Mrg'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
720 DA**                                9  2' '
     A                                 11 77'Flag'
     A                                      DSPATR(HI)
     A*
     A          R DETAIL7                   SFL
     A*%%TS  SD  19941108  194002  DAVE        REL-V2R1M1  5738-PW1
     A  85                                  SFLNXTCHG
     A            W7LINE         3Y 0O 15  5EDTCDE(3)
     A            W7CWGT         7Y 2B 15 14
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A          R HEAD7                     SFLCTL(DETAIL7)
     A*%%TS  SD  19980415  122451  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
410 AA  98                                  ALARM
410 AA                                      CF20
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PT132.07'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Catch Weight'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 30
     A                                  6  2'Route/Stop  . . .'
     A            W7RTE          5A  O  6 21
     A                                  6 27'/'
520 DA*           LBSTOP         2Y 0O  6 29EDTCDE(3)
520 MA            LBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O  6 29EDTCDE(3)
     A                                  7  2'Dept./Section . .'
     A            LBWHDP         5A  O  7 21
     A            WDDESC        30A  O  7 30
     A            W7SECT         5A  O  7 65
     A                                  8  2'Order/Sequence# .'
     A            LBSEQ     R        O  8 31REFFLD(LBREC/LBSEQ *LIBL/LABEL)
     A                                      EDTCDE(3)
     A            LBORD          7Y 0O  8 21EDTCDE(3)
     A                                  8 29'/'
     A                                  9  2'Item  . . . . . .'
     A            LBITEM        15A  O  9 21
     A            ITDESC        30A  O  9 38
409 AA                                 10  2'Quantity  . . . .'
409 AA            LBQPCK         5Y 0O 10 21EDTCDE(3)
409 AA            UMDESC        30A  O 10 28
     A                                 12  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 14  5'Line'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 13'Catch Weight'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A          R SCREEN8
     A*%%TS  SD  19990902  092551  LYLE        REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PT132.08'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A                                  2 36'Receiving'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
412 AA                                  4 31'License number . . .'
416 DA*           W8LCN#         7Y 0O  4 52EDTCDE(3)
416 AA            W8LCN#        15A  O  4 52
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Department  . . .'
     A            LBWHDP         5A  O  6 21
     A            WDDESC        30A  O  6 31
     A                                  7  2'Slot  . . . . . .'
     A            LBDISP        12A  O  7 21
412 AA                                  8  2'Item  . . . . . .'
412 AA            W8ITEM    R        O  8 21REFFLD(ITITEM *LIBL/PIRITEM)
412 AA            W8DESC    R        O  8 37REFFLD(ITDESC *LIBL/PIRITEM)
     A                                  9  2'Quantity  . . . .'
     A            LBQPCK         5Y 0O  9 21EDTCDE(3)
     A            WOUM           5A  O  9 27
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 15  4'Quantity received  . . . .'
     A            W8QRPL         5Y 0B 15 32
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      EDTCDE(3)
     A            WOUM2          5A  O 15 38
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN9
     A*%%TS  SD  19941109  202424  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PT132.09'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Customer Return'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
417c A                                  4 31'License number. . .'
417c A            W9LCN#        15A  O  4 52
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
     A                                  6  2'Department  . . .'
     A            LBWHDP         5A  O  6 21
     A            WDDESC        30A  O  6 31
     A                                  7  2'Slot  . . . . . .'
     A            LBDISP        12A  O  7 21
417c A                                  8  2'Item  . . . . . .'
417c A            W9ITEM    R        O  8 21REFFLD(ITITEM *LIBL/PIRITEM)
417c A            W9DESC    R        O  8 37REFFLD(ITDESC *LIBL/PIRITEM)
417c A*                                 8  2'Quantity  . . . .'
417c A                                  9  2'Quantity  . . . .'
417c A*           LBQPCK         5Y 0O  8 21EDTCDE(3)
417c A            LBQPCK         5Y 0O  9 21EDTCDE(3)
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 15  4'Quantity returned  . . . .'
     A            W9QRPL         5Y 0B 15 32
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5A  O  8 27
     A            WOUM2          5   O 15 38
     A          R SCREEN10
     A*%%TS  SD  19960708  163251  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PT132.10'
     A                                  1 32'Close Transaction'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 28'Broken Case Replenishment'
     A                                      DSPATR(HI)
     A                                  4  2'Transaction . . .'
     A            $PTRN#         7Y 0O  4 21EDTCDE(3)
     A                                  5  2'Warehouse . . . .'
     A            LBWHSE         3Y 0O  5 21EDTCDE(3)
     A            WHDESC        30A  O  5 31
414aDA*                                 8  2'Pick slot . . . .'
414aMA                                  8  2'From slot . . . .'
     A            LBDISP        12A  O  8 21
     A                                  9  2'Pick quantity . .'
     A            LBQPCK         5Y 0O  9 21EDTCDE(3)
414aDA*                                12  2'Replenish slot  .'
414aMA                                 12  2'To slot . . . . .'
     A            LBDSP2        12A  O 12 21
     A                                 13  2'Replenish qty . .'
     A            LBQRMN         5Y 0O 13 21EDTCDE(3)
     A                                 16  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 18  4'Quantity picked . . . . .'
     A            W4QPCK         5Y 0B 18 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 19  4'Quantity replenished  . .'
     A            W4QRPL         5Y 0B 19 31
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A            WOSTAT         6A  O  2  2DSPATR(HI)
     A            WOUM           5A  O  9 27
     A            WOUM2          5A  O 13 27
     A            WOUM3          5A  O 18 37
     A            WOUM4          5A  O 19 37
     A                                  7  2'Pick item . . . .'
     A                                 11  2'Replenish item  .'
     A            WOITM2        15A  O  7 21
     A            LBITEM        15A  O 11 21
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
417a A                                  1  2'ENTER=Save&Return F3=Exit F7=Clr F-
420a A                                      lg F8=Subs F10=Close F20=Accept -
520a A*                                     lg F10=Close F12=Cancel F20=Accept -
417a A*                                 1  2'ENTER=Save&Return F3=Exit F5=Refre-
417a A*                                     sh F10=Close F12=Cancel F20=Accept -
412 AA                                      Warning'
412 AA                                      COLOR(BLU)
     A*
     A* Command line
     A*
     A          R CMDREC2
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F3=Exit F5=Refresh  F12=Cancel  F-
     A                                       20=Accept warning'
     A                                      COLOR(BLU)
