     A*%%TS  SD  20091125  161158  RETHA       REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  LOADR9FM     RF Loader Module - Start Route.
     A*  December 4, 2006
     A*  Jose G. Duque
     A*
     A********************************************************************
     A*  Revisions:
     A*
520 AA* 02/24/09  RBD   5.20
     A*   - Enh: Added route id for scanning on screen1.  User will
     A*     now have the option to enter the route or scan the route
     A*     id.
     A*
530 AA* 11/24/09  RBD   5.30
     A*   - Enh: Added screen4 for unloading a pallet.
     A*
530aAA* 06/24/10  RBD   5.30a
     A*   - Fix: Changed trailer id from 6 bytes to 8.
     A*
640 AA*   2/17/11  JGD  6.40
     A*    - Enh: Add function key "F4" to support pgm Loadr9
     A*    - Enh: process to allow the user to display the list
     A*    - Enh: of existing trailers number.
     A*
650 AA*   5/15/13  RBD  6.50
     A*    - Changed format name to LOADR903.
     A*    - Removed F9 function, add ons not needed. Having the route id
     A*      negates duplicate pallet id's for add ons.
     A*
650aAA*   8/18/14  RBD  6.50a
     A*    - Changed format name to LOADR904.
     A*    - Increased size of entry field which holds the barcode scanned
     A*      on the pallet label. Previously it was the last 2 char of
     A*      route id + pallet id, now it's the full route id + pallet id.
     A*      We're making this change because Coastal has more than 100
     A*      trucks, which means we need more than the last 2 char of
     A*      route id to ensure the barcode is unique for the day's routes.
     A*      By using the full route id + pallet id we can be sure the
     A*      combo field will always be unique, thus making it as fail safe
     A*      as possible that the pallet gets loaded onto the right truck.
     A*
650bAA*   3/06/15  RBD  6.50b
     A*    - Changed format name to LOADR905.
     A*    - If new option OPVFYP verify pallet label against pick label
     A*      is Y, require user to scan the Trax barcode from any pick
     A*      label on the pallet, then go to ORDP to get the label#, then
     A*      go to LABEL to compare the label's route id and pallet id
     A*      to the pallet label and if they don't match throw an error.
     A*
700aAA*   7/14/15  NLK  7.00a
     A*    - Changed format name to LOADR906.
     A*    - screen4 w4plt# was 7 now 10 due to chg 6.50a full rte id + plt id
     A*
700bAA*  11/12/15  RBD  7.00b
     A*    - Changed format name to LOADR907.
     A*    - Reinstated F9 function (rev 650) per Cheney's request.
     A*
700cAA*  02/03/16  RBD  7.00c
     A*    - Changed format name to LOADR908.
     A*    - Increased route id on screen1 from 5 to 10 in case the user
     A*      wants to scan the route id/pallet barcode on the BIG pallet label.
     A*      Also changed the field name from w1rtid to w1rtid10 so that I can
     A*      move it into w1rtid at the beginning of LOADR9.
     A*
710aAA*  10/12/17  RBD  7.10a
     A*    - Changed format name to LOADR909.
     A*    - Added F2=Load Eq to screen1.
     A*
720aAA*  08/07/18  RBD  7.20a
     A*    - Changed format name to LOADR910.
     A*    - Changed a few line numbers on screen2 to make room for CBIa
     A*      revision.
     A*
     A********************************************************************
     A*  Client Custom Revisions: Cheney
     A*
CBIaAA*  08/07/18  RBD  CBIa
     A*    - Added LDRDCOM questions per Keith Sanders.
     A*      Used PIRMODSCBI/LOADR9 rev 3/16/09 CBI as base.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
700bMA                                      CA09
     A********************************************************************
     A*         Enter Route # Screen
     A********************************************************************
     A          R SCREEN1
     A*%%TS  SD  20061205  093435  JOSED       REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA11
     A                                      CA01
710aAA                                      CA02
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9.01'
     A                                  1 12'Plt Load'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
520 AA                                  5  3'Route Id:'
700cDA**          W1RTID         5A  B  5 14
700cMA            W1RTID10      10A  B  5 14
520 AA  29                                  DSPATR(RI)
520 AA N29N90                               DSPATR(UL)
520 AA  09                                  DSPATR(PC)
520 AA  90                                  DSPATR(PR)
520 AA                                      CHECK(ER)
520 DA**                                5  3'Route #:'
520 MA                                  7  3'Route #:'
520 DA**          W1RTE#         5A  B  5 14
520 MA            W1RTE#         5A  B  7 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
710aMA                                 10  1'F1=UnloadPlt  F2=LoadEq'
530 AA                                      COLOR(BLU)
     A N27                             11  1'F3=Exit/Signoff'
     A                                      COLOR(BLU)
     A  27                             11  1'F3=Exit  F11=CloseRte'
     A                                      COLOR(BLU)
     A*
     A*
     A********************************************************************
     A*         Enter Trailer # Screen (if route no assigned)
     A********************************************************************
     A          R SCREEN2
     A*%%TS  SD  20061227  163209  JOSED2      REL-V5R3M0  5722-WDS
     A                                      CA03
640 AA                                      CA04
     A                                      CA12
     A  98                                  ALARM
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(11)
     A                                      OVERLAY
      *
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
      *
     A                                  1  2'LOADR9.02'
720aMA                                  3  2'Route:'
720aMA            W2RTE#         5A  O  3  9
720aMA                                  4  2'Door #:'
720aMA            W2DOOR         3A  O  4 10
720aMA                                  5  2'Plt Count:'
720aMA            W2CNTR         3Y 0O  5 13EDTCDE(Z)
CBIaAA  66                              6  2'Is trailer clean?'
CBIaAA  66        W2TRLQ         1A  B  6 27DSPATR(UL)
CBIaAA N21                                  DSPATR(HI)
CBIaAA  21                                  DSPATR(RI)
CBIaAA  21                                  DSPATR(PC)
CBIaAA  90                                  DSPATR(PR)
CBIaAA  66                              7  2'Is trailer precooled?'
CBIaAA  66        W2TKQ          1A  B  7 27DSPATR(UL)
CBIaAA N22                                  DSPATR(HI)
CBIaAA  22                                  DSPATR(RI)
CBIaAA  22                                  DSPATR(PC)
CBIaAA  90                                  DSPATR(PR)
CBIaAA  66                              8  2'Is trailer load 3 Comp?'
CBIaAA  66        W2LODQ         1   B  8 27DSPATR(UL)
CBIaAA N23                                  DSPATR(HI)
CBIaAA  23                                  DSPATR(RI)
CBIaAA  23                                  DSPATR(PC)
CBIaAA  90                                  DSPATR(PR)
     A                                  9  2'Trl Id:'
530aDA**          W2TRL#         6A  B  9 10DSPATR(UL)
530aMA            W2TRL#         8A  B  9 10DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
640 DA*                                11  1'F3=Exit  F5=Refresh'
640 MA                                 11  1'F3=Exit  F5=Refresh F4=Prompt'
     A                                      COLOR(BLU)
     A*
     A********************************************************************
     A*         Pallet Loading Screen
     A********************************************************************
     A          R SCREEN3
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9.03'
     A                                  2  2'Plt Load'
     A                                      DSPATR(HI)
     A                                  4  2'Route:'
     A            W3RTE#         5A  O  4  9
     A                                  5  2'Door #:'
     A            W3DOOR         3A  O  5 10
     A                                  6  2'Plt Count:'
     A            W3CNTR         3Y 0O  6 13EDTCDE(Z)
700bMA  25                              8  2'Plt Id:'
700bMA**25        W3PLT#         7Y 0B  8 10DSPATR(UL)
700bMA  25        W3PLT#        10Y 0B  8 10DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
700bMA N25                              8  2'Tran #:'
700bMA N25        W3TRN#         7Y 0B  8 10DSPATR(UL)
700bMA N22                                  DSPATR(HI)
700bMA  22                                  DSPATR(RI)
700bMA  22                                  DSPATR(PC)
700bMA  90                                  DSPATR(PR)
700bMA                                      CHECK(ER)
700bMA                                      EDTCDE(Z)
650bAA                                  9  2'Pck Lb:'
650bAA  95                                  DSPATR(ND)
650bAA            W3PCKL        10Y 0B  9 10DSPATR(UL)
650bAA N31                                  DSPATR(HI)
650bAA  31                                  DSPATR(RI)
650bAA  31                                  DSPATR(PC)
650bMA  90
650bAAO 95                                  DSPATR(PR)
650bAA  95                                  DSPATR(ND)
650bAA                                      CHECK(ER)
650bAA                                      EDTCDE(Z)
     A                                 10  2'Trl Id:'
530aDA**          W3TRL#         6A  B 10 10DSPATR(UL)
530aMA            W3TRL#         8A  B 10 10DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
700bMA  25N26N28                       11  1'F3=Exit  F9=Add-Ons'
700bMA                                      COLOR(BLU)
     A*
700bMA N25N26N28                       11  1'F3=Exit  F9=Plt Id'
     A                                      COLOR(BLU)
     A*
     A  26 28                          11  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
530 AA*         Unload Pallet Screen
     A********************************************************************
     A          R SCREEN4
     A*%%TS  SD  20091125  161158  RETHA       REL-V6R1M0  5761-WDS
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR9.04'
     A                                  2  2'** UNLOAD PALLET **'
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  4  2'Whse:'
     A            W4WHSE         3Y 0B  4 10EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Rte Id:'
     A            W4RTID         5A  B  5 10DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22
     AON21N23N24                            DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  2'Plt Id:'
700aDA**          W4PLT#         7Y 0B  6 10DSPATR(UL)
700aMA            W4PLT#        10Y 0B  6 10DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                  7  2'Trl Id:'
530aDA**          W4TRL#         6A  B  7 10DSPATR(UL)
530aMA            W4TRL#         8A  B  7 10DSPATR(UL)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(12)
     A                                      CLRL(1)
