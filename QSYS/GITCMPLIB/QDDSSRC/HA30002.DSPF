     A*%%TS  SD  20120508  074810  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  HA300FM    HACCP Logging - Display/Print
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
640  A*    04/30/12  LMC  6.40
     A*      - Created.
650a A*    01/02/13  LMC  6.50a
     A*      - Enh: Added selection for Vendor#/Customer#
650b A*    03/31/14  LMC  6.50b
     A*      - Enh: Added selection for Equipment.
     A*      Renamed display file from HA30001 to HA30002
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20120426  173127  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            H1ITEM        15A  H
     A            H1LCNS        15A  H
     A            H1SV92         1A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N92                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  92                                  DSPATR(PR)
     A            W1INFO        76A  O  8  5
     A N92                                  DSPATR(HI)
640bDA**          TTPMEM    R        O  8 50REFFLD(TTPREC/TTPMEM *LIBL/TTSTP)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20120507  095539  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0012)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View HACCP Question/Answers'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 10
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A            SUBHEAD       76A  O  7  5DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A*%%TS  SD  20120424  153951  LYNNM       REL-V6R1M0  5761-WDS
     A                                 22  2'F3=Exit  F5=Refresh  F9=Select    -
     A                                                       F12=Cancel  F21=Pr-
     A                                      int'
     A                                      COLOR(BLU)
     A          R MKEY1
     A                                      OVERLAY
     A            W1MSG1        75A  O 24  3
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A          R WINDOW1
     A*%%TS  SD  20120508  074810  LYNNM       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                      CA04
     A                                  1 15'Selection Criteria'
     A                                      DSPATR(HI)
     A                                  4  1'Warehouse  . .'
     A            W1WHSE         3Y 0B  4 16DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5  1'PO#. . . . . .'
     A            W1PO           9A  B  5 16DSPATR(HI)
650bAA                                 13  1'Equipment. . .'
650bAA            W1EQ           8A  B 13 16DSPATR(HI)
650bAA                                 13 37'*ALL'
     A                                 15  1'F3=Exit  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A            W1RTE          5A  B  6 16DSPATR(HI)
     A                                 10  1'Date Range . .'
     A                                  5 37'*ALL'
     A            W1FDTE         6Y 0B 10 16DSPATR(HI)
     A                                 10 23'-'
     A            W1TDTE         6Y 0B 10 25DSPATR(HI)
     A                                 10 38'0=All'
     A                                  6  1'Route Id . . .'
     A                                  6 37'*ALL'
     A                                  7  1'Item . . . . .'
     A            W1ITEM        15A  B  7 16DSPATR(HI)
     A                                  7 37'*ALL'
     A                                 12  1'Other Type . .'
     A            W1LEVL         4A  B 12 16DSPATR(HI)
     A            W1TYPE         3A  B 12 23DSPATR(HI)
     A                                 12 38'F4'
     A                                  8  1'Answer . . . .'
     A            W1ANSWER      30A  B  8 16DSPATR(HI)
650aAA                                  9  1'Vendor#/Cust#.'
650aAA            W1HLCUST      11Y 0B  9 16DSPATR(HI)
650aAA                                      EDTCDE(Z)
     A                                 11  1'Warning Ovr. .'
     A            W1WARN         1A  B 11 16DSPATR(HI)
     A          R WINDOWP
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20111129  103247  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
