     A*%%TS  SD  20060605  123953  ROCKY       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IV212FM   Inventory sheet maintenance - Display
     A*  23 January, 1996
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
409 AA*  07/15/98  RH   4.09
     A*    - Add CMDLN1
414bAA*  05/26/00  RH   4.14b
     A*    - Add F9=License info
414cAA*  08/30/00  RRH  4.14c
     A*    - Add slot quantity fields to check if item can be changed.
416aAA*  12/15/01  DAS  4.16a
     A*    - Added hidden field IVSASL.
416bAA*  05/15/02  RH   4.16b
     A*    - Added F10 to show entire sheet as entered.
500aAA*  04/27/05  RH   5.00a
     A*    - Added slot position
500bAA*  06/05/06  RH   5.00b
     A*    - Added option to call CW entry program
510aAA*  12/20/06  RH   5.10a
     A*    - Add F10=Item/page info
510bAA*  08/28/07  RH   5.10b
     A*    - Add CURSOR to SFLRCDNBR to position to slot passed in.
610aAA*  09/18/09  RH   6.10a
     A*    - Rename IV212FM to IV21201, add option 3=License.
610bAA*  10/05/10  RH   6.10b
     A*    - Rename IV21201 to IV21202, remove Position field.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20060605  123953  ROCKY       REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
500b A            OPTION         1A  B  6  2
500b A  21                                  DSPATR(PC)
500b A N21N90                               DSPATR(UL)
500b A  21                                  DSPATR(RI)
500b A  90                                  DSPATR(PR)
     A            IVSDSP    R        O  6  5REFFLD(IVSREC/IVSDSP *LIBL/INVSLOT)
     A            IVSASL    R        H      REFFLD(IVSREC/IVSASL *LIBL/INVSLOT)
     A            IVSLOC    R        H      REFFLD(IVSREC/IVSLOC *LIBL/INVSLOT)
     A            IVSLVL    R        H      REFFLD(IVSREC/IVSLVL *LIBL/INVSLOT)
     A            IVSHND    R        H      REFFLD(IVSREC/IVSHND *LIBL/INVSLOT)
     A            IVSITM    R        H      REFFLD(IVSREC/IVSITM *LIBL/INVSLOT)
     A            W1ALC1    R        H      REFFLD(SLREC/SLALC1 *LIBL/SLOT2)
     A            W1ALC2    R        H      REFFLD(SLREC/SLALC2 *LIBL/SLOT2)
     A            W1ALC3    R        H      REFFLD(SLREC/SLALC3 *LIBL/SLOT2)
     A            W1TFR1    R        H      REFFLD(SLREC/SLTFR1 *LIBL/SLOT2)
     A            W1TFR2    R        H      REFFLD(SLREC/SLTFR2 *LIBL/SLOT2)
     A            W1TFR3    R        H      REFFLD(SLREC/SLTFR3 *LIBL/SLOT2)
     A            W1PCK1    R        H      REFFLD(SLREC/SLPCK1 *LIBL/SLOT2)
     A            W1PCK2    R        H      REFFLD(SLREC/SLPCK2 *LIBL/SLOT2)
     A            W1PCK3    R        H      REFFLD(SLREC/SLPCK3 *LIBL/SLOT2)
     A            W1RCV1    R        H      REFFLD(SLREC/SLRCV1 *LIBL/SLOT2)
     A            W1RCV2    R        H      REFFLD(SLREC/SLRCV2 *LIBL/SLOT2)
     A            W1RCV3    R        H      REFFLD(SLREC/SLRCV3 *LIBL/SLOT2)
     A            W1SQT1         5Y 0B  6 61TEXT('Normal Qty')
     A                                      EDTCDE(L)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A N37N56
     AO 41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            W1SQT2         3Y 0B  6 70EDTCDE(L)
     A N38                                  DSPATR(PR)
     A  38                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A            W1SQT3         3Y 0B  6 76EDTCDE(L)
     A N38                                  DSPATR(PR)
     A  38                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
610bDA*           W1DESC        15A  O  6 36
610bMA            W1DESC        20A  O  6 36
610bDA*           W1MULT         1A  O  6 53
610bDA*           W1SPOS         3Y 0B  6 55EDTCDE(L)
610bDA* 44                                  DSPATR(PR)
610bDA*N44                                  DSPATR(UL)
510aDA* 90                                  DSPATR(PR)
510aDA*N90                                  DSPATR(UL)
610bDA* 46                                  DSPATR(RI)
610bDA*N37N56
610bDA* 46                                  DSPATR(PC)
     A            W1SITM        15A  B  6 19
     A  37N56
     AO 37N55N41
     A N42N43N56
     AO 40                                  DSPATR(PC)
     A N37                                  DSPATR(PR)
     A  37                                  DSPATR(UL)
     A  40                                  DSPATR(RI)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20060605  123953  ROCKY       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0015)
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
510a A                                      CF10
     A                                      OVERLAY
     A                                      SFLCSRRRN(&@REC)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
510bDA**          RECNO          4S 0H      SFLRCDNBR
510bMA            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A            @REC           5S 0H
     A                                  1  2'IV212.01'
     A                                  1 28'Inventory Sheet Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Sheet # -'
     A            $PSPAG         5Y 0O  3 12EDTCDE(4)
     A                                  3 28'Warehouse -'
     A                                  3 50'Dept -'
     A            $PSWHS         3Y 0O  3 40EDTCDE(4)
     A            $PSWHD         5A  O  3 57
     A                                  4 60'Normal'
     A                                      DSPATR(HI)
     A                                  4 70'Breakdown'
     A                                      DSPATR(HI)
     A                                  5  5'Slot #      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 19'Item #         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
610bDA*                                 5 36'Description    '
610bMA                                  5 36'Description         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
610bDA*                                 5 53'M POS'
610bDA*                                     DSPATR(HI)
610bDA*                                     DSPATR(UL)
     A                                  5 60'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 69'Qty 1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 75'Qty 2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2'OP'
610aMA                                  4  2'1=CW Entry  3=License'
610aDA*                                 4  2'1=CW Entry'
     A*****************************************************************
     A          R SCREEN2
     A*%%TS  SD  19960201  153030  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
416bAA                                      CF10
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'IV212.02'
     A                                  1 28'Inventory Sheet Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  6  6'Sheet # -'
     A            $PSPAG         5Y 0O  6 16EDTCDE(4)
     A                                  7  4'Warehouse -'
     A                                  8  3'Department -'
     A            $PSWHS         3Y 0O  7 16EDTCDE(4)
     A            $PSWHD         5A  O  8 16
     A                                 12  6'Total Quantity . . . . . . . . .'
     A                                 13  6'Total Breakdown Quantity 1 . . .'
     A                                 14  6'Total Breakdown Quantity 2 . . .'
     A            W2TQT1         7Y 0O 12 39EDTCDE(J)
     A            W2TQT2         5Y 0O 13 39EDTCDE(J)
     A            W2TQT3         5Y 0O 14 39EDTCDE(J)
416b A                                 22  3'F10=Entire Sheet  F12=Cancel'
416b A*                                22  3'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A*%%TS  SD  19960213  112113  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79A  O  1  2COLOR(BLU)
     A            CMDLN2        79A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
