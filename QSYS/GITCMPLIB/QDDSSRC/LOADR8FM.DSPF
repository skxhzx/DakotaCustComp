     A*%%TS  SD  20061108  084632  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOADR8FM   Route Summary History - Display - Loader Module
     A*  November 11, 2006
     A*  Jose Duque
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A                                      CA21
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R TITLE
     A*%%TS  SD  20061103  101220  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'LOADR8FM.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 36'List Display'
     A                                      DSPATR(HI)
     A                                  1 25'Route Summary History - View/Print'
     A                                      DSPATR(HI)
     A*
     A*
     A* POSITION TO RECORD
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R POSTNREC
     A*%%TS  SD  20061103  101052  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A*>          PTDMY1        78A  O  1  2
     A*
     A                                  1 28'From:'
     A            WFDATE         6Y 0B  1 34EDTCDE(Y)
     A*
     A                                  1 44'To:'
     A            WTDATE         6Y 0B  1 48EDTCDE(Y)
     A*
     A            LHHWHS         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A*
     A                                  3 43'Change to warehouse . . .'
     A            PTINP1         3Y 0B  3 70EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A*
     A                                  4 43'Position to start date. .'
     A            PTINP2         6Y 0B  4 70EDTCDE(Y)
     A  02                                  DSPATR(PC)
     A*
     A N18                              5 30'NO DATA FOUND'
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A*
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20061108  084632  JOSED       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1DATE         6Y 0O 11  3EDTCDE(Y)
     A*
     A            W1RTE#    R        O 11 13REFFLD(RHRTE RTEHED)
     A*
     A            W1STRT         6Y 0O 11 20EDTWRD('0 :  :  ')
     A*
     A            W1ENDT    R        O 11 29REFFLD(RHPRTT RTEHED)
     A                                      EDTWRD('0 :  :  ')
     A*
     A            W1TDIF         6Y 0O 11 38EDTWRD('0 :  :  ')
     A*
     A            W1USER    R        O 11 58REFFLD(TRUSER PIRTRCK)
     A*
     A            W1CLOS    R        O 11 48REFFLD(RHPRTT RTEHED)
     A                                      EDTWRD('0 :  :  ')
     A            W1SUPV        10A  O 11 69
     A*----------------------------------------------------------------
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20061108  084632  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10 20'Started '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Ended   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Closed  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Rte #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Loader    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69'Supervisor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 38'Load'
     A                                      DSPATR(HI)
     A                                 10 38'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  3'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A* ERROR MESSAGE SUBFILE RECORD
     A*----------------------------------------------------------------
     A          R MSGREC                    SFL
     A*----------------------------------------------------------------
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*
     A*
     A* ERROR MESSAGE SUBFILE CONTROL RECORD
     A*----------------------------------------------------------------
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*----------------------------------------------------------------
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*
     A* COMMAND LINES
     A*----------------------------------------------------------------
     A          R CMDREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*
     A* 'MORE.../BOTTOM' MESSAGE LINE
     A*----------------------------------------------------------------
     A          R MORREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*
     A*
     A* CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*----------------------------------------------------------------
     A          R CLR0306
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
