      *%%TS  SD  20061219  091350  JEFF        REL-V5R3M0  5722-WDS
      ********************************************************************
      *   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
      *   BFC Software, Inc.                                             *
      *   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
      *   West Chicago, IL  60185                                        *
      *   (630) 562-0375                                                 *
      *----------------------------------------------------------------
      *
      *  CKR110FM   Check Right - Options
      *  22 June 2009
      *  Jeffrey Jahnke
      *
      *----------------------------------------------------------------
      *  Revisions
      *
      *    06/22/09  JCJ
      *      - Created. Copied from TAX110FM
      *
      *----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20061217  155032  JEFF        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'CKR110.01'
     A                                  1 33'Check Right Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  3 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  3 75EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Options'
     A                                      DSPATR(HI)
     A                                  9  5'Days to keep export data  . . .'
     A            W1TXHS         3Y 0B  9 38EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 59'1 - 999 days'
     A                                 11  5'Check Right file Processing . .'
     A            W1PCMD         8   B 11 38
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 59'*CREATE, *SEND, *NONE'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
