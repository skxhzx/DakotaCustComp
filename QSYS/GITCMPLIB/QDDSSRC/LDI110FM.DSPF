     A*%%TS  SD  20000224  150042  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LDI110FM   Import items from Layout Designer - Options
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
413 AA*    02/24/00  DAS  4.13
     A*      - Change descriptive text to say that we now add items.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000224  150042  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LDI110.01'
     A                                  1 21'Add/Update Items in PIRITEM from L-
     A                                      AYITEM'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Y=Yes, N=No'
     A                                 11 14'This program will add/update items-
     A                                       in the PIRITEM file from'
     A                                 12 14'the Layout Designer item import fi-
     A                                      le, LAYITEM. If an item'
     A                                 13 14'does not exist it will be created -
     A                                      as a Normal item type.'
     A                                 15 14'The following information will be -
     A                                      updated: Department,'
     A                                 16 14'Section, Activity Zone, Designatio-
     A                                      n Code, Dimensions,'
     A                                 19 16'** This Program Does Not Create An-
     A                                      y Table Codes **'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A                                 17 14'Weight and Cube.'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
