     A*%%TS  SD  19990902  160054  HEMANT      REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  SL352FM   Unverified Zero Slots with Virtual Slot info - View
     A*  02 September, 1999
     A*  Hemant Kapadia
     A*
     A*  Revisions:
     A* 12/01/04  RH   5.00
     A*  - Changed to use SL351A work file and display zero slot with
     A*    related slots.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
500 DA*                                 1 20'Unverified Zero Slots with Virtual-
500 MA                                  1 20'Unverified Zero Slots with Related-
     A                                       Slots Info'
     A                                      DSPATR(HI)
     A                                  1  2'SL352'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A                                  1 41'Change to warehouse   .'
     A            PTINP1         3Y 0B  1 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WDCODE         5A  O  2  2
     A                                  2  8'-'
     A            WDDESC        30A  O  2 10
     A                                  2 41'Change to department  .'
     A            PTINP2         5A  B  2 66
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A            SECTN          5A  O  3  2
     A                                  3 41'Change to section . . .'
     A            PTINP3         1A  B  3 66
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A                                  5 41'Position to slot  . . .'
     A            PTINP5        12A  B  5 66
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990902  160054  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            W1DISP        12A  O 10  3
     A            W1STAT         2A  O 10 17
     A            W1ITEM        15A  O 10 35
     A            W1DESC        15A  O 10 52
     A            W1QTY         10A  O 10 70
     A            W1VDSP        12   O 10 21
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990902  160054  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9 35'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9  3'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 16'Stat'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 52'Description    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 70'Avl Qty   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 DA*                                 9 21'Virtual Slot'
500 MA                                  9 21'Related Slot'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
