     A*%%TS  SD  19941128  161928  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*
     A*  OP110FM   PIR options - Picking
     A*  22 August 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941128  161928  DAVE        REL-V2R1M1  5738-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP110.01'
     A                                  1 33'Picking Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'General options'
     A                                      DSPATR(HI)
     A                                  9  5'Next pallet number  . . . . . .'
     A                                      DSPATR(ND)
     A            W1NXTP         7Y 0B  9 38EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  8  5'Bulk pick definition  . . . . .'
     A            W1BPCK         2Y 0B  8 38EDTCDE(3)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 41'% of full pallet'
     A                                 11  3'PIR options'
     A                                      DSPATR(HI)
     A                                 12  5'Maximum cube  . . . . . . . . .'
     A            W1PCUB         5Y 2B 12 38
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 14  3'Fixed options'
     A                                      DSPATR(HI)
     A                                 15  5'Pallets on fork lift  . . . . .'
     A            W1FPAL         1Y 0B 15 38EDTCDE(3)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  5'Maximum cube per pallet . . . .'
     A            W1FCUB         5Y 2B 16 38
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N28N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1FSTP         2Y 0B 17 38
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N29N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 18  5'Include PIR items in cube . . .'
     A            W1INCL         1A  B 18 38
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 19  5'Previous pallets to search  . .'
     A            W1SRCH         2Y 0B 19 38EDTCDE(3)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  5'Maximum stops per pallet  . . .'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
