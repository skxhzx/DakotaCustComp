     A*%%TS  SD  20130429  105358  KENNETHE    REL-V6R1M0  5761-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A********************************************************************
     A*
415 AA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS IF NECESSARY.
     A*
     A* *******************************************
     A* *****  RSTDSP value must be set *YES  *****
     A* *******************************************
     A*
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  SL630FM      SLOT INFO
     A*
     A********************************************************************
     A*
     A*
     A*  Revisions
412 AA*    09/07/99  RH   4.12
     A*      - Added "Virtual Slot" to screen 1 if SLRSRV = V
     A*
413 AA*    11/26/99  DAS  4.13
     A*     - Added SSB field to screen1
     A*
415 AA*    05/16/01  DAS  4.15
     A*     - Created SCREEN61, a 16-line version of SCREEN1.
     A*     - Created message screens for 16-line displays. They all
     A*       start with M16.
     A*     - Added FRCDTA to screen CLRSCR to force it to clear the
     A*       screen.
     A*     - Removed OVRLAY from SCREEN1 and SCREEN61.
     A*     - Revised to use CLRL(*ALL) instead of a number.
     A*     - Added F10=Adj to SCREEN1. Chopped desc for F8/F9.
     A*
416aAA*    05/25/02  MLB  4.16a
     A*     - Revised SCREEN 1, SCREEN 61 to not display F10/F11
     A*       when called from Will Call Transfers.
417aAA*    02/11/04  RH   4.17a
     A*     - Add tie hi to screen SL630.01
     A*
500 AA*   06/04/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
510aAA*    06/10/06  DAS  5.10a
     A*     - Added F7=Dates to screens
     A*
510bAA*    09/11/06  JCJ  5.10b
     A*     - Revised SCREEN61 to only clear 16 lines.
     A*
600 AA*    04/28/08  DAS  6.00  P#00046
     A*     - License Tracking
     A*
600aAA*    04/28/08  DAS  6.00a P#00046
     A*     - Increased size of w1sdsp from 20 to 25, to hold slot pos.
     A*     - Added slot pos to screen2.
     A*     - These were 500 changes at Jordano's.
650a A*    04/24/13  GJA  6.50a
     A*     - Enh:  Add slot type-virtual, pick, repack or overflow
650c A*    03/19/14  RH   6.50c
     A*     - Enh:  Add F11 Tfr to Screen1,rename format to SL63003
700a A*    05/20/16  LMC  7.00a
     A*     - Enh:  Add Brand to screen1,rename format to SL63004
740a A*    08/21/20  LMC  7.40a
     A*     - Enh:  Add License to screen1 & 61
740b A*    09/01/20  LMC  7.40b
     A*     - Enh:  Add License ent and exp dates to screen1 & 61
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19991126  055831  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
510aAA N52                                  CF07
     A                                      CF08
     A                                      CF09
     A N51                                  CF10
650cAA N51                                  CF11
510bD**                                     CLRL(*ALL)
415 AA                                      OVERLAY
     A                                  1  2'SL630.01'
     A                                  1 12'Slot Info'
650aAA            W1STYP         8A     2  1
650aDA*                                 2  1'Virtual Slot'
650aDA* 21                                  DSPATR(ND)
600aDA**          W1SDSP        20A  O  3  1
600aMA            W1SDSP        25A  O  3  1
     A                                  4  1'Status:'
     A            W1SLST         2A  O  4  9
     A                                  4 14'Def:'
     A            W1SDEF         2A  O  4 19
417a A                                  4 24'Ti/Hi:'
417a A            W1TIE          3Y 0O  4 31EDTCDE(4)
417a A                                  4 35'/'
417a A            W1HIGH         3Y 0O  4 37EDTCDE(4)
     A            W1ITEM        65A  O  5  1
     A                                  6  1'Qty:'
     A            W1QTY         10A  O  6  6
415 DA**                                7  1'F8=Slots F9=View Qty'
416aDA**                                7  1'F8=Slt F9=Qty F10=Adj'
416aMA                                  7  1'F8=Slt F9=Qty'
     A                                      COLOR(BLU)
     A                                  7 15'F10=Adj'
416aAA  51                                  DSPATR(ND)
416aAA                                      COLOR(BLU)
650c A                                  7 23'F11=Tfr'
650cAA  51                                  DSPATR(ND)
650cAA                                      COLOR(BLU)
     A            WOSSB          3   O  6 18DSPATR(HI)
650cDA**                                7 23'F7=Dates'
650cMA                                  7 32'F7=Dates'
510aAA  52                                  DSPATR(ND)
510aAA                                      COLOR(BLU)
700aAA  53                              6 22'Brand:'
700aAA  53        W1BRD         10   O  6 29
740aAA                                  6 40'License:'
740bDA*           W1LCNS        10   O  6 49
740bMA            W1LCNS        15   O  6 49
740bAA                                  4 41'Ent:'
740bAA            W1ENTD         6Y 0O  4 46EDTWRD('  /  /  ')
740bAA                                  4 55'Exp:'
740bAA            W1EXPD         6Y 0O  4 60EDTWRD('  /  /  ')
     A          R SCREEN2
     A*%%TS  SD  19960531  143738  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL630.01A'
     A                                  1 30'Item Slot Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Warehouse :'
     A            W1WHSE        40A  O  3 14
     A                                  4  2'Department:'
     A            W1WHDP         5A  O  4 14
     A                                  5  2'Section ..:'
     A            W1SECT         5A  O  5 14
     A                                  6  2'Slot .....:'
     A            W1DISP        12A  O  6 14
600aAA            W2SLP          8A  O  6 27
     A                                  7  2'Item .....:'
     A            W1ITEM        65A  O  7 14
     A                                  9  2'Pick slot ............:'
     A            W1PICK         3A  O  9 26
     A                                 10  2'Reserved slot ........:'
     A            W1RSRV         3A  O 10 26
     A                                 12  2'Slot status ..........:'
     A            W1SLST         2A  O 12 26
     A            W1SLDS        30A  O 12 31
     A                                 13  2'Slot date/time .......:'
     A            W1SSDT         6Y 0O 13 26EDTWRD('  /  / 0')
     A            W1STIM         6Y 0O 13 36EDTWRD('  :  :  ')
     A                                 14  2'Date item entered slot:'
     A            W1SDTE         6Y 0O 14 26EDTWRD('  /  / 0')
     A                                 15  2'Expiration date ......:'
     A            W1EXDT         6Y 0O 15 26EDTWRD('  /  / 0')
     A                                 17  2'Slot definition ......:'
     A            W1SDEF         2A  O 17 26
     A            W1SDES        30A  O 17 30
     A                                 18  2'Slot tie / high ......:'
     A            W1TIE          4Y 0O 18 26EDTCDE(4)
     A                                 18 31'/'
     A            W1HIGH         2Y 0O 18 34EDTCDE(4)
     A                                 18 38'Preferred:'
     A            W1PFER         3A  O 18 50
     A                                 19  2'Designation ..........:'
     A            W1DESG         5A  O 19 26
     A            W1DESD        30A  O 19 32
     A                                 20  2'Activity zone ........:'
     A            W1ACTV         2A  O 20 26
     A            W1DESA        30A  O 20 30
     A                                 21  2'Build code ...........:'
     A            W1BLD          2A  O 21 26
     A            W1DESB        30A  O 21 30
     A                                 23  2'F5=Refresh  F9=View slot quantitie-
     A                                      s  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN61
     A*%%TS  SD  20130429  105358  KENNETHE    REL-V6R1M0  5761-WDS
     A N52                                  CF07
     A                                      CF08
     A                                      CF09
     A N51                                  CF10
     A N51                                  CF11
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'SL630.61'
     A                                  1 12'Slot Info'
     A            W1STYP         8A  O  2  1
650aDA**                                2  1'Virtual Slot'
650aDA**21                                  DSPATR(ND)
600aDA**          W1SDSP        20A  O  3  1
     A            W1SDSP        25A  O  3  1
     A                                  5  1'Status:'
     A            W1SLST         2A  O  5  9
     A                                  5 14'Def:'
     A            W1SDEF         2A  O  5 19
     A            W1ITEM        65A  O  7  1
     A            W1DESC        30A  O  8  1
     A            WOSSB          3A  O  9  1DSPATR(HI)
     A                                 10  1'Phy Qty:'
700aAA  53                              9  5'Brand:'
700aAA  53        W1BRD         10   O  9 12
740aAA                                  9 23'License:'
740bDA*           W1LCNS        10   O  9 32
740bMA            W1LCNS        15   O  9 32
740bAA                                  9 48'Ent:'
740bAA            W1ENTD         6Y 0O  9 53EDTWRD('  /  /  ')
740bAA                                  9 62'Exp:'
740bAA            W1EXPD         6Y 0O  9 67EDTWRD('  /  /  ')
     A            W1PHYQ        10A  O 10 10
     A                                 11  1'Avl Qty:'
     A            W1AVLQ        10A  O 11 10
     A                                 12  1'Whs Qty:'
     A            W1WHSQ        10A  O 12 10
     A                                 14  1'F8=Slots F9=View Qty'
     A                                      COLOR(BLU)
     A                                 15  1'F10=Adj  F11=Transfer'
     A  51                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A                                 14 23'F7=Dates'
     A  52                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A                                 11 23'(includes all open transactions)'
     A                                 10 23'(Stock minus Voice/Runner picked q-
     A                                      uantity)'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20010516  112712  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20010516  112712  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A*%%TS  SD  20010516  112712  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(01)
     A                                      OVERLAY
     A          R CLRSCR
     A*%%TS  SD  20010604  155910  DAVE        REL-V4R4M0  5769-PW1
     A                                      CLRL(*ALL)
     A                                      FRCDTA
     A          R CLRSC2
     A                                      CLRL(*ALL)
415 AA*****************************************************************
415 AA*
415 AA* Error Message Subfile Records for 16 line screens
415 AA*
415 AA*    Error Message Subfile Record
415 AA*
415 AA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415 AA*
415 AA*    Error Message Subfile Control Record
415 AA*
415 AA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415 AA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19991126  055831  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
510aAA N52                                  CF07
     A                                      CF08
     A                                      CF09
416aMA N51                                  CF10
650cAA N51                                  CF11
510bD**                                     CLRL(*ALL)
510bMA                                      CLRL(15)
415 DA**                                    OVERLAY
     A                                  1  2'SL630.01'
     A                                  1 12'Info de slot'
     A                                  2  1'Slot virtual'
     A  21                                  DSPATR(ND)
600aDA**          W1SDSP        20A  O  3  1
600aMA            W1SDSP        25A  O  3  1
     A                                  4  1'Estado:'
     A            W1SLST         2A  O  4  9
     A                                  4 14'Def:'
     A            W1SDEF         2A  O  4 19
417a A                                  4 24'Ti/Hi:'
417a A            W1TIE          3Y 0O  4 31EDTCDE(4)
417a A                                  4 35'/'
417a A            W1HIGH         3Y 0O  4 37EDTCDE(4)
     A            W1ITEM        65A  O  5  1
     A                                  6  1'Ctd:'
     A            W1QTY         10A  O  6  6
415 DA**                                7  1'F8=Slots F9=View Qty'
416aDA**                                7  1'F8=Slt F9=Qty F10=Adj'
416aMA                                  7  1'F8=Slt F9=Ctd'
     A                                      COLOR(BLU)
416aAA                                  7 15'F10=Aju'
416aAA  51                                  DSPATR(ND)
416aAA                                      COLOR(BLU)
650cAA                                  7 23'F11=Tfr'
650cAA  51                                  DSPATR(ND)
650cAA                                      COLOR(BLU)
     A            WOSSB          3   O  6 18DSPATR(HI)
700aAA  53                              6 22'Marca:'
700aAA  53        W1BRD         10   O  6 29
740aAA                                  6 40'License:'
740bMA            W1LCNS        15   O  6 49
740bAA                                  4 41'Ent:'
740bAA            W1ENTD         6Y 0O  4 46EDTWRD('  /  /  ')
740bAA                                  4 55'Exp:'
740bAA            W1EXPD         6Y 0O  4 60EDTWRD('  /  /  ')
650cDA**                                7 23'F7=Dates'
650cMA                                  7 32'F7=Dates'
510aAA  52                                  DSPATR(ND)
510aAA                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  19960531  143738  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL630.01A'
     A                                  1 30'Item Slot Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Almacen ..:'
     A            W1WHSE        40A  O  3 14
     A                                  4  2'Dept .....:'
     A            W1WHDP         5A  O  4 14
     A                                  5  2'Seccion ..:'
     A            W1SECT         5A  O  5 14
     A                                  6  2'Slot .....:'
     A            W1DISP        12A  O  6 14
600aAA            W2SLP          8A  O  6 27
     A                                  7  2'Producto .:'
     A            W1ITEM        65A  O  7 14
     A                                  9  2'Pick slot ............:'
     A            W1PICK         3A  O  9 26
     A                                 10  2'Slot reservado .......:'
     A            W1RSRV         3A  O 10 26
     A                                 12  2'Estado de slot........:'
     A            W1SLST         2A  O 12 26
     A            W1SLDS        30A  O 12 31
     A                                 13  2'Fech/hora de slot ....:'
     A            W1SSDT         6Y 0O 13 26EDTWRD('  /  / 0')
     A            W1STIM         6Y 0O 13 36EDTWRD('  :  :  ')
     A                                 14  2'Fecha producto incorp.:'
     A            W1SDTE         6Y 0O 14 26EDTWRD('  /  / 0')
     A                                 15  2'Fecha de expiracion ..:'
     A            W1EXDT         6Y 0O 15 26EDTWRD('  /  / 0')
     A                                 17  2'Definicion de slot ..:'
     A            W1SDEF         2A  O 17 26
     A            W1SDES        30A  O 17 30
     A                                 18  2'Tie/high de producto.:'
     A            W1TIE          4Y 0O 18 26EDTCDE(4)
     A                                 18 31'/'
     A            W1HIGH         2Y 0O 18 34EDTCDE(4)
     A                                 18 38'Preferido:'
     A            W1PFER         3A  O 18 50
     A                                 19  2'Designacion ..........:'
     A            W1DESG         5A  O 19 26
     A            W1DESD        30A  O 19 32
     A                                 20  2'Zona de accion .......:'
     A            W1ACTV         2A  O 20 26
     A            W1DESA        30A  O 20 30
     A                                 21  2'Codigo de construir ..:'
     A            W1BLD          2A  O 21 26
     A            W1DESB        30A  O 21 30
     A                                 23  2'F5=Refrescar  F9=Ver cantidades-
     A                                      de slot  F12=Cancelar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN61SP
     A*%%TS  SD  20010614  131346  DAVE        REL-V4R4M0  5769-PW1
510aAA N52                                  CF07
     A                                      CF08
     A                                      CF09
416aMA N51                                  CF10
416aMA N51                                  CF11
     A  98                                  ALARM
     A                                      CLRL(*ALL)
     A                                  1  2'SL630.61'
     A                                  1 12'Info de slot'
     A                                  2  1'Slot virtual'
     A  21                                  DSPATR(ND)
600aDA**          W1SDSP        20A  O  3  1
600aMA            W1SDSP        25A  O  3  1
     A                                  5  1'Estado:'
     A            W1SLST         2A  O  5  9
     A                                  5 14'Def:'
     A            W1SDEF         2A  O  5 19
     A            W1ITEM        65A  O  7  1
     A            W1DESC        30A  O  8  1
     A            WOSSB          3A  O  9  1DSPATR(HI)
700aAA  53                              9  5'Marca:'
700aAA  53        W1BRD         10   O  9 12
740aAA                                  9 23'License:'
740bMA            W1LCNS        15   O  9 32
740bAA                                  9 48'Ent:'
740bAA            W1ENTD         6Y 0O  9 53EDTWRD('  /  /  ')
740bAA                                  9 62'Exp:'
740bAA            W1EXPD         6Y 0O  9 67EDTWRD('  /  /  ')
     A                                 10  1'Avl Qty:'
     A            W1AVLQ        10A  O 10 10
     A                                 10 23'(incluye transacciones abiertas)'
     A                                 11  1'Adj Qty:'
     A            W1ADJQ        10   O 11 10
     A                                 11 23'(excluye transacciones abiertas de-
     A                                      recibir)'
     A                                 12  1'Alm Ctd:'
     A            W1WHSQ        10A  O 12 10
     A                                 14  1'F8=Slots F9=Ver Ctd'
     A                                      COLOR(BLU)
     A                                 15  1'F10=Aju  F11=Transferir'
416aAA  51                                  DSPATR(ND)
     A                                      COLOR(BLU)
510aAA                                 14 23'F7=Dates'
510aAA  52                                  DSPATR(ND)
510aAA                                      COLOR(BLU)
