     A*%%TS  SD  20030403  112143  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  DSPDTAQINF  Display data queue information
     A*  03 April 2003
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    04/03/03  DAS  4.17
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20030403  112143  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'DSPDTAQINF'
     A                                  1 26'Display Data Queue Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Data queue name/lib . . .'
     A            W1DTAQ        10   O  5 31
     A                                  5 42'/'
     A            W1LIB         10   O  5 44
     A                                  7  6'Bytes returned  . . .'
     A            WQBRTN         3  0O  7 29
     A                                  8  6'Bytes available . . .'
     A            WQBAVL         3  0O  8 29
     A                                  9  6'Message length  . . .'
     A            WQMLEN         4  0O  9 29
     A                                 10  6'Key length  . . . . .'
     A            WQKLEN         4  0O 10 29
     A                                 11  6'Sequence  . . . . . .'
     A            WQSEQ          1   O 11 29
     A                                 12  6'Sender Id flag  . . .'
     A            WQSFLG         1   O 12 29
     A                                 13  6'Force flag  . . . . .'
     A            WQFFLG         1   O 13 29
     A                                 14  6'Description . . . . .'
     A            WQDESC        50   O 14 29
     A                                 15  6'Data queue type . . .'
     A            WQTYPE         1   O 15 29
     A                                 16  6'Reclaim storage flag '
     A            WQRFLG         1   O 16 29
     A                                 17  6'Number of messages  .'
     A            WQ#MSG         4  0O 17 29
     A                                 18  6'Entries allocated . .'
     A            WQEALC         4  0O 18 29
     A                                 19  6'Data queue  . . . . .'
     A            WQNAME        10   O 19 29
     A                                 19 40'/'
     A            WQLIB         10   O 19 42
     A                                 20  6'Max entries allowed .'
     A            WQMAX          9  0O 20 29
     A                                 21  6'Initial entries . . .'
     A            WQINIT         9  0O 21 29
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
