     A****************************************************************
     A*
     A* HQ111PR   Pick slot qty derived from host qty.
     A* 04 March, 2002
     A* Hemant Kapadia
     A*
     A* 03/04/00  HNK
     A*  - Created - Custom for Allen
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3EDTCDE(Y)
     A                                    52
     A                                      DFT('Pick Slot Qty')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('HQ111')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    74
     A                                      DFT('Overflow')
     A                                    85
     A                                      DFT('Pick Slot')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Pick Slot')
     A                                      UNDERLINE
     A                                    15
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    32
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    63
     A                                      DFT('Host Qty')
     A*
     A                                    74
     A                                      DFT('Quantity')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('Quantity')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail L1 line.
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            T#DISP        12A  O     1
     A            T#ITEM        15A  O    15
     A            ITDESC    R        O    32REFFLD(ITDESC PIRITEM)
     A            IJQYOH    R        O    63REFFLD(IJQYOH IMPFITMO)
     A                                      EDTCDE(Z)
     A            OFQTY1         7S 0O    74EDTCDE(Z)
     A            PCKQTY         7S 0O    85EDTCDE(L)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('HQ111: End of Report ')
     A*
