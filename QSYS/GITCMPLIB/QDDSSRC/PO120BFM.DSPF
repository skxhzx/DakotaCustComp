     A*%%TS  SD  20000924  224540  KENNETH     REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO120BFM   PO detail maint. - Display screen
     A*  26 June, 2000
     A*  Kenneth Elder
     A*
     A*  NOTE: This display file no longer used. Function added into
     A*        PO120FM. Part of Super P/O Mods. 4.16 MLB
     A*
     A*      - Created.
     A*      - Copy of PO120 to Display/Order by Mfg Code
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
414 AA*    10/12/00  DAS  4.14
     A*      - Added F23.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A                                      CF09
414 AA                                      CF23
     A          R TITLE
     A*%%TS  SD  19961008  154450  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'PO120B.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 28'PO Detail File Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20000626  202718  KENNETH     REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 50'to'
     A                                  1 53'Mfr.'
     A                                  1 58'Code.'
     A            W1MFR         15   B  1 65
     A                                  1 41'Position'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R SCANREC
     A*%%TS  SD  20000627  210749  KENNETH     REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(04)
     A                                  1  2'PO #   '
     A            $KPO           9A  O  1 10
     A            FLD002         1A  O  2  2
     A                                  2 41'Session total. . .'
     A            WOSRC         15Y 0O  2 60EDTCDE(2)
     A                                  1 41'UPC Code . . . . .'
     A            W1UPC         20A  B  1 60CHECK(ER)
     A  20                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000924  194416  KENNETH     REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  4
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PDITEM    R        O 11 23REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC         22A  O 11 39
     A            WOQOR          9A  O 11 62
     A            WOQRC          9A  O 11 72
     A            WOMFR         15A  O 11  7
     A            TOUCH          1A  O 11  2
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000924  192448  KENNETH     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  4'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10 23'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Ordered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Received'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  9'Mfr. Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R DSPSFL1                   SFL
     A*%%TS  SD  20000924  194416  KENNETH     REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  4
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PDITEM    R        O 11 23REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC1        22A  O 11 39
     A            WOQOR          9A  O 11 62
     A            WOQRC          9A  O 11 72
     A            WODSC2        22A  O 12 23
     A            WOPDSC        15A  O 12 47
     A            WOTIE          4Y 0O 12 64EDTCDE(4)
     A                                 12 69'/'
     A            WOHI           2Y 0O 12 71EDTCDE(4)
     A            WOMFR         15A  O 11  7
     A            TOUCH          1A  O 11  2
     A*
     A          R DSPSFC1                   SFLCTL(DSPSFL1)
     A*%%TS  SD  20000924  192448  KENNETH     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0005)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  3'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10 23'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Mfr. Code      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Ordered  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Received '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R VERIFY
     A*%%TS  SD  20000924  224540  KENNETH     REL-V4R2M0  5769-PW1
     A                                      WINDOW(10 20 5 20)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Are you sure you '
     A                                      COLOR(WHT)
     A                                  3  2'want to check as'
     A                                      COLOR(WHT)
     A                                  4  2'touched ?'
     A                                      COLOR(WHT)
     A            WOYES          1A  B  4 12COLOR(WHT)
     A                                      CHECK(ER)
