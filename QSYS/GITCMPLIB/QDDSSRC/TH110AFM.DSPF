     A*%%TS  SD  19961206  090117  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT110AFM   Item maintenance (one whse) - Display screen
     A*  09 July 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
409 AA*    05/27/98  HNK  4.09
     A*      - Position to item disabled.  Position to dept and section
     A*        added.
     A*
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19960502  104251  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'TH110A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 34'Item Tie High'
     A                                      DSPATR(HI)
     A            FLD001         1   O  2 38
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19961206  090117  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 40'Change to warehouse . .'
     A            PTINP1         3Y 0B  1 65EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
409 MA                                  2 40'Position to department.'
409 MA            PTINP2         5A  B  2 65
409 AA                                  3 40'Position to section . .'
409 AA            PTINP3         1A  B  3 65
409 DA*                                 2 40'Position to item  . . .'
409 DA*           PTINP2        15A  B  2 65
     A            FLD001         1A  O  4  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19961206  090117  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            THSDEF    R        O 11 42REFFLD(THREC/THSDEF *LIBL/TIEHIGH)
     A            THPAL     R        O 11 51REFFLD(THREC/THPAL *LIBL/TIEHIGH)
     A            THHANG    R        O 11 62REFFLD(THREC/THHANG *LIBL/TIEHIGH)
     A                                      EDTCDE(3)
     A            THTIE     R        O 11 71REFFLD(THREC/THTIE *LIBL/TIEHIGH)
     A                                      EDTCDE(3)
     A            THHIGH    R        O 11 77REFFLD(THREC/THHIGH *LIBL/TIEHIGH)
     A                                      EDTCDE(3)
     A            THWHDP    R        O 11 23REFFLD(THREC/THWHDP *LIBL/TIEHIGH)
     A            THSTYP    R        O 11 33REFFLD(THREC/THSTYP *LIBL/TIEHIGH)
     A            THITEM    R        O 11  6REFFLD(THREC/THITEM *LIBL/TIEHIGH)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19961206  090117  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 40'Slot Def.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'Pallet'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 60'Overhang'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'Tie'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'High'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 23'Dept.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Section'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
