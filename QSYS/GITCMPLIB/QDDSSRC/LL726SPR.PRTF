     A*******************************************************************
     A*  Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved    *
     A*  BFC Software, Inc.                                             *
     A*  (630) 562-0375                                                 *
     A*******************************************************************
     A*
     A* LL726SPR  Print Stage Export License Log Exception Report
     A* 20 June 2011
     A* Retha Davis
     A*
     A****************************************************************
     A* Revision
     A*
640aAA* 06/20/11  RBD  6.40
     A*   - Created.
     A*
     A****************************************************************
EFCaAA* Client Custom Revisions: Eastland
     A*
EFCaAA* 06/20/11  RBD  EFCa
     A*   - Custom created for Eastland.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            RDATE          6S 0O     2
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O    29
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   127
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            RTIME          6S 0O     2
     A                                      EDTWRD('  :  :  ')
     A            WHSE          40A  O    29
     A                                   125
     A                                      DFT('LL726S')
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                     2'Trans'
     A                                    10'TO License'
     A                                    26'TO License'
     A                                    38'FROM License'
     A                                    54'FROM License'
     A                                    69'-TO Order-'
     A                                   112'Normal'
     A                                   119'Bkdn1'
     A                                   125'Bkdn2'
     A                                      SPACEA(001)
     A                                     2'Number'
     A                                    12'Number'
     A                                    28'Trans/Sq'
     A                                    40'Number'
     A                                    56'Trans/Sq'
     A                                    70'Num / Seq'
     A                                    80'Act'
     A                                    84'Item'
     A                                   100'TO Slot'
     A                                   115'Qty'
     A                                   121'Qty'
     A                                   127'Qty'
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A            LLTRN#    R        O     2REFFLD(LLTRN# LICLOG)
     A                                      EDTCDE(4)
     A            LLLCNS    R        O    10REFFLD(LLLCNS LICLOG)
     A            LLLTRN    R        O    26REFFLD(LLLTRN LICLOG)
     A                                      EDTCDE(4)
     A            LLLTRNSEQ R        O    34REFFLD(LLLTRNSEQ LICLOG)
     A                                      EDTCDE(4)
     A            LLFLCNS   R        O    38REFFLD(LLFLCNS LICLOG)
     A            LLFLTRN   R        O    54REFFLD(LLFLTRN LICLOG)
     A                                      EDTCDE(4)
     A            LLFLTRNSEQR        O    62REFFLD(LLFLTRNSEQ LICLOG)
     A                                      EDTCDE(4)
     A            LLORD     R        O    66REFFLD(LLORD LICLOG)
     A                                      EDTCDE(4)
     A            LLSEQ     R        O    74REFFLD(LLSEQ LICLOG)
     A                                      EDTCDE(4)
     A            LLACTION  R        O    80REFFLD(LLACTION LICLOG)
     A            LLITEM    R        O    84REFFLD(LLITEM LICLOG)
     A            LLDISP    R        O   100REFFLD(LLDISP LICLOG)
     A            LLAQTY1   R        O   113REFFLD(LLAQTY1 LICLOG)
     A                                      EDTCDE(4)
     A            LLAQTY2   R        O   119REFFLD(LLAQTY2 LICLOG)
     A                                      EDTCDE(4)
     A            LLAQTY3   R        O   125REFFLD(LLAQTY3 LICLOG)
     A                                      EDTCDE(4)
     A                                      SPACEA(002)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('LL726S: End of Report ')
     A*
