     A****************************************************************
     A*
     A* ER311PR   Employee management report for a crew(RECV)(Detail)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*       COMPILE WITH 265 CPI
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60       107
     A*
     A                                   240
     A                                      'Page'
     A            PAGE           4S 0O   245
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   115
     A                                   244
     A                                      'ER311'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
530aAA                                      SPACEA(003)
530aMA            MRGTXT        45A  O     2
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    10'  Time  '
     A                                      UNDERLINE
     A                                    18'   PO    '
     A                                      UNDERLINE
     A                                    30' Seq '
     A                                      UNDERLINE
     A                                    36'Item           '
     A                                      UNDERLINE
     A                                    52'Item Description'
     A                                      UNDERLINE
     A                                    83'      Pcs'
     A                                      UNDERLINE
     A                                    93'   Weight'
     A                                      UNDERLINE
     A                                   103'    Cube '
     A                                      UNDERLINE
     A                                   113'Vendor Item'
     A                                      UNDERLINE
     A                                   129'    Vendor#'
     A                                      UNDERLINE
     A                                   144'Vendor Name                      '
     A                                      UNDERLINE
     A                                   185' Trans#'
     A                                      UNDERLINE
     A                                   193'  Label'
     A                                      UNDERLINE
     A                                   201'License  '
     A                                      UNDERLINE
     A                                   217'Employee'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A*           GTDATE         6S 0      1EDTCDE(Y)
     A*           DIRTOT         5S 2     50EDTCDE(3)
     A*           WKPTOT    R             57REFFLD(WKPTOT ER311)
     A*                                     EDTCDE(1)
     A*           WKCTOT    R             67REFFLD(WKCTOT ER311)
     A*                                     EDTCDE(1)
     A*           WKWTOT    R             82REFFLD(WKWTOT ER311)
     A*                                     EDTCDE(1)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            WKRTIM    R             10REFFLD(WKRTIM ER311)
     A                                      EDTWRD('  :  :  ')
     A            WKPO      R             19REFFLD(WKPO ER311)
     A            WKSEQ     R             29REFFLD(WKSEQ ER311)
     A                                      EDTCDE(3)
     A            WKITEM    R             36REFFLD(WKITEM ER311)
     A            WKITMD    R             52REFFLD(WKITMD ER311)
     A            WKPCS     R             83REFFLD(WKPCS  ER311)
     A                                      EDTCDE(1)
     A            WKWGT     R             93REFFLD(WKWGT  ER311)
     A                                      EDTCDE(1)
     A            WKCUBE    R            103REFFLD(WKCUBE ER311)
     A                                      EDTCDE(1)
     A            WKVITM    R            113REFFLD(WKVITM ER311)
     A            WKVEN#    R            129REFFLD(WKVEN# ER311)
     A                                      EDTCDE(1)
     A            WKVENN    R            144REFFLD(WKVENN ER311)
     A            WKTRN#    R            185REFFLD(WKTRN# ER311)
     A                                      EDTCDE(3)
     A            WKLBL#    R            193REFFLD(WKLBL# ER311)
     A                                      EDTCDE(3)
     A            WKLCNS    R            201REFFLD(WKLCNS ER311)
     A            WKEMP#    R            217REFFLD(WKEMP# ER311)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1REFFLD(WKNAME ER311)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          10A        1
     A            DHRS           7S 2     10EDTCDE(1)
     A            T#PTOT         7S 0     83EDTCDE(1)
     A            T#WTOT         7S 0     93EDTCDE(1)
     A            T#CTOT         7S 0    103EDTCDE(1)
     A            T#TTOT         7S 0    183EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER311: End of Report '
     A*
