     A*%%TS  SD  20160329  140636  MIKER       REL-V7R2M0  5770-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A********************************************************************
     A*
415 AA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS IF NECESSARY.
     A*
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/26/99  DAS  4.13
     A*     - Revised screen 2.
     A*        - Added non-display ind to catch wt label.
     A*        - Added SSB fields after brk1 UOM.
     A*
415 AA*    06/14/01  DAS  4.15
     A*     - Revised screen 1 to position cursor to reason code.
     A*     - Created SCREEN61-64, 16-line versions of SCREEN1-4.
     A*     - Created message screens for 16-line displays. They all
     A*       start with M16.
     A*     - Removed CLRL from records. Writing CLRSCR before each
     A*       EXFMT. OVRLAY must be in records for error message
     A*       to show through.
     A*
500 AA*   05/24/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
510 AA*   07/11/06  JCJ  5.10
     A*     - Activate F10 in screen62
520aAA*  04/24/09  RH   5.20a
     A*    - Enh: Qty and catch weight are both entered, both must
     A*      be the same sign (positve or negative).
     A*      Remaned IT222FM to IT22201 for webfacing.
     A*      Add label for minus sign.
     A*
650 AA*   02/27/13  MLB  6.50
     A*     - Enh: Changed to use IT22202, to allow for BIR mod.
     A*
700a A*   05/05/15  LMC  7.00a
     A*     - Enh: Rename to IT22203
     A*     - Enh: Added the ability to protect normal qty entry
     A*
700b A*   03/30/16  MR   7.00b
     A*     - Enh: Rename to IT22204
     A*     - Enh: Added F7 Pounds/Kilograms functionality
720a A*   08/18/16  LMC  7.20a
     A*     - Enh: Rename to IT22205
     A*     - Enh: Added UCI Tracking.
     A*     - Fix: Edit code on weight field does not display the sign
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: BiRite Foodservice
     A*
BIR AA*   02/27/13  MLB  BIR
     A*     - Enh: Revise dspf's SCREEN3B, SCREEN63B. Screens revised
     A*       to add | marker under Memo fld for where to key p/o nbr.
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN5
     A*%%TS  SD  19960531  145503  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT122.01'
     A                                  1 30'Inventory Adjustments'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse :'
     A            WOWHSE        40A  O  4 14
     A                                  5  2'Department:'
     A            WOWHDP        40A  O  5 14
     A                                  6  2'Slot      :'
     A            SLDISP        12A  O  6 14
     A                                  7  2'Slot type :'
     A            WOSTYP         5A  O  7 14
     A                                  8  2'Status    :'
     A            SSDESC        30A  O  8 14
     A                                  9  2'Item      :'
     A            WOITEM        50A  O  9 14
     A                                 11  2'Available slot qty  :'
     A            W1INCQ        10A  O 11 24
     A                                 11 36'(includes all open transactions)'
     A                                 12  2'Adjustable slot qty :'
     A            W1EXCQ        10A  O 12 24
     A                                 12 36'(excludes open receiving transacti-
     A                                      ons)'
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  4'Adjustment type  . . .'
     A            W1TYPE         1A  B 16 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                 16 59'1 = Increase/decrease'
     A                                 17 59'2 = Replace'
     A                                 18  4'Quantity . . . . . . .'
219 DA*           W1QT01         5Y 0B 18 28EDTCDE(4)
219 AA            W1QT01         5Y 0B 18 28EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O 18 35
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 18 42EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 18 47
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 18 54EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 18 59
     A  93                                  DSPATR(ND)
     A                                 19  4'Catch wt adjustment. .'
     A            W1CWTR         9Y 2B 19 28
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
520a A                                 19 59'(-)after # = Decrease'
     A                                 20  4'Reason code  . . . . .'
     A            W1CODE         2A  B 20 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                 21  4'Memo . . . . . . . . .'
     A            W1MEMO        30A  B 21 28CHECK(LC)
     A                                 23  2'F4=Prompt  F5=Refresh  F9=Slot qua-
     A                                      ntities  F12=Cancel  F20=Accept war-
     A                                      ning'
     A                                      COLOR(BLU)
     A          R SCREEN1
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF04
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.01'
     A                                  1 12'Inv. Adj.'
     A                                  2  1'1 = Increase/decrease'
     A                                  3  1'2 = Replace'
     A                                  4  1'Adj type:'
     A            W1TYPE         1A  B  4 12
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5  1'Reason code:'
     A            W1CODE         2A  B  5 14
     A  23                                  DSPATR(RI)
415 DA**03                                  DSPATR(PC)
415 MA                                      DSPATR(PC)
     A                                  7  1'F3/F12=Exit         '
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19991126  052827  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
510 AA                                      CF10
415 DA**                                    CLRL(07)
700bAA                                      CA07
     A                                      OVERLAY
     A                                  1  2'IT222.02'
     A                                  1 12'Inv. Adj.'
520a A                                  2  1'(-)after # = Dec'
     A                                  3  1'Qty:'
     A            W1QT01         5Y 0B  3  6EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
720aAA  96
720aDA* 41                                  DSPATR(PR)
720aMAO 41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O  3 13
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B  4  6EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
720aAA  96
720aDA* 92                                  DSPATR(PR)
720aMAO 92                                  DSPATR(PR)
     A            W1UM2          5A  O  4 13
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5  6EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
720aAA  96
720aDA* 93                                  DSPATR(PR)
720aMAO 93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 13
     A  93                                  DSPATR(ND)
     A                                  6  1'Catch wt:'
     A  95                                  DSPATR(ND)
     A            W1CWTR         9Y 2B  6 11
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
720aAAO 94
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
700bAA            W1UM4          5A  O  6 22
700bAA N31                              7  1'F7=Pounds'
700bAA                                      COLOR(BLU)
700bAA N31                              7 11'F3/F12=Exit'
700bAA                                      COLOR(BLU)
700bAA  31                              7  1'F7=Kilograms'
700bAA                                      COLOR(BLU)
700bAA  31                              7 14'F3/F12=Exit'
700bAA                                      COLOR(BLU)
700bDA*                                 7  1'F3/F12=Exit         '
700bDA*                                     COLOR(BLU)
     A            WOSSB          3   O  4 19DSPATR(HI)
     A          R SCREEN3
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.03'
     A                                  1 12'Inv. Adj.'
     A                                  3  1'Memo:'
     A            W1MEM1        15A  B  4  6CHECK(LC)
     A            W1MEM2        15A  B  5  6CHECK(LC)
     A*           W1MEMO        30A  B 21 28CHECK(LC)
     A                                  7  1'F3/F12=Exit        '
     A                                      COLOR(BLU)
BIR AA*
BIR AA          R SCREEN3B
BIR AA  98                                  ALARM
BIR AA                                      OVERLAY
BIR AA                                  1  2'IT222.03'
BIR AA                                  1 12'Inv. Adj.'
BIR AA                                  3  1'Memo:'
BIR AA            W1MEM1        15A  B  4  6CHECK(LC)
BIR AA            W1MEM2        15A  B  5  6CHECK(LC)
BIR AA                                  6 10'|'
BIR AA                                  7  1'F3/F12=Exit        '
BIR AA                                      COLOR(BLU)
     A*
     A          R SCREEN4
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF10
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.04'
     A                                  1 12'Inv. Adj.'
     A                                  3  1'Slot qty. changed   '
     A                                  4  1'before adjustment.  '
     A                                  5  1'Press F10 to continue'
     A                                  6  1'or F12 to cancel.  '
     A                                  7  1'F12=Exit  F10=Accept'
     A                                      COLOR(BLU)
     A*
720aAA          R SCREEN6
720aAA*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
720aAA  98                                  ALARM
720aAA                                      CF04
720aAA                                      OVERLAY
720aAA                                  1  2'IT222.06'
720aAA                                  1 12'UCI Tracking'
720aAA                                  2  1'1 = Scan UCI to Add'
720aAA                                  3  1'2 = Scan UCI to Remove'
720aAA                                  4  1'3 = Scan All UCI'
720aAA                                  5  1'4 = Empty-No UCI to Scan'
720aAA                                  6  1'Tracking Type'
720aAA            W6TYPE         1A  B  6 15
720aAA  35                                  DSPATR(RI)
720aAA  01                                  DSPATR(PC)
720aAA                                  7  1'F3/F12=Exit         '
720aAA                                      COLOR(BLU)
720aAA          R SCREEN7
720aAA                                      CF22
720aAA  98                                  ALARM
720aAA                                      OVERLAY
720aAA                                  1  2'IT222.07'
720aAA                                  4  1'Scan UCI'
720aAA            W7UCI        100A  B  4 10
720aAA                                      CHECK(ER)
720aAA N21                                  DSPATR(HI)
720aAA N21                                  DSPATR(UL)
720aAA  01                                  DSPATR(PC)
720aAA  21                                  DSPATR(RI)
720aDA*                                 7  1'F12=Cancel'
720aMA                                  7  1'F12=Cancel  F22=Done'
700aAA                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
415 AA*****************************************************************
415 AA*
415 AA* 16-Line versions of screens.
415 AA*
     A          R SCREEN61
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF04
     A                                      OVERLAY
     A                                  1  2'IT222.61'
     A                                  1 12'Inv. Adj.'
     A                                  5  1'1 = Increase/decrease'
     A                                  6  1'2 = Replace'
     A                                  7  1'Adj type:'
     A            W1TYPE         1A  B  7 12
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  9  1'Reason code:'
     A            W1CODE         2A  B  9 14
     A  23                                  DSPATR(RI)
415 DA**03                                  DSPATR(PC)
415 MA                                      DSPATR(PC)
     A                                 15  1'F3/F12=Exit         '
     A                                      COLOR(BLU)
     A          R SCREEN62
     A*%%TS  SD  20160329  140636  MIKER       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CF10
     A                                      CA07
     A                                      OVERLAY
     A                                  1  2'IT222.62'
     A                                  1 12'Inv. Adj.'
     A            W1SDSP        20A  O  3  1
     A                                  4  1'Status:'
     A            W1SLST         2A  O  4  9
     A                                  4 14'Def:'
     A            W1SDEF         2A  O  4 19
     A            W1ITEM        65A  O  5  1
     A            W1DESC        30A  O  6  1
     A                                  7  1'Avl Qty:'
     A            W1AVLQ        10A  O  7 10
     A                                  7 23'(includes all open transactions)'
     A                                  8  1'Adj Qty:'
     A            W1ADJQ        10A  O  8 10
     A                                  8 23'(excludes open receiving transacti-
     A                                      ons)'
     A            WOTYPE        20A  O  9  1
     A                                 11  1'Qty:'
     A            W1QT01         5Y 0B 11  6EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
720aAA  96
720aDA* 41                                  DSPATR(PR)
720aMAO 41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A            W1UM1          5A  O 11 13
     A  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 12  6EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
720aAA  96
720aDA* 92                                  DSPATR(PR)
720aMAO 92                                  DSPATR(PR)
     A            W1UM2          5A  O 12 13
     A  92                                  DSPATR(ND)
     A            WOSSB          3A  O 12 19DSPATR(HI)
     A            W1QT03         3Y 0B 13  6EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
720aAA  96
720aDA* 93                                  DSPATR(PR)
720aMAO 93                                  DSPATR(PR)
     A            W1UM3          5A  O 13 13
     A  93                                  DSPATR(ND)
     A                                 14  1'Catch wt:'
     A  95                                  DSPATR(ND)
     A            W1CWTR         9Y 2B 14 11
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
720aAAO 94
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
700bAA            W1UM4          5A  O 14 22
700bAA N31                             15  1'F7=Pounds'
700bAA                                      COLOR(BLU)
700bAA N31                             15 11'F3/F12=Exit'
700bAA                                      COLOR(BLU)
700bAA  31                             15  1'F7=Kilograms'
700bAA                                      COLOR(BLU)
700bAA  31                             15 14'F3/F12=Exit'
700bAA                                      COLOR(BLU)
     A          R SCREEN63
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'IT222.63'
     A                                  1 12'Inv. Adj.'
     A                                  6  1'Memo:'
     A            W1MEM1        15A  B  8  6CHECK(LC)
     A            W1MEM2        15A  B  9  6CHECK(LC)
     A                                 15  1'F3/F12=Exit        '
     A                                      COLOR(BLU)
BIR AA*
BIR AA          R SCREEN63B
BIR AA  98                                  ALARM
BIR AA                                      OVERLAY
BIR AA                                  1  2'IT222.63'
BIR AA                                  1 12'Inv. Adj.'
BIR AA                                  6  1'Memo:'
BIR AA            W1MEM1        15A  B  8  6CHECK(LC)
BIR AA            W1MEM2        15A  B  9  6CHECK(LC)
BIR AA                                 10 10'|'
BIR AA                                 15  1'F3/F12=Exit        '
BIR AA                                      COLOR(BLU)
     A*
     A          R SCREEN64
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      OVERLAY
     A                                      CF10
     A                                  1  2'IT222.64'
     A                                  1 12'Inv. Adj.'
     A                                  6  1'Slot qty. changed   '
     A                                  7  1'before adjustment.  '
     A                                  8  1'Press F10 to continue'
     A                                  9  1'or F12 to cancel.  '
     A                                 15  1'F12=Exit  F10=Accept'
     A                                      COLOR(BLU)
720aAA          R SCREEN66
720aAA*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
720aAA  98                                  ALARM
720aAA                                      CF04
720aAA                                      OVERLAY
720aAA                                  1  2'IT222.06'
720aAA                                  1 12'UCI Tracking'
720aAA                                  2  1'1 = Scan UCI to Add'
720aAA                                  3  1'2 = Scan UCI to Remove'
720aAA                                  4  1'3 = Scan All UCI'
720aAA                                  5  1'4 = Empty-No UCI to Scan'
720aAA                                  6  1'Tracking Type'
720aAA            W6TYPE         1A  B  6 15
720aAA  35                                  DSPATR(RI)
720aAA  01                                  DSPATR(PC)
720aAA                                  7  1'F3/F12=Exit         '
720aAA                                      COLOR(BLU)
415 AA*****************************************************************
415 AA*
415 AA* Error Message Subfile Records for 16 line screens
415 AA*
415 AA*    Error Message Subfile Record
415 AA*
415 AA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415 AA*
415 AA*    Error Message Subfile Control Record
415 AA*
415 AA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415 AA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500  A          R SCRN5SP
     A*%%TS  SD  19960531  145503  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT122.01'
     A                                  1 30'Ajustes de Inventario'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Incorp. de detalle'
     A                                      DSPATR(HI)
     A                                  4  2'Almacen   :'
     A            WOWHSE        40A  O  4 14
     A                                  5  2'Departamnt:'
     A            WOWHDP        40A  O  5 14
     A                                  6  2'Slot      :'
     A            SLDISP        12A  O  6 14
     A                                  7  2'slot tipo :'
     A            WOSTYP         5A  O  7 14
     A                                  8  2'Estado    :'
     A            SSDESC        30A  O  8 14
     A                                  9  2'Producto  :'
     A            WOITEM        50A  O  9 14
     A                                 11  2'Ctd de slot dispnibl:'
     A            W1INCQ        10A  O 11 24
     A                                 11 36'(incluye  todas trans. abiertas)'
     A                                 12  2'Ctd de slot ajustabl:'
     A            W1EXCQ        10A  O 12 24
     A                                 12 36'(excluye trans. abiertas de recibi-
     A                                      ir)'
     A                                 14  2'Escriba cambios, Oprima ENTER.'
     A                                      COLOR(BLU)
     A                                 16  4'Tipo de ajuste . . . .'
     A            W1TYPE         1A  B 16 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                 16 59'1 = Aumentar/decrecer'
     A                                 17 59'2 = Cambiar'
     A                                 18  4'Cantidad . . . . . . .'
219 DA*           W1QT01         5Y 0B 18 28EDTCDE(4)
219 AA            W1QT01         5Y 0B 18 28EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O 18 35
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 18 42EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 18 47
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 18 54EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 18 59
     A  93                                  DSPATR(ND)
     A                                 19  4'Ajuste de CW . . . . .'
     A            W1CWTR         9Y 2B 19 28
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
     A                                 20  4'Codigo de razon  . . .'
     A            W1CODE         2A  B 20 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                 21  4'Memo . . . . . . . . .'
     A            W1MEMO        30A  B 21 28CHECK(LC)
     A                                 23  2'F4=Inicitar F5=Refrescar F9=Ctd de-
     A                                      slot F12=Cancelar F20=Aceptar aviso'
     A
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF04
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.01'
     A                                  1 12'Aju. de Inv.'
     A                                  2  1'1 = Aumentar/decrecer'
     A                                  3  1'2 = Cambiar'
     A                                  4  1'Aju tipo:'
     A            W1TYPE         1A  B  4 12
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5  1'Codigo de razon:'
     A            W1CODE         2A  B  5 18
     A  23                                  DSPATR(RI)
415 DA**03                                  DSPATR(PC)
415 MA                                      DSPATR(PC)
     A                                  7  1'F3/F12=Salir        '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  19991126  052827  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
415 DA**                                    CLRL(07)
700bAA                                      CA07
     A                                      OVERLAY
     A                                  1  2'IT222.02'
     A                                  1 12'Aju. Inv.'
     A                                  3  1'Ctd:'
     A            W1QT01         5Y 0B  3  6EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
720aAA  96
720aDA* 41                                  DSPATR(PR)
720aMAO 41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O  3 13
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B  4  6EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
720aAA  96
720aDA* 92                                  DSPATR(PR)
720aMAO 92                                  DSPATR(PR)
     A            W1UM2          5A  O  4 13
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5  6EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
720aAA  96
720aDA* 93                                  DSPATR(PR)
720aMAO 93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 13
     A  93                                  DSPATR(ND)
     A                                  6  1'CW      :'
     A  95                                  DSPATR(ND)
     A            W1CWTR         9Y 2B  6 11
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
720aAAO 94
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
700bAA            W1UM4          5A  O  6 22
700bAA N31                              7  1'F7=Libras'
700bAA                                      COLOR(BLU)
700bAA N31                              7 11'F3/F12=Salir'
700bAA                                      COLOR(BLU)
700bAA  31                              7  1'F7=Kilogramos'
700bAA                                      COLOR(BLU)
700bAA  31                              7 16'F3/F12=Salir'
700bAA                                      COLOR(BLU)
700bDA*                                 7  1'F3/F12=Salir        '
700bDA*                                     COLOR(BLU)
700bDA                                  7  1'F3/F12=Salir        '
700bDA                                      COLOR(BLU)
     A            WOSSB          3   O  4 19DSPATR(HI)
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.03'
     A                                  1 12'Aju. Inv.'
     A                                  3  1'Memo:'
     A            W1MEM1        15A  B  4  6CHECK(LC)
     A            W1MEM2        15A  B  5  6CHECK(LC)
     A*           W1MEMO        30A  B 21 28CHECK(LC)
     A                                  7  1'F3/F12=Salir       '
     A                                      COLOR(BLU)
     A*
500 AA*----------------------------------------------------------------
500 AA          R SCRN4SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF10
415 DA**                                    CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT222.04'
     A                                  1 12'Aju. Inv.'
     A                                  3  1'Ctd de slot cambio  '
     A                                  4  1'antes de ajuste.    '
     A                                  5  1'Oprima F10 para continuar'
     A                                  6  1'o F12 para cancelar.'
     A                                  7  1'F12=Salir  F10=Aceptar'
     A                                      COLOR(BLU)
720aAA          R SCRN6SP
720aAA*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
720aAA  98                                  ALARM
720aAA                                      CF04
720aAA                                      OVERLAY
720aAA                                  1  2'IT222.06'
720aAA                                  1 12'UCI Tracking'
720aAA                                  2  1'1 = Scan UCI to Add'
720aAA                                  3  1'2 = Scan UCI to Remove'
720aAA                                  4  1'3 = Scan All UCI'
720aAA                                  5  1'4 = Empty-No UCI to Scan'
720aAA                                  6  1'Tracking Type'
720aAA            W6TYPE         1A  B  6 15
720aAA  35                                  DSPATR(RI)
720aAA  01                                  DSPATR(PC)
720aAA                                  7  1'F3/F12=Exit         '
720aAA                                      COLOR(BLU)
415 AA*****************************************************************
415 AA*
415 AA* 16-Line versions of screens.
415 AA*
500 AA*----------------------------------------------------------------
500 AA          R SCRN61SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CF04
     A                                      OVERLAY
     A                                  1  2'IT222.61'
     A                                  1 12'Aju. Inv.'
     A                                  5  1'1 = Aumentar/decrecer'
     A                                  6  1'2 = Cambiar'
     A                                  7  1'Aju tipo:'
     A            W1TYPE         1A  B  7 12
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  9  1'Codigo de razon:'
     A            W1CODE         2A  B  9 18
     A  23                                  DSPATR(RI)
415 DA**03                                  DSPATR(PC)
415 MA                                      DSPATR(PC)
     A                                 15  1'F3/F12=Salir        '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN62SP
     A*%%TS  SD  19991126  052827  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
700bAA                                      CA07
     A                                      OVERLAY
     A                                  1  2'IT222.62'
     A                                  1 12'Aju. Inv.'
     A            W1SDSP        20A  O  3  1
     A                                  4  1'Estado:'
     A            W1SLST         2A  O  4  9
     A                                  4 14'Def:'
     A            W1SDEF         2A  O  4 19
     A            W1ITEM        65A  O  5  1
     A            W1DESC        30   O  6  1
     A                                  7  1'Ctd Dsp:'
     A            W1AVLQ        10A  O  7 10
     A                                  7 23'(incluye todas trans. abiertas )'
     A                                  8  1'Adj Qty:'
     A            W1ADJQ        10   O  8 10
     A                                  8 23'(excluye trans. abiertas de recibi-
     A                                      r)'
     A            WOTYPE        20A  O  9  1
     A                                 11  1'Ctd:'
     A            W1QT01         5Y 0B 11  6EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
720aAA  96
720aDA* 41                                  DSPATR(PR)
720aMAO 41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O 11 13
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 12  6EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
720aAA  96
720aDA* 92                                  DSPATR(PR)
720aMAO 92                                  DSPATR(PR)
     A            W1UM2          5A  O 12 13
     A  92                                  DSPATR(ND)
     A            WOSSB          3   O 12 19DSPATR(HI)
     A            W1QT03         3Y 0B 13  6EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
720aAA  96
720aDA* 93                                  DSPATR(PR)
720aMAO 93                                  DSPATR(PR)
     A            W1UM3          5A  O 13 13
     A  93                                  DSPATR(ND)
     A                                 14  1'CW      :'
     A  95                                  DSPATR(ND)
     A            W1CWTR         9Y 2B 14 11
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
720aAAO 94
     AO 90                                  DSPATR(PR)
720aDA*                                     EDTCDE(4)
720aMA                                      EDTCDE(M)
700bAA            W1UM4          5A  O 14 22
700bAA N31                             15  1'F7=Libras'
700bAA                                      COLOR(BLU)
700bAA N31                             15 11'F3/F12=Salir'
700bAA                                      COLOR(BLU)
700bAA  31                             15  1'F7=Kilogramos'
700bAA                                      COLOR(BLU)
700bAA  31                             15 16'F3/F12=Salir'
700bAA                                      COLOR(BLU)
700bDA*                                15  1'F3/F12=Salir        '
700bDA*                                     COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN63SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'IT222.63'
     A                                  1 12'Aju. Inv.'
     A                                  6  1'Memo:'
     A            W1MEM1        15A  B  8  6CHECK(LC)
     A            W1MEM2        15A  B  9  6CHECK(LC)
     A                                 15  1'F3/F12=Salir       '
     A                                      COLOR(BLU)
     A*
500 AA*----------------------------------------------------------------
500 AA          R SCRN64SP
     A*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      OVERLAY
     A                                      CF10
     A                                  1  2'IT222.64'
     A                                  1 12'Aju. Inv.'
     A                                  6  1'Ctd de slot cambio  '
     A                                  7  1'antes de ajuste.  '
     A                                  8  1'Oprima F10 para continuar'
     A                                  9  1'o F12 para cancelar'
     A                                 15  1'F12=Salir  F10=Aceptar'
     A                                      COLOR(BLU)
720aAA          R SCRN66SP
720aAA*%%TS  SD  19970211  105540  ROCKY       REL-V3R1M0  5763-PW1
720aAA  98                                  ALARM
720aAA                                      CF04
720aAA                                      OVERLAY
720aAA                                  1  2'IT222.06'
720aAA                                  1 12'UCI Tracking'
720aAA                                  2  1'1 = Scan UCI to Add'
720aAA                                  3  1'2 = Scan UCI to Remove'
720aAA                                  4  1'3 = Scan All UCI'
720aAA                                  5  1'4 = Empty-No UCI to Scan'
720aAA                                  6  1'Tracking Type'
720aAA            W6TYPE         1A  B  6 15
720aAA  35                                  DSPATR(RI)
720aAA  01                                  DSPATR(PC)
720aAA                                  7  1'F3/F12=Exit         '
720aAA                                      COLOR(BLU)
