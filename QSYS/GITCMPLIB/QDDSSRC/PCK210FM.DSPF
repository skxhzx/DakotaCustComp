     A*%%TS  SD  20120907  160752  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PCK210FM   Create test orders for warehouse 900.
     A*  01 September 1997
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*    09/17/97  DAS  4.05
     A*      - Added cross dock option.
     A*
416 AA*    07/17/02  DAS  4.16
     A*      - Added FTP option.
     A*      - Added 'S' type.
     A*
640aAA*    07/21/11  DAS  6.40a
     A*      - Added Batch Pick option.
     A*
640bAA*    07/29/11  DAS  6.40b
     A*      - Changed FTP option into an Import Method option.
     A*
700aAA*    01/30/16  DAS  7.00a
     A*      - Added AMP pick option
     A*
710aAA*    06/13/17  DAS  7.10a
     A*      - Changed Test 7 to a Trax test.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
      ***                                   PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20120907  160752  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PCK210.01'
     A                                  1 23'Create Test Orders for Warehouse 9-
     A                                      00'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
730aMA                                  2  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
730aMA                                  3  4'Place on job queue . . . . .'
730aMA            W1JOBQ         1A  B  3 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
730aMA                                  3 44'Y=Yes, N=No'
730aAA                                  4  4'Warehouse  . . . . . . . . .'
730aAA            W1WHSE         3Y 0B  4 34EDTCDE(4)
730aAA N21N90                               DSPATR(HI)
730aAA  21                                  DSPATR(RI)
730aAA N21N90                               DSPATR(UL)
730aAA  01                                  DSPATR(PC)
730aAA  90                                  DSPATR(PR)
     A                                  5  4'Import method  . . . . . . .'
     A            W1FTP          1A  B  5 34
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                  5 44'F=FTP (PCKTEST), S=Staging'
     A                                  6  4'Order type . . . . . . . . .'
     A            W1TYPE         1A  B  6 34
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                  6 44''' ''=Normal, ''S''=Single, ''W''=-
     A                                      Willcall'
     A                                  7  4'Cross dock . . . . . . . . .'
     A            W1XDK          1A  B  7 34
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                  7 44'Y=Yes, N=No'
     A                                  8  4'Batch pick . . . . . . . . .'
     A            W1BPCK         1A  B  8 34
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                  8 44'Y=Yes, N=No'
     A                                  9  4'Pre pick . . . . . . . . . .'
     A            W1PPCK         1   B  9 34
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(UL)
     A  07                                  DSPATR(PC)
     A                                  9 44'Y=Yes, N=No'
     A                                  9 56'(Coastal, ohmisc pos 17)'
     A                                 10  4'AMP pick . . . . . . . . . .'
     A            W1APCK         1   B 10 34
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A            W1ACOD         3   B 10 36
     A                                 10 44'Y=Yes, N=No'
     A                                 10 56'(hmmsc4 pos 13)         '
     A                                 11  6'Y/N'
     A                                      DSPATR(UL)
     A                                 11 10'Test'
     A                                      DSPATR(UL)
     A                                 11 15'Description                       -
     A                                                                   '
     A                                      DSPATR(UL)
     A            W1YN1          1A  B 12  7
     A                                 12 12'1  Item DA101(10)'
     A            W1YN2          1A  B 13  7
     A                                 13 12'2  Item DA101(10), DP101(5)'
     A            W1YN3          1A  B 14  7
     A                                 14 12'3  Item DA102(10), FA102(10)'
     A            W1YN4          1A  B 15  7
     A                                 15 12'4  Item DA102(10), DP102(5), FA102-
     A                                      (10), FP102(5)'
     A            W1YN5          1A  B 16  7
     A                                 16 12'5  Item DP103(15) - PIR only test'
     A            W1YN6          1A  B 17  7
     A                                 17 12'6  Item DA103(50), DB103(10) - Bul-
     A                                      k pick test'
     A            W1YN7          1A  B 18  7
     A                                 18 12'7  Trax Test'
     A            W1YN8          1A  B 19  7
     A                                 19 12'8  Ord 1: DA105 (30) DB105(20), Or-
     A                                      d 2: DA106 (20), DB106(30)'
     A            W1YN9          1A  B 20  7
     A                                 20 12'9  Item DA107(100) - Non bulk pick-
     A                                       overflow test'
     A            W1YN10         1A  B 21  7
     A                                 21 11'10  Item DC101BA(5), DC102BC(5), D-
     A                                      C103S(5), DC104A(10), DC105C(10)'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
