     A*%%TS  SD  19960524  163717  DAVE3       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR760FM   Remove exported Customer returns - Options
     A*  24 May 1996
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
414aAA*    04/26/00  MLB  4.14a
     A*      - Changed export cutoff from date to number of weeks value.
     A*      - Several misc. screen format changes.
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960524  163717  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
414aAA                                  8 33'. . . . .'
414aMA                                 11  4'Place on job queue ? . . . .'
414aAA                                 11 33'. . . . .'
414aAA            W1WHSE         3Y 0B  8 44EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
414aMA            W1JOBQ         1A  B 11 44
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
414aMA                                 11 53'(Y=Yes, N=No)'
414aDA*                                 9  4'Exported before  . . . . . .'
414aDA*           W1DATE         6Y 0B  9 34EDTCDE(4)
414aAA            W1WEEK         4A  B  9 44
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
414aAA                                  9 53'*DFT, 1-999'
414aAA                                  9  4'Weeks history to keep  . . .'
414aAA                                  9 33'. . . . .'
414aAA            W1CRHS         3Y 0O 10 44EDTCDE(4)
414aAA                                 10  6'Current number of weeks history  .-
414aAA                                       .'
414aDA*                                 9 53'(MMDDYY)'
     A            W1SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
