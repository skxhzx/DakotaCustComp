     A*%%TS  SD  19960102  114329  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TK012FM   Task File maintenance - Change screen (BFC use)
     A*  21 December, 1995
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960102  114329  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TK012.01'
     A                                  1 30'Task File Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Code . . . . . . . . .'
     A            W1CODE         6A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Description  . . . . .'
     A            W1DESC        30A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Category . . . . . . .'
     A            W1CAT          6A  B 10 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Sub category . . . . .'
     A            W1SUB          6A  B 11 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Type . . . . . . . . .'
     A            W1TYPE         1A  B 12 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Standard (Y,N) . . . .'
     A            W1STD          1A  B 13 28DSPATR(UL)
     A                                 14  4'Trackaing Level  . . .'
     A            W1TLVL         1A  B 14 28DSPATR(UL)
     A                                 15  4'Closed by User (Y,N) .'
     A            W1USRC         1A  B 15 28DSPATR(UL)
     A                                 16  4'Track Cube (Y,N) . . .'
     A            W1TRKC         1A  B 16 28DSPATR(UL)
     A                                 17  4'Track Weight (Y,N) . .'
     A            W1TRKW         1A  B 17 28DSPATR(UL)
     A                                 18  4'Track Pieces (Y,N) . .'
     A            W1TRKP         1A  B 18 28DSPATR(UL)
     A                                 19  4'Track Qty 1 (Y,N)  . .'
     A            W1TRK1         1A  B 19 28DSPATR(UL)
     A                                 19 34'Qty 1 Description  . .'
     A            W1DSC1         6A  B 19 59DSPATR(UL)
     A                                 20  4'Track Qty 2 (Y,N)  . .'
     A            W1TRK2         1A  B 20 28DSPATR(UL)
     A                                 20 34'Qty 2 Description  . .'
     A            W1DSC2         6A  B 20 59DSPATR(UL)
     A                                 21  4'Track Qty 3 (Y,N)  . .'
     A            W1TRK3         1A  B 21 28DSPATR(UL)
     A                                 21 34'Qty 3 Description  . .'
     A            W1DSC3         6A  B 21 59DSPATR(UL)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
