     A*%%TS  SD  19970921  182842  JOHNG       REL-V3R7M0  5716-PW1
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  IT290FM      RF Let-Down
     A*  06 February, 1997
     A*  David Sommerville
     A*  Hemant Kapadia
     A*
     A********************************************************************
     A*
     A*
     A*  Program Modification History
     A*  Revisions:
404 AA*    08/26/97   HNK  4.04
     A*      - Department added to screen 1.
409 AA*    06/29/98  RH   4.09
     F*      - Added Screen 8 for zero slot verification.
     A*
     A*
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  19970921  182842  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.01S'
     A                                  1 12'RF LetDn'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  3  3' Warehouse:'
     A            W1WHSE         3Y 0B  3 15EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Department:'
     A            W1WHDP         5A  B  4 15
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  6'To Slot:'
     A            W1TDIS        12A  B  5 15
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A*                                 5  2'     Aisle:'
     A*           W1AISL         3   B  5 14
     A* 23                                  DSPATR(RI)
     A*N23N90                               DSPATR(UL)
     A* 03                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      CF10
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.02S'
     A                                  1 12'RF LetDn'
     A            W2ITEM        65A  O  2  1
     A                                  3  1'From:'
     A            W2FMSL        12A  O  3  7
     A                                  4  2'Qty:'
     A            W2QTY          7Y 0O  4  7EDTCDE(4)
     A                                  5  1'Ti/Hi:'
     A            W2TIE          3Y 0O  5  8EDTCDE(4)
     A                                  5 12'/'
     A            W2HIGH         3Y 0O  5 14EDTCDE(4)
     A                                  6  1'Scan:'
     A            W2SCAN        12A  B  6  7CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F10=Excp F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA12
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.03S'
     A                                  1 12'RF LetDn'
     A            W3ITEM        65A  O  2  1
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  3  3'To:'
     A            W3TOSL        12A  O  3  7
     A                                  4  2'Qty:'
     A            W3QTY          7Y 0O  4  7EDTCDE(4)
     A                                  5  1'Scan:'
     A            W3SCAN        12A  B  5  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN4
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.04S'
     A                                  1 12'RF LetDn'
     A                                  3  1'F10=Next letdown      '
     A                                  4  1' F3=Exit'
     A          R SCREEN5
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.05S'
     A                                  1 12'RF LetDn'
     A                                  3  5'Exception'
     A                                      DSPATR(HI)
     A                                  4  5'Item:'
     A            W5ITEM         1A  B  4 11
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4 14'(Y,N)'
     A                                  5  1'Quantity:'
     A            W5QTY          1A  B  5 11
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 14'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN6
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.06S'
     A                                  1 12'RF LetDn'
     A            W6ITEM        65A  O  2  1
     A                                  3  1'From:'
     A            W6FMSL        12A  O  3  7
     A                                  4  2'Qty:'
     A            W6QTY          7Y 0O  4  7EDTCDE(4)
     A                                  6  1'Scan:'
     A            W6SCAN        12A  B  6  7CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN7
     A*%%TS  SD  19970921  175633  JOHNG       REL-V3R7M0  5716-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.07S'
     A                                  1 12'RF LetDn'
     A            W7ITEM        65A  O  2  1
     A                                  3  1'From:'
     A            W7FMSL        12A  O  3  7
     A                                  4  2'Qty:'
     A            W7QTY          7Y 0O  4  7EDTCDE(4)
     A                                  5  2'New Qty:'
     A            W7NQTY         7Y 0B  5 11EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  1'Scan:'
     A            W7SCAN        12A  B  6  7CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
409 AA          R SCREEN8
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT295.08S'
     A                                  1 12'RF LetDn'
     A                                  3  5'Verify Slot'
     A                                      DSPATR(HI)
     A                                  4  5'Zero:'
     A            W8ZERO         1   B  4 11
409 AA                                      CHECK(ER)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4 14'(Y,N)'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
