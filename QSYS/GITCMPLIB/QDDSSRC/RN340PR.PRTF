     A*%%***********************************************************************
     A*%%TS  RD  20190408  105147  KENNETHE    REL-V7R2M0  5770-WDS
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A**************************************************************************
     A*
     A*   RN340PR  CUSTOMER Returns Licenses Bar Coded
     A*   05 April, 2019
     A*   Kenneth Elder
     A*
     A*   ÂˆCOMPILE WITH DEVTYPE *AFPDS FOR BARCODE PRINTING
     A*
     A*  Revisions
     A*
730 AA*  04/05/19  KDE  7.30
     A*      - Created.
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A*
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    20
     A*
     A                                    72
     A                                      'Page'
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    25
     A                                    72
     A                                      'RN340'
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A*
     A                                      SPACEA(001)
     A                                     2
     A                                      'Dept:  '
     A            WDDESC        30O       10
     A                                      SPACEA(001)
     A                                     2
     A                                      'Cust. Order#'
     A            $CUSTORD       7  0     16
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A*
     A                                     1
     A                                      'Item#'
     A                                      UNDERLINE
     A                                    17
     A                                      'Description    '
     A                                      UNDERLINE
     A*
     A                                    49
     A                                      'Qty'
     A                                      UNDERLINE
     A                                    54
     A                                      'UM'
     A                                      UNDERLINE
     A                                    60
     A                                      'Putaway License'
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 002
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A*
     A                                      SPACEA(003)
     A            SQITEM        15   O     1
     A            SQDESC        30   O    17
     A            SQQTY          5  0     48
     A                                      EDTCDE(Z)
     A            SQUOM          2        54
     A            SQLCNS        15   O    62
     A                                      BARCODE(CODE128 1 *HRITOP (*WIDTH 0-
     A                                      .008) (*RATIO 2.10))
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 003
     A*%%***********************************************************************
     A*
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'RN340: End of Report'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(PIRWORKKE/RN340PR)
     A*%%CP+    DEVTYPE(*AFPDS)
     A*%%CP     PAGESIZE(*N       132      *N     )
     A*%%***********************************************************************
     A*
