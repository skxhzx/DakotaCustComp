     A*%%TS  SD  20100921  102005  BFCTEST1    REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*  LT111BFM RF - Pick Slot w/< 5 Cases - Aisle Range.
     A*
     A*  September 21, 2010
     A*  Jose G. Duque
     A*
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    ##/##/##  XXX-XXX
     A*      Text . . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R SCREEN1
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A  98                                  ALARM
     A*>                                    CLRL(07)
     A                                      OVERLAY
     A*
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A*
     A                                  1  2'LT111B.01'
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  1 15'Aisle Range'
     A                                  3  2'Warehouse . .'
     A            W1WHSE         3Y 0B  3 16
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'Department  .'
     A            W1WHDP         5   B  4 16
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Aisle Range .'
     A            W1FRMA         3   B  5 16
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  5 20'thru'
     A            W1TOA          3   B  5 25
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                      CHECK(ER)
     A                                  5 29'(Blank=All)'
     A                                  6  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  6 11'F5-Refresh'
     A                                      COLOR(BLU)
     A                                  6 23'F12=Cancel'
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R CLRSCR
      *----------------------------------------------------------------
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
      *----------------------------------------------------------------
     A          R MSGREC                    SFL
      *----------------------------------------------------------------
     A                                      SFLMSGRCD(7)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
      *----------------------------------------------------------------
     A          R MSGCTL                    SFLCTL(MSGREC)
      *----------------------------------------------------------------
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
      *----------------------------------------------------------------
     A          R MSGCLR
      *----------------------------------------------------------------
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(07)
     A                                      CLRL(1)
