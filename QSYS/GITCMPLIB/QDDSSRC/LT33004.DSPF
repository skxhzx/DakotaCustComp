     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT330FM    PO DETAIL VIEW   - Display
     A*  Chuck Manny
     A*
     A*  Revisions
640a A*  01/20/11   GJA   6.40a
     A*     Enh:  Added F22 Function to create work file
640b A*  01/20/11   GJA   6.40b
     A*     Enh:  Added customer number to display
640C A*  06/26/12   LMC   6.40C
     A*     Enh:  Added ability to get recall from item maintenance
     A*           added some additional fields and two more subfiles
650aAA*  05/20/13   RBD   6.50a
     A*     Enh:  Increased the size of sflhide to add the printing of
     A*           ship date.
     A*----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF21
640aAA                                      CF22
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A*
640cDA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            #FITEM        15A  O  8  5
     A            W1RQTY        10A  O  8 21
700bMA            W1LOC         32A  O  8 32
700bMA            #FLCNS        15A  O  8 65
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  3
     A                                  1 31'P.O. Recall Listing'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Quantity  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 32'Location                      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700bMA                                  7 65'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A  21                                  SFLNXTCHG
     A*
640cDA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            #FITEM        15A  O  8  5
     A            W1RQTY        10A  O  8 21
     A            #FLCNS        15A  O  8 32
     A            #FOLCNS       15A  O  8 48
640bAA            #FCUST#       11  0O  8 64EDTCDE(3)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  3
     A                                  1 31'P.O. Recall Listing'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Quantity  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 32'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 48'Original Lic   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640cDA*                                 7 64'  Order#   '
640cMA                                  7 64'  Cust#    '
640bAA                                      DSPATR(HI)
640bAA                                      DSPATR(UL)
     A*
     A          R SFL3                      SFL
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
640cDA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            LLLCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            LLADDEUSR R        O  8 25REFFLD(LLREC/LLADDEUSR *LIBL/LICLOG)
     A            LLADDTS   R        O  8 36REFFLD(LLREC/LLADDTS *LIBL/LICLOG)
     A            LLEXCD    R        O  8 63REFFLD(LLREC/LLEXCD *LIBL/LICLOG)
     A          R SF3CTL                    SFLCTL(SFL3)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 63'Info'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'Added                     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 63'Code    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL4                      SFL
     A  21                                  SFLNXTCHG
610aDA**          SFLHIDE      100A  H
640cDA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LLACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            LLLCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            LLITEM    R        O  8 25REFFLD(LLREC/LLITEM *LIBL/LICLOG)
     A            LLPRTY    R        O  8 43REFFLD(LLREC/LLPRTY *LIBL/LICLOG)
     A            W1EXPD         6Y 0O  8 46EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  8 55EDTWRD('  /  /  ')
     A          R SF4CTL                    SFLCTL(SFL4)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'Prty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'Expires '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'Entered '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
640cAA          R SFL5                      SFL
640cAA  21                                  SFLNXTCHG
640cAA*
640cAA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
640cAA            OPTION         2A  B  8  2
640cAA  20                                  DSPATR(PC)
640cAA N21N90                               DSPATR(UL)
640cAA  21                                  DSPATR(RI)
640cAA  90                                  DSPATR(PR)
640cAA            #FITEM        15A  O  8  5
640cAA            W1RQTY        10A  O  8 21
640cAA            #FLCNS        15A  O  8 32
640cAA            #FOLCNS       15A  O  8 48
640cAA            #FEXPDT        8  0O  8 64EDTWRD('    /  /  ')
640cAA*
640cAA          R SF5CTL                    SFLCTL(SFL5)
640cAA                                      SFLSIZ(0034)
640cAA                                      SFLPAG(0014)
640cAA                                      ROLLUP
640cAA                                      OVERLAY
640cAA                                      SFLCSRRRN(&CSRRRN)
640cAA N32                                  SFLDSP
640cAA N31                                  SFLDSPCTL
640cAA  31                                  SFLCLR
640cAA  90                                  SFLEND(*MORE)
640cAA            RRN1           4S 0H      SFLRCDNBR(CURSOR)
640cAA            CSRRRN         5S 0H
640cAA            SCRNAME        8A  O  1  3
640cAA                                  1 31'P.O. Recall Listing'
640cAA                                      DSPATR(HI)
640cAA                                  1 71DATE
640cAA                                      EDTCDE(Y)
640cAA            SCHEAD        58A  O  2 12
640cAA                                  2 71TIME
640cAA            SCOPT1        75A  O  4  2COLOR(BLU)
640cAA            SCOPT2        75A  O  5  2COLOR(BLU)
640cAA                                  7  1'Opt'
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7  5'Item           '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 21'Quantity  '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 32'License        '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 48'Original Lic   '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 64'Expiration '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA*
640cAA          R SFL6                      SFL
640cAA  21                                  SFLNXTCHG
640cAA*
640cAA*           SFLHIDE      110A  H
650aDA*           SFLHIDE      118A  H
650aMA            SFLHIDE      124A  H
640cAA            OPTION         2A  B  8  2
640cAA  20                                  DSPATR(PC)
640cAA N21N90                               DSPATR(UL)
640cAA  21                                  DSPATR(RI)
640cAA  90                                  DSPATR(PR)
640cAA            #FITEM        15A  O  8  5
640cAA            W1RQTY        10A  O  8 21
640cAA            #FLCNS        15A  O  8 32
640cAA            #FOLCNS       15A  O  8 48
640cAA            H1PO           9   O  8 64
640cAA*
640cAA          R SF6CTL                    SFLCTL(SFL6)
640cAA                                      SFLSIZ(0034)
640cAA                                      SFLPAG(0014)
640cAA                                      ROLLUP
640cAA                                      OVERLAY
640cAA                                      SFLCSRRRN(&CSRRRN)
640cAA N32                                  SFLDSP
640cAA N31                                  SFLDSPCTL
640cAA  31                                  SFLCLR
640cAA  90                                  SFLEND(*MORE)
640cAA            RRN1           4S 0H      SFLRCDNBR(CURSOR)
640cAA            CSRRRN         5S 0H
640cAA            SCRNAME        8A  O  1  3
640cAA                                  1 31'P.O. Recall Listing'
640cAA                                      DSPATR(HI)
640cAA                                  1 71DATE
640cAA                                      EDTCDE(Y)
640cAA            SCHEAD        58A  O  2 12
640cAA                                  2 71TIME
640cAA            SCOPT1        75A  O  4  2COLOR(BLU)
640cAA            SCOPT2        75A  O  5  2COLOR(BLU)
640cAA                                  7  1'Opt'
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7  5'Item           '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 21'Quantity  '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 32'License        '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 48'Original Lic   '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA                                  7 64'PO#        '
640cAA                                      DSPATR(HI)
640cAA                                      DSPATR(UL)
640cAA*
     A          R FKEY1
640bDA*****                            24  2'F3=Exit  F5=Refresh  F9=Select/Sor-
640bDA*****                                 t  F11=Next view  F12=Cancel  F21=P-
640bDA*****                                 rint list'
640bMA                                 24  2'F3=Exit F5=Refresh F9=Select/Sort -
640bMA                                      F11=Next view F12=Cancel F21=Print -
640bMA                                      F22=File'
     A                                      COLOR(BLU)
     A*
     A*         R FKEY1
     A          R MKEY1
640bAA                                      OVERLAY
     A            W1MSG1        75A  O 23  3
640bAA  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A          R WINDOW1
     A                                      WINDOW(4 20 18 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order . . . .'
     A            W1SORT         1A  B  3 15DSPATR(HI)
     A                                  3 20'Ascending, Descending'
     A                                  4  1'Order by  . .'
     A            W1OBY          1A  B  4 15DSPATR(HI)
     A                                  4 20'1 = Item/Slots/Orders'
     A                                  7  1'Warehouse . .'
     A            W1WHSE         3Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  8  1'P.O.  . . . .'
     A            W1PO           9A  B  8 15DSPATR(HI)
640cAA  40                                  DSPATR(PR)
     A                                  9  1'Item  . . . .'
     A            W1ITEM        15A  B  9 15DSPATR(HI)
640cAA  41                                  DSPATR(PR)
     A                                  9 32'*ALL'
640cAA                                 10  1'Expiration. .'
640cAA            W1EXPDT        6S 0B 10 15DSPATR(HI)
640cAA                                 10 25'MMDDYY'
640cAA                                 11  1'Cust. Order#.'
640cAA            W1CORD         7S 0B 11 15DSPATR(HI)
     A                                 17  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
