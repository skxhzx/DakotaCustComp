     A****************************************************************
     A*
     A* SL341PR   Virtual slot report.
     A* 08 July, 1998
     A* Roxanne Harrison
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    52
     A                                      DFT('Virtual Slot Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL341')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  92                                90
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Type')
     A                                     6
     A                                      DFT('Zone')
     A                                    11
     A                                      DFT('Slot')
     A                                    25
     A                                      DFT('Item')
     A*
     A                                    41
     A                                      DFT('Type')
     A                                    46
     A                                      DFT('Zone')
     A                                    51
     A                                      DFT('Slot')
     A                                    65
     A                                      DFT('Item')
     A*
     A                                    81
     A                                      DFT('Type')
     A                                    86
     A                                      DFT('Zone')
     A                                    91
     A                                      DFT('Slot')
     A                                   105
     A                                      DFT('Item')
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1DEF          2A  O     2
     A            C1ACTV         2A  O     7
     A            C1SLOT        12A  O    11
     A            C1ITEM        15A  O    25
     A*
     A            C2DEF          2A  O    42
     A            C2ACTV         2A  O    47
     A            C2SLOT        12A  O    51
     A            C2ITEM        15A  O    65
     A*
     A            C3DEF          2A  O    82
     A            C3ACTV         2A  O    87
     A            C3SLOT        12A  O    91
     A            C3ITEM        15A  O   105
     A*
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL341: End of Report ')
     A*
