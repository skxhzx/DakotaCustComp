     A****************************************************************
     A*
     A* IT473PR   Possible Slot Consolidation Report
     A* 29 September, 1999
     A* Roxanne Harrison
     A*
     A*  Revisions
415  A*    03/26/01  RH   4.15
     A*      - Print item description, Change detail 1 to detail 2.
500  A*    05/06/05  RLP  5.00
     A*      - Moved TYPE field down into DETAIL1 record to allow
     A*        for the logic change in RPG program.
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    59
     A                                      DFT('Possible Slot Consolidation')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT473')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
500 DA*           TYPE          11A  O    63
     A  91                                75
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                    18
     A                                      DFT('-----------------------')
     A                                    41
     A                                      DFT('FROM -----------------------')
     A                                    73
     A                                      DFT('-----------------------')
     A                                    95
     A                                      DFT('- TO -----------------------')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Item')
     A                                    18
     A                                      DFT('Slot       ')
     A                                    33
     A                                      DFT('TieHigh')
     A                                    43
     A                                      DFT('Slot Qty')
     A                                    53
     A                                      DFT('Expire')
     A                                    63
     A                                      DFT('Entered')
     A                                    73
     A                                      DFT('Slot       ')
     A                                    88
     A                                      DFT('TieHigh')
     A                                    97
     A                                      DFT('Empty Qty')
     A                                   108
     A                                      DFT('Expire')
     A                                   118
     A                                      DFT('Entered')
     A*
     A* Print detail line
     A*
415 AA          R DETAIL1
     A                                      SPACEA(001)
415 AA            WKITEM    R        O     1
     A                                      REFFLD(WKITEM *LIBL/IT471)
415 AA            ITDESC    R        O    18
     A                                      REFFLD(ITDESC *LIBL/PIRITEM)
500 AA            TYPE          11A  O    58
     A*
415 DA*         R DETAIL1
415 MA          R DETAIL2
     A                                      SPACEA(001)
415 DA* 92        WKITEM    R        O     1
415 DA*                                     REFFLD(WKITEM *LIBL/IT471)
     A            SVDISP    R        O    18
     A                                      REFFLD(WKDISP *LIBL/IT471)
     A*
     A            SVTIHI    R        O    33EDTCDE(3)
     A                                      REFFLD(WKTIHI *LIBL/IT471)
     A            SVSQTY    R        O    43EDTCDE(3)
     A                                      REFFLD(WKSQTY *LIBL/IT471)
     A*
     A            SVEXDT         6S 0O    53EDTCDE(Y)
     A            SVENDT         6S 0O    63EDTCDE(Y)
     A            WKDISP    R        O    73
     A                                      REFFLD(WKDISP *LIBL/IT471)
     A*
     A            WKTIHI    R        O    88EDTCDE(3)
     A                                      REFFLD(WKTIHI *LIBL/IT471)
     A            WKEQTY    R        O    98EDTCDE(3)
     A                                      REFFLD(WKSQTY *LIBL/IT471)
     A*
     A            WKEXDT         6S 0O   108EDTCDE(Y)
     A            WKENDT         6S 0O   118EDTCDE(Y)
     A*
     A*
     A* Print totals
     A*
     A*         R TOTAL1
     A*                                     SPACEB(001)
     A*                                    1
     A*                                     DFT('Total items for')
     A*           T#DESC         5A  O    +1
     A*                                   +1
     A*                                     DFT(':')
     A*           T#CLS          5S 0O    25
     A*                                     EDTCDE(Z)
     A*
     A*         R TOTAL2
     A*                                     SPACEB(001)
     A*                                    1
     A*                                     DFT('Total items printed')
     A*                                   +2
     A*                                     DFT(':')
     A*           T#CLS          5S 0O    25
     A*                                     EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT473: End of Report ')
     A*
