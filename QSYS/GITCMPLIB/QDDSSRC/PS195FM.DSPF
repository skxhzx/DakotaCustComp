     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS195FM   Pallet-View RF Mini Screen
     A*  10 October, 2018
     A*  Ginger Andrews
     A*
     A*  Revisions
     A*
     A*    10/10/18  GJA  7.20
     A*      - Created.
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'PS195.01'
     A                                  1 12'Pallet Info.'
     A                                      DSPATR(HI)
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  8
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'Pallet:'
     A            W1PLT         10A  B  4 10
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            W1ITEM        15A  O  6  2
     A            W1SQTY         7  0H
     A            W1AQTY         7  0H
     A            W1QTY         10A     6 18
     A            W1DESC        30A  O  6 29
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PS195.02'
     A                                  2  2'RouteID'
     A                                      DSPATR(HI)
     A            W1RTID         5A  O  2 10
     A                                  2 17'Rte'
     A                                      DSPATR(HI)
     A            W1RTE          5A  O  2 21
     A                                  3  2'PalletID'
     A                                      DSPATR(HI)
     A            W1PLID         7A  O  3 11
     A                                  3 19'Plt#'
     A                                      DSPATR(HI)
     A            W1PLNO         7A  O  3 24
     A            W1OPCS         5  0H
     A                                  4  2'Qty'
     A                                      DSPATR(HI)
     A            W1PCS         11A  O  4  6
     A            W1SPCS         5  0H
     A            W1SLT         30A  O  4 20
     A                                  5  2'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 18'Short     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 29'Description'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A                                  1  2'F3=Exit F12=Previous'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
