     A*%%TS  SD  19970711  095525  HEMANT      REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  RH170FM  View orders for a route - Display
     A*  10 July, 1997
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
414 AA*    06/30/00  DAS  4.14
     A*      - Changed indicator for SFLNXTCHG from 85 to 21.
     A*        Re: Program never changed to use 85 instead of 21.
     A*
510 AA*    02/21/07  MLB  5.10
     A*      - Changed DSPSFC format to extend underline for
     A*        customer number column heading.
510aAA*    12/19/07  RBD  5.10a
     A*      - Fix: added output of route to view 3, vw3sfl.
     A*
520 AA*    01/22/09  CWM  5.20
     A*      - Enh: Change stop to 5 Digit numeric load from rtestp
     A*             with host stop
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH170 '
     A                                  1 32'Orders for a Route'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970710  111234  HEMANT      REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A*
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*                                 2 48'Change to warehouse . . .'
     A*           PTINP1         3Y 0B  2 74EDTCDE(4)
     A* 21                                  DSPATR(RI)
     A* 01                                  DSPATR(PC)
     A*
     A                                  3  2'Route:'
     A            $KRTE          5   O  3  9
     A                                  3 48'Position to order . . . .'
     A            PTINP2         7Y 0B  3 74EDTCDE(4)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OHCORD    R        O 11  7REFFLD(OHREC/OHCORD *LIBL/ORDH)
     A                                      EDTCDE(Z)
     A            OHCUST    R        O 11 15REFFLD(OHREC/OHCUST *LIBL/ORDH)
     A                                      EDTCDE(Z)
     A            OHCNAM    R        O 11 27REFFLD(OHREC/OHCNAM *LIBL/ORDH)
520 DA**          OHSTOP    R        O 11 70REFFLD(OHREC/OHSTOP *LIBL/ORDH)
520 DA*           DSPSTP         3  0O 11 70
520 MA            DSPSTP         5Y 0O 11 68EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
520 DA*                                10 69'Stop'
520 MA                                 10 68' Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Order #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
510 MA                                 10 15'Customer # '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Name                              -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OHTRUK    R        O 11  7REFFLD(OHREC/OHTRUK *LIBL/ORDH)
     A            OHCPO     R        O 11 18REFFLD(OHREC/OHCPO *LIBL/ORDH)
     A            OHRTE     R        O 11 35REFFLD(OHREC/OHRTE *LIBL/ORDH)
     A            OHORD     R        O 11 45REFFLD(OHREC/OHORD *LIBL/ORDH)
     A                                      EDTCDE(Z)
     A            W1STAT        10A  O 11 55
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Truck     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 18'Customer PO #  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35'Route ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 45'Order ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 55'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
510aAA            $KRTE          5   O 11  7
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19970711  095525  HEMANT      REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  2  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  3  2COLOR(BLU)
     A*           OPTLN2        75A  O  4  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
