     A****************************************************************
     A*
     A* IT318BPR  Item activity report (Summary)
     A* 07 December, 2000
     A* Roxanne Harrison
     A*
     A* 12-07-00  RH   4.15
     A*  Created
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A*
     A                                   +42
     A                                      DFT('Item Activity Report')
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O   +43
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   124
     A                                      DFT('IT318B')
     A            WHSE          30A  O    52
     A                                      SPACEB(001)
     A                                    58'By Item/Date'
     A                                      SPACEB(001)
     A                                    50'Item:'
     A                                      SPACEB(001)
     A            ITEM          15A  O    56
     A                                      SPACEA(001)
     A*
     A*                                     '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Item Number')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    47
     A                                      DFT('Date')
     A                                      UNDERLINE
     A                                    57
     A                                      DFT('Adj Qty')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('Inv Qty')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('Pick Qty')
     A                                      UNDERLINE
     A                                    90
     A                                      DFT('Rcv Qty')
     A                                      UNDERLINE
     A                                   101
     A                                      DFT('Rpl Qty')
     A                                      UNDERLINE
     A                                   112
     A                                      DFT('Tfr Qty')
     A                                      UNDERLINE
     A                                   122
     A                                      DFT('Rtn Qty')
     A                                      UNDERLINE
     A*
     A* Print detail
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A*                                     SPACEA(001)
     A  92        WKITEM    R        O     1
     A                                      REFFLD(WKREC/WKITEM *LIBL/IT313A)
     A  92        WKDESC    R        O    17
     A                                      REFFLD(WKREC/WKDESC *LIBL/IT313A)
     A  91        W1DATE         6S 0O    47
     A                                      EDTCDE(Y)
     A            AQTY          10A  O    57
     A            IQTY          10A  O    68
     A            OQTY          10A  O    79
     A            PQTY          10A  O    90
     A            RQTY          10A  O   101
     A            TQTY          10A  O   112
     A            NQTY          10A  O   123
     A*           T#ATOT         7S 0     64EDTCDE(K)
     A                                      SPACEA(001)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            AQTY          10A  O    57
     A            IQTY          10A  O    68
     A            OQTY          10A  O    79
     A            PQTY          10A  O    90
     A            RQTY          10A  O   101
     A            TQTY          10A  O   112
     A            NQTY          10A  O   123
     A*           T#ATOT         7S 0     64EDTCDE(K)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT318B: End of Report')
     A*
