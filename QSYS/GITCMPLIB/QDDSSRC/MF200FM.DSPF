     A*%%TS  SD  20071002  142025  LYNNM       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  MF125FM    Manufacturer code decipher maintenance - test
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*    09/25/07  DAS  5.10
     A*      - Created
510aAA*    10/02/07  LMC  5.10a
     A*      - Fix: Changed mfr# field size to agree with vendor size
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  20071002  142025  LYNNM       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'MF200.01'
     A                                  1 28'Manufacturer Code Decipher'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11 30'123456789012345'
     A                                 12  4'Code to decipher . . . .'
     A            W1CODE        15A  B 12 30
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  6  4'Mfr number . . . . . . .'
     A                                  7  4'Mfr item number  . . . .'
     A                                  8  4'Dakota item number . . .'
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A            W1MFR#        11Y 0O  6 30EDTCDE(4)
     A            W1MFRITEM     15A  O  7 30
     A            W1DKTITEM     15A  O  8 30
     A                                 10 33'Position'
     A            W1MSG         60A  O 14 11
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*         R SCREEN1
     A          R SCREEN1A
     A*%%TS  SD  20071002  142025  LYNNM       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'MF200.01A'
     A                                  1 13'Code Decipher'
     A                                      DSPATR(HI)
     A                                  5 13'123456789012345'
     A            W1CODE        15A  B  6 13
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  2  2'Mfr#'
     A                                  3  2'Mfr item'
     A                                  4  2'Dakota item'
     A                                  8  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A            W1MFR#        11S 0O  2  7
     A            W1MFRITEM     15A  O  3 11
     A            W1DKTITEM     15A  O  4 14
     A                                  6  2'Enter Code'
     A            W1MSG         60A  O  7  2
