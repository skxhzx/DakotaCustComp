     A*%%TS  SD  20080205  103249  JEFF        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LB310FM   Rack Labels
     A*  28 December,  2007
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
     A*    12/28/07  JCJ  5.10
     A*      - Date Created.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20080205  103249  JEFF        REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LB310.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse. . . .'
     A            W1WHSE         3Y 0B  7 21EDTCDE(4)
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  4'Department . . .'
     A            W1WHDP         5   B  8 21
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Slot range . . .'
     A            W1FSLOT       12A  B  9 21
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 34'to'
     A            W1TSLOT       12A  B  9 37
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Slot type. . . .'
     A            W1STYPE        1A  B 10 21
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49' P=Pick, O=Overflow, B=Both '
     A                                 11  4'Side . . . . . .'
     A            W1SIDE         1A  B 11 21
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 49' O=Odd, E=Even, B=Both '
     A                                 12  4'Level range. . .'
     A            W1FLEV         2Y 0B 12 21
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 12 24'to'
     A            W1TLEV         2Y 0B 12 27
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 12 49' 1 - 99 '
     A                                 13  4'Label type . . .'
     A            W1LTYPE        1A  B 13 21
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 50'H=Horizontal, U=Upright'
     A                                 15  4'Device . . . . .'
     A            W1DEV         10A  B 15 21
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  4'Form . . . . . .'
     A            W1FORM        10A  B 16 21
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
