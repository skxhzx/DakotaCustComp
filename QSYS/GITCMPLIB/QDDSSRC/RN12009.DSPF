     A*%%TS  SD  20041104  154340  BFC2        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RN120FM   CUSTOMER RETURNS - Change screen
     A*  09 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
413aAA*    10/14/99  MLB  4.13a
     A*      - Added PF17 key to SCREEN1,SCREEN2 panels View
     A*        receiving log.
415aAA*  02/07/01  RH   4.15c
     A*    - Add Label field to screen 1.
415cAA*  02/07/01  RH   4.15c
     A*    - Add "Directed Put Away" to screen 1 head.
415dAA*  03/30/01  RH   4.15d
     A*    - Add UPC field to screen 1.
416bAA*  10/30/01  RH   4.16a
     A*    - Add Mfg code/vendor item# to screen1.
     A*    - Add Dispositon, Impact inventory, Use label for putaway,
     A*      Invoice#, Mfg code, and UPC to screen2.
     A*    - Change heading position on screen2 like standard look.
     A*    - Change screen3 to screen4, add new screen3.
416cAA*  11/14/01  RH   4.16c
     A*    - *IN55 to protect/nondisplay fields for QUICK returns.
416dAA*  02/04/02  RH   4.16d
     A*    - Change Impact inventory to Put Away.
     A*    - Change W2FLBL to W2ULBL use label for putaway.
     A*    - Remove User from screen.
416gAA*  03/21/02  RH   4.16g
     A*    - Make F17 Receiving log inactive.
     A*    - Add F9 Breakdown UOM.
416hAA*  06/13/02  MLB  4.16h
     A*    - Change edit code from 3 to 4 for catch weight.
416iAA*  06/17/02  RH   4.16i
     A*    - Add W1RORD (Y,N) to return whole order.
     A*    - Add SC4 to return whole order.
416mAA*    09/09/02  RH   4.16m
     A*      - Move manufacturer date to screen 2.
     A*      - Move corresponding item to screen 1.
500aAA*    11/09/04  RH   500a
     A*      - Add screen 6 to enter Cust# & order#, enable "redo" ord
530aAA*    11/20/09  RH   530a
     A*      - Enh: Add memo to Screen4 for return whole order.
530bAA*    02/02/10  RH   530a
     A*      - Enh: Add Customer # for EGF,
530C A*  05/14/10  JCJ  5.30a
     A*    - Enh:  Increase label to 10 for TRAX processing.
640a A*  01/09/12  GJA/RH 6.40a
     A*    - Enh:  Change custom SFGb change to standard 640a.
     A*      Rename format to RN12002
640b A*  07/18/12  LMC    6.40b
     A*    - Enh:  Added f11 Decipher code.
     A*      Rename format to RN12003
650  A*  01/22/13  RBD    6.50
     A*    - Fix:  Mod SGCa used *in42 to pos curs, but 42 was already
     A*      being used to pos curs to label# ... pos curs on label#
     A*      was changed to 32 and as a result was causing the cursor
     A*      to reposition on customer order# rather than label#.
     A*      Reverted back to using 42 for label#, and changed SGCa
     A*      to use 32 instead of 42.
     A*
650aAA*  06/13/13  LMC    6.50a
     A*    - Enh:  Renamed display file from RN12004 to RN12005
     A*      This was required because of changes CSPb on client box
     A*      source member RN12001 that were never brought back to dev.
650b A*  11/09/12  LMC    6.50b
     A*    - Enh:  Added GS1/UCI code.
     A*      Rename format to RN12006
     A*      Added lot code.
     A*      Added SCREEN7.
     A*      Added SCREEN8.
     A*      Added SCREEN9.
650c A*  10/31/13  LMC    6.50c
     A*    - Enh:  Added PTI entry to screen3
     A*      Rename format to RN12007
650d A*  05/14/15  RBD    6.50d
     A*    - Enh:  Chg customer order number to 15 alpha from 7 numeric
     A*      on screen1. At this time won't change it for quick returns.
     A*      Only Cash Wa will be allowed to enter alphas for customer
     A*      order number, all others will still enter numbers.
     A*    - Rename format to RN12008
700a A*  07/13/15  RH     7.00a
     A*    - Fix:  Rename format to RN12009 Remove check ER from LABEL
     A*      field on screen1.  Mocha was reading field fill for TRAX  .
     A*      and enter & scanner is coded for enter.  RN250 reading    .
     A*      second enter before any data was keyed, causing error.    .
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Springfield Grocery
SGCaAA*    12/18/09  RH   SGCa
     A*      - Position cursor to order #, not label #.
     A*
     A*
     A*  Custom Revisions
CSPbAA*  02/28/13  RTR  CSP   P#01053
     A*  - Enh: Set default disposition from return code.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A            W1SCRN         8   O  1  2
     A            WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  5 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
416d A*                                 9  4'User id . . . . . . . . .'
416d A*           W1USER        10A  B  9 31
416d A* 24                                  DSPATR(RI)
416d A*N24N90                               DSPATR(UL)
416d A* 04
416d A*N32                                  DSPATR(PC)
416d A* 90                                  DSPATR(PR)
416d A*N24N90                               DSPATR(HI)
     A                                  8  4'Customer order #  . . . .'
     A  55                                  DSPATR(ND)
650dDA**          W1ORD          7Y 0B  8 31
650dMA            W1ORDA        15A  B  8 31
     A  55                                  DSPATR(ND)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
SGCaDA* 07                                  DSPATR(PC)
SGCaAA  07
650 DA*O 42                                  DSPATR(PC)
650 MAO 32                                  DSPATR(PC)
     A  55
     A  90                                  DSPATR(PR)
650dDA**                                    EDTCDE(4)
416i A                                  9  4'Return whole order(Y,N) .'
416i A            W1RORD         1A  B  9 31
416i A  24                                  DSPATR(RI)
416i A N24N90                               DSPATR(UL)
416i A  04
416i A N32                                  DSPATR(PC)
416i A  90                                  DSPATR(PR)
416i A N24N90                               DSPATR(HI)
530b A                                 10  4'Customer #  . . . . . . .'
530b A  56                                  DSPATR(ND)
530b A            W1CNUM         7Y 0B 10 31
530b A  56                                  DSPATR(ND)
530b A N35                                  DSPATR(HI)
530b A  35                                  DSPATR(RI)
530b A N35N90                               DSPATR(UL)
530b A  15                                  DSPATR(PC)
530b A  56
530b A  90                                  DSPATR(PR)
530b A                                      EDTCDE(4)
415cAA                                 11  4'-ENTER ONE-'
415 AA                                 12  4'Label # . . . . . . . . .'
530cD ***         W1LBL#         7Y 0B 12 31CHECK(ER)
700aDA***         W1LBL#B       10   B 12 31CHECK(ER)
700aMA            W1LBL#B       10   B 12 31
415 AA  22                                  DSPATR(RI)
415 AA N22N90                               DSPATR(UL)
415 AA  02
416mDA**32                                  DSPATR(PC)
SGCaDA*O 42                                  DSPATR(PC)
650 DA*O 32                                  DSPATR(PC)
650 MAO 42                                  DSPATR(PC)
415 AA  90                                  DSPATR(PR)
415 AA N22N90                               DSPATR(HI)
530cD ***                                   EDTCDE(4)
415dDA*                                13  4'-OR-'
650bDA*                                13  4'UPC code  . . . . . . . .'
650bMA                                 13  4'Scan UCI  . . . . . . . .'
650bDA*           W1UPC         50A  B 13 31
650bMA            W1UCI        100A  B 13 31
415dAA                                      CHECK(ER)
415d A N25                                  DSPATR(HI)
415dAA  25                                  DSPATR(RI)
415dAA N25N90                               DSPATR(UL)
415dAA  05                                  DSPATR(PC)
415dAA  90                                  DSPATR(PR)
650bDA*                                14  4'Item number . . . . . . .'
650bDA*           W1ITEM        15A  B 14 31
650bMA                                 15  4'Item number . . . . . . .'
650bMA            W1ITEM        15A  B 15 31
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
650bDA*                                15  4'Mfg Code/Vendor item #. .'
650bDA*           W1VMFG        15A  B 15 31
650bMA                                 16  4'Mfg Code/Vendor item #. .'
650bMA            W1VMFG        15A  B 16 31
416b A N26                                  DSPATR(HI)
416b A  26                                  DSPATR(RI)
416b A N26N90                               DSPATR(UL)
416b A  06                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
     A                                  6  4'Printer device  . . . . .'
     A            W1DEV         10A  B  6 31
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                  7  4'Printer form  . . . . . .'
     A            W1FORM        10A  B  7 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
416gMA                                      2=Cancel  '
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
416gAA                                      CF09
416gDA*                                     CF17
     A  98                                  ALARM
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A**                                8  4'User id . . . . . . . . .'
     A**          W1USER        10A  O  8 31
     A                                  5  6'Order:'
     A            W1ORD          7Y 0O  5 13EDTCDE(4)
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A**                                6  4'Printer device  . . . . .'
     A**          W1DEV         10A  O  6 31
     A**                                7  4'Printer form  . . . . . .'
     A**          W1FORM        10A  O  7 31
     A                                  9  4'Quantity returned . . . .'
     A            W1QT01         5Y 0B  9 31EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1UM1          5A  O  9 37
     A            W1QT02         3Y 0B  9 45EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N31N90                               DSPATR(UL)
     A  94                                  DSPATR(ND)
     A  94
     AO 90                                  DSPATR(PR)
     A            W1UM2          5A  O  9 49
     A  94                                  DSPATR(ND)
     A            W1QT03         3Y 0B  9 57EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93
     AO 90                                  DSPATR(PR)
     A            W1UM3          5A  O  9 61
     A  93                                  DSPATR(ND)
     A                                 10  4'Catch weight returned . .'
     A            W1CWTR         9Y 2B 10 31
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
416h A                                      EDTCDE(4)
     A N33N90N95                            DSPATR(HI)
     A                                 11  4'Reason code . . . . . . .'
     A            W1CODE         2A  B 11 31
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416b A                                 12  4'Dispositon code . . . . .'
416b A            W2DCDE         5A  B 12 31
CSPbDA*N27                                  DSPATR(HI)
CSPbMA N27N89                               DSPATR(HI)
416b A  27                                  DSPATR(RI)
416b A N27N90                               DSPATR(UL)
416b A  07                                  DSPATR(PC)
CSPbDA* 90                                  DSPATR(PR)
CSPbAA  89
CSPbAAO 90                                  DSPATR(PR)
416b A                                 13  4'Use label for putaway . .'
416dDA**          W2FLBL         1A  B 14 31
416dMA            W2ULBL         1A  B 13 31
416b A N28                                  DSPATR(HI)
416b A  28                                  DSPATR(RI)
416b A N28N90                               DSPATR(UL)
416b A  08                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
416b A                                 14  4'Invoice # . . . . . . . .'
416b A            W2INV#         9A  B 14 31
416b A  29                                  DSPATR(RI)
416b A N29N90                               DSPATR(UL)
416b A  09                                  DSPATR(PC)
416b A  90                                  DSPATR(PR)
     A                                 15  4'Transaction number  . . .'
     A            W1TRN#         7Y 0B 15 31
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
416m A                                 16  3'-ENTER ONE-'
416m A                                 17  3'Corresponding item . . . .'
416m A            W2CITM        15A  B 17 31
416m A  23                                  DSPATR(RI)
416m A N23N90                               DSPATR(UL)
416m A  03                                  DSPATR(PC)
416m A  90                                  DSPATR(PR)
416m A                                 18  3'Corsp. UPC Code  . . . . .'
416m A            W2UPC         50A  B 18 31CHECK(ER)
416m A*N25                                  DSPATR(HI)
416m A  25                                  DSPATR(RI)
416m A N25N90                               DSPATR(UL)
416m A  05                                  DSPATR(PC)
416m A  90                                  DSPATR(PR)
416m A                                 19  3'Cor. Mfg code/Vend Item# .'
416m A            W2VMFG        15A  B 19 31
416m A*N26                                  DSPATR(HI)
416m A  26                                  DSPATR(RI)
416m A N26N90                               DSPATR(UL)
416m A  06                                  DSPATR(PC)
416m A  90                                  DSPATR(PR)
416b A**                               18  4'Corresponding item  . . .'
416b A**          W1CITM        15A  B 18 31
416b A**35                                  DSPATR(RI)
416b A*N35N90                               DSPATR(UL)
416b A* 15                                  DSPATR(PC)
416b A* 90                                  DSPATR(PR)
416b A**                               19  4'Memo  . . . . . . . . . .'
416b A*           W1MEMO        30A  B 19 31
416b A* 27                                  DSPATR(RI)
416b A*N27N90                               DSPATR(UL)
416b A* 07                                  DSPATR(PC)
416b A* 90                                  DSPATR(PR)
416b A*                                     CHECK(LC)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
416gAA                                      =UOM  F12=Cancel '
416gDA*                                     2=Cancel  F17=View rcv log'
     A                                      COLOR(BLU)
     A            W2SCRN         8   O  1  2
     A            WOHEAD        60   O  1 11DSPATR(HI)
     A                                  2 33'Directed Put Away'
     A          R SCREEN3
     A*%%TS  SD  20011031  092817  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
640bAA                                      CF11
416gDA*                                     CF17
650cAA                                      CF20
     A  98                                  ALARM
     A            W3SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Directed Put Away'
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A                                  5  6'Order:'
     A            W1ORD          7Y 0O  5 13EDTCDE(4)
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
416dDA**                                9  3'Impact Inventory . . . . .'
416dMA                                  9  3'Put Away (Y,N) . . . . . .'
     A            W3IINV         1A  B  9 31
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  3'Memo . . . . . . . . . . .'
     A            W3MEMO        30A  B 10 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
416m A                                 11  3'Expiration date . . . . .'
416m A            W3EXPD         6Y 0B 11 31
416m A  25                                  DSPATR(RI)
416m A N25N90                               DSPATR(UL)
416m A  05                                  DSPATR(PC)
416m A  96                                  DSPATR(ND)
416m A  96
650bAAO 83
416m AO 90                                  DSPATR(PR)
416m A                                      EDTCDE(3)
416m A N25N90N96                            DSPATR(HI)
416m A                                 13  3'Manufacturing date  . . . .'
416m A            W3MFDT         6Y 0B 13 31EDTCDE(4)
416m A  32                                  DSPATR(RI)
416m A  87                                  DSPATR(ND)
416m A N32N90                               DSPATR(UL)
416m A  12                                  DSPATR(PC)
650bAA  84
650bDA* 87                                  DSPATR(PR)
650bMAO 87                                  DSPATR(PR)
416m A N32N90N87                            DSPATR(HI)
650bAA                                 14  3'Lot Code. . . . . . . . . .'
650bAA  88                                  DSPATR(ND)
650bAA            W3LOTC        30   B 14 31
650bAA  88                                  DSPATR(ND)
650bAA  85
650bAAO 88                                  DSPATR(PR)
650cAA                                 15  3'PTI Code. . . . . . . . . .'
650cAA  81                                  DSPATR(ND)
650cAA            W3PTI          4Y 0B 15 31
650cAA  81                                  DSPATR(ND)
650cAA  81                                  DSPATR(PR)
650cAA  41                                  DSPATR(RI)
650cAA  41                                  DSPATR(PC)
650cAA N41N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
640bDA*                                     2=Cancel'
650cDA*                                     1=Mfr Code  F12=Cancel'
650cMA                                      1=Mfr Code  F12=Cancel  F20=Ovr'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  3  2'Warehouse:'
     A            WOWHSE        40A  O  3 13
     A                                  5  6'Order:'
     A            W1ORD          7Y 0O  5 13EDTCDE(4)
     A                                  4  7'Item:'
     A            WOITEM        49A  O  4 13
     A                                 12  4'Reason code . . . . . . .'
     A            W4CODE         2A  B 12 31
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416b A                                 13  4'Dispositon code . . . . .'
416b A            W4DCDE         5A  B 13 31
CSPbDA*N27                                  DSPATR(HI)
CSPbMA N27N89                               DSPATR(HI)
416b A  27                                  DSPATR(RI)
416b A N27N90                               DSPATR(UL)
416b A  07                                  DSPATR(PC)
CSPbDA* 90                                  DSPATR(PR)
CSPbAA  89
CSPbAAO 90                                  DSPATR(PR)
640a A                                 14  4'Invoice # . . . . . . . .'
640a A            W4INV#         9A  B 14 31
640a A  29                                  DSPATR(RI)
640a A N29N90                               DSPATR(UL)
640a A  09                                  DSPATR(PC)
640a A  90                                  DSPATR(PR)
640aDA*                                14  3'Memo . . . . . . . . . . .'
640aDA*           W4MEMO        30A  B 14 31
640aMA                                 15  3'Memo . . . . . . . . . . .'
640aMA            W4MEMO        30A  B 15 31
530a A  25                                  DSPATR(RI)
530a A N25N90                               DSPATR(UL)
530a A  05                                  DSPATR(PC)
530a A  90                                  DSPATR(PR)
530a A                                      CHECK(LC)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  -
416gAA                                      F12=Cancel '
     A                                      COLOR(BLU)
     A            W4SCRN         8   O  1  2
     A            WOHEAD        60   O  1 11DSPATR(HI)
     A                                  2 33'                 '
416bDA**        R SCREEN3
416biA          R SCREEN5
     A*%%TS  SD  19941107  211601  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'RN120.01'
     A                                  1 33'Customer Returns'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  6 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A                                  7  4'Order number  . . . . . .'
     A            W1ORD          7Y 0B  7 31
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  4'Item number . . . . . . .'
     A            W1ITEM        15A  B  8 31
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Quantity returned . . . .'
     A            W2QTY          7A  B  9 31
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Catch weight returned . .'
     A  95                                  DSPATR(ND)
     A            W1CWTR         9Y 2B 10 31
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 11  4'Expiration date . . . . .'
     A  96                                  DSPATR(ND)
     A            W1EXPD         6Y 0B 11 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 12  4'Reason code . . . . . . .'
     A            W1CODE         2A  B 12 31
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Memo  . . . . . . . . . .'
     A            W1MEMO        30A  B 13 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 15  4'Printer device  . . . . .'
     A            W1DEV         10A  B 15 31
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                 16  4'Printer form  . . . . . .'
     A            W1FORM        10A  B 16 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
500a A          R SCREEN6
     A*%%TS  SD  20041104  154340  BFC2        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A            W6SCRN         8   O  1  2
     A            W6HEAD        60   O  1 11DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Warehouse . . . . . . . .'
     A            W6WHSE         3Y 0B  5 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  4'Printer device  . . . . .'
     A            W6DEV         10A  B  6 31
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                  7  4'Printer form  . . . . . .'
     A            W6FORM        10A  B  7 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
     A                                  9  4'Customer #. . . . . . . .'
     A            W6CUST        11Y 0B  9 31
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 10  4'Customer order #  . . . .'
     A* 55                                  DSPATR(ND)
     A            W6ORD          7Y 0B 10 31CHECK(ER)
     A* 55                                  DSPATR(ND)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel  '
     A                                      COLOR(BLU)
650bAA          R SCREEN7
650bAA*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
650bAA                                      OVERLAY
650bAA                                      CLRL(23)
650bAA                                      CF04
650bAA                                      CF09
650bAA  98                                  ALARM
650bAA                                  1 72DATE
650bAA                                      EDTCDE(Y)
650bAA                                  7  2'Type changes, press Enter.'
650bAA                                      COLOR(BLU)
650bAA                                  3  2'Warehouse:'
650bAA            WOWHSE        40A  O  3 13
650bAA                                  5  6'Order:'
650bAA            W1ORD          7Y 0O  5 13EDTCDE(4)
650bAA                                  4  7'Item:'
650bAA            WOITEM        49A  O  4 13
650bAA                                 10  4'Quantity returned . . . .'
650bAA            W1QT01         5Y 0B 10 31EDTCDE(4)
650bAA N24N90                               DSPATR(HI)
650bAA  24                                  DSPATR(RI)
650bAA  04                                  DSPATR(PC)
650bAA N24N90                               DSPATR(UL)
650bAAO 90                                  DSPATR(PR)
650bAA            W1UM1          5A  O 10 37
650bAA            W1QT02         3Y 0B 10 45EDTCDE(4)
650bAA  31                                  DSPATR(RI)
650bAA  11                                  DSPATR(PC)
650bAA N31N90                               DSPATR(UL)
650bAA  94                                  DSPATR(ND)
650bAA  94
650bAAO 90                                  DSPATR(PR)
650bAA            W1UM2          5A  O 10 49
650bAA  94                                  DSPATR(ND)
650bAA            W1QT03         3Y 0B 10 57EDTCDE(4)
650bAA  30                                  DSPATR(RI)
650bAA  10                                  DSPATR(PC)
650bAA N30N90                               DSPATR(UL)
650bAA  93                                  DSPATR(ND)
650bAA  93
650bAAO 90                                  DSPATR(PR)
650bAA            W1UM3          5A  O 10 61
650bAA  93                                  DSPATR(ND)
650bAA                                 11  4'Catch weight returned . .'
650bAA            W1CWTR         9Y 2B 11 31
650bAA  33                                  DSPATR(RI)
650bAA N33N90                               DSPATR(UL)
650bAA  33                                  DSPATR(PC)
650bAA  95                                  DSPATR(ND)
650bAA  95
650bAAO 72
650bAAO 90                                  DSPATR(PR)
650bAA                                      EDTCDE(4)
650bAA N33N90N95                            DSPATR(HI)
650bAA                                 12  4'Reason code . . . . . . .'
650bAA            W1CODE         2A  B 12 31
650bAA N26                                  DSPATR(HI)
650bAA  26                                  DSPATR(RI)
650bAA N26N90                               DSPATR(UL)
650bAA  06                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 13  4'Dispositon code . . . . .'
650bAA            W2DCDE         5A  B 13 31
650bAA N27                                  DSPATR(HI)
650bAA  27                                  DSPATR(RI)
650bAA N27N90                               DSPATR(UL)
650bAA  07                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 14  4'Use label for putaway . .'
650bAA            W2ULBL         1A  B 14 31
650bAA N28                                  DSPATR(HI)
650bAA  28                                  DSPATR(RI)
650bAA N28N90                               DSPATR(UL)
650bAA  08                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 15  4'Invoice # . . . . . . . .'
650bAA            W2INV#         9A  B 15 31
650bAA  29                                  DSPATR(RI)
650bAA N29N90                               DSPATR(UL)
650bAA  09                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 16  4'Transaction number  . . .'
650bAA            W1TRN#         7Y 0B 16 31
650bAA  34                                  DSPATR(RI)
650bAA N34N90                               DSPATR(UL)
650bAA  14                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                      EDTCDE(4)
650bAA                                 17  3'-ENTER ONE-'
650bAA                                 18  3'Corresponding item . . . .'
650bAA            W2CITM        15A  B 18 31
650bAA  23                                  DSPATR(RI)
650bAA N23N90                               DSPATR(UL)
650bAA  03                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 19  3'Corsp. UPC Code  . . . . .'
650bAA            W2UPC         50A  B 19 31CHECK(ER)
650bAA  25                                  DSPATR(RI)
650bAA N25N90                               DSPATR(UL)
650bAA  05                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 20  3'Cor. Mfg code/Vend Item# .'
650bAA            W2VMFG        15A  B 20 31
650bAA  26                                  DSPATR(RI)
650bAA N26N90                               DSPATR(UL)
650bAA  06                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
650bAA                                      =UOM  F12=Cancel '
650bAA                                      COLOR(BLU)
650bAA            W7SCRN         8   O  1  2
650bAA            WOHEAD        60   O  1 11DSPATR(HI)
650bAA                                  2 33'Directed Put Away'
650bAA                                 09  4'Scan License. . . . . . . '
650bAA  73                                  DSPATR(ND)
650bAA            W7LCNS        15A  B 09 31
650bAA  43                                  DSPATR(RI)
650bAA N43N90                               DSPATR(UL)
650bAA  43                                  DSPATR(PC)
650bAA  90
650bAAO 80
650bAAO 73                                  DSPATR(PR)
650bAA  73                                  DSPATR(ND)
650bAA          R SCREEN8
650bAA*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
650bAA                                      OVERLAY
650bAA                                      CLRL(23)
650bAA*                                     CF04
650bAA                                      CF20
650bAA                                      CF22
650bAA  98                                  ALARM
650bAA            W8SCRN         8   O  1  2
650bAA            WOHEAD        60   O  1 11DSPATR(HI)
650bAA                                  1 72DATE
650bAA                                      EDTCDE(Y)
650bAA                                  3  2'Warehouse:'
650bAA            WOWHSE        40A  O  3 13
650bAA**                                8  4'User id . . . . . . . . .'
650bAA**          W1USER        10A  O  8 31
650bAA                                  5  6'Order:'
650bAA            W1ORD          7Y 0O  5 13EDTCDE(4)
650bAA                                  4  7'Item:'
650bAA            WOITEM        49A  O  4 13
650bAA                                 07  4'Scan UCI  . . . . . . . .'
650bAA            W1UCI        100A  B 07 31
650bAA                                      CHECK(ER)
650bAA N25                                  DSPATR(HI)
650bAA  25                                  DSPATR(RI)
650bAA N25N90                               DSPATR(UL)
650bAA  05                                  DSPATR(PC)
650bAA  90                                  DSPATR(PR)
650bAA                                 23  2'F3=Exit   F12=Cancel  F20=Override-
650bAA                                        F22=Complete License'
650bAA                                      COLOR(BLU)
650bAA          R SCREEN9
650bAA*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
650bAA                                      OVERLAY
650bAA                                      CLRL(23)
650bAA  98                                  ALARM
650bAA                                  1 72DATE
650bAA                                      EDTCDE(Y)
650bAA                                  7  2'Type changes, press Enter.'
650bAA                                      COLOR(BLU)
650bAA                                  3  2'Warehouse:'
650bAA            WOWHSE        40A  O  3 13
650bAA                                  5  6'Order:'
650bAA            W1ORD          7Y 0O  5 13EDTCDE(4)
650bAA                                  4  7'Item:'
650bAA            WOITEM        49A  O  4 13
650bAA                                 10  4'Quantity returned . . . .'
650bAA            W1QT01         5Y 0B 10 31EDTCDE(4)
650bAA                                      DSPATR(PR)
650bAA            W1UM1          5A  O 10 37
650bAA            W1QT02         3Y 0B 10 45EDTCDE(4)
650bAA  94                                  DSPATR(ND)
650bAA                                      DSPATR(PR)
650bAA            W1UM2          5A  O 10 49
650bAA  94                                  DSPATR(ND)
650bAA            W1QT03         3Y 0B 10 57EDTCDE(4)
650bAA  93                                  DSPATR(ND)
650bAA                                      DSPATR(PR)
650bAA            W1UM3          5A  O 10 61
650bAA  93                                  DSPATR(ND)
650bAA                                 11  4'Catch weight returned . .'
650bAA            W1CWTR         9Y 2B 11 31
650bAA  95                                  DSPATR(ND)
650bAA                                      DSPATR(PR)
650bAA                                      EDTCDE(4)
650bAA                                 12  4'Reason code . . . . . . .'
650bAA            W1CODE         2A  B 12 31
650bAA                                      DSPATR(PR)
650bAA                                 13  4'Dispositon code . . . . .'
650bAA            W2DCDE         5A  B 13 31
650bAA                                      DSPATR(PR)
650bAA                                 14  4'Use label for putaway . .'
650bAA            W2ULBL         1A  B 14 31
650bAA                                      DSPATR(PR)
650bAA                                 15  4'Invoice # . . . . . . . .'
650bAA            W2INV#         9A  B 15 31
650bAA                                      DSPATR(PR)
650bAA                                 16  4'Transaction number  . . .'
650bAA            W1TRN#         7Y 0B 16 31
650bAA                                      DSPATR(PR)
650bAA                                      EDTCDE(4)
650bAA                                 17  3'-ENTER ONE-'
650bAA                                 18  3'Corresponding item . . . .'
650bAA            W2CITM        15A  B 18 31
650bAA                                      DSPATR(PR)
650bAA                                 19  3'Corsp. UPC Code  . . . . .'
650bAA            W2UPC         50A  B 19 31CHECK(ER)
650bAA                                      DSPATR(PR)
650bAA                                 20  3'Cor. Mfg code/Vend Item# .'
650bAA            W2VMFG        15A  B 20 31
650bAA                                      DSPATR(PR)
650bAA                                 23  2'F3=Exit             F5=Refresh    -
650bAA                                            F12=Cancel '
650bAA                                      COLOR(BLU)
650bAA            W9SCRN         8   O  1  2
650bAA            WOHEAD        60   O  1 11DSPATR(HI)
650bAA                                  2 33'Directed Put Away'
650bAA                                 09  4'Scan License. . . . . . . '
650bAA  73                                  DSPATR(ND)
650bAA            W7LCNS        15A  B 09 31
650bAA                                      DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
650bAA          R CNLWDW
650bAA*%%TS  SD  20120907  103814  LYNNM       REL-V6R1M0  5761-WDS
650bAA                                      WINDOW(2 2 10 65)
650bAA                                      CF10
650bAA                                  1 23'Confirm Cancel/Delete'
650bAA                                      DSPATR(HI)
650bAA                                  4 30'DELETED'
650bAA                                      COLOR(RED)
650bAA            W7LCNS        15A  O  7 26DSPATR(HI)
650bAA                                  9 17'F10=Delete   F12=Continue Scanning'
650bAA                                      COLOR(BLU)
650bAA                                  5 17'If you wish to do this press F10.'
650bAA                                  6 12'Otherwise press F12 to scan to thi-
650bAA                                      s license'
650bAA                                  3 12'All barcodes scanned to this licen-
650bAA                                      se will be'
