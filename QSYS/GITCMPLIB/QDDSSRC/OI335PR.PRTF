     A*----------------------------------------------------------------
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  OI335PR    Import FTP orders - Detail Ver 9
     A*  March 18, 2019
     A*  Rod Roller
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
730  A*    03/18/19  RTR  7.30
     F*      - Created from OI285PR.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'OI335'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A          R HEAD2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    80
     A                                      'Stop'
     A                                    88
     A                                      'Truck'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD3
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A            OIHORDX       15A  O     2
     A                                      SPACEB(001)
     A            OIHTYP    R        O    +1
     A                                      REFFLD(OIHTYP IMPFORDH9)
     A            OIHCUS    R        O    +1
     A                                      REFFLD(OIHCUS IMPFORDH9)
     A            OIHCNM    R        O    +1
     A                                      REFFLD(OIHCNM IMPFORDH9)
     A            OIHRTE    R        O    +1
     A                                      REFFLD(OIHRTE IMPFORDH9)
     A            OIHEST    R        O    +1
     A                                      REFFLD(OIHEST IMPFORDH9)
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +1
     A                                      REFFLD(OIHTRK IMPFORDH9)
     A            HEDMSG        20A  O    98
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD4
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Original'
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Item'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 002
     A*%%CL 001
     A*%%***********************************************************************
     A          R DETAIL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDITM IMPFORDD9)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDQ1  IMPFORDD9)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDQ2  IMPFORDD9)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDQ3  IMPFORDD9)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDWH2 IMPFORDD9)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDOR2 IMPFORDD9)
     A                                      EDTCDE(4)
     A            OIDOIT    R        O    +2
     A                                      REFFLD(OIDOIT IMPFORDD9)
     A            DETMSG        20A  O   106
     A*%%***********************************************************************
     A          R DETAIL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            STSMSG        33A  O     6
     A                                    +2
     A                                      DFT('Host Route/Order#/Line#:')
     A            OIHRTE    R        O    +1
     A                                      REFFLD(OIHRTE IMPFORDH9)
     A                                    +1
     A                                      DFT('/')
     A            OIHORDX       15A  O    +1
     A            OIDSEQ    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIDSEQ IMPFORDD9)
     A                                    +4
     A                                      DFT('FSWMS RouteId/Order#:')
     A            PRTRTE    R        O    +1
     A                                      REFFLD(OHRTE  ORDH)
     A                                    +1
     A                                      DFT('/')
     A            PRTORD    R        O    +1
     A                                      REFFLD(OIDORD IMPFORDD9)
     A*%%***********************************************************************
     A*%%SS
     A*%%SN ITDESC     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
     A*%%SN OIDQ1      99999
     A*%%SN OIDQ2      99999
     A*%%SN OIDQ3      99999
     A*%%***********************************************************************
     A          R SUBTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ1          7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ2          7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ3          7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A          R GRNDTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer orders:'
     A                                      SPACEB(001)
     A            GRDORD         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail lines   :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid Cross dock P.O.s      -
     A                                         :')
     A                                      SPACEB(001)
     A            BADPO          7S 0O    39
     A                                      EDTCDE(J)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%SN GRDORD     9999999
     A*%%CL 001                   :
     A*%%SN GRDLIN     9999999
     A*%%CL 002
     A*%%SN PREOH      9999999
     A*%%CL 001
     A*%%SN PREOD      9999999
     A*%%CL 001
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(*libl/OI335PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(66       132      *N     )
     A*%%***********************************************************************
