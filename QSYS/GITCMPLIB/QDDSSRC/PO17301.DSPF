     A*%%TS  SD  20140716  113105  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO173FM   Reprint tags - Request screen
     A*  16 December 2005
     A*  Jeffrey Jahnke
     A*
650a A*    07/16/14  RBD  6.50a
     A*      - Enh: Added additional clam tag data.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20140716  113105  RETHA       REL-V6R1M0  5761-WDS
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO173.01'
     A                                  1 32'HACCP Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Reprint Tags'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'PO Number:'
     A            $PPO           9A  O  5 13
     A                                  6  2'Item     :'
     A            W1ITEM        15A  O  6 13
     A            WODESC        65A  O  7 13
     A                                  8  2'License  :'
     A            W1LIC         15A  O  8 13
     A                                  9  2'Harvest Date .................'
     A            W1HDTE         6  0O  9 33EDTCDE(Y)
     A                                 10  2'Harvest Location .............'
     A            W1HLOC        20A  O 10 33
     A                                 11  2'Original Shipper Cert# .......'
     A            W1CERT        20   O 11 33
     A                                 12  2'Product Wet Stored to Date ...'
     A            W1WDTE         6  0O 12 33EDTCDE(Y)
     A                                 13  2'Lot Number ...................'
     A            W1LOT         15   O 13 33
     A                                 15  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 17  4'Number of labels . . . .'
     A            W1LNBR         5Y 0B 17 29EDTCDE(Z)
     A                                 18  4'Printer device . . . . .'
     A            W1PRT         10A  B 18 29
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Form type  . . . . . . .'
     A            W1FORM        10A  B 19 29
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit             F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
