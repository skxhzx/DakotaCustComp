     A*%%TS  SD  20130725  160704  KENNETHE    REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT116FM   Item Maintenance   Repack Component Edits
     A*  07 February 2013
     A*  Kenneth Elder
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF12
     A                                      CF20
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20130423  143750  KENNETHE    REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            W1SITM2       15A  B 15  3
     A  40                                  DSPATR(RI)
     A  72                                  DSPATR(PR)
     A            W1SQTY1        3Y 0O 15 37EDTCDE(Z)
     A            W1SUM1         2A  O 15 41
     A            W1SQTY2        3Y 0O 15 50EDTCDE(Z)
     A            W1SUM2         2A  O 15 54
     A            W1SQTY3        3Y 0O 15 65EDTCDE(Z)
     A            W1SUM3         2A  O 15 69
     A            W1SDISP       12A  O 15 21
     A  41                                  DSPATR(RI)
     A            W1ARYREC       5S 0O 15 75DSPATR(ND)
     A            W1SITPE        1A  O 15 59DSPATR(ND)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20130725  160704  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0006)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'IT116.01'
     A                                  1 26'Repack Template Maintenance   '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            $PWHSE         3Y 0O  3 13EDTCDE(4)
     A                                  3 17'-'
     A            WHDESC        30A  O  3 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  3  2'Warehouse.'
     A                                  4  2'Item......'
     A                                  9  2'On-Demand Repack Slot .'
     A                                 10  2'Output Quantity . . . .'
     A                                  8  2'Repack type . . . . . .'
     A            W1RPTP         1A  B  8 26
     A  21                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            W1DISP        12A  B  9 26
     A  26                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            W1RQTY1        3Y 0B 10 26EDTCDE(Z)
     A  28                                  DSPATR(RI)
     A  18                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            W1RUM1         2A  O 10 30
     A            W1RQTY2        3Y 0B 10 34EDTCDE(Z)
     A  96                                  DSPATR(ND)
     A  96                                  DSPATR(PR)
     A  29N96                               DSPATR(RI)
     A  19                                  DSPATR(PC)
     A            W1RUM2         2A  O 10 38
     A  96                                  DSPATR(ND)
     A            W1RQTY3        3Y 0B 10 42EDTCDE(Z)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A  30N92                               DSPATR(RI)
     A  20                                  DSPATR(PC)
     A            W1RUM3         2A  O 10 46
     A  92                                  DSPATR(ND)
     A                                  8 42'1=Work Order   2=On_Demand'
     A                                 12  2'(To delete line, clear Input Item -
     A                                      Field.)'
     A                                      COLOR(BLU)
     A                                 13 20'On-Demand Slot'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 36'Normal'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 47'Breakdown 1'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 62'Breakdown 2'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2'Input Item     '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WODESC        50A  O  4 13
     A            W1CITM2       15A  B 14  3
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1CDISP       12A  B 14 21
     A  46                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1CUM1         2A  O 14 41
     A            W1CUM2         2A  O 14 54
     A            W1CUM3         2A  O 14 69
     A            W1CITPE        1A  O 14 59DSPATR(ND)
     A            W1CQTY1A       3A  B 14 37RANGE('0' '999')
     A                                      CHECK(AB)
     A            W1CQTY2A       3A  B 14 50RANGE('0' '999')
     A                                      CHECK(AB)
     A            W1CQTY3A       3A  B 14 65RANGE('0' '999')
     A                                      CHECK(AB)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R CMDREC
     A*%%TS  SD  20130221  111636  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3-Exit    F4-Lookup    F5-Refresh-
     A                                          F12-Cancel'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20130207  082530  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20130213  182731  KENNETHE    REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      LOCK
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R MORREC
     A*%%TS  SD  20130218  102902  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 71COLOR(WHT)
     A          R TYPMSG
     A*%%TS  SD  20130607  223114  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      TEXT('Explain changing Repack Type')
     A                                      WINDOW(7 7 10 30)
     A                                  4  1'will be blanked automatically.'
     A                                      COLOR(WHT)
     A                                  5  1'Flipping from Work Order to '
     A                                      COLOR(WHT)
     A                                  6  1'On-Demand will require entry'
     A                                      COLOR(WHT)
     A                                  7  1'of slot info per parent item'
     A                                      COLOR(WHT)
     A                                  3  1'Work Order all slot info'
     A                                      COLOR(WHT)
     A                                  2  1'If changing from On-Demand to'
     A                                      COLOR(WHT)
     A                                  9  1'F20 - Accept     F12 - Cancel'
     A                                      COLOR(BLU)
