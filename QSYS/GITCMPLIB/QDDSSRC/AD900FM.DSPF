     A*%%TS  SD  19940805  091323  HEMANT      REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  AD900FM    Inventory adjustment code loolup
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    10/29/01  DAS  4.16
     A*      - Add new 'System to Update' field.
     A*
     A*  Custom Revisions
     A*
PCK AA*    09/29/06  JCJ  PCK
     A*      - Enh: Added additional fields from SAPMOVE to PACKERS
     A*        screen.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19930621  153603  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'AD900.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'Inventory Adjustment Code Lookup'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19930527  171516  DAVE        REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ADCODE    R        O 11  8REFFLD(ADREC/ADCODE *LIBL/ADJCODE)
     A            ADDESC    R        O 11 14REFFLD(ADREC/ADDESC *LIBL/ADJCODE)
416 AA            W1SYS          6   O 11 48
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10 14'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Code '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416 AA                                  9 47'System'
416 AA                                      DSPATR(HI)
416 AA                                 10 47'to Update'
416 AA                                      DSPATR(HI)
416 AA                                      DSPATR(UL)
PCK AA*****************************************************************
PCK AA*
PCK AA* SUBFILE FOR LIST DISPLAY PACKERS
PCK AA*
PCK AA          R DSPSFLP                   SFL
PCK AA*%%TS  SD  19930527  171516  DAVE        REL-V2R1M1  5738-PW1
PCK AA  21                                  SFLNXTCHG
PCK AA            OPTION         1A  B 11  3
PCK AA  20                                  DSPATR(PC)
PCK AA N21N90                               DSPATR(UL)
PCK AA  21                                  DSPATR(RI)
PCK AA  90                                  DSPATR(PR)
PCK AA            ADCODE    R        O 11  8REFFLD(ADREC/ADCODE *LIBL/ADJCODE)
PCK AA            ADDESC    R        O 11 14REFFLD(ADREC/ADDESC *LIBL/ADJCODE)
PCK AA            W1SYS          6   O 11 48
PCK AA*
PCK AA            SMSAPC    R        O 11 57REFFLD(SMSAPC SAPMOVE)
PCK AA            SMMOVE    R        O 11 62REFFLD(SMMOVE SAPMOVE)
PCK AA            SMINDC    R        O 11 68REFFLD(SMINDC SAPMOVE)
PCK AA            SMCOST    R        O 11 71REFFLD(SMCOST SAPMOVE)
PCK AA                                      EDTCDE(Z)
PCK AA            SMPRFT    R        O 11 76REFFLD(SMPRFT SAPMOVE)
PCK AA                                      EDTCDE(Z)
PCK AA*
PCK AA          R DSPSFCP                   SFLCTL(DSPSFLP)
PCK AA                                      SFLSIZ(0010)
PCK AA                                      SFLPAG(0010)
PCK AA                                      CF06
PCK AA                                      ROLLDOWN
PCK AA                                      ROLLUP
PCK AA                                      OVERLAY
PCK AA  01                                  SFLDSP
PCK AA  01                                  SFLDSPCTL
PCK AA N01                                  SFLCLR
PCK AA                                 10  2'Opt'
PCK AA                                      DSPATR(HI)
PCK AA                                 10 14'Description                   '
PCK AA                                      DSPATR(UL)
PCK AA                                      DSPATR(HI)
PCK AA                                 10  7'Code '
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                  9 47'System'
PCK AA                                      DSPATR(HI)
PCK AA                                 10 47'to Update'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                  9 57'SAP'
PCK AA                                      DSPATR(HI)
PCK AA                                  9 62'SAP'
PCK AA                                      DSPATR(HI)
PCK AA                                  9 67'SAP'
PCK AA                                      DSPATR(HI)
PCK AA                                  9 71'SAP'
PCK AA                                      DSPATR(HI)
PCK AA                                  9 76'SAP'
PCK AA                                      DSPATR(HI)
PCK AA                                 10 57'Rsn'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                 10 62'Move'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                 10 67'Stk'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                 10 71'Cost'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
PCK AA                                 10 76'Prft'
PCK AA                                      DSPATR(HI)
PCK AA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19930222  110015  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 52'Position to code . . . .'
     A            PTINP1         2A  B  3 78
