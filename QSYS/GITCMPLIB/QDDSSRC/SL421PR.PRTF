     A****************************************************************
     A*
     A* SL421PR   Available pick slots by aisle
650aAA*           Added option to print overflow slots.
     A* 22 January, 2002
     A* Hemant Kapadia
     A*
     A* Created 4.16
     A*
640a A* 02/20/11  GJA   6.40a
     A*     -Enh:  Added flag to denote if there are virtuals
     A*            slots are associated.
     A*
650aAA* 05/02/13  RBD   6.50a
     A*     -Enh:  Added option to print overflow slots.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
650aDA**                                  48
650aMA            RPTITL        24A  O    48
650aDA**                                    DFT('Available Pick Slots')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL421')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Slot')
     A                                    14
     A                                      DFT('Desig')
     A                                    20
     A                                      DFT('Type')
     A                                    26
     A                                      DFT('Status')
     A*
     A                                    34
     A                                      DFT('Slot')
     A                                    48
     A                                      DFT('Desig')
     A                                    54
     A                                      DFT('Type')
     A                                    60
     A                                      DFT('Status')
     A*
     A                                    68
     A                                      DFT('Slot')
     A                                    81
     A                                      DFT('Desig')
     A                                    87
     A                                      DFT('Type')
     A                                    93
     A                                      DFT('Status')
     A*
     A                                   101
     A                                      DFT('Slot')
     A                                   114
     A                                      DFT('Desig')
     A                                   120
     A                                      DFT('Type')
     A                                   126
     A                                      DFT('Status')
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
640aAA            C1VSLT         1A  O     1
     A            C1SLOT        12A  O     3
     A            C1DESG         5A  O    16
     A            C1DEF          2A  O    22
     A            C1STAT         2A  O    28
     A*
640aAA            C2VSLT         1A  O    34
     A            C2SLOT        12A  O    37
     A            C2DESG         5A  O    50
     A            C2DEF          2A  O    56
     A            C2STAT         2A  O    62
     A*
640aAA            C3VSLT         1A  O    68
     A            C3SLOT        12A  O    71
     A            C3DESG         5A  O    83
     A            C3DEF          2A  O    89
     A            C3STAT         2A  O    95
     A*
640aAA            C4VSLT         1A  O   101
     A            C4SLOT        12A  O   104
     A            C4DESG         5A  O   116
     A            C4DEF          2A  O   122
     A            C4STAT         2A  O   128
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL421: End of Report ')
     A*
