     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR365PR    FTP Import Repack Templates - Exceptions
     A*  19 September 2013
     A*  Rod Roller
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650 AA*    09/19/13  RTR  6.50
     A*      - Created
650aAA*    01/07/14  JCJ  6.50a
     A*      - Fix: Corrected field definition for RTPITEM
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A            RDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +39
     A                                   122
     A                                      'Page'
     A            RTIME          6S 0O     1
     A                                      EDTWRD('0  :  :  ')
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   122
     A                                      'WR365'
     A                                     2
     A                                      'Repack Item    '
     A                                      SPACEB(002)
     A                                    19
     A                                      'Pack Size'
     A                                    35
     A                                      'Description'
     A                                    67
     A                                      'Parent Item    '
     A                                    84
     A                                      'Repack Slot '
     A                                    98
     A                                      'Message'
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A*
     A*%%***********************************************************************
     A          R DETAIL
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            RTITEM    R        O     2
650aMA                                      REFFLD(JIITEM *LIBL/IMPFREPAK2)
     A            ITPDSC    R        O    19
     A                                      REFFLD(ITPDSC *LIBL/PIRITEM)
     A            ITDESC    R        O    35
     A                                      REFFLD(ITDESC *LIBL/PIRITEM)
     A            RTPITM    R        O    67
     A                                      REFFLD(JIITEM *LIBL/IMPFREPAK2)
     A            RTPRPS    R        O    84
650aMA                                      REFFLD(JIDISP *LIBL/IMPFREPAK2)
     A            DETMSG        35A  O    98
     A*
     A*%%***********************************************************************
     A          R GRNDTTL
     A*%%***********************************************************************
     A                                     1
     A                                      SPACEB(002)
     A*
     A                                      'Grand Totals'
     A                                      UNDERLINE
     A                                     4
     A*
     A                                      SPACEB(002)
     A                                      'Total items accepted  :'
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Invalid item numbers  :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Item not a repack item:'
     A                                      SPACEB(001)
     A            RPKERR         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A                                     4
     A                                      'Repack slot not found :'
     A                                      SPACEB(001)
     A            SLTERR         7S 0O    +1
     A                                      EDTCDE(J)
     A*
     A*%%***********************************************************************
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('WR365: End of Report ')
     A*
