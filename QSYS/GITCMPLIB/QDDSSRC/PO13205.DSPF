     A*%%TS  SD  19980521  074028  HEMANT      REL-V3R1M0  5763-PW1
     A*%%EC
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO132FM   Close transaction
     A*  20 September 1999
     A*  Dave Sommerville
     A*
     A*  PO132FM
     A*
     A*  Revisions
412 AA*    09/07/99  RH   4.12
     A*      - Add "Directed Put away" to screen.
413aAA*    09/20/99  MLB  4.13a
     A*      - Added PF17 key to SCREEN1 panel.
413eAA*    02/09/00  RH   4.13e
     A*      - Add Pack and default tie/hi to screen.
     A*      - Add F8 to call tie/hi maint.
     A*
415aAA*    04/11/01  MLB  4.15a
     A*      - Add F9 to call catchweight entry program.
     A*      - Reformatted function key text.
     A*
416 AA*    09/24/01  DAS  4.16
     A*      - Revised lines displaying the item numbers.
     A*
416aAA*    09/26/01  DAS  4.16
     A*      - Changed catch weight desc line to variable.
     A*      - Added indicators 81 & 82 to control cw entry and display
     A*
416bAA*    10/27/01  DAS  4.16b
     A*      - Fix - increase catch weight to 9.2
     A*
417 AA*    04/15/03  MLB  4.17
     A*      - Enh - Increased length of UPC entry field to 50 and
     A*        renamed field W1UPCD.
417aAA*    05/13/03  MLB  4.17a
     A*      - Enh - Add entry field for lot code, W1LOTC.
417bAA*    01/20/04  RH   4.17b
     A*      - Enh:  Add Entered date entry field for MOVE IN po to
     A*              allow for FIFO rotation after product is moved in
     A*
500 AA*    04/02/04  DAS  5.00
     A*      - Enh:  Add *IN65 - Dpt Not Live.
510aAA*    10/01/07  LMC  5.10a
     A*      - Enh:  Add F11 - Date code Decipher
SVLaAA*    19/10/09  RH   SVLa
     A*      - Enh:  Add Ordered qty to screen 1.
640aAA*    10/19/12  LMC  6.40a
     A*      - Enh:  Added Brand field.
     A*      Created new display file PO13201 to replace PO132FM.
650aAA*    10/28/13  LMC  6.50a
     A*      - Enh:  Added PTI Entry field.
     A*      Created new display file PO13202 to replace PO13201.
650bAA*    03/10/14  LMC  6.50b
     A*     - Renamed screen format to PO23203.
     A*     - Added slot definition on screen right after ti/high
700aAA*    04/06/16  LMC  7.00a
     A*    - Enh: Adding logic for Metric.
     A*      Created new display file PO13204 to replace PO13203.
700bAA*    09/21/16  LMC  7.00b
     A*      - Enh:  Added screen1b to capture breakdown upc codes
     A*      Created new display file PO13205 to replace PO13204.
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  19980521  074028  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
700aDA*                                     CLRL(23)
700aMA                                      CLRL(21)
     A                                      CF04
700aAA N95                                  CF07
413eAA                                      CF08
415aAA N95                                  CF09
413 AA  55                                  CF17
     A                                      CF20
510aAA N66                                  CF11
     A  98                                  ALARM
     A                                  1  2'PO132.01'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
416 MA                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
416 MA                                  6  2'PO number . .'
416 MA            W1PO           9A  O  6 16
416 MA                                  6 60'Directed Put away'
416 MA                                  7  2'Warehouse . .'
416 MA            W1WHSE         3Y 0O  7 16EDTCDE(4)
416 MA            W1FDSC        30A  O  7 21
416 DA**                                9  2'Item number .'
416 AA            WOLBL         13   O  8  2
416 MA            WOITEM        65A  O  8 16
416 AA            WOLBLS        13   O  9  2
416 AA            WOSITM        65   O  9 16
     A            WODSC1        40A  O 10 16
     A            WODSC2        40A  O 11 16
     A                                 12  2'Home slot . .'
     A            WOSLOT        12A  O 12 16
413eAA            WOPDSC        15A  O 12 40
413eAA            WOTIE          4Y 0O 12 57EDTCDE(4)
413eAA                                 12 62'/'
413eAA            WOHI           2Y 0O 12 64EDTCDE(4)
650bAA            WOSDEF         2A  O 12 67
413eAA*                                12 45'NOT A FSWMS ITEM'
500 AA                                 13 45'DEPT NOT LIVE'
500 AA  65                                  DSPATR(ND)
413eAA                                 13 45'NOT A FSWMS ITEM'
     A  64                                  DSPATR(ND)
SVLaAA                                 13  2'Ordered:'
SVLaAA  63                                  DSPATR(ND)
SVLaAA            WOQOR         15A  O 13 11
SVLaAA  63                                  DSPATR(ND)
     A                                 14  3'UPC code  . . . . . . . . .'
417 DA*           W1UPC         20A  B 14 31CHECK(ER)
417 MA            W1UPCD        50A  B 14 31CHECK(ER)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 92                                  DSPATR(PR)
     A                                 15  3'Quantity received . . . . .'
     A            W1QRC1         5Y 0B 15 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 92                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UM1          5A  O 15 37
     A            W1QRC2         5Y 0B 15 44EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1UM2          5A  O 15 50
     A  94                                  DSPATR(ND)
     A            W1QRC3         5Y 0B 15 57EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1UM3          5A  O 15 63
     A  93                                  DSPATR(ND)
     A                                 16  3'Expiration date . . . . . .'
     A            W1EXDT         6Y 0B 16 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96
     AO 90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      EDTCDE(4)
417bAA                                 16 42'FIFO date:'
417bAA  89                                  DSPATR(ND)
417bAA            W1FIFO         6Y 0B 16 53
417bAA  29                                  DSPATR(RI)
417bAA N29N90                               DSPATR(UL)
417bAA  09                                  DSPATR(PC)
417bAA  89                                  DSPATR(ND)
417bAA  89                                  DSPATR(PR)
417bAA                                      EDTCDE(4)
417bAA*
     A                                 16 71'(MMDDYY)'
     A  96                                  DSPATR(ND)
416aDA**                               17  3'Catch weight received . . .'
416aAA            WOCRCV        27A  O 17  3
416bDA**          W1DWGT         7Y 2B 17 31
416bAA            W1DWGT         9Y 2B 17 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
416aAA  82
416aMAO 95
     AO 90                                  DSPATR(PR)
416aAA  82
416aMAO 95                                  DSPATR(ND)
     A                                      EDTCDE(4)
700aAA            W1KGLB         3A  O 17 42
700aAA  82
700aAAO 95                                  DSPATR(ND)
     A                                 17 50'Average (Y/N) . . . . . .'
416aAA  81
416aAAO 82
416 AAO 95                                  DSPATR(ND)
     A            W1AVG          1A  B 17 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
416aAA  81
416aAAO 82
416aMAO 95
     AO 90                                  DSPATR(PR)
416aAA  81
416aAAO 82
416aMAO 95                                  DSPATR(ND)
     A                                 18  3'Slot override . . . . . . .'
     A                                      DSPATR(ND)
     A            W1SLOT        12A  B 18 31DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 19  3'Manufacturing date  . . . .'
     A            W1MFDT         6Y 0B 19 31EDTCDE(4)
     A  27                                  DSPATR(RI)
     A  87                                  DSPATR(ND)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  87                                  DSPATR(PR)
417aAA                                 20  3'Lot code  . . . . . . . . .'
417aAA  88                                  DSPATR(ND)
417aAA            W1LOTC        30A  B 20 31
417aAA                                      CHECK(LC)
417aAA  28                                  DSPATR(RI)
417aAA N28N90                               DSPATR(UL)
417aAA  08                                  DSPATR(PC)
417aAA  88                                  DSPATR(ND)
417aAA  88                                  DSPATR(PR)
417aAA*
640aAA                                 20 63'Brand:'
640aAA  74                                  DSPATR(ND)
640aAA            W1BRAND       10A  B 20 70
640aAA  36                                  DSPATR(RI)
640aAA N36N90                               DSPATR(UL)
640aAA  16                                  DSPATR(PC)
640aAA  74                                  DSPATR(ND)
640aAA  74                                  DSPATR(PR)
650aAA                                 19 63'PTI:'
650aAA  73                                  DSPATR(ND)
650aAA            W1PTI          4Y 0B 19 70
650aAA  37                                  DSPATR(RI)
650aAA N37N90                               DSPATR(UL)
650aAA  17                                  DSPATR(PC)
650aAA  73                                  DSPATR(ND)
650aAA  73                                  DSPATR(PR)
413eDA*                                23  2'F5=Refresh  F12=Cancel  F20=Accept-
413eDA*                                      warning'
415aMA*                                23  2'F5=Refresh  F8=Tie/Hi  F12=Cancel-
415aMA*                                      F20=Accept warning'
700aDA*                                23  2'F5=Refresh'
700aDA*                                     COLOR(BLU)
700aDA*                                23 13'F8=Tie/Hi'
700aDA*                                     COLOR(BLU)
700aDA*                                23 24'F9=Cwgt Entry'
700aDA* 95                                  DSPATR(ND)
700aDA*                                     COLOR(BLU)
700aDA*                                23 39'F12=Cancel'
700aDA*                                     COLOR(BLU)
415aDA*                                23 44'F17=View receiving log'
700aDA*                                23 51'F17=Rcv log'
700aDA* 55                                  COLOR(BLU)
700aDA*N55                                  DSPATR(ND)
700aDA*                                23 63'F20=Accept warning'
700aDA*                                     COLOR(BLU)
     A*N33N90                               DSPATR(UL)
     A* 13                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
700aDA*                                22 40'F11=Mfr Code'
700aDA*                                     COLOR(BLU)
700aDA* 66                                  DSPATR(ND)
     A          R CLRSCR
     A                                      CLRL(*ALL)
700aAA* Command Line
700aAA          R CMDREC
700aAA                                      LOCK
700aAA                                      OVERLAY
700aAA                                      SLNO(22)
700aAA            CMDLN1        79A  O  1  2COLOR(BLU)
700aAA            CMDLN2        79A  O  2  2COLOR(BLU)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
MSG DA*N97                                  SFLCLR
MSG MA                                      SFLDSP
MSG AA                                      SFLDSPCTL
MSG AA                                      SFLINZ
     A  97                                  SFLEND
MSG AA            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
700bAA          R SCREEN1B
700bAA*%%TS  SD  20160921  083856  LYNNM       REL-V7R2M0  5770-WDS
700bAA                                      OVERLAY
700bAA                                      CLRL(21)
700bAA                                      CF04
700bAA N95                                  CF07
700bAA                                      CF08
700bAA N95                                  CF09
700bAA  55                                  CF17
700bAA                                      CF20
700bAA N66                                  CF11
700bAA  98                                  ALARM
700bAA                                  1  2'PO132.01'
700bAA                                  1 35'PO Receiving'
700bAA                                      DSPATR(HI)
700bAA                                  1 72DATE
700bAA                                      EDTCDE(Y)
700bAA                                  2 35'Detail Entry'
700bAA                                      DSPATR(HI)
700bAA                                  4  2'Type changes, press Enter.'
700bAA                                      COLOR(BLU)
700bAA                                  6  2'PO number . .'
700bAA            W1PO           9A  O  6 16
700bAA                                  6 60'Directed Put away'
700bAA                                  7  2'Warehouse . .'
700bAA            W1WHSE         3Y 0O  7 16EDTCDE(4)
700bAA            W1FDSC        30A  O  7 21
700bAA            WOLBL         13A  O  8  2
700bAA            WOITEM        65A  O  8 16
700bAA            WOLBLS        13A  O  9  2
700bAA            WOSITM        65A  O  9 16
700bAA            WODSC1        40A  O 10 16
700bAA            WODSC2        40A  O 11 16
700bAA                                 14  3'Breakdown UPC . . . . . . .'
700bAA  38                                  DSPATR(ND)
700bAA            W1BUPC1       30A  B 14 36CHECK(ER)
700bAA  15                                  DSPATR(RI)
700bAA N15N90                               DSPATR(UL)
700bAA  15                                  DSPATR(PC)
700bAA  90
700bAAO 38                                  DSPATR(PR)
700bAA  38                                  DSPATR(ND)
700bAA            W1BUOM1        2A  B 14 32CHECK(ER)
700bAA                                      DSPATR(PR)
700bAA  38                                  DSPATR(ND)
700bAA                                 15  3'Breakdown UPC . . . . . . .'
700bAA  39                                  DSPATR(ND)
700bAA            W1BUOM2        2A  B 15 32CHECK(ER)
700bAA                                      DSPATR(PR)
700bAA  39                                  DSPATR(ND)
700bAA            W1BUPC2       30A  B 15 36CHECK(ER)
700bAA  16                                  DSPATR(RI)
700bAA N16N90                               DSPATR(UL)
700bAA  16                                  DSPATR(PC)
700bAA  90
700bAAO 39                                  DSPATR(PR)
700bAA  39                                  DSPATR(ND)
