     A****************************************************************
     A*
     A* LOAD60BPR  Verified Returned Trailer Equipment - Detail Report.
     A*                                                  By Driver
     A* November 17, 2008
     A* Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/10/11  JGD  6.40
     F*      - Created
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*----------------------------------------------------------------
     A          R HEAD1
     A*----------------------------------------------------------------
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A                                    30
     A                                      DFT('(Driver Detail) Returned Trail-
     A                                      er Equipment')
     A            FRMDTE         6S 0O    77EDTCDE(Y)
     A                                    87
     A                                      DFT('Thru')
     A            TODTE          6S 0O    93EDTCDE(Y)
     A                                   124
     A                                      DFT('Page')
     A            PAGE           4S 0O   128
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    37
     A                                   124
     A                                      DFT('LOADR60B')
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A          R HEAD2
     A*----------------------------------------------------------------
     A*                                     TEXT('REPORT HEADING')
     A*
     A                                     2'Route #:'
     A            W1RTE#         5A  O    11
     A*
     A                                    19'LoaderID:'
     A            W1LDR#         5S 0O    29
     A*
     A                                    36'Loader:'
     A            W1USER        28A  O    44
     A                                      SPACEA(001)
     A*
     A                                     2'Door #:'
     A            W1DOOR         3A  O    11
     A*
     A                                    19'Trailer #:'
     A            W1TRL#         8A  O    30
     A*
     A                                    40'Date:'
     A            W1DATE         6  0O    46
     A                                      EDTCDE(Y)
     A*
     A                                    57'Time:'
     A            W1STRT         6  0O    63
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
     A*
     A                                     2'Returned:'
     A*
     A                                    12'Date:'
     A            W1RDTE         6  0O    18
     A                                      EDTCDE(Y)
     A*
     A                                    27'Time:'
     A            W1RTIM         6  0O    33
     A                                      EDTWRD('  :  :  ')
     A*
     A                                    43'User:'
     A            W1RUSR        10A       49
     A                                      SPACEA(001)
     A*
     A                                     2'Route Driver:'
     A                                      SPACEA(002)
     A*
     A*
     A*
     A* Print column headings
     A*----------------------------------------------------------------
     A          R COLUMNS
     A*----------------------------------------------------------------
      *
     A                                     1'Equipment'
      *
     A                                    28'Quantity'
      *
     A                                    41'Driver'
      *
     A                                    61'Loader'
      *
     A                                    83'Bay II'
      *
     A                                   103'Load'
      *
     A                                   113'Return'
      *
     A                                      SPACEA(001)
      *
     A                                     1'Description'
     A                                      UNDERLINE
     A
     A                                    17'Load'
     A                                      UNDERLINE
      *
     A                                    22'Rtrn'
     A                                      UNDERLINE
      *
     A                                    27'Short'
     A                                      UNDERLINE
      *
     A                                    33'Over'
     A                                      UNDERLINE
      *
     A                                    41'Name           '
     A                                      UNDERLINE
      *
     A                                    61'Name           '
     A                                      UNDERLINE
      *
     A                                    83'Name           '
     A                                      UNDERLINE
      *
     A                                   103'Date    '
     A                                      UNDERLINE
      *
     A                                   113'Date    '
     A                                      UNDERLINE
      *
     A                                   123'Door'
     A                                      UNDERLINE
      *
     A                                   128'Rte#'
     A                                      UNDERLINE
      *----------------------------------------------------------------
     A          R DETAIL1
     A*----------------------------------------------------------------
      *
     A                                      SPACEB(001)
     A            W1DESC        15A        1
      *
     A            W1LOAD         4S 0     17EDTWRD('  0 ')
      *
     A            W1UNLD         4S 0     22EDTWRD('  0 ')
      *
     A            W1SHRT         4S 0     27EDTWRD('  0 ')
      *
     A            W1OVER         4S 0     33EDTWRD('  0 ')
      *
     A            W1DNAM        22A       41
      *
     A            W1USER        22A  O    61
      *
     A            W1RUSR        22A       83
      *
     A            W1DATE         6  0O   103
     A                                      EDTCDE(Y)
      *
     A            W1RDTE         6  0O   113
     A                                      EDTCDE(Y)
      *
     A            W1DOOR         3A  O   124
      *
     A            W1RTE#         5A  O   128
     A*----------------------------------------------------------------
     A          R TOTAL1
     A*----------------------------------------------------------------
     A                                      SPACEB(001)
     A                                     2
     A                                      DFT('User: ')
     A            SAVUSR         5A  O     8
     A                                    17
     A                                      DFT('Pallet: ')
     A            CNTR1          5S 0O    25
     A                                      EDTCDE(Z)
     A                                    39
     A                                      DFT('Merge: ')
     A            CNTR2          5S 0O    46
     A                                      EDTCDE(Z)
     A                                    60
     A                                      DFT('Flags: ')
     A            CNTR3          5S 0O    67
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A          R TOTAL2
     A*----------------------------------------------------------------
     A                                      SPACEB(002)
     A                                     2
     A                                      DFT('Final Totals')
     A                                      SPACEA(001)
     A                                     2
     A                                      DFT('Total Pallets: ')
     A            CNTR1T         5S 0O    16
     A                                      EDTCDE(Z)
     A                                    31
     A                                      DFT('Total Merge: ')
     A            CNTR2T         5S 0O    43
     A                                      EDTCDE(Z)
     A                                    58
     A                                      DFT('Total Flags: ')
     A            CNTR3T         5S 0O    70
     A                                      EDTCDE(Z)
      *
     A*
     A* Skip 1 Line
     A*----------------------------------------------------------------
     A          R SKIP1
     A*----------------------------------------------------------------
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*----------------------------------------------------------------
     A          R FOOT1
     A*----------------------------------------------------------------
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('LOADR60B: End of Report   ')
     A*
