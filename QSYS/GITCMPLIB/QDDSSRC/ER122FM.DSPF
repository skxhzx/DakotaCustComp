     A*%%TS  SD  19960116  144505  DAVE        REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RPTOVR)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19960116  144505  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'ER122.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 26'EPM Report Device Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAL1                    SFL
     A*%%TS  SD  19940803  141032  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            H1PRTF    R        H      REFFLD(ROPRTF)
     A            H1WHSE    R        H      REFFLD(ROWHSE)
     A            H1STYP    R        H      REFFLD(ROSTYP)
     A            H1WHDP    R        H      REFFLD(ROWHDP)
     A            W1WHSE         3Y 0B 14  4EDTCDE(3)
     A            W1DEV         10A  B 14 14
     A            W1FORM        10A  B 14 27
     A*
     A          R HEADR1                    SFLCTL(DETAL1)
     A*%%TS  SD  19940803  141032  HEMANT      REL-V2R1M1  5738-PW1
     A  *DS3                                SFLLIN(0008)
     A                                      SFLSIZ(0032)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  9  4'Enter information'
     A                                 12  2'Warehouse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 14'Device    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 27'Form Type '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 43'Warehouse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 55'Device    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 68'Form Type '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  4'Report Description . . .  '
     A                                  5  4'Report Type  . . . . . .  '
     A                                  6  4'Default Device . . . . .  '
     A                                  7  4'Default Form Type  . . .  '
     A            RPDESC        50A  O  4 31
     A            RPTYPE        10A  O  5 31
     A            WDDEV         10A  B  6 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WDFORM        10A  B  7 31
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSWHSE         3Y 0B 13  4EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSDEV         10A  B 13 14
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSFORM        10A  B 13 27
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R DETAL2                    SFL
     A*%%TS  SD  19940727  135219  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            H1PRTF    R        H      REFFLD(ROPRTF)
     A            H1WHSE    R        H      REFFLD(ROWHSE)
     A            H1STYP    R        H      REFFLD(ROSTYP)
     A            H1WHDP    R        H      REFFLD(ROWHDP)
     A            W1WHSE         3Y 0B 14  4EDTCDE(3)
     A            W1DEV         10A  B 14 40
     A            W1FORM        10A  B 14 54
     A            W1STYP         1   B 14 17
     A            W1DEPT         5   B 14 27
     A*
     A          R HEADR2                    SFLCTL(DETAL2)
     A*%%TS  SD  19940803  141032  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0032)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A                                      CF04
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  9  4'Enter information'
     A                                 12  2'Warehouse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 14'Section'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 25'Department'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  4'Report Description . . .  '
     A                                  5  4'Report Type  . . . . . .  '
     A                                  6  4'Default Device . . . . .  '
     A                                  7  4'Default Form Type  . . .  '
     A            RPDESC        50A  O  4 31
     A            RPTYPE        10A  O  5 31
     A            WDDEV         10A  B  6 31
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WDFORM        10A  B  7 31
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSWHSE         3Y 0B 13  4EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSDEV         10A  B 13 40
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSFORM        10A  B 13 54
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 40'Device    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 54'Form Type '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WSSTYP         1A  B 13 17
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WSDEPT         5A  B 13 27
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940720  114854  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            PTDMY1        78A  O  1  2
     A            PTDMY2        78A  O  3  2
