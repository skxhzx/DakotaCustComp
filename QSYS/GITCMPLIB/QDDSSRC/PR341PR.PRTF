     A****************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A* PR341PR   Priority replenishment report - TPC.
     A* 29 October, 2007
     A* Lynn McMahon
     A*
     A****************************************************************
     A*
     A*  Revisions:
     A*
510aAA*     10/29/07  LMC  5.10a
     A*       - Created
TPCaAA*     04/09/14  RH   TPCa
     A*       - Enh: Add demand/needed quantity
     A*
     A**************************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A*                                   52
     A*                                     DFT('Priority Replenishment ')
     A*                                   79
     A*                                     DFT('Report ')
     A            RPTTTL        40        47
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('PR341')
     A                                      SPACEA(001)
     A            FAIS           4A  O    55
     A*                                     EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            TAIS           4A  O    +2
     A*                                     EDTCDE(Y)
     A                                      SPACEA(001)
     A*
     A          R HEAD2
414aDA*                                     SPACEA(002)
414aAA            P#DESC        40A  O    47
414aAA*                                     SPACEA(001)
414aAA*           P#STAT        40A  O    47
414aAA                                      SPACEA(002)
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEA(001)
414aAA*                                   82
414aAA*                                     DFT('* Generated *')
414aAA*                                     SPACEA(001)
     A                                     3
     A                                      DFT('Item')
414aAA                                    19
414aAA                                      DFT('Description')
414aMA                                    50
     A                                      DFT('From Slot')
414aMA                                    63
     A                                      DFT('To Slot')
414aMA                                    76
414aMA                                      DFT('Qty')
TPCaAA                                    82
TPCaAA                                      DFT('Needed Qty')
414aMA*                                   82
     A*                                     DFT('Date')
414aAA*                                   91
414aAA*                                     DFT('Time')
414aAA*                                   99
414aAA*                                     DFT('Gen''d by')
414aAA*                                  111
414aAA*                                     DFT('Task')
414aAA*                                  117
414aAA*                                     DFT('Error Desc')
     A*%%***********************************************************************
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            PRITEM    R        O     3
     A                                      REFFLD(PRITEM PRPLFIL)
414aAA            ITDESC    R        O    19
     A                                      REFFLD(ITDESC PIRITEM)
414aMA            PRFDIS    R        O    50
     A                                      REFFLD(PRFDIS PRPLFIL)
414aMA            PRTDIS    R        O    63
     A                                      REFFLD(PRTDIS PRPLFIL)
414aMA            PRQTY     R        O    76
     A                                      REFFLD(PRQTY PRPLFIL)
     A                                      EDTCDE(K)
TPCaAA            PRAQS     R        O    82
TPCaAA                                      REFFLD(PRAQS PRPLFIL)
TPCaAA                                      EDTCDE(J)
414aMA*           PRADTE         8S 0O    81
     A*                                     EDTCDE(Y)
414aAA*           PRATIM    R        O    90
414aAA*                                     REFFLD(PRATIM PRPLFIL)
414aAA*                                     EDTWRD(' 0:  :  ')
414aAA*           EXGBY     R        O    99
414aAA*                                     REFFLD(EXGBY ITMEXCP)
414aAA*           EXTASK    R        O   110
414aAA*                                     REFFLD(EXTASK ITMEXCP)
414aAA*           ERRMSG        15A  O   117
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A*                                    1
     A*                                     DFT('Total exceptions for')
     A*           T#DESC        13A  O    +1
     A*                                   +1
     A*                                     DFT(':')
     A            T#CLS          5S 0O    34
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A          R TOTAL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('PR341: End of Report ')
     A*
