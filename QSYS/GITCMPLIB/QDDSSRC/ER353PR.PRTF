     A****************************************************************
     A*
     A* ER353PR   Transaction analysis by task for an empl(Summary)
     A* 28 July, 1999
     A* Hemant Kapadia
     A*
     A* Revisions:
     A* 07/28/99  HNK  4.12
     A* - Created
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER353'
530aAA                                      SPACEB(001)
530aMA            MRGTXT        45A  O     2
530aAA            TMERNG        22A  O    56
     A*
     A                                      SPACEA(001)
     A                                    55'Crew:'
     A            WKCCOD         6        +1
     A            WKCREW        40        +1
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            EREMP#    R             +1REFFLD(EREMP# ER350)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A                                      SPACEA(002)
     A  92                                75
     A                                      '(Continued)'
     A*
     A                                     1'   Shift:'
     A            ERSHFT    R             +1REFFLD(ERSHFT ER350)
     A            WKSHFT        40        +1
     A                                      SPACEA(002)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    31'|'
     A                                    32'<------------Transactions---------+
     A                                      ---->'
     A                                    71'|'
     A                                    72'<---------------Pieces------------+
     A                                      ---->'
     A                                   112'|'
     A                                      SPACEA(001)
     A                                    16'Direct'
     A                                    25'Indir'
     A                                    31'|'
     A                                    33'Number'
     A                                    45'Trans'
     A                                    53'Std Tran'
     A                                    62'%of Std'
     A                                    71'|'
     A                                    74'Number'
     A                                    85'Pieces'
     A                                    93'Std Pcs'
     A                                   100'%of Std'
     A                                      SPACEA(001)
     A*
     A                                     1'Dept '
     A                                      UNDERLINE
     A                                     8'Task'
     A                                      UNDERLINE
     A                                    19'Hrs'
     A                                      UNDERLINE
     A                                    27'Hrs'
     A                                      UNDERLINE
     A                                    31'|'
     A                                    33'of Trn'
     A                                      UNDERLINE
     A                                    44'Per Hr'
     A                                      UNDERLINE
     A                                    52'Per Hour'
     A                                      UNDERLINE
     A                                    62'Tran/Hr'
     A                                      UNDERLINE
     A                                    71'|'
     A                                    74'of PCS'
     A                                      UNDERLINE
     A                                    85'Per Hr'
     A                                      UNDERLINE
     A                                    93'Per Hr'
     A                                      UNDERLINE
     A                                   102'Pcs/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(002)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A*
     A* Print detail line
     A*
     A          R SUMAR1
     A                                      SPACEA(001)
     A            S#WHD          5         1
     A            S#TSK          6         8
     A            T#DHRS         5S 3     16EDTCDE(4)
     A            T#IHRS         5S 3     24EDTCDE(4)
     A*
     A                                    31'|'
     A            S#TRN          5S 0     34EDTCDE(4)
     A            T#TPH          7S 3     43EDTCDE(4)
     A            S#TPH          5S 0     54EDTCDE(4)
     A            TPHPCN         5S 2     63EDTCDE(4)
     A*
     A                                    71'|'
     A            S#PCS          5S 0     75EDTCDE(4)
     A            T#PPH         10S 3     83EDTCDE(4)
     A            S#PPH          5S 0     94EDTCDE(4)
     A            PPHPCN         5S 2    103EDTCDE(4)
     A*
     A* Print tatals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          15A        1
     A            T#DTHR         7S 3     14EDTCDE(4)
     A            T#ITHR         7S 3     22EDTCDE(4)
     A            T#TTOT         7S 0     32EDTCDE(4)
     A            T#PTOT         7S 0     73EDTCDE(4)
     A                                      SPACEA(001)
     A                                     1'Average:'
     A            A#TCNT         5S 2     60EDTCDE(4)
     A            A#PCNT         5S 2     97EDTCDE(4)
     A                                      SPACEA(002)
     A*
     A* Print crew summary
     A*
     A          R CRSUMH
     A                                      SPACEB(003)
     A                                     1'Crew Summary'
     A                                      UNDERLINE
     A*
     A          R CRSUMR
     A                                      SPACEB(002)
     A            C#WHD          5         1
     A            C#TSK          6         8
     A            C#DHRS         5S 3     16EDTCDE(4)
     A            C#IHRS         5S 3     24EDTCDE(4)
     A                                    31'|'
     A            C#TRN          5S 0     34EDTCDE(4)
     A            C#TPH          7S 3     42EDTCDE(4)
     A            C#STP          5S 0     54EDTCDE(4)
     A            C#TPC          5S 2     64EDTCDE(4)
     A                                    71'|'
     A            C#PCS          5S 0     75EDTCDE(4)
     A            C#PPH          8S 3     83EDTCDE(4)
     A            C#SPP          5S 0     94EDTCDE(4)
     A            C#PPC          5S 2    103EDTCDE(4)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* Draw 1 Line
     A*
     A          R DRAW1
     A                                      TEXT('DRAW ONE LINE')
     A                                      SPACEA(001)
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------'
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'Note: Direct task includes idle ti+
     A                                       me.'
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER353: End of Report '
     A*
