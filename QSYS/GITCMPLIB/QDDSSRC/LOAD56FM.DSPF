     A*%%TS  SD  20081210  084603  BFCTEST1    REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*  LOAD56FM  Truck Load Module - Trailer Equipment Detail
     A*  October 30, 2008
     A*  Jose G. Duque
     A*
     F*    03/15/11  JGD  6.40
     F*      - Created
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A*         Loading trailer equipment
     A*----------------------------------------------------------------
     A          R WINDW1
     A*%%TS  SD  20081210  084603  BFCTEST1    REL-V5R4M0  5722-WDS
     A                                      WINDOW(1 2 21 72)
     A  98                                  ALARM
     A                                      CLRL(20)
     A                                      OVERLAY
     A*
     A                                  1  2'Loadr56'
     A*
     A                                  1 23'Trailer Equipment Detail Entry'
     A                                      DSPATR(HI)
     A*
     A                                  3  2'Route #:'
CBI AA            W1RTE#         5A  O  3 11DSPATR(HI)
     A*
     A                                  3 19'LoaderID:'
CBI AA            W1LDR#         5S 0O  3 29DSPATR(HI)
     A*
     A                                  3 36'Loader:'
CBI AA            W1USER        28A  O  3 44DSPATR(HI)
     A* 20                                  DSPATR(PC)
     A*N20                                  DSPATR(UL)
     A* 20                                  DSPATR(RI)
     A*
     A                                  4  2'Door #:'
CBI AA            W1DOOR         3A  O  4 11DSPATR(HI)
     A*
     A                                  4 19'Trailer #:'
CBI AA            W1TRL#         8A  O  4 30DSPATR(HI)
     A*
     A                                  4 40'Date:'
CBI AA            W1DATE         6Y 0O  4 46DSPATR(HI)
     A                                      EDTCDE(Y)
     A*
     A                                  4 57'Time:'
CBI AA            W1STRT         6Y 0O  4 63DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  5  2'Returned:'
     A*
     A                                  5 12'Date:'
CBI AA            W1RDTE         6Y 0O  5 18DSPATR(HI)
     A                                      EDTCDE(Y)
     A*
     A                                  5 27'Time:'
CBI AA            W1RTIM         6Y 0O  5 33DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  5 43'User:'
CBI AA            W1RUSR        22A  O  5 49DSPATR(HI)
     A*
     A                                  6  2'Route Driver:'
CBI AA            W1DNAM        28A  O  6 16DSPATR(HI)
     A*
     A                                  7 55'Quantity'
     A                                  8  2'Loaded         '
     A                                      DSPATR(UL)
     A*
     A                                  8 27'Unloaded       '
     A                                      DSPATR(UL)
     A*
     A                                  8 53'Short'
     A                                      DSPATR(UL)
     A*
     A                                  8 61'Over '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*            LOADER TRAILER EQUIPMENT SCREEN
     A* (Fields names ending in '1' and '2' identify 'Loaded' and
     A* 'Unloaded' columns respectively on screen).
     A*----------------------------------------------------------------
     A                                  9  2'Blankets:'
     A            W1BK1          4A  B  9 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                  9 27'Blankets:'
     A            W1BK2          4A  B  9 43
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO1          4Y 0B  9 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO1B         4Y 0B  9 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 10  2'Bulk Heads:'
     A            W1BH1          4A  B 10 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 10 27'Bulk Heads:'
     A            W1BH2          4A  B 10 43
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO2          4Y 0B 10 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO2B         4Y 0B 10 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 11  2'Load Bars:'
     A            W1LB1          4A  B 11 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 11 27'Load Bars:'
     A            W1LB2          4A  B 11 43
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO3          4Y 0B 11 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO3B         4Y 0B 11 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 12  2'IC Bins-Small:'
     A            W1ICS1         4A  B 12 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 12 27'IC Bins-Small:'
     A            W1ICS2         4A  B 12 43
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO4          4Y 0B 12 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO4B         4Y 0B 12 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 13  2'IC Bins-Medium:'
     A            W1ICM1         4A  B 13 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 13 27'IC Bins-Medium:'
     A            W1ICM2         4A  B 13 43
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO5          4Y 0B 13 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO5B         4Y 0B 13 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 14  2'IC Bins-Large:'
     A            W1ICL1         4A  B 14 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 14 27'IC Bins-Large:'
     A            W1ICL2         4A  B 14 43
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO6          4Y 0B 14 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO6B         4Y 0B 14 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 15  2'SF Bins:'
     A            W1SFB1         4A  B 15 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 15 27'SF Bins:'
     A            W1SFB2         4A  B 15 43
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO7          4Y 0B 15 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO7B         4Y 0B 15 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 16  2'Staged Pallet:'
     A            W1SPL1         4A  B 16 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 16 27'Staged Pallet:'
     A            W1SPL2         4A  B 16 43
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO8          4Y 0B 16 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO8B         4Y 0B 16 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 17  2'Total Pallets:'
     A            W1TPL1         4A  B 17 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 17 27'Total Pallets:'
     A            W1TPL2         4A  B 17 43
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(PC)
     A N29                                  DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO9          4Y 0B 17 53DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A            W1SO9B         4Y 0B 17 61DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*----------------------------------------------------------------
     A                                 19  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
CBI AA                                 19 12'F5=Refresh'
     A                                      COLOR(BLU)
     A*
CBI AA                                 19 25'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A            ERRMSG        50A  O 20  2DSPATR(HI)
     A*----------------------------------------------------------------
     A*         Verification window
     A*----------------------------------------------------------------
     A          R WINDW2
     A*%%TS  SD  20060627  164443  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(19 52 2 24 *MSGLIN)
     A*                                     WDWBORDER((*COLOR PNK) (*CHAR '    -
     A*                                         '))
     A*
     A                                  1  1'COMFIRM UPDATE:'
     A*                                     COLOR(PNK)
     A*
     A            YN             1A  B  1 17
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  30                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A  70                                  ERRMSG('Wrong value entered.')
     A
     A                                  1 19'(Y/N)'
     A*                                     COLOR(PNK)
     A*                                     DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
