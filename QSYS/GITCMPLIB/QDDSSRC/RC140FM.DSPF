     A*%%TS  SD  20140627  100903  KENNETHE    REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved    *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RC140FM   RF Receive Production GS1s
     A*  20 June 2014
     A*  Kenneth Elder
     A*
     A********************************************************************
     A*
650 AA*  06/20/14  KDE  6.50
     A*    - Created for Production Module
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SCREEN1
     A*%%TS  SD  20140627  100903  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC140.01'
     A                                  1 12'Recv Prod'
     A****        W1UCI         50A  B  3  6CNTFLD(017)
     A            W1UCI         50A  B  3  6CHECK(ER)
     A                                  3  1'UCI'
     A                                  7  1'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20140626  174822  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC140.02'
     A                                  1 12'Recv Prod'
     A            W2ITMD        65A  O  3  1
     A                                  5  1'License:'
     A            W2LIC         15A  B  5 11
     A  15                                  DSPATR(PC)
     A  35                                  DSPATR(HI)
     A                                  7  1'F12 - CANCEL'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R SCREEN3
     A*%%TS  SD  20140626  174822  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      CA12
     A                                      CF10
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC140.03'
     A                                  1 12'Recv Prod'
      ***         W3UCI         50A  B  3  6CNTFLD(017)
     A            W3UCI         50A  B  3  6CHECK(ER)
     A                                  3  1'UCI'
     A            W3ITMD        65A  O  2  1
     A                                  6  1'CS/WGHT:'
     A            W3CASE         3Y 0O  6 10EDTWRD('  0')
     A                                  6 14'/'
     A            W3WGHT         8Y 3O  6 16EDTWRD('    0.   ')
     A                                  7  1'F10-Comp F12-Cncl F20-Ovr'
     A                                      COLOR(BLU)
     A          R CNLWDW
     A*%%TS  SD  20140625  200313  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      CF10
     A                                      CA12
     A                                      WINDOW(1 2 8 20)
     A                                  1  3' Confirm Cancel'
     A                                      COLOR(RED)
     A                                  3  2'Press F10 to delete'
     A                                  4  2' ALL barcodes for'
     A                                  5  7'license:'
     A            W2LIC         15A  O  6  4
     A                                  7  2'Press F12 to return'
