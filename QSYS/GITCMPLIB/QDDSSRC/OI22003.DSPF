     A*%%TS  SD  19940803  165734  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OI22003    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640aAA*    04/25/12  RTR  6.40a
     A*      - Created 01 version of screen for GUI changes.
     A*      - Added Delivery Date, Route from/to, moved jobq, dev,
     A*        form down.
640bAA*    09/05/12  RTR  6.40b
     A*      - Created 02 version of screen for GUI changes.
     A*      - Added F20 for override of Delivery Date warning.
640cAA*    09/25/12  MLB  6.40c
     A*      - Created 03 version of screen for GUI changes.
     A*      - Revised screen 1 fmt to protect and non-display
     A*        Delivery Date, Route From/To fields when import option
     A*        is not Staging Import. Needed for WST.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940803  165734  DAVE        REL-V2R1M1  5738-PW1
     A                                      CF04
640bAA                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'OI220.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A                                  9  4'Delivery Date  . . . . . . .'
640cAA  31                                  DSPATR(ND)
     A                                 10  4'Route From . . . . . . . . .'
640cAA  31                                  DSPATR(ND)
     A                                 11  4'Route To . . . . . . . . . .'
640cAA  31                                  DSPATR(ND)
640aDA*                                10  4'Place on job queue . . . . .'
640aAA                                 13  4'Place on job queue . . . . .'
640aDA*                                11  4'Printer device . . . . . . .'
640aAA                                 14  4'Printer device . . . . . . .'
640aDA*                                12  4'Form type  . . . . . . . . .'
640aAA                                 15  4'Form type  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aAA            W1DLDT         6Y 0B  9 34EDTCDE(4)
640aAA N26N90                               DSPATR(HI)
640aAA  26                                  DSPATR(RI)
640aAA N26N90                               DSPATR(UL)
640aAA  06                                  DSPATR(PC)
640cAA  31                                  DSPATR(ND)
640cAA  31
640cMAO 90                                  DSPATR(PR)
640aAA                                  9 58'MMDDYY'
640aAA            W1FRTE         5A  B 10 34
640aAA N27N90                               DSPATR(HI)
640aAA  27                                  DSPATR(RI)
640aAA N27N90                               DSPATR(UL)
640aAA  07                                  DSPATR(PC)
640cAA  31                                  DSPATR(ND)
640cAA  31
640cMAO 90                                  DSPATR(PR)
640aAA            W1TRTE         5A  B 11 34
640aAA N28N90                               DSPATR(HI)
640aAA  28                                  DSPATR(RI)
640aAA N28N90                               DSPATR(UL)
640aAA  08                                  DSPATR(PC)
640cAA  31                                  DSPATR(ND)
640cAA  31
640cMAO 90                                  DSPATR(PR)
640aDA*           W1JOBQ         1A  B 10 34
640aAA            W1JOBQ         1A  B 13 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA*                                10 58'Y=Yes, N=No'
640aAA                                 13 58'Y=Yes, N=No'
640aDA*           W1DEV         10A  B 11 34
640aAA            W1DEV         10A  B 14 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA*           W1FORM        10A  B 12 34
640aAA            W1FORM        10A  B 15 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
