     A****************************************************************
     A*
     A* HC311PR   HACCP report
     A* 01 November, 2002
     A* Hemant Kapadia
     A*
     A****************************************************************
     A* 11/01/02  HNK  4.16
     A*  - Created
     A*
520 AA* 10/04/08  MLB  5.20
     A*  - Enh: Added vendor number as 2nd line on report.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    56
     A                                      DFT('HACCP Information Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('HC311')
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    64
     A                                      DFT('PROD')
     A                                    70
     A                                      DFT('PROD')
     A                                    76
     A                                      DFT('Trailer Temp')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('PO Number')
     A                                      UNDERLINE
     A                                    11
     A                                      DFT('Item      ')
     A                                      UNDERLINE
     A                                    22
520 MA                                      DFT('Description             ')
     A                                      UNDERLINE
     A                                    48
     A                                      DFT('Truck Seal #')
     A                                      UNDERLINE
     A                                    64
     A                                      DFT('Area ')
     A                                      UNDERLINE
     A                                    70
     A                                      DFT('Type ')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('CLR')
     A                                      UNDERLINE
     A                                    86
     A                                      DFT('FRZ')
     A                                      UNDERLINE
     A                                    90
     A                                      DFT('Load Condition')
     A                                      UNDERLINE
     A                                   111
     A                                      DFT('Received By')
     A                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            HAPO      R        O     1REFFLD(HAPO   HC311)
     A            W1ITEM        10A  O    11
     A            W1DESC        25A  O    22
     A            HATKSL    R        O    48REFFLD(HATKSL HC311)
     A            HAPRDA    R        O    64REFFLD(HAPRDA HC311)
     A            HAPRDT    R        O    70REFFLD(HAPRDT HC311)
     A            HATTRR    R        O    76REFFLD(HATTRR HC311)
     A                                      EDTCDE(L)
     A            HATTMD    R        O    83REFFLD(HATTMD HC311)
     A                                      EDTCDE(L)
     A            W1LDCN        20A  O    90
     A            W1RCBY        20A  O   111
520 AA*
520 AA* Print 2nd detail line
520 AA*
520 AA          R DETAIL2
520 AA                                      SPACEB(001)
520 AA                                     3'Vendor:'
520 AA            W1VEN     R        O    11REFFLD(PHVEN POHDR)
520 AA                                      EDTCDE(4)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('HC311: End of Report ')
     A*
