     A*%%TS  SD  20110121  133257  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  TR830FM  Tracker - Returns detail (One whse) - Display
     A*  08 Feb 2010
     A*  Retha Davis
     A*
     A*  Revisions:
     A*
     A*    02/08/10  RBD  5.30
     A*      - Created.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970617  143050  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'TR830'
     A                                  1 26'Work with Trax Returns Detail'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 41'Route  -'
     A            W1RTE          5A  O  2 50
     A                                  2 59'Driver Id -'
     A            W1USR         10A  O  2 71
     A                                  3 41'Rte Id -'
     A            W1RTID         5A  O  3 50
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20110121  133257  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            W1CORD    R        H      REFFLD(TTREC/TTORD *LIBL/TTRTN)
     A            TTCWGT    R        H      REFFLD(TTREC/TTCWGT *LIBL/TTRTN)
     A            TTBFLG    R        H      REFFLD(TTREC/TTBFLG *LIBL/TTRTN)
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1ITEM    R        O 11  5REFFLD(TTREC/TTITEM TTRTN)
     A            W1ITMX         1A  O 11 21
     A            W1DESC        30   O 11 23
     A            W1STOP    R        O 11 55REFFLD(TTREC/TTSTOP TTRTN)
     A                                      EDTCDE(4)
     A            W1QTY     R        O 11 59REFFLD(TTREC/TTOQTY TTRTN)
     A                                      EDTCDE(4)
     A            W1UOM     R        O 11 65REFFLD(TTREC/TTUOM TTRTN)
     A            W1ORD     R        O 11 69REFFLD(TTREC/TTORD TTRTN)
     A                                      EDTCDE(4)
     A            W1RSME         3A  O 11 78
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20110121  133257  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
     A                                  9 78'Rtn'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  5'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 23'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 55'Stp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 60'Qty  UM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69' Order '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Cde'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for list display - View 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20110121  133257  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            W1CORD    R        H      REFFLD(TTREC/TTORD *LIBL/TTRTN)
     A            TTCWGT    R        H      REFFLD(TTREC/TTCWGT *LIBL/TTRTN)
     A            TTBFLG    R        H      REFFLD(TTREC/TTBFLG *LIBL/TTRTN)
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2ITEM        14A  O 11  5
     A            W2IMEM        30   O 11 20
     A            W2CNAM        29   O 11 51
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20110121  133257  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD002         1A  O  9  3
     A                                 10  2'Op'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Item          '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Item Note                     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'Customer Name                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press F10 to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
