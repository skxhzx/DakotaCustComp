     A****************************************************************
     A*
     A* IT401PR   Shelf life expiration report
     A* 01 April, 1996
     A* Hemant Kapadia
     A*
     A* Revision
     A*
413 AA* 10/13/99  HNK  4.13
     A*  - Dept added.
417aAA* 09/19/03  RH   4.17a
     A*  - Buyer # added, move fields to the right.
     A*
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT401')
     A                                      SPACEA(002)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    70'Entered'
     A                                    80'Shelf'
     A                                    87'Notice'
     A                                    96'Shelf Life'
     A                                   108'Expiration'
412 AA                                   119'Avail.'
     A                                      SPACEA(001)
417a A                                     1'Buyer#'
417a A                                      UNDERLINE
     A                                     8'Item           '
     A                                      UNDERLINE
     A                                    25'Description                  '
     A                                      UNDERLINE
     A                                    56'Slot        '
     A                                      UNDERLINE
     A                                    70'Date   '
     A                                      UNDERLINE
     A                                    80'Life'
     A                                      UNDERLINE
     A                                    87'Days  '
     A                                      UNDERLINE
     A                                    96'Date     '
     A                                      UNDERLINE
     A                                   108'Date     '
     A                                      UNDERLINE
     A                                   119'Quantity '
     A                                      UNDERLINE
413 AA                                   128'Dept.'
413 AA                                      UNDERLINE
     A                                      SPACEA(001)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
417a A            W1BNBR    R        O     1REFFLD(IMBNBR IT401)
417aAA                                      EDTCDE(Z)
417aDA*           ITITEM    R        O     8REFFLD(ITITEM IT401)
417aMA            W1ITEM    R        O     8REFFLD(ITITEM IT401)
     A            ITDESC    R        O    25REFFLD(ITDESC IT401)
     A            SLDISP    R        O    56REFFLD(SLDISP IT401)
     A            ENDATE         6S 0O    70EDTCDE(Y)
     A            ITSLIF    R        O    80REFFLD(ITSLIF IT401)
     A                                      EDTCDE(3)
     A            ITNDAY    R        O    87REFFLD(ITNDAY IT401)
     A                                      EDTCDE(3)
     A            SLDATE         6S 0O    96EDTCDE(Y)
     A            EXDATE         6S 0O   108EDTCDE(Y)
413 MA            AVAIL1         5S 0O   120EDTCDE(3)
413 AA            ITWHDP         5   O   128
413 DA*           AVLQT@         5S 0O   122EDTCDE(3)
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT401: End of Report ')
     A*
