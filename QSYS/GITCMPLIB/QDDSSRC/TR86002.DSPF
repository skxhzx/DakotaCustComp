     A*%%TS  SD  20130205  115933  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  TR860FM  Tracker - Stop Flags (One whse) - Display
     A*  03 Jan 2011
     A*  Retha Davis
     A*
     A*  Revisions:
     A*
     A*    01/03/11  RBD  6.40
     A*      - Created.
     A*
650aAA*    02/05/13  RBD  6.50a
     A*      - Added display of order, unique id, and display time.
     A*
650bAA*    02/18/13  RBD  6.50b
     A*      - Changed display of order to customer order.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20110103  142528  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                  1  2'TR860'
     A                                  1 29'Work with Trax Stop Flags'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20110103  142528  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 48'Position to route . . . .'
     A            PTINP2         5A  B  3 75
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20130205  115933  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHTRUK    R        O 11 14REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            TTFSTP    R        O 11 27REFFLD(TTFREC/TTFSTP *LIBL/TTSTPFLG)
     A                                      EDTCDE(4)
650bDA**          TTFORD         7  0O 11 32EDTCDE(4)
650bMA            W1CORD         7  0O 11 32EDTCDE(4)
     A            W1CNAM        40A  O 11 41
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20130205  115933  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
650bAA                                  9 33'Cust'
650bAA                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32' Order '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Customer Name                     -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for list display - View 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20130205  115933  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHTRUK    R        O 11 14REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            TTFSTP    R        O 11 27REFFLD(TTFREC/TTFSTP *LIBL/TTSTPFLG)
     A                                      EDTCDE(4)
     A            TTFRTI    R        O 11 33REFFLD(TTFREC/TTFRTI *LIBL/TTSTPFLG)
     A            W1SDTE         6Y 0O 11 42EDTCDE(Y)
     A            TTFUID    R        O 11 52REFFLD(TTFREC/TTFUID *LIBL/TTSTPFLG)
     A            TTFDSP    R        O 11 68REFFLD(TTFREC/TTFDSP *LIBL/TTSTPFLG)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20130205  115933  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD002         1A  O  9  3
     A                                  9 43'Status'
     A                                      DSPATR(HI)
     A                                  9 66'Disp'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32'Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52' Unique ID  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66'Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press F10 to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
