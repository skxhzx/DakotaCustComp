     A*%%TS  SD  19991124  064526  DAVE        REL-V4R2M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/23/99  DAS  4.13
     A*     - Moved warehouse and item headings up one line.
     A*     - Changed hard code 'Item' and 'Base' labels to output fields
     A*       that will be initialized in the IT810 program.
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940820  094143  DAVE        REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IT810.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  9  4'Item number . . . . . . . . .'
     A            W1ITEM        15A  B  9 35
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 31'View Item Quantities'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  19991124  064526  DAVE        REL-V4R2M0  5769-PW1
     A                                      CHGINPDFT
     A                                      CF13
     A  98                                  ALARM
     A                                      CLRL(23)
     A            W2HEAD        55A  O  4 25
     A            W2HEDX        55A  O  5 25
     A                                  7  2'Press Enter to return.'
     A                                      COLOR(BLU)
     A                                  1  2'IT810.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F5=Refresh  F12=Cancel  F13=Linked-
     A                                       items'
     A                                      COLOR(BLU)
     A                                  1 31'View Item Quantities'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse            :'
     A            ITWHSE         3Y 0O  3 25EDTCDE(4)
     A            WHDESC        30A  O  3 29
     A                                 12  4'Stock . . . . . . .'
     A                                 13  4'Allocated . . . . .'
     A                                 14  4'Inactive  . . . . .'
     A                                 16  4'Available . . . . .'
     A            IQSTK1    R        O 12 25REFFLD(IQREC/IQSTK1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQSTK2    R        O 12 42REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQSTK3    R        O 12 58REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC1    R        O 13 25REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC2    R        O 13 42REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC3    R        O 13 58REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD1    R        O 14 25REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD2    R        O 14 42REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD3    R        O 14 58REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            W2AVL1         7Y 0O 16 25EDTCDE(N)
     A            W2AVL2         5Y 0O 16 42EDTCDE(N)
     A            W2AVL3         5Y 0O 16 58EDTCDE(N)
     A            W2UM1          5A  O 10 28
     A            W2UM2          5A  O 10 43
     A            W2UM3          5A  O 10 59
     A                                  9 28'Normal'
     A                                  9 40'Breakdown 1'
     A                                  9 56'Breakdown 2'
     A                                 11 25'----------'
     A                                 11 40'-----------'
     A                                 11 56'-----------'
     A            W1DUMY         1A  I  2  2DSPATR(PC)
     A                                      DSPATR(PR)
     A            W2LBL1        22A  O  4  2
     A            W2LBLX        22A  O  5  2
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
