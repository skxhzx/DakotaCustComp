     A*%%TS  SD  20081022  120125  BFC         REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  POD310PFM   View PODP log - Display
     A*  Retha Davis
     A*
     A*  Revisions
     A*
520 AA*    10/22/08  RBD  5.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20020823  162605  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE       75A  O  6  5
     A            SFLHIDE      200   H
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20080902  205221  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME       10A  O  1  2
     A                                  1 31'View POD Pallet Log'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  3  2
     A            SCOPT2        75A  O  4  2
     A            SFLHEAD       79A  O  5  1
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20020823  162605  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE2      75A  O  6  5
     A            SFLHIDE      200   H
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20080902  205221  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME       10A  O  1  2
     A                                  1 31'View POD Pallet Log'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  3  2
     A            SCOPT2        75A  O  4  2
     A            SFLHEAD2      79A  O  5  1
     A*
     A*
     A          R FKEY1
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F11=Next view  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20081022  120125  BFC         REL-V5R4M0  5722-WDS
     A                                      WINDOW(4 20 18 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'From date . .'
     A            W1DATE         6Y 0B  3 15EDTCDE(3)
     A                                      DSPATR(HI)
     A                                  3 24'MMDDYY'
     A                                  4  1'Warehouse . .'
     A            W1WHSE         3Y 0B  4 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5  1'Route . . . .'
     A            W1RTE          5A  B  5 15DSPATR(HI)
     A                                  5 24'*ALL'
     A                                  6  1'Route id  . .'
     A            W1RTID         5A  B  6 15DSPATR(HI)
     A                                  6 24'*ALL'
     A                                  7  1'Stop  . . . .'
     A            W1STOP         2Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  7 24'0=All'
     A                                  8  1'Pallet id . .'
     A            W1PLID         7Y 0B  8 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  8 24'0=All'
     A                                  9  1'Tote id . . .'
     A            W1UP#          5Y 0B  9 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  9 24'0=All'
     A                                 10  1'Cust Ord# . .'
     A            W1CORD         7Y 0B 10 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 10 24'0=All'
     A                                 11  1'Order id  . .'
     A            W1ORD          7Y 0B 11 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                 11 24'0=All'
     A                                 12  1'Errors only .'
     A            W1ERRONLY      1A  B 12 15DSPATR(HI)
     A                                 12 24'Y,N'
     A                                 14  1'Order by  . .'
     A            W1ORDERBY      1A  B 14 15DSPATR(HI)
     A                                 14 24'1=Rte/Pal/Try'
     A                                 15 24'2=Creation date'
     A                                 17  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW2
     A*%%TS  SD  20080904  184857  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 20 7 18)
     A                                  2  2'Are you sure you'
     A                                  3  2'want to resend?'
     A            YNOPT          1   B  5  6DSPATR(HI)
     A                                      VALUES('Y' 'N')
     A                                  5  9'(Y,N)'
