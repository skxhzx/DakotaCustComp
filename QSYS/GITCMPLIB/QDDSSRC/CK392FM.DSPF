     A*%%TS  SD  20090124  150237  CHUCKM      REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  CK392FM  Pallet lookup by route
     A*  29 October, 2001
     A*  Hemant Kapadia
     A*
     A*  Revisions:
     A*
     A*  06/20/02 HNK 4.16
     A*    - Label file field names changed to WBXXXX in CK391.
     A*
520 AA*  01/24/09 CWM 5.20
     A*    - Changed Stop to 3 Character for use with Host Stop# Chg.
     A*
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'CK392 '
     A                                  1 30'Pallet Details'
     A                                      DSPATR(HI)
     A            W1TITL        30      2 30
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20090124  150237  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 47'Position to label . . . .'
     A            PTINP2         7Y 0B  3 74EDTCDE(4)
     A            PTDMY1        78A  O  1  2
     A                                  2 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3  2'Route:'
     A            W1RTE          5A  O  3  9
     A                                  3 15'/'
     A            W1RTID         5A  O  3 17
     A                                  3 24'Pallet:'
     A            W1PAL#         2S 0O  3 32
     A                                  3 35'/'
     A            W1PLID         7S 0O  3 37
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20011029  160120  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416 MA            WBLBL#         7Y 0O 11  7EDTCDE(3)
     A            W1ITEM        10A  O 11 15
     A            W1DESC        20A  O 11 26
416 MA            WBQPCK         5Y 0O 11 48EDTCDE(3)
     A            W1CKBY        25   O 11 54
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20011029  160120  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 26'Description         '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 15'Item      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A                                  9 47'Qty'
     A                                      DSPATR(HI)
     A                                 10 47'Picked'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Checked by               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20011030  104411  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416 MA            WBITEM        15A  O 11  5
416 MA            WBCUBE         9Y 3O 11 21EDTCDE(3)
416 MA            WBSWGT         9Y 2O 11 32EDTCDE(3)
416 MA* 520       WBSTOP         2Y 0O 11 44EDTCDE(3)
520 AA            WBSTOP         2Y 0H
520 AA            DSPSTP         3Y 0O 11 44EDTCDE(3)
416 MA            WBORD          7Y 0O 11 48EDTCDE(3)
     A            W1PKBY        25A  O 11 56
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20011030  104411  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Op'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 21'     Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35' Weight'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 43'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Order #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 56'Picked by                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416 MA            WBITEM        15   O 11  7
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000803  104317  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
