     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  UT962FM   Get save file from PIRTFR on BFCDEV
     A*  November 2, 2020
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'UT962.01'
     A                                  1 28'Get Save File from BFCDEV'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 18'1. Creates the local file in PIRTF-
     A                                      R.'
     A                                  6 18'2. Gets the save file via FTP from-
     A                                       BFCDEV PIRTFR.'
     A                                  7 18'3. Does not save the user/password-
     A                                       entered.'
     A                                 10 11'Save File Name  . . . . .'
     A            W1FILE        10A  B 10 37DSPATR(HI)
     A  01                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A                                 11 11'Save File Description . .'
     A            W1DESC        40A  B 11 37DSPATR(HI)
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                 13 11'User ID . . . . . . . . .'
     A            W1USER        10A  B 13 37DSPATR(HI)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A                                 14 11'Password  . . . . . . . .'
     A            W1PWD         20A  B 14 37DSPATR(ND)
     A                                      CHECK(LC)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
