     A*----------------------------------------------------------------
     A*  Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*  BFC Software, Inc.
     A*  (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  TR340PR    Trax Route Completion Report
     A*  31 July 2013
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
650 AA*    07/31/13  RBD  6.50
     A*      - Created.
     A*
750aAA*    07/14/21  RBD  7.50a
     A*      - Added totals for cash, check and credit card amounts.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                     1'Run Date'
     A            RDATE          6S 0     10
     A                                      EDTCDE(Y)
     A            W1HEAD        60A  O    37
     A                                   121'Page'
     A                                   126PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A            RTIME          6S 0     10
     A                                      EDTWRD('0  :  :  ')
     A                                   121'TR340'
     A                                      SPACEA(002)
     A                                     2'WHSE'
     A            WHHEAD        34   O     7
     A                                    43'ROUTE:'
     A            TTRRTE    R        O    50REFFLD(TTRRTE TTRTE)
     A                                    57'ROUTE ID:'
     A            TTRRTI    R        O    67REFFLD(TTRRTI TTRTE)
     A                                    74'DRIVER:'
     A            TTRUSR    R        O    82REFFLD(TTRUSR TTRTE)
     A                                      SPACEA(002)
     A                                     2'DLV START:'
     A            STRMDY         6S 0O    13EDTCDE(Y)
     A            TTRSRT    R        O    22REFFLD(TTRSRT TTRTE)
     A                                      EDTWRD('  :  :  ')
     A                                    32'DLV FINISH:'
     A            FINMDY         6S 0O    44EDTCDE(Y)
     A            TTRFIT    R        O    53REFFLD(TTRFIT TTRTE)
     A                                      EDTWRD('  :  :  ')
     A                                    63'TOT STOPS:'
     A            TTRS      R        O    73REFFLD(TTRS   TTRTE)
     A                                      EDTCDE(3)
     A                                    78'STOPS DLV:'
     A            TTRSD     R        O    88REFFLD(TTRSD  TTRTE)
     A                                      EDTCDE(3)
     A                                    92'TOT PCS:'
     A            W1TPCS         5S 0O   100EDTCDE(3)
     A                                   106'PCS DLV:'
     A            W1DPCS         5S 0O   114EDTCDE(3)
     A                                   120'PCS RET:'
     A            W1RPCS         5S 0O   128EDTCDE(3)
     A                                      SPACEA(001)
     A            UNLINE       132A        1
     A                                      SPACEA(002)
     A**************************************************************************
     A          R HEAD2
     A                                     2 'R E T U R N S :'
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     2 'STOP'
     A                                       UNDERLINE
     A                                     7 'CUSTOMER                '
     A                                       UNDERLINE
     A                                    31 '________________'
     A                                    48 'ITEM           '
     A                                       UNDERLINE
     A                                    64 'DESCRIPTION      '
     A                                       UNDERLINE
     A                                    81 '_____________'
     A                                    95 ' QTY '
     A                                       UNDERLINE
     A                                   101 'RET CD'
     A                                       UNDERLINE
     A                                   108 'TRAX#'
     A                                       UNDERLINE
     A                                   114 'VERIFIED'
     A                                       UNDERLINE
     A                                      SPACEA(002)
     A**************************************************************************
     A          R DETL2
     A            TTSTOP    R        O     3REFFLD(TTSTOP TTRTN)
     A                                      EDTCDE(3)
     A            TTCNAM    R        O     7REFFLD(TTCNAM TTRTN)
     A            TTITEM    R        O    48REFFLD(TTITEM TTRTN)
     A            ITDESC    R        O    64REFFLD(ITDESC PIRITEM)
     A            TTOQTY    R        O    95REFFLD(TTOQTY TTRTN)
     A                                      EDTCDE(3)
     A            TTRCOD    R        O   103REFFLD(TTRCOD TTRTN)
     A            TTUCI     R        O   108REFFLD(TTUCI  TTRTN)
     A                                      EDTCDE(3)
     A                                   114 '________'
     A                                      SPACEA(001)
     A**************************************************************************
     A          R HEAD3
     A                                     2 'F I N A N C I A L / S T O P'
     A                                      SPACEB(001)
     A                                    31 'D E T A I L S :'
     A                                      SPACEA(002)
     A                                     2 'STOP'
     A                                       UNDERLINE
     A                                     7 'CUSTOMER                '
     A                                       UNDERLINE
     A                                    31 '________________'
     A                                    48 'PCS DLV'
     A                                       UNDERLINE
     A                                    56 'PCS RET'
     A                                       UNDERLINE
     A                                    64 'ARRIVED '
     A                                       UNDERLINE
     A                                    73 'DEPARTED'
     A                                       UNDERLINE
     A                                    82 '  CASH   '
     A                                       UNDERLINE
     A                                    92 '  CHECK  '
     A                                       UNDERLINE
     A                                   102 '  CHECK# '
     A                                       UNDERLINE
     A                                   112 '  CREDIT '
     A                                       UNDERLINE
     A                                   125 'VERIFIED'
     A                                       UNDERLINE
     A**************************************************************************
     A          R DETL3
     A            TTPSTP    R        O     3REFFLD(TTPSTP TTSTP)
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            OHCNAM    R        O     7REFFLD(OHCNAM ORDH)
     A            W3DPCS         5S 0O    49EDTCDE(3)
     A            W3RPCS         5S 0O    57EDTCDE(4)
     A            TTPSRT    R        O    64REFFLD(TTPSRT TTSTP)
     A                                      EDTWRD('  :  :  ')
     A            TTPFIT    R        O    73REFFLD(TTPFIT TTSTP)
     A                                      EDTWRD('  :  :  ')
     A            W3CASH         9A       82
     A            W3CHEK         9A       92
     A            W3CHK#         9A      102
     A            W3CRED         9A      112
     A                                   125 '________'
     A**************************************************************************
     A          R HEAD4
     A                                     2 'T O T A L :'
750aAA                                      SPACEB(002)
750aAA                                      SPACEA(002)
750aAA            LRCASH         8S 2     82EDTCDE(4)
750aAA            LRCHEK         8S 2     92EDTCDE(4)
750aAA            LRCRED         8S 2    112EDTCDE(4)
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A                                     2 'CHECKS: _____________'
     A                                    25 'CASH: _____________'
     A                                    46 'CREDIT: _____________'
     A                                    69 'VERIFIED: ________'
