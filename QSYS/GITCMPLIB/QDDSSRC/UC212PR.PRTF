     A****************************************************************
     A*
     A* UC212PR   Multiple UPC code list
     A* 13 May, 2003
     A* Roxanne Harrison
     A*
     A* Revisions:
417aAA*    02/24/04  RH   4.17a
     A*      - Enh: Add vendor#, Department and either pick or 1st
     A*        overflow slot.
417bAA*    03/03/04  RH   4.17b
     A*      - Enh: Add # of multiples to heading.
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*                                     SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A                                   125'UC212'
     A          R SBHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1'Warhouse:'
     A            WPSWHS    R             +1REFFLD(WKSWHS IV151)
     A                                      EDTCDE(3)
417b A                                    35'Multiples greater than:'
417b A            WPSMCT    R             +1REFFLD(WKSWHS IV151)
417b A                                      EDTCDE(3)
     A* 93                                75
     A*                                     '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A***                                   SPACEA(001)
     A*
     A                                     1'UPC                 '
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    23'Item #         '
     A                                      UNDERLINE
417a A                                    40'Vendor #   '
417a A                                      UNDERLINE
417a A                                    53'Dept.'
417a A                                      UNDERLINE
417a A                                    60'Slot        '
417a A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
417aDA*           UPUPC     R              1REFFLD(UPUPC UPC)
417a A            WOUPC     R              1REFFLD(UPUPC UPC)
     A            UPITEM    R             23REFFLD(UPITEM UPC)
417a A            UPVEN     R             40REFFLD(UPVEN UPC)
417a A                                      EDTCDE(3)
417a A            WODEPT    R             53REFFLD(WDCODE PIRDICT)
417a A            WODISP    R             60REFFLD(SLDISP SLOT2)
     A*
     A*
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A* Skip 2 Line
     A*
     A          R SKIP2
     A                                      TEXT('SKIP TWO LINE')
     A                                      SPACEA(002)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'UC212: End of Report '
     A*
