     A*%%TS  SD  20101106  165050  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. ZH12201 COPY AND NAME ZH12202
     A*----------------------------------------------------------------
     A*
     A*  ZH122FM   Zone maintenance - Change screen
     A*  11 June 1997
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    09/12/97  DAS  4.05
     A*      - Added 'Screen _ of _' to each screen.
     A*      - Added 'Enter=' on command line of each screen.
     A*
416 AA*    06/20/01  MLB  4.16
     A*      - Added new option 3 to W2PBLD field.
416aAA*    07/30/01  MLB  4.16a
     A*      - Revised SCREEN2 to add option to start new pallet
     A*        when aisle changes when building using 2=Location.
500 AA*    03/17/06  JCJ  5.00
     A*      - Revised SCREEN1 to add option W1TOTE & W1TCUB
     A*
500aAA*    03/31/06  DAS  5.00a
     A*      - Revised SCREEN2 to add option W2FPCS
     A*
500bAA*    04/03/06  JCJ  5.00b
     A*      - Added Template Code / Template in upper right corner
     A*        of Screen1, 2, and 3.
     A*
500cAA*    04/04/06  DAS  5.00c
     A*      - Created SCREEN2T for Totes.
     A*      - Revised SCREEN1 tote option to Type.
     A*      - Moved external cube from SCREEN1 to SCREEN2T.
     A*      - Moved max pieces from SCREEN2 to SCREEN2T.
     A*
500dAA*    06/25/06  DAS  5.00d
     A*      - W1ZTMP was defined as type 'O'. Changed to 'A'
     A*      - This caused an odd DBCS error to occur.
     A*
510aAA*    08/21/06  DAS  5.10a
     A*      - Changed description of totes from "1 order/tote" to
     A*        "1 stop/tote".
510bAA*    09/05/06  MLB  5.10b
     A*      - Enh: Added new field to screen, W2TORD - Orders/tote.
     A*        Changed discription of totes to remove line that reads
     A*        "1 stop/tote". Moved existing lines down 1 line to make
     A*        room for new option. Moved lines marked, not duplicated.
510cAA*    01/03/07  JCJ  5.10c
     A*      - Enh: Added flag to identify whether the *SP code should
     A*             be honored. *SP flag tells the system that this
     A*             stop should be the only one on the pallet.
     A*
520aAA*    03/03/09  JCJ  5.20a/ P#00048
     A*      - made the aisle change label configurable option.
     A*
520bAA*    06/01/09  JCJ  5.20b/ P#00208
     A*      - add two new options to Zone maintenance.
     A*      - added "Picked up by" on screen 1. This options
     A*        identifies if this is a Selector or Loader merge zone.
     A*      - added a blank line after the Description and added
     A*        wording to the Parent zone line.
     A*
520cAA*    08/06/09  RBD  5.20c  P#00219
     A*      - Added new option " include in tax right ".
     A*      - Added new option " include in check right ".
     A*      - Added new zone type B = Batch.
     A*      - Added screen2B for entering Batch pick zone details.
     A*      - Added screen4 & screen5 which include the following
     A*        labels and report and fields for entering their label
     A*        printing pgm, cutter option, printer device and
     A*        printer form :
     A*        Pick label        Retail label        Tote label
     A*        ASN label         Pick list label     Pick list report
     A*
530 AA*    12/14/09  JCJ  5.30
     A*      - Renamed from ZH12203 to ZH12204.
     A*      - Changed text on screen 2 from "Pallets on fork lift" to
     A*        "Pallet jack..."
     A*      - Added option to print Pick labels for zone pickup to
     A*        screen 5.
     A*
610aAA*    11/06/10  DAS  6.10
     A*      - Renamed to ZH12205.
     A*      - Added zone type S=Separate.
     A*      - Added screen SCREEN3S.
     A*
640aAA*    09/07/11  JCJ  6.40A
     A*      - Renamed to ZH12206.
     A*      - Added zone type S=Separate.
     A*
650a A*    05/04/13  GJA  6.50a
     A*      - Renamed to ZH12207.
     A*      - Added Label printing option.
     A*
700a A*    11/04/16  NLK  7.00a
     A*      - SKIPPED 'Renamed to ZH12208'
     A*      - Added NEW Description to line 16 ON SCRN .02
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20101106  123509  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.01'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
520cDA**                                2 74'1 of 3'
     A                                  2 74'1 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 61'General information'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Zone  . . . . . . . . . . . . . .'
     A            W1ZONE         6A  B  9 39
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                 10  4'Description . . . . . . . . . . .'
     A            W1DESC        30A  B 10 39
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 12  4'Truck compartment . . . . . . . .'
     A            W1COMP         1A  B 12 39
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
520BDA***                              13  4'Parent zone . . . . . . . . . . .'
     A                                 13  4'or Parent zone to merge into  . .'
     A            W1PZON         6A  B 13 39
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'   Picked up by . . . . . . . . .'
     A            W1PKUP         1A  B 14 39
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 47'S=Selector, L=Loader'
     A                                 16  4'Zone type . . . . . . . . . . . .'
     A            W1PTYP         1A  B 16 39
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 47'P=Pallet,T=Tote,B=Batch,S=Separate'
     A                                 18  4'Prevent Single Pallet *SP Picks .'
     A            W1STOP         1A  B 18 39
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 47'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel  Enter=Next screen'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20060422  135554  DAVE        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.02'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
520cDA**                                2 74'2 of 3'
520cMA                                  2 74'2 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 65'Picking options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Minimum cube for bulk pick  . . .'
     A            W2BPCK         3Y 0B 10 39EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
530 D ***                              11  4'Pallets on fork lift  . . . . . .'
530 MA                                 11  4'Pallet jack . . . . . . . . . . .'
530 AA                                 11 45'1=Single, 2=Double'
     A            W2FPAL         1Y 0B 11 39EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Maximum cube per pallet . . . . .'
     A            W2FCUB         5Y 2B 12 39
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 13  4'Maximum stops per pallet  . . . .'
     A            W2FSTP         2Y 0B 13 39
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N24N90                               DSPATR(UL)
     A  92
     AO 90                                  DSPATR(PR)
     A                                 14  4'Pallet building method  . . . . .'
     A            W2PBLD         1A  B 14 39
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 45'1=By stop, 2=By location'
     A                                 15 45'3=Descending by stop'
700aD ***                              16  4'Label printing method . . . . . .'
700aAA                                 16  4'Assign pallets to transaction by.'
650aAA            W2PLBL         1A  B 16 39
650aAA  30                                  DSPATR(RI)
650aAA N30N90                               DSPATR(UL)
650aAA  13                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
700aD ***                              16 45'N=Normal, S=Side by Side'
700aAA                                 16 45'N=Row, S=Column'
     A                                 17  4'New pallet for each aisle . . . .'
     A            W2NWPL         1A  B 17 39
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 45'Y=Yes, N=No'
     A                                 18  4'New pallet for each order . . . .'
     A            W2FORD         1A  B 18 39
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 45'Y=Yes, N=No'
520bAA                                 20  4'Merge pallet should only contain '
520bAA  29                                  DSPATR(ND)
520bAA                                 21  4'items for single truck pallet . .'
520bAA  29                                  DSPATR(ND)
520bAA            W2SPAL         1A  B 21 39
520bAA  28                                  DSPATR(RI)
520bAA N28N90                               DSPATR(UL)
520bAA  08                                  DSPATR(PC)
520bAA  29
520bAAO 90                                  DSPATR(PR)
520bAA  29
520bAAO 90                                  DSPATR(ND)
520bAA                                 21 45'Y=Yes, N=No'
520bAA  29                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Next screen'
     A                                      COLOR(BLU)
     A          R SCREEN2T
     A*%%TS  SD  20060821  191203  DAVE        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.02T'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
520cDA**                                2 74'2 of 3'
520cMA                                  2 74'2 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 65'Picking options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Tote Options'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
510bDA*                                10 17'(1 stop/tote, CHEM items separated-
510bDA*                                      into own totes)'
510bMA                                 10 17'(CHEM items separated into own tot-
510bMA                                      es)'
     A                                      DSPATR(HI)
510bAA                                 11  4'Orders per tote . . . . . . . . .'
510bAA            W2TORD         2Y 0B 11 39EDTCDE(3)
510bAA  26                                  DSPATR(RI)
510bAA N26N90                               DSPATR(UL)
510bAA  01                                  DSPATR(PC)
510bAA  90                                  DSPATR(PR)
510bAA                                 11 56'0=Unlimited'
510bMA                                 12  4'External tote cube  . . . . . . .'
510bMA            W2TECB         5Y 2B 12 39EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510bMA            W2TICB         5Y 2B 13 39
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
510bMA                                 14  4'Maximum pieces per tote . . . . .'
510bMA            W2TPCS         3Y 0B 14 39EDTCDE(3)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
510bMA                                 14 56'0=Unlimited'
510bMA                                 16  4'Pallet Options'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
510bMA                                 17  4'Pallet building method  . . . . .'
510bMA            W2PBLD         1A  B 17 39
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510bMA                                 17 56'1=By stop, 3=Desc by stop'
510bMA                                 18  4'Totes per pallet  . . . . . . . .'
     A                                      DSPATR(ND)
510bMA            W2FTOT         2Y 0B 18 39EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
510bMA                                 19  4'Maximum stops per pallet  . . . .'
     A                                      DSPATR(ND)
510bMA            W2FSTP         2Y 0B 19 39
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
640aD ***                              20  4'New pallet for each order . . . .'
640aD ***                                   DSPATR(ND)
640aD ***         W2FORD         1A  B 20 39
640aD * 27                                  DSPATR(RI)
640aD *N27N90                               DSPATR(UL)
640aD * 07                                  DSPATR(PC)
640aD *                                     DSPATR(PR)
640aD *                                     DSPATR(ND)
640aD ***                              20 56'Y=Yes, N=No'
640aD ***                                   DSPATR(ND)
640aAA                                 20  4'Merge pallet should only contain '
640aAA  29                                  DSPATR(ND)
640aAA                                 21  4'items for single truck pallet . .'
640aAA  29                                  DSPATR(ND)
640aAA            W2SPAL         1A  B 21 39
640aAA  28                                  DSPATR(RI)
640aAA N28N90                               DSPATR(UL)
640aAA  08                                  DSPATR(PC)
640aAA  29
640aAAO 90                                  DSPATR(PR)
640aAA  29
640aAAO 90                                  DSPATR(ND)
640aAA                                 21 55'Y=Yes, N=No'
640aAA  29                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Next screen'
     A                                      COLOR(BLU)
510bMA                                 13  4'Internal tote cube  . . . . . . .'
     A*****************************************************************
520cAA          R SCREEN2B
     A*%%TS  SD  20060422  135554  DAVE        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.02B'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'2 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Batch picking options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Minimum cube for batch pick . . .'
     A            W2BPCK         3Y 0B 10 39EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Pallets on fork lift  . . . . . .'
     A            W2FPAL         1Y 0B 11 39EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Maximum cube per pallet . . . . .'
     A            W2FCUB         5Y 2B 12 39
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 13  4'Maximum stops per pallet  . . . .'
     A            W2FSTP         2Y 0B 13 39
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N24N90                               DSPATR(UL)
     A  92
     AO 90                                  DSPATR(PR)
     A                                 14  4'Maximum items per pallet. . . . .'
     A            W2PITM         4Y 0B 14 39EDTCDE(3)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Pallet building method  . . . . .'
     A            W2PBLD         1A  B 15 39
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 55'1=By stop, 2=By location'
     A                                 16 55'3=Descending by stop'
     A                                 17  4'New pallet for each aisle . . . .'
     A            W2NWPL         1A  B 17 39
     A                                      DSPATR(PR)
     A                                 17 55'Y=Yes, N=No'
     A                                 18  4'New pallet for each order . . . .'
     A            W2FORD         1A  B 18 39
     A                                      DSPATR(PR)
     A                                 18 55'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Next screen'
     A                                      COLOR(BLU)
     A*         R SCREEN2B
     A          R SCREEN2S
     A*%%TS  SD  20101106  164334  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.02S'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'2 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Separate zone options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Pallets on fork lift  . . . . . .'
     A            W2FPAL         1Y 0B 10 39EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Next screen'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  20090810  153951  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.03'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
520cDA**                                2 74'3 of 3'
520cMA                                  2 74'3 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 59'Picking Label Options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Qty level for combined labels . .'
     A            W3CLVL         3Y 0B 10 39
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(3)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11  4'Print combined label warning  . .'
     A                                 11 39'Before:'
     A            W3CWRB         1A  B 11 47
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 51'After:'
     A            W3CWRA         1A  B 11 58
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Print unit of measure warning . .'
     A                                 12 39'Before:'
     A            W3UWRB         1A  B 12 47
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 51'After:'
     A            W3UWRA         1A  B 12 58
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Print aisle change label. . . . .'
     A            W3AISL         1A  B 14 39
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 43'Y=Yes, N=No'
     A                                 15  4'Discrepancy label ratio . . . . .'
     A                                 15 39'One label for every'
     A            W3ERR          2Y 0B 15 59EDTCDE(3)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 62'pick labels'
     A                                 16  4'Catch weight label ratio  . . . .'
     A                                 16 39'One label for every'
     A            W3CWGT         2Y 0B 16 59EDTCDE(3)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 62'catch weight items'
520cAA                                 18  4'Include in Tax Right. . . . . . .'
520cAA            W3TAXR         1A  B 18 39
520cAA  21                                  DSPATR(RI)
520cAA N21N90                               DSPATR(UL)
520cAA  01                                  DSPATR(PC)
520cAA  90                                  DSPATR(PR)
520cAA                                 18 43'Y=Yes, N=No'
520cAA                                 19  4'Include in Check Right. . . . . .'
520cAA            W3CHKR         1A  B 19 39
520cAA  22                                  DSPATR(RI)
520cAA N22N90                               DSPATR(UL)
520cAA  02                                  DSPATR(PC)
520cAA  90                                  DSPATR(PR)
520cAA                                 19 43'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Save and exit'
     A                                      COLOR(BLU)
     A*         R SCREEN3
     A          R SCREEN3S
     A*%%TS  SD  20101106  165050  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.03S'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'3 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Include in Tax Right. . . . . . .'
     A            W3TAXR         1A  B 10 39
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 43'Y=Yes, N=No'
     A                                 11  4'Include in Check Right. . . . . .'
     A            W3CHKR         1A  B 11 39
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 43'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Save and exit'
     A                                      COLOR(BLU)
     A*****************************************************************
520cAA          R SCREEN4
     A*%%TS  SD  20090810  153951  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.04'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'4 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 67'Label Options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  3'Print Standard Pick Labels . .'
     A            W4LB1          1   B 10 34
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 10 37'Y=Yes, N=No'
     A                                 10 53'Use cutter. .'
     A            W4CU1          1   B 10 67
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 10 70'Y=Yes, N=No'
     A N94                             11  3'Program / Device / Form. . . .'
     A  94                             11  3'***** Not Defined *****       '
     A            W4LP1         10   B 11 34
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 11 45'/'
     A            W4DV1         10   B 11 47
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 11 58'/'
     A            W4FM1         10   B 11 60
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 14  3'Print Retail Labels. . . . . .'
     A            W4LB2          1   B 14 34
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 14 37'Y=Yes, N=No'
     A                                 14 53'Use cutter. .'
     A            W4CU2          1   B 14 67
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 14 70'Y=Yes, N=No'
     A N95                             15  3'Program / Device / Form. . . .'
     A  95                             15  3'***** Not Defined *****       '
     A            W4LP2         10   B 15 34
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 15 45'/'
     A            W4DV2         10   B 15 47
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 15 58'/'
     A            W4FM2         10   B 15 60
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 18  3'Print Tote Labels. . . . . . .'
     A            W4LB3          1   B 18 34
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90
     AO 96                                  DSPATR(PR)
     A                                 18 37'Y=Yes, N=No'
     A                                 18 53'Use cutter. .'
     A            W4CU3          1   B 18 67
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90
     AO 96                                  DSPATR(PR)
     A                                 18 70'Y=Yes, N=No'
     A N96                             19  3'Program / Device / Form. . . .'
     A  96                             19  3'***** Not Defined *****       '
     A            W4LP3         10   B 19 34
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90
     AO 96                                  DSPATR(PR)
     A                                 19 45'/'
     A            W4DV3         10   B 19 47
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90
     AO 96                                  DSPATR(PR)
     A                                 19 58'/'
     A            W4FM3         10   B 19 60
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90
     AO 96                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Save and exit'
     A                                      COLOR(BLU)
     A*****************************************************************
520cAA          R SCREEN5
     A*%%TS  SD  20090810  153951  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ZH122.05'
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  2 74'5 of 5'
     A            W1ZTMP        10A  O  3 70
     A                                  4  2'Warehouse:'
     A            WHCODE         3S 0O  4 13
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 67'Label Options'
     A                                      DSPATR(HI)
     A                                  5  2'Truck    :'
     A            WOTRUK        43A  O  5 13
     A                                  6  2'Zone     :'
     A            WOZONE        40A  O  6 13
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  3'Print ASN Labels . . . . . . .'
     A            W5LB4          1   B 10 34
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 10 37'Y=Yes, N=No'
     A                                 10 53'Use cutter. .'
     A            W5CU4          1   B 10 67
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 10 70'Y=Yes, N=No'
     A N94                             11  3'Program / Device / Form. . . .'
     A  94                             11  3'***** Not Defined *****       '
     A            W5LP4         10   B 11 34
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 11 45'/'
     A            W5DV4         10   B 11 47
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 11 58'/'
     A            W5FM4         10   B 11 60
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90
     AO 94                                  DSPATR(PR)
     A                                 14  3'Print Pick List Labels . . . .'
     A            W5LB5          1   B 14 34
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 14 37'Y=Yes, N=No'
     A                                 14 53'Use cutter. .'
     A            W5CU5          1   B 14 67
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 14 70'Y=Yes, N=No'
     A N95                             15  3'Program / Device / Form. . . .'
     A  95                             15  3'***** Not Defined *****       '
     A            W5LP5         10   B 15 34
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 15 45'/'
     A            W5DV5         10   B 15 47
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 15 58'/'
     A            W5FM5         10   B 15 60
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90
     AO 95                                  DSPATR(PR)
     A                                 18  3'Print Pick List Report . . . .'
     A            W5LB6          1   B 18 34
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 37'Y=Yes, N=No'
     A                                 18 53'Use cutter. .'
     A            W5CU6          1   B 18 67
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 70'Y=Yes, N=No'
     A                                 19  3'Program / Device / Form. . . .'
     A            W5LP6         10   B 19 34
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 45'/'
     A            W5DV6         10   B 19 47
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 58'/'
     A            W5FM6         10   B 19 60
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
530 AA                                 21  3'Print Merge Pickup Pick Label -
530 AA                                      or List..'
530 AA  42                                  DSPATR(ND)
530 AA            W5LB7          1   B 21 43
530 AA  36                                  DSPATR(RI)
530 AA N36N90                               DSPATR(UL)
530 AA  16                                  DSPATR(PC)
530 MA  42
530 MAO 90                                  DSPATR(PR)
530 MA  42
530 MAO 90                                  DSPATR(ND)
530 AA                                 21 47'1=Pick Label, 2=Zone Pickup List'
530 AA  42                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel  E-
     A                                      nter=Save and exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
