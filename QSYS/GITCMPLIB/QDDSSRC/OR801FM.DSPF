     A*%%TS  SD  19991109  154706  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR801FM   Allocation/Picking Log Detail - Display
     A*  09 November 1999
     A*  Max Blacknik
     A*
     A*  OR801FM - 4.13
     A*
     A*  Revisions
     A*
416 AA*    02/01/02  MLB  4.16
     A*      - Revised SCREEN 1 to disable fkeys 13,14,15,16 when ind 60=On.
     A*      - Revised SCREEN 1 to disable fkey 21, not used at this time.
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*
     A*****************************************************************
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF07
     A                                      CF08
416 MA N60                                  CF13
416 MA N60                                  CF14
416 MA N60                                  CF15
416 MA N60                                  CF16
416 DA*                                     CF21
     A                                      CF24
     A  98                                  ALARM
     A                                  1  2'OR801.01'
     A                                      DSPATR(PC)
     A                                  1 34'Route Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse . .'
     A            W1WHSE         3Y 0O  4 16EDTCDE(4)
     A                                  4 20'-'
     A            W1WHDS        30A  O  4 22
     A                                  6  2'Processing date/time .'
     A            W1JBDT         6Y 0O  6 25EDTCDE(Y)
     A            W1JBTM         6Y 0O  6 34EDTWRD(' 0:  :  ')
     A                                  7  2'Processed by . . . . .'
     A            W1USER        10A  O  7 25
     A                                  8  2'Workstation  . . . . .'
     A            W1DEV         10A  O  8 25
     A                                 10  2'Route  . . . . . . . .'
     A            W1RTE          5A  O 10 25
     A                                 11  2'Route id . . . . . . .'
     A            W1RTID         5A  O 11 25
     A                                 12  2'Truck  . . . . . . . .'
     A            W1TRUK        10A  O 12 25
     A                                 13  2'Order type . . . . . .'
     A            W1OTYP         1A  O 13 25
     A            W1ODSC        15   O 13 27
     A                                 14  2'Batch number . . . . .'
     A            W1PBAT         7Y 0O 14 25EDTCDE(M)
     A                                 16  2'Restart order number .'
     A N40                                  DSPATR(ND)
     A            W1RORD         7Y 0O 16 25EDTCDE(M)
     A N40                                  DSPATR(ND)
     A                                 18  2'Processing option  . .'
     A            W1PCMD         8A  O 18 25
     A                                 19  2'Processing program . .'
     A            W1PGM         10A  O 19 25
     A                                 20  2'Calling program  . . .'
     A            W1PRG         10A  O 20 25
     A*
     A*
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'OR801.02'
     A                                  1 28'General Picking Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse . .'
     A            W1WHSE         3Y 0O  4 16EDTCDE(4)
     A                                  4 20'-'
     A            W1WHDS        30A  O  4 22
     A                                  6  2'Processing date/time .'
     A            W1JBDT         6Y 0O  6 25EDTCDE(Y)
     A            W1JBTM         6Y 0O  6 34EDTWRD(' 0:  :  ')
     A                                  7  2'Processed by . . . . .'
     A            W1USER        10A  O  7 25
     A                                  8  2'Workstation  . . . . .'
     A            W1DEV         10A  O  8 25
     A                                 10  2'Route  . . . . . . . .'
     A            W1RTE          5A  O 10 25
     A                                 11  2'Route id . . . . . . .'
     A            W1RTID         5A  O 11 25
     A                                 12  2'Truck  . . . . . . . .'
     A            W1TRUK        10A  O 12 25
     A                                 13  2'Order type . . . . . .'
     A            W1OTYP         1A  O 13 25
     A            W1ODSC        15A  O 13 27
     A                                 14  2'Batch number . . . . .'
     A            W1PBAT         7Y 0O 14 25EDTCDE(M)
     A                                 15  2'* Error - General picking options -
     A                                      record not found.'
     A  41                                  DSPATR(RI)
     A N41                                  DSPATR(ND)
     A  41                                  COLOR(RED)
     A                                 16  2'Print exception (shorts) error rep-
     A                                      orts . . .'
     A            W2ERPT         1A  O 16 48
     A                                 16 51'Y=Yes, N=No'
     A                                 17  2'Print exception (shorts) labels  .-
     A                                       . . . . .'
     A            W2ELBL         1A  O 17 48
     A                                 17 51'Y=Yes, N=No'
     A                                 18  2'Using RF Will Call . . . . . . . .-
     A                                       . . . . .'
     A            W2RFWC         1A  O 18 48
     A                                 18 51'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R SCREEN3
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OR801.03'
     A                                  1 29'Truck Header Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0O  4 25EDTCDE(4)
     A                                  4 29'-'
     A            W1WHDS        30A  O  4 31
     A                                  6  2'Processing date/time .'
     A            W1JBDT         6Y 0O  6 25EDTCDE(Y)
     A            W1JBTM         6Y 0O  6 34EDTWRD(' 0:  :  ')
     A                                  7  2'Processed by . . . . .'
     A            W1USER        10A  O  7 25
     A                                  8  2'Workstation  . . . . .'
     A            W1DEV         10A  O  8 25
     A                                 10  2'Route  . . . . . . . .'
     A            W1RTE          5A  O 10 25
     A                                 11  2'Route id . . . . . . .'
     A            W1RTID         5A  O 11 25
     A                                 12  2'Truck  . . . . . . . .'
     A            W1TRUK        10A  O 12 25
     A                                 13  2'Order type . . . . . .'
     A            W1OTYP         1A  O 13 25
     A            W1ODSC        15A  O 13 27
     A                                 14  2'Batch number . . . . .'
     A            W1PBAT         7Y 0O 14 25EDTCDE(M)
     A                                 15  2'* Error - Truck header options rec-
     A                                      ord not found.'
     A  42                                  DSPATR(RI)
     A N42                                  DSPATR(ND)
     A  42                                  COLOR(RED)
     A                                 16  2'Allow bulk picks . . .'
     A            W3BULK         1A  O 16 25
     A                                 16 28'Y=Yes, N=No'
     A                                 17  2'Allow batch picks  . .'
     A            W3BAT          1A  O 17 25
     A                                 17 28'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Subfile for list display.
     A*
     A          R DSPSFL4                   SFL
     A            W4ZONE         6A  O  9  2
     A            W4BPCK         3Y 0O  9 12EDTCDE(J)
     A            W4FPAL         1S 0O  9 25
     A            W4FCUB         5Y 2O  9 35EDTCDE(J)
     A            W4FSTP         2S 0O  9 51
     A            W4FBLD        10A  O  9 62
     A*
     A*
     A          R DSPSFC4                   SFLCTL(DSPSFL4)
     A                                      SFLSIZ(0130)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND(*MORE)
     A                                  1  2'OR801.04'
     A                                  1 30'Zone Picking Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            W1WHSE         3Y 0O  4 13EDTCDE(4)
     A                                  4 17'-'
     A            W1WHDS        30A  O  4 19
     A                                  4 51'Route/Truck:'
     A            W1RTE          5A  O  4 64
     A            W1TRUK        10A  O  4 70
     A                                  5  2'Processing date/time:'
     A            W1JBDT         6Y 0O  5 24EDTCDE(Y)
     A            W1JBTM         6Y 0O  5 33EDTWRD(' 0:  :  ')
     A                                  5 45'By:'
     A            W1USER        10A  O  5 49
     A                                  5 61'Workstn:'
     A            W1DEV         10A  O  5 70
     A                                  7 10'Min Cube/'
     A                                      DSPATR(HI)
     A                                  7 22'Pallets/'
     A                                      DSPATR(HI)
     A                                  7 32'Max Cube Per'
     A                                      DSPATR(HI)
     A                                  7 46'Max Stops Per'
     A                                      DSPATR(HI)
     A                                  7 61'Pallet Build'
     A                                      DSPATR(HI)
     A                                  8  2'Zone  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 10'Bulk Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 22'Forklift'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 32'Fixed Pallet'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 46'Fixed Pallet '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 61'   Method   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for list display.
     A*
     A          R DSPSFL5                   SFL
     A            W5SR01         6A  O  8  2
     A            W5SR02         6A  O  8  9
     A            W5SR03         6A  O  8 16
     A            W5SR04         6A  O  8 23
     A            W5SR05         6A  O  8 30
     A            W5SR06         6A  O  8 37
     A            W5SR07         6A  O  8 44
     A            W5SR08         6A  O  8 51
     A            W5SR09         6A  O  8 58
     A            W5SR10         6A  O  8 65
     A            W5SR11         6A  O  8 72
     A*
     A          R DSPSFC5                   SFLCTL(DSPSFL5)
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND(*MORE)
     A                                  1  2'OR801.05'
     A                                  1 30'Subroutines Processed'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            TLFLD1        40A  O  2 21DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            W1WHSE         3Y 0O  4 13EDTCDE(4)
     A                                  4 17'-'
     A            W1WHDS        30A  O  4 19
     A                                  4 51'Route/Truck:'
     A            W1RTE          5A  O  4 64
     A            W1TRUK        10A  O  4 70
     A                                  5  2'Processing date/time:'
     A            W1JBDT         6Y 0O  5 24EDTCDE(Y)
     A            W1JBTM         6Y 0O  5 33EDTWRD(' 0:  :  ')
     A                                  5 44'By:'
     A            W1USER        10A  O  5 48
     A                                  5 61'Workstn:'
     A            W1DEV         10A  O  5 70
     A                                  7  2'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 16'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 23'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 30'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 37'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 44'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 51'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 58'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 72'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R CMDREC4
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN4        79   O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC5
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN5        79   O  2  2COLOR(BLU)
     A*
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR1013
     A                                      OVERLAY
     A                                      SLNO(10)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A*****************************************************************
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79   O  2  2COLOR(BLU)
