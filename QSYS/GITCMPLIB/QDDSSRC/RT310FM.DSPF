     A*%%TS  SD  20090309  203248  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  RT310FM    Route cube summary - Display
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
520 AA*    03/09/09  DAS  5.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A**                                    CF10
     A**                                    CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20090309  201714  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(ND)
     A            W1RTE1         5A  O  8  5
     A            W1RTID         5A  O  8 13
     A            W1IMPD         6Y 0O  8 20EDTCDE(Y)
     A            W1TYPE         1   O  8 32
     A            W1TTLCUBE      7Y 2O  8 36EDTWRD('     .  ')
     A            W1CLRCUBE      7Y 2O  8 46EDTWRD('     .  ')
     A            W1FRZCUBE      7Y 2O  8 56EDTWRD('     .  ')
     A            W1STOPS        2Y 0O  8 67EDTCDE(3)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20090309  201714  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 29'View Route Cube Summary'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 20'Import'
     A                                      DSPATR(HI)
     A                                  6 36'Total'
     A                                      DSPATR(HI)
     A                                  6 46'Cooler'
     A                                      DSPATR(HI)
     A                                  6 56'Freezer'
     A                                      DSPATR(HI)
     A                                  6 66'Total'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(ND)
     A                                  7  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 12'Rte Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 20'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 30'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 56'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 66'Stops'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20090309  175244  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Active Licenses'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A          R FKEY1
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20090309  203248  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 20 16 42)
     A                                  1 13'Selection Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Warehouse . .'
     A            W9WHSE         3Y 0B  3 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  4  1'Route . . . .'
     A            W9RTE          5A  B  4 15DSPATR(HI)
     A                                  4 23'*ALL'
     A                                  5  1'Route Id. . .'
     A            W9RTID         5A  B  5 15DSPATR(HI)
     A                                  5 23'*ALL'
     A                                  6  1'Type  . . . .'
     A            W9TYPE         1A  B  6 15DSPATR(HI)
     A                                      VALUES('N' 'W' 'B')
     A                                  6 23'N=Norm,W=Will,B=Both'
     A                                  8  1'From/To date.'
     A            W9FRMDTE       6Y 0B  8 15EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  8 22'-'
     A            W9TODTE        6Y 0B  8 24EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  8 32'mmddyy'
     A                                 10  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
