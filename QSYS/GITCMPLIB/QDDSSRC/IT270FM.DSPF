     A*%%TS  SD  19970725  175754  DAVE        REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL620FM   Slot Information - View RF Mini Screen
     A*  05 February, 1997
     A*  Dave Sommerville
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970725  175754  DAVE        REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT270.01'
     A                                  1 27'Specific Slot Replenishment'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse . . . .'
     A            W1WHSE         3Y 0B  7 23
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  4'Department  . . .'
     A            W1WHDP         5A  B  8 23
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Pick slot . . . .'
     A            W1TDIS        12A  B  9 23CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 23  3'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IT270.02'
     A                                  1 12'Slot Repl.'
     A                                      DSPATR(HI)
     A                                  2  2'Whse'
     A            W2WHSE         3Y 0O  2  7EDTCDE(4)
     A                                  2 11'Dept.'
     A            W2WHDP         5A  O  2 17
     A                                  3  2'Item'
     A            W2ITEM        15   O  3  7
     A                                  4  2'Pk slot'
     A            W2TDIS        12A  O  4 10
     A                                  5  2'Fm slot'
     A            W2FDIS        12A  B  5 10CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  6  2'Quantity'
     A            W2QTY          5Y 0B  6 12EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N25N90                               DSPATR(HI)
     A                                  7  2'F12=Cancel F20=Replenish'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
