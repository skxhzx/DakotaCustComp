     A*%%TS  SD  20150302  145849  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP220FM   PIR options - Replenishment
     A*  12 January 2000
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
413 AA*    01/11/00  MLB  4.13
     A*      - Created.
     A*
414aAA*    07/17/00  MLB  4.14a
     A*      - Added new option to screen.
     A*
416aAA*    12/18/01  MLB  4.16a
     A*      - Added 2 new fields to screen: W1RPEX, W1RPFI
     A*
417 AA*    04/14/03  MLB  4.17
     A*      - Enh: Added 1 new field: W1LMTS.
500 AA*    04/13/06  RH   5.00
     A*      - Enh: Added 1 new field: W1CQTY.
510aAA*    07/12/06  RH   5.10a
     A*      - Enh: Added 1 new field: W1BDRP.
640aAA*    03/01/12  LMC  6.40a
     A*      - Enh: Added 1 new field: W1RSRT.
650aAA*    10/30/14  RBD  6.50a
     A*      - Changed screen format to OP22002.
     A*      - Enh: Added new field: W1SKPR.
650bAA*    03/02/15  RBD  6.50b
     A*      - Changed screen format to OP22003.
     A*      - Enh: Added new field: W1JITW.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20150302  145849  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP220.01'
     A                                  1 30'Replenishment Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Priority replenishment basis  . . -
     A                                      . . . . .'
     A            W1RPLB         8A  B  6 49
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 64'*AVGQTY, *ORDQTY'
     A                                  7  4'Create replenishments up to order -
     A                                      qty . . .'
     A            W1OQRP         1A  B  7 49
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 64'Y=Yes, N=No'
     A                                  8  4'Sort replenishment. . . . . . . . -
     A                                      . . . . .'
     A            W1RSRT        10A  B  8 49
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 64'*DEMAND, *TOSLOT'
     A                                 11  2'RF Replenishment Options'
     A                                      DSPATR(UL)
     A                                 12  4'Allow replenishments on expired it-
     A                                      ems . . .'
     A            W1RPEX         1A  B 12 49
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 59'1=Allow, 2=Don''t allow'
     A                                 13 59'3=Warn'
     A                                 14  4'Allow replenishments out of FIFO o-
     A                                      rder  . .'
     A            W1RPFI         1A  B 14 49
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 59'1=Allow, 3=Warn'
     A                                 15  4'Limit Specific Slot Replenishments-
     A                                       to only '
     A                                 16  6'one overflow slot at a time . . . -
     A                                      . . . .'
     A            W1LMTS         1A  B 16 49
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 59'Y=Yes, N=No'
     A                                 17  4'Force check of "To Slot" quantity -
     A                                      . . . . .'
     A            W1CQTY         1A  B 17 49
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 59'Y=Yes, N=No'
     A                                 18  4'Check for Bulk drops  . . . . . . -
     A                                      . . . . .'
     A            W1BDRP         1A  B 18 49
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 59'Y=Yes, N=No'
     A                                 19  4'Skip if pick slot physical quantit-
     A                                      y is'
     A                                 20  6'greater than item replenishment le-
     A                                      vel . .'
     A            W1SKPR         1A  B 20 49
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20 59'Y=Yes, N=No'
     A            W1JITW         1A  B 21 49
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21 59'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 21  4'Show JIT window during spec slot r-
     A                                      epl . . .'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
