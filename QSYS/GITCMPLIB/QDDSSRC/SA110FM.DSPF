     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SA110FM - Special Aisle Code Maintenance.
     A*  Chuck Manny
     A*
     A*  Revisions
     A*
520 AA*    02/16/09  CWM  5.20 - Created
520 AA*    02/24/09  CWM  5.20 - Added Subfile Message Processing
     A*
     A*----------------------------------------------------------------
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA08
     A                                      CA10
     A                                      CA12
     A                                      CA23
     A*
     A*****************************************************************
     A* Subfile / Subfile Control
     A*****************************************************************
     A*
     A          R AISP01S1                  SFL
     A*
     A            SFOPT1         2A  B 10  2CHGINPDFT(UL)
     A                                      CHECK(RB)
     A                                      VALUES(' 2' ' 4' ' 5')
     A            SFSAAISL       3A  O 10  8
     A            SFSADESC      30A  O 10 14
     A*
     A          R AISP01C1                  SFLCTL(AISP01S1)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0010)
     A N34                                  ROLLUP(25)
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A            RRN            4S 0H      SFLRCDNBR
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  4'2=Change'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            XXSAAISL       3A  I  5 75CHECK(RZ)
     A                                  5 50'Position To Aisle . . .'
     A                                  1 25'Special Aisle Code Maintenance'
     A                                      DSPATR(HI)
     A                                  1  2'AISP01-FM'
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  7 24'5=Review'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  6  2'Type Options, Press Enter.'
     A                                      COLOR(BLU)
     A                                  9  7'Aisle'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  9 14'Description                   '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  7 14'4=Delete'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A* Help Text Window
     A*****************************************************************
     A*
     A          R AISP01H1
     A                                      WINDOW(*DFT 20 45)
     A                                      OVERLAY
     A                                      RMVWDW
     A                                      USRRSTDSP
     A**                                    MOUBTN(*ULP CF12)
     A                                  2  1'Special Aisle Code Maintenance Hel-
     A                                      p'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  1'To add a new Aisle Code press F6'
     A                                  6  1'To change an existing Aisle Code e-
     A                                      nter a 2'
     A                                  7  1'in the Option field next to the Ai-
     A                                      sle Code'
     A                                  8  1'record you want to change. (field -
     A                                      exit/enter)'
     A                                 11  1'the option field and press enter. -
     A                                       On the'
     A                                 12  1'following screen press enter to co-
     A                                      mplete.'
     A                                 10  1'To delete an existing record enter-
     A                                       a 4 in'
     A                                 14  1'To find an existing record enter t-
     A                                      he code'
     A                                 15  1'in the position to field and press-
     A                                       enter.'
     A                                 16  1'This will position the list to the-
     A                                       record'
     A                                 17  1'or closest logical record searched-
     A                                       for.'
     A                                 19  1'F12=Return'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A* Help Text Window
     A*****************************************************************
     A*
     A          R AISP01R1
     A                                  1  2'AISP02.FM'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            FMSAAISL       3A  B  8 31DSPATR(HI)
     A  11                                  DSPATR(PC)
     A  73                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  21                                  DSPATR(RI)
     A            FMSADESC      30A  B 10 31DSPATR(HI)
     A  12                                  DSPATR(PC)
     A  73                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                 23  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 27'Special Aisle Code Maintenance'
     A                                      DSPATR(HI)
     A            @MSG1         75A  O 19  2
     A  66                                  DSPATR(RI)
     A            @ACTION       12A  O  2 36DSPATR(HI)
     A                                  8  4'Aisle  . . . . . . . .'
     A                                  5  2'Type Changes, Press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Description  . . . . .'
     A*
     A*****************************************************************
     A* Command Keys
     A*****************************************************************
     A*
     A          R AISP01R2
     A                                 23 14'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 26'F6=Add'
     A                                      COLOR(BLU)
     A                                 23  2'F1=Help'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A* Maintenance Screen
     A*****************************************************************
     A*
     A          R AISP01R3
     A*%%TS  SD  20090220  052148  CHUCKM      REL-V6R1M0  5761-WDS
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1  2'AISP01-FM'
     A                                  1 27'Special Aisle Code Maintenance'
     A                                      DSPATR(HI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  6  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  8'Aisle'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            DLOPT          2A  O 11  2
     A            DLSAAISL       3A  O 11  9
     A            DLSADESC      30A  O 11 16
     A                                 21 73'Bottom'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*****************************************************************
     A*    ERROR MESSAGE SUBFILE RECORD
     A*****************************************************************
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*****************************************************************
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*****************************************************************
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A*****************************************************************
     A*    ERROR MESSAGE SUBFILE CLEAR MESSAGE LINE
     A*****************************************************************
     A*
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A*****************************************************************
     A*    ERROR MESSAGE SUBFILE CLEAR SCREEN
     A*****************************************************************
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A*****************************************************************
     A*
     A* EOD
     A*
