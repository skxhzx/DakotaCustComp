     A*%%***********************************************************************
     A*
     A*   IT461PR  Tie/High Report - Slot order
     A*   12 May 1999
     A*   Hemant Kapadia
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    21
     A*
     A                                    70
     A                                      DFT('Page')
     A            PAGE           4S 0O    74
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    26
     A                                    73
     A                                      DFT('IT461')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        11A  O    36
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                                70
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                    51'Mfg'
     A                                      SPACEB(001)
     A                                    59'Slot'
     A                                    64'Pallet'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Item Number   ')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Description                 ')
     A                                      UNDERLINE
     A                                    49
     A                                      DFT('Tie/High')
     A                                      UNDERLINE
     A                                    59
     A                                      DFT('Type')
     A                                      UNDERLINE
     A                                    66
     A                                      DFT('Code')
     A                                      UNDERLINE
     A                                    72
     A                                      DFT('Tie/High')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('Pick Slot')
     A                                      UNDERLINE
     A                                      SPACEA(001)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  61        THITEM    R        O     1
     A                                      REFFLD(THITEM *LIBL/TIEHIGH)
     A  61        W1DESC        30A  O    17
     A            IMTIE     R        O    49
     A                                      REFFLD(IMTIE *LIBL/ITEMMSC)
     A                                      EDTCDE(4)
     A            SLASH1         1A  O    53
     A            IMHIGH    R        O    54
     A                                      REFFLD(IMHIGH *LIBL/ITEMMSC)
     A                                      EDTCDE(4)
     A            THSDEF    R        O    60
     A                                      REFFLD(THSDEF TIEHIGH)
     A            THPAL     R        O    64
     A                                      REFFLD(THPAL  TIEHIGH)
     A            THTIE     R        O    72
     A                                      REFFLD(THTIE TIEHIGH)
     A                                      EDTCDE(4)
     A            SLASH2         1A  O    76
     A            THHIGH    R        O    77
     A                                      REFFLD(THHIGH TIEHIGH)
     A                                      EDTCDE(4)
     A            W1DISP    R        O    85
     A                                      REFFLD(SLDISP SLOT2)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT461: End of Report')
     A*
