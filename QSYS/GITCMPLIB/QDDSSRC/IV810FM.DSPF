     A*%%TS  SD  19990910  154925  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IV810FM   Cleanup Slot, Label, and Trans file by dept.
     A*  31 August, 1999
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
     A*
412 AA*   08/31/99  HNK  4.12
     A*    - Created
     A*
412aAA*   09/10/99  HNK  4.12a
     A*    - Changed to allow clear files by department.
414 AA*   05/10/00  RH   4.14
     A*    - Corrected heading.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990910  154925  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IV810.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
414 DA*                                 1 30'Clear Inventory Work Files'
414 MA                                  1 30'Clean Slot, Label & Trans Files'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A                                 10  4'Place on job queue ? . . . .'
     A                                      DSPATR(ND)
     A                                 10 65'(Y=Yes, N=No)'
     A                                      DSPATR(ND)
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1JOBQ         1A  B 10 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  9 43'(Name, *ALL)'
     A          R SCREEN2
     A  98                                  ALARM
     A                                      CA10
     A                                      CLRL(23)
     A                                  1  2'IV810.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
414 MA                                  1 30'Clean Slot, Label & Trans Files'
414 DA*                                 1 30'Clear Inventory Work Files'
     A                                      DSPATR(HI)
     A                                  7 12'You are about to cleanup Slot, Lab-
     A                                      el and Transaction file,'
     A                                 11 12'You must close all the open transa-
     A                                      ctions (Receiving, Picking,'
     A                                 12 12'Letdown and Returns) before runnin-
     A                                      g this program.'
     A                                 15 12'If you are not sure, please press -
     A                                      F3 to exit.'
     A                                 17 12'Update for warehouse:'
     A            W2WHSE         3Y 0O 17 35EDTCDE(4)
     A                                 19 35'Press F10 to continue.'
     A                                 23  2'F3=Exit  F10=Continue'
     A                                  8 12'for'
     A            W2WHDP         5   O  8 16DSPATR(HI)
     A                                  8 22'department.  Please make sure that-
     A                                       the department'
     A                                  9 12'shown here is the right one.'
     A          R SCREEN3
     A*%%TS  SD  19990910  152515  HEMANT      REL-V4R2M0  5769-PW1
     A  98                                  ALARM
     A                                      CA10
     A                                      CLRL(23)
     A                                  1  2'IV810.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
414 DA*                                 1 30'Clear Inventory Work Files'
414 MA                                  1 30'Clean Slot, Label & Trans Files'
     A                                      DSPATR(HI)
     A                                  8 21'Last chance to exit.  If you are n-
     A                                      ot doing inventory'
     A                                  9 21'for'
     A                                 11 34'Otherwise'
     A                                 13 21'Press F10 to Cleanup Slot, Label a-
     A                                      nd Transaction'
     A                                 14 21'File.'
     A                                 23  2'F3=Exit  F10=Continue'
     A            W3WHDP         5   O  9 25DSPATR(HI)
     A                                  9 31'department, then press F3 to exit.'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
