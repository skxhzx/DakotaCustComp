     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT310FM    View LICACTIVE file - Display
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
600 AA*    02/25/09  DAS  6.00  P#00045
     A*      - Created.
     A*
600aAA*    06/03/09  DAS  6.00a
     A*      - Added W1LCNS to selection window.
     A*
600bAA*    06/12/09  DAS  6.00b
     A*      - Changed fields on subfiles
     A*
610aAA*    08/24/09  DAS  6.10a
     A*      - Added %generic% to some parms.
     A*
610bAA*    08/24/09  DAS  6.10a
     A*      - Added SCREEN2 as a prompt screen for displaying
     A*        special slots.
     A*
610CAA*    09/02/09  CWM  6.10C
     A*      - Added message subfile processing and error checking for
     A*        SC2.
610dAA*    10/22/10  RH   6.10d
     A*      - Enh: add 'XDK' to screen for slot type.
     A*      - Rename LT310fm to LT31002
     A*
650aAA*    02/18/13  LMC  6.50a
     A*      - Renamed display file from LT31002 to LT31003
     A*      - Enh: Change description of ITEM on selection screen to
     A*         Slot Item.
     A*         Add new field for License Item.
700aAA*    01/05/17  LMC  7.00a
     A*      - Renamed display file from LT31003 to LT31004
     A*      - Enh: Additional w2type = RTN is added.
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LALCNS    R        O  8  5REFFLD(LAREC/LALCNS *LIBL/LICACTIVE)
     A            LAITEM    R        O  8 21REFFLD(LAREC/LAITEM *LIBL/LICACTIVE)
     A            LADISP    R        O  8 37REFFLD(LAREC/LADISP *LIBL/LICACTIVE)
     A            W1PRTY    R        O  8 51REFFLD(LAREC/LAPRTY *LIBL/LICACTIVE)
     A            W1EXPD         6Y 0O  8 53EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  8 62EDTWRD('  /  /  ')
     A            W1QTY         10   O  8 71
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20090612  110233  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Active Licenses'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 37'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 50'Pr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 53'Expires '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 62'Entered '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 71'Quantity  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LALCNS    R        O  8  5REFFLD(LAREC/LALCNS *LIBL/LICACTIVE)
     A            LAOLCNS   R        O  8 22REFFLD(LAREC/LAOLCNS *LIBL/LICACTIV-
     A                                      E)
     A            LACHGEUSR R        O  8 39REFFLD(LAREC/LACHGEUSR *LIBL/LICACT-
     A                                      IVE)
     A            LACHGTS   R        O  8 51REFFLD(LAREC/LACHGTS *LIBL/LICACTIV-
     A                                      E)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20090612  110233  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Active Licenses'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 22'Original'
     A                                      DSPATR(HI)
     A                                  6 39'Change'
     A                                      DSPATR(HI)
     A                                  6 51'Change'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 22'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 39'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 51'Timestamp                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SCREEN2
     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LT310.02'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A                                  7  9'Warehouse . . . .'
     A            W2WHSE         3Y 0B  7 28
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  9  9'Department  . . .'
     A            W2WHDP         5A  B  9 28CHECK(ER)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  9 37'*ALL'
     A                                 11  9'Slot type . . . .'
     A            W2TYPE         3   B 11 28
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
700aDA*                                11 37'RCV, STG, OSS, USR, XDK'
700aMA                                 11 37'RCV, RTN, STG, OSS, USR, XDK'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R FKEY1
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F11=Next view  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20090824  105344  DAVE        REL-V6R1M0  5761-WDS
650aDA*                                     WINDOW(6 17 16 45)
650aMA                                      WINDOW(6 17 17 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order . . . .'
     A            W1SORT         1A  B  3 15DSPATR(HI)
     A                                  3 20'Ascending, Descending'
     A                                  4  1'Order by  . .'
     A            W1OBY          1A  B  4 15DSPATR(HI)
     A                                  4 20'1 = Lcns'
     A                                  5 20'2 = FIFO / Lcns'
     A                                  7  1'Warehouse . .'
     A            W1WHSE         3Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  8  1'Dept  . . . .'
     A            W1WHDP        12A  B  8 15DSPATR(HI)
     A                                  8 32'*ALL'
     A                                  9  1'Slot  . . . .'
     A            W1SLOT        12A  B  9 15DSPATR(HI)
     A                                  9 32'*ALL,%generic%'
     A                                 10  1'Base Slot . .'
     A            W1BASE        12A  B 10 15DSPATR(HI)
     A                                 10 32'*ALL,%generic%'
650aDA*                                11  1'Item  . . . .'
650aMA                                 11  1'Slot Item . .'
     A            W1ITEM        15A  B 11 15DSPATR(HI)
     A                                 11 32'*ALL,%generic%'
     A                                 12  1'License . . .'
     A            W1LCNS        15A  B 12 15DSPATR(HI)
     A                                 12 32'*ALL,%generic%'
650aAA                                 13  1'License Item.'
650aAA            W1ITEML       15A  B 13 15DSPATR(HI)
650aAA                                 13 32'*ALL,%generic%'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
