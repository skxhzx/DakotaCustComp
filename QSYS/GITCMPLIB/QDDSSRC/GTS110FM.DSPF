     A*%%TS  SD  20160226  174117  DAVE        REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  GTS110FM   Global Tomcat server maintenance
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
650 AA*    02/22/13  DAS  5.60
     A*      - Created
     A*
700aAA*    08/28/15  DAS  7.00a
     A*      - Added cluster (C) and debug (D) flags
     A*
700bAA*    02/26/16  DAS  7.00b
     A*      - Added window for cluster start/stop options
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20130222  135424  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                  1  2'GTS110.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 25'Global Tomcat Server Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20130222  135424  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 46'Position to code . . .'
     A            GTSINP1       10A  B  2 70
     A            PTDMY1        78A  O  1  2
     A            PTDMY2        78A  O  3  2
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20130222  171331  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            GTSCODE   R        O 11  6REFFLD(GTSREC/GTSCODE BFCGLOBALF/GL-
     A                                      OTOMSRV)
700aDA**          W1DESC        24A  O 11 17
700aMA            W1DESC        20A  O 11 17
700aAA            GTSCLSTR       1A  O 11 38
700aAA            GTSDEBUG       1A  O 11 40
     A            GTSPORT   R        O 11 42REFFLD(GTSREC/GTSPORT BFCGLOBALF/GL-
     A                                      OTOMSRV)
     A                                      EDTCDE(Z)
     A            W1TOMPATH     21A  O 11 48
     A            W1JDKPATH     10A  O 11 70
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20130222  171331  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            FLD001         1A  O  9  2
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Code      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aDA**                               10 17'Description             '
700aMA                                 10 17'Description         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aAA                                 10 38'C'
700aAA                                      DSPATR(HI)
700aAA                                      DSPATR(UL)
700aAA                                 10 40'D'
700aAA                                      DSPATR(HI)
700aAA                                      DSPATR(UL)
     A                                 10 42'Port '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Tomcat/App Location  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'JDK       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20160226  174117  DAVE        REL-V7R2M0  5770-WDS
     A                                      WINDOW(4 20 8 43)
     A                                  1 12'Extra Cluster Options'
     A                                  3  1'Starting node number . . . . . .'
     A            W2START        2Y 0B  3 34EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A                                  3 38'0 - 20'
     A                                  4  1'Number of nodes to start/end . .'
     A            W2NBR          2Y 0B  4 34EDTCDE(4)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A                                  4 38'1 - 20'
     A                                  5  1'Start/End Apache Load Balancer .'
     A            W2APACHE       1A  B  5 34
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A                                  5 38'Y,N'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
