     A*%%TS  SD  20060601  100026  ROBIN       REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  ER611FM   View Summary by User - Display
     A*  17 September, 2001
     A*  Hemant Kapadia
     A*
     A*  Revisions:
     A*
     A*    09/17/01  HNK  4.16
     A*      - Created.
     A*    10/07/09  GJA  5.30
     A*      - Cleaned up and revamped for suspended transactions
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010917  171002  QSECOFR     REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'ER611.01'
     A                                  1 30'View Summary by User'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20010919  102401  BFC         REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A N22                              1  4' Pcs Do Not Include +
     A                                        Merge Pickup Pcs '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                  2 43'Position to employee . . . .'
     A            PTINP1         5Y 0B  2 73EDTCDE(4)
     A            PTDMY2         1A  O  3  2
     A                                  3  5'Employee:'
     A            W0EMP#         5  0O  3 15EDTCDE(3)
     A                                  3 22'Crew:'
     A            W0CREW         6   O  3 28
     A                                  3 36'Date:'
     A            W0FMDT         6Y 0O  3 42EDTCDE(Y)
     A                                  3 51'to'
     A            W0TODT         6Y 0O  3 54EDTCDE(Y)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1NAME        16A  O 11  7
     A            W1RECV         5Y 0O 11 24EDTCDE(3)
     A            W1PUT          5Y 0O 11 30EDTCDE(3)
     A            W1REPL         5Y 0O 11 36EDTCDE(3)
     A            W1TFR          5Y 0O 11 42EDTCDE(3)
     A            W1SEL          5  0O 11 55EDTCDE(3)
     A            W1RTN          5Y 0O 11 67EDTCDE(3)
     A            W1RTNP         5Y 0O 11 73EDTCDE(3)
     A            W1BULK    R        O 11 61REFFLD(WKREC/WKBULK *LIBL/ER611A)
     A                                      EDTCDE(3)
     A            W1STRN    R        O 11 48REFFLD(WKREC/WKSTRN *LIBL/ER611A)
     A                                      EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060601  093358  ROBIN       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
     A                                  9 73'Retrn'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Employee        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 30'Put  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Repl '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Trfr '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 55'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Retrn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Putawy'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Recv '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 55'Selec'
     A                                      DSPATR(HI)
     A                                  9 61'Bulk'
     A                                      DSPATR(HI)
     A                                 10 61'Picks'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 48'Selec'
     A                                      DSPATR(HI)
     A                                 10 48'Trns'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1EMP#         5Y 0O 11  7EDTCDE(3)
     A            W1NAME        16A  O 11 13
     A            W1IDLE         5Y 2O 11 30EDTCDE(3)
     A            W1DHRS         5Y 2O 11 37EDTCDE(3)
     A            W1IHRS         5Y 2O 11 44EDTCDE(3)
     A            W1VRFY         5Y 0O 11 51EDTCDE(3)
     A            W1EQCK         5Y 0O 11 57EDTCDE(3)
     A            W1RUN          5Y 0O 11 64EDTCDE(3)
     A            W1VTOT         8Y 0O 11 70EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 30'Idle'
     A                                      DSPATR(HI)
     A                                  9 37'Direct'
     A                                      DSPATR(HI)
     A                                  9 44'Indir'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Employee              '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 30'Hours '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Hours '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 44'Hours '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'Vrfy'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 57'Equip'
     A                                      DSPATR(HI)
     A                                 10 57'In/Out'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Runner'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Total'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1EMP#         5Y 0O 11  7EDTCDE(3)
     A            W1NAME        16   O 11 13
     A            W1LOAD         5Y 0O 11 31EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Employee              '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 31'Loader'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A  40                              1 36'Time:'
     A  40        W0FMTM         6Y 0O  1 42EDTWRD('0 :  :  ')
     A  40                              1 51'to'
     A  40        W0TOTM         6Y 0O  1 54EDTWRD('0 :  :  ')
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Pick & Print.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R PASWRD
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
