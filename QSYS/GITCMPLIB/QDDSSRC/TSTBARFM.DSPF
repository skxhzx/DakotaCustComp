     A*%%TS  SD  20121001  091549  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTBARFM  Test CVTBAR
     A*  22 February 2001
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
415 AA*    02/22/01  DAS  4.15
     A*      - Created
     A*
700aAA*    12/09/16  DAS  7.00
     A*      - Added reverse image indicator to desc/value
     A*
740aAA*    04/03/20  DAS  7.40a
     A*      - Added item number
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A                                      CA12
     A                                      CA03
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20010222  111830  DAVE3       REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1CODE         3Y 0O 12  3EDTCDE(4)
     A            W1DPOS         1Y 0O 12  9EDTCDE(4)
     A            W1DESC        30A  O 12 12
700aAA  23                                  DSPATR(RI)
     A            W1POS          3Y 0O 12 43EDTCDE(4)
     A            W1VAL         33A  O 12 47
700aAA  23                                  DSPATR(RI)
     A*
     A***DS3                                SFLLIN(0010)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20121001  091549  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'TSTBAR.01'
     A                                  1 30'Test of CVTBAR Program'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'Barcode . . .'
     A            W1BAR        100A  B  4 18CHECK(ER)
     A                                  6  4'Command . . .'
     A            W1CMD          8A  B  6 18
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 28'*ALL,*FIRST,*NEXT,*CWGTLBS,*UPC,*S-
     A                                      ELLBY,*EXPDT'
     A                                  7 28'*PKGDT,*PRODDT,*SCC-14,*SSCC-18,*A-
     A                                      LLOLD'
     A                                  8  4'Vendor #  . .'
     A            W1VEN#        10Y 0B  8 18EDTCDE(4)
     A                                  9  4'Item #  . . .'
     A            W1ITEM        15A  B  9 18
     A                                  9 34'Enter DEF#nnn to test User-Defined-
     A                                       definition'
     A                                 11  3'Code'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  8'Dec'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 12'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 43'Pos'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 47'Value                            '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
