     A*%%TS  SD  20041116  174533  DAVE        REL-V5R2M0  5722-WDS
414cAA* ***************************************************************
414cAA* *****  If the position of any slot entry field is changed *****
414cAA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414cAA* *****  program.                                           *****
414cAA* ***************************************************************
     A*
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  RTS110FM    Returns StuffIt
     A*  03 February 2004
     A*  Dave Sommerville
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
417 AA*     02/03/04  DAS  4.17
     A*       - Created.
     A*
500 AA*     11/16/04  DAS  5.00
     A*       - Added breakdown quantity to Screen3.
     A*       - Put Code/Memo on same line.
500aAA*     03/23/05  MLB  5.00a
     A*       - Enh: Revised 5.00 mod to consolidate case/each
     A*         qty entry on one line on screen 3.
     A*       - Added catchweight entry field on screen 3.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20041008  065648  DAVE        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(08)
     A  98                                  ALARM
     A                                  1  2'RTA110.01'
     A                                  1 12'Rtns StuffIt Adj'
     A                                      DSPATR(HI)
     A                                  2  1'Whse'
     A            W1WHSE         3Y 0B  2  6
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  3  1'Item'
     A            W1ITEM        15A  B  3  6
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     A  90                                  DSPATR(PR)
     A                                  4  1'UPC'
     A            W1UPC         50A  B  4  6CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A**                                3  1'Dept'
     A**          W1WHDP         5A  B  3  6
     A                                  5  1'Lbl#'
     A            W1LBL#         7Y 0B  5  6EDTCDE(4)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
411 AA*
     A          R SCREEN3
     A*%%TS  SD  20041116  174533  DAVE        REL-V5R2M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      CLRL(08)
     A                                  1  2'RTA110.03'
     A                                  1 12'Rtns StuffIt Adj'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            WOITEM        65A  O  2  8
     A                                  3  2'Slot:'
     A            W2DISP        12A  O  3  8
     A                                  4  1'Quantity:'
     A            W3QTY          5Y 0B  4 12EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A            WOUOM1         5   O  4 18
500aDA*           W3QTY2         5Y 0B  5 12EDTCDE(4)
500aMA            W3QTY2         5Y 0B  4 25EDTCDE(4)
     A  95                                  DSPATR(ND)
     A  23                                  DSPATR(RI)
     A N23N95                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  95                                  DSPATR(PR)
500aDA*           WOUOM2         5   O  5 18
500aMA            WOUOM2         5   O  4 31
     A  95                                  DSPATR(ND)
500aAA*
500aAA                                  5  1'Catchwgt:'
500aAA  68                                  DSPATR(ND)
500aAA            W3CWTR         9Y 2B  5 12EDTCDE(M)
500aAA  24                                  DSPATR(RI)
500aAA N24                                  DSPATR(UL)
500aAA  04                                  DSPATR(PC)
500aAA  68                                  DSPATR(ND PR)
500aAA*
     A                                  6  1'Code/Memo:'
     A            W3CODE         2A  B  6 12
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A            W3MEMO        30A  B  6 15DSPATR(UL)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCANREC
     A*%%TS  SD  20041007  181944  DAVE        REL-V5R2M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'RTA110.02'
     A                                  1 13'Putaway slot:'
     A            W2DISP        12A  B  1 27CHECK(ER)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20040203  201150  DAVE        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Item:'
     A            WOITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Select'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
414cMA  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416aMA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
416bAA                                      TEXT('Hidden field: SVEXPD')
416aMA            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
416aMA            SLPICK         1A  O  5 33
416aMA            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
416aMA                                  4 53'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aMA                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417 AA*****************************************************************
417 AA*
417 AA* SUBFILE FOR LIST DISPLAY (16-Lines)
417 AA*
417 AA          R D16SFL                    SFL
417 AA  85                                  SFLNXTCHG
417 AA            OPTION         2A  B  5  2
417 AA N21N90                               DSPATR(UL)
417 AA  21                                  DSPATR(RI)
417 AA  90                                  DSPATR(PR)
417 AA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
417 AA                                      TEXT('Hidden field: SVEXPD')
417 AA            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
417 AA            SLPICK         1A  O  5 33
417 AA            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
417 AA            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
417 AA            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
417 AA            SLDISP        12A  O  5  6
417 AA            W1QTY         10A  O  5 19
417 AA*
417 AA          R D16SFC                    SFLCTL(D16SFL)
417 AA                                      SFLSIZ(0010)
417 AA                                      SFLPAG(0010)
417 AA                                      ROLLDOWN
417 AA                                      ROLLUP
417 AA                                      OVERLAY
417 AA  01                                  SFLDSP
417 AA  01                                  SFLDSPCTL
417 AA N01                                  SFLCLR
417 AA                                  4  2'Opt'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
417 AA                                  4  6'Slot        '
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
417 AA                                  4 53'Pck Prity'
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
417 AA                                  4 44'Ent Date'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
417 AA                                  4 35'Exp Date'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
417 AA                                  4 30'St P'
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
417 AA                                  4 19'Avail Qty'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
417 AA*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
416 DA*                                     SFLMSGRCD(24)
416 MA                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
416 DA*                                     SLNO(24)
416 MA                                      SLNO(09)
     A                                      CLRL(1)
417 AA*
417 AA*****************************************************************
417 AA*
417 AA* Error Message Subfile Records for 16-line screens
417 AA*
417 AA*    Error Message Subfile Record
417 AA*
417 AA          R M16REC                    SFL
417 AA                                      SFLMSGRCD(15)
417 AA            #MSGK                     SFLMSGKEY
417 AA            #PGMQ                     SFLPGMQ
417 AA*
417 AA*    Error Message Subfile Control Record
417 AA*
417 AA          R M16CTL                    SFLCTL(M16REC)
417 AA  98                                  ALARM
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SFLSIZ(2) SFLPAG(1)
417 AA N97                                  SFLCLR
417 AA  97                                  SFLDSP
417 AA  97                                  SFLEND
417 AA          R M16CLR
417 AA                                      OVERLAY
417 AA                                      LOCK
417 AA                                      SLNO(15)
417 AA                                      CLRL(1)
417 AA*
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A            CMDLN1        79   O  1  2COLOR(BLU)
417 AA*
417 AA*    COMMAND LINES
417 AA*
417 AA          R C16REC
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SLNO(16)
417 AA            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
417 AA*
417 AA          R MORREC16
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SLNO(14)
417 AA            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
409 AA*    CLEAR ALL LINES IN SUBFILE LIST
     A*
409 AA          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
416bAA*
416bAA          R WDWMSG
416bAA                                      WINDOW(1 2 6 20)
416bAA*
416bAA                                      WDWTITLE((*TEXT 'Replenishment Erro-
416bAA                                      r') *CENTER)
416bAA                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
416bAA                                      l') *BOTTOM)
416bAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
416bAA                                          '))
416bAA            WOTXT1        20A  O  1  1
416bAA            WOTXT2        20A  O  2  1
416bAA            WOTXT3        20A  O  3  1
416bAA            WOTXT4        20A  O  4  1
416bAA                               O  5  1'Continue:'
416bAA N40                                  DSPATR(ND)
416bAA            WOYES          1A  B  5 11COLOR(WHT)
416bAA                                      VALUES('Y' 'N')
416bAA N40                                  DSPATR(ND)
416bAA N40                                  DSPATR(PR)
416bAA                               O  5 13'(Y/N)'
416bAA N40                                  DSPATR(ND)
     A*
417aAA* Lot/License plate scan window.
417aAA          R WDWLLP
417aAA                                      WINDOW(1 2 6 20)
417aAA                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
417aAA                                      ENTER)
417aAA                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
417aAA                                      l') *BOTTOM)
417aAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
417aAA                                          '))
417aAA            WWTXT         18   O  2  1
417aAA            WWLLP#        15   B  3  1DSPATR(HI)
417aAA                                      DSPATR(UL)
