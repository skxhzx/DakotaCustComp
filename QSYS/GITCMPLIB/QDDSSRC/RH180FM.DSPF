     A*%%TS  SD  20020124  125728  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RH180FM    Order details. - Display screen
     A*  10 July, 1997
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
416 AA*  01/21/02  RH   4.16
     A*    - Added OPTION field.
     A*
500 AA*  09/18/05  MLB  5.00
     A*    - Enh:  Added two new fields to view 3 subfile and headings
     A*      for new fields to subfile control.
     A*    - Added F10 to dspctl and f11 to vw2sfc rcdfmts.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'RH180.01 '
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 34'Order Details'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2  2'Order # :'
     A            $KCORD         7Y 0O  2 13EDTCDE(4)
     A                                  2 41'Position to seq #. . .'
     A            PTINP1         5Y 0B  2 65EDTCDE(4)
     A                                  3  2'Order ID:'
     A            $KORD          7Y 0O  3 13EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20020124  125728  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
416  A            OPTION         2A  B 11  2
416  A  20                                  DSPATR(PC)
416  A N21N90                               DSPATR(UL)
416  A  21                                  DSPATR(RI)
416  A  90                                  DSPATR(PR)
     A            ODSEQ     R        O 11  7REFFLD(ODREC/ODSEQ *LIBL/ORDD)
     A                                      EDTCDE(4)
     A            ODITEM    R        O 11 13REFFLD(ODREC/ODITEM *LIBL/ORDD)
     A            WOQOR         15A  O 11 30
     A            WOQPK         15A  O 11 48
     A            W1STAT        10A  O 11 65
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20020124  125728  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
500 AA                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  2
416  A                                 10  2'Opt'
416  A                                      DSPATR(UL)
416  A                                      DSPATR(HI)
     A                                 10  7'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Qty Ordered    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Qty Picked     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970714  170646  HEMANT      REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
416  A            OPTION         2A  B 11  2
416  A  20                                  DSPATR(PC)
416  A N21N90                               DSPATR(UL)
416  A  21                                  DSPATR(RI)
416  A  90                                  DSPATR(PR)
     A            ODSEQ     R        O 11  7REFFLD(ODREC/ODSEQ *LIBL/ORDD)
     A                                      EDTCDE(4)
     A            ODITEM    R        O 11 14REFFLD(ODREC/ODITEM *LIBL/ORDD)
     A            ODWH2     R        O 11 31REFFLD(ODREC/ODWH2 *LIBL/ORDD)
     A                                      EDTCDE(4)
     A            ODOR2     R        O 11 37REFFLD(ODREC/ODOR2 *LIBL/ORDD)
     A                                      EDTCDE(4)
     A            ODOITM    R        O 11 47REFFLD(ODREC/ODOITM *LIBL/ORDD)
     A            ODSPCL    R        O 11 67REFFLD(ODREC/ODSPCL *LIBL/ORDD)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970714  170646  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
500 AA                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
416  A                                 10  2'Opt'
416  A                                      DSPATR(UL)
416  A                                      DSPATR(HI)
     A                                 10  7'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 14'ITEM           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 31'Corresponding'
     A                                      DSPATR(HI)
     A                                 10 31'Whse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Original Item  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 66'Special'
     A                                      DSPATR(HI)
     A                                 10 64'Status Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970711  124521  HEMANT      REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
416  A            OPTION         2A  B 11  2
416  A  20                                  DSPATR(PC)
416  A N21N90                               DSPATR(UL)
416  A  21                                  DSPATR(RI)
416  A  90                                  DSPATR(PR)
     A            ODSEQ     R        O 11  7REFFLD(ODREC/ODSEQ *LIBL/ORDD)
500 AA                                      EDTCDE(4)
     A            ODITEM    R        O 11 14REFFLD(ODREC/ODITEM *LIBL/ORDD)
500 AA            OSDISP    R        O 11 30REFFLD(OSREC/OSDISP ORDDS)
500 AA            OSBULK    R        O 11 44REFFLD(OSREC/OSBULK ORDDS)
     A*
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970711  124521  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
416  A                                 10  2'Opt'
416  A                                      DSPATR(UL)
416  A                                      DSPATR(HI)
     A                                 10  7'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 DA*                                10 14'ITEM           '
500 MA                                 10 14'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 AA                                 10 30'Pick Slot   '
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
500 AA                                 10 43'Bulk'
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A*                                 1  2'Type options, press Enter.'
     A*                                     COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
