     A*%%TS  SD  20051220  174924  BFCROCKY    REL-V5R3M0  5722-WDS
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A********************************************************************
     A*
     A* NOTE: COMPILE WITH DEFER WRITE *NO.
     A*
     A********************************************************************
     A*
415bAA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS.
     A*
     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  OR299FM      RF single order pick stage Utility
     A*  20 December 2005
     A*  Roxanne Harrison
     A*
     A*  Revisions
     A*
500 AA*  12/20/05  RH   5.00
     A*    - Created using OR200
     A*    - Program has 2 sets of screens, need to maintain both
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A**                                    CA12
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500 DA**                                    CLRL(08)
500cDA*                                     CLRL(07)
     A                                      OVERLAY
     A                                  1  2'OR299.01'
     A                                  1 11'SOS Utility'
     A                                      DSPATR(HI)
     A                                  2  2'Whse:'
     A            W1WHSE         3Y 0B  2  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  3  2'User:'
     A            W1USER        10   B  3  9
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  4  2'TRN #:'
     A            W1TRN#         7Y 0B  4  9EDTCDE(4)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20A  O  5  2
     A            WOINF2        20A  O  6  2
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN6
     A*%%TS  SD  20050824  101815  ROCKY       REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR299.06'
     A                                  1 11'SOS Utility'
     A*                                 2  2'Ord:'
     A*           WOORD          7Y 0O  2  7EDTCDE(4)
     A*                                 2 15'Stg'
     A*           WOLCTR         3Y 0O  2 19EDTCDE(4)
     A                                  3  2'license:'
     A            W6LIC          7Y 0B  3 11EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  4  2'Stage location:'
VGO1 A*           W6STAG        12A  B  5  4CHECK(ER)
VGO1 A* 22                                  DSPATR(RI)
VGO1 A* 02                                  DSPATR(PC)
VGO1 A*N22                                  DSPATR(UL)
VGO1 A            W6STAG        12A  B  5  4
VGO1 A    N41                               CHECK(ER)
VGO1 A  22N41                               DSPATR(RI)
VGO1 A  02N41                               DSPATR(PC)
VGO1 A N22N41                               DSPATR(UL)
VGO1 A  41                                  DSPATR(PR)
VGO1 A  41                              6  2'PALLETS:'
VGO1 A            W6PALT         2A  B  6 11CHECK(RZ)
VGO1 A                                      CHANGE(43 'PALLET COUNT CHANGED')
VGO1 A  41                                  DSPATR(PC)
VGO1 A N41                                  DSPATR(ND)
VGO1 A  41                              6 14'# PER:'
     A            W6SIDE         1A  B  6 21RANGE('0' '5')
     A                                      CHANGE(43)
VGO1 A N41                                  DSPATR(ND)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN9
     A*%%TS  SD  20020719  183236  BFCMAX      REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR299.09'
     A                                  1 11'Single Pick'
     A                                  2  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  3  1
     A            WOLN02        20A  O  3 22
     A            WOLN03        20A  O  4  1
     A            WOLN04        20A  O  4 22
     A            WOLN05        20A  O  5  1
     A            WOLN06        20A  O  5 22
     A            WOLN07        20A  O  6  1
     A            WOLN08        20A  O  6 22
     A            WOLN09        20A  O  7  1
     A            WOLN10        20A  O  7 22
     A                                  8  1'F12=Cancel'
     A                                      COLOR(BLU)
415bAA*****************************************************************
415bAA*
415bAA*  16 line screens.
415bAA*
     A          R SCREEN61
     A*%%TS  SD  20020603  114115  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR29961'
     A                                  1 10'Single Pick'
     A                                      DSPATR(HI)
     A                                  4  2'Whse:'
     A            W1WHSE         3Y 0B  4  9EDTCDE(4)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  5  2'User:'
     A            W1USER        10   B  5  9
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  6  2'TRN #:'
     A            W1TRN#         7Y 0B  6  9EDTCDE(4)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A                                      CHECK(ER)
     A            WOINF1        20   O  8  2
     A            WOINF2        20   O  9  2
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN66
     A*%%TS  SD  20050824  101815  ROCKY       REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'OR29966'
     A                                  1 10'Single Pick'
     A*                                 3  1'Ord:'
     A*           WOORD          7Y 0O  3  6EDTCDE(4)
     A*                                 3 15'Stg'
     A*           WOLCTR         3Y 0O  3 19EDTCDE(4)
     A                                  7  2'license:'
     A            W6LIC          7Y 0B  7 11CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  8  2'Stage location:'
     A            W6STAG        12A  B  9  5CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
VGO1 A  41                             10  2'PALLETS:'
VGO1 A            W6PALT         2A  B 10011CHECK(RZ)
VGO1 A                                      CHANGE(43 'PALLET COUNT CHANGED')
VGO1 A  41                                  DSPATR(PC)
VGO1 A N41                                  DSPATR(ND)
VGO1 A  41                             10 14'# PER:'
     A            W6SIDE         1A  B 10 21RANGE('0' '5')
     A                                      CHANGE(43)
VGO1 A N41                                  DSPATR(ND)
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN69
     A*%%TS  SD  20020531  124648  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
416gAA  45                                  CA10
     A                                      CA12
     A                                      ALARM
500c A*                                     CLRL(15)
500c A                                      OVERLAY
     A                                  1  2'OR29969'
     A                                  1 10'Single Pick'
     A                                  3  3'* * * ERROR * * *'
     A                                      DSPATR(HI)
     A            WOLN01        20A  O  5  1
     A            WOLN02        20A  O  6  1
     A            WOLN03        20A  O  7  1
     A            WOLN04        20A  O  8  1
     A            WOLN05        20A  O  9  1
     A            WOLN06        20A  O 10  1
     A            WOLN07        20A  O 11  1
     A            WOLN08        20A  O 12  1
     A            WOLN09        20A  O 13  1
     A            WOLN10        20A  O 14  1
     A                                 15  1'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
415bAA*****************************************************************
415bAA*
415bAA* Error Message Subfile Records for 16 line screens
415bAA*
415bAA*    Error Message Subfile Record
415bAA*
415bAA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415bAA*
415bAA*    Error Message Subfile Control Record
415bAA*
415bAA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415bAA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
