     A*%%TS  SD  20080418  152500  GINGERA     REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  OR330VFM  View Order Lookup by item
     A*  10 March 2008
     A*  Ginger Andrews
     A*
     A*  Revisions:
     A*
     A*    03/10/08  GJA
     A*      - Creation
710aAA*  09/19/17  JCJ  7.10
     A*    - Renamed to OR330VFM
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20080401  073037  GINGERA     REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30' Order Lookup by Item '
     A                                      DSPATR(HI)
     A                                  1  2'OR330V'
     A                                  2  2USER
     A                                  2 12'Warehouse'
     A            W1WHSE         3  0O  2 22EDTCDE(3)
     A                                      DSPATR(HI)
     A                                  2 26'-'
     A                                      DSPATR(HI)
     A            W1DESC        30   O  2 28DSPATR(HI)
     A                                  2 72TIME
     A            W1FMDT         6Y 0O  3 30EDTCDE(Y)
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0O  3 44EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  3 40'to'
     A                                      DSPATR(HI)
     A                                  4 30'Item:'
     A                                      DSPATR(HI)
     A            W1ITEM        15   O  4 37
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20080401  080554  GINGERA     REL-V5R4M0  5722-WDS
     A            WKCUST    R        O  7  2REFFLD(WKREC/WKCUST OR330)
     A            W1CNAM        30A  O  7 14
     A            W1DATE         6Y 0O  7 45EDTCDE(Y)
     A            WKCORD    R        O  7 54REFFLD(WKREC/WKCORD OR330)
     A                                  7 62'|'
     A            WKQPCK    R        O  7 64REFFLD(WKREC/WKQPCK OR330)
     A                                      EDTCDE(3)
     A            WKLBL#    R        O  7 70REFFLD(WKREC/WKLBL# OR330)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20080401  080554  GINGERA     REL-V5R4M0  5722-WDS
     A                                      CF09
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  6  2'Number'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2'Customer'
     A                                      DSPATR(HI)
     A                                  6 14'Customer Name'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 45'Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 45'Import'
     A                                      DSPATR(HI)
     A                                  6 54'Order#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 54'Cust'
     A                                      DSPATR(HI)
     A                                  6 64'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 64'Qty'
     A                                      DSPATR(HI)
     A                                  6 70'Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 70'Label'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*
     A            WKCUST    R        O  7  2REFFLD(WKREC/WKCUST OR330)
     A            W1CNAM        30A  O  7 14
     A            W1DATE         6Y 0O  7 45EDTCDE(Y)
     A            WKCORD    R        O  7 54REFFLD(WKREC/WKCORD OR330)
     A                                  7 62'|'
     A            WKRTE     R        O  7 64REFFLD(WKREC/WKRTE OR330)
     A            WKTRN#    R        O  7 70REFFLD(WKREC/WKTRN# OR330)
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20080418  152500  GINGERA     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                  6  2'Number'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2'Customer'
     A                                      DSPATR(HI)
     A                                  6 14'Customer Name'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 45'Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 45'Import'
     A                                      DSPATR(HI)
     A                                  6 54'Order#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 54'Cust'
     A                                      DSPATR(HI)
     A                                  6 64'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 70'Trans#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
