     A*
     A*   IT370PR  Warehouse Short/Out Report
     A*   28 October, 1994
     A*   Hemant Kapadia
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(2)
     A*
     A            UDATE          6S 0      3EDTCDE(Y)
     A                                    54'Warehouse Short/Out Report'
     A                                   122'Page'
     A            PAGE           4S 0    126EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A            TIME           6S 0      3EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125'IT370'
     A                                      SPACEA(2)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'Item Number'
     A                                      UNDERLINE
     A                                    19'Description'
     A                                      UNDERLINE
     A                                    52'Restock Level'
     A                                      UNDERLINE
     A                                    68'Qty available'
     A                                      UNDERLINE
     A*
     A                                      SPACEA(1)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A            IQITEM    R              1REFFLD(IQITEM IT371)
     A            ITDESC    R             19REFFLD(ITDESC IT371)
     A            ITREOR    R             56REFFLD(ITREOR IT371)
     A                                      EDTCDE(3)
     A            QTAVL         10A       71
     A*
     A                                      SPACEA(1)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1                     TEXT('SKIP ONE LINE')
     A                                     1' '
     A                                      SPACEA(1)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                     1'IT371: End of Report'
     A                                      SPACEA(1)
     A*
