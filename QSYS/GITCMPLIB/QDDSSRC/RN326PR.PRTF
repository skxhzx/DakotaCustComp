     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*   RN326PR  Print Customer Returns Analysis - summary
     A*   02 November 2005
     A*   Bob Perry
     A*
     A*  Revisions:
500 AA*  11/02/05  RLP  5.00
     A*    - Created.
510aAA*  01/10/07  RH   5.10a
     A*    - Fix: Change report ID to RN326
510bAA*  03/22/07  RBD  5.10b
     A*    - Revised prtf to add TOTALLR format to print LR total
     A*      count and pieces.
     A*
650 AA*  06/09/14  MLB  6.50
     A*    - Fix: Revised 5.10b mod to increase size of LRRTNR field to
     A*      5,0 from 5,0. Chef's Whse ran into RNQ0103 - Variable too
     A*      small to hold result.
     A*
     A*----------------------------------------------------------------
     A*  Compile options:
     A*
     A*  PAGESIZE(66 132)
     A*  CPI(10)
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(2)
     A*
     A            RPTDATE        6S 0      3
     A                                      EDTCDE(Y)
     A                                    41'Customer Returns Anaylsis Summary'
     A                                   112'Page'
     A            PAGE           4S 0    117EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A            RPTTIM         6S 0      3EDTWRD('  :  :  ')
     A*
     A            WOWHSE        60A       27
510aDA*                                  115'RN325'
510aMA                                   115'RN326'
     A                                      SPACEA(1)
     A            DTERNG        24A       45
     A                                      SPACEA(2)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'Emp'
     A                                    28'Return'
     A                                    37'Return'
     A                                    44'Code'
     A                                      SPACEA(1)
     A*
     A                                     1'Nbr  '
     A                                      UNDERLINE
     A                                     7'Name                '
     A                                      UNDERLINE
     A                                    28'Date    '
     A                                      UNDERLINE
     A                                    37'Code  '
     A                                      UNDERLINE
     A                                    44'Description                   '
     A                                      UNDERLINE
     A                                    75'Count'
     A                                      UNDERLINE
     A                                    81'Pieces'
     A                                      UNDERLINE
     A                                      SPACEA(2)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A            P1EMP#    R              1REFFLD(USEMP# PIRUSER)
     A                                      EDTCDE(4)
     A            P1NAME        20A       +1
     A            P1RDTE         6S 0     +1
     A                                      EDTWRD('  .  .  ')
     A            P1RCOD    R             +3REFFLD(ORRCOD ORRTN  )
     A            P1RDSC    R             +3REFFLD(RNDESC RTNCODE)
     A            RCOUNT         3S 0     +2
     A                                      EDTCDE(4)
     A            PCOUNT         6S 0     +2
     A                                      EDTCDE(4)
     A                                      SPACEA(1)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1                     TEXT('Skip One Line')
     A                                     1' '
     A                                      SPACEA(1)
     A*
     A* Total Line 1
     A*
     A*         R TOTAL1                    TEXT('Total Line 1')
     A*                                     SPACEB(1)
     A*                                  159'* Total pieces returned:'
     A*           TLRTNQ        15A      184
     A*                                     SPACEA(1)
     A*
     A*
510bAA* Total Line LR
510bAA          R TOTALLR                   TEXT('Total Line LR')
510bAA                                      SPACEB(1)
650 DA*                                   58'* Grand Totals :'
650 MA                                    56'* Grand Totals :'
650 DA*           LRRTNR         4S 0     75
650 MA            LRRTNR         5S 0     74
510bAA                                      EDTCDE(4)
510bAA            LRRTNP         7S 0     80
510bAA                                      EDTCDE(4)
510bAA                                      SPACEA(1)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                     1'RN326: End of Report'
     A                                      SPACEA(1)
     A*
