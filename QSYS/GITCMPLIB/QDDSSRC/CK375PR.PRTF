     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  CK375PR    Manager Ship List - Print            CBI
     A*
     A*  Revisions
     A*
CBI AA*    12/30/03  MLB  CBI
     A*      - Created.
520 AA*    01/23/09  CWM  5.20
     A*      - Enh: Changed stop to 3 character from 2 character.
650 AA*    05/29/13  RBD  6.50
     A*      - Enh: Changed stop from 3 num to 9 char stop/drop display.
CBI1AF*    05/20/08  CBI-JGD
     F*      Add to report, next to the Transaction #, the
     F*      name of the selector that picked the route/trans
     F*      orders.
     A*
     A*----------------------------------------------------------------
     A*
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A            RDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A                                   115
     A                                      DFT('Page')
     A            PAGE           4S 0O   119
     A                                      EDTCDE(Z)
     A*
     A            RTIME          6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   118
     A                                      DFT('CK375')
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               100
     A                                      DFT('(Continued)')
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1DFT('Transaction:')
     A            W1TRN#         7S 0O    14EDTCDE(Z)
     A*
CBI1AA            W1SELD         9A  O    24
CBI1AA            W1SELN        28A  O    34
CBI1AA*
     A          R HEAD3
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1DFT('Route:')
     A            W1RTE          5A  O     8
     A                                    15DFT('Stop:')
520 DA*           W1STOP         2S  O    20
650 DA*           W1STOP         3S  O    20
650 MA            W1STOP         9   O    21
650 DA*                                     EDTCDE(Z)
650 DA*                                   24DFT('Customer:')
650 MA                                    31DFT('Customer:')
650 DA*           W1CUST        11S  O    34
650 MA            W1CUST        11S  O    41
     A                                      EDTCDE(Z)
650 DA*           W1CNAM        40A  O    46
650 MA            W1CNAM        40A  O    53
650 DA*                                   88DFT('Dept:')
650 MA                                    95DFT('Dept:')
650 DA*           W1WHDP         5A  O    94
650 MA            W1WHDP         5A  O   101
650 DA*                                  101DFT('Door:')
650 MA                                   108DFT('Door:')
650 DA*           W1DOOR         5S  O   107
650 MA            W1DOOR         5S  O   114
     A                                      EDTCDE(Z)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Pallet')
     A                                      UNDERLINE
     A                                     8
     A                                      DFT('Slot        ')
     A                                      UNDERLINE
     A                                    21
     A                                      DFT('Ship    ')
     A                                      UNDERLINE
     A                                    32
     A                                      DFT('Item    ')
     A                                      UNDERLINE
     A                                    42
     A                                      DFT('Description                   -
     A                                                                   ')
     A                                      UNDERLINE
     A                                   103
     A                                      DFT('Brand     ')
     A                                      UNDERLINE
     A                                   114
     A                                      DFT('Pack           ')
     A                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            W1PLT          3S  O     2
     A                                      EDTCDE(Z)
     A            WBDISP    R        O     8
     A                                      REFFLD(WBDISP CK365)
     A            WBQPCK    R        O    21EDTCDE(3)
     A                                      REFFLD(WBQPCK CK365)
     A            WBUCOD    R        O    27
     A                                      REFFLD(WBUCOD CK365)
     A            W1ITEM        10A  O    32
     A            W1DESC        60A  O    42
     A            W1BRND    R        O   103
     A                                      REFFLD(IMBRND ITEMMSC)
     A            W1PDSC    R        O   114
     A                                      REFFLD(ITPDSC PIRITEM)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A*                                     SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('CK375: End of Report ')
     A*
