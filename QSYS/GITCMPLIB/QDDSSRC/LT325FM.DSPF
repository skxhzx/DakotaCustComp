     A*%%TS  SD  20101103  153042  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT325FM   View LICLOG file - View record
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
610 AA*    11/02/10  DAS  6.10
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA08
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20101103  153042  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LT325.01'
     A                                  1 29'View License Log Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  4'Action . . .'
     A            W1ACTION      60A  O  4 18DSPATR(HI)
     A                                  6  4'License  . .'
     A            W1LCNS        60A  O  6 18DSPATR(HI)
     A                                  8  4'Date/Time  .'
     A            W1DTETIM      20   O  8 18DSPATR(HI)
     A                                 10  4'Employee . .'
     A            W1USER        60A  O 10 18DSPATR(HI)
     A                                 12  4'Action qty .'
     A            W1AQTY        17A  O 12 18DSPATR(HI)
     A                                 13  4'Result qty .'
     A            W1RQTY        17A  O 13 18DSPATR(HI)
     A                                 15  2' '
     A                                 15  4'Tran#  . . .'
     A            W1TRAN        60A  O 15 18DSPATR(HI)
     A                                 17  4'Item . . . .'
     A            W1ITEM        60A  O 17 18DSPATR(HI)
     A                                 19  4'Slot . . . .'
     A            W1SLOT        60A  O 19 18DSPATR(HI)
     A                                 23  2'F3=Exit  F7=Log detail  F8=Tran de-
     A                                      tail  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 21' '
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
