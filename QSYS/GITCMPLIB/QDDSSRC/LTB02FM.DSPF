     A*%%TS  SD  20220119  091556  KENNETHE    REL-V7R4M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UM110FM   Unit of Measure Table Maintenance - Display screen
     A*  06 July, 1994
     A*  Hemant Kapadia
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
500 AA*    02/13/06  DAS  5.00
     A*      - Added voice description fields.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R TITLE
     A*%%TS  SD  20220119  090807  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A                                  1  2'LTB02.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 27'Left the Building Slot/Item'
     A                                      COLOR(WHT)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20220117  180712  KENNETHE    REL-V7R4M0  5770-WDS
     A  21                                  SFLNXTCHG
     A            W1ITEM        15A  O  7  4
     A            W1DESC        30A  O  7 20
     A            W1DISP        12A  O  7 65
     A            W1QTY         13A  O  7 51
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20220119  090434  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      CA07
     A                                      CA12
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                  6  4'Item'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 20'Description'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 65'Slot'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 51'Qty'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20220117  180712  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  1  3'Whse:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2  3'Dept:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W1DEPT         5A  O  2  9
     A            W1WHSE         3A  O  1  9
     A            W1WDSC        30A  O  1 17
     A            W1DDSC        30A  O  2 17
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  20220117  180712  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79A  O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A*%%TS  SD  20220114  160435  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CONFIRM
     A*%%TS  SD  20220119  091556  KENNETHE    REL-V7R4M0  5770-WDS
     A                                      CA10
     A                                      CA12
     A                                      OVERLAY
     A                                 11 28'Press F10 to Print/Update'
