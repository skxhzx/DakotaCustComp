     A*%%TS  SD  20120510  081202  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  HQ112FM    Question File Maintenance - change
     A*  20 February 2012
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    02/20/12  LMC  6.40a
     A*      - Created
640BAA*    10/26/12  LMC  6.40b
     A*      - Display renamed, protect non display fields
650aAA*    04/04/13  LMC  6.50a
     A*      - Display renamed, allow sample and required fields
     A*        to be changed after log record exist.
650bAA*    07/03/13  LMC  6.50b
     A*      - Display renamed, allow frequency field
     A*        to be changed after log record exist.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20120510  081202  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'HQ112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Type . . . . . . . . .'
     A            W1TYPE         3A  B  8 33
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91
     AO 92                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                      CHECK(ER)
     A            W1CODE        15A  B  9 28
     A  22                                  DSPATR(RI)
     A N22N90N42                            DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90
     AO 91
     AO 42
640bAAO 41
     AO 92                                  DSPATR(PR)
     A  41
     AO 42                                  DSPATR(ND)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 25'HACCP Question File Maintenance'
     A                                      DSPATR(HI)
     A                                 13  4'Answer Required. . . .'
     A  41                                  DSPATR(ND)
     A            W1TYPEDESC    20A  O  9  4
     A  41
     AO 42                                  DSPATR(ND)
     A            W1QSTN        45A  B 10 28CHECK(LC)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03N92                               DSPATR(PC)
     A  90
640bAAO 41
     AO 92                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                 10  4'Question . . . . . . .'
     A  41                                  DSPATR(ND)
     A            W1ATYP         1A  B 12 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
640bAAO 41
     AO 92                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                 12  4'Answer Type. . . . . .'
     A  41                                  DSPATR(ND)
     A                                 12 32'(F4)'
     A  41                                  DSPATR(ND)
     A            W1REQD         1A  B 13 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90
650aDA*O 41
650aMAO 41                                  DSPATR(PR)
650aDA*O 92                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                 14  4'Sample Question. . . .'
     A  41                                  DSPATR(ND)
     A            W1SAMP         1A  B 14 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
650aDA*O 41
650aMAO 41                                  DSPATR(PR)
650aDA*O 92                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                 15  4'Validation Program . .'
     A  41                                  DSPATR(ND)
     A            W1TABL        10A  B 15 28
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
640bAAO 41
     AO 92                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A                                 23 36' '
     A            W1LEVL         4A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91
     AO 92                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 16  4'Page Number. . . . . .'
     A  41                                  DSPATR(ND)
     A            W1PGE#         5Y 0B 16 28EDTCDE(Z)
     A  28                                  DSPATR(RI)
     A  41                                  DSPATR(ND)
     A N28N90                               DSPATR(UL)
     A  08
     AO 92                                  DSPATR(PC)
640bAA  41
640bDA* 90                                  DSPATR(PR)
640bMAO 90                                  DSPATR(PR)
     A                                 17  4'Page Sequence Number .'
     A  41                                  DSPATR(ND)
     A            W1PGSQ         3Y 1B 17 28EDTWRD('  . ')
     A  29                                  DSPATR(RI)
     A  41                                  DSPATR(ND)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
640bAA  41
640bDA* 90                                  DSPATR(PR)
640bMAO 90                                  DSPATR(PR)
     A                                 13 32'(Y/N)'
     A  41                                  DSPATR(ND)
     A                                 14 32'(Y/N)'
     A  41                                  DSPATR(ND)
     A                                 11  4'Frequency. . . . . . .'
     A  41
     AO 43                                  DSPATR(ND)
     A                                 11 32'(F4)'
     A  41
     AO 43                                  DSPATR(ND)
     A            W1FREQ         1A  B 11 28
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90
640bAAO 41
650bDA*O 92
     AO 43                                  DSPATR(PR)
     A  41
     AO 43                                  DSPATR(ND)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
