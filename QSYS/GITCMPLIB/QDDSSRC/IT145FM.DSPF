     A*%%TS  SD  20001129  112739  ROCKY       REL-V4R2M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  19940810  121709  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IT145.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  9  4'Item number . . . . . . . . .'
     A            W1ITEM        15A  B  9 35
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 28'Item Dimension Maintenance'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R SCREEN2
     A*%%TS  SD  20001129  112739  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  7'Item:'
     A            W2HEAD        50A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IT145.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 10  4'Package dimensions (LxWxH). .'
     A            W2ULEN         5Y 2B 10 35EDTCDE(4)
415aAA N21N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                 10 42'x'
     A            W2UWDT         5Y 2B 10 44EDTCDE(4)
415aAA N22N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                 10 51'x'
     A            W2UHGT         5Y 2B 10 53EDTCDE(4)
415aAA N23N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
415aAA                                 11  4'Recalculate cube  . . . . . .'
415aAA            W2CALC         1A  B 11 35
415aAA N24N90                               DSPATR(HI)
415aAA  24                                  DSPATR(RI)
415aAA N24N90                               DSPATR(UL)
415aAA  04                                  DSPATR(PC)
415aAA  90                                  DSPATR(PR)
415aAA                                 11 53'Y=Yes, N=No'
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 28'Item Dimension Maintenance'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            ITWHSE         3Y 0O  4 13EDTCDE(4)
     A            WHDESC        30A  O  4 19
     A            OPDUM         15A  O 10 61
