     A****************************************************************
     A*
     A* OR661PR  Catchweight report.
     A* 28 July, 2007
     A* Retha Davis
     A*
     A*  Revisions:
     A*    07/28/07   JCJ  5.10
     A*      - Copied OR660CPR
520 AA*    01/26/09   CWM  5.20
     A*      - Enh: Changed stop from 2 to 3 character.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A            UDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            TITLE         40A  O    21
     A                                    72
     A                                      'Page'
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    26
     A                                    75
     A                                      'OR661'
     A            BATCH         30A  O    26
     A                                      SPACEB(001)
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A            P#DESC        80A  O     1
     A  92                                85
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(002)
     A                                     1
     A                                      'Item'
     A                                    18
     A                                      'Description'
     A                                    60
     A                                      'Weight'
     A                                    68
     A                                      'Stop'
     A                                    75
     A                                      'Order#'
     A                                    83
     A                                      'Uci#'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
     A            LBITEM    R        O     1
     A                                      REFFLD(LBITEM OR661)
     A            ITDESC    R        O    18
     A                                      REFFLD(ITDESC OR661)
     A            T#WGHT        11S 2O    52
     A                                      EDTCDE(2)
520 DA*           LBSTOP    R        O    69
520 DA*                                     REFFLD(LBSTOP OR661)
520 AA            DSPSTP         3  0O    69
     A                                      EDTCDE(Z)
     A            ORDN#          7A  O    75
     A            UCINBR    R        O    83
     A                                      REFFLD(UCINBR OR661)
     A*
     A* Print totals
     A*
     A*  Pallet totals
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD          25A  O    20
     A            T#WGHT    R        O    52
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Compartment totals
     A*
     A          R TOTAL3
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#WGHT    R        O    52
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Route totals
     A*
     A          R TOTAL4
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#WGHT    R        O    52
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Item totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#WGHT    R        O    52
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                     1
     A                                      'OR661:  End of Report '
     A*
