     A*%%TS  SD  19940708  113503  HEMANT      REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OS112FM   Will Call Selection Maintenance - Change screen
     A*  11 September, 1998
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940708  113503  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OS112.01'
     A                                  1 28'Will Call Selection '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  8 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Order Type . . . . . .'
     A            W1OTYP         1   B  9 29
     A  90
     AO 91                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                 10  4'Customer order # . . .'
     A            W1CORD         7Y 0B 10 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90N91                            DSPATR(HI)
     A N23N90N91                            DSPATR(UL)
     A                                 11  4'Route  . . . . . . . .'
     A            W1RTE          7Y 0B 11 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90N91                            DSPATR(HI)
     A N24N90N91                            DSPATR(UL)
     A                                 12  4'Pick Zone  . . . . . .'
     A            W1ZONE         6A  B 12 29
     A  90
     AO 91                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25N90N91                            DSPATR(HI)
     A N25N90N91                            DSPATR(UL)
     A                                 13  4'Transaction #. . . . .'
     A            W1TRAN         7Y 0B 13 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90N91                            DSPATR(HI)
     A N26N90N91                            DSPATR(UL)
     A                                 14  4'Order ID # . . . . . .'
     A            W1ORD          7Y 0B 14 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N32N90N91                            DSPATR(HI)
     A N32N90N91                            DSPATR(UL)
     A                                 15  4'Needed Date. . . . . .'
     A            W1NDTE         6Y 0B 15 29EDTCDE(Y)
     A  90
     AO 91                                  DSPATR(PR)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90N91                            DSPATR(HI)
     A N30N90N91                            DSPATR(UL)
     A                                 16  4'Needed Time  . . . . .'
     A            W1NTIM         6Y 0B 16 29
     A                                      EDTWRD(' 0:  :  ')
     A  90
     AO 91                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N31N90N91                            DSPATR(HI)
     A N31N90N91                            DSPATR(UL)
     A                                 17  4'Status . . . . . . . .'
     A            W1STAT         1A  B 17 29
     A  90
     AO 91                                  DSPATR(PR)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N90N91                            DSPATR(HI)
     A N28N90N91                            DSPATR(UL)
     A                                 18  4'Priority . . . . . . .'
     A            W1PRTY         1Y 0B 18 29EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N90                               DSPATR(HI)
     A N29N90                               DSPATR(UL)
     A                                 19  4'Selector . . . . . . .'
     A            W1USER        10A  B 19 29
     A  90                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A                                 23  2'F4=Prompt F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
