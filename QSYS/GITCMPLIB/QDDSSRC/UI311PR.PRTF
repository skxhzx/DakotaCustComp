     *----------------------------------------------------------------
     *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     *   BFC Software, Inc.
     *   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     *   West Chicago, IL  60185
     *   (630) 562-0375
     *----------------------------------------------------------------
     A****************************************************************
     A*
     A* UI311PR    Warehouse Quantity UCI Report
     A* September 5, 2006
     A* Randy Haselhorst
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    33
     A                                      DFT('Warehouse Quantity UCI -
                                            Report')
     A                                   118DFT('Page')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                   123DFT('UI311')
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A*
     A                                     8
     A                                      DFT('Report Criteria:')
     A                                    27
     A                                      DFT('Warehouse:')
     A            $WHSE          3S 0O    39
     A                                    45DFT('Department:')
     A            $DEPT          5A  O    58
     A                                    66DFT('Item:')
     A            $ITEM         15A  O    73
     A                                    91DFT('Variance Only:')
     A            VAR            3A  O   107
     A                                      SPACEA(002)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     5DFT('Item')
     A                                    22DFT('Description')
     A                                    56DFT('Dept')
     A                                    63DFT('Ship Weight')
     A                                    79DFT('Total UCI')
     A                                    95DFT('Total Item Qty')
     A                                      SPACEA(001)
      *
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            ITITEM    R              5REFFLD(ITITEM PIRITEM)
     A            ITDESC    R             22REFFLD(ITDESC PIRITEM)
     A            ITWHDP    R             56REFFLD(ITWHDP PIRITEM)
     A            ITSWGT    R             63REFFLD(ITSWGT PIRITEM)
                                            EDTCDE(3)
     A            $UCIQTY   R             79REFFLD(UCIQTY UCICW)
                                            EDTCDE(3)
     A            IQSTK1    R             95REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(3)
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R TOTALS
     A                                      SPACEB(002)
     A                                     5DFT('Total Items Printed:')
     A            COUNT          4S 0O    26
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                     5DFT('UC311: End of Report')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R NOTOTAL
     A                                      SPACEB(002)
     A                                     5DFT('No items to report')
     A                                      SPACEA(002)
     A                                     5DFT('UC311: End of Report')
