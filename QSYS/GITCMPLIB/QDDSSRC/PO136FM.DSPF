500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO136FM    License entry P.O. receiving
     A*  10 June, 1997
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*    09/17/97  DAS  4.05
     A*      - Added cross dock field.
     A*      - Turned W1TRN# into a hidden filed instead of just non-display.
     A*    10/17/97  RH   4.07
     A*      - Moved fields 1 character left to make position for More sign +.
     A*    07/26/99  RH   4.12
     A*      - Change SFLMORE option to 'Y' to place More at bottom of screen.
412 AA*    09/07/99  RH   4.12
     A*      - Add "Directed Put away" to screen.
     A*
415 AA*    11/30/00  DAS  4.15
     A*      - Added hidden fields W1LBL# and W1SEQ.
     A*
415aAA*    03/10/01  DAS  4.15a
     A*     - Revised to allow us by PO200 RF small screen receiving.
     A*     - Added SCREEN2.
     A*     - Added MSGREC2, MSGCTL2 and MSGCLR2 to be used with SC2.
     A*
416aAF*    05/23/02  RRH  4.16a
     F*      - Increase W2LCNS to 15. Changed SFLLIN to 2 from 6.
     A*        Changed starting position of W2XDCK.
     A*
417 AA*    01/02/03  MLB  4.17
     A*     - Enh: Added W1UTYP, W1WHDP, W1AISL, W1LOC, W1RLVL, W1HAND
     A*       as hidden fields to Detail 1.
     A*       This is in support of lot/license tracking.
     A*
500 AA*   06/07/06  JCJ  5.00b
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20020523  145813  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1TRN#         7S 0H
     A            W1LBL#         7S 0H
     A            W1SEQ          5S 0H
     A            W1UM           2A  H
417 AA            W1UTYP    R        H      REFFLD(LBUTYP LABEL)
417 AA            W1WHDP    R        H      REFFLD(LBWHDP LABEL)
417 AA            W1AISL    R        H      REFFLD(LBAISL LABEL)
417 AA            W1LOC     R        H      REFFLD(LBLOC  LABEL)
417 AA            W1RLVL    R        H      REFFLD(LBRLVL LABEL)
417 AA            W1HAND    R        H      REFFLD(LBHAND LABEL)
     A            W1XDK          1A  O 12 22
     A            W1QTY          5Y 0O 12  3EDTCDE(3)
     A            W1TIE          4Y 0O 12 10EDTCDE(4)
     A                                 12 15'/'
     A            W1HI           2Y 0O 12 17EDTCDE(4)
     A            W1LCNS        15A  B 12 26
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A* *DS3                                SFLLIN(0010)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20020523  145813  ROCKY       REL-V4R2M0  5769-PW1
416aDA* *DS3                                SFLLIN(0006)
416aMA  *DS3                                SFLLIN(0002)
     A                                      SFLSIZ(0072)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PO136.01'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'License Entry'
     A                                      DSPATR(HI)
     A                                  4  2'PO number . .'
     A            W1PO           9A  O  4 16
     A                                  4 60'Directed Put away'
     A                                  5  2'Warehouse . .'
     A            $PWHSE         3Y 0O  5 16EDTCDE(4)
     A                                  5 20'-'
     A            WHDESC        30A  O  5 22
     A                                  6  2'Item number .'
     A            WOITEM        65A  O  6 16
     A            WODSC1        40A  O  7 16
     A            WODSC2        40A  O  8 16
     A                                  9  2'Quantity received . . '
     A            W1QRC1         5Y 0O  9 25EDTCDE(4)
     A            W1UM1          5A  O  9 32
     A            W1QRC2         5Y 0O  9 39EDTCDE(4)
     A            W1UM2          5A  O  9 46
     A            W1QRC3         5Y 0O  9 53EDTCDE(4)
     A            W1UM3          5A  O  9 60
     A                                 11  3'Qty. '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 10'Tie/High '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 26'License'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 43'Qty. '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 50'Tie/High '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 66'License'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 20'XDock'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 60'XDock'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R SCREEN2
     A*%%TS  SD  20010307  161902  DAVE3       REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CF07
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'PO136.02'
     A                                  1 12'RF Recv'
     A                                  2  1'P.O.'
     A            W2PO           9A  O  2  6
     A            W2LINE         5Y 0O  2 16EDTCDE(4)
     A            W2ITEM        65A  O  3  1
     A            W2PCTR         4Y 0O  4  1EDTCDE(4)
     A                                  4  6'of'
     A            W2PAL          4Y 0O  4  9EDTCDE(4)
     A                                  4 14'pallets'
     A                                  5  1'Qty'
     A            W2QTY          5Y 0O  5  5EDTCDE(4)
     A            W2UM           2   O  5 11
     A            W2TIHI         7   O  5 14
     A                                  6  1'Lic'
416aDA*           W2LCNS         7Y 0B  6  5EDTCDE(4)
416aMA            W2LCNS        15A  B  6  5
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A N33N90                               DSPATR(HI)
416aMA            W2XDK          3   O  6 22
416aDA*           W2XDK          3   O  6 14
     A                                  7  1'F7=Prev'
     A                                      COLOR(BLU)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20010313  080513  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS for SCREEN2
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC2                   SFL
     A*%%TS  SD  20010313  080513  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(08)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL2                   SFLCTL(MSGREC2)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*                                     OVERLAY
     A          R MSGCLR2
     A*%%TS  SD  20010313  080513  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(08)
     A                                      CLRL(01)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R DETL1SP                   SFL
     A*%%TS  SD  20020523  145813  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1TRN#         7S 0H
     A            W1LBL#         7S 0H
     A            W1SEQ          5S 0H
     A            W1UM           2A  H
417 AA            W1UTYP    R        H      REFFLD(LBUTYP LABEL)
417 AA            W1WHDP    R        H      REFFLD(LBWHDP LABEL)
417 AA            W1AISL    R        H      REFFLD(LBAISL LABEL)
417 AA            W1LOC     R        H      REFFLD(LBLOC  LABEL)
417 AA            W1RLVL    R        H      REFFLD(LBRLVL LABEL)
417 AA            W1HAND    R        H      REFFLD(LBHAND LABEL)
     A            W1XDK          1A  O 12 22
     A            W1QTY          5Y 0O 12  3EDTCDE(3)
     A            W1TIE          4Y 0O 12 10EDTCDE(4)
     A                                 12 15'/'
     A            W1HI           2Y 0O 12 17EDTCDE(4)
     A            W1LCNS        15A  B 12 26
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A* *DS3                                SFLLIN(0010)
500 AA*----------------------------------------------------------------
500 AA          R HEAD1SP                   SFLCTL(DETL1SP)
     A*%%TS  SD  20020523  145813  ROCKY       REL-V4R2M0  5769-PW1
416  A* *DS3                                SFLLIN(0006)
     A  *DS3                                SFLLIN(0002)
     A                                      SFLSIZ(0072)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'PO136.01'
     A                                  1 35'Recibir PO  '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Incorporar Lic.'
     A                                      DSPATR(HI)
     A                                  4  2'# de PO . . .'
     A            W1PO           9A  O  4 16
     A                                  4 60'Guardar Directado'
     A                                  5  2'Almacen . . .'
     A            $PWHSE         3Y 0O  5 16EDTCDE(4)
     A                                  5 20'-'
     A            WHDESC        30A  O  5 22
     A                                  6  2'# de producto'
     A            WOITEM        65A  O  6 16
     A            WODSC1        40A  O  7 16
     A            WODSC2        40A  O  8 16
     A                                  9  2'Cantidad recibida . . '
     A            W1QRC1         5Y 0O  9 25EDTCDE(4)
     A            W1UM1          5A  O  9 32
     A            W1QRC2         5Y 0O  9 39EDTCDE(4)
     A            W1UM2          5A  O  9 46
     A            W1QRC3         5Y 0O  9 53EDTCDE(4)
     A            W1UM3          5A  O  9 60
     A                                 11  3'Ctd. '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 10'Tie/High '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 26'Licncia'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 43'Ctd. '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 50'Tie/High '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 66'Licncia'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 20'MullX'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 60'MullX'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20010307  161902  DAVE3       REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CF07
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'PO136.02'
     A                                  1 12'Recibir RF'
     A                                  2  1'P.O.'
     A            W2PO           9A  O  2  6
     A            W2LINE         5Y 0O  2 16EDTCDE(4)
     A            W2ITEM        65A  O  3  1
     A            W2PCTR         4Y 0O  4  1EDTCDE(4)
     A                                  4  6'de'
     A            W2PAL          4Y 0O  4  9EDTCDE(4)
     A                                  4 14'paletas'
     A                                  5  1'Ctd'
     A            W2QTY          5Y 0O  5  5EDTCDE(4)
     A            W2UM           2   O  5 11
     A            W2TIHI         7   O  5 14
     A                                  6  1'Lic'
416a A*           W2LCNS         7Y 0B  6  5EDTCDE(4)
416a A            W2LCNS        15A  B  6  5
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A N33N90                               DSPATR(HI)
416a A            W2XDK          3   O  6 22
416a A*           W2XDK          3   O  6 14
     A                                  7  1'F7=Antes'
     A                                      COLOR(BLU)
     A*
     A* Command line
     A*
500 AA*----------------------------------------------------------------
500 AA          R CMDRECSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refrescar'
     A                                      COLOR(BLU)
