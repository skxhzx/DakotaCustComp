     A**************************************************************************
     A*
     A*   PO332PR  PO History Receiving Receipt
     A*   29 December, 2010
     A*   Retha Davis
     A*
     A*   Copied from PO330PR
     A*
     A*  NOTE: compile at 82 columns
     A*  Revisions
     A*
640 AA*  12/29/10  RBD  6.40
     A*      - Created.
650a A*  01/29/15  LMC  6.50a
     A*      - Added * to the end of the detail line if
     A*      quantity received is different than quantity ordered.
     A*      This requires the quantity ordered/received fields to
     A*      be moved slightly.
710a A*  09/06/18  RH   7.10a
     A*      - Enh: Add P#MEMO field to print data from POHDRM
     A****************************************************************
CLD AA*  Client Custom Revisions: Cityline
     A*
CLD AA*    12/29/10  RBD  CLD
     A*      - Added format DETAIL3 to print CLAMS harvest data.
     A*      - Compile with COPIES(2).
     A*
     A****************************************************************
PANaAA*  Client Custom Revisions: PANOS
PANa A*    10/03/17  RH   PANa
    AA*      - Added DETAIL5 to print Vendor Item # only.
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    20
     A*
     A                                    72
     A                                      DFT('Page')
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    25
     A                                    72
     A                                      DFT('PO332')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A*
     A                                      SPACEA(002)
     A                                     6
     A                                      DFT('PO:')
     A            PHHPO          9A  O    10
     A                                    24
     A                                      DFT('Status:')
     A            P#DESC        11A  O    32
710aAA            P#MEMO        15A  O    50
     A                                      SPACEA(001)
     A                                     2DFT('Vendor:')
     A            PHHVNA        40   O    10
     A                                      SPACEA(001)
     A            VADR1         70   O    10
     A                                      SPACEA(001)
     A            VADR2         40   O    10
     A*
     A          R RPHEAD
     A*
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                                60DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Seq #')
     A                                      UNDERLINE
     A                                     7
     A                                      DFT('Item Number    ')
     A                                      UNDERLINE
     A                                    23
     A                                      DFT('Description             ')
     A                                      UNDERLINE
     A                                    48
     A                                      DFT('Weight    ')
     A                                      UNDERLINE
650aDA*                                   60
650aMA                                    59
650aDA*                                     DFT('Ordered')
650aMA                                      DFT('Ordered   ')
     A                                      UNDERLINE
650aDA*                                   72
650aMA                                    70
650aDA*                                     DFT('Received')
650aMA                                      DFT('Received  ')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PHDSEQ    R        O     1REFFLD(PHDSEQ POHDTL)
     A                                      EDTCDE(Z)
     A            PHDITM    R        O     7
     A                                      REFFLD(PHDITM POHDTL)
     A            W1DESC        25A  O    23
     A            W1WT           9S 2O    48EDTCDE(3)
650aDA*           W1ORD         10A  O    60
650aMA            W1ORD         10A  O    59
650aDA*           W1RCV         10A  O    72
650aMA            W1RCV         10A  O    70
650aMA  62                                81'*'
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            W1EXCP        24   O    23
     A            W1QEXC        10A  O    48
     A**************************************************************************
CLD AA* Print CLAMS harvest data
CLD AA*
CLD AA          R DETAIL3
CLD AA*
CLD AA                                      SPACEA(001)
CLD AA            CLAMS1        75   O     7
CLD AA                                      SPACEA(001)
CLD AA            CLAMS2        75   O     7
     A*
PANa A          R DETAIL5
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    10'Mfr#:'
     A            ITVIT#        15   O    16
     A            ITWHDP         5   O    35
     A*
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* Print total line
     A*
     A          R TOTAL1
     A*
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                     1DFT('Date:    ')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Receiver:')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Shipper: ')
     A                                    10DFT('___________________')
     A*
     A* Print accumulated totals
     A*
     A          R TOTAL2
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                    40DFT('Totals:')
650aDA*                                   60DFT('__________')
650aMA                                    59DFT('__________')
650aDA*                                   72DFT('__________')
650aMA                                    70DFT('__________')
     A                                      SPACEA(001)
650aDA*           W1OTOT        10A  O    60
650aMA            W1OTOT        10A  O    59
650aDA*           W1RTOT        10A  O    72
650aMA            W1RTOT        10A  O    70
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('PO332: End of Report')
     A*
