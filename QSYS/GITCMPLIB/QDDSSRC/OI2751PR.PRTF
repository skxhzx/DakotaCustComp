     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OI2571PR    Import FTP orders w/pick slot - Exceptions
     A*  07 September 2005
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    09/07/05  MLB  5.00
     F*      - Created.
     A*
500aAA*    04/26/06  DAS  5.00a
     F*      - Added "Items with no quantity" count.
     A*
520 AA*    01/26/09  CWM  5.20
     A*      - CHANGED STOP TO HOST STOP #
     A*
530 AA*    11/20/09  MLB  5.30
     A*      - Enh: Added format DETAIL 22 to print item pack/size
     A*        on report.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD11
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HDR1        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'OI275'
     A*
     A          R HEAD21
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    79
     A                                      'Stop'
     A                                    85
     A                                      'Truck'
     A*
     A          R HEAD31
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHREC/OIHORD IMPFORDH3)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHREC/OIHTYP IMPFORDH3)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHREC/OIHCUS IMPFORDH3)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHREC/OIHCNM IMPFORDH3)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHREC/OIHRTE IMPFORDH3)
520 DA*           OIHSTP    R        O    +3
520 DA*                                     REFFLD(OIHREC/OIHSTP IMPFORDH3)
520 AA            DSPHSTP        3  0     +2
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHREC/OIHTRK IMPFORDH3)
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*
     A          R HEAD41
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Pick    '
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Slot'
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDREC/OIDITM IMPFORDD3)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDREC/OIDQ1  IMPFORDD3)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ2  IMPFORDD3)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ3  IMPFORDD3)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDREC/OIDWH2 IMPFORDD3)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDREC/OIDOR2 IMPFORDD3)
     A                                      EDTCDE(4)
520 DA*           OIDSLT    R        O    +2
520 DA*                                     REFFLD(OIDREC/OIDSLT IMPFORDD3)
520 AA            DSPDSTP        3  0     +2
     A            DETMSG        20A  O   106
530 AA*
530 AA          R DETAIL22
530 AA                                      SPACEB(001)
530 AA            ITPDSC    R        O    23
530 AA                                      REFFLD(ITPDSC *LIBL/PIRITEM)
     A*
     A          R SUBTTL1
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ11         7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ21         7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ31         7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A          R GRNDTTL1
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer order Exceptions:'
     A                                      SPACEB(001)
     A            GRORD1         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail line Exceptions   :'
     A                                      SPACEB(001)
     A            GRLIN1         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
500aAA                                     4
500aAA                                      DFT('Items with zero quantity      -
500aAA                                         :')
500aAA                                      SPACEB(001)
500aAA            ZROQTY         7S 0O    39
500aAA                                      EDTCDE(J)
