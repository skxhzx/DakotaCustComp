     A********************************************************************
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved    *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RC180FM   RF Receive Item without a PO
     A*  14 May 2014
     A*  Retha Davis
     A*
     A********************************************************************
     A*
650 AA*  05/14/14  RBD  6.50
     A*    - Created for City Line, Quote 1207.
720aAA*  12/13/18  LMC  7.20a
     A*    - Added ability to scan UCI/UPC.
     A*
     A********************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(7)
     A                                      OVERLAY
     A                                  1  2'RC180.01'
     A                                  1 12'Recv Item'
     A                                  3  1'Item'
     A            W1ITEM        15A  B  3  6
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
720aAA                                  4  1'UCI '
720aAA            W1UCI         50A  B  4  6
720aAA  28                                  DSPATR(RI)
720aAA  08                                  DSPATR(PC)
720aAA N28N90                               DSPATR(UL)
720aAA  90                                  DSPATR(PR)
     A          R SCREEN2
     A                                      CF10
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC180.02'
     A                                  1 12'Recv Item'
     A            WOITEM        65A  O  3  1
     A            WOUM1          5A  O  4  2
     A            W2QT01         5Y 0B  4 10EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A            WOUM2          5   O  5  2
     A  95                                  DSPATR(ND)
     A            W2QT02         5Y 0B  5 10EDTCDE(4)
     A  25                                  DSPATR(RI)
     A  95                                  DSPATR(ND)
     A N25N95                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  95                                  DSPATR(PR)
     A                                  6  2'Pallets'
     A  96                                  DSPATR(ND)
     A            W2PLT          3Y 0B  6 10EDTCDE(4)
     A N26N96                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N26N96                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  96                                  DSPATR(PR)
     A                                  7  1'F10=WOvr F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A                                      CF10
     A                                      CA12
     A                                      CF11
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC180.03'
     A                                  1 12'Recv Item'
     A            WOITEM        65A  O  3  1
     A            WODTE          8   O  5  1
     A  94                                  DSPATR(ND)
     A            W3DTE          6Y 0B  5 10
     A  26                                  DSPATR(RI)
     A N26N94                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A N26N94                               DSPATR(HI)
     A                                  5 17'(MDY)'
     A  94                                  DSPATR(ND)
     A                                  6  1'Wt'
     A  93                                  DSPATR(ND)
     A            W3DWGT         7Y 2B  6  4
     A  29                                  DSPATR(RI)
     A N29N93                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  82
     AO 93
     AO 90                                  DSPATR(PR)
     A  82
     AO 93                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A N29N93                               DSPATR(HI)
     A            WOCRCV         3   O  6 16
     A  82
     AO 93                                  DSPATR(ND)
     A            W3AVG          1A  B  6 20
     A N32N93                               DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N93                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  81
     AO 82
     AO 93
     AO 90                                  DSPATR(PR)
     A  81
     AO 82
     AO 93                                  DSPATR(ND)
     A                                  7  1'F12=Cancel F10=WOvr'
     A                                      COLOR(BLU)
     A                                  7 21'F11=Mfrcode'
     A  66                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
