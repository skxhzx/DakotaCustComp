     *----------------------------------------------------------------
     *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     *   BFC Software, Inc.
     *   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     *   West Chicago, IL  60185
     *   (630) 562-0375
     *----------------------------------------------------------------
     A****************************************************************
     A*
     A* SD301PR    Print Check Digit Exceptions Report
     A* November 22, 2006
     A* Randy Haselhorst
     A*
     A****************************************************************
     A*
     A*  Revisions
     A*
510aAA*    12/06/06  JCJ  5.10a
     A*      - Enh: Added date range heading
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    33
     A                                      DFT('Check Digit Exception --
                                            Report')
     A                                   118DFT('Page')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
510aAA            DTERNG        22A  O   +25
     A                                   123DFT('UI311')
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A*
     A                                     8
     A                                      DFT('Report Criteria:')
     A                                    27
     A                                      DFT('Warehouse:')
     A            $WHSEA         3A  O    39
     A                                    45DFT('Department:')
     A            $DEPT          5A  O    58
     A                                    66DFT('Employee:')
     A            $EMPA          5A  O    77
     A                                      SPACEA(002)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     1DFT('Dept   ')
     A                                   + 2DFT('Emp    ')
     A                                   + 2DFT('Label  ')
     A                                   + 2DFT('Slot#       ')
     A                                   + 2DFT('Item#          ')
     A                                   + 2DFT('Description                  ')
     A                                   + 3DFT('Chk Dgt')
     A                                   + 2DFT('Excp Date')
     A                                      SPACEA(001)
      *
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            SDELBL    R             19REFFLD(SDELBL SLTDGTEX)
     A                                      EDTCDE(Z)
     A            SDEDSP    R            + 2REFFLD(SDEDSP SLTDGTEX)
     A            LBITEM    R            + 2REFFLD(LBITEM LABEL)
     A            ITDESC    R            + 2REFFLD(ITDESC PIRITEM)
     A            SDEDGT    R            + 3REFFLD(SDEDGT SLTDGTEX)
     A                                      EDTCDE(Z)
     A            SDEATS    R            + 6REFFLD(SDEATS SLTDGTEX)
     A*
     A*----------------------------------------------------------------
     A* Department break
     A*
     A          R WHDP
     A                                      SPACEB(001)
     A            SDEWHD    R              1REFFLD(SDEWHD SLTDGTEX)
     A            WDDESC    R            + 2REFFLD(WDDESC WHDEPT)
     A*----------------------------------------------------------------
     A* Employee break
     A*
     A          R EMP
     A                                      SPACEB(001)
     A            SDEEMP    R             10REFFLD(SDEEMP SLTDGTEX)
     A            $EMPNAM   R            + 2REFFLD(WDDESC WHDEPT)
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R TOTALS
     A                                      SPACEB(002)
     A                                     5DFT('Total Exceptions Printed:')
     A            COUNT          4S 0O    32
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                     5DFT('SD302: End of Report')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R NOTOTAL
     A                                      SPACEB(002)
     A                                     5DFT('No exceptions to report')
     A                                      SPACEA(002)
     A                                     5DFT('SD302: End of Report')
