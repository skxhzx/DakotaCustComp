     A*%%TS  SD  20010719  125908  ROCKY       REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  SL383FM   Slots with negative quantity - View
     A*  16 July, 2001
     A*  Roxanne Harrison
     A*
     A*  Revisions:
640a A* 09/27/11  RH   6.40a
     A*  - ENH: Add license, replace transfer and receive qty.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA10
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'Slots with Negative Quantity'
     A                                      DSPATR(HI)
     A                                  1  2'SL383'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A            WDCODE         5A  O  2  2
     A                                  2  8'-'
     A            WDDESC        30A  O  2 10
     A                                  2 41'Change to department  .'
     A            PTINP2         5A  B  2 66
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  3 41'Position to item  . . .'
     A            PTINP5        15A  B  3 66
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20010719  125908  ROCKY       REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            W1DISP        12A  O  9  2
     A            W1STYP         5A  O  9 17
     A            W1ITEM        15A  O  9 23
     A            W1DESC        15A  O  9 40
     A            W1QTY         10A  O  9 57
     A            W1STK         10A  O  9 69
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20010719  125908  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  8  2'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 17'Sect '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 23'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 40'Description    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 57'Avl Qty   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 69'Stock Qty '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19960703  093827  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            W1DISP        12A  O  9  3
     A**          W1ITEM        15A  O  9 17
     A**          W1QTY         10A  O  9 17
     A            W1STK         10A  O  9 17
     A            W1ALC         10A  O  9 29
     A            W1PCK         10A  O  9 41
640aMA            W1LCNS        15A  O  9 53
640aDA*           W1TFR         10A  O  9 53
640aDA*           W1RCV         10A  O  9 65
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19960703  093827  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                  8  3'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 17'Stock Qty '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 29'Allocated'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 41'Pick      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA*                                 8 53'Transfer  '
640aDA                                  8 53'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aDA*                                 8 65'Receive   '
640aDA*                                     DSPATR(HI)
640aDA*                                     DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A**                                    SLNO(23)
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
