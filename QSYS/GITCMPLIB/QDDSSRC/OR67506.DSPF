     A*%%TS  SD  20180705  142043  BFCBBI      REL-V7R3M0  5770-WDS
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OR675FM   Reprint closed route
     A*  15 August 2000
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
414 AA*    08/15/00  DAS  4.14
     A*      - Created.
     A*
415 AA*    12/14/00  DAS  4.15
     A*      - Added option to reprint labels.
     A*        This should only be used for testing purposes since
     A*        there should be no reason to reprint labels to be used
     A*        for picking after a route has been closed.
415bAA*    07/05/01  RH   4.15b
     A*      - Added option to reprint Route stop detail or both.
     A*
510 AA*    03/31/07  DAS  5.10
     A*      - Enh: Added new option to call cw summary
     A*
520 AA*    05/11/09  CWM  5.20
     A*      - Enh: Added new functionality for report distribution.
     A*
530 AA*    02/20/10  MLB  5.30  Note: was 5.20 on MJ Kellner.
     A*      - Enh: Added new option E for exceptions to
     A*             Route Stop Detail / Summary print options.
     A*
640aAA*    03/16/11  JCJ  6.40a
     A*      - Enh: Renamed from OR67501 to OR67502 per GUI standards.
     A*             Added reprint by Transaction and Label Numbers.
     A*
640bAA*    06/29/11  JCJ  6.40b
     A*      - Enh: Renamed from OR67502 to OR67503 per GUI standards.
     A*             Added label range for reprinting by Label Number.
     A*
700aAA*    10/16/15  JCJ  7.00a
     A*      - Enh: Renamed from OR67503 to OR67504 per GUI standards.
     A*             Added pallet reprint option.
     A*
700bAA*    09/06/16  JCJ  7.00B
     A*      - Enh: Renamed from OR67504 to OR67505 per GUI standards.
     A*             Added Manager Ship List reprint option.
710aAA*    07/05/18  JCJ  7.10a
     A*      - Enh: Renamed from OR67505 to OR67506 per GUI standards.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20160909  151024  BFCTEST1    REL-V7R1M0  5770-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OR675.01'
     A                                  1 31'Reprint Closed Route'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'Warehouse  . . . . . . . . . . .'
     A            W1WHSE         3Y 0O  4 38EDTCDE(Z)
     A                                  4 42'-'
     A            WHDESC        30A  O  4 44
     A                                  5  4'Route  . . . . . . . . . . . . .'
     A            W1RTE          5A  O  5 38
     A                                  5 48'Print'
     A                                      DSPATR(HI)
     A                                  6 38'Reprint'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
640aDA***                               6 48'Print Override'
     A                                  6 48'Override'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  7  4'Labels . . . . . . . . . . . . .'
     A            W1LBLS         1A  B  7 38
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***                               7 64'Y=Yes, N=No'
     A                                  7 58'Y=Yes, N=No'
640aDA***                               8  6'Zone . . . . . . . . . . . . .'
     A                                  8  6'Reprint Type . . . . . . . . .'
640aDA***         W1ZONE         6A  B  8 38
     A            W1TYPE         1A  B  8 38
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Z=Zone, T=Tran, L=Lbl#'
     A                                  9  6'Reprint Value. . . . . . . . .'
     A            W1ZONE         7A  B  9 38
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ZON2         7A  B  9 48
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640bDA***                               9 58'*ALL, Code, Trn#, LBL#'
     A                                  9 58'*ALL, Zone, Trn#, Lbl#'
     A                                 10 59'range. F4=Prompt'
     A                                 11  4'Pallet Label Reprint Type. . . .'
     A            W1PLT          1A  B 11 38
     A N35N90                               DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 58'Z=Zone, T=Tran, P=Plt#'
     A                                 12  6'Reprint Value. . . . . . . . .'
     A            W1PTYP         7A  B 12 38
     A N36N90                               DSPATR(HI)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 58'*ALL, F4=Prompt'
640aDA***                              10  4'Route stop . . . . . . . . . . .'
     A                                 14  4'Route stop . . . . . . . . . . .'
640aDA***         W1RTES         1A  B 10 38
     A            W1RTES         1A  B 14 38
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W1RSDT         1   B 10 48COLOR(WHT)
     A            W1RSDT         1A  B 14 48COLOR(WHT)
     A  23                                  DSPATR(RI)
     A  93                                  DSPATR(ND)
     A  23                                  DSPATR(PC)
     A  93                                  DSPATR(PR)
640aDA***                              10 64'Y=Yes, N=No'
     A                                 14 58'Y=Yes, N=No'
640aDA***                              11  6'Detail or Summary or Both  . .'
     A                                 15  6'Detail or Summary or Both  . .'
640aDA***         W1RTDS         1A  B 11 38
     A            W1RTDS         1A  B 15 38
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640ADA***                              11 64'D=Det S=Smry'
710aDA***                              15 58'D=Det S=Smry'
710aMA                                 15 58'D=Dt S=Sum B=Bth E=Ex'
640aDA***                              12 64'B=Both, E=Excp'
710aDA***                              16 58'B=Both, E=Excp'
640aDA***                              12 64'B=Both'
640aDA***                              13  4'Truck summary  . . . . . . . . .'
     A                                 17  4'Truck Map. . . . . . . . . . . .'
640aDA***         W1TRK          1A  B 13 38
     A            W1TRK          1A  B 17 38
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W1POTS         1   B 13 48COLOR(WHT)
     A            W1POTS         1A  B 17 48COLOR(WHT)
     A  24                                  DSPATR(RI)
     A  94                                  DSPATR(ND)
     A  24                                  DSPATR(PC)
     A  94                                  DSPATR(PR)
640aDA***                              13 64'Y=Yes, N=No'
     A                                 17 58'Y=Yes, N=No'
     A                                 18  4'Manager Ship List. . . . . . . .'
     A            W1MGR          1A  B 18 38
     A N34N90                               DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 58'Y=Yes, N=No'
640aDA***                              15  4'Truck pallets  . . . . . . . . .'
     A                                 19  4'Truck pallets  . . . . . . . . .'
640aDA***         W1TRPL         1A  B 15 38
     A            W1TRPL         1A  B 19 38
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W1POTP         1   B 15 48COLOR(WHT)
     A            W1POTP         1A  B 19 48COLOR(WHT)
     A  26                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A  26                                  DSPATR(PC)
     A  96                                  DSPATR(PR)
640ADA***                              15 64'Y=Yes, N=No'
     A                                 19 58'Y=Yes, N=No'
640aDA***                              16  6'Detail or Summary  . . . . . .'
     A                                 20  6'Detail or Summary  . . . . . .'
640aDA***         W1DS           1A  B 16 38
     A            W1DS           1A  B 20 38
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***                              16 64'D=Det S=Smry'
     A                                 20 58'D=Det S=Smry'
640aDA***                              18  4'Catchweight summary  . . . . . .'
     A                                 22  4'Catchweight summary  . . . . . .'
640aDA***         W1CSUM         1A  B 18 38
     A            W1CSUM         1A  B 22 38
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aDA***         W1POCW         1   B 18 48COLOR(WHT)
     A            W1POCW         1A  B 22 48COLOR(WHT)
     A  27                                  DSPATR(RI)
     A  95                                  DSPATR(ND)
     A  27                                  DSPATR(PC)
     A  95                                  DSPATR(PR)
640aDA***                              18 64'Y=Yes, N=No'
     A                                 22 58'Y=Yes, N=No'
     A            FLD001         1A  B 22 72
     A                                 23  2'F3=Exit  F4=Select  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
