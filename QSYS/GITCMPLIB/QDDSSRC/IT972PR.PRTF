     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  IT972PR    Item Quantity Compare Report - By Whse/Dept/Item
     A*  14 March 2013
     A*  Rod Roller
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
650a A*    03/14/13  RTR  6.50a
     A*      - Created from IT971.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W1HEAD        60A  O    30
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
      *
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A            WHHEAD        40A  O    40
     A                                   121
     A                                      'IT972'
     A                                      SPACEA(002)
     A                                     2
     A                                      'Dept:'
     A            H1DEPT    R        O     8
     A                                      REFFLD(ITWHDP PIRITEM)
     A                                    53
     A                                      'Q u a n t i t y  1'
     A                                    77
     A                                      'Q u a n t i t y  2'
     A                                   101
     A                                      'Q u a n t i t y  3'
     A                                      SPACEA(001)
     A                                     2
     A                                      'Item'
     A                                    18
     A                                      'Description'
     A                                    52
     A                                      'Host'
     A                                    58
     A                                      'Dakota'
     A                                    68
     A                                      'Diff'
     A                                    76
     A                                      'Host'
     A                                    82
     A                                      'Dakota'
     A                                    90
     A                                      'Diff'
     A                                    98
     A                                      'Host'
     A                                   104
     A                                      'Dakota'
     A                                   113
     A                                      'Diff'
     A                                   120
     A                                      'Pick Slot'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            IQITEM    R        O     2
     A                                      REFFLD(ITITEM PIRITEM)
     A            ITDESC    R        O    18
     A                                      REFFLD(ITDESC PIRITEM)
     A            ICTQH1    R        O    49
     A                                      REFFLD(ICTQH1 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK1    R        O    57
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF1     R        O    65
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            ICTQH2    R        O    73
     A                                      REFFLD(ICTQH2 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK2    R        O    81
     A                                      REFFLD(IQSTK2 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF2     R        O    87
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            ICTQH3    R        O    95
     A                                      REFFLD(ICTQH3 IMPFCQTY)
     A                                      EDTCDE(M)
     A            IQSTK3    R        O   103
     A                                      REFFLD(IQSTK3 ITEMQTY)
     A                                      EDTCDE(M)
     A            DIFF3     R        O   109
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(M)
     A            SLDISP    R        O   119
     A                                      REFFLD(SLDISP SLOT)
     A*
     A          R HEAD2
     A                                      SKIPB(002)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W2HEAD        60A  O    30
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A            WHHEAD        40A  O    40
     A                                   121
     A                                      'IT972'
     A                                      SPACEA(002)
     A                                    59
     A                                      'Q u a n t i t y  1'
     A                                    83
     A                                      'Q u a n t i t y  2'
     A                                   107
     A                                      'Q u a n t i t y  3'
     A                                      SPACEA(001)
     A                                     8
     A                                      'Item'
     A                                    24
     A                                      'Description'
     A                                    58
     A                                      'Host'
     A                                    64
     A                                      'Dakota'
     A                                    74
     A                                      'Diff'
     A                                    82
     A                                      'Host'
     A                                    88
     A                                      'Dakota'
     A                                    98
     A                                      'Diff'
     A                                   106
     A                                      'Host'
     A                                   112
     A                                      'Dakota'
     A                                   122
     A                                      'Diff'
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            ICITEM    R        O     8
     A                                      REFFLD(ICITEM IMPFCQTY)
     A            ICTQH1    R        O    55
     A                                      REFFLD(ICTQH1 IMPFCQTY)
     A                                      EDTCDE(L)
     A            ICTQH2    R        O    79
     A                                      REFFLD(ICTQH2 IMPFCQTY)
     A                                      EDTCDE(L)
     A            ICTQH3    R        O   103
     A                                      REFFLD(ICTQH3 IMPFCQTY)
     A                                      EDTCDE(L)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT972: End of Report')
     A*
