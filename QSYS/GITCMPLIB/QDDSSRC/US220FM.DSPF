     A*%%TS  SD  20140922  094526  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  US220FM    Program Security - Select User
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/PGMUSRID)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140918  220105  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  7  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PGUSER    R        O  7  6
     A            S1DESC        50A  O  7 17
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140918  220105  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A                                  1  2'US220.01'
     A                                  1 31'BFC Program Security'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Select User ID'
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A                                  6  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  6' User ID  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 17' Description                      -
     A                                                      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A          R SFL2                      SFL
     A*%%TS  SD  20140911  215053  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            PGPGM     R        O  7  3REFFLD(PGREC/PGPGM *LIBL/PGMUSRID)
     A            PGOPT     R        O  7 14REFFLD(PGREC/PGOPT *LIBL/PGMUSRID)
     A            PSCDESC   R        O  7 35REFFLD(PSCREC/PSCDESC *LIBL/PGMSECC-
     A                                      FG)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20140911  215053  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A                                  1  2'US220.02'
     A                                  1 31'BFC Program Security'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 29'User Authorized Programs'
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A                                  6  3' Program  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 14' Options            '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 35' Description                      -
     A                                            '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY2
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20140911  215053  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'US220.03'
     A                                  1 31'BFC Program Security'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Copy User ID'
     A                                      DSPATR(HI)
     A                                  8 30'Enter user to copy to:'
     A                                 10 28'From User . . .'
     A            S3FUSER       10A  O 10 44DSPATR(HI)
     A                                 12 28'To User . . . .'
     A            S3TUSER       10A  B 12 44DSPATR(HI)
     A  81                                  ERRMSG('User does not exist' 81)
     A  82                                  ERRMSG('Error copying user' 82)
     A                                 23  2'F3=Exit   Enter=Copy'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  20140922  094526  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'US220.04'
     A                                  1 31'BFC Program Security'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Remove User ID'
     A                                      DSPATR(HI)
     A                                  8 29'Are you sure you want to'
     A                                      COLOR(RED)
     A                                 10 30'delete user'
     A                                      COLOR(RED)
     A            S4FUSER       11A  O 10 42COLOR(RED)
     A                                 12 33'If so, press F5'
     A                                      COLOR(RED)
     A                                 23  2'F3=Exit   F5=Delete   F12=Cancel'
     A                                      COLOR(BLU)
