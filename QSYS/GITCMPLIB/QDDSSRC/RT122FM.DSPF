     A*%%TS  SD  19960612  135322  DAVE3       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RT122FM   Route options maintenance - Change screen
     A*  23 August 1994
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960612  135322  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'RT122.01'
     A                                  1 21'Route Override Options - Picking L-
     A                                      abels'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 10  3'Qty level for combined labels . .'
     A            W1CLVL         3Y 0B 10 38
     A  22                                  DSPATR(RI)
     A                                      EDTCDE(3)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11  3'Print combined label warning  . .'
     A            W1CWRB         1A  B 11 46
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  3'Text for last line of label . . .'
     A            W1TEXT        43A  B 18 38CHECK(LC)
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11 38'Before:'
     A                                 11 50'After:'
     A            W1CWRA         1A  B 11 57
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  3'Print unit of measure warning . .'
     A                                 13 38'Before:'
     A                                 13 50'After:'
     A            W1UWRB         1A  B 13 46
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UWRA         1A  B 13 57
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  3'Discrepancy label ratio . . . . .'
     A                                 16  3'Catch weight label ratio  . . . .'
     A                                 15 38'One label for every'
     A                                 16 38'One label for every'
     A            W1ERR          2Y 0B 15 58EDTCDE(3)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1CWGT         2Y 0B 16 58EDTCDE(3)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 61'pick labels'
     A                                 16 61'catch weight items'
     A                                  8  3'Route . . . . . . . . . . . . . .'
     A            W1RTE          5   B  8 38
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
