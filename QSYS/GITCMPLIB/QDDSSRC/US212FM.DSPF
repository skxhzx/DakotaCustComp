     A*%%TS  SD  20071212  112400  CHUCKM      REL-V5R4M0  5722-WDS
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20071212  112400  CHUCKM      REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  2 33'Detail Entry'
     A                                      DSPATR(HI)
     A                                  1 22'User ID Option Access Maintenance'
     A                                      DSPATR(HI)
     A                                  1  3'US212   '
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  3'Type Changes, Press Enter'
     A                                      COLOR(BLU)
     A                                  8  6'Program.............:'
     A                                  9  6'Option..............:'
     A                                 10  6'User................:'
     A            W2PGUSR       10A  B 10 29
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            W2PGOPT       20A  B  9 29
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A                                      CHECK(RB)
     A            W2PGPGM       10A  B  8 29
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            MSGAR1        78A  O 21  3
     A N40                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                 23  3'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 16'F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
