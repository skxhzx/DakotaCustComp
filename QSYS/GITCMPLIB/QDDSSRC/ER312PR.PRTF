     A****************************************************************
     A*
     A* ER312PR   Employee management report for a crew (Recv) (Summary)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A*      Compile 165 cpi
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   142
     A                                      'Page'
     A            PAGE           4S 0O   146
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   145
     A                                      'ER312'
     A          R HEAD2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                    20' Cases  '
     A                                   101'Purchase Order'
     A                                      SPACEA(001)
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    10'  PO  '
     A                                      UNDERLINE
     A                                    21'Received'
     A                                      UNDERLINE
     A                                    30'Trans'
     A                                      UNDERLINE
     A                                    39'Weight'
     A                                      UNDERLINE
     A                                    51'Cube'
     A                                      UNDERLINE
     A                                    56'Vendor'
     A                                      UNDERLINE
     A                                    97'Ordered'
     A                                      UNDERLINE
     A                                   105'Received'
     A                                      UNDERLINE
     A                                   115'Differ'
     A                                      UNDERLINE
     A                                   125'Weight'
     A                                      UNDERLINE
     A                                   137'Cube'
     A                                      UNDERLINE
     A                                   145'Start'
     A                                      UNDERLINE
     A                                   155'End'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            WKPO      R             10REFFLD(WKPO ER311)
     A            WKPTOT         7  0     20EDTCDE(1)
     A            WKTTOT         4  0     30EDTCDE(1)
     A            POWGTR         7S 0     36EDTCDE(1)
     A            POCBER         7S 0     46EDTCDE(1)
     A            PHHVNA    R             56REFFLD(PHHVNA POHHDR)
     A            ORDPCS         6S 0     97EDTCDE(1)
     A            RCVPCS         6S 0    105EDTCDE(1)
     A            DIFPCS         6S 0    114EDTCDE(1)
     A            POWGT          7S 0    122EDTCDE(1)
     A            POCBE          7S 0    132EDTCDE(1)
     A            STIME          6S 0    144EDTWRD('  :  :  ')
     A            ETIME          6S 0    154EDTWRD('  :  :  ')
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD           8A        1
     A            DHRS           7S 2     10EDTCDE(1)
     A            T#PTOT         7S 0     20EDTCDE(1)
     A            T#TTOT         4S 0     30EDTCDE(1)
     A            T#WTOTR        7S 0     36EDTCDE(1)
     A            T#CTOTR        7S 0     46EDTCDE(1)
     A            T#WTOT         7S 0    122EDTCDE(1)
     A            T#CTOT         7S 0    132EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER312: End of Report '
     A*
