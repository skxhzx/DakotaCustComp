     A*%%TS  SD  20071008  154428  LYNNM       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  MF125FM    Manufacturer code decipher maintenance - test
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*    09/25/07  DAS  5.10
     A*      - Created
510aAA*    10/02/07  LMC  5.10a
     A*      - Fix: Changed mfr# field size to agree with vendor size
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  20071008  154428  LYNNM       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'MF125.01'
     A                                  1 25'Manufacturer Code Decipher Test'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6 30'123456789012345'
     A                                  7  4'Code to decipher . . . .'
     A            W1CODE        15A  B  7 30
     A                                  9  4'Extracted date . . . . .'
     A            W1DATECMD      8Y 0O  9 30EDTWRD('    -  -  ')
     A            W1MSG         60A  B 12 11
     A                                 15  4'Mfr number . . . . . . .'
     A            W1MFR#        11Y 0B 15 30EDTCDE(3)
     A                                 16  4'Mfr def# . . . . . . . .'
     A            W1MFRDEF#      2Y 0B 16 30EDTCDE(3)
     A                                 17  4'Mfr item number  . . . .'
     A            W1MFRITEM     15A  B 17 30
     A                                 18  4'Dakota item number . . .'
     A            W1DKTITEM     15A  B 18 30
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
