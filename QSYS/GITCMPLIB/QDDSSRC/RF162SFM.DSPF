     A*%%TS  SD  19970213  062130  DAVE4       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*       Change are marked with MSG.
     A*
     A*----------------------------------------------------------------
     A*
     A*  RF162SFM  RF 16 X 20 Menu    Stand Alone
     A*  11 June 2001
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    11/10/06  RH   5.10
     A*      - Created for stand alone
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970213  062130  DAVE4       REL-V3R1M0  5763-PW1
     A                                      ROLLUP(81)
     A                                      ROLLDOWN(82)
     A  98                                  ALARM
     A                                      OVERLAY
     A            WOLIN1        20A  O  1  2
     A            WOLIN2        20A  O  2  1
     A            WOLIN3        20A  O  3  1
     A            WOLIN4        20A  O  4  1
     A            WOLIN5        20A  O  5  1
     A            WOLIN6        20A  O  6  1
     A            WOLIN7        20A  O  7  1
     A            WOLIN8        20A  O  8  1
     A            WOLIN9        20A  O  9  1
     A            WOLI10        20A  O 10  1
     A            WOLI11        20A  O 11  1
     A            WOLI12        20A  O 12  1
     A            WOLI13        20A  O 13  1
     A                                 14  1'Selection:'
     A            W1OPT          2Y 0B 14 12
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                      CHECK(ER)
     A                                      EDTCDE(4)
     A                                 15  1'F3/F12=Exit'
     A                                      COLOR(BLU)
     A                                 15 14'PgUp/Dn'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
MSG MA            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
MSG MA                                      SFLSIZ(10) SFLPAG(1)
MSG DA*N97                                  SFLCLR
MSG MA                                      SFLDSP
MSG AA                                      SFLDSPCTL
MSG AA                                      SFLINZ
     A  97                                  SFLEND
MSG AA            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
