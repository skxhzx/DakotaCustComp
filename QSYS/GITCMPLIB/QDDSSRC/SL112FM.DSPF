     A*%%TS  SD  20000131  054518  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL112FM   Slot Maintenance - Change screen
     A*  12 July,1994
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*  07/08/94 HNK
     A*  - Display attributes added for all the fields.
     A*
413  A*  01/31/00 DAS 4.13
     A*    - Added F7=Change item.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000131  054518  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A                                      CF21
     A                                      CF07
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL112.01'
     A                                  1 33'Slot Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A            WOWHSE         3A  O  4  2
     A                                  4  8'-'
     A            WHDESC        30A  O  4 10
     A            WDCODE         5A  O  5  2
     A                                  5  8'-'
     A            WDDESC        30A  O  5 10
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Slot . . . . . . . . .'
     A            W1AISL         3A  B  9 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9 32'-'
     A N90N91                               DSPATR(HI)
     A            W1LOC          3Y 0B  9 34EDTCDE(4)
     A N22N90N91                            DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90N91                            DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                  9 38'-'
     A N90N91                               DSPATR(HI)
     A            W1RLVL         2Y 0B  9 40EDTCDE(4)
     A N23N90N91                            DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90N91                            DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A            W1HAND         2A  B  9 44
     A N24N90N91                            DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90N91                            DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 10  4'Section  . . . . . . .'
     A            W1STYP         1A  B 10 28
     A  24                                  DSPATR(RI)
     A N24N90N91                            DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N24N90N91                            DSPATR(HI)
     A                                 10 59'F=Fixed, P=PIR'
     A                                 13  4'Display  . . . . . . .'
     A            W1DISP        12A  B 13 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N26N90                               DSPATR(HI)
     A                                 12  4'Picking sequence . . .'
     A            W1PSEQ         5Y 0B 12 28EDTCDE(4)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N27N90                               DSPATR(HI)
     A                                 14  4'Slot type  . . . . . .'
     A            W1SDEF         2A  B 14 28
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N28N90                               DSPATR(HI)
     A                                 15  4'Status . . . . . . . .'
     A            W1STAT         2A  B 15 28
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N29N90                               DSPATR(HI)
     A                                 16  4'Designation area . . .'
     A            W1DESG         5A  B 16 28
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  4'Activity zone  . . . .'
     A            W1ACTV         2A  B 17 28
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  4'Build code . . . . . .'
     A            W1BLD          2A  B 18 28
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Picking slot . . . . .'
     A            W1PICK         1A  B 19 28
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 59'Y=Yes, N=No'
     A                                 20  4'Reserved . . . . . . .'
     A            W1RSRV         1A  B 20 28
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90
     AO 36                                  DSPATR(PR)
     A                                 20 59'Y=Yes, N=No'
     A                                 21  4'Item . . . . . . . . .'
     A            W1ITEM        15A  B 21 28
     A  35                                  DSPATR(RI)
     A N35N90N92                            DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90
     AO 92                                  DSPATR(PR)
     A                                 22  2'F3=Exit  F4=Prompt  F5=Refresh  F7-
     A                                      =Change item  F9=Auto create  F12=C-
     A                                      ancel'
     A                                      COLOR(BLU)
     A                                 23  2'F20=Accept warning  F21=Tie/High m-
     A                                      aintenance'
     A                                      COLOR(BLU)
     A                                 13 59'F9 to auto format'
     A                                 12 59'F9 to auto calculate'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
