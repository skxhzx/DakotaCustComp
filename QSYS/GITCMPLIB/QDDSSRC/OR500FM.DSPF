     A*%%TS  SD  19960521  101530  DAVE3       REL-V3R1M0  5763-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960521  101530  DAVE3       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'OR500.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      COLOR(BLU)
     A                                  1 31'Pick Customer Orders'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A                                 12  4'Place on job queue ? . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1JOBQ         1A  B 12 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Route  . . . . . . . . . . .'
     A            W1RTE          5   B 10 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 67'Name, *ALL'
     A                                 12 67'Y=Yes, N=No'
     A                                  8 67'1-999'
     A          R SCREEN2
     A*%%TS  SD  19930318  205938  DAVE        REL-V2R1M1  5738-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'OR500.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Pick Customer Orders'
     A                                      DSPATR(HI)
     A                                 15 31'Press Enter to Exit'
     A                                  6 15'This program can not be run at thi-
     A                                      s time for one of'
     A                                  7 15'the following reasons:'
     A                                  9 19'- Someone is already running this -
     A                                      program.'
     A                                 11 19'- Someone is running Receive Custo-
     A                                      mer Orders.'
     A          R SCREEN3
     A*%%TS  SD  19930318  205938  DAVE        REL-V2R1M1  5738-PW1
     A  98                                  ALARM
     A                                      CA10
     A                                      CLRL(23)
     A                                  1  2'OR500.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Pick Customer Orders'
     A                                      DSPATR(HI)
     A                                  5 13'The last time this program was run-
     A                                       it ended abnormally.'
     A                                  8 25'Batch# :'
     A                                  9 25'Date   :'
     A                                 10 25'Time   :'
     A                                 11 25'User   :'
     A                                 12 25'Status :'
     A            W1BAT          7Y 0O  8 34EDTCDE(3)
     A            W1DATE         6Y 0O  9 34EDTCDE(Y)
     A            W1TIME         6Y 0O 10 34EDTWRD('  :  :  ')
     A            W1USER        10   O 11 34
     A            W1STAT        30   O 12 34
     A                                 15 21'Press F10 to pickup where it left -
     A                                      off '
     A                                 16 27'F3 to exit and investigate further-
     A                                      .'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
