     A*%%TS  SD  19990610  153159  DAVE        REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI210FM    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990610  153159  DAVE        REL-V4R2M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'PCK140.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 11  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B 11 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 51'Y=Yes, N=No'
730aAA                                 12  4'Warehouse  . . . . . . . . .'
730aAA            W1WHSE         3Y 0B 12 34EDTCDE(4)
730aAA N25N90                               DSPATR(HI)
730aAA  25                                  DSPATR(RI)
730aAA N25N90                               DSPATR(UL)
730aAA  05                                  DSPATR(PC)
730aAA  90                                  DSPATR(PR)
     A                                  1 19'Initialize Slots for Whse 900 Rece-
     A                                      iving Test'
     A                                      DSPATR(HI)
     A                                 15 14'This program will initialize all t-
     A                                      he slots in whse 900.'
     A                                 16 14'according to the option selected a-
     A                                      bove. The item number'
     A                                 17 14'of filled slots will be the Aisle -
     A                                      + Location. All non-stock'
     A                                 18 14'quantities will be set to zero. If-
     A                                       a slot is filled the'
     A                                  7  4'Quantity option  . . . . . .'
     A            W1OPT          1   B  7 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 51'1 = Init all slots to zero'
     A                                  8 51'2 = Fill all home slots'
     A                                  9 51'3 = Partially fill home slots'
     A                                 19 14'status will be set to ''A'', other-
     A                                      wise it will be set to ''V''.'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
