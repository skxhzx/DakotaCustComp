     A*%%TS  SD  20130208  123455  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR862FM   Trax - Stop Flags - Change screen
     A*  05 Jan, 2011
     A*  Retha Davis
     A*
     A*  Revisions
640aAA*    01/05/11  RBD  6.40
     A*      - Created.
     A*
650aAA*    02/08/13  RBD  6.50a
     A*      - TTSTPFLG file has changed.  Removed type and icon.
     A*        Added order, unique Id, and display time.
     A*
650bAA*    02/19/13  RBD  6.50b
     A*      - Changed display of order to customer order.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20130208  123455  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TR862.01'
     A                                  1 27'Trax Stop Flags Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 13EDTCDE(3)
     A                                  5 17'-'
     A            WHDESC        30A  O  5 19
     A                                  6  2'Route    :'
     A            $PRTE          5A  O  6 13
     A                                  7  2'Route Id :'
     A            $PRTID         5A  O  7 13
     A                                  8  2'Truck    :'
     A            $TRUK         10A  O  8 13
     A                                  9  2'Stop     :'
     A            $PSTOP         2Y 0O  9 13EDTCDE(3)
650bDA**                               10  2'Order    :'
650bMA                                 10  2'Cust Ord :'
650bDA**          $PORD          7Y 0O 10 13EDTCDE(3)
650bMA            $PCORD         7Y 0O 10 13EDTCDE(3)
     A                                 11  2'Customer :'
     A            $CNAM         40A  O 11 13
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 15  4'Stop Flag Character . . .'
     A            W1CHAR         1A  B 15 30
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 16  4'Unique ID . . . . . . . .'
     A            W1UID         12A  B 16 30
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 17  4'Display Time  . . . . . .'
     A            W1DSP          1A  B 17 30
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 17 33'1=After dwnld rte  2=Before scan s-
     A                                      top'
     A                                 18 33'3=After scan stop'
     A                                 19  4'Alert . . . . . . . . . .'
     A            W1ALE1        50A  B 19 30
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            W1ALE2        50A  B 20 30
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 23  2'F5=Refresh  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
