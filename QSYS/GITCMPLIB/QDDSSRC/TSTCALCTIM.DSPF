     A*%%TS  SD  20020226  084421  DAVE3       REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTCALCTIM  Test CALCTIME subprogram.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    02/26/02  DAS  4.16
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020226  084421  DAVE3       REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTCALCTIM.1'
     A                                  1 29'Test CALCTIME Subprogram'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Duration type  . . . .'
     A            W1TYPE         8A  B  7 27
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  7 47'*DAYS, *HOURS, *MINUTES, *SECONDS'
     A                                  9  4'From date/time . . . .'
     A            W1FDTE         8Y 0B  9 27EDTCDE(4)
     A            W1FTIM         6Y 0B  9 37EDTCDE(4)
     A                                  9 47'YYYYMMDD/HHMMSS'
     A                                 11  4'To date/time . . . . .'
     A            W1TDTE         8Y 0B 11 27EDTCDE(4)
     A            W1TTIM         6Y 0B 11 37EDTCDE(4)
     A                                 11 47'YYYYMMDD/HHMMSS'
     A                                 14  4'Duration . . . . . . .'
     A            W1DUR          7Y 2O 14 27EDTCDE(M)
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
