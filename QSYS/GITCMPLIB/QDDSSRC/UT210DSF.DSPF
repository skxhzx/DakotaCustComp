     A*%%TS  SD  19990904  064906  DAVE        REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    05/17/00  RH   4.14a
     A*      - Add Zone field to change for all zones or one.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990904  064906  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'UT210DS.01'
     A                                  1 26'Change Cube for Pick Zone/Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  8 51'1 - 999'
     A                                  9  4'Truck  . . . . . . . . . . .'
     A            W1TRUK        10A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  9 51'*ALL, Code'
414aAA                                 10  4'Zone . . . . . . . . . . . .'
414aAA            W1ZONE         6A  B 10 34
414aAA N25N90                               DSPATR(HI)
414aAA  25                                  DSPATR(RI)
414aAA N25N90                               DSPATR(UL)
414aAA  05                                  DSPATR(PC)
     A                                 10 51'*ALL, Code'
     A                                 12  4'New cube . . . . . . . . . .'
     A            W1CUB          5S 2B 12 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 51'(not tested for validity)'
     A*                                13  4'New printer device . . . . .'
     A*           W1DEV         10A  B 13 34
     A*N24N90                               DSPATR(HI)
     A* 24                                  DSPATR(RI)
     A*N24N90                               DSPATR(UL)
     A* 04
     A* 24                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19990904  064906  DAVE        REL-V4R2M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT210DS.02'
     A                                  1 26'Change Cube for Pick Zone/Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  8 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  9  4'Truck  . . . . . . . . . . .'
     A            W1TRUK    R        O  9 34REFFLD(W1TRUK *SRC)
414aAA                                 10  4'Zone . . . . . . . . . . . .'
414aAA            W1ZONE    R        O 10 34REFFLD(W1ZONE *SRC)
     A                                 12  4'Cube . . . . . . . . . . . .'
     A            W1CUB     R        O 12 34REFFLD(W1CUB *SRC)
     A*                                13  4'Device . . . . . . . . . . .'
     A*           W1DEV     R        O 13 34REFFLD(W1DEV *SRC)
     A                                 17 16'Please make sure you know what you-
     A                                       are doing.'
     A                                 19 31'Press F10 to continue.'
     A                                 23  2'F12=Cancel'
     A                                 14 16'You are about to change the label -
     A                                      program in all of'
     A                                 15 16'zones for all of the trucks selece-
     A                                      td above'
     A          R SCREEN3
     A*%%TS  SD  19990904  064906  DAVE        REL-V4R2M0  5769-PW1
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT210DS.03'
     A                                  1 26'Change Cube for Pick Zone/Truck'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                  6 13'to cancel your request.'
     A                                  9 17'If you are sure, press F22 to chan-
     A                                      ge the trucks.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
    DA*                                     SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
