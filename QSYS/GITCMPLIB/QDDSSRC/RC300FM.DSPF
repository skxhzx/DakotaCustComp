     A*%%TS  SD  20000630  134633  DAVE        REL-V4R4M0  5769-PW1
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  RC300FM      RF Staged Put-Away
     A*  13 September, 1999
     A*  Roxanne Harrison
     A*  Created using RC200
     A*
     A*  Revisions
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Removed tran# from SCREEN1.
     A*      - Redefined usage of SCREEN2. It is now used for entering
     A*        of multiple licence plates.
     A*      - Revised SCREEN3
     A*          - Changed quantity field to output.
     A*          - Added fields to identify the number of lic#'s to
     A*            stage and current one being staged.
     A*          - Rearranged fields on screen.
416 AF*    05/23/02  RRH  4.16
     F*      - Increase W1LCNS, W2LCNS, W2LST#, W3SCAN, and W3LIC#
     F*        to 15 alphanumeric.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20000623  155749  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC300.01S'
     A                                  1 12'Stage Put'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse:'
     A            W1WHSE         3Y 0B  3 13EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'License #:'
416 DA*           W1LCNS         7Y 0B  5 13CHECK(ER)
416 MA            W1LCNS        15A  B  5 13CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
416 DA*                                     EDTCDE(4)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000628  161057  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC300.02S'
     A                                  1 12'Stage Put'
     A                                  3  2'Scanned  :'
     A            W2CNT          2Y 0O  3 13EDTCDE(3)
     A                                  4  2'Last lic#:'
416 DA*           W2LST#         7Y 0O  4 13EDTCDE(3)
416 MA            W2LST#        15A  O  4 13
     A                                  5  2'Next lic#:'
416 DA*           W2LCNS         7Y 0B  5 13CHECK(ER)
416 MA            W2LCNS        15A  B  5 13CHECK(ER)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
416 DA*                                     EDTCDE(3)
     A                                  6  1'(Enter 0 for nomore)'
     A*                                 7  1'F12=Cancel F10=List'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20000628  161057  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC300.03S'
     A                                  1 12'Stage Put'
     A                                  2  1'Staging'
     A            W3CNT          2Y 0O  2  9EDTCDE(3)
     A                                  2 12'of'
     A            W3TTL          2Y 0O  2 15EDTCDE(3)
     A                                  3  1'Zone:'
     A            W3ZONE         6A  O  3  7
     A  22                                  DSPATR(RI)
     A            W3ZDSC        30A  O  3 14
     A  22                                  DSPATR(RI)
     A                                  4  1'L#/Qty'
416 DA*           W3LIC#         7Y 0O  4  8EDTCDE(3)
416 MA            W3LIC#        15A  O  4  8
     A            W3QTY          5Y 0O  4 16EDTCDE(3)
     A            W3ITEM        65A  O  5  1
     A                                  6  1'Scan lic#:'
416 DA*           W3SCAN         7Y 0B  6 12CHECK(ER)
416 MA            W3SCAN        15A  B  6 12CHECK(ER)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
416 DA*                                     EDTCDE(3)
     A*                                 7  1'F12=Cancel F10=List'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  20000630  134633  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RC300.04S'
     A                                  1 12'Stage Put'
     A                                  3  2'Go to zone'
     A            W4ZONE         6   O  3 13
     A                                  5  2'Scan zone:'
     A            W4SCAN         6   B  5 13CHECK(ER)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
