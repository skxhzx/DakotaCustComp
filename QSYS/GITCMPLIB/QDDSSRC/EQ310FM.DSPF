     A*%%TS  SD  20030131  134931  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EQ310FM    Equipment excpetions
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    01/29/03  DAS  4.17
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA12
     A                                      CF09
     A                                      CA21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20020823  162605  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE       75A  O  5  5
     A            SFLHIDE       45   H
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20030130  144849  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A            RRN1           4S 0H      SFLRCDNBR
     A            SCRNAME        8A  O  1  2
     A                                  1 31'Equipment Exceptions'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        50A  O  2 16
     A                                  2 71TIME
     A            SCOPT         75A  O  3  2
     A            SFLHEAD       79A  O  4  1
     A*
     A          R FKEY1
     A*
     A            SCKEY         78A  O 23  2
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20030129  150453  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(6 20 14 40)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Warehouse . .'
     A            W1WHSE         3Y 0B  3 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5  1'From date . .'
     A            W1FROM         6Y 0B  5 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5 22'MMDDYY, 0=First'
     A                                  7  1'To date . . .'
     A            W1TO           6Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  7 22'MMDDYY, 999999=Last'
     A                                  9  1'Equip id  . .'
417bDA*           W1CODE         6A  B  9 15DSPATR(HI)
417b A            W1CODE         8A  B  9 15DSPATR(HI)
     A                                  9 24'Id, *ALL'
     A                                 11  1'Sort by . . .'
     A            W1SORT         1A  B 11 15DSPATR(HI)
     A                                 11 22'1=Date, 2=Id'
     A                                 13  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOWP
     A*%%TS  SD  20030131  134931  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(6 20 8 40)
     A                                  1 12'Printer Information'
     A                                      DSPATR(HI)
     A                                  4  1'Device . . .'
     A            WPDEV         10A  B  4 15DSPATR(HI)
     A                                  4 27'*JOB, name'
     A                                  5  1'Form . . . .'
     A            WPFORM        10A  B  5 15DSPATR(HI)
     A                                  5 27'*STD, name'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
