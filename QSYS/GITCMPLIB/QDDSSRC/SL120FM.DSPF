500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A********************************************************************
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  SL120FM      RF Change Pick Slot
     A*  11 April 2001
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*  Revisions
415 AA*    04/09/01  MLB  4.15
     A*      - Created.
     A*
500 AA*   06/04/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'SL120.01'
     A                                  1 12'Chg Pick'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N21N90                               DSPATR(HI)
     A                                  4  2'Department:'
     A            W1WHDP         5A  B  4 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  5  2'Slot:'
     A            W1DISP        12A  B  5  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A N03
     AO 03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  7 11'F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                      CSRLOC(WOROW      WOCOL)
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SL120.02'
     A                                  1 12'Chg Pick'
     A                                      DSPATR(HI)
     A                                  2  2'W/H :'
     A            W1WHSE         3  0O  2  8
     A                                  3  2'Dept:'
     A            W1WHDP         5   O  3  8
     A                                  4  2'Slot:'
     A            W1DISP        12A  O  4  8
     A            W2ITEM        65A  O  5  1
     A                                  6  2'Pick:'
     A            W2PICK         1A  B  6  8
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 17'Y/N'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A  98                                  ALARM
     A                                      OVERLAY
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'SL120.01'
     A                                  1 12'Cambiar Pick'
     A                                      DSPATR(HI)
     A                                  3  2'Almacen  :'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N21N90                               DSPATR(HI)
     A                                  4  2'Departamnt:'
     A            W1WHDP         5A  B  4 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  5  2'Slot:'
     A            W1DISP        12A  B  5  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A N03
     AO 03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                  7  2'F3=Salir'
     A                                      COLOR(BLU)
     A                                  7 12'F4=Inicitar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                      CSRLOC(WOROW      WOCOL)
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SL120.02'
     A                                  1 12'Cambiar '
     A                                      DSPATR(HI)
     A                                  2  2'Almc:'
     A            W1WHSE         3  0O  2  8
     A                                  3  2'Dept:'
     A            W1WHDP         5   O  3  8
     A                                  4  2'Slot:'
     A            W1DISP        12A  O  4  8
     A            W2ITEM        65A  O  5  1
     A                                  6  2'Pick:'
     A            W2PICK         1A  B  6  8
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 17'Y/N'
     A                                  7  1'F12=Cancelar'
     A                                      COLOR(BLU)
     A*
