     A*%%TS  SD  20111129  092226  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LB360FM   Trailer label - options
     A*  28 December,  2007
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
     A*    12/28/07  JCJ  5.10
     A*      - Date Created.
     A*
640aAA*    11/23/11  JCJ  6.10a
     A*      - Enh: Added the ability to enter a Trailer range.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20111123  120342  JEFF        REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LB360.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
640aMA                                  8  4'Trailer code range .'
640aMA            W1TRAILF       8A  B  8 27
640aMA N21N90                               DSPATR(HI)
640aMA  21                                  DSPATR(RI)
640aMA N21N90                               DSPATR(UL)
640aMA  01                                  DSPATR(PC)
640aMA  90                                  DSPATR(PR)
640aAA            W1TRAILT       8A  B  8 39
640aAA N24N90                               DSPATR(HI)
640aAA  24                                  DSPATR(RI)
640aAA N24N90                               DSPATR(UL)
640aAA  04                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
     A                                  9  4'Trailer size . . . .'
640aAA  51                                  DSPATR(ND)
     A            W1SIZE         3A  B  9 27
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
640aAA  51
640aMAO 90                                  DSPATR(PR)
640aAA  51                                  DSPATR(ND)
     A                                  9 53'Blank for all'
640aAA  51                                  DSPATR(ND)
     A                                 10  4'Quantity . . . . . .'
     A            W1QTY          2Y 0B 10 27
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 53'1 - 99'
     A                                 12  4'Device . . . . . . .'
     A            W1DEV         10A  B 12 27
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Form . . . . . . . .'
     A            W1FORM        10A  B 13 27
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8 36'to'
