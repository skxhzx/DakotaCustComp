     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OI295PR    Import FTP orders w/pick slot - Print
     A*  22 January 2009
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    01/22/09  MLB  5.20
     F*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'OI295'
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    80
     A                                      'Stop'
     A                                    88
     A                                      'Truck'
     A*
     A          R HEAD3
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHREC/OIHORD IMPFORDH5)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHREC/OIHTYP IMPFORDH5)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHREC/OIHCUS IMPFORDH5)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHREC/OIHCNM IMPFORDH5)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHREC/OIHRTE IMPFORDH5)
     A            OIHEST    R        O    +3
     A                                      REFFLD(OIHREC/OIHEST IMPFORDH5)
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHREC/OIHTRK IMPFORDH5)
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*
     A          R HEAD4
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Pick    '
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Slot'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDREC/OIDITM IMPFORDD5)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDREC/OIDQ1  IMPFORDD5)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ2  IMPFORDD5)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ3  IMPFORDD5)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDREC/OIDWH2 IMPFORDD5)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDREC/OIDOR2 IMPFORDD5)
     A                                      EDTCDE(4)
     A            OIDSLT    R        O    +2
     A                                      REFFLD(OIDREC/OIDSLT IMPFORDD5)
     A            DETMSG        20A  O   106
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            STSMSG        33A  O     6
     A                                    +2
     A                                      DFT('Host Route/Order#/Line#:')
     A            OIHRTE    R        O    +1
     A                                      REFFLD(OIHREC/OIHRTE IMPFORDH5)
     A                                    +1
     A                                      DFT('/')
     A            OIHORD    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIHREC/OIHORD IMPFORDH5)
     A                                    +1
     A                                      DFT('/')
     A            OIDSEQ    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIDREC/OIDSEQ IMPFORDD5)
     A                                    +4
     A                                      DFT('FSWMS RouteId/Order#:')
     A            PRTRTE    R        O    +1
     A                                      REFFLD(OHRTE  *LIBL/ORDH)
     A                                    +1
     A                                      DFT('/')
     A            PRTORD    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIDREC/OIDORD IMPFORDD5)
     A*
     A          R SUBTTL
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ1          7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ2          7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ3          7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A          R GRNDTTL
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer orders:'
     A                                      SPACEB(001)
     A            GRDORD         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail lines   :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid Cross dock P.O.s      -
     A                                         :')
     A                                      SPACEB(001)
     A            BADPO          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Items with zero quantity      -
     A                                         :')
     A                                      SPACEB(001)
     A            ZROQTY         7S 0O    39
     A                                      EDTCDE(J)
     A*
