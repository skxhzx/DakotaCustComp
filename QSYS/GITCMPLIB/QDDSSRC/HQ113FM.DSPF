     A*%%TS  SD  20120416  165624  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  HQ113FM    Question File Maintenance - Copy
     A*  16 April 2012
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    04/16/12  LMC  6.40a
     A*      - Created
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20120416  165624  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'HQ113.01'
     A                                  1 31'Question Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 39'Copy'
     A                                      DSPATR(HI)
     A                                  4  5'From Warehouse:'
     A            W1FMWH        40A  O  4 21
     A                                  5 10'From Type:'
     A                                  7  6'Type the following informtion to r-
     A                                      eceive the copied question.'
     A                                  9  7'To Warehouse. . . . . . . . .'
     A            W1TWHS         3Y 0B  9 37EDTCDE(3)
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 10  7'To Type . . . . . . . . . . .'
     A            W1TLVL         4A  B 10 37
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A            W1FLVL         4A  O  5 21
     A            W1FTYP         3A  O  5 26
     A            W1FCODE       15A  O  5 31
     A            W1FQSTN       45A  O  6 21
     A            CODETEXT      29A  O 11  7
     A            W1TCODE       15A  B 11 37
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90
     AO 30                                  DSPATR(PR)
     A  30                                  DSPATR(ND)
     A            W1TTYP         3A  B 10 42
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
