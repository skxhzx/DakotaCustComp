     A*%%TS  SD  19960621  112026  DAVE3       REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  19960621  112026  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'TF172.01'
     A                                  1 32'Transfer Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Transfer number . . . . . .'
     A            W1DNUM         7Y 0O  7 31EDTCDE(4)
     A                                  8  3'Picking warehouse . . . . .'
     A            W1HFWH         3Y 0O  8 31EDTCDE(4)
     A            W1FDSC        30A  O  8 36
     A                                  9  3'Requesting warehouse  . . .'
     A            W1HTWH         3Y 0O  9 31EDTCDE(4)
     A            W1TDSC        30A  O  9 36
     A                                 10  3'Item number . . . . . . . .'
     A            W1DITM        15A  O 10 31
     A                                 12  3'Quantity received . . . . .'
     A            W1DQR1         5Y 0B 12 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UM1          5A  O 12 37
     A            W1DQR2         5Y 0B 12 44EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1UM2          5A  O 12 50
     A  94                                  DSPATR(ND)
     A            W1DQR3         5Y 0B 12 57EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1UM3          5A  O 12 63
     A  93                                  DSPATR(ND)
     A                                 13  3'Expiration date . . . . . .'
     A            W1EXDT         6Y 0B 13 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96
     AO 90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 13 71'(MMDDYY)'
     A                                 14  3'Weight received . . . . . .'
     A            W1DWGT         7Y 2B 14 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 23  2'F5=Refresh  F12=Cancel  F20=Accept-
     A                                       warning'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
MSG DA*N97                                  SFLCLR
MSG MA                                      SFLDSP
MSG AA                                      SFLDSPCTL
MSG AA                                      SFLINZ
     A  97                                  SFLEND
MSG AA            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
