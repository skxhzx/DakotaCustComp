     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CK365PR    Ship check list - Print            CBI
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    08/25/03  RH   4.17
     A*      - Created using SL401
     A*
PCKa A*    07/15/04  HNK  PCKa
     A*      - Remove Brand and add cust Ord #
     A*
PCKb A*    09/01/04  HNK  PCKb
     A*      - print * for catch weight items.
     A*      - print total quantity
     A*
PCKc F*    12/30/04  MLB  PCKc
     A*      - Print '*Start Tran*' every time transaction number
     F*        changes on report heading.
     A*      - Added new format ENDTRN to print when transaction
     A*        number is changing.
PCKdAA*  10/08/07  MLB  PCKd
     A*  - Revised to add SAP Delivery Number to new DETAIL1_PK format.
     A*
     A*----------------------------------------------------------------
     A*
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   115
     A                                      DFT('Page')
     A            PAGE           4S 0O   119
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   118
     A                                      DFT('CK365')
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1DFT('ROUTE:')
     A            W1RTE          5A  O     8
     A                                    25DFT('TRANS:')
     A            W1TRN          7S  O    32
     A                                      EDTCDE(Z)
PCKcAA            W1SFLG        12A  O    40
PCKcDA*                                   50DFT('PALLET:')
PCKcMA                                    55DFT('PALLET:')
PCKcDA*           W1PLT          7S  O    58
PCKcMA            W1PLT          7S  O    63
     A                                      EDTCDE(Z)
     A                                    76DFT('DEPT:')
     A            W1DPT          5A  O    82
     A                                    96DFT('EMP:')
     A            W1EMP          5S  O   101
     A                                      EDTCDE(Z)
     A                                   109DFT('DOOR:')
     A            W1DOOR         5S  O   119
     A                                      EDTCDE(Z)
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               100
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    16
     A                                      DFT('Ship')
     A                                      UNDERLINE
     A                                    24
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    39
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                   100
PCKa A                                      DFT('Order#')
PCKa A**                                    DFT('Brand')
     A                                      UNDERLINE
     A                                   112
     A                                      DFT('Pack')
     A                                      UNDERLINE
PCKdAA*
PCKdAA          R COLUMNS_PK
PCKdAA                                      SPACEB(001)
PCKdAA                                      SPACEA(001)
PCKdAA                                     1
PCKdAA                                      DFT('Slot')
PCKdAA                                      UNDERLINE
PCKdAA                                    16
PCKdAA                                      DFT('Ship')
PCKdAA                                      UNDERLINE
PCKdAA                                    24
PCKdAA                                      DFT('Item')
PCKdAA                                      UNDERLINE
PCKdAA                                    39
PCKdAA                                      DFT('Description')
PCKdAA                                      UNDERLINE
PCKdAA                                   100
PCKdAA                                      DFT('Delivery#')
PCKdAA                                      UNDERLINE
PCKdAA                                   112
PCKdAA                                      DFT('Pack')
PCKdAA                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            WBDISP    R        O     1
     A                                      REFFLD(WBDISP CK365)
     A            WBQPCK    R        O    14EDTCDE(3)
     A                                      REFFLD(WBQPCK CK365)
     A            WBUCOD    R        O    20
     A                                      REFFLD(WBUCOD CK365)
     A            WBITEM    R        O    24
     A                                      REFFLD(WBITEM CK365)
PCKb A            W1ASTR         1A  O    36
     A            W1DESC        60A  O    39
PCKa A**          W1BRND    R        O   101
PCka A**                                    REFFLD(IMBRND ITEMMSC)
PCKa A            W1CORD    R        O   100
PCka A                                      REFFLD(OHCORD ORDH)
     A            W1PDSC    R        O   112
     A                                      REFFLD(ITPDSC PIRITEM)
PCKdAA*
PCKdAA          R DETAIL_PK
PCKdAA                                      SPACEB(001)
PCKdAA            WBDISP    R        O     1
PCKdAA                                      REFFLD(WBDISP CK365)
PCKdAA            WBQPCK    R        O    14EDTCDE(3)
PCKdAA                                      REFFLD(WBQPCK CK365)
PCKdAA            WBUCOD    R        O    20
PCKdAA                                      REFFLD(WBUCOD CK365)
PCKdAA            WBITEM    R        O    24
PCKdAA                                      REFFLD(WBITEM CK365)
PCKdAA            W1ASTR         1A  O    36
PCKdAA            W1DESC        60A  O    39
PCKdAA            W1DLVY         9A  O   100
PCKdAA            W1PDSC    R        O   112
PCKdAA                                      REFFLD(ITPDSC PIRITEM)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
PCKb A* Print total line
     A*
PCKb A          R TOTAL1
PCKb A                                      SPACEB(001)
PCKb A                                     4'Total:'
PCKb A            T#QPCK         7  0O    12EDTCDE(3)
     A*
PCKc A* Print End of Transaction message.
     A*
PCKc A          R ENDTRN
PCKc A                                      SPACEB(001)
PCKc A                                     4'* End of Transaction:'
PCKc A            HLDTRN         7S 0O    26EDTCDE(3)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('CK365: End of Report ')
     A*
