     A****************************************************************
     A*
413 MA* ER282PR   Employee management report for selection crew (Summary)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
413 AA* 03/13/00 HNK 4.13
     A*  Task code added to report.
     A*
413  A*  NOTE: THIS PROGRAM IS NOW REPLACED BY ER382PR
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER282'
     A*
     A* Print crew code and description
     A*
     A          R CRHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'CREW:'
     A            WKCREW    R             +1REFFLD(WKCREW ER281)
     A            CRDESC    R             +1REFFLD(CRDESC CREW)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    54'Direct'
     A                                    66'Cases'
     A                                    83'Cube'
     A                                    97'Weight'
     A                                   106'Cases'
     A                                      SPACEA(001)
     A*
     A                                     2'  Date  '
     A                                      UNDERLINE
     A                                    11'Employee                       '
     A                                      UNDERLINE
413 AA                                    45'Task  '
413 AA                                      UNDERLINE
     A                                    54' Hours'
     A                                      UNDERLINE
     A                                    62' Selected'
     A                                      UNDERLINE
     A                                    78' Selected'
     A                                      UNDERLINE
     A                                    94' Selected'
     A                                      UNDERLINE
     A                                   105'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A            GTDATE         6S 0      2EDTCDE(Y)
     A  91                                75
     A                                      '(Continued)'
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKEMP#    R             11REFFLD(WKEMP# ER281)
     A                                       EDTCDE(Z)
     A            WKNAME    R             17REFFLD(WKNAME ER281)
413 AA            WKCODE    R             45REFFLD(WKCODE ER281)
     A            DIRTOT         5S 2     54EDTCDE(3)
     A            WKPTOT    R             62REFFLD(WKPTOT ER281)
     A                                      EDTCDE(1)
     A            WKCTOT    R             73REFFLD(WKCTOT ER281)
     A                                      EDTCDE(1)
     A            WKWTOT    R             89REFFLD(WKWTOT ER281)
     A                                      EDTCDE(1)
     A            CASEPH         5S 0    106EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#DTOT         7S 2     52EDTCDE(3)
     A            T#PTOT         7S 0     62EDTCDE(1)
     A            T#CTOT        11S 3     73EDTCDE(1)
     A            T#WTOT        11S 2     89EDTCDE(1)
     A            T#CSPH         7S 0    104EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER282: End of Report '
     A*
