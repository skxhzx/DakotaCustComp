     A*****************************************************************
     A*
     A*  SL42202   View Available Pick or Overflow Slots.
     A*  02 May 2013
     A*  Retha Davis
     A*
     A*  Revisions:
     A*
     A*  04/08/05  RLP  5.00
     A*    - Created from SL312FM.
     A*
640a A*  02/21/11  GJA  6.40a
     A*    - Enh:  Add '*' to denote there are virtual
     A*
650 AA*  05/02/13  RBD  6.50
     A*    - Created from SL42201.
     A*    - Fix: Adjusted column headings to line up with detail.
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20050408  161610  BOBP        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
650 DA**                                1 31'Available Pick Slots'
650 MA            W1TITL        24A  O  1 29
     A                                      DSPATR(HI)
     A                                  1  3'SL422'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20050413  160034  BOBP        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A                                  1 41'Change to warehouse   .'
     A            PTINP1         3Y 0B  1 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WDCODE         5A  O  2  2
     A                                  2  8'-'
     A            WDDESC        30A  O  2 10
     A                                  2 41'Change to department  .'
     A            PTINP2         5A  B  2 66
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A            SECTN          5   O  3  2
     A                                  3 41'Change to section . . .'
     A            PTINP3         1A  B  3 66
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A            AISLE          3A  O  4  2
     A                                  4 41'Position to aisle . . .'
     A            PTINP4         3A  B  4 66
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
640aAA                                  5 10'* denotes virtual found'
     A                                  5 41'Position to type  . . .'
     A                                      DSPATR(ND)
     A            PTINP5         2A  B  5 66
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20050408  214449  BOBP        REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
640aAA            SLVSLT         1A  O 10  2
     A            SLDISP        12A  O 10  4
     A            SLDESG         5A  O 10 17
     A            SLSDEF         2A  O 10 24
     A            SLSTAT         2A  O 10 29
     A*
     A* *DS3                                SFLLIN(0007)
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20050408  214449  BOBP        REL-V5R3M0  5722-WDS
     A  *DS3                                SFLLIN(0012)
     A                                      SFLSIZ(0078)
     A                                      SFLPAG(0024)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  8 23'Slot'
     A                                      DSPATR(HI)
650 MA                                  8 64'Slot'
     A                                      DSPATR(HI)
     A                                  9  4'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 17'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 23'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 28'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650 MA                                  9 45'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
650 MA                                  9 58'Desig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650 MA                                  9 64'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650 MA                                  9 69'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
