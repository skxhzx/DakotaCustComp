     A*%%TS  SD  20200114  080748  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CW122FM   P/O Catch Weight Maint. - Display
     A*  11 April 2001
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
415 AA*  04/12/01  MLB  4.15
     A*    - Created.
650aAA*  07/25/14  LMC  6.50a
     A*    - Renamed from CW122FM to CW12201
     A*    - Added a hidden field for storing weights entered into
     A*      UCIINFO file.
700aAA*  03/22/16  LMC  7.00a
     A*    - Renamed from CW12201 to CW12202
     A*    - Enh: Adding logic for Metric.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A N92                                  CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A  85                                  SFLNXTCHG
650aAA            W1ENT         50   H
     A            W1SEQ          4Y 0O  8  2EDTCDE(3)
     A            W1CWGT         7Y 0B  8  8EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20200114  080748  LYNNM       REL-V7R2M0  5770-WDS
     A  *DS3                                SFLLIN(0003)
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0075)
     A                                      OVERLAY
     A  98                                  ALARM
     A                                      CF07
     A                                      CF20
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'CW122.03'
     A                                  1 24'P/O Catch Weight Entry Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3  2'Item:'
     A            $PITEM        15A  O  3  8
     A            WODESC        15A  O  3 24
     A                                  3 44'Qty:'
     A            WOPQTY         5Y 0O  3 50EDTCDE(3)
     A                                  3 62'P/O:'
     A            WOPO           9A  O  3 68
     A                                  4 26'Catch Weights Entered:'
     A            WOCW#          7Y 0O  4 50EDTCDE(3)
     A                                  4 60'Total:'
     A            WOTCW          9Y 2O  4 68EDTCDE(3)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  6'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 22'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 38'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 54'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 70'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            W1KGLB         3A  O  7  1
     A            W1WGT         50A  B  7  6CHECK(ER)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 58'Assume 2 decimals.'
     A                                      COLOR(BLU)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
700aAA            CMDLN1        79A  O  1  2COLOR(BLU)
700aDA*                                 1  2'F5=Refresh'
700aDA*                                     COLOR(BLU)
700aDA*                                 1 14'F12=Cancel'
700aDA*                                     COLOR(BLU)
700aDA*                                 1 26'F20=Accept warning'
700aDA*                                     COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
