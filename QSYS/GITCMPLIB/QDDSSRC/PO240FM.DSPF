     A*%%TS  SD  20030416  115821  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PO240FM   PO Quantity Exception Entry - Change
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*
     A*  Compile Options:
     A*   RSTDSP(*YES)
     A*
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
417 AA*  03/27/03  MLB  4.17
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA10
     A                                      CA12
     A*
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PO240.01'
     A                                  1 27'PO Quantity Exception Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*
     A*    Title format - 16 Line Display.
     A          R TITLE16
     A                                      OVERLAY
     A                                  1  2'PO Qty Exceptions'
     A                                      DSPATR(HI)
     A                                  1 63'PO240.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*****************************************************************
     A*
     A* Position To Record
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A            $PWHSE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2  2'PO .:'
     A            $PPO           9A  O  2  8
     A                                  2 48'Position to code  . . . .'
     A            PTINP2         2A  B  2 75
     A                                  3  2'Item:'
     A            WOITEM        65A  O  3  8
     A                                  4  2'Qty ordered :'
     A            PDQOR1         5Y 0O  4 16EDTCDE(M)
     A            PDQOR2         5Y 0O  4 23
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(M)
     A            PDQOR3         5Y 0O  4 30
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(M)
     A                                  4 38'Total exceptions . :'
     A            WONREX         5Y 0O  4 59EDTCDE(L)
     A                                  5  2'Qty received:'
     A            PDQRC1         5Y 0O  5 16EDTCDE(L)
     A            PDQRC2         5Y 0O  5 23
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(L)
     A            PDQRC3         5Y 0O  5 30
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(L)
     A                                  5 38'Total exception qty:'
     A            WOQEX1         5Y 0O  5 59EDTCDE(L)
     A            WOQEX2         5Y 0O  5 66
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(L)
     A            WOQEX3         5Y 0O  5 73
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(L)
     A*
     A* Position To Record - 16 Line Display.
     A*
     A          R POSTNR16
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A            $PWHSE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2  2'PO .:'
     A            $PPO           9A  O  2  8
     A                                  3  2'Item:'
     A            WOITEM        65A  O  3  8
     A                                  4  2'TotEx'
     A            WONREX         5Y 0O  4  8EDTCDE(3)
     A                                  4 26'QtyOrd'
     A*
     A            PDQOR1         5Y 0O  4 33EDTCDE(3)
     A            PDQOR2         5Y 0O  4 39
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A            PDQOR3         5Y 0O  4 45
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A                                  5  2'ExQty'
     A            WOQEX1         5Y 0O  5  8EDTCDE(3)
     A            WOQEX2         5Y 0O  5 14
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A            WOQEX3         5Y 0O  5 20
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A                                  5 26'QtyRcv'
     A            PDQRC1         5Y 0O  5 33EDTCDE(3)
     A            PDQRC2         5Y 0O  5 39
     A  92                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A            PDQRC3         5Y 0O  5 45
     A  93                                  DSPATR(ND)
     A                                      EDTCDE(3)
     A*
     A*****************************************************************
     A*
     A* Subfile for List Display
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1CODE         2A  O 12  7
     A            W1DESC        30A  O 12 10
     A            W1QEX1         5Y 0O 12 41EDTCDE(3)
     A            W1QEX2         5Y 0O 12 47EDTCDE(4)
     A            W1QEX3         5Y 0O 12 53EDTCDE(4)
     A*
     A* Subfile for List Control
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01 41                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&CRCD &CFLD &CPOS)
     A            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A            CRCD          10A  H      TEXT('Hidden: Cursor rcdfmt')
     A            CFLD          10A  H      TEXT('Hidden: Cursor field')
     A            CPOS           4S 0H      TEXT('Hidden: Cursor fld pos')
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Exception code/Description       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Qty 1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Qty 2'
     A  92                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Qty 3'
     A  93                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1EXCD         2A  B 11  7
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(HI)
     A            W1QTY1         5Y 0B 11 10EDTCDE(4)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(HI)
     A            W1UM1          5A  O 11 16
     A            W1QTY2         5Y 0B 11 22EDTCDE(4)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  92
     AO 90                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A N90N92                               DSPATR(HI)
     A            W1UM2          5A  O 11 28
     A            W1QTY3         5Y 0B 11 34EDTCDE(4)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A N90N93                               DSPATR(HI)
     A            W1UM3          5A  O 11 40
     A*
     A* Subfile for List Display - 16 Line Display.
     A*
     A          R D16SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1CODE         2A  O 12  6
     A            W1QEX1         5Y 0O 12  9EDTCDE(3)
     A            W1QEX2         5Y 0O 12 15EDTCDE(4)
     A            W1QEX3         5Y 0O 12 21EDTCDE(4)
     A            W1DESC        30A  O 12 27
     A*
     A* Subfile for List Control - 16 Line Display.
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01 41                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND
     A                                      RTNCSRLOC(&CRCD &CFLD &CPOS)
     A            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A            CRCD          10A  H      TEXT('Hidden: Cursor rcdfmt')
     A            CFLD          10A  H      TEXT('Hidden: Cursor field')
     A            CPOS           4S 0H      TEXT('Hidden: Cursor fld pos')
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1UM1          5A  O 10  9DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1UM2          5A  O 10 15
     A  92                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1UM3          5A  O 10 21
     A  93                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Description                      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1EXCD         2A  B 11  6
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(HI)
     A            W1QTY1         5Y 0B 11  9EDTCDE(4)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(HI)
     A            W1QTY2         5Y 0B 11 15EDTCDE(4)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  92
     AO 90                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A N90N92                               DSPATR(HI)
     A            W1QTY3         5Y 0B 11 21EDTCDE(4)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A N90N93                               DSPATR(HI)
     A*
     A* Define Window format.
     A*
     A          R SCREEN2W
     A                                      WINDOW(&WROW &WCOL 7 50 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT 'Update Quantity Ex-
     A                                      ception') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit F4=Prompt -
     A                                      F10=Accept warning F12=Cancel')   -
     A                                      *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      OVERLAY
     A            WROW           3S 0P
     A            WCOL           3S 0P
     A*
     A* Define Window layout.
     A*
     A          R SCREEN2
     A                                      WINDOW(SCREEN2W)
     A  98                                  ALARM
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      RTNCSRLOC(&CRCD &CFLD &CPOS)
     A                                      OVERLAY
     A**                                    CF04
     A**                                    CF10
     A            CRCD          10A  H      TEXT('Hidden: Cursor rcdfmt')
     A            CFLD          10A  H      TEXT('Hidden: Cursor field')
     A            CPOS           4S 0H      TEXT('Hidden: Cursor fld pos')
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  2  2'ExCode:'
     A            W2EXCD         2A  B  2 10
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A            W2DESC        30A  O  2 13
     A                                  3  2'ExQty1:'
     A            W2QTY1         5Y 0B  3 10EDTCDE(3)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A            W2UM1          5A  O  3 16
     A                                  4  2'ExQty2:'
     A  92                                  DSPATR(ND)
     A*
     A            W2QTY2         5Y 0B  4 10EDTCDE(3)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  92
     AO 90                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            W2UM2          5A  O  4 16
     A  92                                  DSPATR(ND)
     A                                  5  2'ExQty3:'
     A  93                                  DSPATR(ND)
     A*
     A            W2QTY3         5Y 0B  5 10EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            W2UM3          5A  O  5 16
     A  93                                  DSPATR(ND)
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*   Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A* Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*   Error Message Subfile Record - 16 Line Display.
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A* Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Control Record - Window
     A*
     A*
     A*   Error Message Subfile Record
     A*
     A          R MSGRECW                   SFL
     A                                      SFLMSGRCD(07)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A          R MSGCTLW                   SFLCTL(MSGRECW)
     A                                      WINDOW(SCREEN2W)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLRW
     A                                      WINDOW(SCREEN2W)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(07)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* Option, Command and EOF Message Lines for Lookup Panels.
     A*
     A* Option Lines
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A* Command Lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A          R C16REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A* 'More.../Bottom' Message Line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A* Delete Display Instructions.
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  4  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  5  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A                                  6  2' '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to Clear Different Portions of a Panel.
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A* Clear All Lines After Title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A* Clear Lines Between Title and Subfile List
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A*
     A* Command Lines
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R C16DEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A                                  1  2'F12=Cancel'
     A                                      COLOR(BLU)
