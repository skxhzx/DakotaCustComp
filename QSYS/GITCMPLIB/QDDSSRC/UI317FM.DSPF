     A*%%TS  SD  20061110  073233  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A*
     A*  UI317FM   UCI Inquiry -  RF Mini Screen
     A*  08 November, 2006
     A*  Jeffrey C. Jahnke
     A*
     A*  Revisions
     A*    xx/xx/xx  xxx  x.xxa
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20061109  202229  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(15)
     A  98                                  ALARM
     A                                  1  2'UI317.01'
     A                                  1 13'UCI Inquiry'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5  2'UCI Code'
     A            W1UCI         10A  B  6  2
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      CHECK(ER)
     A                                 15  2'F3/F12=Exit '
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20061110  073233  JEFF        REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A*                                     CLRL(15)
     A*                                     CSRLOC(WOROW      WOCOL)
     A*                                     OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'UI317.02 '
     A                                  1 13'UCI Inquiry'
     A                                  3  3'Whse'
     A                                      DSPATR(HI)
     A            W1WHSE         3Y 0O  3  8EDTCDE(Z)
     A                                  4  3'PO#'
     A                                      DSPATR(HI)
     A            W2PO           9A  O  4  8
     A            W2ITEM        65A  O  5  3
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                  6  3'Lic#'
     A                                      DSPATR(HI)
     A            W2LCN         15A  O  6  8
     A                                  7  3'Wght'
     A                                      DSPATR(HI)
     A            W2WGT          7Y 2O  7  8EDTCDE(4)
     A                                  8  3'Rcv Date'
     A                                      DSPATR(HI)
     A            W2IRDT         8   O  8 13
     A                                  9  3'Rcv Time'
     A                                      DSPATR(HI)
     A            W2IRTM         6Y 0O  9 13EDTWRD('  :  :  ')
     A                                 10  3'Rtn Date'
     A                                      DSPATR(HI)
     A            W2IRDE         8   O 10 13
     A                                 11  3'Rtn Date'
     A                                      DSPATR(HI)
     A            W2IRTI         6Y 0O 11 13EDTWRD('  :  :  ')
     A                                 15  2'F3=Exit '
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20061110  071302  JEFF        REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(15)
     A*                                     CSRLOC(WOROW      WOCOL)
     A*                                     OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'UI317.03 '
     A                                  1 13'UCI Inquiry'
     A                                  3  2'Whse'
     A            W1WHSE         3  0O  3  8DSPATR(HI)
     A                                  5  2'Pick Flag..'
     A            UCIPCK         1   O  5 15DSPATR(HI)
     A                                  6  2'Return Flag'
     A            UCIRTS         1   O  6 15DSPATR(HI)
     A                                  7  2'Stock Code.'
     A            UCISTS         1   O  7 15DSPATR(HI)
     A                                  8  2'Reason Code'
     A            UCIRCD         2   O  8 15DSPATR(HI)
     A                                 10  2'Rcv Tran#.'
     A            UCIRCT         7  0O 10 15DSPATR(HI)
     A                                 11  2'Pick Tran#'
     A            UCIPKT         7  0O 11 15DSPATR(HI)
     A                                 12  2'Rtn Tran#.'
     A            UCIRTT         7  0O 12 15DSPATR(HI)
     A                                 15  2'F3=Exit'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
