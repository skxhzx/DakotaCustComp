     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2021 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTPFMT     Test PFMT
     A*
     A*  Revisions
     A*
750 AA*    04/08/21  DAS  7.50
     A*      - Created
     A*
750aAA*    09/29/21  DAS  7.50a
     A*      - Added some text
     A*
770aAA*    04/30/23  DAS  7.70a
     A*      - Revised display of command options
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTPFMT.1'
     A                                  1 29'Test PFMT'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  3'Client:'
     A            W1CLIENT      10   O  3 11
     A                                  3 24'Location:'
     A            W1CLILOC      10   O  3 34
     A                                  3 47'Host:'
     A            W1HOSTSYS     10   O  3 53
     A                                  5  2'Command . . . . . . . . . . .'
     A            W1CMD          8   B  5 32
     A                                  5 44'any'
     A                                  6  4'*PRINT, *BLTTEST, -
     A                                       *PRTLBL, *PRTZON, *PRTTRN -
     A                                       *RPRTLB, *TEST1, *TEST2'
     A                                  8  2'Output Type . . . . . . . . .'
     A            W1OUTTYPE      1   B  8 32
     A                                  8 44'blank/N=Normal'
     A                                  9  4'D=Normal with device override, Q=Q-
     A                                      PRINT, O=LABELOUT, I=IFS Cleaned, R-
     A                                      =IFS Raw'
     A                                 11  2'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B 11 32
     A                                 12  2'Route Id  . . . . . . . . . .'
     A            W1RTID         5   B 12 32
     A                                 13  2'Transaction . . . . . . . . .'
     A            W1TRN#         7Y 0B 13 32
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 13 44'Must be entered if Label is zero'
     A                                 14  2'Label . . . . . . . . . . . .'
     A            W1LBL#         7Y 0B 14 32
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 14 44'Must be entered if Tran is zero'
     A                                 16  2'Label Set . . . . . . . . . .'
     A            W1SET          6A  B 16 32
     A                                 16 44'Must be entered for *BLTTEST   '
     A                                 17  2'Label Format  . . . . . . . .'
     A            W1FORMAT       8A  B 17 32
     A                                 18  2'Printer Device Override . . .'
     A            W1DEV         10A  B 18 32
     A                                 18 44'Used with Output Type D'
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
