     A*%%TS  SD  20070428  150430  BFCPACK     REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UI112FM   UCI Catch Weight - Detail Display
     A*  03 August 2006
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
510 AA*  08/03/06  JCJ  5.10
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070428  150430  BFCPACK     REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'UI112.01'
     A                                  1 25'UCI Catch Weight Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A            $PCMD          8A  O  2 72
     A            $PWHSE         3Y 0O  3  2EDTCDE(4)
     A                                  3  6'-'
     A            W1DESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Uci Number . . . . . .'
     A            W1INBR    R        B  7 27REFFLD(UCIREC/UCINBR *LIBL/UCICW)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  4'Item Number. . . . . .'
     A            W1ITEM    R        O  8 27REFFLD(UCIREC/UCIITM *LIBL/UCICW)
     A            W1IDES        30A  O  8 43DSPATR(HI)
     A                                  9  4'License Number . . . .'
     A            W1LCN     R        B  9 27REFFLD(UCIREC/UCILCN *LIBL/UCICW)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Catch Weight . . . . .'
     A            W1WGT          7Y 2B 10 27EDTWRD('     .  ')
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 11  4'Lot Code Value . . . .'
     A            W1LOT     R        B 11 27REFFLD(UCIREC/UCILOT *LIBL/UCICW)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Inbound delivery num .'
     A            W1INB     R        B 12 27REFFLD(UCIREC/UCIINB *LIBL/UCICW)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Outbound delivery num.'
     A            W1OUT     R        B 13 27REFFLD(UCIREC/UCIOUT *LIBL/UCICW)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Receive Date . . . . .'
     A            W1RDT          6Y 0B 14 27
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Y)
     A                                      TEXT('Receive Date')
     A                                 14 48'Receive Tran Number. . .'
     A            #W1RCT         7Y 0B 14 73EDTCDE(Z)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Receive Time . . . . .'
     A            W1RTM     R        B 15 27REFFLD(UCIREC/UCIRTM *LIBL/UCICW)
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTWRD('  :  :  ')
     A                                 16  4'Expiration Date. . . .'
     A            W1EDT          6Y 0B 16 27
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Y)
     A                                      TEXT('Receive Date')
     A                                 17  4'Pick Flag. . . . . . .'
     A            W1PCK     R        B 17 27REFFLD(UCIREC/UCIPCK *LIBL/UCICW)
     A  31                                  DSPATR(RI)
     A N31                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 31'(Y=Yes, N=No)'
     A                                      DSPATR(HI)
     A                                 17 48'Picking Tran Number. . .'
     A            W1PKT          7Y 0B 17 73EDTCDE(Z)
     A  35                                  DSPATR(RI)
     A N35                                  DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  4'Returns Flag . . . . .'
     A            W1RTS     R        B 18 27REFFLD(UCIREC/UCIRTS *LIBL/UCICW)
     A  32                                  DSPATR(RI)
     A N32                                  DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 31'(Y=Yes, N=No)'
     A                                      DSPATR(HI)
     A                                 18 48'Return Tran Number . . .'
     A            W1RTT          7Y 0B 18 73EDTCDE(Z)
     A  36                                  DSPATR(RI)
     A N36                                  DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Returns Date . . . . .'
     A            W1RDE          6Y 0B 19 27
     A  33                                  DSPATR(RI)
     A N33                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Y)
     A                                      TEXT('Returns Date')
     A                                 20  4'Returns Reason Code. .'
     A            W1RCD     R        B 20 27REFFLD(UCIREC/UCIRCD *LIBL/UCICW)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 AA                                 20 48'Label Number . . . . . .'
510 AA            W1LBL          7Y 0B 20 73EDTCDE(Z)
     A                                 21  4'Stock Code . . . . . .'
     A            W1STS          1A  B 21 27
     A  37                                  DSPATR(RI)
     A N37                                  DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
