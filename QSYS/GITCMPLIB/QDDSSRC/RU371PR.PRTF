     A****************************************************************
     A*
     A* RU371PR    Cleared flags report
     A* 15 July, 2004
     A* Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650a A*  02-27-13  GJA  650a
     A*    -Enh: Add item total line
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    41
     A                                      DFT('Cleared Flags-by Dept')
     A            FRMDTE         6S 0O    71EDTCDE(Y)
     A                                    81
     A                                      DFT('Thru')
     A            TODTE          6S 0O    86EDTCDE(Y)
     A                                   120
     A                                      DFT('Page')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    37
     A                                   125
     A                                      DFT('RU371')
     A                                      SPACEA(002)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    54
     A                                      DFT('Start')
     A                                    64
     A                                      DFT('Close')
     A                                    81
     A                                      DFT('Flag')
     A                                    87
     A                                      DFT('Found')
     A                                    94
     A                                      DFT('Orig')
     A                                   103
     A                                      DFT('Flag')
     A                                   123
     A                                      DFT('Flag')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Dept')
     A                                     8
     A                                      DFT('Item')
     A                                    18
     A                                      DFT('Description')
     A                                    54
     A                                      DFT('Time')
     A                                    64
     A                                      DFT('Time')
     A                                    73
     A                                      DFT('Diff')
     A                                    82
     A                                      DFT('Qty')
     A                                    88
     A                                      DFT('Qty')
     A                                    94
     A                                      DFT('Tran#')
     A                                   103
     A                                      DFT('Tran#')
      *
     A                                   110
     A                                      DFT('Route')
     A                                   117
     A                                      DFT('Emp#')
     A                                   123
     A                                      DFT('Type')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RUWHDP    R              1REFFLD(LBWHDP LABEL)
     A            ITITEM    R              8REFFLD(ITITEM PIRITEM)
     A            ITDESC    R             18REFFLD(ITDESC PIRITEM)
     A            W1STRT         6S 0     52EDTWRD('0 :  :  ')
     A            W1CLST         6S 0     62EDTWRD('0 :  :  ')
     A            W1DIFF         6S 0     71EDTWRD('0 :  :  ')
     A            RUQYSO    R             80REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(Z)
     A            RUQYFD    R             86REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(3)
     A            RUOTRN    R             93REFFLD(TRAN# PIRDICT)
     A            RUTRN#    R            102REFFLD(TRAN# PIRDICT)
     A            RHRTE     R            111REFFLD(RHRTE RTEHED)
     A            PTEMP#    R            116REFFLD(PTEMP# PIRTRAN)
     A                                       EDTCDE(Z)
     A            W1TYPE         6A      123
     A*
     A*----------------------------------------------------------------
     A          R DEPTOT
     A                                      SPACEB(001)
     A            WDEPT          5A        1
     A                                     6
     A                                      DFT('Items Flagged:')
     A            WDPTTO         5S 0     21
     A                                      EDTCDE(Z)
     A*
650aAA*----------------------------------------------------------------
     A          R ITMTOT
     A                                      SPACEB(001)
     A                                     6
     A                                      DFT('Items Flagged:')
     A            WITTTO         5S 0     21EDTCDE(Z)
     A            WITQTY         5S 0     80EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A          R ALLDPT
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total Items Flagged:')
     A            WALLDP         5S 0O    22
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('RU371: End of Report ')
     A*
