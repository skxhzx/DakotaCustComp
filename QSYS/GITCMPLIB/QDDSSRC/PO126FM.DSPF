     A*%%TS  SD  20060411  125959  ROCKY       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PO126FM    PO Pallet estimate - Display screen
     A*  10 April 2006
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
500 AA*    04/10/06  RH   5.00
     A*      - Created using PO120.                                    umented.
     A*
417aAA*    04/03/03  RH   4.17a
     A*      - Add DSPSFC6 to display UPC multiple items in window.
417bAA*    04/08/03  MLB  4.17b
     A*      - Fix: Added protect attribute to PTINP2 on SCANREC and
     A*        SCANREC2 formats.
417cAA*    04/15/03  MLB  4.17c
     A*      - Enh - Increased length of UPC entry field to 50 and
     A*        renamed field W1UPCD.
     A*      - Changed SLNO on the following formats to move them
     A*        down one line: OPTREC, SCANREC and SCANREC2.
     A*      - Moved P/O text and display field from SCANREC, SCANREC2
     A*        display formats to POSTNREC, POSTNREC2 formats.
417dAA*    04/19/03  DAS  4.17d
     A*      - BabyGUI had trouble with displaying split UPC field.
     A*      - Removed warehouse # and desc from POSTNREC and POSTNRC2.
     A*      - Moved PO# into TITLE rec.
     A*      - Moved UPC and position fields to left side of screen.
417eAA*    04/20/04  RH   4.17e
     A*      - Add DSPSFC7 to display Subtotal for normal PO.
417fAA*    04/27/04  RH   4.17f
     A*      - Add DSPSFC8 to display Subtotal items for Super PO
     A*      - Change DSPSFC to use F8 for item subtotal.
500aAA*    04/01/05  RH   5.00a
     A*      - Enable F24 for normal PO like Super PO.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A                                      CA09
     A                                      CF11
     A                                      CF23
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PO126.01'
     A                                  1 33'PO Pallet Estimate'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
417dAA                                  2  2'PO #   '
417dAA            WOPO           9A  O  2 10
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  2 65'** Super P/O **'
     A  40                                  DSPATR(RI)
     A N40                                  DSPATR(ND)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
417dAA            FLD001         1A  O  1  2
417dAA                                  2  2'Scan UPC . . . . . . . . .'
417dAA            W1UPCD        50A  B  2 30CHECK(ER)
417dAA  20                                  DSPATR(PC)
     A*
     A*    Mfr number view.
     A          R POSTNRC2
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
417dAA            FLD001         1A  O  1  2
417dAA                                  2  2'Scan UPC . . . . . . . . .'
417dAA            W1UPCD        50A  B  2 30CHECK(ER)
417dAA  20                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R SCANREC
     A                                      OVERLAY
     A                                      LOCK
417cMA                                      SLNO(05)
417dMA                                  1  2'OR Position to Item  . . .'
417dMA            PTINP1        15A  B  1 30
417dMA N40        FLD002         1A  O  2 79
417dMA                                  1 47'and/or PO . .'
     A N40                                  DSPATR(ND)
417dMA            PTINP2         9A  B  1 61
     A N40                                  DSPATR(ND)
417bAA N40                                  DSPATR(PR)
     A*
     A*    Mfr number view.
     A          R SCANREC2
     A                                      OVERLAY
     A                                      LOCK
417cMA                                      SLNO(05)
417dAA                                  1  2'OR Position to Mfr Code  .'
417dAA            W1MFR         15A  B  1 30
417dAA                                  1 47'and/or PO . .'
417dAA N40                                  DSPATR(ND)
417dAA            PTINP2         9A  B  1 61
417dAA N40                                  DSPATR(ND)
417dAA N40                                  DSPATR(PR)
417dAA N40        FLD002         1A  O  2 79
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A*           OPTION         2A  B 11  4
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            PDITEM    R        O 11  8REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC         22A  O 11 24
     A            WOQOR         15A  O 11 48
     A            WODPT          5A  O 11 65
     A            WOPLT          5Y 0O 11 72EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      CF06
500a A                                      CF24
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A*                                10  4'Opt'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
     A                                 10  8'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Ordered        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500X A                                 10 65'Dept'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Pallets'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R DSPSFL1                   SFL
     A  21                                  SFLNXTCHG
     A*           OPTION         2A  B 11  4
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            PDITEM    R        O 11  8REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC1        22A  O 11 24
     A            WOQOR         15A  O 11 48
     A            WODPT          5A  O 11 65
     A            WOPLT          5Y 0O 11 72EDTCDE(4)
     A*           WOQRC         15A  O 11 65
     A            WOPOTX         3A    12  9
     A                                      DSPATR(HI)
     A            WOPO#          9A  O 12 13DSPATR(HI)
     A            WODSC2        22A  O 12 24
     A            WOPDSC        15A  O 12 48
     A            WOTIE          4Y 0O 12 65EDTCDE(4)
     A                                 12 70'/'
     A            WOHI           2Y 0O 12 72EDTCDE(4)
     A*
     A          R DSPSFC1                   SFLCTL(DSPSFL1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A                                      CF06
417e A                                      CF08
     A  40                                  CF07
500aMA                                      CF24
500aDA* 40                                  CF24
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
417cDA*           FLD001         1A  O  9  2
     A*                                10  4'Opt'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
     A                                 10  8'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Ordered        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500X A                                 10 65'Dept'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Pallets'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R DSPSFL2                   SFL
     A*%%TS  SD  20060411  125959  ROCKY       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A*           OPTION         2A  B 11  4
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            PDITEM    R        O 11 18REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC         22A  O 11 34
     A            WOQOR2         9A  O 11 57
     A*           WOQRC2         9A  O 11 72
     A            WODPT          5A  O 11 67
     A            WOPLT          5Y 0O 11 74EDTCDE(4)
     A            WOMFR         15A  O 11  2
     A*           TOUCH          1A  O 11  2
     A*
     A          R DSPSFC2                   SFLCTL(DSPSFL2)
     A*%%TS  SD  20060411  125959  ROCKY       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A N40                                  CF06
     A                                      CF07
     A                                      CF24
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A*                                10  4'Opt'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
417cDA*           FLD001         1A  O  9  2
     A                                 10 18'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Ordered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500X A                                 10 66'Dept'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Pallets'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  2'Mfr. Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A          R DSPSFL3                   SFL
     A  21                                  SFLNXTCHG
     A*           TOUCH          1A  O 11  2
     A*           OPTION         2A  B 11  4
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            WOMFR         15A  O 11  7
     A            PDITEM    R        O 11 23REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            WODSC1        22A  O 11 39
     A            WOQOR2         9A  O 11 62
     A            WOQRC2         9A  O 11 72
     A            WOPOTX         3A    12  9
     A                                      DSPATR(HI)
     A            WOPO#          9A  O 12 13DSPATR(HI)
     A            WODSC2        22A  O 12 23
     A            WOPDSC        15A  O 12 47
     A            WOTIE          4Y 0O 12 64EDTCDE(4)
     A                                 12 69'/'
     A            WOHI           2Y 0O 12 71EDTCDE(4)
     A*
     A          R DSPSFC3                   SFLCTL(DSPSFL3)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A N40                                  CF06
417e A                                      CF08
     A  40                                  CF07
500a A                                      CF24
500aDA* 40                                  CF24
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
417cDA*           FLD001         1A  O  9  2
     A*                                10  3'Opt'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
     A                                 10  7'Mfr. Code      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Description           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Ordered  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Received '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A          R WINDOW4
     A                                      WINDOW(07 02 16 75 *MSGLIN)
     A*
     A                                      WDWTITLE((*TEXT &W4TITL) *CENTER)
     A*
     A                                      WDWTITLE((*TEXT 'F5=Refresh  F12=Ca-
     A                                      ncel') *BOTTOM)
     A*
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            W4TITL        75A  P
     A*
     A*
     A          R DSPSFL4                   SFL
     A  21                                  SFLNXTCHG
     A*           OPTION         2A  B  8  3
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            HSEQ      R        H      REFFLD(PDREC/PDSEQ *LIBL/PODTL)
     A                                      TEXT('HIDDEN: PO Seq#')
     A            HITEM     R        H      REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A                                      TEXT('HIDDEN: PO Item')
     A            HVITM     R        H      REFFLD(PDREC/PDVITM *LIBL/PODTL)
     A                                      TEXT('HIDDEN: PO Mfr#')
     A            WOPO4     R        O  8  7REFFLD(PDREC/PDPO  *LIBL/PODTL)
     A            WOVEN     R        O  8 18REFFLD(PHREC/PHVEN *LIBL/POHDR)
     A                                      EDTCDE(3)
     A            WOQOR         15A  O  8 31
     A            WOQRC         15A  O  8 48
     A*
     A          R DSPSFC4                   SFLCTL(DSPSFL4)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOW4)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  57                                  SFLEND(*MORE)
     A            SLCTXT         5A  O  2  2
     A            SLCVAL        15A  O  2  8
     A  41                              4  2'Type option, press Enter.'
     A                                      COLOR(BLU)
     A  41                              5  4'1=Select'
     A                                      COLOR(BLU)
     A*                                 7  2'Opt'
     A*                                     DSPATR(HI)
     A*                                     DSPATR(UL)
     A                                  7  7'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 18'Vendor     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 31'Ordered        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 48'Received       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R BLANK4                    WINDOW(WINDOW4)
     A                                      OVERLAY
     A                                      SLNO(8)
     A                                      CLRL(5)
     A*
     A          R BLANK5                    WINDOW(WINDOW4)
     A                                      OVERLAY
     A                                      SLNO(6)
     A                                      CLRL(5)
     A*
     A          R DSPSFL5                   SFL
     A  21                                  SFLNXTCHG
     A            HSEQ      R        H      REFFLD(PDREC/PDSEQ *LIBL/PODTL)
     A                                      TEXT('HIDDEN: PO Seq#')
     A*           OPTION         2A  B  6  1
     A* 21                                  DSPATR(PC)
     A*N21N90                               DSPATR(UL)
     A* 21                                  DSPATR(RI)
     A* 90                                  DSPATR(PR)
     A            W5PO      R        O  6  5REFFLD(PDREC/PDPO *LIBL/PODTL)
     A            W5VAL1    R        O  6 16REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            W5DESC    R        O  6 33REFFLD(IXREC/IXDSC1 *LIBL/ITEMDSC)
     A            W5TXT2         4   O  7 18
     A            W5VAL2    R        O  7 23REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A                                  7 52'Pack:'
     A            W5PDSC    R        O  7 58REFFLD(ITREC/ITPDSC *LIBL/PIRITEM)
     A*
     A          R DSPSFC5                   SFLCTL(DSPSFL5)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0004)
     A                                      WINDOW(WINDOW4)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  57                                  SFLEND(*MORE)
     A                                  2  2'Type option, press Enter.'
     A                                      COLOR(BLU)
     A                                  3  4'1=Select'
     A                                      COLOR(BLU)
     A                                  5  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W5TXT1        15   O  5 16
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 33'Description                       -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A**********
     A          R DSPSFL6                   SFL
     A*%%TS  SD  20030404  120934  ROCKY       REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            HSEQ      R        H      REFFLD(PDREC/PDSEQ *LIBL/PODTL)
     A                                      TEXT('HIDDEN: PO Seq#')
     A            OPTION         2A  B  6  1
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W6PO      R        O  6  5REFFLD(PDREC/PDPO *LIBL/PODTL)
     A            W6VAL1    R        O  6 16REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            W6DESC    R        O  6 33REFFLD(IXREC/IXDSC1 *LIBL/ITEMDSC)
     A            W6TXT2         4A  O  7 10
     A            W6VAL2    R        O  7 15REFFLD(PDREC/PDITEM *LIBL/PODTL)
     A            W6BRND    R        O  7 31REFFLD(IMREC/IMBRND *LIBL/ITEMMSC)
     A                                  7 42'Pack:'
     A            W6PDSC    R        O  7 48REFFLD(ITREC/ITPDSC *LIBL/PIRITEM)
     A            W6PSLT    R        O  7 64REFFLD(SLREC/SLDISP *LIBL/SLOT)
     A*
417a A          R DSPSFC6                   SFLCTL(DSPSFL6)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0004)
     A                                      WINDOW(WINDOW4)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  57                                  SFLEND(*MORE)
     A                                  2  2'Type option, press Enter.'
     A                                      COLOR(BLU)
     A                                  3  4'1=Select  X=Exception'
     A                                      COLOR(BLU)
     A                                  5  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W6TXT1        15   O  5 16
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 33'Description                       -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417e A*
417f A*
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC4                   SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL4                   SFLCTL(MSGREC4)
     A                                      WINDOW(WINDOW4)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR4
     A                                      WINDOW(WINDOW4)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
417cDA*                                     SLNO(06)
417cMA                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 41'Total pallet estimate:'
     A            WOTPLT        10Y 0O  1 64EDTCDE(2)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79A  O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R VERIFY
     A                                      WINDOW(10 20 5 20)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Are you sure you '
     A                                      COLOR(WHT)
     A                                  3  2'want to check as'
     A                                      COLOR(WHT)
     A                                  4  2'touched ?'
     A                                      COLOR(WHT)
     A            WOYES          1A  B  4 12COLOR(WHT)
     A                                      CHECK(ER)
