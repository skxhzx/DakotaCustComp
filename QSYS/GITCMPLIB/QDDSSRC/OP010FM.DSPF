     A*%%TS  SD  19940713  172602  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP010FM   PIR options - Select warehouse
     A*  13 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940713  172602  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Select Warehouse'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . .'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A            W1HEAD        60A  O  1 11DSPATR(HI)
     A            W1PGM          8A  O  1  2
     A            W1WHSE         3Y 0B  8 28
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
