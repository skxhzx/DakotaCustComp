     A*%%TS  SD  20070126  085849  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*
     A*  LOAD14FM Truck Loader - Close Route Pallet Loading Screen.
     A*  January 10, 2007
     A*  Jose Duque
     A*
     A*  Revisions:
     A*
650aAA*  01/19/15  RBD  6.50a
     A*    - Increased ldhcub field size from 5,2 to 7,3.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    ##/##/##  XXX-XXX
     A*      Text . . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF10
     A                                      CA12
     A                                      CF17
     A                                      CF18
     A          R TITLE
     A*%%TS  SD  20070126  085849  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'LOADR14.01'
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 24'Closed Routes Pallet Loading Detai-
     A                                      l'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A* Position to record
     A*
     A*----------------------------------------------------------------
     A          R POSTNREC
     A*%%TS  SD  20061206  164755  JOSED       REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 46'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 73EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 46'Position to pallet id . .'
     A  24        PTINP2         7Y 0B  3 73
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
     A  25        PTINP3         7Y 0B  3 73
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      CHECK(ER)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display
     A*
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20061207  082643  JOSED       REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LDHPLI         7Y 0O 11  7EDTCDE(Z)
     A            W1STAT         7A  O 11 16
     A            LDHTRK         6   O 11 25
     A            LDHLDS         2S 0O 11 34
     A            STOPR1         2A  O 11 38
     A                                 11 41'-'
     A            STOPR2         2A  O 11 43
     A            LDHPCS         5Y 0O 11 47EDTCDE(Z)
650aDA**          LDHCUB         5  2O 11 54EDTWRD('   .  ')
650aMA            LDHCUB         7  3O 11 54EDTWRD('    .   ')
650aDA**          LDHSTT         6Y 0O 11 62EDTWRD('  :  :  ')
650aMA            LDHSTT         6Y 0O 11 63EDTWRD('  :  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20061207  082643  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9  3
     A                                 10 33'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'Load'
     A                                      DSPATR(HI)
650aDA**                               10 62'Time    '
650aMA                                 10 63'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'PltId  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 16'Pallet'
     A                                      DSPATR(HI)
     A                                 10 16'Status '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aDA**                                9 62'Load'
650aMA                                  9 63'Load'
     A                                      DSPATR(HI)
650aMA                                 10 54'Cubes   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 38'Stops  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'Dept  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display - view 2
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R VW2SFL                    SFL
     A*%%TS  SD  20061107  105209  JOSED       REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LDHPLI         7Y 0O 11  7EDTCDE(Z)
     A            LDHTRK         6A  O 11 17
     A            LDHLDS         2S 0O 11 25
     A            STOPR1         2   O 11 30
     A            STOPR2         2   O 11 35
     A            LDHPCS         5Y 0O 11 40EDTCDE(Z)
650aDA**          LDHCUB         5Y 2O 11 48EDTWRD('   .  ')
650aMA            LDHCUB         7Y 3O 11 48EDTWRD('    .   ')
650aDA**          W1STRD         6Y 0O 11 56EDTCDE(Y)
650aMA            W1STRD         6Y 0O 11 57EDTCDE(Y)
     A            LDHSTT         6Y 0O 11 67EDTWRD('  :  :  ')
     A                                 11 33'-'
     A*
     A*----------------------------------------------------------------
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20060525  104406  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9  3
     A                                 10 24'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 24'Load'
     A                                      DSPATR(HI)
     A                                 10 48'Cubes   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 40'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aDA**                                9 56'Started'
650aMA                                  9 57'Started'
     A                                      DSPATR(HI)
650aDA**                               10 56'Date    '
650aMA                                 10 57'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 67'Started'
     A                                      DSPATR(HI)
     A                                 10 67'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Stops  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'PltId  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display - view 3
     A*
     A*----------------------------------------------------------------
     A          R VW3SFL                    SFL
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A*----------------------------------------------------------------
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
CBiaDA*                                     CF10
CBIaDA*                                     CF11
CBIaDA*                                     CF17
CBIaDA*                                     CF18
CBIaDA*                                     CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Error Message Subfile Records
     A*
     A*----------------------------------------------------------------
     A          R MSGREC                    SFL
     A*----------------------------------------------------------------
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*----------------------------------------------------------------
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R OPTREC
     A*%%TS  SD  20061206  162428  JOSED       REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 46'Rte#:'
     A                                      COLOR(YLW)
     A            OPTRTE         5A  O  1 54COLOR(YLW)
     A                                  1 62'Door#:'
     A                                      COLOR(YLW)
     A            OPTDOR         3   O  1 69COLOR(YLW)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A*    Command lines
     A*
     A*----------------------------------------------------------------
     A          R CMDREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A*----------------------------------------------------------------
     A          R MORREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A*    Delete display instructions
     A*
     A*----------------------------------------------------------------
     A          R DELREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A* Records to clear different portions of a panel
     A*
     A*----------------------------------------------------------------
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A*----------------------------------------------------------------
     A          R CLR03END
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*----------------------------------------------------------------
     A*
     A*    Clear lines between title and subfile list
     A*
     A*----------------------------------------------------------------
     A          R CLR0306
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*----------------------------------------------------------------
     A*
     A          R CMDDEL
     A*
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
