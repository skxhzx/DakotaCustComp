     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A* *** NOTE ***
     A* After compiling change the CPI setting to 13.1
     A*----------------------------------------------------------------
     A*
     A*  CK365PPR   Ship check list with Blanks - Print     Packers
     A*
     A*  Revisions
     A*    07/23/07  RH   5.10
     A*      - Created using CK365PR, previous comments remain
     A*
     A*    08/25/03  RH   4.17
     A*      - Created using SL401
     A*
PCKa A*    07/15/04  HNK  PCKa
     A*      - Remove Brand and add cust Ord #
     A*
PCKb A*    09/01/04  HNK  PCKb
     A*      - print * for catch weight items.
     A*      - print total quantity
     A*
PCKc F*    12/30/04  MLB  PCKc
     A*      - Print '*Start Tran*' every time transaction number
     F*        changes on report heading.
     A*      - Added new format ENDTRN to print when transaction
     A*        number is changing.
PCKdAA*  10/08/07  MLB  PCKd
     A*  - Revised to add SAP Delivery Number to new DETAIL1_PK format.
     A*
PCKeAA*    01/15/08  JCJ  PCKe
     A*      - Revised to add Lot code to DETAIL_LOT line
     A*
     A*----------------------------------------------------------------
     A*
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   115
     A                                      DFT('Page')
     A            PAGE           4S 0O   119
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   118
     A                                      DFT('CK365P')
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1DFT('ROUTE:')
     A            W1RTE          5A  O     8
     A                                    25DFT('TRANS:')
     A            W1TRN          7S  O    32
     A                                      EDTCDE(Z)
PCKcAA            W1SFLG        12A  O    40
PCKcDA*                                   50DFT('PALLET:')
PCKcMA                                    55DFT('PALLET:')
PCKcDA*           W1PLT          7S  O    58
PCKcMA            W1PLT          7S  O    63
     A                                      EDTCDE(Z)
     A                                    76DFT('DEPT:')
     A            W1DPT          5A  O    82
     A                                    96DFT('EMP:')
     A            W1EMP          5S  O   101
     A                                      EDTCDE(Z)
     A                                   109DFT('DOOR:')
     A            W1DOOR         5S  O   119
     A                                      EDTCDE(Z)
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               100
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    98
     A                                      DFT('Qty')
     A                                      UNDERLINE
     A                                   104
     A                                      DFT('Qty')
     A                                      UNDERLINE
     A                                   111
     A                                      DFT('Adjust')
     A                                      UNDERLINE
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    28
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    88
PCKa A                                      DFT('Order#')
PCKa A**                                    DFT('Brand')
     A                                      UNDERLINE
     A                                    98
     A                                      DFT('Order')
     A                                      UNDERLINE
     A                                   104
     A                                      DFT('Picked')
     A                                      UNDERLINE
     A                                   111
     A                                      DFT('Sum/Sub')
     A                                      UNDERLINE
     A                                   119
     A                                      DFT('Comments')
     A                                      UNDERLINE
     A*
PCKdAA          R COLUMNS_PK
PCKdAA                                      SPACEB(001)
PCKdAA                                      SPACEA(001)
PCKdAA                                    98
PCKdAA                                      DFT('Qty')
PCKdAA                                      UNDERLINE
PCKdAA                                   104
PCKdAA                                      DFT('Qty')
PCKdAA                                      UNDERLINE
PCKdAA                                   111
PCKdAA                                      DFT('Adjust')
PCKdAA                                      UNDERLINE
PCKdAA                                      SPACEA(001)
PCKdAA                                     1
PCKdAA                                      DFT('Slot')
PCKdAA                                      UNDERLINE
PCKdAA                                    14
PCKdAA                                      DFT('Item')
PCKdAA                                      UNDERLINE
PCKdAA                                    28
PCKdAA                                      DFT('Description')
PCKdAA                                      UNDERLINE
PCKdAA                                    88
PCKdAA                                      DFT('Delivery#')
PCKdAA                                      UNDERLINE
PCKdAA                                    98
PCKdAA                                      DFT('Order')
PCKdAA                                      UNDERLINE
PCKdAA                                   104
PCKdAA                                      DFT('Picked')
PCKdAA                                      UNDERLINE
PCKdAA                                   111
PCKdAA                                      DFT('Sum/Sub')
PCKdAA                                      UNDERLINE
PCKdAA                                   119
PCKdAA                                      DFT('Comments')
PCKdAA                                      UNDERLINE
PCKdAA*
     A*
     A* Print detail line
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            WBDISP    R        O     1
     A                                      REFFLD(WBDISP CK365)
     A  65                                  HIGHLIGHT
     A*           WBQPCK    R        O    14EDTCDE(3)
     A*                                     REFFLD(WBQPCK CK365)
     A*           WBUCOD    R        O    20
     A*                                     REFFLD(WBUCOD CK365)
     A            WBITEM    R        O    14
     A                                      REFFLD(WBITEM CK365)
     A  65                                  HIGHLIGHT
PCKb A            W1ASTR         1A  O    26
     A            W1DESC        60A  O    28
     A  65                                  HIGHLIGHT
PCKa A**          W1BRND    R        O   101
PCka A**                                    REFFLD(IMBRND ITEMMSC)
PCKa A            W1CORD    R        O    88
PCka A                                      REFFLD(OHCORD ORDH)
     A  65                                  HIGHLIGHT
     A            WBQALC    R        O    98EDTCDE(3)
     A                                      REFFLD(WBQALC CK365)
     A  65                                  HIGHLIGHT
     A            WBQPCK    R        O   105EDTCDE(3)
     A                                      REFFLD(WBQPCK CK365)
     A  65                                  HIGHLIGHT
     A            W1BLK1         7A  O   111
     A                                      UNDERLINE
     A            W1BLK2        14A  O   119
     A                                      UNDERLINE
     A*           W1PDSC    R        O   112
     A*                                     REFFLD(ITPDSC PIRITEM)
PCKdAA          R DETAIL_PK
PCKdAA                                      SPACEB(001)
PCKdAA            WBDISP    R        O     1
PCKdAA                                      REFFLD(WBDISP CK365)
PCKdAA  65                                  HIGHLIGHT
PCKdAA            WBITEM    R        O    14
PCKdAA                                      REFFLD(WBITEM CK365)
PCKdAA  65                                  HIGHLIGHT
PCKdAA            W1ASTR         1A  O    26
PCKdAA            W1DESC        60A  O    28
PCKdAA  65                                  HIGHLIGHT
PCKdAA            W1DLVY         9A  O    88
PCKdAA  65                                  HIGHLIGHT
PCKdAA            WBQALC    R        O    98EDTCDE(3)
PCKdAA                                      REFFLD(WBQALC CK365)
PCKdAA  65                                  HIGHLIGHT
PCKdAA            WBQPCK    R        O   105EDTCDE(3)
PCKdAA                                      REFFLD(WBQPCK CK365)
PCKdAA  65                                  HIGHLIGHT
PCKdAA            W1BLK1         7A  O   111
PCKdAA                                      UNDERLINE
PCKdAA            W1BLK2        14A  O   119
PCKdAA                                      UNDERLINE
PCKeAA          R DETAIL_LOT
PCKeAA                                      SPACEB(001)
PCKeAA                                    24
PCKeAA                                      DFT('Lot')
PCKeAA            W1LOTA    R        O    28
PCKeAA                                      REFFLD(ODLLOT ORDDL)
PCKeAA            W1LOTB    R        O    69
PCKeAA                                      REFFLD(ODLLOT ORDDL)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
PCKb A* Print total line
     A*
PCKb A          R TOTAL1
PCKb A                                      SPACEB(001)
PCKb A                                    87'Total:'
PCKb A            T#QALC         7  0O    96EDTCDE(3)
PCKb A            T#QPCK         7  0O   103EDTCDE(3)
     A*
PCKc A* Print End of Transaction message.
     A*
PCKc A          R ENDTRN
PCKc A                                      SPACEB(001)
PCKc A                                     4'* End of Transaction:'
PCKc A            HLDTRN         7S 0O    26EDTCDE(3)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('CK365P: End of Report')
     A*
