     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A********************************************************************
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  EX155FM      Catch weight exception report
     A*  13 September 2004
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
417 AA*     09/13/04  RH   4.17
     A*       - Created using EX125FM                                  and
     A*
520 AA*     03/24/09  CWM  5.20
     A*       - ENH: Added Report Distribution / Override Functionality
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'EX155.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 10 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'(Y=Yes, N=No)'
520 DA*                                11  4'Printer device . . . . . . .'
520 dA*           W1DEV         10A  B 11 34
520 dA*N26N90                               DSPATR(HI)
520 dA* 26                                  DSPATR(RI)
520 dA*N26N90                               DSPATR(UL)
520 dA* 06                                  DSPATR(PC)
520 dA* 90                                  DSPATR(PR)
520 dA*                                12  4'Form type  . . . . . . . . .'
520 dA*           W1FORM        10A  B 12 34
520 dA*N27N90                               DSPATR(HI)
520 dA* 27                                  DSPATR(RI)
520 dA*N27N90                               DSPATR(UL)
520 dA* 07                                  DSPATR(PC)
520 dA* 90                                  DSPATR(PR)
520 aA*
520 aA                                 13  4'Enter 1 Time Print Override:'
520 aA            W1POVR         1A  B 13 34
520 aA N30N90                               DSPATR(HI)
520 aA  30                                  DSPATR(RI)
520 aA N30N90                               DSPATR(UL)
520 aA  30                                  DSPATR(PC)
520 aA  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'EX155.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Item . . . . . . . . . . . .'
     A            W2ITEM        15A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 50'(Item # or *ALL)'
     A                                 11  4'Date range . . . . . . . . .'
     A            W2FMDT         6Y 0B 11 34EDTCDE(4)
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 42'to'
     A            W2TODT         6Y 0B 11 46EDTCDE(4)
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'MMDDYY'
     A                                 12  4'Include status . . . . . . .'
     A            W2STAT         1A  B 12 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 50'(O=Open, C=Closed, B=Both)'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
