     A*%%TS  SD  20061208  120440  JOSED       REL-V5R3M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  LOADR10FM    RF Loader Module - Clear Flag.
     A*  December 14, 2006
     A*  Jose G. Duque
     A*
     A********************************************************************
     A*  Revisions:
     A*
520 AA*  09/23/09  RBD  520  P#00221
     A*    - Added F8=Return functionality to be used if LOADR10
     A*      is called from RU310.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A********************************************************************
     A*         Enter Route # Screen
     A********************************************************************
     A          R SCREEN1
     A*%%TS  SD  20061205  093435  JOSED       REL-V5R3M0  5722-WDS
     A                                      CA03
520 AA                                      CA08
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(11)
     A                                      OVERLAY
     A                                  1  2'LOADR10.01'
     A                                  1 13'Clr Flag'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  3'Label #:'
     A            W1LBL#         7Y 0B  5 14EDTCDE(Z)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
520 DA**                               10  1'F3=Exit/Signoff'
520 MA            W1FKEY        15   O 10  1
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A*>>>                                  SFLMSGRCD(8)
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A*>>>                                  SLNO(8)
     A                                      SLNO(12)
     A                                      CLRL(1)
