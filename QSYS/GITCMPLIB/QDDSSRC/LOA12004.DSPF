     A*%%TS  SD  20150305  162041  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LOA120FM   LOADER - options
     A*  09 January 2009
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    01/09/09  RBD  5.20
     A*      - Created.
     A*
650a A*    04/09/14  GJA  6.50a
     A*      - Added voice loader options:
     A*          Loading order
     A*          Speak trailer ID
     A*          Speak row/column
     A*          Verify pallet position
     A*
650b A*    03/05/15  RBD  6.50b
     A*      - Added OPVFYP option, verify pallet label against
     A*        pick label.
     A*
700a A*    02/17/16  RBD  7.00a
     A*      - Added OPADDO option, auto define add-on routes Y/N.
     A*
700bAA*    08/23/16  RBD  7.00b
     A*      - Added OPEQIP option, Y/N. If Y call Load Equip program
     A*        from Merged Pallet Loading program.
     A*      - Added OPBYPL option, Y/N. If Y bypass the entry of quantity
     A*        when loading equipment in pgm EQ480.
     A*      - Added OPBYPR option, Y/N. If Y bypass the entry of quantity
     A*        when returning equipment in pgm EQ485.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20150305  162041  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'LOA120.01'
     A                                  1 31'LOADER - Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Earliest route loading start time -
     A                                      ...'
     A            W1STTM         6Y 0B  6 42CHECK(RZ)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6 50'(hhmmss)'
     A                                  7  4'Latest route loading end time ....-
     A                                      ...'
     A            W1ENTM         6Y 0B  7 42CHECK(RZ)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7 50'(hhmmss)'
     A                                  8  4'Loading order ....................-
     A                                      ...'
     A            W1LDOR         1A  B  8 42
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 50'0=Any, 1=Std, 2=Row/Col, 3=Col'
     A                                  9  4'Speak trailer ID to user .........-
     A                                      ...'
     A            W1TLID         1A  B  9 42
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 50'Y=Yes, N=No'
     A                                 10  4'Speak row/column to user..........-
     A                                      ...'
     A            W1RWCL         1A  B 10 42
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 50'Y=Yes, N=No'
     A                                 11  4'Verify pallet position on trailer.-
     A                                      ...'
     A            W1VYPL         1A  B 11 42
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 50'Y=Yes, N=No'
     A                                 12  4'Verify pallet lbl against pick lbl-
     A                                      ...'
     A            W1VFYP         1A  B 12 42
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 50'Y=Yes, N=No'
700aAA                                 13  4'Auto define add-on routes ........-
700aAA                                      ...'
700aAA            W1ADDO         1A  B 13 42
700aAA  28                                  DSPATR(RI)
700aAA N28N90                               DSPATR(UL)
700aAA  08                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                 13 50'Y=Yes, N=No'
700bAA                                 14  4'Load Equip by zone ...............-
700bAA                                      ...'
700bAA            W1EQIP         1A  B 14 42
700bAA  29                                  DSPATR(RI)
700bAA N29N90                               DSPATR(UL)
700bAA  09                                  DSPATR(PC)
700bAA  90                                  DSPATR(PR)
700bAA                                 14 50'Y=Yes, N=No'
700bAA                                 15  4'Bypass qty loading equipment .....-
700bAA                                      ...'
700bAA            W1BYPL         1A  B 15 42
700bAA  30                                  DSPATR(RI)
700bAA N30N90                               DSPATR(UL)
700bAA  10                                  DSPATR(PC)
700bAA  90                                  DSPATR(PR)
700bAA                                 15 50'Y=Yes, N=No'
700bAA                                 16  4'Bypass qty returning equipment ...-
700bAA                                      ...'
700bAA            W1BYPR         1A  B 16 42
700bAA  31                                  DSPATR(RI)
700bAA N31N90                               DSPATR(UL)
700bAA  11                                  DSPATR(PC)
700bAA  90                                  DSPATR(PR)
700bAA                                 16 50'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
