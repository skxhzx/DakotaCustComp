     A*%%***********************************************************************
     A*%%TS  RD  20011003  140656  DAVE        REL-V4R2M0  5769-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A****************************************************************
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  #8-51                                    *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A* IT359PR      Department Inventory Activity report
     A* 07 May 2007
     A* Retha Davis
     A*
     A*  Revisions:
     A*
     A*    05/07/07  RBD  5.10
     A*      - Created
     A*
     A********************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      'IT359'
     A                                      SPACEA(001)
     A            FMDT           6S 0O    55
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      'thru'
     A            TODT           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                      SPACEA(002)
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     7
     A                                      'Department'
     A                                    22
     A                                      'Receiving'
     A                                    38
     A                                      'Picking'
     A                                    48
     A                                      'Adjustments'
     A                                      SPACEA(001)
     A                                     7
     A                                      '----------'
     A                                    22
     A                                      '---------'
     A                                    38
     A                                      '-------'
     A                                    48
     A                                      '-----------'
     A*
     A* Print total line
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            LBWHDP    R        O    10
     A                                      REFFLD(LBWHDP LABEL)
     A            RCVQTY         8  0O    20
     A                                      EDTCDE(K)
     A            PCKQTY         8  0O    34
     A                                      EDTCDE(K)
     A            ADJQTY         8  0O    48
     A                                      EDTCDE(K)
     A*
     A* Print LR totals
     A*
     A          R TOTAL9
     A                                      SPACEB(002)
     A                                     1
     A                                      '** Grand Totals **'
     A            RCVL9          8  0O    20
     A                                      EDTCDE(K)
     A            PCKL9          8  0O    34
     A                                      EDTCDE(K)
     A            ADJL9          8  0O    48
     A                                      EDTCDE(K)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                     1
     A                                      'IT359: End of Report '
     A*
