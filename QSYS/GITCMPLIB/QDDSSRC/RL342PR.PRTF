     A*%%***********************************************************************
     A*
     A*   RL342PR  Print Letdown/Replenishment transactions by Slot
     A*   22 April, 2004
     A*   Roxanne Harrison
     A*
     A*  Revisions
520aAA*    10/20/08  RH   5.20a
     A*      - ENH: Add item # to report.
     A*
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                    85DFT('Repl:')
     A            REPL#          3S 0O    91
     A                                      EDTCDE(Z)
     A                                   125
     A                                      DFT('RL342')
     A                                      SPACEA(001)
     A*
     A* Print employee number and name
     A*
     A*         R EPHEAD
     A*                                     SPACEB(001)
     A*                                     SPACEA(002)
     A*                                    1'Employee:'
     A*           PTEMP#    R             +1REFFLD(PTEMP# RL342)
     A*                                     EDTCDE(Z)
     A*           WKNAME        40        +1
     A* 92                                75
     A*                                     '(Continued)'
     A* Warehouse department
     A*
     A          R DPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Dept:'
     A            WKWHDP    R             +1REFFLD(WKWHDP RL342)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
416 AA                                    10
416 AA                                      DFT('To slot    ')
416 AA                                      UNDERLINE
416 AA                                    23
416 AA                                      DFT('From slot  ')
416 AA                                      UNDERLINE
416 AA                                    36
416 AA                                      DFT('Qty  ')
416 AA                                      UNDERLINE
416 AA                                    42
416 AA                                      DFT('UOM  ')
416 AA                                      UNDERLINE
416 MA                                    49
     A                                      DFT('Start Dt')
     A                                      UNDERLINE
416 MA                                    59
     A                                      DFT('Start Tm')
     A                                      UNDERLINE
416 MA                                    69
     A                                      DFT('End Date')
413 AA                                      UNDERLINE
416 MA                                    79
     A                                      DFT('End Time')
     A                                      UNDERLINE
416 MA                                    89
     A                                      DFT('Task')
     A                                      UNDERLINE
416 AA                                    97
416 AA                                      DFT('Minutes')
416 AA                                      UNDERLINE
416 AA                                   107
416 AA                                      DFT('Emp #')
416 AA                                      UNDERLINE
520aAA                                   114
520aAA                                      DFT('Item#')
520aAA                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
     A                                      REFFLD(PTTRN# *LIBL/RL341)
416 AA            LBDISP    R        O    23REFFLD(LBDISP *LIBL/RL341)
416 AA            LBDSP2    R        O    10REFFLD(LBDSP2 *LIBL/RL341)
416 AA            LBQPCK    R        O    36EDTCDE(3)
416 AA                                      REFFLD(LBQPCK *LIBL/RL341)
416 AA            LBUCOD    R        O    42
416 AA                                      REFFLD(LBUCOD *LIBL/RL341)
416 MA            W1SDTE         6  0O    49EDTCDE(Y)
416 MA            W1STIM         6  0O    59EDTWRD('  :  :  ')
416 MA            W1EDTE         6  0O    69EDTCDE(Y)
416 MA            W1ETIM         6  0O    79EDTWRD('  :  :  ')
416 MA            PTTASK    R        O    89REFFLD(PTTASK *LIBL/RL341)
416 AA            PTDMIN    R        O    97EDTCDE(3)
416 AA                                      REFFLD(PTDMIN *LIBL/RL341)
xxx  A            PTEMP#    R            107REFFLD(PTEMP# RL341)
     A                                      EDTCDE(Z)
520aAA            LBITEM    R        O   114REFFLD(LBITEM *LIBL/RL341)
     A*           WKNAME        40        +1
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Total transactions:')
     A            TRNCNT         5  0O    25EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('RL342: End of Report')
     A*
