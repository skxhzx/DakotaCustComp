     A*
     A*   IT430PR  Fixed Items with Multiple Pick Slots
     A*   27 August, 1997
     A*   Hemant Kapadia
     A*
     F*   Revisions
510a F*    01/30/07  RH   5.10a
     F*      - Enh: Add slot reserved field.
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(2)
     A*
     A            UDATE          6S 0      3EDTCDE(Y)
     A                                    50'Fixed Items with Multiple Pick Slo-
     A                                      ts'
     A                                   122'Page'
     A            PAGE           4S 0    126EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A            TIME           6S 0      3EDTWRD('  :  :  ')
     A*
     A            WOWHSE        60A       37
     A                                   125'IT431'
     A                                      SPACEA(2)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A*                                   67'Available'
     A*                                     SPACEA(1)
     A*
     A                                     1'Item Number'
     A                                      UNDERLINE
     A                                    19'Pack'
     A                                      UNDERLINE
     A                                    33'Description'
     A                                      UNDERLINE
     A                                    67'Slot'
     A                                      UNDERLINE
     A                                    82'Quantity '
     A                                      UNDERLINE
     A                                    93'Type'
     A                                      UNDERLINE
     A                                    99'Tie'
     A                                      UNDERLINE
     A                                   103'High'
     A                                      UNDERLINE
     A                                   109'Designation'
     A                                      UNDERLINE
510a A                                   122'Reserved'
510a A                                      UNDERLINE
     A*
     A                                      SPACEA(1)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A  61        SLITEM    R              1REFFLD(SLITEM SLOT)
     A  61        ITPDSC    R             19REFFLD(ITPDSC PIRITEM)
     A  61        ITDESC    R             33REFFLD(ITDESC PIRITEM)
     A            SLDISP        12A       67
     A            W1QTY         10A       82
     A            SLSDEF         2A       94
     A            IDTIE          4S 0     98EDTCDE(L)
     A            IDHIGH         2S 0    104EDTCDE(L)
     A  10                               107'*'
     A            SLDESG         5A      110
510a A            W1RSRV         1A      122
     A*
     A                                      SPACEA(1)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1                     TEXT('SKIP ONE LINE')
     A                                     1' '
     A                                      SPACEA(1)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                     1'IT431: End of Report'
     A                                      SPACEA(1)
     A*
