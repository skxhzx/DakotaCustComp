     A*%%TS  SD  20120912  170043  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  RH182FM   View order details - Add
     A*  09 December 2004
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
500 AA*    12/09/04  DAS  5.00
     A*      - Created.
510 AA*    06/25/07  RBD  5.10
     A*      - Eliminate hardcoding uom's in RH182 for normal qty
     A*        ordered, breakdown 1 qty ordered, and breakdown 2 qty
     A*        ordered by using headings to identify them as such.
520 AA*    02/11/09  RBD  5.20
     A*      - Disable F9=Pick slot for item function if client is
     A*        outbound only (same as stand alone).
640 AA*    09/12/12  RBD  6.40
     A*      - Changed heading to 'Item Substitute'.
640aAA*    10/24/12  RBD  6.40a
     A*      - Added 8 x 20 screen.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20120912  170043  RETHA       REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'RH182.01'
640 AA                                  1 32'Item Substitute'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Route    :'
     A            RHRTE          5A  O  5 13
     A                                  5 20'Id:'
     A            RHRTID         5A  O  5 24
     A                                  5 31'Status:'
     A            RHSTAT         1A  O  5 39
     A                                  5 41'-'
     A            WOSTAT         7A  O  5 43
     A                                  6  2'Order No.:'
     A            $PCORD         7Y 0O  6 13EDTCDE(4)
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Sequence number  . . . . .'
     A            W1SEQ          5Y 0B 10 32
     A N21N90N91                            DSPATR(UL)
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 11  4'Item . . . . . . . . . . .'
     A            W1ITM         15A  B 11 32
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                 12  4'Original item  . . . . . .'
     A            W1OIT         15A  B 12 32
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Quantity ordered . . . . .'
     A            W1QOR1         5Y 0B 13 32EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510 DA**          W1UM1          2A  O 13 38
     A                                 13 38'Normal'
     A            W1QOR2         5Y 0B 13 46EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  92
     AO 90                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
510 DA**          W1UM2          2A  O 13 48
     A                                 13 52'BrkDwn 1'
     A  92                                  DSPATR(ND)
640iDA**          W1QOR3         5Y 0B 13 62EDTCDE(4)
640iMA            W1QOR3         4Y 0B 13 62EDTCDE(4)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  93
     AO 90                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
510 DA**          W1UM3          2A  O 13 58
     A                                 13 68'BrkDwn 2'
     A  93                                  DSPATR(ND)
     A                                 15  4'Slot to get item from  . .'
     A  94                                  DSPATR(ND)
     A            W1DISP        12A  B 15 32
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A                                 15 58'F9=Pick slot for item'
     A  94
     AO 96                                  DSPATR(ND)
     A                                 16  4'Pallet to put item on  . .'
     A  94                                  DSPATR(ND)
     A                                 16 32'Row:'
     A  94                                  DSPATR(ND)
     A            W1ROW          2Y 0B 16 37EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A                                 16 42'Column:'
     A  94                                  DSPATR(ND)
     A            W1COL          1Y 0B 16 50EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  94
     AO 90                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A                                 16 58'F9=Pallet for orig item'
     A  94                                  DSPATR(ND)
     A                                 18  4'Label printer device . . .'
     A  95                                  DSPATR(ND)
     A            W1DEV         10A  B 18 32
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  95
     AO 90                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 18 58'F9=Device for pallet'
     A  95                                  DSPATR(ND)
     A                                 23  2'F4=Prompt  F5=Refresh  F9=Get  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
640aAA          R SCREEN81
640aAA                                      CF04
640aAA                                      CF09
640aAA  98                                  ALARM
640aAA                                      CLRL(7)
640aAA                                      OVERLAY
640aAA                                  1  2'Item Sub'
640aAA                                      DSPATR(HI)
640aAA                                  1 12'Seq'
640aAA            W1SEQ          5Y 0B  1 16
640aAA N21N90N91                            DSPATR(UL)
640aAA  21                                  DSPATR(RI)
640aAA  01                                  DSPATR(PC)
640aAA  90
640aAAO 91                                  DSPATR(PR)
640aAA                                      EDTCDE(3)
640aAA                                  2  2'Sub'
640aAA            W1ITM         15A  B  2  6
640aAA  22                                  DSPATR(RI)
640aAA  02                                  DSPATR(PC)
640aAA  90
640aAAO 91                                  DSPATR(PR)
640aAA N22N90N91                            DSPATR(UL)
640aAA                                  3  2'Org'
640aAA            W1OIT         15A  B  3  6
640aAA  23                                  DSPATR(RI)
640aAA N23N90                               DSPATR(UL)
640aAA  03                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
640aAA                                  4  2'Qty:Norm   Bk1  Bk2'
640aAA            W1QOR1         5Y 0B  5  5EDTCDE(4)
640aAA  24                                  DSPATR(RI)
640aAA N24N90                               DSPATR(UL)
640aAA  04                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
640aAA            W1QOR2         5Y 0B  5 11EDTCDE(4)
640aAA  25                                  DSPATR(RI)
640aAA N25N90                               DSPATR(UL)
640aAA  05                                  DSPATR(PC)
640aAA  92
640aAAO 90                                  DSPATR(PR)
640aAA  92                                  DSPATR(ND)
640aAA            W1QOR3         4Y 0B  5 17EDTCDE(4)
640aAA  26                                  DSPATR(RI)
640aAA N26N90                               DSPATR(UL)
640aAA  06                                  DSPATR(PC)
640aAA  93
640aAAO 90                                  DSPATR(PR)
640aAA  93                                  DSPATR(ND)
640aAA                                  6  2'Slot'
640aAA  94                                  DSPATR(ND)
640aAA            W1DISP        12A  B  6  7
640aAA  27                                  DSPATR(RI)
640aAA N27N90                               DSPATR(UL)
640aAA  07                                  DSPATR(PC)
640aAA  94
640aAAO 90                                  DSPATR(PR)
640aAA  94                                  DSPATR(ND)
640aAA                                  7  2'Row'
640aAA  94                                  DSPATR(ND)
640aAA            W1ROW          2Y 0B  7  6EDTCDE(4)
640aAA  28                                  DSPATR(RI)
640aAA N28N90                               DSPATR(UL)
640aAA  08                                  DSPATR(PC)
640aAA  94
640aAAO 90                                  DSPATR(PR)
640aAA  94                                  DSPATR(ND)
640aAA                                  7  9'Col'
640aAA  94                                  DSPATR(ND)
640aAA            W1COL          1Y 0B  7 13EDTCDE(4)
640aAA  28                                  DSPATR(RI)
640aAA N28N90                               DSPATR(UL)
640aAA  94
640aAAO 90                                  DSPATR(PR)
640aAA  94                                  DSPATR(ND)
640aAA                                  7 15'F9=Get'
640aAA                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
      *
640aAA*****************************************************************
640aAA*
640aAA* 8 LINE RF SCREEN
640aAA*
640aAA* ERROR MESSAGE SUBFILE RECORDS
640aAA*
640aAA*    ERROR MESSAGE SUBFILE RECORD
640aAA*
640aAA          R M08REC                    SFL
640aAA                                      SFLMSGRCD(8)
640aAA            #MSGK                     SFLMSGKEY
640aAA            #PGMQ                     SFLPGMQ
640aAA*
640aAA*    ERROR MESSAGE SUBFILE CONTROL RECORD
640aAA*
640aAA          R M08CTL                    SFLCTL(M08REC)
640aAA  98                                  ALARM
640aAA                                      LOCK
640aAA                                      OVERLAY
640aAA                                      SFLSIZ(2) SFLPAG(1)
640aAA N97                                  SFLCLR
640aAA  97                                  SFLDSP
640aAA  97                                  SFLEND
640aAA          R M08CLR
640aAA                                      OVERLAY
640aAA                                      LOCK
640aAA                                      SLNO(8)
640aAA                                      CLRL(1)
