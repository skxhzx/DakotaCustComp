     A*%%TS  SD  20050331  100815  DAVE        REL-V5R3M0  5722-WDS
     A**************************************************************************
     A*  IT352FM   Inventory transfer -  RF Mini Screen
     A*
     A*  03/29/00  HNK  4.13
     A*    Created - Mirror of IT252FM except for screen size.
     A*              More fields displayed and command keys added to
     A*              this screens because of availability of space
     A*              on the screen than IT252FM.
     A*
     A*  Revisions:
     A*
     A*    11/25/99  DAS  4.13
     A*     - Moved lines down to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
413 AA*    02/07/00  HNK  4.13
     A*     - Screen 1 Slot number removed. Item number added instead.
     A*     - Screen 2 added for slot number input.
416CAA*    04/16/02  RH   4.16C
     A*     - Screen 2 adjustment code added if item # changed.
     A*
500 AA*    03/31/05  DAS  5.00
     A*     - Added slot position to screens.
     A*     - Enh: Add cw field to screen 2 if item changed and is cw.
530aAA*   10/22/09  RH   5.30a
     A*     - Renamed to IT35201.
530bAA*   01/27/10  GJA  5.30b
     A*     - Renamed to IT35202.
     A*     - Enh: Add pick slot to screen 2
     A*
700a A*   05/05/15  LMC  7.00a
     A*     - Enh: Rename to IT35203
     A*     - Enh: Added the ability to protect normal qty entry
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20050331  100815  DAVE        REL-V5R3M0  5722-WDS
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT352.01'
     A                                  1 33'Inventory Transfer'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Warehouse  :'
     A            WOWHSE         3A  O  4 15
     A            WHDESC        30A  O  4 22
     A                                  5  2'Slot Dept. :'
     A            WDCODE         5A  O  5 15
     A            WDDESC        30A  O  5 22
     A                                  6  2'From Slot  :'
     A            FMDISP        12A  O  6 15
     A            W1SLP          8   O  6 29
     A                                  7  2'Slot type  :'
     A            W1STYP         1A  O  7 15
     A                                  8  2'Status     :'
     A            SSDESC        30A  O  8 15
     A                                  9  2'Item:'
     A            W1ITEM        55A  O  9  8
     A            W1LBLS        12A  O 10  2DSPATR(HI)
     A            W1ITMS        55A  O 10 15DSPATR(HI)
     A                                 11  2'Available quantity:'
     A            W1CURQ        30A  O 11 22
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  2'Qty:'
     A            W1QT01         5Y 0B 16  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A            W1UM1          5A  O 16 14
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 16 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 16 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 16 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 16 36
     A  93                                  DSPATR(ND)
     A                                 17  2'Item:'
     A            W1ITM         15A  B 17  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 23  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20050331  100815  DAVE        REL-V5R3M0  5722-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF20
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT352.02'
     A                                  1 32'Inventory Transfer'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Warehouse  :'
     A            WOWHSE         3A  O  4 15
     A            WHDESC        30A  O  4 22
     A                                  5  2'Slot Dept. :'
     A            WDCODE         5A  O  5 15
     A            WDDESC        30A  O  5 22
     A                                  6  2'From Slot  :'
     A            FMDISP        12A  O  6 15
     A            W1SLP          8   O  6 29
530bAA                                  6 50'Pick Slot  :'
530bAA            W1PSLT        12A  O  6 63
     A                                  7  2'Slot type  :'
     A            W1STYP         1A  O  7 15
     A                                  8  2'Status     :'
     A            SSDESC        30A  O  8 15
     A                                  9  2'Item:'
     A            W2ITEM        55A  O  9  8
     A            W2LBLS        12A  O 10  2DSPATR(HI)
     A            W2ITMS        55A  O 10 15DSPATR(HI)
     A                                 11  2'Qty being transferred:'
     A            W2CURQ        30A  O 11 25
     A                                 13  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 15  2'Adjustment code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 15 19DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                 15 22'Catch Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B 15 32DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
     A                                 16  2'To slot:'
     A            W2DISP        12A  B 16 11DSPATR(UL)
     A  01N59                               DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 22  2'F4=Prompt  F5=Refresh  F8=View "To-
     A                                      " item/slot info.  F12=Cancel   '
     A                                      COLOR(BLU)
     A                                 23  2'F20=Accept warning  F21=Tie/High m-
     A                                      aintenance        '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRBTM
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(16)
     A          R CLRSCR
     A                                      CLRL(*ALL)
