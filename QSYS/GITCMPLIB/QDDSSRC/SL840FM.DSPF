     A*%%TS  SD  20040311  175554  BFC         REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  SL840FM   Display open labels for slot - Display
     A*  11 March 2004
     A*  Roxanne Harrison
     A*
     A*****************************************************************
     A*  Revisions:
     A*
417 AA*    03/11/04  RH   4.17
     A*      - Created.
417aAA*    03/16/04  MLB  4.17a
     A*      - Enh: Added customer route/order number to display.
     A*        Moved route/order id's to View 3.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010424  093754  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'SL840.01'
     A                                  1 26'Display Open Labels for Slot'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WODISP        22A  O  2 30DSPATR(HI)
417  A**          WOTYPE         6A  O  2 72DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20010418  170904  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A                                  2 48'Position to label . . .'
     A            PTINP1         7Y 0B  2 73EDTCDE(4)
     A            PTDMY2         1   O  3  2
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
414 DA* 85                                  SFLNXTCHG
     A          R DSPSFL                    SFL
     A*%%TS  SD  20040311  175554  BFC         REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LBLBL#    R        O 11  7REFFLD(LBREC/LBLBL# *LIBL/LABEL)
     A                                      EDTCDE(3)
     A*           LBSTAT    R        O 11 16REFFLD(LBREC/LBSTAT *LIBL/LABEL)
     A            W1LSTA         7A  O 11 15
     A            W1SDTE         6Y 0O 11 24EDTCDE(Y)
     A            W1TYPE        10A  O 11 34
417aDA*           LBRTE     R        O 11 46REFFLD(LBREC/LBRTE *LIBL/LABEL)
417aMA            RHRTE     R        O 11 46REFFLD(RHREC/RHRTE *LIBL/RTEHED)
417aDA*           LBORD     R        O 11 53REFFLD(LBREC/LBORD *LIBL/LABEL)
417aMA            OHCORD    R        O 11 53REFFLD(OHREC/OHCORD *LIBL/ORDH)
     A            LBPO      R        O 11 62REFFLD(LBREC/LBPO *LIBL/LABEL)
     A            LBQPCK    R        O 11 73REFFLD(LBREC/LBQPCK *LIBL/LABEL)
     A                                      EDTCDE(O)
     A*           LBITEM    R        O 11 38REFFLD(LBREC/LBITEM *LIBL/LABEL)
     A*           LBDISP    R        O 11 54REFFLD(LBREC/LBDISP *LIBL/LABEL)
     A*           LBDSP2    R        O 11 67REFFLD(LBREC/LBDSP2 *LIBL/LABEL)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20040311  175554  BFC         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
417aAA                                  9 45'   Customer    '
417aAA                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 15'Status '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Type      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417aDA*                                10 45'RouteId'
417aMA                                 10 45'Route  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'PO       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Quantity'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
414 DA* 85                                  SFLNXTCHG
     A          R VW2SFL                    SFL
     A*%%TS  SD  20010417  154243  DAVE3       REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LBLBL#    R        O 11  7REFFLD(LBREC/LBLBL# *LIBL/LABEL)
     A                                      EDTCDE(3)
     A*           LBITEM    R        O 11 38REFFLD(LBREC/LBITEM *LIBL/LABEL)
     A            LBDISP    R        O 11 16REFFLD(LBREC/LBDISP *LIBL/LABEL)
     A            LBDSP2    R        O 11 30REFFLD(LBREC/LBDSP2 *LIBL/LABEL)
     A            LBTRN#    R        O 11 44REFFLD(LBREC/LBTRN# *LIBL/LABEL)
     A                                      EDTCDE(3)
     A            W1TSTA         7A  O 11 53
     A            W1TDTE         6Y 0O 11 68EDTCDE(Y)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20040311  165708  BFC         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A*                                10  2'Opt'
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 16'Slot 1      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Slot 2      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 44'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Trans Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LBLBL#    R        O 11  7REFFLD(LBREC/LBLBL# *LIBL/LABEL)
     A                                      EDTCDE(3)
417aDA*           LBITEM    R        O 11 16REFFLD(LBREC/LBITEM *LIBL/LABEL)
417aMA            LBITEM    R        O 11 15REFFLD(LBREC/LBITEM *LIBL/LABEL)
417aAA            LBRTE     R        O 11 32REFFLD(LBREC/LBRTE *LIBL/LABEL)
417aAA            LBORD     R        O 11 39REFFLD(LBREC/LBORD *LIBL/LABEL)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Label# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 15'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417aAA                                 10 31'RouteId'
417aAA                                      DSPATR(HI)
417aAA                                      DSPATR(UL)
417aAA                                 10 39'OrderId'
417aAA                                      DSPATR(HI)
417aAA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Pick & Print.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
