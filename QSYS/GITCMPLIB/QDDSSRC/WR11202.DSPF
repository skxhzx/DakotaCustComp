     A*%%TS  SD  20170831  173025  KENNETHE    REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR112FM    Repack work orders
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    03/02/07  DAS  5.10
     A*      - Created
510aAA*    04/02/07  TJ   5.10a
     A*      - Change headings and add Date Needed By field
     A*      - Add detail of quantity already built and rearrange sc1
530 AA*    03/18/10  JCJ  5.30
     A*      - Change W1Made field size from 4.0 to 3.0.
     A*      - Change W2Qty  field size from 4.0 to 3.0.
     A*      - Moved reference field on screen 2 to last line.
530aAA*    03/29/10  JCJ  5.30a
     A*      - Add expiration date to screen 2.
610aAA*    05/03/10  JCJ  6.10a
     A*      - Add F20=Accept warning.
610bAA*    07/06/10  JCJ  6.10b
     A*      - Non display expiration date if repack item is not date
     A*        sensative.
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF08
     A                                      CF09
     A                                      CA12
     A          R SCREEN2
     A*%%TS  SD  20170831  173025  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'WR112.02'
510aDA***                               1 19'Inventory Adjustment Code Table Ma-
510aDA***                                   intenance'
     A                                  1 28'Repack Work Orders - Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Add Work Order'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  3'Repack item . . . . .'
     A            W2ITEM        15A  B  8 26
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  3'Quantity needed . . .'
530 DA***         W2QTY          5Y 0B  9 26EDTCDE(4)
     A            W2QTY          3Y 0B  9 26EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  3'Reference . . . . . .'
     A            W2REF         15A  B 11 26
     A                                 10  3'Date Needed By  . . .'
     A            W2NDT          6Y 0B 10 26EDTCDE(Z)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 42'(MMDDYY)'
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel -
     A                                       F20=Accept warning'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20130227  153007  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  20130227  153007  KENNETHE    REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R DETAIL1                   SFL
     A*%%TS  SD  20170831  173025  KENNETHE    REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            W1SQTY1        5Y 0O 17 21EDTCDE(Z)
     A            W1SUM1         2A  O 17 27
     A            W1SQTY2        3Y 0O 17 32EDTCDE(Z)
     A            W1SUM2         2A  O 17 36
     A            W1SQTY3        3Y 0O 17 41EDTCDE(Z)
     A            W1SUM3         2A  O 17 45
     A            W1SWQ1         5Y 0B 17 50EDTCDE(Z)
     A  92                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A            W1SWQ2         3Y 0B 17 62EDTCDE(Z)
     A  93                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1SWQ3         3Y 0B 17 72EDTCDE(Z)
     A  94                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1SWU1         2A  O 17 56
     A            W1SWU2         2A  O 17 66
     A            W1SWU3         2A  O 17 76
     A            W1SITM2       15A  O 17  3
     A  21                                  DSPATR(RI)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20170831  173025  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      CF20
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0005)
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'WR112.01'
     A                                  1 28'Repack Work Orders - Entry'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 38'Build'
     A                                      DSPATR(HI)
     A                                  3  3'Work order       :'
     A            WOWORD         7Y 0O  3 22EDTCDE(4)
     A                                  4  3'Repack item      :'
     A            WORITM        50A  O  4 22
     A            WORQN          3Y 0O  8 30EDTCDE(4)
     A            WORQP          3Y 0O  9 30EDTCDE(4)
     A            W1MADE         3Y 0B 10 30EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  3'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  3'Output qty needed. . . . .'
     A                                  9  3'Previously made. . . . . .'
     A                                 10  3'Output qty made. . . . . .'
     A                                 11  3'License plate. . . . . . .'
     A                                 12  3'Lot Number . . . . . . . .'
     A  95                                  DSPATR(ND)
     A            W1LIC         15A  B 11 30
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A            W1LOT         30A  B 12 30COLOR(WHT)
     A  95                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A                                 16  3'Input Item'
     A                                      COLOR(WHT)
     A                                 16 22'Normal'
     A                                      COLOR(WHT)
     A                                 16 41'Brk 2'
     A                                      COLOR(WHT)
     A                                 16 51'Normal'
     A                                      COLOR(WHT)
     A                                 16 62'Brk 1'
     A                                      COLOR(WHT)
     A                                 16 72'Brk 2'
     A                                      COLOR(WHT)
     A                                 16 32'Brk 1'
     A                                      COLOR(WHT)
     A                                 15 22'-- Quantity Required --'
     A                                      COLOR(WHT)
     A                                 15 52'---- Waste Quantity ----'
     A                                      COLOR(WHT)
     A                                 13  3'Expiration Date. . . . . .'
     A  70                                  DSPATR(ND)
     A            W2EDT          6Y 0B 13 30
     A  25                                  DSPATR(RI)
     A  70                                  DSPATR(ND)
     A  25                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A          R CMDREC
     A*%%TS  SD  20130227  153007  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F5-Refresh   F12-Cancel'
     A                                      COLOR(BLU)
     A          R MORREC
     A*%%TS  SD  20130301  160620  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7   O  1 71COLOR(WHT)
