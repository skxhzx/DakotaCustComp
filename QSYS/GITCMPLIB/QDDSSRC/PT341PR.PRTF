     A*%%***********************************************************************
     A*
     A*   PT341PR  Print Open Return transactions
     A*   13 November, 2002
     A*   Roxanne Harrison
     A*
416 AA*    11/13/02  RH   4.16
     A*  - Created
     A*
417 AF*    05/16/03  MLB  4.17
     F*      - Enh: Revised Detail 1 to support extended license plate
     A*        field.
640aAF*    05/29/12  LMC  6.40a
     F*      - Enh: Added the W1UCOD to printout after the w1qty field
     F*        moved the w1qty field 2 positions to the right to make
     F*        room for the W1UCOD field.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                   125
     A                                      DFT('PT341')
     A                                      SPACEA(001)
     A                                    52DFT('Department: ')
     A            W1WHDP         5A  O    64
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('License')
     A                                      UNDERLINE
417 DA*                                   20
417 MA                                    26
     A                                      DFT('Item    ')
     A                                      UNDERLINE
417 DA*                                   37
417 MA                                    42
     A                                      DFT('Description')
     A                                      UNDERLINE
417 DA*                                   64
417 MA                                    69
     A                                      DFT('Slot    ')
     A                                      UNDERLINE
417 DA*                                   76
417 MA                                    82
     A                                      DFT('Quantity')
     A                                      UNDERLINE
417 DA*                                   88
417 MA                                    92
     A                                      DFT('Status  ')
     A                                      UNDERLINE
     A                                   102
     A                                      DFT('Pick Slot')
     A                                      UNDERLINE
     A                                   115
417 DA*                                     DFT('DATE  ')
417 MA                                      DFT('Date  ')
     A                                      UNDERLINE
     A                                   125
417 DA*                                     DFT('TIME  ')
417 MA                                      DFT('Time  ')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
     A                                      REFFLD(PTTRN# *LIBL/PT341)
416 DA*           LILCNS    R        O    10EDTCDE(3)
416 MA            LILCNS    R        O    10
     A                                      REFFLD(LILCNS *LIBL/PT341)
417 DA*           W1ITEM        15   O    20
417 MA            W1ITEM        15   O    26
417 DA*           W1DESC        25   O    37
417 MA            W1DESC        25   O    42
417 DA*           W1DISP        12   O    64
417 MA            W1DISP        12   O    69
417 DA*           W1QTY          5  0O    76EDTCDE(3)
640aDA*           W1QTY          5  0O    84EDTCDE(3)
640aMA            W1QTY          5  0O    82EDTCDE(3)
640aAA            W1UCOD         2   O    88
417 DA*           W1STAT         8   O    88
417 MA            W1STAT         8   O    92
     A            W1PICK        12   O   102
     A            W1GDTE         6S 0O   115EDTCDE(Y)
     A            W1GTIM         6  0O   125EDTWRD('  :  :  ')
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Total transactions:')
     A            TRNCNT         5  0O    25EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('PT341: End of Report')
     A*
