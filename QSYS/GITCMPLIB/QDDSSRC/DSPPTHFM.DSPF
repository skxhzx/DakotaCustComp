      *===============================================================
      * Display access paths for a file
      *===============================================================
      * To compile:
      *
      *      CRTDSPF    FILE(XXX/FIG630DS) SRCFILE(XXX/QDDSSRC)
      *
      *===============================================================
     A                                      CF03   CF12
     A          R SFLRCD                    SFL
     A            SFLIBRARY     10A  O  7  2
     A            SFFILENAME    10A  O  7 13
     A            SFKEYFIELD    10A  O  7 35
     A            SFFORMAT      10A  O  7 24
     A            SFASENDDEC     1   O  7 46
     A            SFTEXT        32   O  7 48
     A  59                                  DSPATR(HI)
     A          R SFLCTL                    SFLCTL(SFLRCD)
     A                                      SFLSIZ(0024)  SFLPAG(0012)
     A                                      OVERLAY
     A  21                                  SFLDSP
     A                                      SFLDSPCTL
     A  53                                  SFLEND(*MORE)
     A                                  1 29'Display Access Paths' DSPATR(HI)
     A                                  3  2'Physical File  . . . . . . . .:'
     A            OUTFILE       10A  O  3 35DSPATR(HI)
     A                                  4  2'Library  . . . . . . . . . . .:'
     A            OUTLIBRARY    10A  O  4 35DSPATR(HI)
     A                                  6  2'Library   '   DSPATR(HI)
     A                                  6 13'File      '   DSPATR(HI)
     A                                  6 35'Key Field'    DSPATR(HI)
     A                                  6 49'Select/Omit Values'  DSPATR(HI)
     A                                  6 24'Format'       DSPATR(HI)
     A                                  6 45'Seq'          DSPATR(HI)
     A                                  3 47'Number of logicals. . . . .:'
     A            NBRLOGICS      4  0O  3 77DSPATR(HI)
     A          R FORMAT1
     A                                 23  4'F3=Exit'   COLOR(BLU)
     A                                 23 18'F12=Previous'  COLOR(BLU)
