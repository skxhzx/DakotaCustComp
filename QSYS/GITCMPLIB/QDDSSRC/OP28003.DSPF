     A*%%TS  SD  20090718  083639  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP280FM   PIR options - Virtual Slot Options
     A*  27 November 2002
     A*  Max Blacknik
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
417 AA*    11/27/02  MLB  4.17
     A*      - Created.
     A*
600aAA*    05/15/08  DAS  6.00a
     A*      - Renamed to OP28001.
     A*      - Added option C=Custom to Pick slot option.
     A*
600bAA*    02/23/09  DAS  6.00a
     A*      - Because H and M tracking force creation of virtual slots
     A*        we only need 2 options, one for the pick slots and one
     A*        for L tracked items.
     A*
610aAA*    06/24/09  RBD  6.10
     A*      - Added 'overflow slots only' notation to heading.
     A*
640aAA*    11/13/12  DAD  6.40a
     A*      - Added 'Allow virtual overflow slots' option.
     A*
650aAA*    04/25/14  LMC  6.50a
     A*      - Renamed to OP28003.
     A*      - Added C=Custom to Allow Virtual overflow slots.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20090718  083639  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP280.01'
610aDA**                                1 31'Virtual Slot Options'
     A                                  1 31'Virtual Slot Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Allow Virtual pick slots . . . . .-
     A                                       . . . . .'
     A            W1VPCK         1A  B  7 48
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  7 51'Y=Yes,N=No,C=Custom'
640aAA                                  8  3'Allow Virtual overflow slots . . .-
640aAA                                       . . . . .'
640aAA            W1VOVF         1A  B  8 48
640aAA N28N90                               DSPATR(HI)
640aAA  28                                  DSPATR(RI)
640aAA N28N90                               DSPATR(UL)
640aAA  08                                  DSPATR(PC)
650aDA*                                 8 51'Y=Yes,N=No'
650aMA                                  8 51'Y=Yes,N=No,C=Custom'
     A                                 10  3'Always create Virtual overflow slo-
     A                                      t for ...'
     A                                 12  6'Low tracked items . . . . . . . . -
     A                                      . . . .'
     A            W1VALL         1A  B 12 48
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                 12 51'Y=Yes,N=No'
     A                                 14  6'Medium tracked items  . . . . . . -
     A                                      . . . .'
     A                                 14 48'Y'
     A N90                                  DSPATR(HI)
     A                                 16  6'High tracked items  . . . . . . . -
     A                                      . . . .'
     A                                 16 48'Y'
     A N90                                  DSPATR(HI)
600aDA*                                10  2'Create new Virtual slot when . . .'
600aDA*                                     DSPATR(UL)
600aDA*                                12  4'Item not in base slot or correspon-
600aDA*                                     ding Virtual slot . :'
600aDA*           W1VSLT         1A  B 12 61CHGINPDFT
600aDA*N23N90                               DSPATR(HI)
600aDA* 23                                  DSPATR(RI)
600aDA* 03                                  DSPATR(PC)
600aDA*                                     DSPATR(PR)
600aDA*                                12 64'Y=Yes, N=No'
600aDA*                                13  4'Item exists and expiration date is-
600aDA*                                      different  . . . . :'
600aDA*           W1VEXP         1A  B 13 61
600aDA*N24N90                               DSPATR(HI)
600aDA* 24                                  DSPATR(RI)
600aDA*N24N90                               DSPATR(UL)
600aDA* 04                                  DSPATR(PC)
600aDA*                                13 64'Y=Yes, N=No'
600aDA*                                14  4'Item exists and entered date is di-
600aDA*                                     fferent . . . . . . :'
600aDA*           W1VEND         1A  B 14 61
600aDA*N25N90                               DSPATR(HI)
600aDA* 25                                  DSPATR(RI)
600aDA*N25N90                               DSPATR(UL)
600aDA* 05                                  DSPATR(PC)
600aDA*                                14 64'Y=Yes, N=No'
600aDA*                                16  2'New Virtual slot not allowed, merg-
600aDA*                                     e product when . . .'
600aDA*                                     DSPATR(UL)
600aDA*                                18  4'Entered date and expiration dates -
600aDA*                                     are the same  . . . :'
600aDA*           W1VSAM         1A  B 18 61
600aDA*N26N90                               DSPATR(HI)
600aDA* 26                                  DSPATR(RI)
600aDA*N26N90                               DSPATR(UL)
600aDA* 06                                  DSPATR(PC)
600aDA*                                18 64'Y=Yes, N=No'
600aDA*                                19  4'Entered dates are different'
600aDA*                                20  4'and expiration dates are the same -
600aDA*                                       . . . . . . . . . :'
600aDA*           W1VEDF         1A  B 20 61
600aDA*N27N90                               DSPATR(HI)
600aDA* 27                                  DSPATR(RI)
600aDA*N27N90                               DSPATR(UL)
600aDA* 07                                  DSPATR(PC)
600aDA*                                20 64'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
