     A*%%TS  SD  20001218  120348  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  VC091FM    Talkman Simulation Program 2 CW
     A*  16 December 2000
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A                                      CA12
     A                                      CA03
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20001218  120348  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OCORD          7  0O 11  3EDTCDE(3)
     A            OCSEQ#         5  0O 11 12EDTCDE(3)
     A            OCSEQ          3  0O 11 38EDTCDE(3)
     A            OCITEM        15   O 11 19
     A            OCCWGT         7  2O 11 46EDTCDE(3)
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20001218  120348  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'VC091.01'
     A                                  1 31'Talkman Simulation'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Type  . . . .'
     A            W1TYPE         8A  O  5 18
     A                                  5 31'Label # . . .'
     A            W1LBL#         7  0O  5 45EDTCDE(3)
     A                                  5 56'Catch Weight . .'
     A            W1CWGT         7  2B  5 73EDTCDE(3)
     A                                  6  4'Trans # . . .'
     A            W1TRN#         7  0O  6 18EDTCDE(3)
     A                                  6 31'Qty picked. .'
     A            W1QPCK         5  0O  6 45EDTCDE(3)
     A                                 10  3'Order #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 19'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 12'Seq #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'CW Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Catch Wt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
