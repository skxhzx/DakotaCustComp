     A*%%TS  SD  20070111  163258  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD12FM  Truck Load Module - Trailer Equipment Detail
     A*  December 21, 2006
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A*         Assigning loader & trailer to route 1st time
     A*----------------------------------------------------------------
     A          R WINDW1
     A*%%TS  SD  20070111  163258  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(1 2 21 72)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  2  2'Loadr12'
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 11DSPATR(HI)
     A*
     A                                  4 19'LoaderID:'
     A            W1LDR#         5S 0O  4 29DSPATR(HI)
     A*
     A                                  4 36'Loader:'
     A            W1USER        28A  O  4 44DSPATR(HI)
     A*
     A                                  5  2'Door #:'
     A            W1DOOR         3A  O  5 11DSPATR(HI)
     A*
     A                                  5 19'Trailer #:'
     A            W1TRL#         8A  O  5 30DSPATR(HI)
     A*
     A                                  5 40'Date:'
     A            W1DATE         6Y 0O  5 46DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  5 57'Time:'
     A            W1STRT         6Y 0O  5 63DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A                                  7 56'Short/Over'
     A*
     A                                  8  2'Loaded         '
     A                                      DSPATR(UL)
     A*
     A                                  8 27'Unloaded       '
     A                                      DSPATR(UL)
     A*
     A                                  8 56'Quantity  '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*            LOADER TRAILER EQUIPMENT SCREEN
     A* (Fields names ending in '1' and '2' identify 'Loaded' and
     A* 'Unloaded' columns respectively on screen).
     A*----------------------------------------------------------------
     A                                  9  2'Blankets:'
     A            W1BK1          4A  B  9 18
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A*
     A                                  9 27'Blankets:'
     A            W1BK2          4Y 0B  9 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO1          4Y 0B  9 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 10  2'Bulk Heads:'
     A            W1BH1          4A  B 10 18
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A*
     A                                 10 27'Bulk Heads:'
     A            W1BH2          4Y 0B 10 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO2          4Y 0B 10 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 11  2'Load Bars:'
     A            W1LB1          4A  B 11 18
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A*
     A                                 11 27'Load Bars:'
     A            W1LB2          4Y 0B 11 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO3          4Y 0B 11 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 12  2'IC Bins-Small:'
     A            W1ICS1         4A  B 12 18
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A*
     A                                 12 27'IC Bins-Small:'
     A            W1ICS2         4Y 0B 12 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO4          4Y 0B 12 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 13  2'IC Bins-Medium:'
     A            W1ICM1         4A  B 13 18
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A*
     A                                 13 27'IC Bins-Medium:'
     A            W1ICM2         4Y 0B 13 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO5          4Y 0B 13 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 14  2'IC Bins-Large:'
     A            W1ICL1         4A  B 14 18
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A*
     A                                 14 27'IC Bins-Large:'
     A            W1ICL2         4Y 0B 14 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO6          4Y 0B 14 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 15  2'SF Bins:'
     A            W1SFB1         4A  B 15 18
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI)
     A*
     A                                 15 27'SF Bins:'
     A            W1SFB2         4Y 0B 15 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO7          4Y 0B 15 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 16  2'Staged Pallet:'
     A            W1SPL1         4A  B 16 18
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A  28                                  DSPATR(RI)
     A*
     A                                 16 27'Staged Pallet:'
     A            W1SPL2         4Y 0B 16 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO8          4Y 0B 16 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 17  2'Total Pallets:'
     A            W1TPL1         4A  B 17 18
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(PC)
     A N29                                  DSPATR(UL)
     A  29                                  DSPATR(RI)
     A*
     A                                 17 27'Total Pallets:'
     A            W1TPL2         4Y 0B 17 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A            W1SO9          4Y 0B 17 59EDTCDE(Z)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 19  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 19 12'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2 23'Trailer Equipment Detail Entry'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A*         Changing assigned loader/trailer to route
     A*----------------------------------------------------------------
     A          R WINDW2
     A*%%TS  SD  20061113  134126  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(3 15 19 50)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A*
     A                                  2  2'Loadr12'
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 35'Door#:'
     A            W1DOOR         3A  O  4 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  6  2'Loader:'
     A            W1LDR#         5Y 0O  6 11DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A                                  6 26'New Loader:'
     A            W1LDR2         5Y 0B  6 39DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A*
     A                                  8  2'Trailer #:'
     A            W1TRL#         8A  B  8 14DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  2'Reason Code:'
     A            W1RCDE         2A  B 11 16DSPATR(UL)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 14  2'Comment:'
     A            W1COM1        35A  B 14 12
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A            W1COM2        35A  B 15 12
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 18  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 18 11'F4=Prompt'
     A                                      COLOR(BLU)
     A                                 18 22'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2 12'Loader/Trailer Assignment (Change)'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A*         Viewing assigned route
     A*----------------------------------------------------------------
     A          R WINDW3
     A*%%TS  SD  20061113  134126  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(3 15 19 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  2  2'Loadr12'
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 35'Door#:'
     A            W1DOOR         3A  O  4 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  6  2'Loader:'
     A            W1LDR#         5Y 0O  6 11DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A N24        W1USER        28A  O  6 19DSPATR(HI)
     A*
     A                                  8  2'Trailer #:'
     A            W1TRL#         8A  B  8 14DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A  29                              8 33'(Current)'
     A N29 30                           8 33'(Exception)'
     A*
     A  29                             10  2'Start Date:'
     A  29        W1DTEC         6Y 0O 10 14DSPATR(HI)
     A                                      EDTCDE(Y)
     A*
     A  29                             10 27'Start Time:'
     A  29        W1TIMC         6Y 0O 10 39DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A N29 30                          10  2'Except Date:'
     A N29 30     W1DTEE         6Y 0O 10 15DSPATR(HI)
     A                                      EDTCDE(Y)
     A*
     A N29 30                          10 25'Except Time:'
     A N29 30     W1TIME         6Y 0O 10 38DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A  21N24                          12  2'Reason:'
     A  21N24     W1REAS        35A  B 12 12
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A  21                             14  2'Comment:'
     A  21        W1COM1        35A  B 14 12
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A  21        W1COM2        35A  B 15 12
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A  20 28                          17 42'More'
     A*
     A  20N28                          17 42'Bottom'
     A*
     A                                 18  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 18 12'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 18 24'Enter=Exceptions'
     A                                      COLOR(BLU)
     A                                  2 13'Loader/Trailer Assignment (View)'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A*         Unloading trailer assigned to route
     A*----------------------------------------------------------------
     A          R WINDW4
     A*%%TS  SD  20061208  151441  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(3 15 19 50)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A*
     A                                  2  2'Loadr12'
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 35'Door#:'
     A            W1DOOR         3A  O  4 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  6  2'Loader:'
     A            W1LDR#         5Y 0O  6 11DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A                                  8  2'Trailer #:'
     A            W1TRL#         8A  O  8 14DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 11  2'Reason Code:'
     A            W1RCDE         2A  O 11 16DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 14  2'Comment:'
     A            W1COM1        35A  B 14 12
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A            W1COM2        35A  B 15 12
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 18  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 18 11'F4=Prompt'
     A                                      COLOR(BLU)
     A                                 18 22'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2 12'Loader/Trailer Assignment (Change)'
     A                                      DSPATR(HI)
     A                                 11 26'Unload Trailer'
     A                                      DSPATR(HI)
     A*
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
