     A*%%TS  SD  19940711  090811  HEMANT      REL-V2R1M1  5738-PW1
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  @@900FM   Simple table lookup - Display screen
     A*  20 February 1993
     A*  Dave Sommerville
     A*
     A*  SK900FM - 1.00
     A*
     A*  Revisions
     A*
500 AA*   05/24/06  DAS  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19940706  104632  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'WH900.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Warehouse Table Lookup'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940711  090811  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            WHCODE    R        O 11  7REFFLD(WHREC/WHCODE *LIBL/WAREHS)
     A                                      EDTCDE(4)
     A            WHDESC    R        O 11 14REFFLD(WHREC/WHDESC *LIBL/WAREHS)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10 14'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Code '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940711  090811  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 51'Position to code  . . .'
     A            PTINP1         3Y 0B  2 77EDTCDE(4)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R TITLESP
     A*%%TS  SD  19940706  104632  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'WH900.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Buscar tabla de almacen'
     A                                      DSPATR(HI)
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A*%%TS  SD  19940711  090811  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            WHCODE    R        O 11  7REFFLD(WHREC/WHCODE *LIBL/WAREHS)
     A                                      EDTCDE(4)
     A            WHDESC    R        O 11 14REFFLD(WHREC/WHDESC *LIBL/WAREHS)
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opc'
     A                                      DSPATR(HI)
     A                                 10 14'Descripcion                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Codig'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 AA*----------------------------------------------------------------
500 AA          R POSTNSP
     A*%%TS  SD  19940711  090811  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 51'Posicion a codigo . . .'
     A            PTINP1         3Y 0B  2 77EDTCDE(4)
500 AA*----------------------------------------------------------------
500 AA          R OPTRECSP
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Escriba opc, oprima ENTER.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Seleccionar                     -
     A                                       '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R CMDRECSP
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refrescar  F12=Cancelar'
     A                                      COLOR(BLU)
