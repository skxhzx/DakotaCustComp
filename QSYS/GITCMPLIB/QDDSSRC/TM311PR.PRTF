     A****************************************************************
     A* Compile with 80 width
     A****************************************************************
     A* TM311PR   Truck layout
     A* 20 June, 1997
     A* Hemant Kapadia
     A*
650a A* 05/31/13   GJA   6.50a
     A*     Enh:  Add truck description to heading
650b A* 02/27/15   GJA   6.50b
     A*     Enh:  Add zone template to heading
700a A* 04/01/15   GJA   7.00a
     A*     Enh:  Reinstate summary
     A*
     A****************************************************************
     A* COMPILE INSTRUCTIONS:  66 LENGTH 80 WIDTH
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    40
     A                                      DFT('Truck Layout')
     A                                    70
     A                                      DFT('Page')
     A            PAGE           4S 0O    74
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    30
     A                                    73
     A                                      DFT('TM311')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
650bDA***                                 40'Truck:'
650bMA                                    35'Truck:'
650bDA***         TRUCK     R        O    48REFFLD(TDTRUK TRUCKD)
650bMA            TRUCK     R        O    43REFFLD(TDTRUK TRUCKD)
     A  91                                75
     A                                      DFT('(Continued)')
650bAA            THZTMP    R        O    25REFFLD(THZTMP TRUCKH)
     A            TRKDESC   R        O    35REFFLD(THDESC TRUCKH)
650aAA                                      SPACEB(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     3'|'
     A                                    17'|'
     A                                    25'|'
     A                                    26'Unload'
     A                                    34'||'
     A                                    49'|'
     A                                    57'|'
     A                                    59'Unload'
     A                                    67'|'
     A                                      SPACEA(001)
     A                                     3'|'
     A                                     5'Compartment'
     A                                    17'|'
     A                                    19'Cube'
     A                                    25'|'
     A                                    27'Seq'
     A                                    34'||'
     A                                    37'Compartment'
     A                                    49'|'
     A                                    51'Cube'
     A                                    57'|'
     A                                    60'Seq'
     A                                    67'|'
     A                                      SPACEA(001)
     A                                     3'|'
     A                                    17'|'
     A                                    25'|'
     A                                    34'||'
     A                                    49'|'
     A                                    57'|'
     A                                    67'|'
     A                                      SPACEA(001)
     A                                     3'|'
     A                                     4'-------------'
     A                                    17'|'
     A                                    18'-------'
     A                                    25'|'
     A                                    26'---------'
     A                                    34'||'
     A                                    36'--------------'
     A                                    49'|'
     A                                    50'--------'
     A                                    57'|'
     A                                    58'----------'
     A                                    67'|'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A                                     3'|'
     A            C1COMP         1A  O     9
     A                                    17'|'
     A            C1CUBE         5S 2O    18EDTCDE(3)
     A                                    25'|'
     A            C1PICK         2S 0O    28EDTCDE(3)
     A                                    34'||'
     A*
     A            C2COMP         1A  O    41
     A                                    49'|'
     A            C2CUBE         5S 2O    50EDTCDE(3)
     A                                    57'|'
     A            C2PICK         2S 0O    61EDTCDE(3)
     A                                    67'|'
     A*
     A* Print DASH1
     A*
     A          R DASH1
     A*
     A                                      SPACEA(001)
     A                                     3'|'
     A                                    17'|'
     A                                    25'|'
     A                                    34'||'
     A                                    49'|'
     A                                    57'|'
     A                                    67'|'
     A*
     A* Print DASH2
     A*
     A          R DASH2
     A*
     A                                      SPACEA(001)
     A                                     3'|'
     A                                     4'-------------'
     A                                    17'|'
     A                                    18'-------'
     A                                    25'|'
     A                                    26'---------'
     A                                    34'||'
     A                                    36'--------------'
     A                                    49'|'
     A                                    50'--------'
     A                                    57'|'
     A                                    58'----------'
     A                                    67'|'
     A*
     A*
     A* Print totals
     A*
700aAA          R TOTAL1
700aAA                                      SPACEB(001)
700aAA            TCOMP          1A  O     8
700aAA            TPDESC        15A  O    10
700aAA            TCUBE          5S 2O    30EDTCDE(3)
700aAA            TNUM           2S 0O    40EDTCDE(3)
     A*
700aAA          R TOTAL2
700aAA                                      SPACEB(001)
700aAA                                    20'Total'
700aAA            GCUBE          6S 2O    29EDTCDE(3)
700aAA            GNUM           2S 0O    40EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('TM311: End of Report ')
     A*
