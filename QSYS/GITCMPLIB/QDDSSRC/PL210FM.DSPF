     A*%%TS  SD  19960521  093740  HEMANT      REL-V3R1M0  5763-PW1
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960521  093740  HEMANT      REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PL210.01'
     A                                  1 30'Create Pallet Pattern'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Pallet code . . . . . . .'
     A            W1PAL          6A  B  7 31
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A                                  7 62'Name'
     A                                  8  4'Package length  . . . . .'
     A            W1LENF         5Y 2B  8 31EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A N23                                  DSPATR(UL)
     A                                  8 38'thru'
     A            W1LENT         5Y 2B  8 43EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(HI)
     A N24                                  DSPATR(UL)
     A                                  8 62'0-999.99'
     A                                  9  4'Package width . . . . . .'
     A            W1WDTF         5Y 2B  9 31EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(HI)
     A N26                                  DSPATR(UL)
     A                                  9 38'thru'
     A            W1WDTT         5Y 2B  9 43EDTCDE(4)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27                                  DSPATR(HI)
     A N27                                  DSPATR(UL)
     A                                  9 62'0-999.99'
     A                                 23  2'F3=Exit   F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
