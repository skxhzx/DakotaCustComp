     A*%%TS  SD  19940722  112651  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RK112FM   Rack definition maint. (One whse) - Change screen
     A*  21 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940722  112651  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'RK112.01'
     A                                  1 30'Rack Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Code . . . . . . . . . . . .'
     A            W1CODE         5A  B  8 34
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Description  . . . . . . . .'
     A            W1DESC        30A  B  9 34CHECK(LC)
     A N90                                  DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 10  4'Slots per rack bay . . . . .'
     A            W1SLTS         1Y 0B 10 34EDTCDE(3)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(PC)
     A                                 14 13'Slot'
     A                                 15 13'Definition'
     A                                      DSPATR(UL)
     A                                 15 27'Positions'
     A                                      DSPATR(UL)
     A                                 16  6'1'
     A            W1L01          2A  B 16 17
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS01         2Y 0B 16 30EDTCDE(4)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  6'2'
     A            W1L02          2A  B 17 17
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS02         2Y 0B 17 30EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  34                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  6'3'
     A            W1L03          2A  B 18 17
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS03         2Y 0B 18 30EDTCDE(4)
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  35                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  6'4'
     A            W1L04          2A  B 19 17
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS04         2Y 0B 19 30EDTCDE(4)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  36                                  DSPATR(PC)
     A                                 20  6'5'
     A            W1L05          2A  B 20 17
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS05         2Y 0B 20 30EDTCDE(4)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  37                                  DSPATR(PC)
     A                                 16 45'6'
     A            W1L06          2A  B 16 56
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS06         2Y 0B 16 69EDTCDE(4)
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  38                                  DSPATR(PC)
     A                                 17 45'7'
     A            W1L07          2A  B 17 56
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS07         2Y 0B 17 69EDTCDE(4)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  39                                  DSPATR(PC)
     A                                 18 45'8'
     A            W1L08          2A  B 18 56
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS08         2Y 0B 18 69EDTCDE(4)
     A  40                                  DSPATR(RI)
     A N40N90                               DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                 19 45'9'
     A            W1L09          2A  B 19 56
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS09         2Y 0B 19 69EDTCDE(4)
     A  41                                  DSPATR(RI)
     A N41N90                               DSPATR(UL)
     A  41                                  DSPATR(PC)
     A                                 20 44'10'
     A            W1L10          2A  B 20 56
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HS10         2Y 0B 20 69EDTCDE(4)
     A  42                                  DSPATR(RI)
     A N42N90                               DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 11  4'Maximum picking level  . . .'
     A            W1PLVL         2Y 0B 11 34EDTCDE(4)
     A  43                                  DSPATR(RI)
     A N43N90                               DSPATR(UL)
     A  43                                  DSPATR(PC)
     A                                 15  4'Level'
     A                                      DSPATR(UL)
     A                                 14 27'Handstack'
     A                                 15 43'Level'
     A                                      DSPATR(UL)
     A                                 15 52'Definition'
     A                                      DSPATR(UL)
     A                                 15 66'Positions'
     A                                      DSPATR(UL)
     A                                 14 52'Slot'
     A                                 14 66'Handstack'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
