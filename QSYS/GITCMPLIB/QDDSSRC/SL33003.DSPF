     A*%%TS  SD  20090331  052705  CHUCKM      REL-V6R1M0  5761-WDS
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  Revisions
413aAA*  03/06/00  RH   4.13
     A*    - Add Item/description option (W1ITMD)
     A*
520 AA*     03/25/09  CWM  5.20
     A*       - ENH: Added Report Distribution / Override Functionality
     A*
610aAA*  11/05/09  GJA  6.10a
     A*    - Changed item/description option to show license
640aAA*  04/18/11  RH   6.40a
     A*    - FIX: 6.10a missed in previous verison, rename format
640bAA*  04/13/12  RH   6.40b
     A*    - ENH: Add select by slot status, rename format
650a A*  04/03/13  GJA  6.50a
     A*    - ENH: Add select by slot side and virtual slot, rename
     A*           format
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20090331  052705  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL330.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 49'0-999'
     A                                  7  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  7 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 49'Name, *ALL'
     A                                  8  4'Aisle  . . . . . . . . . . .'
     A            W1AISL         4A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 49'Name, *ALL'
650aAA                                  9  4'Location . . . . . . . . . .'
650aAA            W1LOCS         3Y 0B  9 34EDTCDE(3)
650aAA N32N90                               DSPATR(HI)
650aAA  32                                  DSPATR(RI)
650aAA N32N90                               DSPATR(UL)
650aAA  13                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA            W1LOCE         3Y 0B  9 39EDTCDE(3)
650aAA N33N90                               DSPATR(HI)
650aAA  33                                  DSPATR(RI)
650aAA N33N90                               DSPATR(UL)
650aAA  13                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 10  4'Side . . . . . . . . . . . .'
650aAA            W1SIDE         1A  B 10 34
650aAA N34N90                               DSPATR(HI)
650aAA  34                                  DSPATR(RI)
650aAA N34N90                               DSPATR(UL)
650aAA  14                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 10 49'O=Odd, E=Even, B=Both'
650aAA                                 11  4'Virtual slots only . . . . .'
650aAA            W1VSLT         1A  B 11 34
650aAA N35N90                               DSPATR(HI)
650aAA  35                                  DSPATR(RI)
650aAA N35N90                               DSPATR(UL)
650aAA  15                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA                                 11 49'Y=Yes, N=No'
     A*                                 9  4'Include item/description'
610aMA*                                10  4'        /license . . . . . .'
     A                                 14  4'Include item/description'
610aMA                                 15  4'        /license . . . . . .'
     A*           W1ITMD         1A  B 10 34
     A            W1ITMD         1A  B 15 34
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 49'Y=Yes, N=No'
     A*                                10 49'Y=Yes, N=No'
     A*                                11  4'Slot type  . . . . . . . . .'
     A*           W1TYPE         1A  B 11 34
650aMA                                 12  4'Slot type  . . . . . . . . .'
650aMA            W1TYPE         1A  B 12 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 49'1=All 2=Picking'
     A                                 13 49'3=Non-picking'
     A*                                11 49'1=All slots'
     A*                                12 49'2=Picking slots'
     A*                                13 49'3=Non-picking slots'
640bAA*                                14  4'Slot status  . . . . . . . .'
640bAA*           W1STAT         4A  B 14 34
650aAA                                 16  4'Slot status  . . . . . . . .'
650aAA            W1STAT         4A  B 16 34
640bAA N31N90                               DSPATR(HI)
640bAA  31                                  DSPATR(RI)
640bAA N31N90                               DSPATR(UL)
640bAA  09                                  DSPATR(PC)
640bAA  90                                  DSPATR(PR)
650aMA                                 16 49'Name, *ALL'
640bAA*                                14 49'Name, *ALL'
640bAA*
640bDA*                                15  4'Printing sequence  . . . . .'
640bMA*                                16  4'Printing sequence  . . . . .'
650aMA                                 17  4'Printing sequence  . . . . .'
640bMA*           W1SEQ          1A  B 16 34
650aMA            W1SEQ          1A  B 17 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
650aMA                                 17 49'1=Slot 2=Picking seq'
640bMA*                                16 49'1=Slot number'
640bMA*                                17 49'2=Picking sequence'
640bDA*                                16 49'2=Picking sequence'
     A                                 18  4'View or print  . . . . . . .'
     A                                      DSPATR(ND)
640bDA*           W1VP           1A  B 17 34
640bMA            W1VP           1A  B 18 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 18 49'V=View, P=Print'
     A                                      DSPATR(ND)
640bDA*                                18  4'Place on job queue ? . . . .'
640bMA                                 19  4'Place on job queue ? . . . .'
640bMA            W1JOBQ         1A  B 19 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640bMA                                 19 49'Y=Yes, N=No'
520 DA*                                18  4'Printer device . . . . . . .'
520 DA*           W1DEV         10A  B 18 34
520 DA*N28N90                               DSPATR(HI)
520 DA* 28                                  DSPATR(RI)
520 DA*N28N90                               DSPATR(UL)
520 DA* 08                                  DSPATR(PC)
520 DA* 90                                  DSPATR(PR)
520 DA*                                19  4'Form type  . . . . . . . . .'
520 DA*           W1FORM        10A  B 19 34
520 DA*N29N90                               DSPATR(HI)
520 DA* 29                                  DSPATR(RI)
520 DA*N29N90                               DSPATR(UL)
520 DA* 09                                  DSPATR(PC)
520 DA* 90                                  DSPATR(PR)
520 AA*
640bDA*                                19  4'One Time Print Override. . .'
640bAA                                 20  4'One Time Print Override. . .'
520 AA            W1POVR         1A  B 20 34
520 AA N30N90                               DSPATR(HI)
520 AA  30                                  DSPATR(RI)
520 AA N30N90                               DSPATR(UL)
520 AA  08                                  DSPATR(PC)
520 AA  90                                  DSPATR(PR)
520 AA                                 20 49'Y=Yes, N=No'
520 AA*
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
