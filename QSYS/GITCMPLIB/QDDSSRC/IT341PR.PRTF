     A****************************************************************
     A*
     A* IT341PR   Unslotted items
     A* 01 September, 1994
     A* Hemant Kapadia
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    59
     A                                      DFT('Unslotted Items')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT341')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A            TYPE          11A  O    63
     A  91                                75
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Item')
     A                                    18
     A                                      DFT('Description')
     A                                    45
     A                                      DFT('Item')
     A                                    62
     A                                      DFT('Description')
     A                                    89
     A                                      DFT('Item')
     A                                   106
     A                                      DFT('Description')
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1ITEM        15A  O     1
     A            C1DESC        25A  O    18
     A*
     A            C2ITEM        15A  O    45
     A            C2DESC        25A  O    62
     A*
     A            C3ITEM        15A  O    89
     A            C3DESC        25A  O   106
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT341: End of Report ')
     A*
