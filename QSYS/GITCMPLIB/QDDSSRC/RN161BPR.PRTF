     A*%%***********************************************************************
     A*
     A*   RN161BPR  Customer returns report by Date/Item
     A*   12 May, 1999
     A*   Roxanne Harrison
     A*
     A* ****   COMPILE WITH 198 COLUMNS AND 15 CPI
     A*
     A*    05/12/99  RH   4.11
     A*      - Create using IT316BPR.  Print by Date, Item.            d
     A*    01/06/05  RLP  5.00
     A*      - Adding field to RN161A. City Line Mod
     A*        New columns/detail Disp Code, Route, Stop, Memo, and Tran#
     A*        COMPILE WITH 198 COLUMNS AND 15 CPI
     A*    01/26/05  RLP  5.00c
     A*      - Adding column MULT to report, in conjunction with finding the
     A*        original Tran# if only Ord#/Item# was entered on the return.
     A*    03/26/07  RBD  5.10
     A*      - Added employee# and name of original selector.
530a A*    11/24/09  GJA  5.30a
     A*      - Enh:  Cleaned up mod marks & RLU, reinstated detail2
530b A*    07/29/10  RH   5.30b
     A*      - Enh:  Add customer #
DRYa A*    03/18/11  RH   DRYa
     A*      - Create DETAILD w/ W1CSTA alpha cust # for Dairyland
640a A*    01/12/11  RH   6.40a
     A*      - Enh:  Add customer name to detail2 line
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    77
     A                                      DFT('Customer Returns Report')
     A                                   180
     A                                      DFT('Page')
     A            PAGE           4S 0O   184
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O    76
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   182
     A                                      DFT('RN161B')
     A            WHSE          30A  O    74
     A                                      SPACEB(001)
     A                                    82'By Date/Item'
     A                                      SPACEB(001)
     A                                    10'Item:'
     A                                      SPACEB(001)
     A            ITEM          15A  O    16
     A                                    40'Reason code:'
410 AA            CODE           4A  O    53
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Date    ')
     A                                      UNDERLINE
510 MA                                    10
     A                                      DFT('Time ')
     A                                      UNDERLINE
510 MA                                    16
     A                                      DFT('Item Number    ')
     A                                      UNDERLINE
510 MA                                    32
     A                                      DFT('Description             ')
     A                                      UNDERLINE
510 AA                                    56
510 AA                                      DFT('      ')
510 AA                                      UNDERLINE
510 MA                                    63
     A                                      DFT('User      ')
     A                                      UNDERLINE
510 AA                                    74
510 AA                                      DFT('Emp# ')
510 AA                                      UNDERLINE
510 AA                                    80
510 AA                                      DFT('Employee Name       ')
510 AA                                      UNDERLINE
510 MA                                   101
     A                                      DFT('Quantity  ')
     A                                      UNDERLINE
510 MA                                   112
     A                                      DFT('Code')
     A                                      UNDERLINE
510 MA                                   117
     A                                      DFT('Corresponding Item')
     A                                      UNDERLINE
510 MA                                   136
500 AA                                      DFT('Disp Cd')
500 AA                                      UNDERLINE
510 MA                                   144
500 AA                                      DFT('Route')
500 AA                                      UNDERLINE
510 MA                                   150
500 AA                                      DFT('Stop')
500 AA                                      UNDERLINE
510 MA                                   155
500 AA                                      DFT('Memo                ')
500 AA                                      UNDERLINE
510 AA                                   175
510 AA                                      DFT('          ')
510 AA                                      UNDERLINE
510 MA                                   186
500 AA                                      DFT('Tran#  ')
500 AA                                      UNDERLINE
510 MA                                   194
500cAA                                      DFT('Mult#')
500cAA                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  92        W1DATE         6S 0O     1
     A                                      EDTCDE(Y)
510 MA            W1TIME         4S 0O    10
     A                                      EDTWRD('  :  ')
510 MA  91        ORITEM    R        O    16
     A                                      REFFLD(ORREC/ORITEM *LIBL/ORRTN)
510 MA  91        ITDESC    R        O    32
     A                                      REFFLD(ITDESC PIRITEM)
510 MA            ORRUSR    R        O    63
     A                                      REFFLD(ORREC/ORRUSR *LIBL/ORRTN)
510 AA            W1EMP#         5  0O    74
510 AA                                      EDTCDE(Z)
510 AA            W1NAM         20A  O    80
510 MA            W1QTY         10A  O   101
510 MA            ORRCOD    R        O   113
     A                                      REFFLD(ORREC/ORRCOD *LIBL/ORRTN)
510 MA            ORCITM    R        O   117
     A                                      REFFLD(ORREC/ORCITM *LIBL/ORRTN)
510 MA            W1DSPC         5A  O   137
510 MA            W1RTE          5A  O   144
510 MA            W1STP          2  0O   151
510 MA            W1MEM1        30A  O   155
510 MA            W1TRN#         7  0O   186
500 AA                                      EDTCDE(Z)
510 MA            W1MLT#         2   O   195
     A*%%***********************************************************************
     A          R DETAIL2
530bAA                                     6'Cust#:'
530aAA            W1CUST        11S 0O    13EDTCDE(Z)
640aAA            W1CNAM        40A  O    30
530aAA                                    85'Invoice#:'
530aAA            W1INV#        12A  O    +2
530aAA                                    +5'Order#:'
530aAA            W1ORD          7S 0O    +2
530aAA                                    +5'Org Order#:'
530aAA            W1OORD         7S 0O    +2
530aMA            W1MEM2        15A  O   165
     A*%%***********************************************************************
DRYaAA          R DETAILD
DRYAAA                                     6'Cust#:'
DRYAAA            W1CSTA        11A  O    13
DRYaAA                                    85'Invoice#:'
DRYaAA            W1INV#        12A  O    +2
DRYaAA                                    +5'Order#:'
DRYaAA            W1ORD          7S 0O    +2
DRYaAA                                    +5'Org Order#:'
DRYaAA            W1OORD         7S 0O    +2
DRYaMA            W1MEM2        15A  O   165
     A*
     A* Print detail line3
     A*
     A          R DETAIL3
214 AA                                      SPACEB(001)
214 AA                                      SPACEA(001)
214 AA                                     1'NO RECORDS MATCH ENTERED CRITERIA'
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('RN161B: End of Report')
     A*
