     A****************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A* EX325PR   Single Order Selection exception report.
     A* 15 November 2002
     A* Max Blacknik
     A*
     A****************************************************************
     A*
     A*  Revisions:
     A*
416 AA*    11/15/02  MLB  4.16
     A*       - Created.
     A*
     A**************************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A*                                   52
     A            RPTTTL        40        47
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('EX325')
     A                                      SPACEA(001)
     A            FMDT           6S 0O    55
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            TODT           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A            P#DESC        40A  O    47
     A                                      SPACEA(001)
     A            P#STAT        40A  O    47
     A                                      SPACEA(002)
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                    69
     A                                      DFT('* Generated *')
     A                                      SPACEA(001)
     A                                     3
     A                                      DFT('Item')
     A                                    19
     A                                      DFT('Description')
     A                                    50
     A                                      DFT('From Slot')
     A                                    63
     A                                      DFT('Qty')
     A                                    69
     A                                      DFT('Date')
     A                                    78
     A                                      DFT('Time')
     A                                    86
     A                                      DFT('Gen''d by')
     A                                    98
     A                                      DFT('Task')
     A                                   104
     A                                      DFT('Error Desc')
     A*%%***********************************************************************
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            EXITEM    R        O     3
     A                                      REFFLD(EXITEM ITMEXCP)
     A            ITDESC    R        O    19
     A                                      REFFLD(ITDESC PIRITEM)
     A            EXFDIS    R        O    50
     A                                      REFFLD(EXFDIS ITMEXCP)
     A            EXQTY     R        O    63
     A                                      REFFLD(EXQTY ITMEXCP)
     A                                      EDTCDE(K)
     A            GENDTE         6S 0O    68
     A                                      EDTCDE(Y)
     A            EXGTIM    R        O    77
     A                                      REFFLD(EXGTIM ITMEXCP)
     A                                      EDTWRD(' 0:  :  ')
     A            EXGBY     R        O    86
     A                                      REFFLD(EXGBY ITMEXCP)
     A            EXTASK    R        O    97
     A                                      REFFLD(EXTASK ITMEXCP)
     A            ERRMSG        28A  O   104
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total exceptions for')
     A            T#DESC        13A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    34
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A          R TOTAL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('EX325: End of Report ')
     A*
