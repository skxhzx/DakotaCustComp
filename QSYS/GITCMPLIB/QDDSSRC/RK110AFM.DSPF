     A*%%TS  SD  19940726  140436  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RK110AFM   Rack definition maint. (one whse) - Display
     A*  21 July 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A*%%TS  SD  19940721  062844  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'RK110A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 27'Rack Definition Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940711  163426  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 48'Position to code  . . . .'
     A            PTINP2         5A  B  3 75
     A            PTDMY1         1A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940726  140436  DAVE        REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RKCODE    R        O 11  7REFFLD(RKREC/RKCODE *LIBL/RACKS)
     A            RKDESC    R        O 11 14REFFLD(RKREC/RKDESC *LIBL/RACKS)
     A            RKL01     R        O 11 62REFFLD(RKREC/RKL01 *LIBL/RACKS)
     A            RKL02     R        O 11 66REFFLD(RKREC/RKL02 *LIBL/RACKS)
     A            RKL03     R        O 11 70REFFLD(RKREC/RKL03 *LIBL/RACKS)
     A            RKL04     R        O 11 74REFFLD(RKREC/RKL04 *LIBL/RACKS)
     A            RKL05     R        O 11 78REFFLD(RKREC/RKL05 *LIBL/RACKS)
     A            RKSLTS    R        O 11 49REFFLD(RKREC/RKSLTS *LIBL/RACKS)
     A            RKPLVL    R        O 11 56REFFLD(RKREC/RKPLVL *LIBL/RACKS)
     A                                      EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19940726  140436  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Code '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 14'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                  9 63'Level Definitions'
     A                                      DSPATR(HI)
     A                                 10 62' 1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Per Bay'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66' 2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70' 3'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74' 4'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78' 5'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 55'Pick'
     A                                      DSPATR(HI)
     A                                 10 55'Level'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 46'Slots'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
