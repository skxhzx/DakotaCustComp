     A*%%TS  SD  20010628  124253  DAVE        REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*  DQ130FM   Work with daily quantities - Item Quantities
     A*  28 June 2001
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
415 AA*    06/28/01  DAS  4.15
     A*      - Created.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010628  124253  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'DQ130'
     A                                  1 23'Work with Daily Quantities for Ite-
     A                                      m'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Run:'
     A                                      DSPATR(HI)
     A            WORDTE         6Y 0O  2 35EDTCDE(Y)
     A                                      DSPATR(HI)
     A            WORTIM         6Y 0O  2 45EDTWRD(' 0:  :  ')
     A                                      DSPATR(HI)
     A            DMY1           1A  O  3  2
     A                                  4  2'Warehouse            :'
     A            WOWHSE        35A  O  4 25
     A            W1LBL         22   O  5  2
     A            W1ITEM        55A  O  5 25
     A            W1LBLB        22   O  6  2
     A            W1BITM        55A  O  6 25
     A            DMY2           1A  O  7  2
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A            W1TYPE        19   O 11  2
     A            W1QTY1         5Y 0O 11 22EDTCDE(M)
     A            W1QTY2         5Y 0O 11 29EDTCDE(M)
     A            W1QTY3         3Y 0O 11 36EDTCDE(M)
     A            W1STK1         5Y 0O 11 42EDTCDE(M)
     A            W1STK2         5Y 0O 11 49EDTCDE(M)
     A            W1STK3         3Y 0O 11 56EDTCDE(M)
     A            W1ALC1         5Y 0O 11 62EDTCDE(M)
     A            W1ALC2         5Y 0O 11 69EDTCDE(M)
     A            W1ALC3         3Y 0O 11 76EDTCDE(M)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  9 22'-- Period Total --'
     A                                      DSPATR(HI)
     A                                  9 42'------ Stock -----'
     A                                      DSPATR(HI)
     A                                  9 62'---- Allocated ---'
     A                                      DSPATR(HI)
     A                                 10  2'Type               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Normal'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'  Brk1'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 36'Brk2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Normal'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 49'  Brk1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 56'Brk2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Normal'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69'  Brk1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Brk2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A            W1TYPE        19   O 11  2
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                 10  2'Type               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A            W1TYPE        19   O 11  2
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                 10  2'Type               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
