     A*%%TS  SD  20111115  135819  BFC         REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. PT64001 COPY AND NAME PT64002
     A*----------------------------------------------------------------
     A*
414 AA*  PT640FM  Route transactions - Display
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*  Roxanne Harrison
     A*
     A*----------------------------------------------------------------
     A*  Revisions
404 AA*    08/15/97  RH
     A*      - Change delete message to close message.
     A*
414 AA*    06/30/00  DAS  4.14
     A*      - Changed indicator for SFLNXTCHG from 85 to 21.
     A*        Re: Program never changed to use 85 instead of 21.
     A*
414aAA*    08/15/00  DAS  4.14a
     A*      - Added F7
     A*
417 AA*    09/23/03  HNK  4.17
     A*      - Added Zone description
417aAA*    01/16/04  HNK  4.17a
     A*      - Added flag for runners. W1FLAG
     A*
500 AA*    11/30/05  MLB  5.00
     A*      - Enh: Added transaction, employee#'s to View 2.
510aAA*    11/01/06  RH   5.10a
     A*      - Enh: Merged GSC code from PIRSTD library.
     A*
530 AA*    11/30/09  JCJ  5.30
     A*      - Moved POSTNREC/OPTREC into DSPSFC.
     A*        Re: Webfacing had problem with double read.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Grocery Supply
     A*
GSC AA*    01/27/06  JCJ  GSC
     A*      - Added new Subfile/Control formats DSPSFLGS/DSPSFCGS
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Harbor
     A*
HAR AA*    07/26/06  MLB  HAR
     A*      - Revised program to place cursor on position to
     A*        transaction number field.
     A*
     A*----------------------------------------------------------------
PAK AA*    11/03/06  RAH  PAK
     A*      - Add hold values to be passed to CE190 on write of
     A*        subfile record.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Avalon Foodservice
     A*
AVL AA*    11/11/11  MLB  AVL
     A*      - Added new Subfile/Control formats DSPSFLAV/DSPSFCAV
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A                                      CF16
     A                                      CA07
     A          R TITLE
     A*%%TS  SD  19970626  112612  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'PT640 '
     A                                  1 32'Route Transactions'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970626  160006  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A                                  2  2'Warehouse:'
     A            WOWHSE        40A  O  2 13
     A                                  3  2'Route:'
     A            WORTE          5A  O  3 13
     A                                  3 43'Position to transaction . .'
     A            PTINP1         7Y 0B  3 72EDTCDE(4)
HAR AA  35                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
414 DA* 85                                  SFLNXTCHG
     A          R DSPSFL                    SFL
     A*%%TS  SD  20030923  131546  BFC         REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PTTRN#    R        O 11  7REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
417aDA*           W1TYPE        30A  O 11 17
417aMA            W1TYPE        25A  O 11 16
417a A            W1FLAG         1   O 11 44
     A            W1STAT        16A  O 11 48
     A            W1ZDSC        15   O 11 65
     A*
PAK AA            HTRN#          7  0H
PAK AA            HORD      R        H      REFFLD(LBREC/LBORD *LIBL/LABEL)
PAK AA            HITEM         15   H
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20030923  131546  BFC         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO          4S 0H      SFLRCDNBR
530 AA            PTDMY1         1A  O  3  2
530 AA                                  4  2'Warehouse:'
530 AA            WOWHSE        40A  O  4 13
530 AA                                  5  2'Route:'
530 AA            WORTE          5A  O  5 13
530 AA                                  5 43'Position to transaction . .'
530 AA            PTINP1         7Y 0B  5 72EDTCDE(4)
530 AA                                  6  2'Type options, press Enter.'
530 AA                                      COLOR(BLU)
530 AA            OPTLN2        75A  O  8  2COLOR(BLU)
530 AA            OPTLN1        75A  O  7  2COLOR(BLU)
530 D ***                              10  2'Opt'
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417aDA*                                10 17'Description                   '
417aMA                                 10 16'Description              '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Status          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Zone           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417aAA                                 10 43'Flag'
417aAA                                      DSPATR(HI)
417aAA                                      DSPATR(UL)
GSC AA*****************************************************************
GSC AA*
GSC AA* Subfile for list display
GSC AA*
GSC AA          R DSPSFLGS                  SFL
GSC AA*%%TS  SD  20060127  134954  JEFF        REL-V5R3M0  5722-WDS
GSC AA  21                                  SFLNXTCHG
GSC AA            OPTION         2A  B 11  2
GSC AA  20                                  DSPATR(PC)
GSC AA N21N90                               DSPATR(UL)
GSC AA  21                                  DSPATR(RI)
GSC AA  90                                  DSPATR(PR)
GSC AA            PTTRN#    R        O 11  7REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
GSC AA                                      EDTCDE(3)
GSC AA            W1TYP2        10A  O 11 16
GSC AA            W1HTR#    R        O 11 28REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
GSC AA                                      EDTCDE(3)
GSC AA            W1WHDP    R        O 11 37REFFLD(PTREC/PTWHDP *LIBL/PIRTRAN)
GSC AA            W1FLAG         1A  O 11 44
GSC AA            W1STAT        16A  O 11 48
GSC AA            W1ZDSC        15A  O 11 65
GSC AA*
GSC AA          R DSPSFCGS                  SFLCTL(DSPSFLGS)
GSC AA*%%TS  SD  20030923  131546  BFC         REL-V5R2M0  5722-WDS
GSC AA                                      SFLSIZ(0011)
GSC AA                                      SFLPAG(0010)
GSC AA                                      CF10
GSC AA                                      CF11
GSC AA                                      CF17
GSC AA                                      CF18
GSC AA                                      CF20
GSC AA                                      ROLLDOWN
GSC AA                                      ROLLUP
GSC AA                                      OVERLAY
GSC AA  81                                  SFLDSP
GSC AA  82                                  SFLDSPCTL
GSC AA  83                                  SFLCLR
GSC AA                                 10  2'Opt'
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 10  7'Trans #'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 16'Description'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 28'Doc Nbr'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 37'Dept '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 48'Status          '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 65'Zone           '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 43'Flag'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
AVL AA*****************************************************************
AVL AA*
AVL AA* Subfile for list display for Avalon Foodservice
AVL AA*
     A          R DSPSFLAV                  SFL
     A*%%TS  SD  20111115  135819  BFC         REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PTTRN#    R        H      REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
     A            W1UOW          9Y 0O 11  6EDTCDE(4)
     A            W1TYPE        25A  O 11 18
     A            W1FLAG         1A  O 11 44
     A            W1STAT        16A  O 11 48
     A            W1ZDSC        15A  O 11 65
AVL AA*
     A            HTRN#          7S 0H
     A            HORD      R        H      REFFLD(LBREC/LBORD *LIBL/LABEL)
     A            HITEM         15A  H
AVL AA*
AVL AA          R DSPSFCAV                  SFLCTL(DSPSFLAV)
AVL AA                                      SFLSIZ(0011)
AVL AA                                      SFLPAG(0010)
AVL AA                                      CF10
AVL AA                                      CF11
AVL AA                                      CF17
AVL AA                                      CF18
AVL AA                                      CF20
AVL AA                                      ROLLDOWN
AVL AA                                      ROLLUP
AVL AA                                      OVERLAY
AVL AA  81                                  SFLDSP
AVL AA  82                                  SFLDSPCTL
AVL AA  83                                  SFLCLR
AVL AA            PTDMY1         1A  O  3  2
AVL AA                                  4  2'Warehouse:'
AVL AA            WOWHSE        40A  O  4 13
AVL AA                                  5  2'Route:'
AVL AA            WORTE          5A  O  5 13
AVL AA                                  5 43'Position to transaction . .'
AVL AA            PTINP1         7Y 0B  5 72EDTCDE(4)
AVL AA                                  6  2'Type options, press Enter.'
AVL AA                                      COLOR(BLU)
AVL AA            OPTLN2        75A  O  8  2COLOR(BLU)
AVL AA            OPTLN1        75A  O  7  2COLOR(BLU)
AVL AA                                 10  2'Opt'
AVL AA                                      DSPATR(UL)
AVL AA                                      DSPATR(HI)
AVL AA                                 10  6'Unit of Wrk'
AVL AA                                      DSPATR(HI)
AVL AA                                      DSPATR(UL)
AVL AA                                 10 18'Description            '
AVL AA                                      DSPATR(HI)
AVL AA                                      DSPATR(UL)
AVL AA                                 10 48'Status          '
AVL AA                                      DSPATR(HI)
AVL AA                                      DSPATR(UL)
AVL AA                                 10 65'Zone           '
AVL AA                                      DSPATR(HI)
AVL AA                                      DSPATR(UL)
AVL AA                                 10 43'Flag'
AVL AA                                      DSPATR(HI)
AVL AA                                      DSPATR(UL)
AVL AA*
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
500 AA            W2TRN#    R        O 11  7REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
500 AA                                      EDTCDE(3)
500 AA            W2EMP#    R        O 11 16REFFLD(PTREC/PTEMP# *LIBL/PIRTRAN)
500 AA                                      EDTCDE(3)
PAK AA            HTRN#          7  0H
PAK AA            HORD      R        H      REFFLD(LBREC/LBORD *LIBL/LABEL)
PAK AA            HITEM         15   H
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
500 AA                                 10  7'Trans #'
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
500 AA                                 10 16'Emp #'
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
PAK AA            HTRN#          7  0H
PAK AA            HORD      R        H      REFFLD(LBREC/LBORD *LIBL/LABEL)
PAK AA            HITEM         15   H
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19970821  104252  HEMANT      REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A                                  4  2' '
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
404 MA                                      s for 9=Close.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
