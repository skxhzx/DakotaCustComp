     A*%%TS  SD  20050125  162523  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP320FM   PIR options - Slot Pallet Position Tracking Options
     A*  25 August 2004
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
500 AA*    08/25/04  DAS  5.00
     A*      - Created.
     A*
500aAA*    01/25/05  DAS  5.00a
     A*      - Enh: Added '(Future)' to some options.
     A*        Not implementing Directed in initial release.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20050125  162523  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP320.01'
     A                                  1 26'Slot Pallet Position Tracking'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Allow pallet position tracking'
     A                                  8  5'within a multiple position slot  .-
     A                                       . . . . . .'
     A            W1SPTR         1A  B  8 53
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                  8 57'Y=Yes, N=No'
     A                                 11  3'If pallet position tracking is Y=Y-
     A                                      es then,'
     A                                      DSPATR(UL)
     A                                 13  5'When merging Putaway'
     A                                 14  7'onto existing pallet . . . . . .'
     A            W1MRG          1A  B 14 41
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 14 45'1 = Only ask for pallet position'
     A                                 15 45'2 = Direct user step by step'
     A                                 16 49'(future)'
     A                                 17  5'When returning pallets to'
     A                                 18  7'slot during replenishment  . . .'
     A            W1RTN          1A  B 18 41
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 18 45'1 = Assume user returns pallets in'
     A                                 19 49'Last Out First In (LOFI) order'
     A                                 20 45'2 = Direct user step by step'
     A                                 21 49'(future)'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
