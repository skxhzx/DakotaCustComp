     A*%%TS  SD  19960306  134913  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TF122FM   Transfer order maintenance - Change screen
     A*  29 February 1996
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
520 AA*  01/24/09  CWM  5.20
     A*    - Enh: Changed stop from 2 to 3 for host stop mods.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960306  134913  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TF122.01'
     A                                  1 28'Transfer Order Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  7'Transfer number:'
     A            W1TNUM         7Y 0O  4 24EDTCDE(4)
     A                                  5  2'Requesting warehouse:'
     A            $PWHSE         3Y 0O  5 24EDTCDE(3)
     A                                  5 28'-'
     A            WHDESC        30A  O  5 30
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  3'Picking warehouse . . . . . . .'
     A            W1HFWH         3Y 0B  9 35
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            W1HFWD        30   O  9 40
     A                                 10  3'Requested by  . . . . . . . . .'
     A            W1HRQB        10A  B 10 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  3'Transfer reason code  . . . . .'
     A            W1HRSN         2A  B 11 35
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  3'Customer order number . . . . .'
     A            W1HCO#         7Y 0B 12 35
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 13  3'Customer number . . . . . . . .'
     A            W1HCC#        11Y 0B 13 35
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 14  3'Customer P.O. number  . . . . .'
     A            W1HCPO        15A  B 14 35
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  3'Customer name . . . . . . . . .'
     A            W1HCNM        40A  B 15 35
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  3'Address 1 . . . . . . . . . . .'
     A            W1HCA1        40A  B 16 35
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 17  3'Address 2 . . . . . . . . . . .'
     A            W1HCA2        40A  B 17 35
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  3'City/State/Zip  . . . . . . . .'
     A            W1HCCT        20A  B 18 35
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HCST         2A  B 18 58
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HCZP        10A  B 18 63
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  3'Route . . . . . . . . . . . . .'
     A            W1HRTE         5A  B 19 35
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20  3'Stop  . . . . . . . . . . . . .'
520 CA            W1HSTP         2Y 0H
520 AA            DSPSTP         3Y 0B 20 35EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21  3'Truck . . . . . . . . . . . . .'
     A            W1HTRK        10A  B 21 35
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TF122.02'
     A                                  1 28'Transfer Order Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  7'Transfer number:'
     A            W2TNUM         7Y 0O  4 24EDTCDE(4)
     A                                  5  2'Requesting warehouse:'
     A            $PWHSE         3Y 0O  5 24EDTCDE(3)
     A                                  5 28'-'
     A            WHDESC        30   O  5 30
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  3'Release order (Y,N) . . . . . .'
     A            W2YSNO         1   B  9 35
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
