     A*%%TS  SD  20001201  140923  HEMANT      REL-V4R2M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    12/01/00  HNK  4.15
     A*     - CREATED
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20001201  140923  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'FSI090.1'
     A                                  1 30'Food Star Interface Test'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Transaction code . . .'
     A            W1CODE         2A  B  7 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                 12  4'Normal quantity  . . .'
     A            W1QTY1         5Y 0B 12 28EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . .'
     A                                  9  4'Department . . . . . .'
     A                                 10  4'Pick slot  . . . . . .'
     A                                 11  4'Item . . . . . . . . .'
     A                                 13  4'Brk 1 quantity . . . .'
     A                                 14  4'Brk 2 quantity . . . .'
     A            W1WHSE         3S 0B  8 28
     A            W1WHDP         5A  B  9 28
     A            W1ISLT        12A  B 10 28
     A            W1ITEM        15A  B 11 28
     A            W1QTY2         3S 0B 13 28
     A            W1QTY3         3S 0B 14 28
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
