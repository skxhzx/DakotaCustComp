     A*%%TS  SD  19940824  122209  DAVE        REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  BT110BFM   Batch control display (All whse)
     A*  24 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19940824  114240  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'BT110B.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 30'Batch Control Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940824  122209  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 45'Position to batch . . . .'
     A            PTINP1         7Y 0B  2 72EDTCDE(4)
     A            FLD001         1   O  3  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940824  122209  DAVE        REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            BTBAT#    R        O 11  2REFFLD(BTREC/BTBAT# *LIBL/BATCTL)
     A                                      EDTCDE(3)
     A            BTTYPE    R        O 11 15REFFLD(BTREC/BTTYPE *LIBL/BATCTL)
     A            BTSTAT    R        O 11 26REFFLD(BTREC/BTSTAT *LIBL/BATCTL)
     A            BTRTE     R        O 11 29REFFLD(BTREC/BTRTE *LIBL/BATCTL)
     A            BTORD     R        O 11 35REFFLD(BTREC/BTORD *LIBL/BATCTL)
     A                                      EDTCDE(4)
     A            BTSTRT    R        O 11 53REFFLD(BTREC/BTSTRT *LIBL/BATCTL)
     A                                      EDTWRD('  :  :  ')
     A            BTENDT    R        O 11 72REFFLD(BTREC/BTENDT *LIBL/BATCTL)
     A                                      EDTWRD('  :  :  ')
     A            W1ENDD         6Y 0O 11 63EDTCDE(Y)
     A            W1STRD         6Y 0O 11 44EDTCDE(Y)
     A            BTWHSE    R        O 11 10REFFLD(BTREC/BTWHSE *LIBL/BATCTL)
     A                                      EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19940824  122209  DAVE        REL-V2R1M1  5738-PW1
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A            FLD001         1A  O  9  2
     A                                 10  2'Batch  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Type    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Stat'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 29'Last'
     A                                      DSPATR(HI)
     A                                  9 35'Last'
     A                                      DSPATR(HI)
     A                                 10 44'     Started     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'     Finished    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'Whse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
