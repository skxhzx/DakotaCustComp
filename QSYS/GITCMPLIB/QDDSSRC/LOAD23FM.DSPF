     A*%%TS  SD  20070615  142033  BFCTEST1    REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  LOAD23FM  RF Loader Module - View Flags by Route
     A*  June 14, 2007
     A*
     A*  Jose Duque
     A*
     A*  Revisions:
     A*
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA10
     A                                      CA12
     A                                      CA21
     A          R TITLE
     A*%%TS  SD  20061025  170138  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'Loadr23'
     A                                  1 10'Route Flags'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19941028  115201  JOSED       REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(02)
     A                                      OVERLAY
     A                                  1  2'Route # . .'
     A            PTINP1         5A  O  1 14
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A*----------------------------------------------------------------
     A* Subfile for list display
     A*
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070615  142033  BFCTEST1    REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1WHDP         4A  O  4  2
     A            W1ITEM         6A  O  4  7
     A            W1STAT         3A  O  4 14
     A  15                                  COLOR(BLU)
     A  16                                  COLOR(YLW)
     A  17                                  COLOR(RED)
     A            W1EMP#         4Y 0O  4 18EDTCDE(Z)
      *----------------------------------------------------------------
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070615  142033  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  3  2'Dept'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  7'Item  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 14'Sta'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 18'Emp#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      *----------------------------------------------------------------
     A          R VW2SFL                    SFL
     A*%%TS  SD  20050331  084611  BFCTEST1    REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1WHDP         4A  O  9  3
     A            W1ITEM         6A  O  9 10
     A            W1STRT         6Y 0O  9 22EDTWRD('0 :  :  ')
     A            W1CLST         6Y 0O  9 32EDTWRD('0 :  :  ')
     A            W1DIFF         6Y 0O  9 42EDTWRD('0 :  :  ')
     A            W1OTRN         7A  O  9 55
     A            W1RTE          5A  O  9 68
     A            W1EMP#         4Y 0O  9 74EDTCDE(Z)
     A*----------------------------------------------------------------
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20050331  084611  BFCTEST1    REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  7 24'Start'
     A                                      DSPATR(HI)
     A                                  7 34'Close'
     A                                      DSPATR(HI)
     A                                  7 55'Flag'
     A                                      DSPATR(HI)
     A                                  8  3'Dept'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 10'Item     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 22'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 32'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 42'  Diff  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 55'Tran#  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 68'Rte#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 75'Emp#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(13)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(14)
     A                                      OVERLAY
     A            MORELN         7A  O  1 15DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
