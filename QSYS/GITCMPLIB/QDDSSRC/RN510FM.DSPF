     A*%%TS  SD  19961002  165405  ROCKY       REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19930527  170033  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'RN510.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 26'Batch Customer Return Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19961002  121037  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OBRWHS    R        O 11  8REFFLD(OBRREC/OBRWHS *LIBL/ORTNBAT)
     A                                      EDTCDE(Z)
     A            OBRORD    R        O 11 13REFFLD(OBRREC/OBRORD *LIBL/ORTNBAT)
     A                                      EDTCDE(Z)
     A            OBRITM    R        O 11 22REFFLD(OBRREC/OBRITM *LIBL/ORTNBAT)
     A            OBRQT1    R        O 11 39REFFLD(OBRREC/OBRQT1 *LIBL/ORTNBAT)
     A                                      EDTCDE(2)
     A            OBRQT2    R        O 11 46REFFLD(OBRREC/OBRQT2 *LIBL/ORTNBAT)
     A                                      EDTCDE(2)
     A            OBRQT3    R        O 11 53REFFLD(OBRREC/OBRQT3 *LIBL/ORTNBAT)
     A                                      EDTCDE(2)
     A            OBREDT    R        O 11 61REFFLD(OBRREC/OBREDT *LIBL/ORTNBAT)
     A                                      EDTWRD('    /  /  ')
     A            OBRSTS    R        O 11 72REFFLD(OBRREC/OBRSTS *LIBL/ORTNBAT)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19961002  165405  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Whse'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 40'Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Brk 1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Brk 2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Expire    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Status  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19930222  104338  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 41'Position to order . . . .'
     A            PTINP2         5Y 0B  3 68EDTCDE(4)
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  2 41'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 68EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WOWHSE         3A  O  2  2
     A                                  2  8'-'
     A            WHDESC        30A  O  2 10
     A            FLD001         1A  O  1  2
     A                                  4 41'Position to item  . . . .'
     A            PTINP3        12A  B  4 68
     A*****************************************************************
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
