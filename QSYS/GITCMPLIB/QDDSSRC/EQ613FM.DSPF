     A*%%TS  SD  20040216  164720  BFC2        REL-V5R2M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  EQ613FM      RF Equipment - Yard Status - Update
     A*  13 February 2004
     A*  Max Blacknik
     A*
     A********************************************************************
     A*  Revisions:
     A*
417 AA*  02/13/04  MLB  4.17
     A*    - Created.
500aAA*    04/28/04  RH   5.00A
     A*      - Increase length of equipment code to 8.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'EQ613.01'
     A                                  1 11'Yard Status'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Equipment:'
500a A*           W1CODE         6A  B  4 14CHECK(ER)
500a A            W1CODE         8A  B  4 14CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'EQ613.02'
     A                                  1 11'Yard Status'
500a A*           W2CODE         6A  O  3  1
500a A            W2CODE         8A  O  3  1
500a A** The following fields moved 2 spaced due to EQCODE increase
     A            W2DESC        30A  O  3 10
     A                                  4  1'Status:'
     A            W2STS          1A  B  4  9
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4 11'I=In'
     A                                  4 16'O=Out'
     A                                  5 11'D=Dock'
     A                                  5 18'N=N/A'
     A                                  6 11'L=Loaded'
     A                                  7  1'F3/F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'EQ613.02'
     A                                  1 11'Yard Status'
500a A*           W2CODE         6A  O  3  1
500a A            W2CODE         8A  O  3  1
500a A** The following fields moved 2 spaced due to EQCODE increase
     A            W2DESC        30A  O  3 10
     A                                  4  2'Loc:'
     A                                  4  8'Dock'
     A                                  5  2'Door:'
     A            W3DOOR         3Y 0B  5  8EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3/F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
