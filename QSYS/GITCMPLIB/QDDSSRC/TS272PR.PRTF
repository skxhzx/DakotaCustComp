     A****************************************************************
     A*
     A* TS272PR   Employee management report for an employee (Summary)
     A* 19 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'TS272'
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(WKEMP# ER271)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1REFFLD(WKNAME ER271)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    12'Direct'
     A                                    24'Cases'
     A                                    40'Cube'
     A                                    53'Weight'
     A                                    68'Cases'
     A                                      SPACEA(001)
     A*
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    12' Hours'
     A                                      UNDERLINE
     A                                    20' Selected'
     A                                      UNDERLINE
     A                                    35' Selected'
     A                                      UNDERLINE
     A                                    50' Selected'
     A                                      UNDERLINE
     A                                    67'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            DIRTOT         5S 2     12EDTCDE(3)
     A            WKPTOT    R             20REFFLD(WKPTOT ER271)
     A                                      EDTCDE(1)
     A            WKCTOT    R             30REFFLD(WKCTOT ER271)
     A                                      EDTCDE(1)
     A            WKWTOT    R             45REFFLD(WKWTOT ER271)
     A                                      EDTCDE(1)
     A            CASEPH         5S 0     68EDTCDE(3)
     A            LINES          5S 0    105EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD           8A        1
     A            T#DTOT         7S 2     10EDTCDE(3)
     A            T#PTOT         7S 0     20EDTCDE(1)
     A            T#CTOT        11S 3     30EDTCDE(1)
     A            T#WTOT        11S 2     45EDTCDE(1)
     A            T#CSPH         7S 0     66EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'TS272: End of Report '
     A*
