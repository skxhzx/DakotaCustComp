     A*%%***********************************************************************
     A*%%TS  RD  19960214  133548  DAVE        REL-V3R1M0  5763-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A*****************************************************************
     F*  Revisions:
409 AF*    07/14/98  RH    4.09
     F*      - Add 'Continued on next page' if IVPMOR=ON
414aAF*    06/22/00  RH    414a
     F*      - Add detail2 to print virtual slots and items
414cAF*    07/13/00  RH    414c
     F*      - Add item description to detail2.
416aAF*    05/14/02  RH    416a
     F*      - Add field to show exception was previously printed.
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A            RPRT           7       109
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            W1WHSE        30A  O    52
     A                                   125
     A                                      'IV345'
     A                                     3
     A                                      'Department:'
     A                                      SPACEB(002)
     A            W1WHDP        45A  O    15
     A                                     3
     A                                      'Section:'
     A                                      SPACEB(001)
     A            W1STYP         5A  O    15
     A                                     3
     A                                      'Aisle:'
     A                                      SPACEB(001)
     A            IVEASL    R        O    +6
     A                                      REFFLD(IVEREC/IVEASL *LIBL/INVEXCP)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 002
     A*%%CL 001
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
416a A                                    23
416a A                                      'Prev.'
     A                                    96
     A                                      '------------ Quantities -----------
     A                                      ---'
     A                                     1
     A                                      'Slot        '
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    15
416a A*                                     'Page #      '
416a A                                      'Page #'
     A                                      UNDERLINE
416a A                                    23
416a A                                      'Print'
416a A                                      UNDERLINE
416a A*                                   29
416a A                                    31
     A                                      'Entered By'
     A                                      UNDERLINE
416a A*                                   41
416a A                                    45
     A                                      'Except'
     A                                      UNDERLINE
     A                                    53
     A                                      'Item                '
     A                                      UNDERLINE
     A                                    73'                    '
     A                                      UNDERLINE
     A                                    96
     A                                      'Normal       '
     A                                      UNDERLINE
     A                                    +2
     A                                      'Brk 1     '
     A                                      UNDERLINE
     A                                    +2
     A                                      'Brk 2     '
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A            IVEDSP    R        O     1
     A                                      REFFLD(IVEDSP INVEXCP)
     A            IVEPAG    R        O    15EDTCDE(4)
     A                                      REFFLD(IVEPAG INVEXCP)
416a A            W1STAT         1A  O    25
416a A*           IVEEBY    R        O    29
416a A            IVEEBY    R        O    31
     A                                      REFFLD(IVEEBY INVEXCP)
416a A            W1EXCP         6A  O    47
416a A*           W1EXCP         6A  O    43
     A                                    53'                    '
     A                                      UNDERLINE
     A                                    73'                    '
     A                                      UNDERLINE
     A                                    96
     A                                      '             '
     A                                      UNDERLINE
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
     A*
414a A          R DETAIL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
414a A                                      SPACEB(001)
414a A            VSLOT     R        O    53
414a A                                      REFFLD(SLDISP SLOT2)
414a A            VITEM     R        O    73
414a A                                      REFFLD(SLITEM SLOT2)
414c A            VDESC     R        O    90
414c A                                      REFFLD(ITDESC PIRITEM)
     A*%%***********************************************************************
     A*%%SS
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 034
     A*%%***********************************************************************
     A                                      SKIPB(050)
     A                                      SPACEA(001)
     A                                     1
     A                                      'Counted by'
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
     A                                    +5
     A                                      'Keyed by'
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
409 AA  25                                +5
409 AA                                      'Continued on next page'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(PIR/IV345PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(66       132      *N     )
     A*%%***********************************************************************
     A*
