     A*%%TS  SD  19970707  060322  DAVE        REL-V3R1M0  5763-PW1
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A* Revisions:
     A*   05/13/04   RH  V4.17
     A*     - Enh: Add option to ignore items with 0 dimensions, cube
     A*            will not be recalculated.
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970707  060322  DAVE        REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'UT840.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 23'Recalculate Item Cube'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 38EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 57'Code'
     A                                  9  4'Item . . . . . . . . . . . . . .'
     A            W1ITEM        15A  B  9 38
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 57'Name, *All'
417  A                                 11  4'Ignore items without dimensions.'
417  A            W1IITM         1A  B 11 38
417  A N24N90                               DSPATR(HI)
417  A N24N90                               DSPATR(UL)
417  A  24                                  DSPATR(RI)
417  A  04                                  DSPATR(PC)
417  A  90                                  DSPATR(PR)
417  A                                 11 57'Y=Yes, N=No'
     A                                 13  4'Place on job queue ? . . . . . .'
     A            W1JOBQ         1A  B 13 38
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 57'Y=Yes, N=No'
     A                                 23  2'F3=Exit  F4=Select  F5=Refresh  F-
     A                                      12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
