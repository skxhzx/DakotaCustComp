     A*%%TS  SD  20051227  071302  DAVE        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTTAGFMTF  Test Print Clam Tag labels
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
     A*    12/26/05  JCJ  5.00
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20051227  071302  DAVE        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTTAGFMTF.1'
     A                                  1 28'Test Print Clam Tag label'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  6'Command code . . . . .'
     A            W1CMD          8A  B  5 30
     A                                  6  6'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  6 30EDTCDE(4)
     A                                  7  6'PO number  . . . . . .'
     A            W1PO           9A  B  7 30
     A                                  8  6'Sequence number. . . .'
     A            W1SEQ          5Y 0B  8 30EDTCDE(4)
     A                                  9  6'Item . . . . . . . . .'
     A            W1ITEM        15A  B  9 30
     A                                 10  6'Description  . . . . .'
     A            W1DESC        30A  B 10 30CHECK(LC)
     A                                      DSPATR(HI)
     A                                 11  6'License  . . . . . . .'
     A            W1LIC         15A  B 11 30
     A                                 12  6'Quantity . . . . . . .'
     A            W1QTY          5Y 0B 12 30EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                 13  6'Harvest date . . . . .'
     A            W1HDTE         8Y 0B 13 30EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                 14  6'Harvest location . . .'
     A            W1HLOC        20A  B 14 30DSPATR(HI)
     A                                 15  6'Original Shipper Cert#'
     A            W1CERT        20A  B 15 30DSPATR(HI)
     A                                 16  6'Label number . . . . .'
     A            W1LBL#         7Y 0B 16 30EDTCDE(Z)
     A                                 17  6'Printer device . . . .'
     A            W1PRT         10A  B 17 30DSPATR(HI)
     A                                 18  6'Form type  . . . . . .'
     A            W1FORM        10A  B 18 30DSPATR(HI)
     A                                 23  2'F3=Exit             F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
