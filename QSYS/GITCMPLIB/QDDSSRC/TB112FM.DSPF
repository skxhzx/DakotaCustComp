     A*%%TS  SD  20080708  095640  RETHA       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TB112FM   Zone Group Code maintenance - Change screen
     A*  02 April, 2010
     A*  Retha Davis
     A*
     A*  Revisions
     A*    04/02/10  RBD  5.30
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080708  095640  RETHA       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TB112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 27'Zone Group Code Maintenance'
     A                                      DSPATR(HI)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 13EDTCDE(3)
     A                                  5 17'-'
     A            WHDESC        30A  O  5 19
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Zone Group Code  . . .'
     A            W1ZONG    R        B 10 28REFFLD(ZGREC/ZGZONG *LIBL/ZONEG)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Zone . . . . . . . . .'
     A            W1SZON    R        B 11 28REFFLD(ZGREC/ZGSZON *LIBL/ZONEG)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
