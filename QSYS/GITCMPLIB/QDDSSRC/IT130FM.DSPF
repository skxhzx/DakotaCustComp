     A*%%TS  SD  20090210  064038  DAVE        REL-V6R1M0  5761-WDS
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/24/99  DAS  4.13
     A*     - Removed designation info line.
     A*     - Moved item headings up one line.
     A*     - Changed hard code 'Item' and 'Base' labels to output fields
     A*       that will be initialized in the program.
     A*    09/22/00  RH   4.14a
     A*     - Add extended description option and print line.
415 AA*    05/15/01  HNK  4.15
     A*     - Add space to clear leftover fields on line 12.
415aAA*    05/17/01  DAS  4.15a
     A*     - Revised to use a dummy field instead of a space
     A*       on line 12.
416aAA*    10/17/01  RH   4.16a
     A*     - Add Pack description (W1PDSC) to screen 1.
     A*
500 AA*    03/29/05  DAS  5.00
     A*     - Added slot position to list.
     A*
600aAA*    02/10/05  DAS  6.00a
     A*     - Removed slot position.
     A*     - Added license
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19930313  135658  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'IT130.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 26'List Display in Picking Order'
     A                                      DSPATR(HI)
     A                                  1 30'Item Slot Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19991124  110300  DAVE        REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            W1ITEM        55A  O  4 25
     A                                  2  2'Warehouse            :'
     A                                  3  2'Department           :'
     A            WDCODE         5A  O  3 25
     A            WDDESC        30A  O  3 31
     A            WOWHSE        35A  O  2 25
414a A**          W1BITM        55A  O  5 25
     A            W1LBL         22   O  4  2
416a A                                  5  2'Pack:'
416a A            W1PDSC        15   O  5  8
414a A            W1DSC2        40   O  5 25
414a A**          W1LBLB        22   O  5  2
414a A            W1LBLB        22   O  6  2
414a A            W1BITM        55A  O  6 25
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20090210  064038  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 14  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLSTAT    R        O 14  7REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O 14 13
     A            W1PRTY    R        O 14 19REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O 14 25EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O 14 34EDTWRD('  /  /  ')
     A            SLDISP        12A  O 14 43
     A            W1LCNS        14A  O 14 56
     A            W1QTY         10A  O 14 71
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20090210  064038  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
415aDA**                               12  2 ' '
     A            DUMMY1         1A  O 12  2
     A                                 13  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 43'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 16'Priority'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 34'Entered '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 25'Expires '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 11'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13  6'Stat'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 13 71'Avail Qty '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 56'License       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
