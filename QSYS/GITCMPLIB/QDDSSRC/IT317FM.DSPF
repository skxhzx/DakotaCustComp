     A*%%TS  SD  19941109  162934  HEMANT      REL-V2R1M1  5738-PW1
     A*****************************************************************
     A*
     A*  IT322FM   View warehouse book by item.
     A*  11 December 1993
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*---------------------------------------------------------------*
     A*  Revisions:
520 AA*    09/23/09  MLB  5.20
     A*      - Enh: Revised REFFLD statements that have part of file
     A*        name on 2nd line.  Was causing problems for Webfacing
     A*        conversion program.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19941109  162934  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Inventory  Adjustments'
     A                                      DSPATR(HI)
     A                                  1  2'IT317'
     A            W1FMDT         6Y 0O  2 30EDTCDE(Y)
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0O  2 44EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  2 40'to'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19941109  161411  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHDESC        30A  O  1 10
     A                                  1  8'-'
     A                                  2 41'Position to item  . . .'
     A            WOWHSE         3A  O  1  2
     A            PTINP1        15A  B  2 65
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19941109  161411  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            IAITEM        15A  O  9 11
     A            QTY           10A  O  9 65
520 DA*           IACODE    R        O  9 77REFFLD(IAREC/IACODE *LIBL/ITEMAD-
520 MA            IACODE    R        O  9 77REFFLD(IAREC/IACODE *LIBL/ITEMADJ)
520 DA*                                     J)
     A            IABY      R        O  9 54REFFLD(IAREC/IABY *LIBL/ITEMADJ)
     A            W1DATE         6Y 0O  9  2EDTCDE(Y)
     A            W1DESC        20A  O  9 27
     A            W1TIME         4Y 0O  9 48EDTWRD('  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19941109  140503  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0026)
     A                                      SFLPAG(0013)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  8 11'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 65'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  2'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 27'Description         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 48'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 54'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 76'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
