     A*%%TS  SD  20160324  100538  RODR        REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  OP420FM   PIR Options - Adjustment Interface
     A*  22 March 2016
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*    03/22/16  RTR  7.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20160324  100538  RODR        REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'OP420.01'
     A                                  1 27'Adjustment Interface Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Use movement file (LICADJ) . . . .'
     A            W1ULAF         1A  B  6 40
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  6'Adj. code for transfers/changes '
     A            W1ADJC         2A  B  7 40
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
