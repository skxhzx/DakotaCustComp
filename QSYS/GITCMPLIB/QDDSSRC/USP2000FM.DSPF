     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A          R USP200001                 SFL
     A            QUAL_JOB      28A  H
     A            QUSIJI        16A  H
     A            OPTION         2Y 0B  9  3EDTCDE(Z)
     A            QUSSN00       10A  O  9  7
     A            QUSJNU        10A  O  9 18
     A            QUSUNU        10A  O  9 29
     A            QUSJNBR07      6   O  9 40
     A            QUSCUN        10A  O  9 47
     A            QUSJUIP       15A  O  9 59
     A            OPENFIL        4  0O  9 75EDTCDE(Z)
     A          R USP200002                 SFLCTL(USP200001)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A  40                                  SFLDSP
     A  41                                  SFLDSPCTL
     A  42                                  SFLCLR
     A  43                                  SFLEND(*MORE)
     A            BMD            4S 0H      SFLRCDNBR(CURSOR)
     A                                  1 32'Work with ODBC Jobs'
     A                                      DSPATR(HI)
     A                                  1  2SYSNAME
     A                                  2  2USER
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  3  3'Select one of the following:'
     A                                      COLOR(WHT)
     A                                  3 50'Number of jobs listed-'
     A            MAX_RRN        4Y 0    +01EDTCDE(Z)
     A                                  4  5'1=Job Status'
     A                                      COLOR(BLU)
     A                                  4 20'2=Job Definition'
     A                                      COLOR(BLU)
     A                                  4 39'3=Run Attributes'
     A                                      COLOR(BLU)
     A                                  4 58'4=Spool Files'
     A                                      COLOR(BLU)
     A                                  5  4'10=Job Log'
     A                                      COLOR(BLU)
     A                                  5 19'11=Call Stack'
     A                                      COLOR(BLU)
     A                                  5 38'12=Locks'
     A                                      COLOR(BLU)
     A                                  5 57'13=Library List'
     A                                      COLOR(BLU)
     A                                  6  4'14=Open Files'
     A                                      COLOR(BLU)
     A                                  6 19'15=Overrides'
     A                                      COLOR(BLU)
     A                                  6 38'16=Commitment Status'
     A                                      COLOR(BLU)
     A                                  6 61'99=Kill Job'
     A                                      COLOR(BLU)
     A*
     A                                  7 32'Job'
     A                                      DSPATR(HI)
     A                                  7 41'Job'
     A                                      DSPATR(HI)
     A                                  7 47'Servicing'
     A                                      DSPATR(HI)
     A                                  7 75'Open'
     A                                      DSPATR(HI)
     A*
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                  8  7'Subsystem'
     A                                      DSPATR(HI)
     A                                  8 18'Job Name'
     A                                      DSPATR(HI)
     A                                  8 29'User Name'
     A                                      DSPATR(HI)
     A                                  8 40'Number'
     A                                      DSPATR(HI)
     A                                  8 47'User Name'
     A                                      DSPATR(HI)
     A                                  8 59'IP Address'
     A                                      DSPATR(HI)
     A                                  8 75'Files'
     A                                      DSPATR(HI)
     A*************************************************************
     A          R WWMSGSFL                  SFL
     A                                      TEXT('MESSAGE SUBFILE')
     A                                      SFLMSGRCD(24)
     A            @MSGK                     SFLMSGKEY
     A            @PGMQ                     SFLPGMQ(10)
     A*************************************************************
     A          R USP200003                 SFLCTL(WWMSGSFL)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('MESSAGE SUBFILE CONTROL RECOR-
     A                                      D')
     A  11                                  LOCK
     A  11                                  FRCDTA
     A*                                     OVERLAY
     A N12                                  SFLDSP
     A N12                                  SFLDSPCTL
     A N12                                  SFLINZ
     A N12                                  SFLEND
     A            @PGMQ                     SFLPGMQ(10)
     A                                 22  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 22 12'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22 25'F12=Cancel'
     A                                      COLOR(BLU)
