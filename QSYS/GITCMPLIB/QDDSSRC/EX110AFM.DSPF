     A*%%TS  SD  20000714  221946  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EX110FM   Clear Item Replenishment Exceptions (one whse) - Display
     A*  14 July 2000
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     F*
415 AF*    05/18/01  MLB  4.15
     F*      - Revised display file to not allow F8 to be used.
     F*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
415 DA*                                     CA08
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'EX110.01'
     A                                  1 25'Clear Item Replenishment Exception-
     A                                      s'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 36'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  2  2
     A                                  2  8'-'
     A            WHDESC        30A  O  2 10
     A                                  2 41'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A                                  3 41'Position to item  . . .'
     A            PTINP2        15   B  3 66
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1ITEM        15A  O 11  6
     A            W1WHDP         5A  O 11 22
     A            W1STYP         1A  O 11 29
     A            W1FRSL    R        O 11 34REFFLD(EXREC/EXFDIS *LIBL/ITMEXCP)
     A            W1TOSL    R        O 11 48REFFLD(EXREC/EXTDIS *LIBL/ITMEXCP)
     A            W1TRQY         7Y 0O 11 62EDTCDE(J)
     A            W1STAT         6   O 11 73
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 62'Transfer'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Sect'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'From Slot   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'To Slot     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2PDSC    R        O 11  5REFFLD(ITREC/ITPDSC *LIBL/PIRITEM)
     A            W2DESC    R        O 11 21REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A            W2GDTE         6Y 0O 11 52EDTCDE(Y)
     A            W2GTIM    R        O 11 61REFFLD(EXREC/EXGTIM *LIBL/ITMEXCP)
     A                                      EDTWRD(' 0:  :  ')
     A            W2GBY     R        O 11 70REFFLD(EXREC/EXGBY *LIBL/ITMEXCP)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 52'---- Created ----'
     A                                      DSPATR(HI)
     A                                  9 70'Created'
     A                                      DSPATR(HI)
     A                                 10  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Size           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'By        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3TASK    R        O 11  7REFFLD(EXREC/EXTASK *LIBL/ITMEXCP)
     A            W3ERDS        25A  O 11 15
     A            W3USER    R        O 11 42REFFLD(EXREC/EXUSER *LIBL/ITMEXCP)
     A            W3CDTE         6Y 0O 11 54EDTCDE(Y)
     A            W3CTIM    R        O 11 64REFFLD(EXREC/EXCTIM *LIBL/ITMEXCP)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 42'Closed'
     A                                      DSPATR(HI)
     A                                  9 54'----- Closed -----'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Error Description        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'By        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Clear exception.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    Clear lines in subfile body.
     A*
     A          R CLR1120
     A                                      OVERLAY
     A                                      SLNO(11)
     A                                      CLRL(10)
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
