     A*%%TS  SD  20110803  135416  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TL112FM   Task Detail File maintenance - Change screen
     A*  03 August, 2011
     A*  Ginger Andrews
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20110803  135416  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TL112.01'
     A                                  1 30'Task Detail File Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Code . . . . . . . . .'
     A            W1CODE         6A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Description  . . . . .'
     A            W1DESC        30A  B 11 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Category . . . . . . .'
     A            W1CAT          6A  B 12 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 13  4'Sub category . . . . .'
     A            W1SUB          6A  B 13 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 14  4'Type . . . . . . . . .'
     A            W1TYPE         1A  B 14 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 44'P=Payable N=Non-Payable'
     A                                 15  4'Minutes. . . . . . . .'
     A            W1MINS         3Y 2B 15 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(L)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
