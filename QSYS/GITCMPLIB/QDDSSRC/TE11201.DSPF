     A*%%TS  SD  20031105  112928  ROCKY       REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TE112FM   Task entry
     A*  28 December, 1995
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
409 AA*  06/04/98  HNK  4.09
     A*    - Command 20 added to screen 2.
413aAA*    03/02/00  RH   4.13a
     A*      - Add warehouse, department and section
     A*        to screen 1.
415 AA*    12/06/00  HNK  4.15
     A*      - Position cursor to employee number.
     A*      - Dept and section entry field not highlighted.
417aAA*    11/05/03  RH   4.17a
     A*      - Add screen 4 to allow scan of multiple transactions.
640aAA*    04/29/11  JCJ  6.40a
     A*      - Fix: remove the CHECK(ER) keyword from the W1EMP# field
     A*        to fix webfacing handling of auto advance to the next
     A*        screen.  Renamed screen format to TE11201.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SCREEN1
     A*%%TS  SD  19960109  100641  DAVE        REL-V3R1M0  5763-PW1
     A                                      CA03
417a A                                      CF06
417a A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TE112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Task Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
413aAA                                  9  3'Warehouse . . . . . . . . . . . .'
413aAA            W1WHSE         3Y 0B  9 38EDTCDE(4)
413aAA  23                                  DSPATR(RI)
413aAA N23N90                               DSPATR(UL)
413aAA  03                                  DSPATR(PC)
413aAA  90                                  DSPATR(PR)
413aAA N22N90                               DSPATR(HI)
413aAA                                 10  3'Department  . . . . . . . . . . .'
413aAA            W1WHDP         5A  B 10 38
413aAA  22                                  DSPATR(RI)
413aAA N22N90                               DSPATR(UL)
415 MA  02                                  DSPATR(PC)
413aAA  90                                  DSPATR(PR)
415 DA*N22N90                               DSPATR(HI)
413aAA                                 11  3'Section . . . . . . . . . . . . .'
413aAA            W1STYP         1A  B 11 38
413aAA  24                                  DSPATR(RI)
413aAA N24N90                               DSPATR(UL)
413aAA  04                                  DSPATR(PC)
413aAA  90                                  DSPATR(PR)
415 DA*N24N90                               DSPATR(HI)
413aDA*                                 9  3'Employee number . . . . . . . . .'
413aDA*           W1EMP#    R     Y00B  9 38REFFLD(USEMP# *LIBL/PIRUSER)
413aMA                                 12  3'Employee number . . . . . . . . .'
413aMA            W1EMP#    R     Y00B 12 38REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(Z)
640aD ***                                   CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
415 MA                                      DSPATR(PC)
415 DA* 01                                  DSPATR(PC)
413aAA N21N90                               DSPATR(HI)
     A                                      CHECK(FE)
417a A                                 23  2'F3=Exit  F6=Start Multi Trans-
417a A                                        F9=End Multi Trans'
417a A*                                23  2'F3=Exit'
     A          R SCREEN2
     A*%%TS  SD  19960109  100956  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF06
     A                                      CF10
     A                                      CA12
409 AA                                      CF20
     A  98                                  ALARM
     A                                  1  2'TE112.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Task Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  8'Employee:'
     A            USEMP#    R        O  9 19REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A                                  9 25'-'
     A            W2NAME        40A  O  9 27
     A                                 10  4'Current task:'
     A            W2CTSK         6A  O 10 19
     A            W2DESC        30A  O 10 26
     A                                 11  9'Started:'
     A            W2DATE         6Y 0O 11 19EDTWRD('  /  /  ')
     A            W2TIME         6Y 0O 11 29EDTWRD('  :  :  ')
     A                                 13  3'Task . . . . . . . . . .'
     A            W2TASK         7A  B 13 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 23  2'F6=Enter other tasks  F10=End curr-
409 MA                                      ent task  F12=Cancel  F20 = Accept -
409 AA                                      warning'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19960117  143536  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF09
     A                                      CA12
     A  98                                  ALARM
     A                                  1  2'TE112.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Task Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9 18'Employee:'
     A            USEMP#    R        O  9 29REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A            W3NAME        40A  O  9 36
     A                                 10  2'Last manual task entered:'
     A            W3MTSK         6A  O 10 28
     A            W3DESC        30A  O 10 36
     A                                 11 19'Started:'
     A            W3SLDT         6Y 0O 11 28EDTWRD('  /  /  ')
     A            W3SLTM         6Y 0O 11 37EDTWRD('  :  :  ')
     A                                 12 21'Ended:'
     A            W3ELDT         6Y 0O 12 28EDTWRD('  /  /  ')
     A            W3ELTM         6Y 0O 12 37EDTWRD('  :  :  ')
     A                                 14  3'Task . . . . . . . . . .'
     A            W3TASK         7A  B 14 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 15  3'Start date . . . . . . .'
     A            W3SDTE         6Y 0B 15 29EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(CS)
     A  02                                  DSPATR(PC)
     A                                 15 45'MMDDYY'
     A                                 16  3'Start time . . . . . . .'
     A            W3SHRS         2Y 0B 16 29EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(CS)
     A  03                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 16 32':'
     A            W3SMIN         2Y 0B 16 34EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(CS)
     A  04                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3SAP          1   B 16 38
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(CS)
     A  05                                  DSPATR(PC)
     A                                 16 45'HH:MM A,P'
     A                                 18  3'End date . . . . . . . .'
     A            W3EDTE         6Y 0B 18 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(CS)
     A  06                                  DSPATR(PC)
     A                                 18 45'MMDDYY'
     A                                 19  3'End time . . . . . . . .'
     A                                 19 45'HH:MM A,P'
     A            W3EHRS         2Y 0B 19 29EDTCDE(4)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(CS)
     A  07                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 19 32':'
     A            W3EMIN         2Y 0B 19 34EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(CS)
     A  08                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3EAP          1   B 19 38
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(CS)
     A  09                                  DSPATR(PC)
     A                                 23  2'F9=Transaction maintenance  F12=Ca-
                                            ncel'
     A                                      COLOR(BLU)
417a A          R SCREEN4
     A*%%TS  SD  19960109  100956  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF09
     A  98                                  ALARM
     A                                  1  2'TE112.04'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Task Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  8'Employee:'
     A            USEMP#    R        O  9 19REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A                                  9 25'-'
     A            W4NAME        40A  O  9 27
     A                                 10  4'Current task:'
     A            W4CTSK         6A  O 10 19
     A            W4DESC        30A  O 10 26
     A                                 11  9'Started:'
     A            W4DATE         6Y 0O 11 19EDTWRD('  /  /  ')
     A            W4TIME         6Y 0O 11 29EDTWRD('  :  :  ')
     A                                 12  8'Scanned :'
     A            W4SCNT         3Y 0O 12 19EDTCDE(4)
     A                                 13  8'Last transaction:'
     A            W4LAST         7Y 0O 13 27EDTCDE(4)
     A                                 15  3'Scan transaction . . . .'
     A            W4TASK         7A  B 15 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 23  2'F9=Complete '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
