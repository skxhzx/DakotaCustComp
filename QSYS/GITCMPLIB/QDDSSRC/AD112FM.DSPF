     A*%%TS  SD  20060922  141257  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  AD112FM    Inventory adjustment code maintenance
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    10/29/01  DAS  4.16
     A*      - Add new 'System to Update' field.
     A*
510  A*    06/15/06  RMC  5.10
     A*      - Enh: Create record format SCRPCK1 which is a duplicate
     A*      - of SCREEN1 and added fields from SAPMOVE file. PACKERS
     A*
510b A*    09/19/06  RAH  5.10b
     A*      - Enh: Added additional fields from SAPMOVE to PACKERS
     A*        screen.
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20011029  152607  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'AD112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Code . . . . . . . . .'
     A                                  9  4'Description  . . . . .'
     A            W1CODE         2A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A            W1DESC        30A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 19'Inventory Adjustment Code Table Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
416 AA                                 11  4'System to update . . .'
416 AA            W1SYS          1   B 11 28
416 AA  23                                  DSPATR(RI)
416 AA  03                                  DSPATR(PC)
416 AA  90
416 AA N23N90                               DSPATR(HI)
416 AA N23N90                               DSPATR(UL)
510  A  90                                  DSPATR(PR)
416 AA                                 11 64'D = Dakota'
416 AA                                 12 64'H = Host'
416 AA                                 13 64'B = Both'
     A*****************************************************************
510  A*Version 5.10 display SAPMOVE fields for Packers
     A*****************************************************************
     A          R SCRPCK1
     A*%%TS  SD  20060615  162239  ROBIN       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'AD112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Code . . . . . . . . .'
     A                                  9  4'Description  . . . . .'
     A            W1CODE         2A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A            W1DESC        30A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 19'Inventory Adjustment Code Table Ma-
     A                                      intenance'
     A                                      DSPATR(HI)
     A                                 11  4'System to update . . .'
     A            W1SYS          1A  B 11 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90N23N90                            DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 11 64'D = Dakota'
     A                                 12 64'H = Host'
     A                                 13 64'B = Both'
     A                                 14  4'SAP reason code. . . .'
     A                                 15  4'SAP movement type. . .'
     A                                 16  4'SAP stock indicator. .'
510bAA                                 17  4'SAP cost center. . . .'
510bAA                                 18  4'SAP profit center. . .'
     A            W1SAPC         4   B 14 28
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A            W1MOVE         3   B 15 28
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A            W1STCK         1   B 16 28
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90N24N90                            DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
510bAA            W1COST         4  0B 17 28
510bAA  90                                  DSPATR(PR)
510bAA N90                                  DSPATR(UL)
510bAA            W1PRFT         4  0B 18 28
510bAA  90                                  DSPATR(PR)
510bAA N90                                  DSPATR(UL)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
