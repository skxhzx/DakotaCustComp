     A*%%TS  SD  19990924  155602  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RN146FM    License entry Cust. returns
     A*  02 July, 1997
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
412 AA*  09/24/99  RH   4.12
     A*    - Add "Directed Put Away" to screen 1 head.
416 AF*    05/23/02  RRH  4.16
     F*      - Increase W1LCNS to 15.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  19990513  114117  QSECOFR     REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1TRN#         7Y 0O 12  2EDTCDE(4)
     A            W1QTY          5Y 0O 12 17EDTCDE(3)
     A            W1TIE          4Y 0O 12 29EDTCDE(4)
     A                                 12 34'/'
     A            W1HI           2Y 0O 12 36EDTCDE(4)
     A            W1DISP        12A  O 12 42
416 DA*           W1LCNS         7Y 0B 12 57EDTCDE(4)
416 MA            W1LCNS        15A  B 12 57
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*           W1TRN#         7Y 0O 12  2DSPATR(ND)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  19990924  155602  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'RN146.01'
     A                                  1 35'Customer Returns'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'License Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Order number.'
     A            W1ORD          7Y 0O  4 16EDTCDE(4)
     A                                  5  2'Warehouse . .'
     A            $PWHSE         3Y 0O  5 16EDTCDE(4)
     A                                  5 20'-'
     A            WHDESC        30A  O  5 22
     A                                  6  2'Item number .'
     A            WOITEM        65A  O  6 16
     A            WODSC1        40A  O  7 16
     A            WODSC2        40A  O  8 16
     A                                  9  2'Quantity received . . '
     A            W1QRC1         5Y 0O  9 25EDTCDE(4)
     A            W1QRC2         5Y 0O  9 39EDTCDE(4)
     A            W1QRC3         5Y 0O  9 53EDTCDE(4)
     A                                 11  2'Transaction'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 17'Quantity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 29'Tie/High '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 42'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 57'License'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 35'Directed Put Away'
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
