     A*%%TS  SD  20130222  173045  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  GTS112FM   Global Tomcat server maintenance
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
650 AA*    02/22/13  DAS  6.50
     A*      - Created
     A*
700aAA*    08/28/15  DAS  7.00
     A*      - Added cluster and debug
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20130222  173045  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'GTS112.01'
     A                                  1 25'Global Tomcat Server Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Code . . . . . . .'
     A            W1CODE        10A  B  6 23
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  7  4'Description  . . .'
     A            W1DESC        30A  B  7 23CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aMA                                  8  4'Port . . . . . . .'
700aMA            W1PORT         5Y 0B  8 23EDTCDE(Z)
700aMA                                  8 30'(Informational only)'
700aAA                                  9  4'Cluster  . . . . .'
700aAA            W1CLSTR        1A  B  9 23
700aAA  23                                  DSPATR(RI)
700aAA N23N90                               DSPATR(UL)
700aAA  03                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                  9 30'Y,N'
700aAA                                 10  4'Debug  . . . . . .'
700aAA            W1DEBUG        1A  B 10 23
700aAA  24                                  DSPATR(RI)
700aAA N24N90                               DSPATR(UL)
700aAA  04                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                 10 30'Y,N'
     A                                 11  4'User . . . . . . .'
     A            W1USER        10A  B 11 23
     A                                 13  4'Tomcat path  . . .'
     A            W1TOMPATH     50A  B 13 23CHECK(LC)
     A                                 15  4'App location . . .'
     A            W1APPLOC      20A  B 15 23CHECK(LC)
     A                                 15 44'(blank for Production Webfacing)'
     A                                 17  4'JDK path . . . . .'
     A            W1JDKPATH    100A  B 17 23CHECK(LC)
     A                                 20  8'Actual port used is defined in <to-
     A                                      mpath>/<apploc>/conf/server.xml'
     A                                      DSPATR(HI)
     A                                 20 78' '
     A                                 21 14'startup.sh and shutdown.sh are run-
     A                                       from <tompath>/bin/'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F5=Refresh   F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23 36' '
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
