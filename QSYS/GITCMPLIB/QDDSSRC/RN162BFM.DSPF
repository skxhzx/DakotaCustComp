     A*%%TS  SD  20051021  175114  BOBP        REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  RN162BFM  View Customer returns report. Date/item
     A*  12 May 1999
     A*  Roxanne Harrison
     A*
     A*  Revisions:
410 AA*    12/07/98  RH   4.10
     A*      - Add Adjustment code PARM to screen.
     A*
500bAA*    10/21/05  RLP  5.00b
     A*      - Enh: Added ORTRN# to query file RN161A to get original
     A*        TRN# instead of Batch# - plugging ORTRN# into View2
     A*
530a A*    11/24/09  GJA  5.30a
     A*      - Enh: Added ORINV# to query file RN161A to get invoice#
     A*        added view4, plugging ORTRN# into View4
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19941109  162934  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 34'Customer Returns'
     A                                      DSPATR(HI)
     A                                  1  2'RN162B'
     A                                  2 30'  By Date/Item        '
     A                                      DSPATR(HI)
     A            W1FMDT         6Y 0O  3 30EDTCDE(Y)
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0O  3 44EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  3 40'to'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19960612  144124  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(04)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A                                  2  2'Item:'
     A            W1ITEM        15A  O  2  8
     A                                  2 50'Return code:'
410 AA            W1CODE         4A  O  2 63
     A                                  3 41'Position to date  . . .'
     A            PTINP1         6A  B  3 65
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960613  085113  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            W1DATE         6Y 0O  9  2EDTCDE(Y)
     A            W1TIME         4Y 0O  9 11EDTWRD('  :  ')
     A            ORITEM    R        O  9 18REFFLD(OQREC/ORITEM *LIBL/RN161B)
     A            ORRUSR    R        O  9 34REFFLD(OQREC/ORRUSR *LIBL/RN161B)
     A            QTY           10A  O  9 45
     A            ORRCOD    R        O  9 57REFFLD(OQREC/ORRCOD *LIBL/RN161B)
     A            ORCITM    R        O  9 62REFFLD(OQREC/ORCITM *LIBL/RN161B)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960613  085113  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  8  2'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 11'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 18'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 34'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 45'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 57'Code'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 62'Corresponding Item'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20051021  175114  BOBP        REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1DATE         6Y 0O  9  2EDTCDE(Y)
     A            ORITEM    R        O  9 12REFFLD(OQREC/ORITEM *LIBL/RN161B)
     A            W1DESC        16A  O  9 28
     A            QTY           10A  O  9 45
     A            ORRCOD    R        O  9 57REFFLD(OQREC/ORRCOD *LIBL/RN161B)
     A            W1CWTA         7Y 2O  9 61EDTCDE(M)
500bAA            ORTRN#    R        O  9 71REFFLD(OQREC/ORTRN# *LIBL/RN161A)
500bAA                                      EDTCDE(4)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20051021  171253  BOBP        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                  8  2'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 12'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 28'Description     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 45'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 56'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 61'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
500bAA                                  8 71'Tran#  '
500bAA                                      DSPATR(UL)
500bAA                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19990512  160335  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1DATE         6Y 0O  9  2EDTCDE(Y)
     A            W1TIME         4Y 0O  9 12EDTWRD('  :  ')
     A            ORITEM    R        O  9 19REFFLD(OQREC/ORITEM *LIBL/RN161B)
     A            ORRUSR    R        O  9 36REFFLD(OQREC/ORRUSR *LIBL/RN161B)
     A            ORRMEM    R        O  9 50REFFLD(OQREC/ORRMEM *LIBL/RN161B)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19960613  085113  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO3         4S 0H      SFLRCDNBR
     A                                  8  2'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 12'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 18'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 36'User      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 48'Memo                          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 4
     A*
     A          R VW4SFL                    SFL
     A*%%TS  SD  19990512  133839  ROCKY       REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            ORITEM    R        O  9  2REFFLD(OQREC/ORITEM *LIBL/RN161A)
     A            W1DATE         6Y 0O  9 19EDTCDE(Y)
     A            W1TIME         4Y 0O  9 29EDTWRD('  :  ')
     A            ORINV#    R        O  9 36REFFLD(OQREC/ORINV# *LIBL/RN161A)
     A            ORORD     R        O  9 50REFFLD(OQREC/ORORD  *LIBL/RN161A)
     A                                      EDTCDE(Z)
     A            OROORD    R        O  9 60REFFLD(OQREC/OROORD *LIBL/RN161A)
     A                                      EDTCDE(Z)
     A*
     A          R VW4SFC                    SFLCTL(VW4SFL)
     A*%%TS  SD  19990512  133839  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO4         4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 19'Date    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 29'Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 36'Invoice     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 50'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 60'Org Order '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
