     A*%%TS  SD  20080311  134529  GINGERA     REL-V5R4M0  5722-WDS
     A*%%EC
     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  OR330FM    Order Lookup by Item
     A*  21 March 2008
     A*  Ginger Andrews
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*  03/21/08  GJA  5.20  P#00023
     A*    - Created.
710aAA*  09/18/17  JCJ  7.10
     A*    - Renamed to OR330FM
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080311  134529  GINGERA     REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OR330.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 21'View/Print Order Lookup by Item'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  2  2USER
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  9 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 53'Code'
     A                                 10  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B 10 34EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 42'to'
     A            W1TODT         6Y 0B 10 46EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 53'MMDDYY'
     A                                 11  4'Item . . . . . . . . . . . .'
     A            W1ITEM        15A  B 11 34
     A N29N90                               DSPATR(HI)
     A N29N90                               DSPATR(UL)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'Name'
     A                                 12  4'View or Print  . . . . . . .'
     A            W1VP           1A  B 12 34
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 53'V=View, P=Print'
     A                                 13  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 13 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 53'Y=Yes, N=No'
     A                                 14  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 14 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 15 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
