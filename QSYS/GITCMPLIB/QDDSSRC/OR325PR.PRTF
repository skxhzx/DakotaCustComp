     A*******************************************************************
     A*  Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*  BFC Software, Inc.                                             *
     A*  245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*  West Chicago, IL  60185                                        *
     A*  (630) 562-0375                                                 *
     A*******************************************************************
     A*
     A* OR325PR   PRINT SOS SLOT TRANSFER QUANTITY
     A* 1 June 2011
     A* Roxanne Harrison
     A*
     A****************************************************************
     A* Compile Options:
     A*   PAGESIZE(66 80)
     A*
     A****************************************************************
     A* Revision
     A*
640 AA* 06/01/11  RH   6.40
     A*   - Created.
     A*
     A****************************************************************
     A* Client Custom Revisions:
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            RDATE          6S 0O     2
     A                                      EDTCDE(Y)
     A            REPRNT        10A  O    14
     A            RPTTTL        40A  O    29
     A                                    73
     A                                      DFT('Page')
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            RTIME          6S 0O     2
     A                                      EDTWRD('  :  :  ')
     A            WHSE          40A  O    29
     A                                    76
     A                                      DFT('OR325')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEB(001)
     A                                     2'Route:'
     A            RHRTE     R        O     9REFFLD(RHRTE RTEHED)
     A                                    16'RtID:'
     A            RHRTID    R        O    22REFFLD(RHRTID RTEHED)
     A
     A  91                                70
     A                                      DFT('(Continued)')
     A*
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    55'Transfer'
     A                                    66'Cust'
     A                                      SPACEA(001)
     A                                     2'Slot'
     A                                    16'Item'
     A                                    33'Description'
     A                                    55'Qty  '
     A                                    66'Order'
     A                                    74'Pallet '
     A                                      SPACEA(001)
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
417aDA*                                     SPACEB(001)
417aMA                                      SPACEB(002)
     A            P1DISP    R        O     2REFFLD(LBDISP LABEL)
     A            P1ITEM    R        O    16REFFLD(LBITEM LABEL)
     A            P1DESC        21A  O    33
     A            P1QYSO         9S 0O    56
     A                                      EDTCDE(4)
     A            P1CORD    R        O    66REFFLD(OHCORD ORDH)
     A                                      EDTCDE(4)
     A            P1PAL     R        O    74REFFLD(LBPAL# LABEL)
     A*
     A
     A*
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('OR325: End of Report ')
     A*
