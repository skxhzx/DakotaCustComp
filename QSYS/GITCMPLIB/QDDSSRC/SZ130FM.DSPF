     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  SZ130FM  Stage Zone maintenance
     A*  14 September 1999
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'SZ130 '
     A                                  1 29'Stage Zone Detail Maintenance'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTINP1         5A  B  1 74
     A                                  1 46'Position to department . .'
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A            W1ZONE        40   O  3  2
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SDWHDP    R        O 11 10REFFLD(SDREC/SDWHDP *LIBL/STAGD)
     A            SDSTYP    R        O 11 22REFFLD(SDREC/SDSTYP *LIBL/STAGD)
     A            SDAISL    R        O 11 29REFFLD(SDREC/SDAISL *LIBL/STAGD)
     A            SDLOC1    R        O 11 44REFFLD(SDREC/SDLOC1 *LIBL/STAGD)
     A                                      EDTCDE(3)
     A            SDLOC2    R        O 11 54REFFLD(SDREC/SDLOC2 *LIBL/STAGD)
     A                                      EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970611  155400  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(DSPSFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO          4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Department'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9  3
     A                                 10 19'Section'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Aisle'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Location'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'Location'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'From'
     A                                      DSPATR(HI)
     A                                  9 52'To'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
