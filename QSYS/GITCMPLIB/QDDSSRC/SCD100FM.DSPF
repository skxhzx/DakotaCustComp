     A*%%TS  SD  20080305  153927  RETHA       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SCD100FM  Check digit entry - RF
     A*  December 23, 2000
     A*  Kenneth Elder
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
417 AA*    12/17/03  DAS  4.17
     A*      - Added WOROW and WOCOL to work with the slot API.
510 AA*    07/10/07  RBD  5.10
     A*      - Added Screen3 to allow multiple check digits.
     A*        Screen2 was used for a single check digit and
     A*        will no longer be used.
510aAA*    07/10/07  RBD  5.10a
     A*      - Fix: shifted some screen output to comply with
     A*        the 8 x 20 size.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R SCREEN2
     A*%%TS  SD  20070710  124558  RETHA       REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SCD100'
     A                                  1  9'Check Digits'
     A                                      DSPATR(HI)
     A            WMODSC         6   O  2 12DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                  4  2'Slot :'
     A            W2DSP         12A  B  4  9
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                  5  2'Digit:'
     A            W2DGT          3Y 0B  5  9EDTCDE(Z)
     A                                      CHECK(ER)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A            W2ERR          7A  O  5 14DSPATR(HI)
     A                                  7  1' F3=Exit   F9=Last'
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  20080305  153927  RETHA       REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SCD100'
     A                                  1  9'CkDig'
     A                                      DSPATR(HI)
     A            WMODSC         6A  O  1 15DSPATR(RI)
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
     A                                  2  2'Slot :'
     A            W3DSP         12A  B  2  9
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            W3IDC1         6A  O  4  2
     A                                  4  9':'
     A            W3DGT1         3Y 0B  4 11EDTCDE(X)
     A  95                                  CHECK(ER)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A            W3IDC2         6A  O  5  2
     A  95                                  DSPATR(ND)
     A                                  5  9':'
     A  95                                  DSPATR(ND)
     A            W3DGT2         3Y 0B  5 11EDTCDE(X)
     A  94                                  CHECK(ER)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  95                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A            W3IDC3         6A  O  6  2
     A  95
     AO 94                                  DSPATR(ND)
     A                                  6  9':'
     A  95
     AO 94                                  DSPATR(ND)
     A            W3DGT3         3Y 0B  6 11EDTCDE(X)
     A N95N94                               CHECK(ER)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A  95
     AO 94                                  DSPATR(PR)
     A  95
     AO 94                                  DSPATR(ND)
     A                                  7  1'F3=Ex'
     A                                  7  7'F9=Lst'
     A            W3ERR          7A  O  7 14DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R SCREEN1
     A*%%TS  SD  20070710  124558  RETHA       REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SCD100'
     A                                  1  9'Check Digits'
     A                                      DSPATR(HI)
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  8EDTCDE(Z)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  4  2'Dept:'
     A            W1WHDP         5A  B  4  8
     A  03                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                  5  2'Mode:'
     A            W1MODE         1A  B  5  8CHECK(ER)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 13'1=Entry'
     A                                  6 13'2=Verify'
     A                                  7  2'F3=Exit  F5=Refresh'
