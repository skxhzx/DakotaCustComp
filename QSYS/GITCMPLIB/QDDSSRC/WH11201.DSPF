     A*%%TS  SD  20080124  144534  DAVE        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  WH112FM   Warehouse code Table Maintenance - Change screen
     A*  11 July, 1994
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.00
     A*
     A*  Revisions
     A*
520aAA*   01/24/08  DAS  5.20  P#00002
     A*     - Added address fields.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080124  144534  DAVE        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'WH112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Code . . . . . . . . .'
     A                                  9  4'Description  . . . . .'
     A            W1DESC        30A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 27'Warehouse Table Maintenance'
     A                                      DSPATR(HI)
     A            W1CODE         3Y 0B  8 28EDTCDE(3)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 10  4'Address line 1 . . . .'
     A            W1ADR1        40   B 10 28CHECK(LC)
     A                                 11  4'Address line 2 . . . .'
     A            W1ADR2        40   B 11 28CHECK(LC)
     A                                 12  4'City, State, Zip . . .'
     A            W1CITY        20   B 12 28CHECK(LC)
     A            W1ST           2   B 12 50CHECK(LC)
     A            W1ZIP         10   B 12 54CHECK(LC)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
