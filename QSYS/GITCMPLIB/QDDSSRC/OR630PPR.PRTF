     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OR630PPR   Truck build - Exception Report - Customer Pick Slot
     A*  11 September 2005
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*
     A* Revisions
     A*
500 AA*   09/11/05  MLB  5.00
     A*     - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +37
     A                                      'Truck Build - Picking Exception Re-
     A                                      port'
     A                                   125
     A                                      'Page'
     A            PAGE           3S 0O   130
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            HEDLN2        60A  O   +26
     A                                   125
     A                                      'OR630P'
     A*
     A          R HEAD2
     A*
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                   120
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Order #'
     A                                    11
     A                                      'Item'
     A                                   +14
     A                                      'Slot'
     A                                   +10
     A                                      'Allocated'
     A                                    +3
     A                                      'Picked'
     A                                    +4
     A                                      'Message'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*
     A                                      SPACEB(001)
     A            LWORD     R        O     1
     A                                      REFFLD(LWREC/LWORD *LIBL/WORKLBL)
     A                                      EDTCDE(3)
     A            LWITEM    R        O    +3
     A                                      REFFLD(LWREC/LWITEM *LIBL/WORKLBL)
     A            LWDISP    R        O    +3
     A                                      REFFLD(LWREC/LWDISP *LIBL/WORKLBL)
     A            LWQALC    R        O    +5
     A                                      REFFLD(LWREC/LWQALC *LIBL/WORKLBL)
     A                                      EDTCDE(L)
     A            PICKED         5S 0O    +4
     A                                      EDTCDE(L)
     A            P1MSG         40A  O    +3
     A*
     A          R DETAIL2
     A*
     A                                      SPACEB(001)
     A            DSCLN         80A  O     1
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'OR630P: End of Report '
     A                                      SPACEB(002)
     A*
