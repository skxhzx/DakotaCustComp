     A*%%TS  SD  19970617  143409  DAVE        REL-V3R1M0  5763-PW1
     A*****************************************************************
     A*
     A*  RH150AFM  Work with closed routes (one warehouse)
     A*  27 June 1997
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*  Roxanne Harrison
     A*
     A*  Revisions:
404 AA*    08/28/97  RH
     A*      - Change message from confirm Pick & print to confirm export.
     A*
411 AA*    03/09/99  DAS
     A*      - Changed to allow F1 key.
     A*
414 AF*    06/30/00  DAS  4.14
     F*      - Changed indicator for SFLNXTCHG from 85 to 21.
     F*        Re: Program never changed to use 85 instead of 21.
     A*
416 AF*    04/16/02  MLB  4.16
     F*      - Added new function key to call Dock Matrix display.
510  A*    06/15/06  RMC  5.10
     A*      - Enh: Create record format DSPSPF which is a duplicate
     A*      - of DSPSFL and added Shipment number for Packers.
530  A*    09/03/10  RBD  5.30
     A*      - Renamed to RH150A01.
     A*      - Replaced display of exported date and time with driver.
     F*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
411 AA                                      CA01
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
416 AA                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970617  143409  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH150A'
     A                                  1 30'Work with Closed Routes'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Position to route . . . .'
     A            PTINP2         5A  B  3 75
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970610  120842  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT        10A  O 11 14
     A            RHTRUK    R        O 11 26REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1CUBE         9Y 3O 11 38EDTCDE(3)
     A            W1SWGT         9Y 2O 11 50EDTCDE(3)
     A            W1PCS          5Y 0O 11 62EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970610  201910  DAVE        REL-V3R1M0  5763-PW1
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(DSPSFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
222 DA*           RECNO          4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 38'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 50'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 62'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A*****************************************************************
510  A*Version 5.10 display Shipment Number for Packers  DSPPSF
     A*****************************************************************
     A          R DSPPSF                    SFL
     A*%%TS  SD  20060605  155318  ROBIN       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1SHIP        11A  O 11  5
     A            RHRTE     R        O 11 17REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT        10A  O 11 24
     A            RHTRUK    R        O 11 36REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1CUBE         9Y 3O 11 48EDTCDE(3)
     A            W1SWGT         9Y 2O 11 60EDTCDE(3)
     A            W1PCS          5Y 0O 11 72EDTCDE(3)
     A*
     A          R DSPPFC                    SFLCTL(DSPPSF)
     A*%%TS  SD  20060605  155318  ROBIN       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Shipment'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 60'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 72'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 AA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHIMPT    R        O 11 33REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHBLDT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHPRTT    R        O 11 71REFFLD(RHREC/RHPRTT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2RPTD         6Y 0O 11 14EDTCDE(Y)
     A            W2IMPD         6Y 0O 11 24EDTCDE(Y)
     A            W2BLDD         6Y 0O 11 43EDTCDE(Y)
     A            W2PRTD         6Y 0O 11 62EDTCDE(Y)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 24'---- Imported ---'
     A                                      DSPATR(HI)
     A                                 10 43'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 43'----- Built -----'
     A                                      DSPATR(HI)
     A                                 10 62'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 62'---- Printed ----'
     A                                      DSPATR(HI)
     A                                 10 14'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 14'Report'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
530 DA**          W3EXPD         6Y 0O 11 15EDTCDE(Y)
530 MA            TTRUSR    R        O 11 15REFFLD(TTRREC/TTRUSR *LIBL/TTRTE)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            W3STAT        10   O 11 53
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHRTID    R        O 11 67REFFLD(RHREC/RHRTID *LIBL/RTEHED)
530 DA**          RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
530 DA**                                    EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
530 DA**                               10 15'  Date  '
530 DA**                                    DSPATR(HI)
530 DA**                                    DSPATR(UL)
530 DA**                               10 24'  Time  '
530 DA**                                    DSPATR(HI)
530 DA**                                    DSPATR(UL)
530 DA**                                9 15'---- Exported ---'
530 DA**                                    DSPATR(HI)
530 MA                                 10 15'Driver    '
530 MA                                      DSPATR(HI)
530 MA                                      DSPATR(UL)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 53'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Export.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
