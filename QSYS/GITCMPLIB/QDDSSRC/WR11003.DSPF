     A*%%TS  SD  20170825  173140  KENNETHE    REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR110FM    Work order / Repack
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    02/19/07  DAS  5.10
     A*      - Created
     A*
510aAA*    03/22/07  TJ   5.10a
     A*      - Added Qty Made and Date Needed fields.
     A*
700aAA*    12/07/15  JCJ  7.00a
     A*      - Added check for parent UOM Quantity equal to 0.
     A*
760aAA*    03/03/22  JCJ  7.60a
     A*      - Added Pack Description and License#.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF11
     A                                      CA12
     A                                      CA09
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'WR110.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 32'Repack Work Orders'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20170825  173140  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2 38'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 63EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
510aDA***                               3 38'Position to order . . .'
510aDA***         PTINP2         7Y 0B  3 63EDTCDE(4)
     A N95        PTINP2         8Y 0B  3 63EDTCDE(Z)
     A  02                                  DSPATR(PC)
     A            PTSORT        20A  O  2  9COLOR(WHT)
     A            PTINP2N       15A  B  3 63
     A N95                                  DSPATR(PR)
     A N95                                  DSPATR(ND)
     A            PTSORTD       25A  O  3 32
     A                                  3 58'. .'
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20170825  133430  KENNETHE    REL-V7R2M0  5770-WDS
     A  21                                  SFLNXTCHG
510aMA**85                                  SFLNXTCHG
760aAA            W1PDESC       15A  H
760aAA            W1HREF        15A  H
     A            W1UMSHRT       5A  H
700aAA            IN22           1A  H
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
510aDA***         W1ORD          7Y 0O 11  7EDTCDE(3)
     A            W1ORD          7Y 0O 11  6EDTCDE(3)
510aDA***         W1DESC        30   O 11 33
     A            W1DESC        28A  O 11 30
510aDA***         W1NEED         5  0O 11 65EDTCDE(3)
     A            W1NEED         5Y 0O 11 59EDTCDE(3)
     A            W1NDDT         8Y 0O 11 65EDTCDE(Z)
     A            W1STAT         7A  O 11 74
     A            W1ITEM        15A  O 11 14
700aAA  22                                  DSPATR(RI)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20170825  133430  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
510aDA***                               9  7'Work'
     A                                  9  6'Work'
     A                                      DSPATR(HI)
     A                                  9 14'Repack'
     A                                      DSPATR(HI)
510aDA***                               9 65'Qty'
     A                                  9 58'Qty'
     A                                      DSPATR(HI)
     A                                  9 65'Date'
     A                                      DSPATR(HI)
510aDA***                              10  7'Order  '
     A                                 10  6'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510aDA***                              10 65'Needed'
     A                                 10 58'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Needed  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74'Status '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Repack Desc               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 14'Item          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510aAA*****************************************************************
510aAA* VIEW 2
510aAA*****************************************************************
     A          R VW2SFL                    SFL
     A*%%TS  SD  20170825  133430  KENNETHE    REL-V7R2M0  5770-WDS
     A  21                                  SFLNXTCHG
700aAA            IN22           1A  H
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1ORD          7Y 0O 11  6EDTCDE(3)
     A            W1MADE         5Y 0O 11 32EDTCDE(3)
     A            W1RATIO       25A  O 11 39
     A            W1PQN          3Y 0O 11 66EDTCDE(3)
     A            W1PQNU         2A  O 11 70
700aAA            W1UERR         6A  O 11 73
700aAA  22                                  DSPATR(RI)
     A            W1ITEM        15A  O 11 15
700aAA  22                                  DSPATR(RI)
510aAA*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20130601  210109  KENNETHE    REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 15'Repack'
     A                                      DSPATR(HI)
     A                                  9 32'Qty'
     A                                      DSPATR(HI)
     A                                  9 39'Build'
     A                                      DSPATR(HI)
     A                                  9 66'Parent'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Order  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Item    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32'Made '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 39'Ratio                    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Qty Req'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9  6'Work'
     A                                      DSPATR(HI)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  20170825  133430  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Close.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
