     A****************************************************************
     A*
     A* CD311PR   Cross dock Report
     A* 05 September, 1997
     A* Hemant Kapadia
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    55
     A                                      DFT('Cross Dock Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('CD311')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A            TYPE          11A  O    63
     A  91                                75
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1'Dept'
     A                                     7'Slot       '
     A                                    20'Item           '
     A                                    36'Description    '
     A                                    57'Order  '
     A                                    65'P.O.     '
     A                                    75'Stock    '
     A                                    86'Allocated'
     A                                    97'Picked   '
     A                                   108'Received '
     A                                   119'Available'
     A                                   129'Stat'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            SLWHDP    R        O     1REFFLD(SLWHDP CD311)
     A            SLDISP    R        O     7REFFLD(SLDISP CD311)
     A            SLITEM    R        O    20REFFLD(SLITEM CD311)
     A            ITMDSC        20A  O    36
     A            CDORD     R        O    57REFFLD(CDORD  CD311)
     A                                      EDTCDE(4)
     A            CDPO      R        O    65REFFLD(CDPO   CD311)
     A            STKQTY        10A  O    75
     A            ALCQTY        10A  O    86
     A            PCKQTY        10A  O    97
     A            RCVQTY        10A  O   108
     A            AVLQTY        10A  O   119
     A            SLSTAT    R        O   129REFFLD(SLSTAT CD311)
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('CD311: End of Report ')
     A*
