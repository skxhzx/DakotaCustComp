     A*%%TS  SD  20180927  135236  BFCMQS      REL-V7R2M0  5770-WDS
     A********************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  LT110FM      Create Priority Replenishments file - Options
     A*  12 January 2000
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A* Revisions:
     A*
500 AA*   10/18/04  DAS  5.00
     A*     - Added *RPLVL and *AVGnnn
     A*     - Added description for each command.
     A*
720 AA*   09/27/18  KDE  7.20
     A*     - Added OPTION TO SELECT SPECIFIC REPLEN TYPE ONLY
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20180927  135236  BFCMQS      REL-V7R2M0  5770-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A                                  1  2'LT110.01'
     A                                  1 23'Create Priority Replenishment File'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 47'Department, *ALL'
     A                                 10  4'Replenishment basis  . . . .'
     A            W1RPLB         8A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 47'*AVGQTY - Dakota average sales'
     A                                 11 47'*AVGnnn - *AVGQTY plus nnn percent'
     A                                 12 49'(ie. *AVG010 for avg plus 10%)'
     A                                 13 47'*ORDQTY - Host ordered quantity'
     A                                 14 47'*RPLLVL - Item replenishment level'
     A                                 19  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 19 34
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 47'Y=Yes, N=No'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                 16  4'Replenishment Types  . . . .'
     A                                 16 47'N-NORMAL  B-BREAKDOWN  F-FLOW'
     A                                 17 47'R-REPACK  S-STAGING    A-ALL'
     A            W1RTYPE        1   B 16 34
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
