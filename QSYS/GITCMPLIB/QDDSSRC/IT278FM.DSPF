     A*%%TS  SD  20180320  223914  BFCMQS      REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Multilingual Version
     A*   Every screen has a corresponding Spanish screen.
     A*   All Spanish screens are at the end.
     A*   Any changes must be made to both screens.
     A*   If necessary, contact appropriate person to do translation.
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A*
     A********************************************************************
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  IT278FM      RF Store Specific Slot Replenishment - Display.
     A*  26 February 2018
     A*  Lynn McMahon
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
720 AA*     02/26/18  LMC  7.20
     A*       - Created
     A*       - Copied from IT27603
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20180226  065324  LYNNM       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                  1  2'IT278.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Pick Slots'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCANREC
     A*%%TS  SD  20180320  223914  BFCMQS      REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1 20'To slot:'
     A  80                                  DSPATR(ND)
     A            W1DISP        12A  B  1 29CHECK(ER)
     A  80                                  DSPATR(ND)
     A  80                                  DSPATR(PR)
     A                                  1  2'IT278.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19990618  135332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Item:'
     A            W1ITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Select'
     A                                       COLOR(BLU)
     A                                  2 14'9=View Qty'
     A                                       COLOR(BLU)
     A*                                 2 26'X=Slot Error'
     A*                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLDISP        12A  O  5  6
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL2                   SFL
     A*%%TS  SD  20180226  164941  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15A  O  5  6
     A            SLDISP        12A  O  5 22
     A          R DSPSFC2                   SFLCTL(DSPSFL2)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(09)
     A                                      CLRL(1)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A          R SC16REC
     A*%%TS  SD  20180226  065324  LYNNM       REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  2  1'To slot'
     A            W1DISP        12A  B  2  9CHECK(ER)
     A                                  1  2'IT278.01'
     A          R SC16RESP
     A*%%TS  SD  20071031  071806  LYNNM       REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1 18'Slot De  :'
     A            W1DISP        12A  B  1 29CHECK(ER)
     A                                  1  2'IT278.01'
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL                    SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLDISP        12A  O  6  5
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  5  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL2                   SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  6  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15A  O  6  5
     A            SLDISP        12A  O  6 21
     A          R D16SFC2                   SFLCTL(D16SFL2)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  5  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 21'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Records for 16-line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R C16REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(16)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A          R MORREC16
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(14)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    CLEAR ALL LINES IN SUBFILE LIST
     A*
     A          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
     A          R WDWMSG
     A                                      WINDOW(1 2 6 20)
     A*
     A                                      WDWTITLE((*TEXT 'Replenishment Erro-
     A                                      r') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WOTXT1        20A  O  1  1
     A            WOTXT2        20A  O  2  1
     A            WOTXT3        20A  O  3  1
     A            WOTXT4        20A  O  4  1
     A                               O  5  1'Continue:'
     A N40                                  DSPATR(ND)
     A            WOYES          1A  B  5 11COLOR(WHT)
     A                                      VALUES('Y' 'N' '1' '9')
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A            WOYN           5A  O  5 13
     A N40                                  DSPATR(ND)
     A*
     A* Lot/License plate scan window.
     A          R WDWLLP
     A                                      WINDOW(1 2 6 20)
     A                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
     A                                      ENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WWTXT         18   O  2  1
     A            WWLLP#        15   B  3  1DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A* Spanish Screens
     A*****************************************************************
      *----------------------------------------------------------------
     A          R TITLESP
     A*%%TS  SD  19970725  165203  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'IT278.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Slots de reaprovisionamiento'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
      *----------------------------------------------------------------
     A          R SCANRESP
     A*%%TS  SD  19990618  100251  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A            SCANHD         6A  O  1 11
     A                                  1 18'Slot De  :'
     A            W1DISP        12A  B  1 29CHECK(ER)
     A                                  1  2'IT278.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
      *----------------------------------------------------------------
     A          R POSTNRSP
     A*%%TS  SD  19990618  135332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Prdc:'
     A            W1ITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Seleccionar'
     A                                       COLOR(BLU)
     A                                  2 18'9=Ver Ctd'
     A                                       COLOR(BLU)
     A*                                 2 28'X=Error de slot'
     A*                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A*----------------------------------------------------------------
     A          R DSPSFLSP                  SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLDISP        12A  O  5  6
     A*
     A*----------------------------------------------------------------
     A          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A*----------------------------------------------------------------
     A          R DSPSFLSP2                 SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15A  O  5  6
     A            SLDISP        12A  O  5 22
     A*----------------------------------------------------------------
     A          R DSPSFCSP2                 SFLCTL(DSPSFLSP2)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
      *----------------------------------------------------------------
     A          R OPTRECSP
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Escriba opciones, oprima ENTER.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*    COMMAND LINES
     A*
      *----------------------------------------------------------------
     A          R WDWMSGSP
     A                                      WINDOW(1 2 6 20)
     A*
     A                                      WDWTITLE((*TEXT 'Error de Reap.    -
     A                                       ') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Salir F12=Cance-
     A                                      lar') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WOTXT1        20A  O  1  1
     A            WOTXT2        20A  O  2  1
     A            WOTXT3        20A  O  3  1
     A            WOTXT4        20A  O  4  1
     A                               O  5  1'Continuar:'
     A N40                                  DSPATR(ND)
     A            WOYES          1A  B  5 12COLOR(WHT)
     A                                      VALUES('Y' 'N')
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A                               O  5 14'(Y/N)'
     A N40                                  DSPATR(ND)
     A*
     A* Lot/License plate scan window.
      *----------------------------------------------------------------
     A          R WDWLLPSP
     A                                      WINDOW(1 2 6 20)
     A                                      WDWTITLE((*TEXT 'Escn Lot/#Lic') *C-
     A                                      ENTER)
     A                                      WDWTITLE((*TEXT 'F3=Salir  F12=Canc-
     A                                      elar') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WWTXT         18   O  2  1
     A            WWLLP#        15   B  3  1DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A* POSITION TO RECORD
     A*
     A          R PO16NREC
     A*%%TS  SD  20071030  145326  LYNNM       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Item:'
     A            W1ITEM        65A  O  1  8
     A                                  2  1' '
     A                                  2  3'1=Sel'
     A                                      COLOR(BLU)
     A                                  2  9'9=Qty'
     A                                      COLOR(BLU)
     A*                                 2 15'X=Err'
     A*                                     COLOR(BLU)
     A          R PO16NRSP
     A*%%TS  SD  20071031  071806  LYNNM       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Prdc:'
     A            W1ITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Seleccionar'
     A                                      COLOR(BLU)
     A                                  2 18'9=Ver Ctd'
     A                                      COLOR(BLU)
     A*                                 2 28'X=Error de slot'
     A*                                     COLOR(BLU)
     A*****************************************************************
     A*----------------------------------------------------------------
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFLSP                  SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLDISP        12A  O  5  6
     A*
     A          R D16SFCSP                  SFLCTL(D16SFLSP)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFLSP2                 SFL
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15A  O  5  6
     A            SLDISP        12A  O  5 22
     A          R D16SFCSP2                 SFLCTL(D16SFLSP2)
     A*%%TS  SD  20180226  163416  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
