     A*%%TS  SD  20200708  074040  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*
     A********************************************************************
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  LT350FM      View License History Summary   - Display.
     A*  02 July 2020
     A*  Lynn McMahon
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
740 AA*     07/02/20  LMC  7.40
     A*       - Created
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20200702  110029  LYNNM       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                  1  2'LT350.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCANREC
     A*%%TS  SD  20200708  074040  LYNNM       REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT350.01'
     A            W1LCNS        15   O  1 11DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20200707  071332  LYNNM       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Item:'
     A            ITHEAD        65A  O  1  8
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20200708  074040  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            LASTDATE       6Y 0O  4 17EDTWRD('  /  /  ')
     A            ACTION         3A  O  4  2
     A            SUMQTY        10A  O  4  6
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20200708  074040  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0033)
     A                                      SFLPAG(0003)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A                                  3  2'Act'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 17'Last Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*                                     SFLMSGRCD(24)
     A                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A*                                     SLNO(24)
     A                                      SLNO(09)
     A                                      CLRL(1)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A          R SC16REC
     A*%%TS  SD  20200708  074040  LYNNM       REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            W1LCNS        15   O  1 11DSPATR(HI)
     A                                  1  2'LT350.01'
     A          R SC16RESP
     A*%%TS  SD  20180518  083731  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1 18'Slot De  :'
     A            W1DISP        12A  B  1 29CHECK(ER)
     A                                  1  2'LT350.01'
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL                    SFL
     A*%%TS  SD  20200702  103637  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            LASTDATE       6Y 0O  6 17EDTWRD('  /  /  ')
     A            ACTION         3A  O  6  2
     A            SUMQTY        10A  O  6  6
     A*
     A*                                     SFLSIZ(0010)
     A          R D16SFC                    SFLCTL(D16SFL)
     A*%%TS  SD  20200702  103637  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0038)
     A                                      SFLPAG(0008)
     A***                                   ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RECNO          4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A                                  5  2'Act'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 17'Last Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  6'Qty       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Records for 16-line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*                                     SLNO(22)
     A*           CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R C16REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(16)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A          R MORREC16
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(14)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    CLEAR ALL LINES IN SUBFILE LIST
     A*
     A          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
     A          R WDWMSG
     A                                      WINDOW(1 2 6 20)
     A*
     A                                      WDWTITLE((*TEXT 'Replenishment Erro-
     A                                      r') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WOTXT1        20A  O  1  1
     A            WOTXT2        20A  O  2  1
     A            WOTXT3        20A  O  3  1
     A            WOTXT4        20A  O  4  1
     A                               O  5  1'Continue:'
     A N40                                  DSPATR(ND)
     A            WOYES          1A  B  5 11COLOR(WHT)
     A                                      VALUES('Y' 'N' '1' '9')
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A            WOYN           5A  O  5 13
     A N40                                  DSPATR(ND)
     A*
     A* Lot/License plate scan window.
     A          R WDWLLP
     A                                      WINDOW(1 2 6 20)
     A                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
     A                                      ENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WWTXT         18   O  2  1
     A            WWLLP#        15   B  3  1DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A* POSITION TO RECORD
     A*
     A          R PO16NREC
     A*%%TS  SD  20200707  074344  LYNNM       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Item:'
     A            ITHEAD        65A  O  1  8
     A                                  2  1' '
