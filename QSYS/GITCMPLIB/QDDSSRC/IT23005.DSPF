     A*%%TS  SD  20191204  111720  KENNETHE    REL-V7R2M0  5770-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT230FM   Item Lookup - RF
     A*  17 April, 1997
     A*  Dave Sommerville
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
416aAA*  03/27/02  RH  4.16a
     A*   - Added Mfg/Vend item# W1VMFG
417aAA*  09/09/03  RH  4.17a
     A*   - Add Screen2 to display description and pack
     A*
500 AA*   05/24/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
510aAA*   03/19/07  DAS  5.10a
     A*     - Changed w1upc length 20 to w1up50 length 50
520aAA*  03/26/08  RH  5.20a
     A*   - Add Linked item message to screen2.
700aAA*  03/22/16  MR  7.00a
     A*   - Rename IT230FM to IT23001.
     A*   - Adding Inventory Quantities to IT230.02.
700bAA*  05/20/16  LMC 7.00b
     A*   - Rename IT23001 to IT23002.
     A*   - Add Brand to screen2
700cAA*  09/19/16  LMC 7.00c
     A*   - Rename IT23002 to IT23003.
     A*   - Changed Text from Trn to Ina. So it matches the description
     A*     Shown in Item Maintenance - View Item Quantity
730aAA*   11/06/19  LMC  7.30a
     A*     - Rename to IT23004, increasing the upc field to 74 pos
     A*       so any vendors using gs1 barcodes that exceed the 50
     A*       characters won't overflow into the next field.
     A*       Wanted to make it 100 but that would require adding
     A*       additional lines to the screen. and 8 line screen is
     A*       already maxed out. so I made it as large as I could
     A*       without overlaying any fields.
730bAA*  10/16/19  KDE 7.30
     A*   - Add F8 to print item label
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IT230.01'
     A                                  1 11'Item Lookup'
     A                                      DSPATR(HI)
     A                                  2  1'Whse'
     A            W1WHSE         3Y 0B  2  6
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  3  1'Item'
     A            W1ITEM        15A  B  3  6
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
223 AAON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  1'Mfg#'
     A            W1VMFG        15A  B  4  6
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  1'UPC:'
510aDA**          W1UPC         20A  B  6  1CHECK(ER)
730aDA*           W1UP50        50A  B  6  1CHECK(ER)
730aMA            W1UP50        74A  B  6  1CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
223 DA*N32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20191204  111720  KENNETHE    REL-V7R2M0  5770-WDS
     A  70                                  CF08
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT230.02'
     A                                  1 11'Item Lookup'
     A                                      DSPATR(HI)
     A            W2ITEM        15A  O  2  2
     A            W2PACK        15A  O  3  2
     A  53                              3 18'Brand:'
     A  53        W2BRND        10A  O  3 25
     A*                                 4  1'Desc'
     A            W2DSC1        40A  O  4  2
     A            W2DSC2        40A  O  5  2
     A                                 14  2'Linked item exists '
     A  81                                  DSPATR(RI)
     A N81                                  DSPATR(ND)
     A                                 15  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  2'Stk:'
     A                                  8  2'Alo:'
700cDA*                                 9  2'Trn:'
     A                                  9  2'Ina:'
     A                                 10  2'Avl:'
     A            IQSTK1    R        O  7  7REFFLD(IQREC/IQSTK1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQSTK2    R        O  7 18REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQSTK3    R        O  7 26REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC1    R        O  8  7REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC2    R        O  8 18REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQALC3    R        O  8 26REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD1    R        O  9  7REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD2    R        O  9 18REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQHLD3    R        O  9 26REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQAVL1    R        O 10  7REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQAVL2    R        O 10 18REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A            IQAVL3    R        O 10 26REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
     A                                      EDTCDE(O)
     A                                 15 21'F8=Label'
     A                                      COLOR(BLU)
     A N70                                  DSPATR(ND)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 A *----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970212  075630  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IT230.01'
     A                                  1 11'Buscar Producto'
     A                                      DSPATR(HI)
     A                                  2  1'Almc'
     A            W1WHSE         3Y 0B  2  6
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  3  1'Prdc'
     A            W1ITEM        15A  B  3  6
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
223 AAON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  1'#Mfc'
     A            W1VMFG        15A  B  4  6
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  1'UPC:'
     A            W1UPC         20A  B  6  1CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
223 DA*N32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
     A          R SCRN2SP
     A*%%TS  SD  20160322  143414  MIKER       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IT230.02'
     A                                  1 11'Buscar Producto'
     A                                      DSPATR(HI)
     A            W2ITEM        15A  O  2  2
     A            W2PACK        15A  O  3  2
700bAA  53                              3 18'Marca:'
700bAA  53        W2BRND        10A  O  3 25
     A*                                 4  1'Desc'
     A            W2DSC1        40A  O  4  2
     A            W2DSC2        40A  O  5  2
     A                                 15  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
700aAA                                  7  2'Inv:'
700aAA                                  8  2'Asg:'
700cDA*                                 9  2'Trn:'
700cMA                                  9  2'Ina:'
700aAA                                 10  2'Dsp:'
700aAA            IQSTK1    R        O  7  7REFFLD(IQREC/IQSTK1 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQSTK2    R        O  7 18REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQSTK3    R        O  7 26REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQALC1    R        O  8  7REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQALC2    R        O  8 18REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQALC3    R        O  8 26REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQHLD1    R        O  9  7REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQHLD2    R        O  9 18REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQHLD3    R        O  9 26REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQAVL1    R        O 10  7REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQAVL2    R        O 10 18REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
700aAA            IQAVL3    R        O 10 26REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
700aAA                                      EDTCDE(O)
