     A*%%TS  SD  20021202  165553  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP290FM   PIR options - Expiration / Entered Date Options
     A*  27 November 2002
     A*  Max Blacknik
     A*
     A*  SK112FM - 1.01
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    11/27/02  MLB  4.17
     A*      - Created.
     A*
500 AA*    08/18/05  MLB  5.00
     A*      - Enh: Added two new options: Changed text on
     A*        W1RCVO - Receiving to overflow slot
     A*        W1RTNO - Returns to overflow slot
     A*        Changed text on screen for W1PARC, W1PART:
     A*        screen for W1PARC, W1PART.
     A*        W1PARC - Receiving to pick slot
     A*        W1PART - Returns to pick slot
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP300.01'
     A                                  1 24'Expiration / Entered Date Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  2'How should expiration / entered da-
     A                                      tes be updated'
     A                                  8  2'in the destination slot for the fo-
     A                                      llowing transaction types?'
     A                                 10  4'Letdowns . . . . . . . . . . . :'
     A            W1LTDN         1A  B 10 38
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 10 56'1=Use from slot date'
     A                                 11  4'On-demand replenishments . . . :'
     A            W1ODRP         1A  B 11 38
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 11 56'2=Use oldest date'
     A                                 12  4'Slot transfers . . . . . . . . :'
     A            W1SLTF         1A  B 12 38
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
     A                                 12 56'3=Use from slot date'
     A                                 13 58'if to slot quantity'
     A                                 14  2'Product Put-Away'
     A                                      DSPATR(UL)
     A                                 14 58'is zero'
500 DA*                                16  4'Receiving  . . . . . . . . . . :'
500 MA                                 16  4'Receiving to pick slot . . . . :'
     A            W1PARC         1A  B 16 38
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
500 AA                                 16 56'1=Use receiving date'
500 AA                                 17  4'Receiving to overflow slot . . :'
500 AA            W1RCVO         1A  B 17 38
500 AA N26N90                               DSPATR(HI)
500 AA  26                                  DSPATR(RI)
500 AA N26N90                               DSPATR(UL)
500 AA  06                                  DSPATR(PC)
500 AA  92                                  DSPATR(PR)
500 AA                                 17 56'2=Use oldest date'
500 DA*                                17  4'Returns  . . . . . . . . . . . :'
500 MA                                 18  4'Returns to pick slot . . . . . :'
500 DA*           W1PART         1A  B 17 38
500 MA            W1PART         1A  B 18 38
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  92                                  DSPATR(PR)
500 AA                                 18 56'3=Use receiving date'
500 AA                                 19  4'Returns to overflow slot . . . :'
500 AA            W1RTNO         1A  B 19 38
500 AA N27N90                               DSPATR(HI)
500 AA  27                                  DSPATR(RI)
500 AA N27N90                               DSPATR(UL)
500 AA  07                                  DSPATR(PC)
500 AA  92                                  DSPATR(PR)
500 AA                                 19 58'if to slot quantity'
500 AA                                 20 58'is zero'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
