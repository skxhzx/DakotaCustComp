     A*%%TS  SD  20121002  125050  LYNNM       REL-V6R1M0  5761-WDS
     A*%%EC
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  PO137FM   UCI P/O Receiving-directed
     A*  02 October 2012
     A*  Lynn McMahon
     A*
     A*  PO137FM
     A*
     A*  Revisions:
     A*
     A*    10/02/12  LMC  6.40
     A*     - Created
650aAA*    07/16/13  LMC  6.50a
     A*     - Added auto record advance when UCI is scanned
650bAA*    08/07/13  LMC  6.50b
     A*     - Renamed screen format to PO23702.
     A*     - Need to allow F20 on screen3.
     A*
650cAA*    08/12/13  LMC  6.50C
     A*     - Renamed screen format to PO23703.
     A*     - Add Error window that requires the user to press command
     A*     key to continue and sound alarm.
650dAA*    01/27/14  LMC  6.50d
     A*     - Renamed screen format to PO23704.
     A*     - Add F8 screen3 for ti/hi.
650eAA*    01/29/14  LMC  6.50e
     A*     - Renamed screen format to PO23705.
     A*     - Auto record advance after license is scanned
     A*     - Added scanned count to screen1.
650fAA*    03/10/14  LMC  6.50f
     A*     - Renamed screen format to PO23706.
     A*     - Added slot definition on screen right after ti/high
700aAA*    04/06/16  LMC  7.00a
     A*    - Enh: Adding logic for Metric.
     A*      Created new display file PO13707 to replace PO13706.
     A**************************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20121001  104121  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(21)
     A                                      CF20
     A                                      CF22
     A  98                                  ALARM
     A                                  1  2'PO137.01'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'UCI Scanning'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'PO number . .'
     A            W1PO           9A  O  6 16
650eAA                                  6 26'Scanned .'
650eAA            SAVEQTY        5  0O  6 36EDTCDE(Z)
     A                                  7  2'Warehouse . .'
     A            W1WHSE         3Y 0O  7 16EDTCDE(4)
     A            W1FDSC        30A  O  7 21
     A            WOLBL         13A  O  8  2
     A            WOITEM        65A  O  8 16
     A            WOLBLS        13A  O  9  2
     A            WOSITM        65A  O  9 16
650aDA*           W1UCI        100A  B 10 25
650aMA            W1UCI        100A  B 10 25CHECK(ER)
     A            W1LHDG        20A  O 10  3
     A*****************************************************************
     A          R SCREEN2
     A*%%TS  SD  20121002  125050  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(21)
     A                                      CF04
700aAA N72                                  CF07
     A                                      CF08
700aDA*N95                                  CF09
700aMA N95N72                               CF09
     A  55                                  CF17
     A                                      CF20
700aDA*N66                                  CF11
700aMA N66N83N84                            CF11
     A  98                                  ALARM
     A                                  1  2'PO137.02'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'PO number . .'
     A            W1PO           9A  O  6 16
     A                                  6 60'Directed Put away'
     A                                  7  2'Warehouse . .'
     A            W1WHSE         3Y 0O  7 16EDTCDE(4)
     A            W1FDSC        30A  O  7 21
     A            WOLBL         13A  O  8  2
     A            WOITEM        65A  O  8 16
     A            WOLBLS        13A  O  9  2
     A            WOSITM        65A  O  9 16
     A            WODSC1        40A  O 10 16
     A            WODSC2        40A  O 11 16
     A                                 12  2'Home slot . .'
     A            WOSLOT        12A  O 12 16
     A            WOPDSC        15A  O 12 40
     A            WOTIE          4Y 0O 12 57EDTCDE(4)
     A                                 12 62'/'
     A            WOHI           2Y 0O 12 64EDTCDE(4)
650fAA            WOSDEF         2A  O 12 67
     A                                 13 45'DEPT NOT LIVE'
     A  65                                  DSPATR(ND)
     A                                 13 45'NOT A FSWMS ITEM'
     A  64                                  DSPATR(ND)
     A                                 13  2'Ordered:'
     A  63                                  DSPATR(ND)
     A            WOQOR         15A  O 13 11
     A  63                                  DSPATR(ND)
     A                                 14  3'Scan License. . . . . . . .'
     A            W1LCNS        15A  B 14 31
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90
     AO 80
     AO 73                                  DSPATR(PR)
650eAA                                      CHECK(ER)
     A                                 15  3'UPC code  . . . . . . . . .'
     A            W1UPCD        50A  B 15 31CHECK(ER)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 92                                  DSPATR(PR)
     A                                 16  3'Quantity received . . . . .'
     A            W1QRC1         5Y 0B 16 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 92                                  DSPATR(PC)
     A  90
     AO 71                                  DSPATR(PR)
     A            W1UM1          5A  O 16 37
     A            W1QRC2         5Y 0B 16 44EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  94
     AO 90
     AO 71                                  DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1UM2          5A  O 16 50
     A  94                                  DSPATR(ND)
     A            W1QRC3         5Y 0B 16 57EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  93
     AO 90
     AO 71                                  DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1UM3          5A  O 16 63
     A  93                                  DSPATR(ND)
     A                                 17  3'Expiration date . . . . . .'
     A            W1EXDT         6Y 0B 17 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96
     AO 83
     AO 90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 42'FIFO date:'
     A  89                                  DSPATR(ND)
     A            W1FIFO         6Y 0B 17 53
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  89                                  DSPATR(ND)
     A  89                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 17 71'(MMDDYY)'
     A  96                                  DSPATR(ND)
     A            WOCRCV        27A  O 18  3
     A            W1DWGT         9Y 2B 18 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  82
     AO 95
700aAAO 72
     AO 90                                  DSPATR(PR)
     A  82
     AO 95                                  DSPATR(ND)
     A                                      EDTCDE(4)
700aAA            W1KGLB         3A  O 18 42
700aAA  82
700aAAO 95                                  DSPATR(ND)
     A                                 18 50'Average (Y/N) . . . . . .'
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A            W1AVG          1A  B 18 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  81
     AO 82
700aAAO 72
     AO 95
     AO 90                                  DSPATR(PR)
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A                                 19  3'Slot override . . . . . . .'
     A                                      DSPATR(ND)
     A            W1SLOT        12A  B 19 31DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 20  3'Manufacturing date  . . . .'
     A            W1MFDT         6Y 0B 20 31EDTCDE(4)
     A  27                                  DSPATR(RI)
     A  87                                  DSPATR(ND)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  84
     AO 87                                  DSPATR(PR)
     A                                 21  3'Lot code  . . . . . . . . .'
     A  88                                  DSPATR(ND)
     A            W1LOTC        30A  B 21 31CHECK(LC)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  88                                  DSPATR(ND)
     A  85
     AO 88                                  DSPATR(PR)
     A                                 21 63'Brand:'
     A  74                                  DSPATR(ND)
     A            W1BRAND       10A  B 21 70
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  74                                  DSPATR(ND)
     A  74
     AO 80                                  DSPATR(PR)
     A*                                23  2'F5=Refresh'
     A*                                     COLOR(BLU)
     A*                                23 13'F8=Tie/Hi'
     A*                                     COLOR(BLU)
     A*                                23 24'F9=Cwgt Entry'
     A* 95                                  DSPATR(ND)
     A*                                     COLOR(BLU)
     A*                                23 39'F12=Cancel'
     A*                                     COLOR(BLU)
     A*                                23 51'F17=Rcv log'
     A* 55                                  COLOR(BLU)
     A*N55                                  DSPATR(ND)
     A*                                23 63'F20=Accept warning'
     A*                                     COLOR(BLU)
     A*N33N90                               DSPATR(UL)
     A* 13                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A*                                22 40'F11=Mfr Code'
     A*                                     COLOR(BLU)
     A* 66                                  DSPATR(ND)
     A          R SCREEN3
     A*%%TS  SD  20121001  110003  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(21)
     A*                                     CF04
650dAA                                      CF08
     A*N95                                  CF09
     A* 55                                  CF17
650bAA                                      CF20
     A*N66                                  CF11
     A  98                                  ALARM
     A                                  1  2'PO137.03'
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Summary Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'PO number . .'
     A            W1PO           9A  O  6 16
     A                                  6 60'Directed Put away'
     A                                  7  2'Warehouse . .'
     A            W1WHSE         3Y 0O  7 16EDTCDE(4)
     A            W1FDSC        30A  O  7 21
     A            WOLBL         13A  O  8  2
     A            WOITEM        65A  O  8 16
     A            WOLBLS        13A  O  9  2
     A            WOSITM        65A  O  9 16
     A            WODSC1        40A  O 10 16
     A            WODSC2        40A  O 11 16
     A                                 12  2'Home slot . .'
     A            WOSLOT        12A  O 12 16
     A            WOPDSC        15A  O 12 40
     A            WOTIE          4Y 0O 12 57EDTCDE(4)
     A                                 12 62'/'
     A            WOHI           2Y 0O 12 64EDTCDE(4)
650fAA            WOSDEF         2A  O 12 67
     A                                 13 45'DEPT NOT LIVE'
     A  65                                  DSPATR(ND)
     A                                 13 45'NOT A FSWMS ITEM'
     A  64                                  DSPATR(ND)
     A                                 13  2'Ordered:'
     A  63                                  DSPATR(ND)
     A            WOQOR         15A  O 13 11
     A  63                                  DSPATR(ND)
     A                                 14  3'Scan License. . . . . . . .'
     A            W1LCNS        15A  B 14 31
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 15  3'UPC code  . . . . . . . . .'
     A            W1UPCD        50A  B 15 31CHECK(ER)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 16  3'Quantity received . . . . .'
     A            W1QRC1         5Y 0B 16 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 92                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A            W1UM1          5A  O 16 37
     A            W1QRC2         5Y 0B 16 44EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A  94                                  DSPATR(ND)
     A            W1UM2          5   O 16 50
     A  94                                  DSPATR(ND)
     A            W1QRC3         5Y 0B 16 57EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A  93                                  DSPATR(ND)
     A            W1UM3          5   O 16 63
     A  93                                  DSPATR(ND)
     A                                 17  3'Expiration date . . . . . .'
     A                                      DSPATR(ND)
     A            W1EXDT         6Y 0B 17 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 42'FIFO date:'
     A  89                                  DSPATR(ND)
     A            W1FIFO         6Y 0B 17 53
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  89                                  DSPATR(ND)
     A  89                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 17 71'(MMDDYY)'
     A  96                                  DSPATR(ND)
     A            WOCRCV        27A  O 18  3
     A            W1DWGT         9Y 2B 18 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  82
     AO 95
     AO 90                                  DSPATR(PR)
     A  82
     AO 95                                  DSPATR(ND)
     A                                      EDTCDE(4)
700aAA            W1KGLB         3A  O 18 42
700aAA  82
700aAAO 95                                  DSPATR(ND)
     A                                 18 50'Average (Y/N) . . . . . .'
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A            W1AVG          1A  B 18 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  81
     AO 82
     AO 95
     AO 90                                  DSPATR(PR)
     A  81
     AO 82
     AO 95                                  DSPATR(ND)
     A                                 19  3'Slot override . . . . . . .'
     A                                      DSPATR(ND)
     A            W1SLOT        12A  B 19 31DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 20  3'Manufacturing date  . . . .'
     A                                      DSPATR(ND)
     A            W1MFDT         6Y 0B 20 31EDTCDE(4)
     A  27                                  DSPATR(RI)
     A                                      DSPATR(ND)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 21  3'Lot code  . . . . . . . . .'
     A                                      DSPATR(ND)
     A            W1LOTC        30A  B 21 31CHECK(LC)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 21 63'Brand:'
     A  74                                  DSPATR(ND)
     A            W1BRAND       10A  B 21 70
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  74                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A* Command Line
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79A  O  1  2COLOR(BLU)
     A            CMDLN2        79A  O  2  2COLOR(BLU)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CNLWDW
     A*%%TS  SD  20120907  103814  LYNNM       REL-V6R1M0  5761-WDS
     A                                      WINDOW(2 2 10 65)
     A                                      CF10
     A                                  1 23'Confirm Cancel/Delete'
     A                                      DSPATR(HI)
     A                                  4 30'DELETED'
     A                                      COLOR(RED)
     A            W1LCNS        15A  O  7 26DSPATR(HI)
     A                                  9 17'F10=Delete   F12=Continue Scanning'
     A                                      COLOR(BLU)
     A                                  5 17'If you wish to do this press F10.'
     A                                  6 12'Otherwise press F12 to scan to thi-
     A                                      s license'
     A                                  3 12'All barcodes scanned to this licen-
     A                                      se will be'
650cAA          R ERRWDW
650cAA*%%TS  SD  20130812  160332  LYNNM       REL-V6R1M0  5761-WDS
650cAA*                                     CA12
650cAA                                      ALARM
650cAA                                      WINDOW(2 2 10 60)
650cAA                                  9 18'F12=Display Error'
650cAA                                      COLOR(BLU)
650cAA                                  3  6'Error Occurred - See Message at Bo-
650cAA                                      ttom of Screen'
650cAA                                      DSPATR(HI)
