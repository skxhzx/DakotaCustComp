     A*
     A*   IT376PR  Item Slot List in Picking Order
     A*   15 January, 2002
     A*   Roxanne Harrison
     A*
     A*-----------------------------------------------------------
     F*  Revisions
     F*
     F*    03/18/03  RH   4.17a
     F*      - Change heading, print for item range.
     F*    06/18/05  RLP  5.00
     F*      - Added slot status field to report.
500b F*    07/20/05  RH   5.00b
     F*      - Added 1st UPC for item to report.
610a F*    01/25/10  GJA  6.10a
     F*      - Added license number to report.
700 AA*    09/27/17  JGD 7.00
     A*      Report Mod:
     A*      Replace UPC info with MFG ITEM #.
     A*      Replace STATUS info with ENTERED DATE.
700a F*    06/01/16  RH/LMC 7.00a
     F*      - Added Item Qty totals to bottom of report
     A*
720 AA*    07/17/18  MLB  7.20
     A*      - Enh: Revised 7.00 mod. Added indicator to control
     A*        if UPC or Mfr Item# from UPC file should be printed.
     A*        Controlled from IT376.
     A*
     A*****************************************************************
     A*****************************************************************
     A*
CBIaAA*      03/19/18  TDF   Work List #2017081
     A*         Total "Stock" column
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(2)
     A*
     A            UDATE          6S 0      3EDTCDE(Y)
     A                                    54'Item Slot List In Picking Order'
     A                                   122'Page'
     A            PAGE           4S 0    126EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A            TIME           6S 0      3EDTWRD('  :  :  ')
     A*
     A            WOWHSE        60A       37
     A                                   125'IT376'
     A                                      SPACEA(2)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A**                                  82'Available'
     A**                                    SPACEA(1)
     A*
     A                                     1'Item           '
     A                                      UNDERLINE
     A                                    30'Pack'
     A                                      UNDERLINE
     A**                                  33'Description'
     A**                                    UNDERLINE
700 D **                                  52'Slot'
700 MA                                    49'Slot'
     A                                      UNDERLINE
700 DA*                                   66'Sts'
700 AA                                    62'Entered'
500 AA                                      UNDERLINE
500 DA*                                   66'Expire  '
500 MA                                    71'Expire  '
     A                                      UNDERLINE
     A**                                  82'Quantity '
     A**                                    UNDERLINE
     A                                    82'Stock'
     A                                      UNDERLINE
     A                                    93'Alloc'
     A                                      UNDERLINE
     A                                   104'Pick '
     A                                      UNDERLINE
     A                                   115'Trans'
     A                                      UNDERLINE
     A                                   126'Recvd'
     A                                      UNDERLINE
     A*
     A                                      SPACEA(1)
     A*
     A* Print detail line
     A*
     A          R HEAD2
     A            ITITEM    R              1REFFLD(ITITEM PIRITEM)
     A                                      SPACEB(1)
     A            ITDESC    R             17REFFLD(ITDESC PIRITEM)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A**61        SLITEM    R              1REFFLD(SLITEM SLOT)
     A* 61                                  SPACEA(1)
     A**61        ITDESC    R             17REFFLD(ITDESC PIRITEM)
     A* 61        ITDESC    R              1REFFLD(ITDESC PIRITEM)
     A**61                                  SPACEA(1)
     A* 61        ITPDSC    R             33REFFLD(ITPDSC PIRITEM)
720 MA  61 62     UPUPC     R              6REFFLD(UPUPC UPC)
720 DA* 61        UPVITM    R              6REFFLD(UPVITM UPC)
720 MA  61N62     UPVITM    R              6REFFLD(UPVITM UPC)
     A  61        ITPDSC    R             30REFFLD(ITPDSC PIRITEM)
610aAA N61        LCNS          15A       30
700 DA**10***>>>>                         50'*'
700 MA  10                                47'*'
700 DA*******>>>> SLDISP        12A       52
700 MA            SLDISP        12A       49
700 DA*******>>>> SLSTAT         2A       66
700 AA            W1EDTE         6S 0     62EDTCDE(Y)
500 DA*           W1EXPD         6S 0     66EDTCDE(Y)
500 MA            W1EXPD         6S 0     71EDTCDE(Y)
     A            W1QTY         10A       82
     A            W1STK         10A       82
     A            W1ALC         10A       93
     A            W1PCK         10A      104
     A            W1TFR         10A      115
     A            W1RCV         10A      126
     A**10                               107'*'
     A*
     A                                      SPACEA(1)
700aAA*
700aAA* Print TOTAL line
700aAA*      ITEMQTY information
700aAA          R TOTAL1
     A                                    30'Stock Qty'
700aAA            IQSTK1    R             40REFFLD(IQSTK1 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQSTK2    R             49REFFLD(IQSTK2 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQSTK3    R             57REFFLD(IQSTK3 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA                                      SPACEA(1)
700aAA                                    30'Alloc Qty'
700aAA            IQALC1    R             40REFFLD(IQALC1 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQALC2    R             49REFFLD(IQALC2 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQALC3    R             57REFFLD(IQALC3 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA                                      SPACEA(1)
700aAA                                    30'Hold Qty '
700aAA            IQHLD1    R             40REFFLD(IQHLD1 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQHLD2    R             49REFFLD(IQHLD2 ITEMQTY)
700aAA                                      EDTCDE(Z)
700aAA            IQHLD3    R             57REFFLD(IQHLD3 ITEMQTY)
700aAA                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1                     TEXT('SKIP ONE LINE')
     A                                     1' '
     A                                      SPACEA(1)
     A*
     A* End of report message
     A*
     A          R FOOT1
CBIaAA  38                                33'TOTAL'
CBIaAA  38                                82'________'
CBIaAA  38                                  SPACEA(1)
CBIaAA  38        STKTOT        10S 2     77EDTCDE(M)
CBIaAA  38                                  SPACEA(1)
416A A                                     1'* Denotes pick slot'
416A A                                      SPACEA(1)
     A          R FOOT2
     A                                     1'IT376: End of Report'
     A                                      SPACEA(1)
     A*
