     A*%%TS  SD  20080905  130816  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  POD130FM   POD - Error message recipients
     A*  05 September 2008
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    09/05/08  DAS  5.20
     A*      - Created.
520aAA*    06/03/09  RBD  5.20a  P#00212
     A*      - Added 5 more POD error message recipients.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080905  130816  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'POD130.01'
     A                                  1 26'POD - Error Message Recipients'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'If an error occurs sending a file -
     A                                      to the POD server a message will'
     A                                  8  4'be sent to QSYSOPR and the followi-
     A                                      ng users:'
     A            W1USR1        10A  B 11  8
     A            W1USR2        10A  B 11 21
     A            W1USR3        10A  B 11 34
     A            W1USR4        10A  B 11 47
     A            W1USR5        10A  B 11 60
520aAA            W1USR6        10A  B 13  8
520aAA            W1USR7        10A  B 13 21
520aAA            W1USR8        10A  B 13 34
520aAA            W1USR9        10A  B 13 47
520aAA            W1US10        10A  B 13 60
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
