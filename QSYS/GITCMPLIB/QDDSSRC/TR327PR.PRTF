     A****************************************************************
     A*
     A* TR327PR   Trax route by item detail - Print
     A* 12 October, 2011
     A* Ginger Andrews
     A*
     A* Revisions:
     A*
     A* 10/12/11  GJA  6.40
     A*  - Created
     A*
650a A* 08/27/13  GJA  6.50a
     A*  - Add item description
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    41
     A                                      'Trax Route Item Detail'
     A                                   123
     A                                      'Page'
     A            PAGE           4S 0O   127
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIMECUR        6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            SCHEAD        58A  O    26
     A                                   126
     A                                      'TR327'
     A                                      SPACEA(002)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A  91                               120
     A                                      '(Continued)'
     A*
     A* Print column headings for Date order.
     A*
     A          R COLUMNS1
     A                                      SPACEA(001)
     A
     A                                    54
     A                                      'Pieces'
     A                                    66
     A                                      'Pieces'
     A                                    78
     A                                      'Curr / Prev'
     A                                    93
     A                                      'Opr'
     A                                   100
     A                                      'Delivery'
     A                                      SPACEA(001)
     A                                     2
     A                                      'Item '
650aAA                                    18
650aAA                                      'Description'
     A                                      UNDERLINE
     A                                    49
     A                                      'UOM'
     A                                      UNDERLINE
     A                                    54
     A                                      'Delivered'
     A                                      UNDERLINE
     A                                    66
     A                                      'NonScanned'
     A                                      UNDERLINE
     A                                    78
     A                                      'Pcs Returned'
     A                                      UNDERLINE
     A                                    93
     A                                      'Flag'
     A                                      UNDERLINE
     A                                   101
     A                                      'Start date/time'
     A                                      UNDERLINE
     A*
     A* Print detail line for Date order
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            TTIITM    R        O     2REFFLD(TTIREC/TTIITM *LIBL/TTITM)
650aAA            W1DESC        30   O    18
     A            TTIUM     R        O    50REFFLD(TTIREC/TTIUM  *LIBL/TTITM)
     A            W1QDLV         6  0O    54EDTCDE(4)
     A            W1QDLN         6  0O    66EDTCDE(4)
     A            W1QCRT         6  0O    78EDTCDE(4)
     A            W1QPRT         6  0O    85EDTCDE(4)
     A            TTIOPF    R        O    95REFFLD(TTIREC/TTIOPF *LIBL/TTITM)
     A            W1SRD          6  0O   101EDTWRD('  /  /  ')
     A            TTISRT    R        O   110REFFLD(TTIREC/TTISRT *LIBL/TTITM)
     A                                      EDTWRD('  :  :  ')
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     2
     A                                      'Total Items:'
     A            WKCOUNT       10  0O    16
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'TR327: End of Report '
     A*
