     A****************************************************************
     A*
     A* ER302PR   Employee management report for an employee (Recv) (Summary)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        47
     A*
     A                                   152
     A                                      'Page'
     A            PAGE           4S 0O   156
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +55
     A                                   156
     A                                      'ER302'
     A          R HEAD2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(WKEMP# ER301)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1REFFLD(WKNAME ER301)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                    20' Cases  '
     A                                   101'Purchase Order'
     A                                      SPACEA(001)
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    10'  PO  '
     A                                      UNDERLINE
     A                                    21'Received'
     A                                      UNDERLINE
     A                                    30'Trans'
     A                                      UNDERLINE
     A                                    39'Weight'
     A                                      UNDERLINE
     A                                    51'Cube'
     A                                      UNDERLINE
     A                                    56'Vendor'
     A                                      UNDERLINE
     A                                    97'Ordered'
     A                                      UNDERLINE
     A                                   105'Received'
     A                                      UNDERLINE
     A                                   114'Differ'
     A                                      UNDERLINE
     A                                   126'Weight'
     A                                      UNDERLINE
     A                                   137'Cube'
     A                                      UNDERLINE
     A                                   145'Start'
     A                                      UNDERLINE
     A                                   155'End'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            WKPO      R             10REFFLD(WKPO ER301)
     A            WKPTOT         7  0     20EDTCDE(1)
     A            WKTTOT         4  0     30EDTCDE(1)
     A            POWGTR         7S 0     36EDTCDE(1)
     A            POCBER         7S 0     46EDTCDE(1)
     A            PHHVNA    R             56REFFLD(PHHVNA POHHDR)
     A            ORDPCS         6S 0     97EDTCDE(1)
     A            RCVPCS         6S 0    105EDTCDE(1)
     A            DIFPCS         6S 0    114EDTCDE(1)
     A            POWGT          7S 0    123EDTCDE(1)
     A            POCBE          7S 0    133EDTCDE(1)
     A            STIME          6S 0    143EDTWRD('  :  :  ')
     A            ETIME          6S 0    152EDTWRD('  :  :  ')
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD           8A        1
     A            DHRS           7S 2     10EDTCDE(1)
     A            T#PTOT         7S 0     20EDTCDE(1)
     A            T#TTOT         4S 0     30EDTCDE(1)
     A            T#WTOTR        7S 0     36EDTCDE(1)
     A            T#CTOTR        7S 0     46EDTCDE(1)
     A            T#WTOT         7S 0    123EDTCDE(1)
     A            T#CTOT         7S 0    133EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER302: End of Report '
     A*
