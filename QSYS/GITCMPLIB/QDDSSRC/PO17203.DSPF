     A*%%TS  SD  20140709  170219  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO172FM   Harvest Clam Information - Entry screen
     A*  16 December 2005
     A*  Jeffrey Jahnke
     A*
650A A* 09019/13   RH   6.50A
650AAA*   - Rename format, add error indicator 23 & 24
650bAA*   10/01/13  LMC  6.50b
     A*     - Enh: Added License field display.
     A*     We now allow the user to enter information for each license
     A*     - Screen format renamed from PO17201 to PO17202
     A*
650cAA* 07/09/14   RBD  6.50c
     A*   - Renamed format to PO17203.
     A*   - Added entry of "Product Wet Stored to Date" and lot number.
     A*     Neither are mandatory, but if entered, both must be entered.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20140709  170014  RETHA       REL-V6R1M0  5761-WDS
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO172.01'
     A                                  1 32'HACCP Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  4 52'License:'
     A            $PLCNS        15A  O  4 61
     A                                  5  2'PO Number:'
     A            $PPO           9A  O  5 13
     A                                  5 23'-'
     A            PHVNAM        40A  O  5 25
     A                                  6  7'Item:'
     A            W1ITEM        15A  O  6 13
     A            WODESC        65A  O  7 15
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Harvest Date . . . . . .'
     A            W1HDTE         6Y 0B 10 29
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 10 36'MMDDYY'
     A                                 11  4'Harvest Location . . . .'
     A            W1HLOC        20A  B 11 29
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Original Shipper Cert# .'
     A            W1CERT        20A  B 12 29
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'Prod Wet Stored To . . .'
     A            W1WDT          6Y 0B 13 29
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 13 36'MMDDYY'
     A                                 14  4'Lot Number . . . . . . .'
     A            W1LOT         15A  B 14 29
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 45'(Enter NA if no wet date and lot)'
     A                                 16  4'Printer device . . . . .'
     A            W1PRT         10A  B 16 29
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18  4'Form type  . . . . . . .'
     A            W1FORM        10A  B 18 29
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit             F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
