     A*%%TS  SD  20011102  114821  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CK391FM   Checker pallet - Change status
     A*  02 November, 2001
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*    11/02/01  HNK  4.16
     A*      - Created
     A*
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA20
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'CK391.01'
     A                                  1 28'Change Route Status for Checking'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Route . . . .'
     A            W1RTE          5   O  8 18
     A                                  8 24'/'
     A            W1RTID         5   O  8 26
     A                                  9  4'Pallet  . . .'
     A            W1PLT          2  0O  9 18
     A                                 12  5'The pallet has been marked as Comp-
     A                                      leted.  If you are sure press F20 t-
     A                                      o '
     A                                      DSPATR(HI)
     A                                 13  5'open it again for checking.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F20=Open'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SCREEN2
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'CK391.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SCREEN3
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'CK391.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
