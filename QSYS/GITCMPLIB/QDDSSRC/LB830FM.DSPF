     A*%%TS  SD  20010416  170933  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LB830FM   Change label utility
     A*  23 May 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
414 AA*    05/23/00  DAS  4.14
     A*      - Created
     A*
415 AA*    04/24/01  DAS  4.15
     A*      - Added window PASWRD
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010416  132325  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LB830.01'
     A                                  1 31'Change Label Utility'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Label number  . . . . . . . .'
     A            W1LBL#         7Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20010416  170933  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LB830.02'
     A                                  1 35'Label Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Lbl#'
     A            W2LBL#         7Y 0O  2 40EDTCDE(Z)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  3  4'Stat:'
     A            W2STAT         1A  B  3 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2SDTE         8Y 0B  3 13EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STIM         6Y 0B  3 24EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4  4'Type:'
     A            W2TYPE         1A  B  4 10DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  4 13'Grp1:'
     A            W2GRP1         1A  B  4 19DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4 22'Grp2:'
     A            W2GRP2         1A  B  4 28DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4 31'Batch:'
     A            W2PBAT         7S 0B  4 38DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  4 48'Rpt Dte:'
     A            W2RDTE         8S 0B  4 57DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6  4'Slt1:'
     A                                  6 10'Whse:'
     A            W2WHSE         3Y 0B  6 16EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 21'Whdp:'
     A            W2WHDP         5A  B  6 27DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 34'Aisl:'
     A            W2AISL         3A  B  6 40DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 46'Loc:'
     A            W2LOC          3Y 0B  6 51EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 58'Lvl:'
     A            W2RLVL         2Y 0B  6 63EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 68'Hand:'
     A            W2HAND         2A  B  6 74DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 10'Disp:'
     A            W2DISP        12A  B  7 16DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 30'Styp:'
     A            W2STYP         1A  B  7 36DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 39'Pick Seq:'
     A            W2PSEQ         5S 0B  7 49DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  7 57'Query Factor:'
     A            W2QRYF         1S 0B  7 71DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  9  4'Slt2:'
     A                                  9 10'Whs2:'
     A            W2WHS2         3Y 0B  9 16EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9 21'Whd2:'
     A            W2WHD2         5A  B  9 27DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9 34'Asl2:'
     A            W2ASL2         3A  B  9 40DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9 45'Loc2:'
     A            W2LOC2         3Y 0B  9 51EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9 57'Lvl2:'
     A            W2LVL2         2Y 0B  9 63EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9 68'Hnd2:'
     A            W2HND2         2A  B  9 74DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 10'Disp:'
     A            W2DSP2        12A  B 10 16DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 12  4'Item:'
     A            W2ITEM        15A  B 12 10DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W2DESC        50A  O 12 27
     A                                 13  4'UM Type/Code:'
     A            W2UTYP         1A  B 13 18DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 13 20'/'
     A            W2UCOD         2A  B 13 22DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 13 27'Cube Unit :'
     A            W2UCUB         7Y 3B 13 41DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(L)
     A                                 13 52'Weight Unit :'
     A            W2UWGT         7Y 2B 13 68DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(L)
     A                                 14 32'Total:'
     A            W2CUBE         9Y 3B 14 39EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 14 59'Total:'
     A            W2SWGT         9Y 2B 14 66EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 16  4'Seq#:'
     A            W2SEQ          5S 0B 16 10DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 16 17'PO:'
     A            W2PO           9A  B 16 21DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 16 31'Ord#:'
     A            W2ORD          7S 0B 16 37DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 16 46'Stop:'
     A            W2STOP         2S 0B 16 52DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 16 56'Route:'
     A            W2RTE          5A  B 16 63DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 18  4'Qty Alloc:'
     A            W2QALC         5Y 0B 18 15EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 18 23'Picked:'
     A            W2QPCK         5Y 0B 18 31EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 18 39'Avail:'
     A            W2QAVL         5Y 0B 18 46EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 18 54'Remain:'
     A            W2QRMN         5Y 0B 18 62EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 20  4'Trn#:'
     A            W2TRN#         7S 0B 20 10DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 20 20'Cor#:'
     A            W2CTR#         7S 0B 20 26DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 20 36'Pallet#:'
     A            W2PAL#         7S 0B 20 45DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 20 55'Tie/Hi:'
     A            W2TIE          4S 0B 20 63DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 20 69'/'
     A            W2HIGH         2S 0B 20 71DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 21  4'Vrfy:'
     A            W2VRFY         1A  B 21 10DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 21 13'Priority:'
     A            W2PRTY         1S 0B 21 23DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 21 27'Spcl Sts:'
     A            W2SPCL         2A  B 21 37DSPATR(HI)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20010416  132325  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LB830.03'
     A                                  1 35'Label Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  8 32'Label#'
     A            W2LBL#         7S 0O  8 39
     A                                 11 27'Press F10 to Confirm Update'
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A*%%TS  SD  20010424  095629  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
