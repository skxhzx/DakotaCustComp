     A*%%TS  SD  20140220  164122  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  UI320FM    View UCIINFO file - Display
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
650 AA*    02/18/14  LMC  6.50
     A*      - Created. - copied from LT32003
650aAA*    01/16/15  LMC  6.50a
     A*      - Renamed display file from UI32001 to UI32002
     A*      - Added item description to heading
     A*      - Added printer override
730aAA*    11/15/19  LMC  7.30a
     A*      - Renamed display file from UI32002 to UI32003
     A*      - if UCI is no longer in facilty display weight in
     A*        a different color.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF10
     A                                      CF11
650aAA                                      CF21
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140220  140758  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            #UILCNS       15A  O  8  5
     A            #WEIGHT       12A  O  8 21
730aAA  40                                  COLOR(BLU)
     A            #PRODDT        8A  O  8 35
     A            #PACKDT        8A  O  8 45
     A            #SELLBY        8A  O  8 55
     A            #EXPDAT        8A  O  8 65
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140220  140758  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Weights Received'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
650aAA            SCDESC        58A  O  3 12
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 55'Sell By'
     A                                      DSPATR(HI)
     A                                  6 66'Expire'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(ND)
     A                                  7  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 45'  Pack  '
     A                                      DSPATR(HI)
     A                                  7 45'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 35'  Prod  '
     A                                      DSPATR(HI)
     A                                  7 35'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Weight      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20140220  164122  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            #UILCNS   R        O  8  5REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            #GTIN         14A  O  8 21
     A            #LOT          20A  O  8 36
     A            #SERIAL       20A  O  8 57
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20140220  130836  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Weights Received'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(ND)
     A                                  7  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'     GTIN     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'Lot Number          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 57'Serial Number       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*         R SFL2                      SFL
     A*
     A          R SFL3                      SFL
     A*%%TS  SD  20140220  140854  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A            #UILCNS   R        O  8  5REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            #UIUCI        60A  O  8 21
     A          R SF3CTL                    SFLCTL(SFL3)
     A*%%TS  SD  20140220  130836  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Weights Received'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(ND)
     A                                  7  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'Scanned                           -
     A                                                                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A*%%TS  SD  20090226  172828  DAVE        REL-V6R1M0  5761-WDS
650aDA*                                23  2'F3=Exit  F5=Refresh               -
650aDA*                                        F10=Prev view  F11=Next view  F1-
650aDA*                                     2=Cancel'
650aMA                                 23  2'F3=Exit  F5=Refresh  F10=Prev view-
650aMA                                        F11=Next view  F12=Cancel  F21=Pr-
650aMA                                      int'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20090226  161952  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(2 20 20 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order . . . .'
     A            W1SORT         1A  B  3 15DSPATR(HI)
     A                                  3 20'Ascending, Descending'
     A                                  4  1'Order by  . .'
     A            W1OBY          1A  B  4 15DSPATR(HI)
     A                                  4 20'1 = Lcns / Date'
     A                                  5 20'2 = Date / Lcns'
     A                                  6  1'Warehouse . .'
     A            W1WHSE         3Y 0B  6 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  7  1'Action. . . .'
     A            W1ACTION       3A  B  7 15DSPATR(HI)
     A                                  7 32'ALL'
     A                                  8  1'License . . .'
     A            W1LCNS        15A  B  8 15DSPATR(HI)
     A                                  8 32'*ALL'
     A                                  9  1'Dept  . . . .'
     A            W1WHDP        12A  B  9 15DSPATR(HI)
     A                                  9 32'*ALL'
     A                                 10  1'Slot  . . . .'
     A            W1SLOT        12A  B 10 15DSPATR(HI)
     A                                 10 32'*ALL'
     A                                 11  1'Base Slot . .'
     A            W1BASE        12A  B 11 15DSPATR(HI)
     A                                 11 32'*ALL'
     A                                 14  1'License Item.'
     A            W1ITEML       15A  B 14 15DSPATR(HI)
     A                                 14 32'*ALL'
     A                                 15  1'Employee# . .'
     A            W1EMP#         5Y 0B 15 15EDTCDE(3)
     A                                      DSPATR(HI)
     A                                 15 32'0=All'
     A                                 16  1'Tran# . . . .'
     A            W1TRN#         7Y 0B 16 15EDTCDE(3)
     A                                      DSPATR(HI)
     A                                 16 32'0=All'
     A                                 17  1'Hide USR  . .'
     A            W1HIDE         1   B 17 15DSPATR(HI)
     A                                 17 32'Y,N'
XXX  A                                 18  1'PO# . . . . .'
XXX  A            W1PO           9A  B 18 15DSPATR(HI)
XXX  A                                 18 32'*ALL'
     A                                 19  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 12  1'Slot Item . .'
     A            W1ITEM        15A  B 12 15DSPATR(HI)
     A                                 12 32'*ALL'
     A                                 13  1'From/To Date.'
     A            W1FDAT         6Y 0B 13 15
     A                                      DSPATR(HI)
     A            W1TDAT         6Y 0B 13 22
     A                                      DSPATR(HI)
     A                                 13 32'0=All'
650aAA          R WINDOWP
650aAA                                      WINDOW(6 20 8 40)
650aAA                                  1 12'Printer Information'
650aAA                                      DSPATR(HI)
650aAA                                  4  1'Device . . .'
650aAA            WPDEV         10A  B  4 15DSPATR(HI)
650aAA                                  4 27'*JOB, name'
650aAA                                  5  1'Form . . . .'
650aAA            WPFORM        10A  B  5 15DSPATR(HI)
650aAA                                  5 27'*STD, name'
650aAA                                  7  1'F3=Exit  F12=Cancel'
650aAA                                      COLOR(BLU)
