     A*%%***********************************************************************
     A*%%TS  RD  19930715  163657  DAVE        REL-V2R1M1  5738-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A*
     A*   RN161CPR  Customer returns report - sort 3 by route/item/date
     A*   08 December 2014
     A*   Lynn McMahon
     A*
     A*  ****  COMPILE WITH 198 COLUMNS AND 15 CPI
     A*
650aAA*    12/08/14  LMC  6.50a
     A*      - Copied from RN161APR.                                   d
     A*
     A**************************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    77
     A                                      DFT('Customer Returns Report')
     A                                   180
     A                                      DFT('Page')
     A            PAGE           4S 0O   184
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O    76
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   182
     A                                      DFT('RN161C')
     A            WHSE          30A  O    74
     A                                      SPACEB(001)
     A                                    82'By Route/Item/Date'
     A                                      SPACEB(001)
     A                                    10'Item:'
     A                                      SPACEB(001)
     A            ITEM          15A  O    16
     A                                    40'Reason code:'
     A            CODE           4A  O    53
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Item Number    ')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Description             ')
     A                                      UNDERLINE
     A                                    41
     A                                      DFT('      ')
     A                                      UNDERLINE
     A                                    48
     A                                      DFT('Date    ')
     A                                      UNDERLINE
     A                                    57
     A                                      DFT('Time ')
     A                                      UNDERLINE
     A                                    63
     A                                      DFT('User      ')
     A                                      UNDERLINE
     A                                    74
     A                                      DFT('Emp# ')
     A                                      UNDERLINE
     A                                    80
     A                                      DFT('Employee Name       ')
     A                                      UNDERLINE
     A                                   101
     A                                      DFT('Quantity  ')
     A                                      UNDERLINE
     A                                   112
     A                                      DFT('Code')
     A                                      UNDERLINE
     A                                   117
     A                                      DFT('Corresponding Item')
     A                                      UNDERLINE
     A                                   136
     A                                      DFT('Disp Cd')
     A                                      UNDERLINE
     A                                   144
     A                                      DFT('Route')
     A                                      UNDERLINE
     A                                   150
     A                                      DFT('Stop')
     A                                      UNDERLINE
     A                                   155
     A                                      DFT('Memo                ')
     A                                      UNDERLINE
     A                                   175
     A                                      DFT('          ')
     A                                      UNDERLINE
     A                                   186
     A                                      DFT('Tran#  ')
     A                                      UNDERLINE
     A                                   194
     A                                      DFT('Mult#')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  92        ORITEM    R        O     1
     A                                      REFFLD(ORREC/ORITEM *LIBL/ORRTN)
     A  92        ITDESC    R        O    17
     A                                      REFFLD(ITDESC PIRITEM)
     A  91        W1DATE         6S 0O    48
     A                                      EDTCDE(Y)
     A            W1TIME         4S 0O    57
     A                                      EDTWRD('  :  ')
     A            ORRUSR    R        O    63
     A                                      REFFLD(ORREC/ORRUSR *LIBL/ORRTN)
     A            W1EMP#         5  0O    74
     A                                      EDTCDE(Z)
     A            W1NAM         20A  O    80
     A            W1QTY         10A  O   101
     A            ORRCOD    R        O   113
     A                                      REFFLD(ORREC/ORRCOD *LIBL/ORRTN)
     A            ORCITM    R        O   117
     A                                      REFFLD(ORREC/ORCITM *LIBL/ORRTN)
     A            W1DSPC         5A  O   137
     A            W1RTE          5A  O   144
     A            W1STP          2  0O   151
     A            W1MEM1        30A  O   155
     A            W1TRN#         7  0O   186
     A                                      EDTCDE(Z)
     A            W1MLT#         2   O   195
     A*%%***********************************************************************
     A          R DETAIL2
     A                                     6'Cust#:'
     A            W1CUST        11S 0O    13 EDTCDE(Z)
     A            W1CNAM        40A  O    30
     A                                    85'Invoice#:'
     A            W1INV#        12A  O    +2
     A                                    +5'Order#:'
     A            W1ORD          7S 0O    +2 EDTCDE(Z)
     A                                    +5'Org Order#:'
     A            W1OORD         7S 0O    +2 EDTCDE(Z)
     A            W1MEM2        15A  O   165
     A*%%***********************************************************************
     A          R DETAILD
     A                                     6'Cust#:'
     A            W1CSTA        11A  O    13
     A                                    85'Invoice#:'
     A            W1INV#        12A  O    +2
     A                                    +5'Order#:'
     A            W1ORD          7S 0O    +2 EDTCDE(Z)
     A                                    +5'Org Order#:'
     A            W1OORD         7S 0O    +2 EDTCDE(Z)
     A            W1MEM2        15A  O   165
     A*
     A* Print detail line3
     A*
     A          R DETAIL3
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'NO RECORDS MATCH ENTERED CRITERIA'
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('RN161C: End of Report')
     A*
