     A*%%TS  SD  20060614  151747  ROBIN       REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
414 MA*  RH140AFM  Printed routes (One whse) - Display
     A*  11 June 1996
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*  Roxanne Harrison
     A*
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. RH140AFM COPY AND NAME RH140A01
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
414 AA*    06/30/00  DAS  4.14
     A*      - Changed indicator for SFLNXTCHG from 85 to 21.
     A*        Re: Program never changed to use 85 instead of 21.
     A*
416 AA*    09/17/01  DAS  4.16
     A*      - Revised wording in DELREC to say 4=Unallocate.
416aAA*    04/16/02  MLB  4.16a
     A*      - Added new function key to call Dock Matrix display.
510aAA*    11/01/06  RH   5.10a
     A*      - Merged GSC & GSC1 code from PIRSTD library.
520 AA*    05/30/08  RBD  5.20  P#00060
     F*      - Added F19 function key, link to Loader system.
520aAA*    07/08/09  JCJ  5.20a
     A*      - Enh: Renamed to RH140A01.
530  A*    09/02/10  RBD  5.30
     A*      - Renamed to RH140A02.
     A*      - Replaced display of exported date and time with driver.
     A*****************************************************************
     A*  Client Custom Revisions: Ledyard's
     A*
LED AA*    04/27/05  MLB  LED
     A*      - Chg'd format DELREC to add new linse for opt
     A*        70=Export Cwgts
     A*
     A*  Client Custom Revisions: Packers
510  A*    06/14/06  RMC  5.10
     A*      - Enh: Create record format DSPSPF which is a duplicate
     A*      - of DSPSFL and added Shipment number for Packers.
     A*
     A*  Client Custom Revisions: Grocery Supply
GSC AA*    01/05/06  JCJ  GSC
     A*      - Added new Subfile/Control formats DSPSFLGS/DSPSFCGS
GSC1AA*    01/27/06  JCJ  GSC1
     A*      - Added new position-to display screen for GSC
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970625  084306  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RH140A'
     A                                  1 29'Work with Printed Routes'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Position to route . . . .'
     A            PTINP2         5A  B  3 75
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
GSC1AA*
GSC1AA* Position to record for GSC
GSC1AA*
GSC1AA          R POSTNGSC
GSC1AA                                      LOCK
GSC1AA                                      SLNO(03)
GSC1AA                                      OVERLAY
GSC1AA            PTDMY1        78A  O  1  2
GSC1AA                                  2 47'Change to warehouse .'
GSC1AA            PTINP1         3Y 0B  2 70EDTCDE(4)
GSC1AA  21                                  DSPATR(RI)
GSC1AA  01                                  DSPATR(PC)
GSC1AA            WHCODE         3Y 0O  2  2EDTCDE(3)
GSC1AA                                  2  6'-'
GSC1AA            WHDESC        30A  O  2  8
GSC1AA                                  3 47'Position to customer.'
GSC1AA            PTINPG        11Y 0B  3 70EDTCDE(4)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970610  120842  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT        10A  O 11 14
     A            RHTRUK    R        O 11 26REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1CUBE         9Y 3O 11 38EDTCDE(3)
     A            W1SWGT         9Y 2O 11 50EDTCDE(3)
     A            W1PCS          5Y 0O 11 62EDTCDE(3)
LED AA            W1CWGT         3A  O 11 72
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19970610  201910  DAVE        REL-V3R1M0  5763-PW1
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
520 AA                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(DSPSFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
222 DA*           RECNO          4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 38'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 50'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 62'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
LED AA  60                             10 69'Cwgt Export'
LED AA  60                                  DSPATR(HI)
LED AA  60                                  DSPATR(UL)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 26'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
GSC AA*****************************************************************
GSC AA*
GSC AA* Subfile for list display
GSC AA*
GSC AA          R DSPSFLGS                  SFL
GSC AA*%%TS  SD  20060105  173215  JEFF        REL-V5R3M0  5722-WDS
GSC AA  21                                  SFLNXTCHG
GSC AA            OPTION         2A  B 11  2
GSC AA  20                                  DSPATR(PC)
GSC AA N21N90                               DSPATR(UL)
GSC AA  21                                  DSPATR(RI)
GSC AA  90                                  DSPATR(PR)
GSC AA            RHRTE     R        O 11  6REFFLD(RHREC/RHRTE *LIBL/RTEHED)
GSC AA            W1CORD         7Y 0O 11 12EDTCDE(4)
GSC AA            W1CUST        11Y 0O 11 20EDTCDE(4)
GSC AA            W1CNAM        30A  O 11 32
GSC AA            W1DELD         6Y 0O 11 63EDTCDE(Y)
GSC AA            W1PCS          5Y 0O 11 72EDTCDE(3)
GSC AA*
GSC AA          R DSPSFCGS                  SFLCTL(DSPSFLGS)
GSC AA*%%TS  SD  20060105  173215  JEFF        REL-V5R3M0  5722-WDS
GSC AA                                      SFLSIZ(0011)
GSC AA                                      SFLPAG(0010)
GSC AA                                      CF10
GSC AA                                      CF11
GSC AA                                      CF17
GSC AA                                      CF18
520 AA                                      CF19
GSC AA                                      CF20
GSC AA                                      ROLLDOWN
GSC AA                                      ROLLUP
GSC AA                                      OVERLAY
GSC AA  81                                  SFLDSP
GSC AA  82                                  SFLDSPCTL
GSC AA  83                                  SFLCLR
GSC AA            FLD001         1A  O  9  3
GSC AA                                  9 12'Order'
GSC AA                                      DSPATR(HI)
GSC AA                                  9 20'Customer'
GSC AA                                      DSPATR(HI)
GSC AA                                  9 63'Delivery'
GSC AA                                      DSPATR(HI)
GSC AA                                 10  2'Opt'
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 10  6'Route'
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 10 12'Number '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 20'Number     '
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 10 32'Customer Name                 '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 10 63'Date    '
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 10 72'Pieces'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
414 DA* 85                                  SFLNXTCHG
     A*****************************************************************
510  A*Version 5.10 display Shipment Number for Packers DSPPSF
     A*****************************************************************
     A          R DSPPSF                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11 17REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT        10A  O 11 24
     A            RHTRUK    R        O 11 36REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1CUBE         9Y 3O 11 48EDTCDE(3)
     A            W1SWGT         9Y 2O 11 60EDTCDE(3)
     A            W1PCS          5Y 0O 11 73EDTCDE(3)
     A            W1SHIP        11A  O 11  5
     A*
     A          R DSPPFC                    SFLCTL(DSPPSF)
     A*%%TS  SD  20060614  151747  ROBIN       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
520 AA                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 48'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 60'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 72'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Shipment'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHIMPT    R        O 11 33REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHBLDT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHPRTT    R        O 11 71REFFLD(RHREC/RHPRTT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2RPTD         6Y 0O 11 14EDTCDE(Y)
     A            W2IMPD         6Y 0O 11 24EDTCDE(Y)
     A            W2BLDD         6Y 0O 11 43EDTCDE(Y)
     A            W2PRTD         6Y 0O 11 62EDTCDE(Y)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
520 AA                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 24'---- Imported ---'
     A                                      DSPATR(HI)
     A                                 10 43'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 43'----- Built -----'
     A                                      DSPATR(HI)
     A                                 10 62'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 62'---- Printed ----'
     A                                      DSPATR(HI)
     A                                 10 14'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 14'Report'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
414 DA* 85                                  SFLNXTCHG
414 MA  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
530 DA**          W3EXPD         6Y 0O 11 15EDTCDE(Y)
530 MA            TTRUSR    R        O 11 15REFFLD(TTRREC/TTRUSR *LIBL/TTRTE)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            W3STAT        10   O 11 53
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHRTID    R        O 11 67REFFLD(RHREC/RHRTID *LIBL/RTEHED)
530 DA**          RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
530 DA**                                    EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
520 AA                                      CF19
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
530 DA**                               10 15'  Date  '
530 DA**                                    DSPATR(HI)
530 DA**                                    DSPATR(UL)
530 DA**                               10 24'  Time  '
530 DA**                                    DSPATR(HI)
530 DA**                                    DSPATR(UL)
530 DA**                                9 15'---- Exported ---'
530 DA**                                    DSPATR(HI)
530 MA                                 10 15'Driver    '
530 MA                                      DSPATR(HI)
530 MA                                      DSPATR(UL)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 53'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
LED MA                                      s for'
LED AA                                      COLOR(BLU)
LED AA            CFRMTX        20A  O  3 42
LED DA*                                     s for 4=Unpick.'
416 DA**                                    s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
