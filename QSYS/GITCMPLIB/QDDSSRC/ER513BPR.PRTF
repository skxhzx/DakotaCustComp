     A*%%***********************************************************************
     A*
     A*   ER513BPR  Employee Activity Report Item,Date,Slot
     A*   05 Sept, 2001
     A*   Hemant Kapadia
     A*
     F*  Revisions
     F*
     F*    09/05/01  HNK  4.16
     F*      - Created
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +42
     A                                      DFT('Item/Slot Activity Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O   +43
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   124
     A                                      DFT('ER513B')
     A            WHSE          30A  O    52
     A                                      SPACEB(001)
     A                                    58'By Item/Date/Slot'
     A                                      SPACEB(001)
     A                                    50'Item:'
     A                                      SPACEB(001)
     A            ITEM          15A  O    56
     A                                    74'Slot:'
     A            DISP          12A  O    80
     A                                    93'Task:'
     A            TASK           6A  O    99
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Item     ')
     A                                      UNDERLINE
     A                                    12
     A                                      DFT('Description         ')
     A                                      UNDERLINE
     A                                    33
     A                                      DFT('  Date  ')
     A                                      UNDERLINE
     A                                    42
     A                                      DFT('From Slot')
     A                                      UNDERLINE
     A                                    55
     A                                      DFT('To Slot')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('Type    ')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('Qty  ')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('UOM  ')
     A                                      UNDERLINE
     A                                    91
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    99
     A                                      DFT('Employee            ')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A  92        W1ITEM        10A  O     1
     A  92        W1DESC        20A  O    12
     A  91        W1DATE         6S 0O    33
     A                                      EDTCDE(Y)
     A            WKDISP    R        O    42
     A                                      REFFLD(WKREC/WKDISP *LIBL/ER513A)
     A            WKDSP2    R        O    55
     A                                      REFFLD(WKREC/WKDSP2 *LIBL/ER513A)
     A            W1TYPE        10A  O    68
     A            W1QTY          5S 0O    79
     A                                      EDTCDE(K)
     A            WKUOM     R        O    85
     A                                      REFFLD(WKREC/WKUOM *LIBL/ER513A)
     A            WKTRN#    R        O    91EDTCDE(3)
     A                                      REFFLD(WKREC/WKTRN# *LIBL/ER513A)
     A            WKEMP#    R        O    99EDTCDE(3)
     A                                      REFFLD(WKREC/WKEMP# *LIBL/ER513A)
     A            W1NAME        20A  O   105
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'NO RECORDS MATCH ENTERED CRITERIA'
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('ER513B: End of Report')
     A*
