     A*%%TS  SD  19990927  141524  DAVE        REL-V4R2M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990927  141524  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT210.01'
     A                                  1 16'Initialize Slots to Specific Quant-
     A                                      ity for Testing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  6 51'1 - 999'
     A                                  7  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  7 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  7 51'*ALL, Code'
     A                                  8  4'Item . . . . . . . . . . . .'
     A            W1ITEM        15A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  8 51'*ALL, Code'
     A                                  8 65'(Y=Yes, N=No)'
     A                                      DSPATR(ND)
     A                                 11  4'Normal stock qty . . . . . .'
     A            W1QTY1         3Y 0B 11 34EDTCDE(3)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 11 51'0 - 999'
     A                                 12  4'Breakdown 1 stock qty  . . .'
     A            W1QTY2         3Y 0B 12 34EDTCDE(3)
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 12 51'0 - 999'
     A                                 13  4'Breakdown 2 stock qty  . . .'
     A            W1QTY3         3Y 0B 13 34EDTCDE(3)
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                 13 51'0 - 999'
     A                                 15  9'This program will initialize the s-
     A                                      tock quantities of slots that'
     A                                 16  9'contain an item and match the abov-
     A                                      e criteria. After the slots'
     A                                 17  9'have been initialized, the warehou-
     A                                      se quantities for ALL items'
     A                                 20  9'WARNING: All other quantities (All-
     A                                      ocated, Picked, Transfered,'
     A                                 21 18'Received) will be initialized to z-
     A                                      ero.'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
     A                                 18  9'will be recalculated.'
     A                                  5  2'Selection Criteria'
     A                                      DSPATR(HI)
     A                                 10  3'New Quantities'
     A                                      DSPATR(HI)
     A          R SCREEN2
     A*%%TS  SD  19990927  100512  DAVE        REL-V4R2M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL210.02'
     A                                  1 16'Initialize Slots to Specific Quant-
     A                                      ity for Testing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  6 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  7  4'Department . . . . . . . . .'
     A            W1WHDP    R        O  7 34REFFLD(W1WHDP *SRC)
     A                                  8  4'Item . . . . . . . . . . . .'
     A            W1ITEM    R        O  8 34REFFLD(W1ITEM *SRC)
     A                                  9  4'Normal stock quantity  . . .'
     A            W1QTY1    R        O  9 34REFFLD(W1QTY1 *SRC)
     A                                      EDTCDE(3)
     A                                 10  4'Breakdown 1 stock qty  . . .'
     A            W1QTY2    R        O  9 34REFFLD(W1QTY2 *SRC)
     A                                      EDTCDE(3)
     A                                 11  4'Breakdown 2 stock qty  . . .'
     A            W1QTY3    R        O  9 34REFFLD(W1QTY3 *SRC)
     A                                      EDTCDE(3)
     A                                 14 16'You are about to initialize all th-
     A                                      e slots'
     A                                 15 16'that match the above criteria.'
     A                                 17 16'Please make sure you know what you-
     A                                       are doing.'
     A                                 19 31'Press F10 to continue.'
     A                                 23  2'F12=Cancel'
     A          R SCREEN3
     A*%%TS  SD  19990927  100512  DAVE        REL-V4R2M0  5769-PW1
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL210.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                  6 13'to cancel your request.'
     A                                  9 17'If you are sure, press F22 to init-
     A                                      ialize slots.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A                                  1 16'Initialize Slots to Specific Quant-
     A                                      ity for Testing'
     A                                      DSPATR(HI)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
    DA*                                     SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
