     A*%%TS  SD  20030305  094645  ROCKY       REL-V4R2M0  5769-PW1
     A*
     A*  EQ322FM  Equip Check In/Out History - Options
     A*  25 February, 2003
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
     A*    02/25/03  RH   4.17 Created
     A*    07/23/03  RH   4.17a
     A*      - Add select by Equipment type and sort by employee/date
417bAA*    04/29/04  RH   4.17b
     A*      - Increase equip ID from 6 to 8.
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20030305  094645  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'EQ322.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'View/Print Equipment History'
     A                                      DSPATR(HI)
417aDA*                                 8  4'Warehouse  . . . . . . . . .'
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 53'Code'
     A                                  8  4'Equipment Type . . . . . . .'
     A            W1TYPE         6A  B  8 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 53'Code, *All'
     A                                  9  4'- OR - '
     A                                 10  4'Equipment ID Code  . . . . .'
417bDA*           W1EQID         6A  B 10 34
417b A            W1EQID         8A  B 10 34
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 55'Code, *All'
     A                                 11  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B 11 34EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 42'to'
     A            W1TODT         6Y 0B 11 46EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'MMDDYY'
     A                                 12  4'Order report . . . . . . . .'
     A            W1SORT         1Y 0B 12 34
     A N28N90                               DSPATR(HI)
     A N28N90                               DSPATR(UL)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 53'1 = Date'
     A                                 13 53'2 = Equipment/Date'
417aAA                                 14 53'3 = Employee/Date'
417aDA*                                12 53'2 = Equipment/Date'
     A                                 16  4'View or Print  . . . . . . .'
     A            W1VP           1A  B 16 34
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 53'V=View, P=Print'
     A                                 17  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 17 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 53'Y=Yes, N=No'
     A                                 18  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 18 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 19 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
