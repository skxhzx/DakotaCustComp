     A*%%TS  SD  19960305  093550  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*  Revisions
411 AF*    05/03/99  RH
     F*      - Add parm W1ESLT exception slots.
     F*      - Add parm W1USLT unentered slots.
416 AA*  09/16/02  RH   4.16
     A*    - Added DEPARTMENT.
416bAA*  01/27/03  RH   4.16b
     F*      - Add parm W1RSLT error slots. Add F7 error slots.
     A*
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960228  144312  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IV140.01'
     A                                  1 31'Inventory Status '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  7  4'Warehouse number . . . . . .'
     A            W1WHSE         3Y 0B  7 34
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      EDTCDE(4)
416  A                                  9  4'Department . . . . . . . . .'
416  A            W1WHDP         5A  B  9 34
416  A  22                                  DSPATR(RI)
416  A  02                                  DSPATR(PC)
416  A N22                                  DSPATR(HI)
416  A                                  9 43'*ALL'
     A                                 23  2'F3=Exit   F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960305  093550  ROCKY       REL-V3R1M0  5763-PW1
416BAA                                      CF07
     A                                      CF08
414aAA                                      CF09
     A                                  1  2'IV140.02'
     A                                  1 31'Inventory Status '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  3'Sheet numbers from'
     A                                 23  2'F3=Exit  F7=Error slots  F8=Unent-
414aAA                                      ered sheets  F9=Unentered slots -
414aAA                                      F12=Cancel'
     A                                      COLOR(BLU)
     A                                 12  3'Sheets pending entry  . . .'
     A                                 14  3'Sheets entered  . . . . . .'
     A            W1PPAG         5Y 0O 12 32EDTCDE(3)
     A            W1EPAG         5Y 0O 14 32EDTCDE(3)
     A                                  4  3'Warehouse # '
     A            W2WHSE         3Y 0O  4 17EDTCDE(4)
416  A                                  5  3'Department  '
416  A            W2WHDP         5   O  5 17
     A                                 10  3'Sheets created  . . . . . .'
     A            W1CPAG         5Y 0O 10 32EDTCDE(3)
     A            W1PLIN        18   O  8 22
411 AA                                 16  3'Exception slots . . . . . .'
     A  92                                  DSPATR(ND)
411 AA            W1ESLT         5Y 0O 16 32EDTCDE(3)
     A  92                                  DSPATR(ND)
411 AA                                 18  3'Unentered slots . . . . . .'
     A  92                                  DSPATR(ND)
411 AA            W1USLT         5Y 0O 18 32EDTCDE(3)
     A  92                                  DSPATR(ND)
416bAA                                 20  3'Error slots . . . . . . . .'
416b A  92                                  DSPATR(ND)
416bAA            W1RSLT         5Y 0O 20 32EDTCDE(3)
416b A  92                                  DSPATR(ND)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
