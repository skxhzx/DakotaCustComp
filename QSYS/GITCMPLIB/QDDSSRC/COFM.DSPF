     A*%%TS  SD  20020829  165528  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  COFM    Customer order detail
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    08/28/02  DAS  4.16
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20020829  111118  DAVE        REL-V4R4M0  5769-PW1
     A            SFLHIDE       45A  H
     A            SFLLINE       79A  O  6  2
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20020829  165528  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0032)
     A                                      SFLPAG(0016)
     A                                      ROLLUP
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR
     A            SCRNAME        8A  O  1  2
     A                                  1 30'Customer Order Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        59A  O  2 11
     A                                  2 71TIME
     A            SFLHEAD1      79A  O  4  1DSPATR(HI)
     A            SFLHEAD2      79A  O  5  1DSPATR(HI)
     A*
     A          R FKEY1
     A*
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                    +2'F9=Select/Sort'
     A                                      COLOR(BLU)
     A                                    +2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20020829  133822  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(6 20 8 40)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Warehouse . . . . .'
     A            W1WHSE         3Y 0B  3 21DSPATR(HI)
     A                                      COMP(GE 0)
     A                                      EDTCDE(3)
     A                                  4  1'Customer ord# . . .'
     A            W1ORD#         7Y 0B  4 21DSPATR(HI)
     A                                      COMP(GE 0)
     A                                      EDTCDE(3)
     A                                  5  1'Sort by . . . . . .'
     A            W1SORT         1A  B  5 21DSPATR(HI)
     A                                      VALUES('1' '2')
     A                                  5 28'1=Seq, 2=Item'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
