     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LB411FM   GS1- 99 LABEL - OPTIONS
     A*  01 April, 2013
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
     A*    04/01/13  LMC  6.50a
     A*      - Date Created.
650b A*    09/11/14  LMC  6.50b
     A*      - changed weight field to not require dec position
     A*      - changed display file from LB411FM to LB41101.
     A*      - Added warning override on CW variance.
     A*
700aAA*    04/01/16  LMC  7.00a
     A*      - Added Metric system changes.
     A*      - changed display file from LB41101 to LB41102.
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(01)
     A                                      OVERLAY
     A          R SCREEN1
     A                                      CF04
700aAA                                      CF07
650bAA                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LB411.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Item number. . . . .'
     A  43                                  DSPATR(ND)
     A            W1ITEM        15A  B 10 25
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 43                                  DSPATR(PR)
     A  43                                  DSPATR(ND)
     A                                 19  4'Quantity . . . . . .'
     A            W1QTY          2Y 0B 19 25
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90
     AO 45                                  DSPATR(PR)
     A                                 19 53'1 - 99'
     A                                 20  4'Device . . . . . . .'
     A            W1DEV         10A  B 20 25
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21  4'Form . . . . . . . .'
     A            W1FORM        10A  B 21 25
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aAA            CMDLN1        79A  O 23  2COLOR(BLU)
700aDA*                                23  2'F3=Exit   F4=Select   F5=Refresh  -
650bDA*                                      F12=Cancel'
700aDA*                                      F12=Cancel  F20=Override'
700aDA*                                     COLOR(BLU)
     A                                 12  4'Catch Weight . . . .'
     A N41                                  DSPATR(ND)
650bDA*           W1DWGT         6Y 2B 12 25EDTCDE(4)
650bMA            W1DWGT         6Y 0B 12 25EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A N41
     AO 90                                  DSPATR(PR)
     A N41                                  DSPATR(ND)
700aAA            W1KGLB         3A  O 12 32
700aAA N41                                  DSPATR(ND)
700aDA*                                12 34'(assumed decimal point with 2 deci-
700aMA                                 12 36'(assumed decimal point with 2 deci-
650bAA                                      mal places)'
650bAA                                      COLOR(BLU)
650bAA N41                                  DSPATR(ND)
     A                                 13  4'Lot code . . . . . .'
     A N42                                  DSPATR(ND)
     A            W1LOTC        20A  B 13 25
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A N42
     AO 90                                  DSPATR(PR)
     A N42                                  DSPATR(ND)
700aDA*           W1WGTTYPE      2A  B 12 38
700aDA*N25N90                               DSPATR(HI)
700aDA* 25                                  DSPATR(RI)
700aDA*N25N90                               DSPATR(UL)
700aDA* 05                                  DSPATR(PC)
700aDA*N41
700aDA* 90                                  DSPATR(PR)
700aDA*N41                                  DSPATR(ND)
     A                                  8  4'Scan GS1-99 Label. .'
     A  44                                  DSPATR(ND)
     A            W1GS199       50A  B  8 25
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 44                                  DSPATR(PR)
     A  44                                  DSPATR(ND)
     A          R SCREEN1R8
700aAA                                      CF07
     A                                      CF20
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'LB411.01R8'
     A            RPTTTL        40A  O  1 13DSPATR(HI)
     A N46 43                           3  2'Dept'
     A N46 43     W1WHDP         5A  B  3  7
     A N43 46                           4  2'Item'
     A N46 43                           4  2'Slot'
     A N43 46     W1ITEM        15A  B  4  7
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 43                                  DSPATR(PR)
     A  43                                  DSPATR(ND)
     A                                  4  2'Slot'
     A  46N43                               DSPATR(ND)
     A N46 43     W1DISP        12A  B  4  7
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
     AO 46                                  DSPATR(PR)
     A  46                                  DSPATR(ND)
700aAA            CMDLN1        79A  O  7  2COLOR(BLU)
700aDA*                                 7  2'F3=Exit   F4=Select -
700aDA*                                     F12=Cancel  F20=Override'
700aDA*                                     COLOR(BLU)
     A                                  5  2'C/W '
     A N41                                  DSPATR(ND)
650bDA*           W1DWGT         6Y 2B  5  7EDTCDE(4)
650bMA            W1DWGT         6Y 0B  5  7EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A N41
     AO 90                                  DSPATR(PR)
     A N41                                  DSPATR(ND)
700aAA            W1KGLB         3A  O  5 14
700aAA N41                                  DSPATR(ND)
700aDA*                                 5 14'(assumed decimal point with 2 deci-
700aMA                                  5 18'(assumed decimal point with 2 deci-
650bAA                                      mal places)'
650bAA                                      COLOR(BLU)
650bAA N41                                  DSPATR(ND)
     A                                  6  2'Lot '
     A N42                                  DSPATR(ND)
     A            W1LOTC        20A  B  6  7
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A N42
     AO 90                                  DSPATR(PR)
     A N42                                  DSPATR(ND)
700aDA*           W1WGTTYPE      2A  B  5 16
700aDA*N25N90                               DSPATR(HI)
700aDA* 25                                  DSPATR(RI)
700aDA*N25N90                               DSPATR(UL)
700aDA* 05                                  DSPATR(PC)
700aDA*N41
700aDA* 90                                  DSPATR(PR)
700aDA*N41                                  DSPATR(ND)
     A                                  2  2'Scan GS1'
     A  44                                  DSPATR(ND)
     A            W1GS199       50A  B  2 11
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 44                                  DSPATR(PR)
     A  44                                  DSPATR(ND)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC8                   SFL
     A                                      SFLMSGRCD(08)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A          R MSGCTL8                   SFLCTL(MSGREC8)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R SCREEN2
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LB411.02'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 19  4'Quantity . . . . . .'
     A                                      DSPATR(ND)
     A            W2QTY          2Y 0B 19 25
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 20  4'Device . . . . . . .'
     A            W2DEV         10A  B 20 25
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21  4'Form . . . . . . . .'
     A            W2FORM        10A  B 21 25
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
650bDA*                                      F12=Cancel'
650bMA                                       F12=Cancel  F20=Override'
     A                                      COLOR(BLU)
     A                                  8  4'Enter GS1 ID . . . . . .'
     A  44                                  DSPATR(ND)
     A            W2GS199       12A  B  8 29
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                 10  4'Harvest date . . . . . .'
     A            W2HDTE         6  0O 10 29EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                 10 39'MMDDYY'
     A                                      DSPATR(ND)
     A                                 12  4'Harvest location . . . .'
     A            W2HLOC        20   O 12 29DSPATR(HI)
     A                                 14  4'Original Shipper Cert# .'
     A            W2CERT        20   O 14 29DSPATR(HI)
