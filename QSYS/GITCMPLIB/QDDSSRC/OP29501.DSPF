     A*%%TS  SD  20090310  180019  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP295FM   PIR options - Item Tracking Default Levels
     A*  15 November 2004
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
500 AA*    11/15/04  DAS  5.00
     A*      - Created.
     A*
500aAA*    01/25/05  DAS  5.00a
     A*      - Enh: Added '(Future)' to Medium and High options.
     A*        Only going to allow Low option in this initial release.
     A*
600aAA*    02/23/09  DAS  6.00a
     A*      - Renamed to OP29501.
     A*      - Removed 'future' from H and M.
     A*
610aAA*    06/24/09  RBD  6.10a
     A*      - Corrected spelling, manditory to mandatory.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20090310  180019  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                      CF09
     A                                  1  2'OP295.01'
     A                                  1 27'Item Tracking Default Levels'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Date sensitive items . . . . . . .-
     A                                       . . . . . . . . . . . . .'
     A            W1DATE         1A  B  7 66
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  7 71'M,H'
     A                                  8  4'Lot code items . . . . . . . . . .-
     A                                       . . . . . . . . . . . . .'
     A            W1LOT          1A  B  8 66
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  8 71'M,H'
     A                                  9  4'Catchweight items  . . . . . . . .-
     A                                       . . . . . . . . . . . . .'
     A            W1CWGT         1A  B  9 66
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  9 71'M,H'
     A                                 10  4'All other items  . . . . . . . . .-
     A                                       . . . . . . . . . . . . .'
     A            W1OTHR         1A  B 10 66
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 10 71'L,M,H'
     A                                 11 22'Use F9 to set flags for existing i-
     A                                      tems'
     A                                      DSPATR(HI)
     A                                 13  6'Low    - User is not asked for any-
     A                                       extra information.'
     A                                 14 15'All tracking is done in the backgr-
     A                                      ound.'
     A                                 16  6'Medium - User may be asked to appl-
     A                                      y new license plate to a split quan-
     A                                      tity.'
600aDA**                               17  6'(future)'
     A                                 17 15'Applying new license plate is mand-
610aDA**                                    itory.'
610aMA                                      atory.'
     A                                 19  6'High   - User may be asked to appl-
     A                                      y new license plate to a split quan-
     A                                      tity.'
600aDA**                               20  6'(future)'
     A                                 20 15'Applying new license plate is mand-
610aDA**                                    itory.'
610aMA                                      atory.'
     A                                 21 15'User may be asked to confirm licen-
     A                                      se plate being used for task.'
     A                                 23  2'F3=Exit  F5=Refresh  F9=Set item f-
     A                                      lags  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20041116  061117  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                      CA10
     A                                  1  2'OP295.01'
     A                                  1 27'Default Item Tracking Levels'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 31'Change Item Records'
     A                                      DSPATR(HI)
     A            WHCODE         3Y 0O  4  2EDTCDE(3)
     A                                  4  6'-'
     A            WHDESC        30A  O  4  8
     A                                  7 16'Are you sure you want to change th-
     A                                      e tracking level'
     A                                  8 16'for ALL items to the default value-
     A                                      s listed here?'
     A                                 10 26'Date sensitive items . . .'
     A            W1DATE         1A  O 10 54
     A                                 11 26'Lot code items . . . . . .'
     A            W1LOT          1A  O 11 54
     A                                 12 26'Catchweight items  . . . .'
     A            W1CWGT         1A  O 12 54
     A                                 13 26'All other items  . . . . .'
     A            W1OTHR         1A  O 13 54
     A                                 16 17'Press F10 to change tracking level-
     A                                       of ALL items'
     A                                 18 18'Press F3/F12 to cancel without cha-
     A                                      nging items'
     A                                 23  2'F3=Exit  F5=Refresh  F10=Change it-
     A                                      em records  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
