     A*%%TS  SD  20131104  195150  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  MNU112FM   Custom Menu Maintenance - Change
     A*  November 4, 2013
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20131104  194506  RODR        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'MNU112.01'
     A                                  1 29'Custom Menu Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Menu Name . . . . . . . .'
     A            W1KEY         10A  B  8 31
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  91
     AO 90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A                                  9  4'Sequence  . . . . . . . .'
     A            W1SEQ          3S 0B  9 31
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  91
     AO 90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A                                      CHECK(RB)
     A                                 10  4'Display Y/N . . . . . . .'
     A            W1ACT          1A  B 10 31
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A                                 11  4'Option Number . . . . . .'
     A            W1OPT          2S 0B 11 31
     A N24N90                               DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A                                 12  4'Option Text . . . . . . .'
     A            W1TXT         32A  B 12 31
     A N25N90                               DSPATR(HI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 13  4'Command . . . . . . . . .'
     A            W1CMD        120A  B 13 31
     A N26N90                               DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 11'F4=Prompt'
     A                                      COLOR(BLU)
     A                                 23 22'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 34'F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
