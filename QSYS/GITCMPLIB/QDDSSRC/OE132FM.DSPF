     A********************************************************************
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*****************************************************************
     A*
     A*  OE132FM   Customer Truck table maintneance - Update/Inquiry
     A*  July 11, 2012
     A*  Ginger Andrews
     A*
     A*****************************************************************
     A*  Revisions:
     A*
640  A*    07/11/12  GJA  6.40
     A*      - Created
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OE132.01'
     A                                  1 26'Customer Truck Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Route . . . . . . . . .'
     A            W1RTE          5   B  8 29
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Truck . . . . . . . . .'
     A            W1TRUK        10   B 10 29
     A  02                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Driver. . . . . . . . .'
     A            W1DRVR        10   B 11 29
     A  03                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  50                             23  2'F5=Refresh  F12=Cancel'
     A N50                             23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
