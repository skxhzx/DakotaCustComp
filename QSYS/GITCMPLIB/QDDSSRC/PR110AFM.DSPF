     A*%%TS  SD  20031002  064818  BFC         REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  PR110AFM  View priority replenishments by item
     A*  23 August 2000
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
414 AA*    08/23/00  DAS  4.14
     A*     - Created
     A*
417 AA*    10/01/03  MLB  4.17
     A*     - Enh: Added support to allow user to display list by
     A*       department, aisle range. Added ability for user to
     A*       display Open, Closed or Both Priority Replenishment
     A*       types.
417aAA*    10/07/03  MLB  4.17a
     A*     - Enh: Added new field; PRRPLT - Replenishment type to
     A*       View 3.
     A*     - Enh: Added new field; W2ITMT - Item type description to
     A*       View 2.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000823  163625  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'PR110A'
     A                                  1 23'View Priority Replenishments by It-
     A                                      em'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20000823  163625  DAVE        REL-V4R4M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
417 DA*           PTDMY1        78A  O  1  2
417 AA            WHCODE         3Y 0O  1  2EDTCDE(3)
417 AA                                  1  6'-'
417 AA            WHDESC        30A  O  1  8
417 AA                                  1 43'Change to warehouse .'
417 AA            PTINP1         3Y 0B  1 65EDTCDE(4)
417 AA  21                                  DSPATR(RI)
417 AA  11                                  DSPATR(PC)
417 DA*           WHCODE         3Y 0O  2  2EDTCDE(3)
417 DA*                                 2  6'-'
417 DA*           WHDESC        30A  O  2  8
417 DA*                                 2 43'Change to warehouse .'
417 DA*           PTINP1         3Y 0B  2 65EDTCDE(4)
417 DA* 21                                  DSPATR(RI)
417 DA* 01                                  DSPATR(PC)
417 AA            WDCODE         5A  O  2  2
417 AA                                  2  8'-'
417 AA            WDDESC        30A  O  2 10
417 AA                                  2 43'Change to department.'
417 AA            PTINP2         5A  B  2 65
417 AA  22                                  DSPATR(RI)
417 AA  12                                  DSPATR(PC)
417 AA            WOSTAT         4A  O  3  2
417 AA                                  3  7'-'
417 AA            WOSTSD        30A  O  3  9
417 AA                                  3 43'Status *OPN,*CLS,*ALL'
417 AA            PTINP5         4A  B  3 65
417 AA  25                                  DSPATR(RI)
417 AA  15                                  DSPATR(PC)
417 AA                                  4 43'Aisle range . . . . .'
417 AA            PTINP3         3A  B  4 65
417 AA  23                                  DSPATR(RI)
417 AA  13                                  DSPATR(PC)
417 AA                                  4 69'to'
417 AA            PTINP4         3A  B  4 72
417 AA  24                                  DSPATR(RI)
417 AA  14                                  DSPATR(PC)
417 DA*                                 3 43'Position to item  . .'
417 DA*           PTINP2        15A  B  3 65
     A*
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000824  093406  DAVE        REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PRITEM    R        O 11  7REFFLD(PRREC/PRITEM *LIBL/PRPLFIL)
     A            PRWHDP    R        O 11 23REFFLD(PRREC/PRWHDP *LIBL/PRPLFIL)
     A            PRSEQ#    R        O 11 29REFFLD(PRREC/PRSEQ# *LIBL/PRPLFIL)
     A                                      EDTCDE(3)
     A            PRFDIS    R        O 11 35REFFLD(PRREC/PRFDIS *LIBL/PRPLFIL)
     A            PRTDIS    R        O 11 48REFFLD(PRREC/PRTDIS *LIBL/PRPLFIL)
     A            PRQTY     R        O 11 61REFFLD(PRREC/PRQTY *LIBL/PRPLFIL)
     A                                      EDTCDE(3)
     A            PRAQS     R        O 11 66REFFLD(PRREC/PRAQS *LIBL/PRPLFIL)
     A                                      EDTCDE(J)
     A            PRFLAG    R        O 11 78REFFLD(PRREC/PRFLAG *LIBL/PRPLFIL)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000823  163625  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
417 DA*           FLD001         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 23'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 29'Seq #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 35'From Slot   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 48'To Slot     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66'Needed   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 76'Flag'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20000824  093211  DAVE        REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PRITEM    R        O 11  7REFFLD(PRREC/PRITEM *LIBL/PRPLFIL)
     A            PRWHDP    R        O 11 23REFFLD(PRREC/PRWHDP *LIBL/PRPLFIL)
     A            PRSEQ#    R        O 11 29REFFLD(PRREC/PRSEQ# *LIBL/PRPLFIL)
     A                                      EDTCDE(3)
     A            W2ADTE         6Y 0O 11 35EDTCDE(Y)
     A            PRATIM    R        O 11 44REFFLD(PRREC/PRATIM *LIBL/PRPLFIL)
     A                                      EDTWRD(' 0:  :  ')
417aAA            W2ITMT        10A  O 11 53
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 23'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 29'Seq #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 35'----- Added -----'
     A                                      DSPATR(HI)
     A                                 10 35'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 44'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
417aAA                                 10 53'Item Type '
417aAA                                      DSPATR(UL)
417aAA                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000824  093211  DAVE        REL-V4R4M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PRITEM    R        O 11  7REFFLD(PRREC/PRITEM *LIBL/PRPLFIL)
     A            PRWHDP    R        O 11 23REFFLD(PRREC/PRWHDP *LIBL/PRPLFIL)
     A            PRSEQ#    R        O 11 29REFFLD(PRREC/PRSEQ# *LIBL/PRPLFIL)
     A                                      EDTCDE(3)
417aAA            W3RPLT        10A  O 11 35
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
222 DA*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
222 DA*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 23'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 29'Seq #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
417aAA                                 10 35'Repl Type '
417aAA                                      DSPATR(UL)
417aAA                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
417 DA*                                     SLNO(06)
417 MA                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
