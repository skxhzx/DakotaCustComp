     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A********************************************************************
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  EX165FM      RF Selection exception report
     A*  23 March 2006
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
500 AA*     03/23/06  RH   5.00
     A*       - Created using EX155FM                                  and
510aAA*     08/08/06  RH   5.10a
     A*       - Add time range to screen                               and
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'EX165.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 10 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'(Y=Yes, N=No)'
     A                                 11  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 11 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 12 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20011003  135718  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'EX165.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Item . . . . . . . . . . . .'
     A            W2ITEM        15A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 50'(Item # or *ALL)'
     A                                 11  4'Date range . . . . . . . . .'
     A            W2FMDT         6Y 0B 11 34EDTCDE(4)
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 42'to'
     A            W2TODT         6Y 0B 11 46EDTCDE(4)
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'MMDDYY'
510a A                                 12  4'Time range . . . . . . . . .'
510a A            W2FMTM         4Y 0B 12 34EDTCDE(4)
510a A N23N90                               DSPATR(HI)
510a A  23                                  DSPATR(RI)
510a A N23N90                               DSPATR(UL)
510a A  03                                  DSPATR(PC)
510a A  90                                  DSPATR(PR)
510a A                                 12 41'to'
510a A            W2TOTM         4Y 0B 12 44EDTCDE(4)
510a A N24N90                               DSPATR(HI)
510a A  24                                  DSPATR(RI)
510a A N24N90                               DSPATR(UL)
510a A  04                                  DSPATR(PC)
510a A  90                                  DSPATR(PR)
510a A                                 12 59'Time (HHMM)'
510a A*                                12  4'Include status . . . . . . .'
510a A*           W2STAT         1A  B 12 34
510a A                                 13  4'Include status . . . . . . .'
510a A            W2STAT         1A  B 13 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 50'(O=Open, C=Closed, B=Both)'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
