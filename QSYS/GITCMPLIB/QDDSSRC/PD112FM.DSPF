     A*%%TS  SD  19951221  152624  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PD112FM   Device File  Maintenance - Change screen
     A*  21 Dec.  1995
     A*  Hemant Kapadia/RH
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     F*
413 AF*    02/11/00  HNK/RH  4.13
     F*      - File PIRDEV obsolete. Program not used.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19951221  152624  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PD112.01'
     A                                  1 29'Device File Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Device . . . . . . . .'
     A            W1DEV         10A  B  8 29
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Type . . . . . . . . .'
     A            W1TYPE         1A  B  9 29
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 10  4'Warehouse  . . . . . .'
     A                                 11  4'Department . . . . . .'
     A                                 12  4'Section  . . . . . . .'
     A            W1WHSE         3Y 0B 10 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90                               DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1WHDP         5A  B 11 29
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1STYP         1A  B 12 29
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28N90                               DSPATR(HI)
     A N28N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F4=Prompt F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
