     A*%%TS  SD  20021217  114914  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W Roosevelt Rd., #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PO180FM   Lumper service - Change screen
     A*  16 December, 2002
     A*  Hemant Kapadia
     A*
416 AA*    12/16/02  HNK  4.16
     A*      Created
     A*
     A*  Revisions
     A*
417 AA*    04/16/03  HNK  4.17
     A*      Fix - Add error indicators.
     A*
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO180.01'
     A                                  1 33'Lumper Service'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'PO Number:'
     A            $PPO           9A  O  5 13
     A                                  6  2'   Vendor:'
     A            W1VEN     R        O  6 13REFFLD(PHVEN *LIBL/POHDR)
     A                                      EDTCDE(3)
     A                                  6 25'-'
     A            W1VNAM    R        O  6 27REFFLD(PHVNAM *LIBL/POHDR)
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Lumper service . . . . .'
     A            W1LMPR         1A  B 10 29
417 AA N21N90                               DSPATR(HI)
417 AA  21                                  DSPATR(RI)
417 AA N21N90                               DSPATR(UL)
417 AA  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 35'(Y,N)'
     A                                 11  4'Palletized . . . . . . .'
     A            W1PALT         1A  B 11 29
417 AA N22N90                               DSPATR(HI)
417 AA  22                                  DSPATR(RI)
417 AA N22N90                               DSPATR(UL)
417 AA  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 35'(Y,N)'
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
