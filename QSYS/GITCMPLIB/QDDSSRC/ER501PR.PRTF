     A*%%***********************************************************************
     A*
     A*   ER501PR  Print all transactions by employee
     A*   25 June, 2000
     A*   Hemant Kapadia
     A*
     A*   Revision
     A*   09/19/01  HNK  4.16
     A*   Add Direct and Indirect minutes
     A*   07/31/06  RMC  5.10
     A*   Add Start Time
     A*
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                   125
     A                                      DFT('ER501')
     A                                      SPACEA(001)
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            PTEMP#    R             +1REFFLD(PTEMP# ER501)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A  92                                75
     A                                      '(Continued)'
     A            TMERNG        22A  O    52
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                   109'Dirct'
     A                                   116'Indir'
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('Type  ')
     A                                      UNDERLINE
     A                                    18
     A                                      DFT('Start Dt')
     A                                      UNDERLINE
     A                                    28
     A                                      DFT('Item          ')
     A                                      UNDERLINE
     A                                    45
     A                                      DFT('Description             ')
     A                                      UNDERLINE
     A                                    72
     A                                      DFT('From Slot')
     A                                      UNDERLINE
     A                                    86
     A                                      DFT('To Slot')
     A                                      UNDERLINE
     A                                   100
     A                                      DFT('Quantity')
     A                                      UNDERLINE
     A                                   109
     A                                      DFT('Mins ')
     A                                      UNDERLINE
     A                                   116
     A                                      DFT('Mins ')
     A                                      UNDERLINE
510 AA                                   123
510 AA                                      DFT('Start Time')
510 AA                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
     A                                      REFFLD(PTTRN# *LIBL/ER501)
     A            PTTASK    R        O    10
     A                                      REFFLD(PTTASK *LIBL/ER501)
     A            W1SDTE         6  0O    18EDTCDE(Y)
     A            LBITEM    R        O    28REFFLD(LBITEM *LIBL/ER501)
     A            W1DESC        25A  O    45
     A            LBDISP    R        O    72REFFLD(LBDISP *LIBL/ER501)
     A            LBDSP2    R        O    86REFFLD(LBDSP2 *LIBL/ER501)
     A            LBQPCK    R        O   100EDTCDE(3)
     A                                      REFFLD(LBQPCK *LIBL/ER501)
     A            PTDMIN    R        O   109EDTCDE(3)
     A                                      REFFLD(PTDMIN *LIBL/ER501)
     A            PTIMIN    R        O   116EDTCDE(3)
     A                                      REFFLD(PTIMIN *LIBL/ER501)
510 AA            PTSTIM         6S 0O   123
510 AA                                      EDTWRD('  :  :  ')
     A*
     A*         R TOTAL1
     A*                                     SPACEB(002)
     A*                                     SPACEA(001)
     A*                                    1
     A*                                     DFT('Total transactions:')
     A*           TRNCNT         5  0O    25EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('ER501: End of Report')
     A*
