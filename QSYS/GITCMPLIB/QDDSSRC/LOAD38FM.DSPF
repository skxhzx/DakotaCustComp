     A*%%TS  SD  20080424  171835  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD38FM  Loader/Cases Payroll info - Chg,View,Add.
     A*  March 31, 2008
     A*  Jose Duque
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    XX/XX/XX  CBI-JGD
     A*      TEXT. . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080424  171835  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  3'LOADR38.01'
     A                                      DSPATR(HI)
     A                                  1 22'Loader/Cases Payroll Adjustment (C-
     A                                      hange)'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A*
     A                                  8  4'Warehouse  . . . . . .'
     A                                      DSPATR(HI)
     A            W1WHSE         3Y 0B  8 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90N91                            DSPATR(HI)
     A N26N90N91                            DSPATR(UL)
     A                                      COLOR(WHT)
     A*----------------------------------------------------------------
     A                                 11  4'Emp#:'
     A                                      DSPATR(HI)
     A                                 11 18'Rte #'
     A                                      DSPATR(HI)
     A                                 11 32'RteId'
     A                                      DSPATR(HI)
     A                                 11 46'Pieces'
     A                                      DSPATR(HI)
     A*>                               11 46'StrtDte'
     A*                                     DSPATR(HI)
     A*                                11 65'Pieces'
     A*>                                    DSPATR(HI)
     A*----------------------------------------------------------------
     A            WEMP#          5Y 0B 11 10COLOR(PNK)
     A                                      EDTCDE(Z)
     A  90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90                               DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A            WRTE#          5Y 0B 11 24COLOR(PNK)
     A                                      EDTCDE(Z)
     A  90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A            WRTEID         5Y 0B 11 38COLOR(PNK)
     A                                      EDTCDE(Z)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A            WPCS           4Y 0B 11 53COLOR(PNK)
     A                                      EDTCDE(M)
     A  90                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N27N90                               DSPATR(HI)
     A N27N90                               DSPATR(UL)
     A*           WSTRDT         6Y 0O 11 54EDTWRD('  /  /  ')
     A* 90                                  DSPATR(PR)
     A* 24                                  DSPATR(RI)
     A* 04                                  DSPATR(PC)
     A*N24N90                               DSPATR(HI)
     A*N24N90                               DSPATR(UL)
     A*                                     COLOR(PNK)
     A*           WPCS           4Y 0B 11 72COLOR(PNK)
     A*                                     EDTCDE(M)
     A* 90                                  DSPATR(PR)
     A* 27                                  DSPATR(RI)
     A* 06                                  DSPATR(PC)
     A*N27N90                               DSPATR(HI)
     A*N27N90                               DSPATR(UL)
     A*---------------------------------
     A N25                             14  4'Updated by:'
     A                                      DSPATR(HI)
     A N25                             15  4'Employee # . . . . . .'
     A                                      DSPATR(HI)
     A N25        W1EMP#         5Y 0O 15 29EDTCDE(4)
     A                                      DSPATR(HI)
     A*
     A N25                             16  4'First Name . . . . . .'
     A                                      DSPATR(HI)
     A N25        W1FNM         15A  O 16 29DSPATR(HI)
     A N25                             17  4'Middle Initial . . . .'
     A                                      DSPATR(HI)
     A N25        W1INT          1A  O 17 29DSPATR(HI)
     A N25                             18  4'Last Name  . . . . . .'
     A                                      DSPATR(HI)
     A N25        W1LNM         20A  O 18 29DSPATR(HI)
     A N25                             19  4'Date . . . . . . . . .'
     A                                      DSPATR(HI)
     A N25        W1UPD          6Y 0O 19 29EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                 23  2'F4=Prompt F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2 32'Change Detail Entry'
     A                                      DSPATR(HI)
     A N25                             20  4'Time . . . . . . . . .'
     A                                      DSPATR(HI)
     A N25        W1TIME         6Y 0O 20 29DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
