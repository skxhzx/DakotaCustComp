     A*%%TS  SD  20091103  170441  MLBLACKNIK  REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   24 N. Washington Ave Suite 203                                 *
     A*   Batavia, IL 60510                                              *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  IV560FM      Export Pre-Post Inventory Quantity - Options
     A*  03 November 2009
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ALTHELP
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      HELP
     A          R SCREEN1
     A*%%TS  SD  20091103  170441  MLBLACKNIK  REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IV560.01'
     A                                  1 30'Export Pre-Post Quantity'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . . . . .-
     A                                       .'
     A            W1WHSE         3Y 0B  8 42EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department . . . . . . . . . . . .-
     A                                       .'
     A            W1WHDP         5A  B  9 42
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 57'Code, *ALL'
     A                                 10  4'Section  . . . . . . . . . . . . .-
     A                                       .'
     A            W1SECN         1A  B 10 42
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 57'P=PIR, F=Fixed, B=Both'
     A                                 13  4'Place on job queue ? . . . . . . .-
     A                                       .'
     A            W1JOBQ         1A  B 13 42
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 57'Y=Yes, N=No'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
