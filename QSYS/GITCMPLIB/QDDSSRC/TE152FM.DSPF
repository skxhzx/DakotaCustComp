500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TE152FM   RF - Task entry
     A*  15 February, 2000
     A*  Hemant Kapadia
     A*
     A*  ************************
     A*  NOTES:
     A*  Mirror of TE112
     A*    - Screen 3 not used for RF
     A*  ************************
     A*
415 AF*    12/06/00  HNK  4.15
     F*      - Position cursor to employee number.
     F*      - Dept and section entry field not highlighted.
     A*
500 AA*   06/07/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SCREEN1
     A*%%TS  SD  20000302  153839  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(07)
413X A*                                     CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TE152.01'
     A                                  1 31DATE
     A                                      EDTCDE(Y)
     A                                  1 15'Task Entry'
     A                                      DSPATR(HI)
     A                                  3  3'Warehouse . . .'
     A            W1WHSE         3Y 0B  3 19EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  4  3'Department  . .'
     A            W1WHDP         5A  B  4 19
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
415 MA  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
415 DA*N22N90                               DSPATR(HI)
     A                                  5  3'Section . . . .'
     A            W1STYP         1A  B  5 19
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
415 DA*N24N90                               DSPATR(HI)
     A                                  6  3'Employee #. . .'
     A            W1EMP#    R     Y00B  6 19REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(Z)
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
413X A*N21                                  DSPATR(CS)
415 MA                                      DSPATR(PC)
415 DA* 01                                  DSPATR(PC)
     A N21N90                               DSPATR(HI)
     A                                      CHECK(FE)
     A                                  7  2'F3=Exit'
     A          R SCREEN2
     A*%%TS  SD  20000215  105347  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
413X A*                                     CLRL(23)
413 DA*                                     CF06
     A                                      CF10
     A                                      CA12
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'TE152.02'
     A                                  1 31DATE
     A                                      EDTCDE(Y)
     A                                  1 16'Task Entry'
     A                                      DSPATR(HI)
     A                                  2  2'Employee:'
     A            USEMP#    R        O  2 12REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A                                  2 18'-'
     A            W2NAME        40A  O  2 20
     A                                  3  2'Current task:'
     A            W2CTSK         6A  O  3 16
     A            W2DESC        30A  O  3 23
     A                                  4  7'Started:'
     A            W2DATE         6Y 0O  4 16EDTWRD('  /  /  ')
     A            W2TIME         6Y 0O  4 26EDTWRD('  :  :  ')
     A                                  5  3'Task/Transaction . . . .'
     A            W2TASK         7A  B  5 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                  7  1'F10=End cur task F12=Cancl F20=Acp-
     A                                      t Warning'
     A                                      COLOR(BLU)
     A*
413 AA* Screen 3 not used for RF
     A*
     A          R SCREEN3
     A*%%TS  SD  19960117  143536  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF09
     A                                      CA12
     A  98                                  ALARM
     A                                  1  2'TE112.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Task Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9 18'Employee:'
     A            USEMP#    R        O  9 29REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A            W3NAME        40A  O  9 36
     A                                 10  2'Last manual task entered:'
     A            W3MTSK         6A  O 10 28
     A            W3DESC        30A  O 10 36
     A                                 11 19'Started:'
     A            W3SLDT         6Y 0O 11 28EDTWRD('  /  /  ')
     A            W3SLTM         6Y 0O 11 37EDTWRD('  :  :  ')
     A                                 12 21'Ended:'
     A            W3ELDT         6Y 0O 12 28EDTWRD('  /  /  ')
     A            W3ELTM         6Y 0O 12 37EDTWRD('  :  :  ')
     A                                 14  3'Task . . . . . . . . . .'
     A            W3TASK         7A  B 14 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 15  3'Start date . . . . . . .'
     A            W3SDTE         6Y 0B 15 29EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(CS)
     A  02                                  DSPATR(PC)
     A                                 15 45'MMDDYY'
     A                                 16  3'Start time . . . . . . .'
     A            W3SHRS         2Y 0B 16 29EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(CS)
     A  03                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 16 32':'
     A            W3SMIN         2Y 0B 16 34EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(CS)
     A  04                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3SAP          1   B 16 38
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(CS)
     A  05                                  DSPATR(PC)
     A                                 16 45'HH:MM A,P'
     A                                 18  3'End date . . . . . . . .'
     A            W3EDTE         6Y 0B 18 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(CS)
     A  06                                  DSPATR(PC)
     A                                 18 45'MMDDYY'
     A                                 19  3'End time . . . . . . . .'
     A                                 19 45'HH:MM A,P'
     A            W3EHRS         2Y 0B 19 29EDTCDE(4)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(CS)
     A  07                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 19 32':'
     A            W3EMIN         2Y 0B 19 34EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(CS)
     A  08                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3EAP          1   B 19 38
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(CS)
     A  09                                  DSPATR(PC)
     A                                 23  2'F9=Transaction maintenance  F12=Ca-
                                            ncel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  20000302  153839  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(07)
413X A*                                     CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TE152.01'
     A                                  1 31DATE
     A                                      EDTCDE(Y)
     A                                  1 15'Incorp. Tarea'
     A                                      DSPATR(HI)
     A                                  3  3'Almacen . . . .'
     A            W1WHSE         3Y 0B  3 19EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  4  3'Departamento . '
     A            W1WHDP         5A  B  4 19
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
415 MA  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
415 DA*N22N90                               DSPATR(HI)
     A                                  5  3'Seccion . . . .'
     A            W1STYP         1A  B  5 19
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
415 DA*N24N90                               DSPATR(HI)
     A                                  6  3'# de empleado .'
     A            W1EMP#    R     Y00B  6 19REFFLD(USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(Z)
     A                                      CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
413X A*N21                                  DSPATR(CS)
415 MA                                      DSPATR(PC)
415 DA* 01                                  DSPATR(PC)
     A N21N90                               DSPATR(HI)
     A                                      CHECK(FE)
     A                                  7  2'F3=Salir'
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20000215  105347  HEMANT      REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
413X A*                                     CLRL(23)
413 DA*                                     CF06
     A                                      CF10
     A                                      CA12
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'TE152.02'
     A                                  1 31DATE
     A                                      EDTCDE(Y)
     A                                  1 16'Incorp. Tarea'
     A                                      DSPATR(HI)
     A                                  2  2'Empleado:'
     A            USEMP#    R        O  2 12REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A                                  2 18'-'
     A            W2NAME        40A  O  2 20
     A                                  3  2'Tarea actual:'
     A            W2CTSK         6A  O  3 16
     A            W2DESC        30A  O  3 23
     A                                  4  7'Empezado:'
     A            W2DATE         6Y 0O  4 17EDTWRD('  /  /  ')
     A            W2TIME         6Y 0O  4 27EDTWRD('  :  :  ')
     A                                  5  3'Tarea/Transaccion . . . '
     A            W2TASK         7A  B  5 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                  7  1'F10=Terminar tarea F12=Cancelar -
     A                                      F20=Acpt aviso'
     A                                      COLOR(BLU)
     A*
413 AA* Screen 3 not used for RF
     A*
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A*%%TS  SD  19960117  143536  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF09
     A                                      CA12
     A  98                                  ALARM
     A                                  1  2'TE112.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 36'Incorp. Tarea'
     A                                      DSPATR(HI)
     A                                  7  2'Escriba cambios, oprima ENTER.'
     A                                      COLOR(BLU)
     A                                  9 18'Empleado:'
     A            USEMP#    R        O  9 29REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A            W3NAME        40A  O  9 36
     A                                 10  2'Ultima tarea manual incorp:'
     A            W3MTSK         6A  O 10 30
     A            W3DESC        30A  O 10 38
     A                                 11 19'Empezado:'
     A            W3SLDT         6Y 0O 11 29EDTWRD('  /  /  ')
     A            W3SLTM         6Y 0O 11 38EDTWRD('  :  :  ')
     A                                 12 21'Termino:'
     A            W3ELDT         6Y 0O 12 29EDTWRD('  /  /  ')
     A            W3ELTM         6Y 0O 12 38EDTWRD('  :  :  ')
     A                                 14  3'Tarea. . . . . . . . . .'
     A            W3TASK         7A  B 14 29CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(CS)
     A  01                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 15  3'Fecha empezada . . . . .'
     A            W3SDTE         6Y 0B 15 29EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(CS)
     A  02                                  DSPATR(PC)
     A                                 15 45'MMDDYY'
     A                                 16  3'Hora empezada. . . . . .'
     A            W3SHRS         2Y 0B 16 29EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(CS)
     A  03                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 16 32':'
     A            W3SMIN         2Y 0B 16 34EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(CS)
     A  04                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3SAP          1   B 16 38
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(CS)
     A  05                                  DSPATR(PC)
     A                                 16 45'HH:MM A,P'
     A                                 18  3'Fecha termindada . . . .'
     A            W3EDTE         6Y 0B 18 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(CS)
     A  06                                  DSPATR(PC)
     A                                 18 45'MMDDYY'
     A                                 19  3'Hora terminada . . . . .'
     A                                 19 45'HH:MM A,P'
     A            W3EHRS         2Y 0B 19 29EDTCDE(4)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(CS)
     A  07                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 19 32':'
     A            W3EMIN         2Y 0B 19 34EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(CS)
     A  08                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W3EAP          1   B 19 38
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(CS)
     A  09                                  DSPATR(PC)
     A                                 23  2'F9=Mantenamiento de transaccion F1-
     A                                      2=Cancelar'
     A                                      COLOR(BLU)
