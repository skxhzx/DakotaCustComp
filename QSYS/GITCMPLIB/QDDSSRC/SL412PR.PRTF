     A****************************************************************
     A*
     A* SL412PR   Pick slots only with overflow.
     A* 21 August, 2001
     A* Roxanne Harrison
     A*
     A* 08/21/01  RH   4.16
     A*  - Created
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    53
     A                                      DFT('Pick slots only with +
     A                                           overflow')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL412')
     A*
     A                                     3
     A                                      DFT('Department: ')
     A                                      SPACEB(002)
     A            WKWHDP         5   O    15
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A*           TYPE          11A  O    62
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    50
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    64
     A                                      DFT('Available')
     A                                      UNDERLINE
     A                                    76
     A                                      DFT('Stock ')
     A                                      UNDERLINE
     A                                    88
     A                                      DFT('Allocated')
     A                                      UNDERLINE
     A                                   100
     A                                      DFT('Pick ')
     A                                      UNDERLINE
     A                                   112
     A                                      DFT('Transfer')
     A                                      UNDERLINE
     A                                   124
     A                                      DFT('Receive ')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Section: ')
     A            WOSTYP         5A  O    10
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A  92        WKITEM    R        O     1REFFLD(WKITEM SL411A)
     A  92        ITDESC    R        O    17REFFLD(ITDESC PIRITEM)
     A            W1PICK         1A  O    48
     A            WKDISP    R        O    50REFFLD(WKDISP SL411A)
     A            W1AVL         10A  O    64
     A            W1STK         10A  O    76
     A            W1ALC         10A  O    88
     A            W1PCK         10A  O   100
     A            W1TFR         10A  O   112
     A            W1RCV         10A  O   124
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('SL412: End of Report ')
     A*
