     A*%%***********************************************************************
     A*
     A*   CW301APR  Catch Weight Variance Report
     A*   3 June 2010
     A*   Ginger Andrews
     A*
     A*%%***********************************************************************
     A*  Revisions:
     A*    06/03/10  GJA  5.30
     A*      - Created                                                 d
700 AA*    09/15/15  MLB  7.00
     A*      - Fix: Revised report to add printer ID, CW301A.
     A*
     A*%%***********************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    57
     A                                      DFT('Catch Weight Variance Check')
700 DA*                                  180
700 MA                                   120
     A                                      DFT('Page')
700 DA*           PAGE           4S 0O   184
700 MA            PAGE           4S 0O   125
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEB(001)
700 DA*                                  182
700 MA                                   127
     A                                      DFT('CW301A')
     A            WHSE          30A  O    56
     A                                      SPACEA(001)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     2
     A                                      DFT('Upload')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('Whse')
     A                                      UNDERLINE
     A                                    15
     A                                      DFT('Route')
     A                                      UNDERLINE
     A                                    21
     A                                      DFT('RouteID')
     A                                      UNDERLINE
     A                                    37
     A                                      DFT('Order')
     A                                      UNDERLINE
     A                                    45
     A                                      DFT('Item           ')
     A                                      UNDERLINE
     A                                    58
     A                                      DFT('LowCW')
     A                                      UNDERLINE
     A                                    67
     A                                      DFT('HighCW')
     A                                      UNDERLINE
     A                                    76
     A                                      DFT('CWeight')
     A                                      UNDERLINE
     A                                    86
     A                                      DFT('SWeight')
     A                                      UNDERLINE
     A                                    95
     A                                      DFT('Low%')
     A                                      UNDERLINE
     A                                   105
     A                                      DFT('High%')
     A                                      UNDERLINE
     A                                   117
     A                                      DFT('Tran#')
     A                                      UNDERLINE
     A                                   127
     A                                      DFT('Label#')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            W1DTE          6S 0O     1EDTCDE(Y)
     A            W1WHSE         3S 0O    10EDTCDE(Z)
     A            W1RTE          5A  O    15
     A            W1RTID         5A  O    21
     A            W1ORD          7S 0O    35EDTCDE(Z)
     A            W1ITEM        15A  O    45
     A            W1LOW          7S 2O    55EDTCDE(3)
     A            W1HIGH         7S 2O    65EDTCDE(3)
     A            W1CWGT         7S 2O    75EDTCDE(3)
     A            W1SWGT         7S 2O    85EDTCDE(3)
     A            W1RG1C         3S 1O    95EDTCDE(3)
     A            W1RG2C         3S 1O   105EDTCDE(3)
     A            W1TRAN         7S 0O   115EDTCDE(Z)
     A            W1LBL#         7S 0O   125EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('RN161A: End of Report')
     A*
