     A*%%TS  SD  20040520  065535  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTLICINFO  Test DRILICINFO interface.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    05/17/04  DAS  4.17
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040520  065535  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTLICINFO.1'
     A                                  1 28'Test DRILICINFO Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Adjustment type  . .'
     A            W1TYPE        10A  B  3 25
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 37'*ADD,'
     A                                  3 43'*CHANGE,'
     A                                  3 52'*DELETE,'
     A                                  3 61'*DELETEALL,'
     A                                  3 73'*GET'
     A                                  4  4'Dakota call  . . . .'
     A            W1DKTA         1A  B  4 25VALUES('Y' 'N')
     A                                  4 37'*VERIFYADD/CHG/DEL,'
     A                                  4 57'*VFYDELALL'
     A                                  7  4'Warehouse  . . . . .'
     A            W1WHSE         3Y 0B  7 25
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  8  4'Type . . . . . . . .'
     A            W1LTYP         1A  B  8 25
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  8 58'For negative value put'
     A                                  9  4'License  . . . . . .'
     A            W1LCNS        15A  B  9 25
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  9 58'put minus sign on right'
     A                                 11  1'Send'
     A                                      DSPATR(UL)
     A                                 11  6'Code    '
     A                                      DSPATR(UL)
     A                                 11 15'Seq# '
     A                                      DSPATR(UL)
     A                                 11 22'Type'
     A                                      DSPATR(UL)
     A                                 11 27'Character Value               '
     A                                      DSPATR(UL)
     A                                 11 58'Numeric Value    '
     A                                      DSPATR(UL)
     A*
     A            WSCODE1        1A  B 12  2
     A            W1CODE1        8A  B 12  6
     A  31                                  DSPATR(RI)
     A            W1CSEQ1        5Y 0B 12 15
     A  32                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP1        1A  B 12 23
     A  33                                  DSPATR(RI)
     A            W1VALC1       30A  B 12 27
     A  34                                  DSPATR(RI)
     A            W1VALN1       15Y 5B 12 58EDTCDE(M)
     A  35                                  DSPATR(RI)
     A*
     A            WSCODE2        1A  B 13  2
     A            W1CODE2        8A  B 13  6
     A  36                                  DSPATR(RI)
     A            W1CSEQ2        5Y 0B 13 15
     A  37                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP2        1A  B 13 23
     A  38                                  DSPATR(RI)
     A            W1VALC2       30A  B 13 27
     A  39                                  DSPATR(RI)
     A            W1VALN2       15Y 5B 13 58EDTCDE(M)
     A  40                                  DSPATR(RI)
     A*
     A            WSCODE3        1A  B 14  2
     A            W1CODE3        8A  B 14  6
     A  41                                  DSPATR(RI)
     A            W1CSEQ3        5Y 0B 14 15
     A  42                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP3        1A  B 14 23
     A  43                                  DSPATR(RI)
     A            W1VALC3       30A  B 14 27
     A  44                                  DSPATR(RI)
     A            W1VALN3       15Y 5B 14 58EDTCDE(M)
     A  45                                  DSPATR(RI)
     A*
     A            WSCODE4        1A  B 15  2
     A            W1CODE4        8A  B 15  6
     A  46                                  DSPATR(RI)
     A            W1CSEQ4        5Y 0B 15 15
     A  47                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP4        1A  B 15 23
     A  48                                  DSPATR(RI)
     A            W1VALC4       30A  B 15 27
     A  49                                  DSPATR(RI)
     A            W1VALN4       15Y 5B 15 58EDTCDE(M)
     A  50                                  DSPATR(RI)
     A*
     A            WSCODE5        1A  B 16  2
     A            W1CODE5        8A  B 16  6
     A  51                                  DSPATR(RI)
     A            W1CSEQ5        5Y 0B 16 15
     A  52                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP5        1A  B 16 23
     A  53                                  DSPATR(RI)
     A            W1VALC5       30A  B 16 27
     A  54                                  DSPATR(RI)
     A            W1VALN5       15Y 5B 16 58EDTCDE(M)
     A  55                                  DSPATR(RI)
     A*
     A            WSCODE6        1A  B 17  2
     A            W1CODE6        8A  B 17  6
     A  56                                  DSPATR(RI)
     A            W1CSEQ6        5Y 0B 17 15
     A  57                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP6        1A  B 17 23
     A  58                                  DSPATR(RI)
     A            W1VALC6       30A  B 17 27
     A  59                                  DSPATR(RI)
     A            W1VALN6       15Y 5B 17 58EDTCDE(M)
     A  60                                  DSPATR(RI)
     A*
     A            WSCODE7        1A  B 18  2
     A            W1CODE7        8A  B 18  6
     A  61                                  DSPATR(RI)
     A            W1CSEQ7        5Y 0B 18 15
     A  62                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1VTYP7        1A  B 18 23
     A  63                                  DSPATR(RI)
     A            W1VALC7       30A  B 18 27
     A  64                                  DSPATR(RI)
     A            W1VALN7       15Y 5B 18 58EDTCDE(M)
     A  65                                  DSPATR(RI)
     A                                 21  4'Return:'
     A            W1RTN         10A  O 21 12
     A                                 21 23'-'
     A            W1MSG         50A  O 21 25
     A                                 23  2'F3=Exit  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
