     A*%%TS  SD  20041017  072437  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTLICHIST   Test DRILICHIST interface.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    07/24/04  DAS  4.17
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20041017  072437  DAVE        REL-V5R2M0  5722-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF13
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTLICHIST.1'
     A                                  1 28'Test DRILICHIST Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Command:'
     A            W1SUBCMD      10A  B  3 16
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  4  2'Warehouse:'
     A            WSWHSE         1A  B  4 13
     A            W1WHSE         3Y 0B  4 16EDTCDE(4)
     A                                  4 26'Empl#:'
     A            W1EMP#         5Y 0B  4 34EDTCDE(Z)
     A                                  5  2'Lic Tran#:'
     A            WSLTRN         1A  B  5 13
     A            W1LTRN         7Y 0B  5 16EDTCDE(4)
     A                                  5 26'ACFL:'
     A            WSACFL         1A  B  5 32
     A            W1ACFL         2A  B  5 35
     A                                  6  2'Node:'
     A            WSNODE         1A  B  6 13
     A            W1NODE        50A  B  6 16
     A                                  7  2'Tran#:'
     A            WSTRN#         1A  B  7 13
     A            W1TRN#         7Y 0B  7 16EDTCDE(4)
     A                                  7 26'Label#:'
     A            WSLBL#         1A  B  7 34
     A            W1LBL#         7Y 0B  7 37EDTCDE(4)
     A                                  7 59'*OPEN'
     A                                  7 65'*CLOSE'
     A                                  8  2'Quantity:'
     A            WSLQY1         1A  B  8 13
     A            W1LQY1         7S 0B  8 16
     A            WSLQY2         1A  B  8 26
     A            W1LQY2         7S 0B  8 29
     A            WSLQY3         1A  B  8 39
     A            W1LQY3         7S 0B  8 42
     A                                  8 59'*ASK{IN,OUT}'
     A                                  9  2'Adj Qty:'
     A            WSAQY1         1A  B  9 13
     A            W1AQY1         7S 0B  9 16
     A            WSAQY2         1A  B  9 26
     A            W1AQY2         7S 0B  9 29
     A            WSAQY3         1A  B  9 39
     A            W1AQY3         7S 0B  9 42
     A                                  9 59'*ADDLIC'
     A                                 10 13'------- To -------'
     A                                 10 37'------ From ------'
     A                                 10 59'*CLS{PUT,RPL,TFR}{P,T}'
     A                                 11  2'License:'
     A            WSLCNS         1A  B 11 13
     A            W1LCNS        15A  B 11 16
     A            WSFLIC         1A  B 11 37
     A            W1FLIC        15A  B 11 40
     A                                 11 59'*GET{LIC#,LOT#,SLOT}'
     A                                 12  2'Code:'
     A            WSTCOD         1A  B 12 13
     A            W1TCOD         2A  B 12 16
     A            WSFCOD         1A  B 12 37
     A            W1FCOD         2A  B 12 40
     A                                 12 59'*MOVE{BACK,FWD}'
     A                                 13  2'Area:'
     A            WSTARA         1A  B 13 13
     A            W1TARA        15A  B 13 16CHECK(LC)
     A            WSFARA         1A  B 13 37
     A            W1FARA        15A  B 13 40CHECK(LC)
     A                                 13 59'*NEW{LIC#,LOT#}'
     A                                 14  2'Dept:'
     A            WSTWHD         1A  B 14 13
     A            W1TWHD         5A  B 14 16
     A            WSFWHD         1A  B 14 37
     A            W1FWHD         5A  B 14 40
     A                                 14 59'*PUSH'
     A                                 15  2'Aisle:'
     A            WSTASL         1A  B 15 13
     A            W1TASL         3A  B 15 16
     A            WSFASL         1A  B 15 37
     A            W1FASL         3A  B 15 40
     A                                 15 59'*PUSHPA{P,T}'
     A                                 16  2'Loc:'
     A            WSTLOC         1A  B 16 13
     A            W1TLOC         3Y 0B 16 16EDTCDE(4)
     A            WSFLOC         1A  B 16 37
     A            W1FLOC         3Y 0B 16 40EDTCDE(4)
     A                                 16 59'*PULL'
     A                                 17  2'Level:'
     A            WSTLVL         1A  B 17 13
     A            W1TLVL         2Y 0B 17 16EDTCDE(4)
     A            WSFLVL         1A  B 17 37
     A            W1FLVL         2Y 0B 17 40EDTCDE(4)
     A                                 17 59'*UNDOPUT'
     A                                 18  2'Hand:'
     A            WSTHND         1A  B 18 13
     A            W1THND         2A  B 18 16
     A            WSFHND         1A  B 18 37
     A            W1FHND         2A  B 18 40
     A                                 18 59'*UPDLOC{ ,0}'
     A                                 19  2'Slot Pos:'
     A            WSTSLP         1A  B 19 13
     A            W1TSLP         3Y 0B 19 16EDTCDE(4)
     A            WSFSLP         1A  B 19 37
     A            W1FSLP         3Y 0B 19 40EDTCDE(4)
     A                                 19 59'*UPDPUT'
     A                                 20  2'Item:'
     A            WSTITM         1A  B 20 13
     A            W1TITM        15A  B 20 16
     A            WSFITM         1A  B 20 37
     A            W1FITM        15A  B 20 40
     A                                 20 59'*UPD{RPL,TFR}{P,T}'
     A                                 22  2'Return:'
     A            W1RTN         10A  O 22 10
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
