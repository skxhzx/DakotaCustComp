500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  SL160FM      RF Verify zero slot
     A*  25 March, 1997
     A*  David Sommerville
     A*  Roxanne Harrison
     A*
     A********************************************************************
     A*
     A*
     A*  Program Modification History
     A*
412 AA*  08/31/99  HNK  4.12
     A*   - Item number and description added.
412A A*  09/07/99  RH   4.12
     A*      - Added "Virtual Slot" to screen 2 if SLRSRV = V
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCREEN2.
     A*      - Both of the above added for api program APICVTSLT
     A*
500 AA*   06/04/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL160.01'
     A                                  1 12'Zero Slot'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Department:'
     A            W1WHDP         5   B  4 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'     Aisle:'
     A            W1AISL         3   B  5 14
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  20000622  143904  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF10
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'SL160.02'
     A                                  1 12'Zero Slot'
     A            W2ITEM        15A  O  3  1
     A            W2DESC        30A  O  3 17
     A                                  4  1'Slot:'
     A            W2DISP        12A  O  4  7
     A                                  5  7'Virtual Slot'
     A  51                                  DSPATR(ND)
     A                                  6  1'Scan:'
     A            W2SCAN        12A  B  6  7CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F10=Excp F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SL160.01'
     A                                  1 12'Slot de 0'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Salir'
     A                                      COLOR(BLU)
     A                                  3  2'   Almacen:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'      Dept:'
     A            W1WHDP         5   B  4 14
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'   Pasillo:'
     A            W1AISL         3   B  5 14
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20000622  143904  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF10
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'SL160.02'
     A                                  1 12'Slot de 0'
     A            W2ITEM        15A  O  3  1
     A            W2DESC        30A  O  3 17
     A                                  4  1'Slot:'
     A            W2DISP        12A  O  4  7
     A                                  5  7'Slot Virtual'
     A  51                                  DSPATR(ND)
     A                                  6  1'Escn:'
     A            W2SCAN        12A  B  6  7CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F10=Excp F12=Cancelar'
     A                                      COLOR(BLU)
     A*
