     A*%%TS  SD  20140409  170709  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG130FM    Staging Import View Purchase Order Header
     A*  Rod Roller
     A*
     A*  Revisions
     A*
700aAA*    08/12/16  RTR  7.00a
     A*      - Added prompt for RP=Reset PO option.
     A*      - Changed to SG17501.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140409  132754  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            PIHPO     R        O  8  9REFFLD(PIHREC/PIHPO *LIBL/IMPSPOH)
     A            PIHTYP    R        O  8 19REFFLD(PIHREC/PIHTYP *LIBL/IMPSPOH)
     A            PIHVEN    R        O  8 21REFFLD(PIHREC/PIHVEN *LIBL/IMPSPOH)
     A                                      EDTCDE(Z)
     A            S1NAME        30A  O  8 33
     A            PIHDTE    R        O  8 64REFFLD(PIHREC/PIHDTE *LIBL/IMPSPOH)
     A                                      EDTCDE(Y)
     A            PIHTIM    R        O  8 75REFFLD(PIHREC/PIHTIM *LIBL/IMPSPOH)
     A                                      EDTWRD('0  :  ')
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140409  152857  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A*                                     OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Purchase Order Header Info'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'   PO#   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19'T'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 21'  Vendor#  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 33'         Vendor Name          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 64'Deliv Date/Time '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20140409  170709  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG130.02'
     A                                  1 25'View Purchase Order Header Info'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Full Header Information'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            PIHWHS    R        O  5 18REFFLD(PIHREC/PIHWHS *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            PIHBATID  R        O  5 54REFFLD(PIHREC/PIHBATID *LIBL/IMPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  6  2'PO #  . . . . .'
     A            PIHPO     R        O  6 18REFFLD(PIHREC/PIHPO *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            PIHADDTS  R        O  6 54REFFLD(PIHREC/PIHADDTS *LIBL/IMPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  7  2'Update Type . .'
     A            PIHTYP    R        O  7 18REFFLD(PIHREC/PIHTYP *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            PIHSTSTS  R        O  7 54REFFLD(PIHREC/PIHSTSTS *LIBL/IMPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  9  2'Status  . . . .'
     A            PIHSTATUS R        O  9 18REFFLD(PIHREC/PIHSTATUS *LIBL/IMPSP-
     A                                      OH)
     A                                      DSPATR(HI)
     A            PIHTEXT   R        O  9 24REFFLD(PIHREC/PIHTEXT *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 13  2'Vendor  . . . .'
     A            PIHVEN    R        O 13 18REFFLD(PIHREC/PIHVEN *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            PIHVNA    R        O 13 30REFFLD(PIHREC/PIHVNA *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 14  2'Delivery D/T  .'
     A            PIHDTE    R        O 14 18REFFLD(PIHREC/PIHDTE *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            PIHTIM    R        O 14 27REFFLD(PIHREC/PIHTIM *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 16  2'Misc 1  . . . .'
     A            PIHMM1    R        O 16 18REFFLD(PIHREC/PIHMM1 *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 17  2'Misc 2  . . . .'
     A            PIHMM2    R        O 17 18REFFLD(PIHREC/PIHMM2 *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 18  2'Misc 3  . . . .'
     A            PIHMM3    R        O 18 18REFFLD(PIHREC/PIHMM3 *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 19  2'Misc 4  . . . .'
     A            PIHMM4    R        O 19 18REFFLD(PIHREC/PIHMM4 *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 20  2'Misc 5  . . . .'
     A            PIHMM5    R        O 20 18REFFLD(PIHREC/PIHMM5 *LIBL/IMPSPOH)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
700aAA          R WINDOW2
700aAA                                      WINDOW(7 27 6 24)
700aAA                                      CA08
700aAA                                  2  1'Are you sure you want to'
700aAA                                      DSPATR(HI)
700aAA  41N42                           3  5'reset this PO?'
700aAA                                      DSPATR(HI)
700aAA  42N41                           3  3'clear these errors?'
700aAA                                      DSPATR(HI)
700aAA  41N42                           5  2'F8=Reset'
700aAA                                      COLOR(BLU)
700aAA  42N41                           5  2'F8=Clear'
700aAA                                      COLOR(BLU)
700aAA                                  5 14'F12=Cancel'
700aAA                                      COLOR(BLU)
700aAA          R WINDOW3
700aAA                                      CA08
700aAA                                      WINDOW(7 25 6 28)
700aAA                                  2  2'You are not authorized to'
700aAA                                      DSPATR(HI)
700aAA                                      COLOR(RED)
700aAA                                  3  7'use this option.'
700aAA                                      DSPATR(HI)
700aAA                                      COLOR(RED)
700aAA                                  5  2'Press any key to continue.'
700aAA                                      COLOR(BLU)
