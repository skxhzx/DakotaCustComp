     A*%%TS  SD  20060919  115913  JEFF        REL-V5R3M0  5722-WDS
     *----------------------------------------------------------------
     *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     *   BFC Software, Inc.
     *   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     *   West Chicago, IL  60185
     *   (630) 562-0375
     *----------------------------------------------------------------
     A*
     A*  CW133FM   UCI Inventory Adjustments
     A*  20 September 2006
     A*  Randy Haselhorst
     A*
     A*  Revisions
     A*
510 AA*  09/20/06  RAH  5.10
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A N92                                  CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20060628  071125  JEFF        REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1SUCI        10A  O  9  2
     A            W1SWGT         5Y 2B  9 13
     A                                      EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            H1SWGT         5Y 2H
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20060707  113720  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A  98                                  ALARM
     A                                      CF20
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'CW133'
     A                                   + 1'UCI Inv Adj'
     A                                      DSPATR(HI)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A            $PITEM        15A  O  2  6
     A                                  4  2'Qty:'
     A            WOPQTY         5Y 2O  4  7EDTCDE(3)
     A                                  5  2'Type changes...'
     A                                      COLOR(BLU)
     A                                  6  1'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 12'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            W1UCI         10A  B  7  1CHECK(ER)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A N17
     AO 16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  91        W1WGT          5Y 2B  7 12EDTCDE(3)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(14)
     A                                  1  2'F5=Refrsh F12=Cancl'
     A                                      COLOR(BLU)
     A                                  2  2'F20=Override'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
