     A*%%TS  SD  20011026  164854  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO110CFM   PO Super P/O Maintenance (one whse) - display
     A*  26 October 2001
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
416 AA*    10/26/01  MLB  4.16
     A*      - Created.
     A*
520 AA*    08/20/08  MLB  5.20
     A*      - Fix: Changed field name referenced for po number to PUPO
     A*        instead of PHPO.
     A*
650 AA*    08/05/14  MLB  6.50
     A*      - Fix: Changed screen formats:NEWSPO,CSPSPO to be full screen
     A*        instead of window because KEEP, ASSUME keywords were
     A*        causing screen overlays making the screens unreadable
     A*        when displayed using Webfacing.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Coastal Sunbelt Produce
     A*
CSP AA*    12/16/10  MLB  CSP  P#01028
     A*      - Added 1 line to NEWSPO format to allow entry of Load
     A*        Number when new Super P/O is created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CA12
     A                                      CF04
650 DA*         R DUMMY
650 DA*                                     KEEP
650 DA*                                     ASSUME
650 DA*                                     OVERLAY
650 DA*                                 1  2' '
     A*
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PO110C.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 36'Super P/O'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            FLD002         1A  O  1  2
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*                                 2 47'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 71EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      DSPATR(ND PR)
     A                                  3  2'Super P/O:'
     A            W1SPO#         9   O  3 13
     A                                  3 47'Position to PO  . . . .'
     A            PTINP2         9A  B  3 71DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
520 DA*           PHPO      R        O 11  7REFFLD(PHREC/PHPO *LIBL/POHDR)
520 MA            PUPO      R        O 11  7REFFLD(PUREC/PUPO *LIBL/POSPO)
     A            PHVEN     R        O 11 18REFFLD(PHREC/PHVEN *LIBL/POHDR)
     A                                      EDTCDE(Z)
     A            W1VNAM        20A  O 11 30
     A            W1STAT        16A  O 11 65
     A            W1DATE         6A  O 11 51
     A                                 11 58'/'
     A            W1TIME         4A  O 11 60
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10  7'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Status          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Vendor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Name                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51' Date  / Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Remove.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R NEWSPO
650 DA*                                     WINDOW(*DFT 5 60)
650 DA*                                     WDWBORDER((*DSPATR RI) (*CHAR '    -
650 DA*                                         '))
     A                                  1 20'Create New Super P/O'
     A                                      DSPATR(HI)
650 MA                                  3  2'Super P/O Nbr :'
650 MA            W1SPO          9A  O  3 18COLOR(WHT)
650 MA                                  4  2'Super P/O Name:'
650 MA            W1NAME        40A  B  4 18COLOR(WHT)
     A                                      CHECK(LC)
650 MA                                 22  2'ENTER=Continue  F12=Cancel'
     A                                      COLOR(BLU)
CSP AA          R CSPSPO
650 DA*                                     WINDOW(*DFT 6 60)
650 DA*                                     WDWBORDER((*DSPATR RI) (*CHAR '    -
650 DA*                                         '))
CSP AA                                  1 20'Create New Super P/O'
CSP AA                                      DSPATR(HI)
650 MA                                  3  2'Super P/O Nbr :'
650 MA            W1SPO          9A  O  3 18COLOR(WHT)
650 MA                                  4  2'Super P/O Name:'
650 MA            W1NAME        40A  B  4 18COLOR(WHT)
CSP AA                                      CHECK(LC)
650 MA                                  5  2'Load Number   :'
650 MA            W1LODN         6A  B  5 18
CSP AA                                      COLOR(WHT)
650 MA                                 22  2'ENTER=Continue  F12=Cancel'
CSP AA                                      COLOR(BLU)
