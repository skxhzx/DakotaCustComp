     A*%%TS  SD  20070917  121258  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD26FM  Truck Load Module - Route Comment History - Options
     A*  September 4, 2007
     A*  Jose Duque
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*----------------------------------------------------------------
     A*         Select options
     A*----------------------------------------------------------------
     A          R SCREEN1
     A*%%TS  SD  20070917  121258  JOSED       REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A*
     A                                  1  2'LOADR26.01'
     A*
     A                                  1 22'Route Comment History - View/Print'
     A                                      DSPATR(HI)
     A*
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A*
     A                                  7  4'Warehouse  . . . . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 40EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                  9  4'Route #  . . . . . . . . . . . . .'
     A            W1RTE#         5A  B  9 40
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 49'Code, *All'
     A*
     A                                 10  4'From date  (MMDDYY). . . . . . . .'
     A            W1FRDT         6Y 0B 10 40EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 11  4'To date    (MMDDYY). . . . . . . .'
     A            W1TODT         6Y 0B 11 40EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 12  4'Comment types. . . . . . . . . . .'
     A            W1TYPE         1A  B 12 40
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 49'1 = Transportation'
     A                                 13  4'Blank = All'
     A                                 13 49'2 = Trailer condition'
     A                                 14 49'3 = Loader        '
     A                                 15 49'4 = Loading delays          '
     A                                 16 49'5 = Other'
     A*
     A                                 17  4'View or print  . . . . . . . . . .'
     A            W1VP           1A  B 17 40
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 51'(V=View, P=Print)'
     A*
     A            W1SORT         1   B 18 40
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 51'1 = Route, 2 = Written by'
     A*
     A                                 19  4'Place on job queue ? . . . . . . .'
     A            W1JOBQ         1A  B 19 40
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 19 51'(Y=Yes, N=No)'
     A                                 18  4'Order report . . . . . . . . . . .'
     A*
     A                                 20  4'Printer device . . . . . . . . . .'
     A            W1DEV         10A  B 20 40
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 21  4'Form type  . . . . . . . . . . . .'
     A            W1FORM        10A  B 21 40
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A*         Select comment type
     A*----------------------------------------------------------------
     A          R WINDW1
     A*%%TS  SD  20070122  085751  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(1 15 21 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  2  2'Loadr26'
     A*
     A                                  2 16'Comment Types Entry'
     A                                      DSPATR(HI)
     A*
     A                                  9  2'Select type:'
     A*
     A                                  9 16'1=Select'
     A*
     A                                 11  2'Transportation . . . '
     A            W1TRNP         2A  B 11 24
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A*
     A                                 12  2'Trailer Condition  .'
     A            W1TRLR         2A  B 12 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A*
     A                                 13  2'Loader . . . . . . .'
     A            W1LODR         2A  B 13 24
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A*
     A                                 14  2'Loading Delays . . .'
     A            W1DLAY         2A  B 14 24
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A*
     A                                 15  2'Other  . . . . . . .'
     A            W1OTHR         2A  B 15 24
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A*
     A                                 20  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A                                 20 12'F12=Cancel'
     A                                      COLOR(BLU)
