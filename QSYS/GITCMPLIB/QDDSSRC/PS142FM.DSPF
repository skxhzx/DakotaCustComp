     A*%%TS  SD  19991201  173838  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS142FM   Truck pallet detail  - Move  screen
     A*  24 June 1997
     A*  Roxanne Harrison
     A*
     A*  Revisions
404 AA*    08/19/97  RH   4.04
     A*      - F20 accept warning.
     A*
413aAA*    12/01/99  MLB  4.13a
     A*      - Added WOROW, WOCOL to SCREEN1 panel.
     A*      - Revised screen placement for the following fields:
     A*        $PWHSE, WHDESC and '-' on SCREEN1, SCREEN2.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'PS142.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Pallet Detail'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
413aMA            $PWHSE         3Y 0O  4 19EDTCDE(3)
413aMA                                  4 23'-'
413aMA            WHDESC        30A  O  4 25
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  7 19EDTCDE(4)
     A                                  7 23'Row:'
     A            WOROW          2  0O  7 28
     A                                  7 32'Column:'
     A            WOCOL          2  0O  7 40
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 11  3'Stop  . . . . . .'
     A            W1STOP         3Y 0O 11 21EDTCDE(4)
     A                                 12  3'Bulk Seq #  . . .'
     A            W1BSEQ         1Y 0O 12 21EDTCDE(4)
     A                                 13  3'Total pieces  . .'
     A            W1PCS          5Y 0O 13 21EDTCDE(4)
     A                                 14  3'Total cube  . . .'
     A            W1CUBE         7Y 3O 14 21EDTCDE(4)
     A                                 15  3'Total weight  . .'
     A            W1WGHT         9Y 2O 15 21EDTCDE(4)
     A                                 17  3'Move to Row . . .'
     A            W1ROW          2Y 0B 17 21EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 28'Column  . . .'
     A            W1COL          1Y 0B 17 42EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel  F20=Accept-
     A                                       warning'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PS142.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Pallet Detail'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
413aMA            $PWHSE         3Y 0O  4 19EDTCDE(3)
413aMA                                  4 23'-'
413aMA            WHDESC        30A  O  4 25
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  7 19EDTCDE(4)
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
