     A*%%TS  SD  20070808  125244  ROCKY       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RN620FM   RF CUSTOMER RETURNS with UCI - Change screen
     A*  08 August 2007
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
5.10 A*  08/08/07   RH  5.10
     A*    - Created for RF 16 line screen.
     A*  Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070808  114348  ROCKY       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.01'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, Enter '
     A                                      COLOR(BLU)
     A                                  5  2'Whse'
     A            W1WHSE         3Y 0B  5  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  2'Del#'
     A*           W1DLVY         7Y 0B  6  7
     A            W1DLVY         9A  B  6  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*                                     EDTCDE(4)
     A                                  7  2'Inv#'
     A            W1INVC        12A  B  7  7
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  2'Whole order'
     A            W1RORD         1A  B  9 14
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04N32                               DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  9 16'(Y/N)'
     A                                 15  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 15 22' '
     A                                 15 24' '
     A          R SCREEN2
     A*%%TS  SD  20070808  114348  ROCKY       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                  1  2'RN620.02'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A                                  4  2'Item/UCI'
     A            W2ITMU        10A  B  4 12
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'UPC'
     A            W2UPC         50A  B  5  6CHECK(ER)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  2'Label #:'
     A            W2LBL#         7Y 0B  6 11CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02
     AO 42                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  7  2'Qty'
     A            W2QT01         5Y 0B  7  6EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*                                 7 12'UOM'
     A*           W2UM           2A  B  7 16
     A*                                 7 19'EA/CS'
     A            W2UM           2A  B  7 12
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 15'EA/CS'
     A                                  8  2'Stock code'
     A            W2DCDE         1A  B  8 13
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 15'(1/2/3)'
     A                                  9  2'(1:Stk 2:Damage Stk)'
     A                                 10  2'(3:Non-Stk Carrer)'
     A                                 11  2'Reason code:'
     A            W2CODE         2A  B 11 15
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F4=Prompt  F9=Done '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Pop-up Window to enter Expiration or manufacturer date.
     A*
     A          R WINDOW1
     A                                      WINDOW(9 20 10 30 *MSGLIN)
     A                                      WDWTITLE((*TEXT &WDWTITLE) -
     A                                      *CENTER)
     A                                      WDWTITLE((*TEXT 'F5=Refresh  F12=Ca-
     A                                      ncel') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WDWTITLE      28A  P
     A*
     A*
     A          R WDWFMT1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A            WDWLABEL       8   O  2  2
     A            WWIDTE         6Y 0B  2 11EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A*                                 3  2'Inventory team:'
     A*           WWTEAM        10A  B  3 18
     A* 24                                  DSPATR(RI)
     A*N24                                  DSPATR(UL)
     A* 04                                  DSPATR(PC)
     A*
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.03'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Reason code:'
     A            W3CODE         2A  B  5 15
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel  '
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.04'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Mispicked '
     A                                  6  2'Item:'
     A            W4CITM        15A  B  6  8
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit  F4=Prompt  F1-
     A                                      2=Cancel  '
     A                                      COLOR(BLU)
     A          R SCREEN5
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.05'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'License:'
     A            W5LCNS        15A  B  6  2
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*******************************************
     A          R SCRN1SP
     A*%%TS  SD  20070808  114348  ROCKY       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.01'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, Enter '
     A                                      COLOR(BLU)
     A                                  5  2'Alma'
     A            W1WHSE         3Y 0B  5  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  2'Del#'
     A*           W1DLVY         7Y 0B  6  7
     A            W1DLVY         9A  B  6  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*                                     EDTCDE(4)
     A                                  7  2'Inv#'
     A            W1INVC        12A  B  7  7
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  2'Whole order'
     A            W1RORD         1A  B  9 14
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04N32                               DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  9 16'(Y/N)'
     A                                 15  2'F3=Salir F12=Salir'
     A                                      COLOR(BLU)
     A                                 15 22' '
     A                                 15 24' '
     A          R SCRN2SP
     A*%%TS  SD  20070808  114348  ROCKY       REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                  1  2'RN620.02'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A                                  4  2'Prdc/UCI'
     A            W2ITMU        10A  B  4 12
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03
     AO 32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'UPC'
     A            W2UPC         50A  B  5  6CHECK(ER)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  2'Label #:'
     A            W2LBL#         7Y 0B  6 11CHECK(ER)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02
     AO 42                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  7  2'Ctd'
     A            W2QT01         5Y 0B  7  6EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A*                                 7 12'UOM'
     A*           W2UM           2A  B  7 16
     A*                                 7 19'EA/CS'
     A            W2UM           2A  B  7 12
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 15'EA/CS'
     A                                  8  2'Stock code'
     A            W2DCDE         1A  B  8 13
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 15'(1/2/3)'
     A                                  9  2'(1:Stk 2:Damage Stk)'
     A                                 10  2'(3:Non-Stk Carrer)'
     A                                 11  2'Reason code:'
     A            W2CODE         2A  B 11 15
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F4=Prompt  F9=Done '
     A                                      COLOR(BLU)
     A          R SCRN3SP
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.03'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Reason code:'
     A            W3CODE         2A  B  5 15
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Salir F4=Prompt  F5=Refresh  F1-
     A                                      2=Salir  '
     A                                      COLOR(BLU)
     A          R SCRN4SP
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.04'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Mispicked '
     A                                  6  2'Prdc:'
     A            W4CITM        15A  B  6  8
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit  F4=Prompt  F1-
     A                                      2=Cancel  '
     A                                      COLOR(BLU)
     A          R SCRN5SP
     A*%%TS  SD  19960524  091037  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(15)
     A                                      CF04
416gDA*                                     CF17
     A  98                                  ALARM
     A*           W1SCRN         8   O  1  2
     A*           WOHEAD        60   O  1 11DSPATR(HI)
     A                                  1  2'RN620.05'
     A                                  1 12'Returns-UCI'
     A                                      DSPATR(HI)
     A***                               1 72DATE
     A***                                   EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'License:'
     A            W5LCNS        15A  B  6  2
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A*
     A*    Window Error Message Subfile Record.
     A*
     A          R MSGREC1                   SFL
     A                                      SFLMSGRCD(05)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Window Error Message Subfile Control Record.
     A*
     A          R MSGCTL1                   SFLCTL(MSGREC1)
     A                                      WINDOW(WINDOW1)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(05)
     A                                      CLRL(1)
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
