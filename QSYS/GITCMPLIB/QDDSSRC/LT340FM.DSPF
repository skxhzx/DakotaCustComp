     A*%%TS  SD  20111031  174626  BFC         REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT340FM    Order License Listing - View
     A*  Roxanne Harrison
     A*
     A*  Revisions
640 AA*    06/16/11  RH   6.40
     A*      - Created using LT330PR
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA09
     A                                      CA12
     A                                      CF23
     A          R TITLE
     A*%%TS  SD  20110801  150757  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            SCRNAME        8A  O  1  2
     A                                  1 30'Order License Listing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20110803  151741  JEFF        REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20111031  174217  BFC         REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  O  9  2
     A            W1ITEM        15A  O  9  5
     A            W1RQTY         5Y 0O  9 21EDTCDE(Z)
     A            W1LOC         12A  O  9 27
     A            W1LCNS        15A  O  9 40
     A            W1PO#          9A  O  9 56
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20111031  174626  BFC         REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  7  3
     A                                  8  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  5'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 21'Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 27'Location    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 40'License        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 56'P/O Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20111031  174511  BFC         REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            W2ITEM        15A  O  9  2
     A            W2QTY          5Y 0O  9 18EDTCDE(Z)
     A            W2LCNS        15A  O  9 24
     A            W2OLCNS       15A  O  9 40
     A            W2VNAM        24A  O  9 56
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20111031  174511  BFC         REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Qty  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 24'License #      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 40'Original Lic   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 56'Vendor Name             '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
414 DA* 85                                  SFLNXTCHG
     A          R VW3SFL                    SFL
     A*%%TS  SD  20111010  114354  BFC         REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            W3ACTIVE       3A  O  9  7
     A            W3LCNS        12A  O  9 12
     A            W3ADDUSER     10A  O  9 26
     A            W3ADDTS       26A  O  9 38
     A            W3CODE         8A  O  9 66
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20111010  114354  BFC         REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  7 66'Info'
     A                                      DSPATR(HI)
     A                                  8  7'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 12'License #   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 26'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 38'Added                     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 66'Code    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A          R VW4SFL                    SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W4ACTION  R        O  8  5REFFLD(LLREC/LLACTION *LIBL/LICLOG)
     A            W4LCNS    R        O  8  9REFFLD(LLREC/LLLCNS *LIBL/LICLOG)
     A            W4ITEM    R        O  8 25REFFLD(LLREC/LLITEM *LIBL/LICLOG)
     A            W4PRTY    R        O  8 43REFFLD(LLREC/LLPRTY *LIBL/LICLOG)
     A            W4EXPD         6Y 0O  8 46EDTWRD('  /  /  ')
     A            W4ENTD         6Y 0O  8 55EDTWRD('  /  /  ')
     A          R VW4SFC                    SFLCTL(VW4SFL)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View License History'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Act'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'Prty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'Expires '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'Entered '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
416 MA                                      s for 4=Unallocate.'
416 DA**                                    s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Pick & Print.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
