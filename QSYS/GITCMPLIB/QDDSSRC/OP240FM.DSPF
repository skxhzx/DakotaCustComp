     A*%%TS  SD  20010727  112611  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP240FM   RF Putaway options
     A*  06 September 1997
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
415 AA*    07/27/01  DAS  4.15
     A*      - Created.
     A*
415aAA*    08/02/01  DAS  4.15a
     A*      - Added option for verifying pallet qty.
     A*
417 AA*    06/26/03  MLB  4.17
     A*      - Enh: Add 1 new field to support option to allow scanning
     A*        of multiple license/labels during putaway.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010727  112611  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP240.01'
     A                                  1 32'RF Putaway Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Verify scanned slot for manual put-
     A                                      away . . .'
     A            W1VRFY         1A  B  7 49
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 66'Y=Yes, N=No'
415aAA                                  9  4'Verify pallet quantity . . . . . .-
415aAA                                       . . . . .'
415aAA            W1PQTY         1A  B  9 49
415aAA  22                                  DSPATR(RI)
415aAA N22N90                               DSPATR(UL)
415aAA  02                                  DSPATR(PC)
415aAA  90                                  DSPATR(PR)
415aAA                                  9 66'Y=Yes, N=No'
417 AA                                 11  4'Allow multiple lic/label scan in -
417 AA                                      putaway . .'
417 MA            W1MSCN         1A  B 11 49
417 AA N23                                  DSPATR(HI)
417 AA  23                                  DSPATR(RI)
417 AA N23N90                               DSPATR(UL)
417 AA  03                                  DSPATR(PC)
417 AA  90                                  DSPATR(PR)
417 AA                                 11 66'Y=Yes, N=No'
417 AA*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
