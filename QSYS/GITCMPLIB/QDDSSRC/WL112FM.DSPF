     A*%%TS  SD  20001004  110755  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  WL112FM   Warehouse rack layout maint. (one whse) - Display
     A*  21 July 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
414 AA*    10/04/00  DAS  4.14
     A*      - Added field W1SINC to SCREEN2 and HEAD1.
     A*      - Changed length of W1RCKS on SCREEN2 from 2.0 to 3.0.
     A*      - Changed length of W1SEQ# on DETAIL1 from 2.0 to 3.0.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940811  102049  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'WL112.01'
     A                                  1 24'Warehouse Aisle Layout Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  B  8 35
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Aisle . . . . . . . . . . . .'
     A            W1AISL         3A  B  9 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Section . . . . . . . . . . .'
     A            W1STYP         1A  B 10 35
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 61'F=Fixed   P=PIR'
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20001004  110755  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'WL112.02'
     A                                  1 24'Warehouse Aisle Layout Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 30'PIR Aisle Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  O  8 35
     A                                  9  4'Aisle . . . . . . . . . . . .'
     A            W1AISL         3A  O  9 35
     A                                 10  4'Section . . . . . . . . . . .'
     A            W1STYP         1A  O 10 35
     A                                 12  4'Starting slot number  . . . .'
     A            W1SNUM         3Y 0B 12 35EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N34N90                               DSPATR(HI)
414 AA                                 12 40'Side increment:'
414 AA            W1SINC         2Y 0B 12 56EDTCDE(4)
414 AA  31                                  DSPATR(RI)
414 AA N31N90                               DSPATR(UL)
414 AA  11                                  DSPATR(PC)
414 AA  90                                  DSPATR(PR)
414 AA N31N90                               DSPATR(HI)
     A                                 13  4'Current maximum level . . . .'
     A            W1CLVL         2Y 0B 13 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Future maximum level  . . . .'
     A            W1FLVL         2Y 0B 14 35EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Starting level  . . . . . . .'
     A            W1SLVL         1Y 0B 15 35EDTCDE(3)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A                                 16  4'Rack definition . . . . . . .'
     A            W1RACK         5A  B 16 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  4'Racks on one side . . . . . .'
414 DA**          W1RCKS         2Y 0B 17 35
414 MA            W1RCKS         3Y 0B 17 35
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 18  4'Build code  . . . . . . . . .'
     A            W1BLD          2A  B 18 35
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  19940722  162541  DAVE        REL-V2R1M1  5738-PW1
     A  85                                  SFLNXTCHG
414 DA**          W1SEQ          2Y 0O 16  8EDTCDE(3)
414 MA            W1SEQ          3Y 0O 16  8EDTCDE(3)
     A            W1LEFT         5A  B 16 18
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1RGHT         5A  B 16 30
     A  40                                  DSPATR(RI)
     A N40N90                               DSPATR(UL)
     A  20                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20001004  105943  DAVE        REL-V4R4M0  5769-PW1
     A  *DS3                                SFLLIN(0010)
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'WL112.03'
     A                                  1 24'Warehouse Aisle Layout Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Fixed Aisle Detail Entry'
     A                                      DSPATR(HI)
     A            $PWHSE         3Y 0O  3  2EDTCDE(4)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  O  7 35
     A                                  8  4'Aisle . . . . . . . . . . . .'
     A            W1AISL         3A  O  8 35
     A                                  9  4'Section . . . . . . . . . . .'
     A            W1STYP         1A  O  9 35
     A                                 10  4'Starting slot number  . . . .'
     A            W1SNUM         3Y 0B 10 35EDTCDE(4)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N34N90                               DSPATR(HI)
414 AA                                 10 40'Side increment:'
414 AA            W1SINC         2Y 0B 10 56
414 AA  31                                  DSPATR(RI)
414 AA N31N90                               DSPATR(UL)
414 AA  11                                  DSPATR(PC)
414 AA  90                                  DSPATR(PR)
414 AA N31N90                               DSPATR(HI)
414 AA                                      EDTCDE(4)
     A                                 11  4'Start from  . . . . . . . . .'
     A            W1STRT         1A  B 11 35
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N35N90                               DSPATR(HI)
     A                                 11 64'F=Front  B=Back'
     A                                 12  4'Build code  . . . . . . . . .'
     A            W1BLD          2A  B 12 35
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  5'Position'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 16'Left Def.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 28'Right Def.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 42'Position'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14 53'Left Def.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14 65'Right Def.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1ADDL         5A  B 15 18
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1ADDR         5A  B 15 30
     A  38                                  DSPATR(RI)
     A N38N90                               DSPATR(UL)
     A  18                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A*                                     OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*                                     OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
