     A*%%TS  SD  20070906  130634  LYNNM       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TM130FM   Truck Pallet detail maint. (one whse) - Display screen
     A*  28 May 1997
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
404 AA*    08/25/97  RH
     A*      - Remove NEW field from sc1.
     A*
413aAA*    11/24/99  MLB  4.13a
     A*      - Add F8=Chk stop splits, F9=Chk item splits.
     A*
416 AA*    12/17/02  DAS  4.16
     A*      - Changed 'Cmp' columns to 'Cmp/Zone' columns.
     A*      - Changed size of W1COM1/2 from 1-char to 8-char.
     A*
417 AA*    07/30/03  HNK  4.17
     A*      - Add Single pallet flag 'S' in the same position with Blk
     A*
500 AA*    05/02/06  JCJ  5.00
     A*      Enh: When in view mode display the word VIEW under the
     A*           date in reverse image.
     A*
510bAA*    04/03/07  RBD  5.10b
     A*      Enh: Display total weight for each column in column heading.
     A*
510cAA*    08/01/07  TJ   5.10c
     A*      Enh: Change W1BLK1/2 fields to be 2 characters instead of g.
     A*           1.
510dAA*    08/21/07  TJ   5.10d
     A*      Enh: Change W1BLK1/2 fields to be 3 characters instead of g.
     A*           2.
510eAA*    09/06/07  LMC  5.10e
     A*      Enh: Added F7 for route note maintenance
510fAA*    11/15/07  LMC  5.10f
     A*      Enh: Disabled F6 add function, it is not used and causes
     A*           PS130 to crash.
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19970617  150737  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'PS130.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
222 DA*                                 2 35'Pallet Detail'
222 MA                                  2 35'Truck Detail'
     A                                      DSPATR(HI)
500 AA            WMODE          6   O  2 73
500 AA  30                                  DSPATR(RI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20070906  130634  LYNNM       REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  1  2'Warehouse:'
     A            WOWHSE        35A  O  1 13
     A                                  1 52'Route:'
     A            WORTE          5A  O  1 59
     A                                  2 52'Position to Row . . . .'
     A            PTINP2         2Y 0B  2 77EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20021217  131030  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1ROW          2Y 0O 11  7EDTCDE(4)
     A            W1COM1         8A  O 11 11
     A            W1SEQ1         2Y 0O 11 21EDTCDE(4)
510cD***          W1COM2         8A  O 11 48
510cMA            W1COM2         8A  O 11 47
510cD***          W1SEQ2         2Y 0O 11 58EDTCDE(4)
510cMA            W1SEQ2         2Y 0O 11 57EDTCDE(4)
     A            W1CUB1         3Y 0O 11 24EDTCDE(3)
510cD***          W1BLK1         1A  O 11 35
510dD***          W1BLK1         2A  O 11 35
510dMA            W1BLK1         3A  O 11 34
510cD***          W1CUB2         3Y 0O 11 61EDTCDE(3)
510cMA            W1CUB2         3Y 0O 11 60EDTCDE(3)
510cD***          W1BLK2         1A  O 11 72
510dD***          W1BLK2         2A  O 11 71
510dMA            W1BLK2         3A  O 11 70
510cD***          W1ST11         3Y 0O 11 37EDTCDE(4)
510cMA            W1ST11         3Y 0O 11 38EDTCDE(4)
510cD***          W1ST21         3Y 0O 11 41EDTCDE(4)
510cMA            W1ST21         3Y 0O 11 42EDTCDE(4)
     A            W1ST12         3Y 0O 11 74EDTCDE(4)
     A            W1ST22         3Y 0O 11 78EDTCDE(4)
     A            W1PCS1         5Y 0O 11 28EDTCDE(3)
510cD***          W1PCS2         5Y 0O 11 65EDTCDE(3)
510cMA            W1PCS2         5Y 0O 11 64EDTCDE(3)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070906  130634  LYNNM       REL-V5R4M0  5722-WDS
510fDA*                                     CF06
     A                                      CF08
     A                                      CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
510eAA                                      CF07
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
510bDA*                                 8 11'----------- COLUMN 1 ------------'
     A                                  8 11'----------- COLUMN 1 -'
     A                                      DSPATR(HI)
     A            W1WGT1         7Y 2O  8 34EDTCDE(3)
     A                                  8 43'-'
     A                                      DSPATR(HI)
510bDA*                                 8 48'----------- COLUMN 2 ------------'
     A                                  8 48'----------- COLUMN 2 -'
     A                                      DSPATR(HI)
     A            W1WGT2         7Y 2O  8 71EDTCDE(3)
     A                                  8 80'-'
     A                                      DSPATR(HI)
     A                                  9 20'Unl'
     A                                      DSPATR(HI)
     A                                  9 57'Unl'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Row'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 11'Cmp/Zone'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 20'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29' Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Blk'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                               10 48'Cmp/Zone'
     A                                 10 47'Cmp/Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                               10 57'Seq'
     A                                 10 56'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                               10 61'Cube'
     A                                 10 60'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                               10 66' Pcs'
     A                                 10 65' Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                               10 71'Blk'
     A                                 10 70'Blk'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417 DA**                                9 34'Inc'
     A                                  9 34'*SP'
     A                                      DSPATR(HI)
417 DA**                                9 71'Inc'
510cDA**                                9 71'*SP'
     A                                  9 70'*SP'
     A                                      DSPATR(HI)
     A                                 10 38'Range '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 38'Stop'
     A                                      DSPATR(HI)
510cDA**                               10 75'Range '
     A                                 10 74'Range '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510cDA**                                9 75'Stop'
     A                                  9 74'Stop'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
404 DA*                                     SLNO(06)
404 MA                                      SLNO(05)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  20070906  130634  LYNNM       REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79A  O  1  2COLOR(BLU)
     A            CMDLN2        79A  O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
