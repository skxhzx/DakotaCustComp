     A*%%TS  SD  20021031  150425  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  WD112FM   Simple table maintenance - Change screen
     A*  20 February 1993
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*    07/06/94  DAS
     A*      - Added warehouse code and description.
     A*
415 AA*    03/13/01  DAS  4.15
     A*      - Added manual putaway option.
     A*
416 AA*    10/30/02  HNK  4.16
     A*      - Original Screen 1 disabled.
     A*      - DETAIL1 and HEAD1 records added.
     A*      - Added subfile for designation codes.
     A*      - Added CMDREC.
     A*
417 AA*    12/18/03  MMLB 4.17
     A*      - Fix: Corrected spelling of 1=Directed.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
416 DA*         R SCREEN1
416 DA*                                     OVERLAY
416 DA*                                     CLRL(23)
416 DA*                                     CF04
416 DA* 98                                  ALARM
416 DA*                                 1  2'WD112.01'
416 DA*                                 1 22'Warehouse Department Table Mainten-
416 DA*                                     ance'
416 DA*                                     DSPATR(HI)
416 DA*                                 1 72DATE
416 DA*                                     EDTCDE(Y)
416 DA*                                 2 35'Detail Entry'
416 DA*                                     DSPATR(HI)
416 DA*                                 4  2'Warehouse:'
416 DA*           $PWHSE         3Y 0O  4 13EDTCDE(3)
416 DA*                                 4 17'-'
416 DA*           WHDESC        30A  O  4 19
416 DA*                                 7  2'Type changes, press Enter.'
416 DA*                                     COLOR(BLU)
416 DA*                                10  4'Code . . . . . . . . .'
416 DA*           W1CODE         5A  B 10 28
416 DA* 21                                  DSPATR(RI)
416 DA* 01                                  DSPATR(PC)
416 DA* 90
416 DA* 91                                  DSPATR(PR)
416 DA*N21N90N91                            DSPATR(HI)
416 DA*N21N90N91                            DSPATR(UL)
416 DA*                                11  4'Description  . . . . .'
416 DA*           W1DESC        30A  B 11 28CHECK(LC)
416 DA* 22                                  DSPATR(RI)
416 DA*N22N90                               DSPATR(UL)
416 DA* 02                                  DSPATR(PC)
416 DA* 90                                  DSPATR(PR)
416 DA*                                13  4'Putaway method . . . .'
416 DA*           W1PMTH         1A  B 13 28
416 DA* 23                                  DSPATR(RI)
416 DA*N23N90                               DSPATR(UL)
416 DA* 03                                  DSPATR(PC)
416 DA* 90                                  DSPATR(PR)
416 DA*N23N90                               DSPATR(HI)
416 DA*                                13 60'1 = Directed'
416 DA*                                14 60'2 = Manual'
416 DA*                                23  2'F5=Refresh  F12=Cancel'
416 DA*                                     COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20021031  150425  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            W1DSCD         5A  B 16  9
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1SEQ#         3Y 0O 16  5EDTCDE(3)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20021031  150425  HEMANT      REL-V4R2M0  5769-PW1
     A  *DS3                                SFLLIN(0010)
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0024)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'WD112.01'
     A                                  1 29'Department Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Detail Entry'
     A                                      DSPATR(HI)
     A            $PWHSE         3Y 0O  5  4EDTCDE(4)
     A                                  5  8'-'
     A            WHDESC        30A  O  5 10
     A                                  7  4'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Code . . . . . . . . .'
     A            W1CODE         5A  B  9 34
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 10  4'Description . . . . . . . . .'
     A            W1DESC        30A  B 10 34CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Putaway method  . . . . . . .'
     A            W1PMTH         1A  B 11 34
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
417 MA                                 11 65'1=Directed '
     A                                 12 65'2=Manual'
     A                                 14  5'Designation'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 24'Designation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14 43'Designation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 14 62'Designation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1DESG         5A  B 15  9
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
