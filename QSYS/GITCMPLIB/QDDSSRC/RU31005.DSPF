     A*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RU310FM      RF - Runner Clear Flags
     A*  25 May 2004
     A*  Max Blacknik
     A*
     A********************************************************************
     A*  Revisions:
     A*
417 AA*  05/25/04  MLB  4.17
     A*    - Created.
500 AA*  02/27/06  JCJ  5.00
     A*    ENH: Add new quantity fields for Breakdown units
500aAA*  04/08/06  JCJ  5.00a
     A*    ENH: Added 4 screens Screen4,Screen64,Screen5,Screen65
     A*
510 AA*  07/01/06  MLB  5.10
     A*    Fix: Revised screen 3, screen 63 to convert qty found fields
     A*    to character from numeric. This was done to prevent users
     A*    from pressing Enter without entering zero or qty found for
     A*    shorted item.
510aAA*  08/22/06  MLB  5.10a
     A*    Enh: Changed titles on screen 4 and screen 5 to reflect
     A*    actual function of screen. Both 8 and 16 line screens.
510bAA*  05/24/07  DAS  5.10b
     A*    Enh: Added F10=Override Warning to screens 3 and 63.
520 AA*  01/22/09  RBD  5.20
     A*    Enh: Added label# to screen1 and screen61.  New *runner
     A*    option oprscn dictates whether tran# or label# will
     A*    be scanned.
     A*    Added F8=Load function to appear on screen1 and screen61
     A*    if new *runner option oprloa = Y.
     A*    Added entry field to screen2 and screen62 for scanning
     A*    pick slot if new *runner option oprvfs = Y.
     A*    Added screens screen6 and screen66 to ask for confirmation
     A*    of qty found if new *runner option oprvfq = Y.
     A*
530a A*  05/14/10  GJA  5.30a
     A*    Enh: Increase label# to 10.
640a A*  09/12/11  RH   6.40a
     A*    Fix: Clear 15 lines on Screen64 & Screen65, left garbage
650a A*  07/15/14  GJA  6.50a
     A*    Enh:  Add UCI selection when clearing a runner flag.
700a A*  07/10/16  DAS  7.00a
     A*    Enh:  Renamed to RU31004.
     A*    Enh:  Added F6=Sub to Screen3
700b A*  07/12/16  DAS  7.00b
     A*    Enh:  Change F6=Sub to F6=Sub Override when F10 is displayed.
     A*
710a A*  07/06/17  TAB  7.10a
     A*    Fix:  Screen 7 overlapped error message with command keys
     A********************************************************************
     A*  Client Custom Revisions: European Imports
     A*
EI  AA*    07/30/10  RBD  EI
     A*      - Added screens SCREEN20 and SCREEN620 for entering the
     A*        emp# of the runner.
EIa AA*    08/30/11  RH   EIa
     A*      - Renamed to RU31002 to merge EI 7-30-10 for update.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
520 AA  95                                  CA08
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.01'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
520 DA**                                4  3'Flag trn#:'
520 MA  41                              4  3'Flag trn#:'
520 DA**          W1TRN#         7Y 0B  4 14CHECK(ER)
520 MA  41        W1TRN#         7Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
520 AA N41                              4  3'Flag lab#:'
530aDA***** N41        W1LAB#         7Y 0B  4 14CHECK(ER)
530aMA N41        W1LAB#B       10   B  4 14CHECK(ER)
530aDA*****                                 EDTCDE(M)
520 AA  28                                  DSPATR(RI)
520 AA N28N90                               DSPATR(UL)
520 AA                                      DSPATR(PC)
520 AA  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
520 AA                                  7 11'F8=Load'
520 AA                                      COLOR(BLU)
520 AA N95                                  DSPATR(ND)
     A*
     A          R SCREEN2
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.02'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  1'Nbr scanned:'
     A            W2NRSN         5Y 0O  3 16EDTCDE(3)
     A                                  4  1'Slot:'
     A            W2DISP        12   O  4  9
     A                                  5  1'Label nbr:'
530aDA*****       W2LBL#         7Y 0B  5 14CHECK(ER)
530aMA            W2LBL#B       10   B  5 14CHECK(ER)
530aDA*****                                 EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
520 AA  42                              6  1'Scn slt'
520 AA  42        W2DSP         12A  B  6  9CHECK(ER)
520 AA  29                                  DSPATR(RI)
520 AA N29N90                               DSPATR(UL)
520 AA  09                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 14'F9=Skip'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA07
700aAA                                      CF06
     A  94                                  CA10
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.03'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  2  2'Lbl#'
     A            W3LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
     A                                  3  2'Slot:'
     A            W3DISP        12A  O  3  8
     A            WOITEM        50A  O  4  2
     A                                  5  2'Qty short/out'
     A            W3QYSO    R        O  5 16REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A                                  6  2'Qty found:'
510 DA*           W3QYFD    R        B  6 13REFFLD(RUQYFD RUNNER)
     A            W3QYFD         5A  B  6 13
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O  6 19REFFLD(LBUCOD LABEL)
     A                                  7  1'F7=Slots'
     A                                      COLOR(BLU)
700aAA N94                              7 10'F6=Sub'
700aAA N94                                  COLOR(BLU)
700aDA*N94                              7 11'F12=Cancel                        -
700aMA N94                              7 17'F12=Cancel                        -
     A                                                     '
     A N94                                  COLOR(BLU)
     A  94                              7 10'F10=Override warning'
     A  94                                  COLOR(BLU)
700bMA  94                              7 31'F6=Sub Override'
700aAA  94                                  COLOR(BLU)
700aDA* 94                              7 31'F12=Cancel'
700aMA  94                              7 47'F12=Cancel'
     A  94                                  COLOR(BLU)
     A*
500aAA          R SCREEN4
500aAA                                      CA03
500aAA                                      CA07
500aAA                                      CA12
500aAA  98                                  ALARM
500aAA                                      CLRL(07)
500aAA                                  1  2'RU310.04'
510aDA*                                 1 11'Clear Flags'
510aMA                                  1 11'Exp Date Entry'
500aAA                                      DSPATR(HI)
500aAA                                  2  2'Lbl#'
500aAA            W4LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
500aAA                                  3  2'Slot:'
500aAA            W4DISP        12A  O  3  8
500aAA            WOITEM        50A  O  4  2
500aAA                                  5  2'Qty found:'
500aAA            W4QYFD    R        O  5 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA            W4UCOD    R        O  5 19REFFLD(LBUCOD LABEL)
500aAA                                  6  2'Exp date:'
500aAA            W4DATE    R        B  6 12REFFLD(OEDATE ORDEXPDTE)
500aAA                                      EDTCDE(4)
500aAA  26                                  DSPATR(RI)
500aAA N26N90                               DSPATR(UL)
500aAA  06                                  DSPATR(PC)
500aAA  90                                  DSPATR(PR)
500aAA                                  7  2'F3/F12=Cancel'
500aAA                                      COLOR(BLU)
     A*
500aAA          R SCREEN5
500aAA                                      CA03
500aAA                                      CA07
500aAA                                      CA12
500aAA  98                                  ALARM
500aAA                                      CLRL(07)
500aAA                                  1  2'RU310.05'
510aDA*                                 1 11'Clear Flags'
510aMA                                  1 11'Box Nbr Entry'
500aAA                                      DSPATR(HI)
500aAA                                  2  2'Lbl#'
500aAA            W5LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
500aAA                                  3  2'Slot:'
500aAA            W5DISP        12A  O  3  8
500aAA            WOITEM        50A  O  4  2
500aAA                                  5  2'Qty found:'
500aAA            W5QYFD    R        O  5 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA            W5UCOD    R        O  5 19REFFLD(LBUCOD LABEL)
500aAA                                  6  2'Box nbr:'
500aAA            W5BOX#    R        B  6 11REFFLD(OBBOX# ORDBOX)
500aAA                                      EDTCDE(4)
500aAA  26                                  DSPATR(RI)
500aAA N26N90                               DSPATR(UL)
500aAA  06                                  DSPATR(PC)
500aAA  90                                  DSPATR(PR)
500aAA                                  7  2'F3/F12=Cancel'
500aAA                                      COLOR(BLU)
     A*
     A          R SCREEN6
     A*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.06'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  2  2'Lbl#:'
     A            W6LBL#    R        O  2  8REFFLD(LBLBL# LABEL)
     A                                  3  2'Slot:'
     A            W6DISP        12A  O  3  8
     A            WOITEM        50A  O  4  2
     A                                  5  2'Qty found:'
     A            W6QYFD    R        O  5 13REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(4)
     A            W6UCOD    R        O  5 19REFFLD(LBUCOD LABEL)
     A                                  6  2'Confirm Qty Y/N:'
     A            W6CONF         1A  B  6 19CHECK(ER)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A                                  7 11'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
650aAA          R SCREEN7
650aAA                                      CA03
650aAA                                      CA07
650aAA                                      CA12
650aAA                                      CF22
650aAA  98                                  ALARM
650aAA                                      CLRL(07)
650aAA                                  1  2'RU310.07'
650aAA                                  1 12'UCI Entry'
650aAA                                      DSPATR(HI)
650aAA                                  2  2'Lbl#'
650aAA            W7LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
650aAA                                  2 15'Qty Short'
650aAA            W3QYSO    R        O  2 25REFFLD(RUQYSO RUNNER)
650aAA                                      EDTCDE(3)
650aAA                                  3  2'Slot:'
650aAA            W7DISP        12A  O  3  8
650aAA            WOITEM        50A  O  4  2
650aAA                                  5  2'Qty found:'
650aAA            W7QYFD    R        O  5 13REFFLD(RUQYFD RUNNER)
650aAA                                      EDTCDE(4)
650aAA            W7UCOD    R        O  5 19REFFLD(LBUCOD LABEL)
650aAA                                  6  2'UCI:'
710aDA***         W7UCI        100   B  6  8CHECK(ER)
710aMA            W7UCI         50   B  6  8CHECK(ER)
650aAA  26                                  DSPATR(RI)
650aAA N26N90                               DSPATR(UL)
650aAA  06                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA N26N90                               DSPATR(HI)
710aDA***                               8  2'F12=Cancel F22=Done'
710aMA                                  7  2'F12=Cancel F22=Done'
650aAA                                      COLOR(BLU)
650aAA          R SCREEN8
650aAA*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
650aAA                                      CA03
650aAA                                      CA07
650aAA  94                                  CA10
650aAA                                      CA12
650aAA  98                                  ALARM
650aAA                                      CLRL(07)
650aAA                                  1  2'RU310.08'
650aAA                                  1 11'Clear Flags'
650aAA                                      DSPATR(HI)
650aAA                                  2  2'Lbl#'
650aAA            W3LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
650aAA                                  3  2'Slot:'
650aAA            W3DISP        12A  O  3  8
650aAA            WOITEM        50A  O  4  2
650aAA                                  5  2'Qty short/out'
650aAA            W3QYSO    R        O  5 16REFFLD(RUQYSO RUNNER)
650aAA                                      EDTCDE(3)
650aAA                                  6  2'Qty found:'
650aAA            W3QYFD         5A  B  6 13
650aAA                                      DSPATR(PR)
650aAA            W3UCOD    R        O  6 19REFFLD(LBUCOD LABEL)
650aAA                                  7  1'F7=Slots'
650aAA                                      COLOR(BLU)
650aAA N94                              7 11'F12=Cancel                        -
650aAA                                                     '
650aAA N94                                  COLOR(BLU)
650aAA  94                              7 10'F10=Override warning'
650aAA  94                                  COLOR(BLU)
650aAA  94                              7 31'F12=Cancel'
650aAA  94                                  COLOR(BLU)
650aAA*
EI  AA          R SCREEN20
EI  AA                                      CA03
EI  AA                                      CA12
EI  AA  98                                  ALARM
EI  AA                                      CLRL(07)
EI  AA                                  1  2'RU310.20'
EI  AA                                  1 12'Clear Flags'
EI  AA                                      DSPATR(HI)
EI  AA                                  3  2'Runner trn:'
EI  AA            W1TRN#         7Y 0O  3 14EDTCDE(4)
EI  AA                                  5  3'Emp #  :'
EI  AA            W20EMP         5Y 0B  5 14CHECK(ER)
EI  AA                                      EDTCDE(4)
EI  AA                                      DSPATR(UL)
EI  AA                                  7  2'F12=Cancel'
EI  AA                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*  16 line screens.
     A*
     A          R SCREEN61
     A  98                                  ALARM
     A                                      CA03
520 AA  95                                  CA08
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU310.01'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
520 DA**                                4  3'Scan trn#:'
520 MA  41                              4  3'Scan trn#:'
520 DA**          W1TRN#         7Y 0B  4 14CHECK(ER)
520 MA  41        W1TRN#         7Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
520 AA N41                              4  3'Scan lab#:'
530aDA*****N41        W1LAB#         7Y 0B  4 14CHECK(ER)
530aMA N41        W1LAB#B       10   B  4 14CHECK(ER)
530aDA*****                                 EDTCDE(M)
520 AA  28                                  DSPATR(RI)
520 AA N28N90                               DSPATR(UL)
520 AA                                      DSPATR(PC)
520 AA  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
520 AA                                 15 11'F8=Load'
520 AA                                      COLOR(BLU)
520 AA N95                                  DSPATR(ND)
     A*
     A          R SCREEN62
     A  98                                  ALARM
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU310.02'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  1'Nbr scanned:'
     A            W2NRSN         5Y 0O  3 16EDTCDE(3)
     A                                  5  1'Slot:'
     A            W2DISP        12A  O  5  9
     A                                  7  1'Label nbr:'
530aDA*****       W2LBL#         7Y 0B  7 14CHECK(ER)
530aMA            W2LBL#B       10   B  7 14CHECK(ER)
530aDA*****                                 EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
520 AA  42                              9  1'Scn slt'
520 AA  42        W2DSP         12A  B  9  9CHECK(ER)
520 AA  29                                  DSPATR(RI)
520 AA N29N90                               DSPATR(UL)
520 AA  09                                  DSPATR(PC)
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 15 14'F9=Skip'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN63
     A*%%TS  SD  20070524  172056  DAVE        REL-V5R3M0  5722-WDS
     A                                      CA03
700aAA                                      CF06
     A                                      CA07
     A  94                                  CA10
     A                                      CA12
     A  98                                  ALARM
520 DA**                                    CLRL(07)
520 MA                                      CLRL(15)
     A                                  1  2'RU310.03'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2'Lbl#'
     A            W3LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(3)
     A                                  5  2'Slot:'
     A            W3DISP        12A  O  5  8
     A            WOITEM        50A  O  7  2
     A                                  9  2'Qty short/out'
     A            W3QYSO    R        O  9 16REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A            W3UCO3    R        O  9 22REFFLD(LBUCOD LABEL)
     A                                 11  2'Qty found:'
510 DA*           W3QYFD    R        B 11 13REFFLD(RUQYFD RUNNER)
     A            W3QYFD         5A  B 11 13
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O 11 19REFFLD(LBUCOD LABEL)
     A                                 12  2'Qty found:'
     A  92                                  DSPATR(ND)
510 DA*           W3QYF1    R        B 12 13REFFLD(RUQYFD RUNNER)
     A            W3QYF1         5A  B 12 13
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90
     AO 92                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A            W3UCO1    R        O 12 19REFFLD(LBUCOD LABEL)
     A  92                                  DSPATR(ND)
     A                                 13  2'Qty found:'
     A  93                                  DSPATR(ND)
510 DA*           W3QYF2    R        B 13 13REFFLD(RUQYFD RUNNER)
     A            W3QYF2         5A  B 13 13
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90
     AO 93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W3UCO2    R        O 13 19REFFLD(LBUCOD LABEL)
     A  93                                  DSPATR(ND)
     A                                 15  1'F7=Slots'
     A                                      COLOR(BLU)
700aAA N94                             15 10'F6=Sub'
700aAA N94                                  COLOR(BLU)
700aDA*N94                             15 11'F12=Cancel                        -
700aMA N94                             15 17'F12=Cancel                        -
     A                                               '
     A N94                                  COLOR(BLU)
     A  94                             15 10'F10=Override warning'
     A  94                                  COLOR(BLU)
700bAA  94                             15 31'F6=Sub Override'
700aAA  94                                  COLOR(BLU)
700aDA* 94                             15 31'F12=Cancel'
700aMA  94                             15 47'F12=Cancel'
     A  94                                  COLOR(BLU)
     A*
500aAA          R SCREEN64
500aAA*%%TS  SD  20060215  075811  JEFF        REL-V5R3M0  5722-WDS
500aAA                                      CA03
500aAA                                      CA07
500aAA                                      CA12
500aAA  98                                  ALARM
640aDA*                                     CLRL(07)
640aMA                                      CLRL(15)
500aAA                                  1  2'RU310.04'
510aDA*                                 1 11'Clear Flags'
510aMA                                  1 11'Exp Date Entry'
500aAA                                      DSPATR(HI)
500aAA                                  3  2'Lbl#'
500aAA            W4LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
500aAA                                      EDTCDE(3)
500aAA                                  5  2'Slot:'
500aAA            W4DISP        12A  O  5  8
500aAA            WOITEM        50A  O  7  2
500aAA                                  9  2'Qty found:'
500aAA            W4QYFD    R        O  9 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA            W4UCOD    R        O  9 19REFFLD(LBUCOD LABEL)
500aAA                                 10  2'Qty found:'
500aAA  92                                  DSPATR(ND)
500aAA            W4QYF1    R        O 10 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA  92                                  DSPATR(ND)
500aAA            W4UCO1    R        O 10 19REFFLD(LBUCOD LABEL)
500aAA  92                                  DSPATR(ND)
500aAA                                 11  2'Qty found:'
500aAA  93                                  DSPATR(ND)
500aAA            W4QYF2    R        O 11 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA  93                                  DSPATR(ND)
500aAA            W4UCO2    R        O 11 19REFFLD(LBUCOD LABEL)
500aAA  93                                  DSPATR(ND)
500aAA                                 13  2'Exp date:'
500aAA            W4DATE    R        B 13 12REFFLD(OEDATE ORDEXPDTE)
500aAA                                      EDTCDE(4)
500aAA  26                                  DSPATR(RI)
500aAA N26N90                               DSPATR(UL)
500aAA  06                                  DSPATR(PC)
500aAA  90                                  DSPATR(PR)
500aAA                                 15  1'F3/F12=Cancel                '
500aAA                                      COLOR(BLU)
500aAA*
500aAA          R SCREEN65
500aAA*%%TS  SD  20060215  075811  JEFF        REL-V5R3M0  5722-WDS
500aAA                                      CA03
500aAA                                      CA07
500aAA                                      CA12
500aAA  98                                  ALARM
640aDA*                                     CLRL(07)
640aMA                                      CLRL(15)
500aAA                                  1  2'RU310.04'
510aDA*                                 1 11'Clear Flags'
510aMA                                  1 11'Box Nbr Entry'
500aAA                                      DSPATR(HI)
500aAA                                  3  2'Lbl#'
500aAA            W5LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
500aAA                                      EDTCDE(3)
500aAA                                  5  2'Slot:'
500aAA            W5DISP        12A  O  5  8
500aAA            WOITEM        50A  O  7  2
500aAA                                  9  2'Qty found:'
500aAA            W5QYFD    R        O  9 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA            W5UCOD    R        O  9 19REFFLD(LBUCOD LABEL)
500aAA                                 10  2'Qty found:'
500aAA  92                                  DSPATR(ND)
500aAA            W5QYF1    R        O 10 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA  92                                  DSPATR(ND)
500aAA            W5UCO1    R        O 10 19REFFLD(LBUCOD LABEL)
500aAA  92                                  DSPATR(ND)
500aAA                                 11  2'Qty found:'
500aAA  93                                  DSPATR(ND)
500aAA            W5QYF2    R        O 11 13REFFLD(RUQYFD RUNNER)
500aAA                                      EDTCDE(4)
500aAA  93                                  DSPATR(ND)
500aAA            W5UCO2    R        O 11 19REFFLD(LBUCOD LABEL)
500aAA  93                                  DSPATR(ND)
500aAA                                 13  2'Box number:'
500aAA            W5BOX#    R        B 13 15REFFLD(OBBOX# ORDBOX)
500aAA                                      EDTCDE(4)
500aAA  26                                  DSPATR(RI)
500aAA N26N90                               DSPATR(UL)
500aAA  06                                  DSPATR(PC)
500aAA  90                                  DSPATR(PR)
500aAA                                 15  1'F3/F12=Cancel                '
500aAA                                      COLOR(BLU)
     A*
     A          R SCREEN66
     A*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                  1  2'RU310.06'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2'Lbl#'
     A            W6LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(3)
     A                                  5  2'Slot:'
     A            W6DISP        12A  O  5  8
     A            WOITEM        50A  O  7  2
     A                                  9  2'Qty sh/out'
     A            W6QYSO    R        O  9 13REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A            W6UCO3    R        O  9 19REFFLD(LBUCOD LABEL)
     A                                 11  2'Qty found:'
     A            W6QYFD    R        O 11 13REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(4)
     A            W6UCOD    R        O 11 19REFFLD(LBUCOD LABEL)
     A                                 12  2'Qty found:'
     A  92                                  DSPATR(ND)
     A            W6QYF1    R        O 12 13REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(4)
     A  92                                  DSPATR(ND)
     A            W6UCO1    R        O 12 19REFFLD(LBUCOD LABEL)
     A  92                                  DSPATR(ND)
     A                                 13  2'Qty found:'
     A  93                                  DSPATR(ND)
     A            W6QYF2    R        O 13 13REFFLD(RUQYFD RUNNER)
     A                                      EDTCDE(4)
     A  93                                  DSPATR(ND)
     A            W6UCO2    R        O 13 19REFFLD(LBUCOD LABEL)
     A  93                                  DSPATR(ND)
     A                                 14  2'Confirm Qty Y/N:'
     A            W6CONF         1A  B 14 19CHECK(ER)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A                                 15 11'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
650aAA          R SCREEN67
650aAA                                      CA03
650aAA                                      CA07
650aAA                                      CA12
650aAA                                      CF22
650aAA  98                                  ALARM
650aAA                                      CLRL(15)
650aAA                                  1  2'RU310.07'
650aAA                                  1 12'UCI Entry'
650aAA                                      DSPATR(HI)
650aAA                                  2  2'Lbl#'
650aAA            W7LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
650aAA                                  2 15'Qty Short'
650aAA            W3QYSO    R        O  2 25REFFLD(RUQYSO RUNNER)
650aAA                                      EDTCDE(3)
650aAA                                  3  2'Slot:'
650aAA            W7DISP        12A  O  3  8
650aAA            WOITEM        50A  O  4  2
650aAA                                  5  2'Qty found:'
650aAA            W7QYFD    R        O  5 13REFFLD(RUQYFD RUNNER)
650aAA                                      EDTCDE(4)
650aAA            W7UCOD    R        O  5 19REFFLD(LBUCOD LABEL)
650aAA                                  6  2'UCI:'
710aDA***         W7UCI        100   B  6  8CHECK(ER)
710aMA            W7UCI         50   B  6  8CHECK(ER)
650aAA  26                                  DSPATR(RI)
650aAA N26N90                               DSPATR(UL)
650aAA  06                                  DSPATR(PC)
650aAA  90                                  DSPATR(PR)
650aAA N26N90                               DSPATR(HI)
650aAA                                 15  2'F12=Cancel F22=Done'
650aAA                                      COLOR(BLU)
650aAA          R SCREEN68
650aAA*%%TS  SD  20090414  084006  RETHA       REL-V6R1M0  5761-WDS
650aAA                                      CA03
650aAA                                      CA07
650aAA  94                                  CA10
650aAA                                      CA12
650aAA  98                                  ALARM
650aAA                                      CLRL(15)
650aAA                                  1  2'RU310.08'
650aAA                                  1 11'Clear Flags'
650aAA                                      DSPATR(HI)
650aAA                                  2  2'Lbl#'
650aAA            W3LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
650aAA                                  3  2'Slot:'
650aAA            W3DISP        12A  O  3  8
650aAA            WOITEM        50A  O  4  2
650aAA                                  5  2'Qty short/out'
650aAA            W3QYSO    R        O  5 16REFFLD(RUQYSO RUNNER)
650aAA                                      EDTCDE(3)
650aAA                                  6  2'Qty found:'
650aAA            W3QYFD         5A  B  6 13
650aAA                                      DSPATR(PR)
650aAA            W3UCOD    R        O  6 19REFFLD(LBUCOD LABEL)
650aAA                                 15  1'F7=Slots'
650aAA                                      COLOR(BLU)
650aAA N94                             15 11'F12=Cancel                        -
650aAA                                                     '
650aAA N94                                  COLOR(BLU)
650aAA  94                             15 10'F10=Override warning'
650aAA  94                                  COLOR(BLU)
650aAA  94                             15 31'F12=Cancel'
650aAA  94                                  COLOR(BLU)
650aAA*
EI  AA          R SCREEN620
EI  AA                                      CA03
EI  AA                                      CA12
EI  AA  98                                  ALARM
EI  AA                                      CLRL(07)
EI  AA                                  1  2'RU310.20'
EI  AA                                  1 12'Clear Flags'
EI  AA                                      DSPATR(HI)
EI  AA                                  3  2'Runner trn:'
EI  AA            W1TRN#         7Y 0O  3 14EDTCDE(4)
EI  AA                                  5  3'Emp #  :'
EI  AA            W20EMP         5Y 0B  5 14CHECK(ER)
EI  AA                                      EDTCDE(4)
EI  AA                                      DSPATR(UL)
EI  AA                                  7  2'F12=Cancel'
EI  AA                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Records for 16 line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
