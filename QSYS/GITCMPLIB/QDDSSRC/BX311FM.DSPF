     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  BX311FM    View AMPXREF file - Display NO BOXID; NO DELV RT
     A*  NANCY KOELZ
     A*
     A*  Revisions
     A*
700 AA*    05/10/16  NLK  7.00
     A*      - Created.
700aAA*    06/14/16  NLK  7.00a
     A*      - Increased BOX# from 3.0 to 5.0
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
700aMA            #BBOX          5Y 0O  8  2
     A            #BITEM    R        O  8  8REFFLD(AXREC/AXITEM *LIBL/AMPXREF)
     A            #BDESC        30A  O  8 24
     A            #BQALC         5Y 0O  8 55EDTCDE(4)
     A            #BQPCK         5Y 0O  8 61EDTCDE(4)
     A            #BCORD    R     Y  O  8 69REFFLD(AXREC/AXCORD *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20090612  110233  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View AMP ITEMS'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
      *
     A                                  2 31'for'
     A                                      DSPATR(HI)
     A            W1DT           8  0O  2 36EDTWRD('    -  -  ')
     A                                      DSPATR(HI)
      *
     A N45                              4  6'List of-
     A                                       NO DELIVERY ROUTE-
     A                                       with these items /-
     A                                       AMP picked, not on DelvRt'
     A                                      COLOR(BLU)
     A  45                              5  6'List of NO BOX ID-
     A                                       associated with-
     A                                       these items /-
     A                                       SKIP BOX or ZERO QTY'
     A                                      COLOR(BLU)
      *
700aMA                                  7  2'Box #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aMA                                  6 56'Quantity'
     A                                      DSPATR(HI)
700aMA                                  7  8'ITEM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aMA                                  7 24'Description'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aMA                                  7 55'ALLOC'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aMA                                  7 62'PICK'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aMA                                  7 69'CustOrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A          R FKEY1
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
     A                                 23  5'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
