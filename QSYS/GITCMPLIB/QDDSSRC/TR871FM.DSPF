     A*%%TS  SD  20120111  135015  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR871FM    Routes List Display
     A*  Retha Davis
     A*
     A*  Revisions
     A*
640 AA*    01/06/12  RBD  6.40
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20120106  112451  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O  8  5REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHRTID    R        O  8 11REFFLD(RHREC/RHRTID *LIBL/RTEHED)
     A            W1STAT        10   O  8 17
     A            RHTRUK    R        O  8 28REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            W1UNAM        10   O  8 39
     A            W1PCS          5Y 0O  8 50EDTCDE(4)
     A            W1IMPD         6Y 0O  8 56EDTCDE(Y)
     A            RHIMPT    R        O  8 65REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20120106  120505  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'Routes List Display'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        80A  O  2  1
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 57'--- Imported --'
     A                                      DSPATR(HI)
     A                                  7  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'RteId'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 17'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 28'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 39'Driver    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 50'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 56'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            RHRTE     R        O  8  5REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHRTID    R        O  8 11REFFLD(RHREC/RHRTID *LIBL/RTEHED)
     A            W1STAT        10   O  8 17
     A            W2BLDD         6Y 0O  8 28EDTCDE(Y)
     A            RHBLDT    R        O  8 37REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2PRTD         6Y 0O  8 46EDTCDE(Y)
     A            RHPRTT    R        O  8 55REFFLD(RHREC/RHPRTT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2EXPD         6Y 0O  8 64EDTCDE(Y)
     A            RHEXPT    R        O  8 73REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20120106  120505  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'Routes List Display'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        80A  O  2  1
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 29'---- Built ----'
     A                                      DSPATR(HI)
     A                                  6 47'--- Printed ---'
     A                                      DSPATR(HI)
     A                                  6 65'--- Exported --'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'RteId'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 17'Status    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 28'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 37'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 46'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 55'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 64'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 73'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F11=Next view  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20120111  135015  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Sort order . . . . . .'
     A            W1SORT         1A  B  3 24
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  3 31'Ascend,Descend'
     A                                  4  1'Order by . . . . . . .'
     A            W1OBY          1A  B  4 24
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  4 31'1=Route'
     A                                  5 31'2=Route Id'
     A                                  6  1'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  6 24
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  7  1'Route  . . . . . . . .'
     A            W1RTE          5A  B  7 24
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30                                  DSPATR(UL)
     A  10                                  DSPATR(PC)
     A                                  7 31'*ALL,generic%'
     A                                  8  1'Include route status :'
     A                                      COLOR(BLU)
     A                                  9  3'Open . . . . . . . .'
     A            W1OPEN         1A  B  9 24
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                  9 31'Y/N'
     A                                 10  3'Built  . . . . . . .'
     A            W1BILT         1A  B 10 24
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                 10 31'Y/N'
     A                                 11  3'Printed  . . . . . .'
     A            W1PRNT         1A  B 11 24
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(UL)
     A  07                                  DSPATR(PC)
     A                                 11 31'Y/N'
     A                                 12  3'Closed . . . . . . .'
     A            W1CLOS         1A  B 12 24
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A                                 12 31'Y/N'
     A                                 13  3'Exported . . . . . .'
     A            W1EXPO         6Y 0B 13 24
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(UL)
     A  09                                  DSPATR(PC)
     A                                 13 31'From MMDDYY or'
     A                                 14 31'0=No'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
