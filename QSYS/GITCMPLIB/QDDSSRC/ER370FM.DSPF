     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  ER370FM   Task report for an employee
     A*  24 March, 2000
     A*  Hemant Kapadia
     A*
     A*  03/24/00  HNK  4.13
     A*    Created
     A*
     A* ----- ATTENTION: THIS FORMAT NEEDS TO BE COMPILED WITH
     A*       DFTWRT = *NO SO THAT STATUS MESSAGES CAN BE DISPLAYED.
     A*
     A*  Revisions
     A*
     A*  09/18/09  GJA  5.30
     A*    Cleaned up and revamped for suspended transactions
     A*
640a A*  11/17/11  GJA  6.40a
     A*    Enh:  Added ability to select crew.
     A*
     A*----------------------------------------------------------------
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'ER370.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  8  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 53'Date (MMDDYY)'
     A                                  8 41'to'
     A            W1TODT         6Y 0B  8 44
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9  4'Time range . . . . . . . . .'
     A            W1FMTM         4Y 0B  9 34EDTCDE(4)
     A N32N90                               DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 53'Time (HHMM)'
     A                                  9 41'to'
     A            W1TOTM         4Y 0B  9 44
     A N33N90                               DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 10  4'Employee number  . . . . . .'
     A            W1EMP#         5Y 0B 10 34EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640aAA                                 11 20'OR'
640aAA                                 12  4'Crew code  . . . . . . . . .'
640aAA            W1CREW         6A  B 12 34
640aAA N29N90                               DSPATR(HI)
640aAA  29                                  DSPATR(RI)
640aAA N29N90                               DSPATR(UL)
640aAA  09                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
     A                                 16  4'Include merge pieces . . . .'
     A            W1MRG          1A  B 16 34
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 53'Y=Yes, N=No'
     A                                 18  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 18 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 53'Y=Yes, N=No'
     A                                 19  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 19 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 19 53'Name, *SYSVAL, *JOB'
     A                                 20  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 20 34
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 20 53'Character value, *STD'
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                  2 72TIME
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
