    AA*----------------------------------------------------------------
    AA*   Multilingual Version
    AA*   Every screen has a corresponding Spanish screen.
    AA*   All Spanish screens are at the end.
    AA*   Any changes must be made to both screens.
    AA*   If necessary, contact appropriate person to do translation.
    AA*----------------------------------------------------------------
    AA* ***************************************************************
    AA* *****  If the position of any slot entry field is changed *****
    AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
    AA* *****  program.                                           *****
    AA* ***************************************************************
     A*
     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  OR401FM      RF Select New Change Slot Location - Display.
     A*  14 November 2013
     A*  Lynn McMahon
     A*    Copied from OR681FM
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
650aAA*     11/14/13  LMC  6.50a
     A*       - Created.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'OR401.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
650aAA                                  1 31'Slot Locations'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCANREC
     A*%%TS  SD  19990618  100251  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A                                      CA08
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A N86                              1  2'Slot:'
650aAA  86                              1  2'Lcns:'
     A N86        W1DISP        12A  B  1  8CHECK(ER)
650aAA  86        W1LCNS        15A  B  1  8CHECK(ER)
     A*                                 1  2'OR401.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Item:'
     A            W1ITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Select'
     A                                       COLOR(BLU)
     A*                                 2 14'9=View Qty'
     A*                                      COLOR(BLU)
     A*                                 2 26'X=Slot Error'
     A*                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Subfile for List Display.
     A*
     A          R DSPSFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 33
     A            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 53'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R DSPSFL1                   SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLDISP        12A  H
     A            SLSTAT    R        O  5 33REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 36
     A            W1PRTY    R        O  5 59REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 38EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 47EDTWRD('  /  /  ')
     A            LALCNS        15A  O  5  6
     A            W1QTY         10A  O  5 22
     A*
     A          R DSPSFC1                   SFLCTL(DSPSFL1)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'License       '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 56'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 47'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 38'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 33'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 22'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display (16-Lines)
     A*
     A          R D16SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 33
     A            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 53'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R D16SFL1                   SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLSTAT    R        O  5 33REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 36
     A            W1PRTY    R        O  5 59REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 38EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 47EDTWRD('  /  /  ')
     A            LALCNS        15A  O  5  6
     A            W1QTY         10A  O  5 22
     A*
     A          R D16SFC1                   SFLCTL(D16SFL1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'License     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 56'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 47'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 38'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 33'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 22'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record. (08)
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record. (08)
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(09)
     A                                      CLRL(1)
     A*
     A*    Error Message Subfile Record. (16)
     A*
     A          R MSGREC16                  SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record. (08)
     A*
     A          R MSGCTL16                  SFLCTL(MSGREC16)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR16
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* Option, Command and EOF Message Lines for Lookup Panels.
     A*
     A*    Option Lines.
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    Command Lines. (08)
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    Command Lines. (16)
     A*
     A          R CMDREC16
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(16)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    'More.../Bottom' Message Line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    Display Delete Instructions.
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*
     A*    Bulk pick quantity to take.
     A*
     A          R WDWQTY
     A                                      WINDOW(1 2 6 20 *NOMSGLIN)
     A                                      USRRSTDSP
     A                                      WDWTITLE((*TEXT 'Enter Bulk Pick Qt-
     A                                      y') *TOP)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      OVERLAY
     A*
     A          R QTYREC
     A                                      WINDOW(WDWQTY)
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(01)
     A                                  1  1'Original'
     A                                  2  3'Slot'
     A            $DISP         12   O  2  8
     A                                  3  3'Qty '
417 DA*           $BLKQ          5Y 0O  3  8EDTCDE(L)
417 MA            $OBLKQ         5Y 0O  3  8EDTCDE(L)
     A                                  4  1'New slt'
     A            W1DISP        12   O  4  9
417 DA*                                 5  1'Bulk pick qty'
417 MA                                  5  1'Qty'
417 DA*           W3BQTY         5Y 0B  5 15EDTCDE(4)
417 MA            W3BQTY         5Y 0B  5  5EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
417 AA*
417 AA                                  5 12'Bal'
417 AA            W3BALQ         5Y 0O  5 16EDTCDE(4)
417 AA*
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    CLEAR ALL LINES IN SUBFILE LIST
     A*
     A          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A******************************************************
     A*
     A*    Clear All Lines.
     A*
     A          R CLRALL
     A                                      CLRL(*ALL)
     A*
     A******************************************************
     A*
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R WDWMSG
     A                                      WINDOW(1 2 6 20)
     A                                      USRRSTDSP
     A*
     A                                      WDWTITLE((*TEXT 'Bulk Picking Error-
     A                                      ') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WOTXT1        20A  O  1  1
     A            WOTXT2        20A  O  2  1
     A            WOTXT3        20A  O  3  1
     A            WOTXT4        20A  O  4  1
     A                               O  5  1'Continue:'
     A N40                                  DSPATR(ND)
     A            WOYES          1A  B  5 11COLOR(WHT)
     A                                      VALUES('Y' 'N')
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A                               O  5 13'(Y/N)'
     A N40                                  DSPATR(ND)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCANRESP
     A*%%TS  SD  19990618  100251  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A                                      CA08
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1 16'De slot  :'
     A            W1DISP        12A  B  1 27CHECK(ER)
     A                                  1  2'OR401.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
500 AA*----------------------------------------------------------------
500 AA          R POSTNRSP
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Prdc:'
     A            W1ITEM        65A  O  1  8
     A                                  2  2' '
     A                                  2  4'1=Escoge'
     A                                       COLOR(BLU)
     A                                  2 14'9=Ver Ctd '
     A                                       COLOR(BLU)
     A                                  2 26'X=Error de slot'
     A                                       COLOR(BLU)
     A*****************************************************************
     A*
     A* Subfile for List Display.
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 33
     A            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 53'Priord Pk'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Fcha Exp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Ctd Dispo'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display (16-Lines)
     A*
500 AA*----------------------------------------------------------------
500 AA          R D16SFLSP                  SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
     A            SLPICK         1A  O  5 33
     A            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
500 AA*----------------------------------------------------------------
500 AA          R D16SFCSP                  SFLCTL(D16SFLSP)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA08
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 53'Priord Pk'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Fcha Ent'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Fcha Exp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Ctd Dispn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R DELRECSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Oprima Enter para confirmar -
     A                                       4=Borrar.'
     A                                      COLOR(BLU)
     A                                  4  2'Oprima F12 para vover a cambiar -
     A                                       seleccion.'
     A                                      COLOR(BLU)
     A*
     A*    Bulk pick quantity to take.
     A*
500 AA*----------------------------------------------------------------
500 AA          R WDWQTYSP
     A                                      WINDOW(1 2 6 20 *NOMSGLIN)
     A                                      USRRSTDSP
     A                                      WDWTITLE((*TEXT 'Incorpore Ctd de -
     A                                      Pick de Bulto') *TOP)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      lar') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      OVERLAY
     A*
500 AA*----------------------------------------------------------------
500 AA          R QTYRECSP
     A                                      WINDOW(WDWQTY)
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(01)
     A                                  1  1'Original'
     A                                  2  3'Slot'
     A            $DISP         12   O  2  8
     A                                  3  3'Ctd '
417 DA*           $BLKQ          5Y 0O  3  8EDTCDE(L)
417 MA            $OBLKQ         5Y 0O  3  8EDTCDE(L)
     A                                  4  1'Slt Nvo'
     A            W1DISP        12   O  4  9
417 DA*                                 5  1'Ctd Pick Blto'
417 MA                                  5  1'Ctd'
417 DA*           W3BQTY         5Y 0B  5 15EDTCDE(4)
417 MA            W3BQTY         5Y 0B  5  5EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
417 AA*
417 AA                                  5 12'Bal'
417 AA            W3BALQ         5Y 0O  5 16EDTCDE(4)
417 AA*
     A******************************************************
     A*
     A*    COMMAND LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R CMDDELSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A*
500 AA*----------------------------------------------------------------
500 AA          R WDWMSGSP
     A                                      WINDOW(1 2 6 20)
     A                                      USRRSTDSP
     A*
     A                                      WDWTITLE((*TEXT 'Error de Pck Bult-
     A                                      ') *CENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      lar') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WOTXT1        20A  O  1  1
     A            WOTXT2        20A  O  2  1
     A            WOTXT3        20A  O  3  1
     A            WOTXT4        20A  O  4  1
     A                               O  5  1'Continue:'
     A N40                                  DSPATR(ND)
     A            WOYES          1A  B  5 11COLOR(WHT)
     A                                      VALUES('Y' 'N')
     A N40                                  DSPATR(ND)
     A N40                                  DSPATR(PR)
     A                               O  5 13'(Y/N)'
     A N40                                  DSPATR(ND)
