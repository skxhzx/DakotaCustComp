     A*%%TS  SD  20040506  120053  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RU110FM      RF - Runner Flag Entry
     A*  22 April 2004
     A*  Max Blacknik
     A*
     A********************************************************************
     A*  Revisions:
     A*
417 AA*  04/22/04  MLB  4.17
     A*    - Created.
     A*
530a A*  05/14/10  GJA  5.30a
     A*    - Enh:  Increase label to 10
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU110.01'
     A                                  1 11'Flag Entry'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Scan lbl#:'
530aDA*****       W1LBL#         7Y 0B  4 14CHECK(ER)
530aMA            W1LBL#B       10   B  4 14CHECK(ER)
530aDA*****                                 EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU110.02'
     A                                  1 11'Flag Entry'
     A                                  2  2'Lbl#'
     A            W2LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(4)
     A            WOITEM        50A  O  3  2
     A                                  4  2'Slot:'
     A            W2DISP        12A  O  4  8
     A                                  5  2'Pick qty:'
     A            W2QPCK    R        O  5 12REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(4)
     A            W2UCOD    R        O  5 18REFFLD(LBUCOD LABEL)
     A                                  6  2'Short/out qty'
     A            W2QYSO    R        B  6 16REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                  7  9'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*  16 line screens.
     A*
     A          R SCREEN61
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU110.01'
     A                                  1 11'Flag Entry'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Scan lbl#:'
530aDA*****       W1LBL#         7Y 0B  4 14CHECK(ER)
530aMA            W1LBL#B       10   B  4 14CHECK(ER)
530aDA*****                                 EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15 11'F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN62
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU110.02'
     A                                  1 11'Flag Entry'
     A                                      DSPATR(HI)
     A            W2FILLER       1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  3  2'Lbl#'
     A            W2LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(4)
     A            WOITEM        50A  O  5  2
     A                                  7  2'Slot:'
     A            W2DISP        12A  O  7  8
     A                                  9  2'Pick qty:'
     A            W2QPCK    R        O  9 12REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(4)
     A            W2UCOD    R        O  9 18REFFLD(LBUCOD LABEL)
     A                                 11  2'Short/out qty'
     A            W2QYSO    R        B 11 16REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15  9'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Records for 16 line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
