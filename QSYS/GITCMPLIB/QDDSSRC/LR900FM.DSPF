     A*%%TS  SD  20030117  113637  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  LR900FM   Lot/License plate lookup/verify
     A*  17 January 2003
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    01/17/03  MLB  4.17
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'LR900.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 28'Lot/License Plate Lookup'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  2 47'Position to code:'
     A            PTINP1        15A  B  2 65
     A            FLD002         1A  O  3  2
     A            WHCODE         3Y 0O  2  3EDTCDE(4)
     A                                  2  7'-'
     A            WHDESC        30A  O  2  9
     A            FLD005         1A  O  1  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LRLCNS    R        O 11  6REFFLD(LRREC/LRLCNS *LIBL/LICHIST)
     A            LRTWHD    R        O 11 22REFFLD(LRREC/LRTWHD *LIBL/LICHIST)
     A            LRTASL    R        O 11 28REFFLD(LRREC/LRTASL *LIBL/LICHIST)
     A            LRTLOC    R        O 11 33REFFLD(LRREC/LRTLOC *LIBL/LICHIST)
     A            LRTLVL    R        O 11 37REFFLD(LRREC/LRTLVL *LIBL/LICHIST)
     A            LRTHND    R        O 11 41REFFLD(LRREC/LRTHND *LIBL/LICHIST)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  6'Lot/License    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Aisl'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 33'Loc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 37'Lvl'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 41'Hnd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
