     A*%%TS  SD  20020628  151345  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A********************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  IT263FM      RF Will Call Transfer Returns
     A*  28 June 2002
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*
     A*  Revisions:
     A*
416 AA*  06/28/02  MLB  4.16
     A*    - Created.
416bAÂ‚*    10/29/02  RH   4.16b
     A*    - Add F20 to screen 4, program already had code for F20.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT26361'
     A                                  1 10'Will TfrRtn'
     A                                      DSPATR(HI)
     A                                  5  2'W i l l   C a l l'
     A                                      DSPATR(HI)
     A                                  7  2' T r a n s f e r '
     A                                      DSPATR(HI)
     A                                  9  2'  R e t u r n s  '
     A                                      DSPATR(HI)
     A                                 12  2'Warehouse . :'
     A            W1WHSE         3Y 0B 12 16
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  2'Department .:'
     A            W1WHDP         5A  B 13 16
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPT2           1A  B  6  2
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2TXT         70A  O  6  4
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      CA03
     A                                      CA12
     A                                  5  2'O'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  4'Item /Description                 -
     A                                                                         -
     A                                       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R SCREEN3
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                      CA03
     A                                      CA12
     A                                  1  2'IT26363'
     A                                  1 10'Wil TfrRtn'
     A                                  3  1'Item'
     A            W3ITEM        15A  O  3  6
     A                                  4  1'Desc'
     A            W3DESC        30A  O  4  6
     A                                  5  1'Pack'
     A            W3PDSC        15A  O  5  6
     A                                  6  1'Dept'
     A            W3WHDP         5A  O  6  6
     A                                  8  1'Avl '
     A            W3AVAL        15A  O  8  6
     A                                 10  1'Qty  . . :'
     A            W3QTY1         5S 0B 10 12
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A            W3UM1          2A  O 10 19
     A            W3QTY2         5S 0B 11 12
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25                                  DSPATR(UL)
     A N52                                  DSPATR(ND)
     A            W3UM2          2A  O 11 19
     A N52                                  DSPATR(ND)
     A            W3QTY3         5S 0B 12 12
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A N53                                  DSPATR(ND)
     A            W3UM3          2   O 12 19
     A N53                                  DSPATR(ND)
     A                                 15  1'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 15 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN4
416b A                                      CF20
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                      CA03
     A                                      CA12
     A                                  1  2'IT26364'
     A                                  1 10'Willcal Tfr'
     A                                  3  1'Item'
     A            W4ITEM        15A  O  3  6
     A                                  4  1'Desc'
     A            W4DESC        30A  O  4  6
     A                                  5  1'Pack'
     A            W4PDSC        15A  O  5  6
     A                                  6  1'Dept'
     A            W4WHDP         5A  O  6  6
     A                                  7  1'Slot'
     A            W4DISP        12A  O  7  6
     A                                  8  1'RtnQty'
     A            W4RQTY        12A  O  8  8
     A                                 10  1'Scan slot  . . . :'
     A            W4SCAN        12A  B 11  3
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A                                      CHECK(ER)
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN99
     A  98                                  ALARM
     A                                      SLNO(01)
     A                                      CLRL(15)
     A                                  1  2'IT26399'
     A                                  1 10'Will TfrRtn'
     A            W9TXT01      273A  B  2  2CNTFLD(021)
     A                                      DSPATR(PR)
     A  53                                  DSPATR(UL)
     A                                 15  2'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(01)
     A                                  1  2'IT26362'
     A                                  1 10'Will TfrRtn'
     A            OPTLN1        75A  O  3  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(14)
     A            MORELN         7A  O  1 14DSPATR(HI)
     A*
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A*         R CLR03END
     A*                                     OVERLAY
     A*                                     SLNO(3)
     A*                                 1  2' '
     A*                                 2  2' '
     A*                                 3  2' '
     A*                                 4  2' '
     A*                                 5  2' '
     A*                                 6  2' '
     A*                                 7  2' '
     A*                                 8  2' '
     A*                                 9  2' '
     A*                                10  2' '
     A*                                11  2' '
     A*                                12  2' '
     A*                                13  2' '
     A*                                14  2' '
     A*                                15  2' '
     A*                                16  2' '
     A*                                17  2' '
     A*                                18  2' '
     A*                                19  2' '
     A*                                20  2' '
     A*                                21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(2)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A*                                 5  2' '
     A*                                 6  2' '
     A*                                 7  2' '
     A*                                 8  2' '
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
