     A*%%TS  SD  20071004  161044  BFC         REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UI110FM   UCI Catch Weight - Display
     A*  04 August 2006
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
510 AA*  08/04/06  JCJ  5.10
     A*    - Created.
     A*
510b A*  10/04/07  HNK  5.10b
     A*    - Add stock status flag.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20060821  113729  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'UI110.01'
     A                                  1 29'UCI Catch Weight Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 37'List Display'
     A                                      DSPATR(HI)
     A*           $PWHSE         3Y 0O  2  2EDTCDE(4)
     A*                                 2  6'-'
     A*           WHDESC        30A  O  2  8
     A*                                 3  2'Item:'
     A*           $PITEM        15A  O  3  8
     A*           WODESC        20A  O  3 24
     A*                                 3 46'Qty:'
     A*           WOPQTY         5Y 0O  3 51EDTCDE(3)
     A*                                 4 28'Catch Weights Entered:'
     A*           WOCW#          7Y 0O  4 51EDTCDE(3)
     A*                                 4 61'Total:'
     A*           WOTCW          9Y 2O  4 68EDTCDE(3)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20060805  205357  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(05)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A*%%TS  SD  20051117  091028  JEFF        REL-V5R3M0  5722-WDS
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20071004  160745  BFC         REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1SEQ          4Y 0H
     A            W1OPT          2A  B  9  2
     A  20                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1UCI2        10A  O  9  6
     A            W1CWGT         7Y 0B  9 17EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
510b A            W1STS          1   O  9 25
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20071004  161044  BFC         REL-V5R2M0  5722-WDS
     A  *DS3                                SFLLIN(0002)
     A                                      SFLSIZ(0144)
     A                                      SFLPAG(0036)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  7 17'Catch'
     A                                      DSPATR(HI)
     A                                  7 43'Catch'
     A                                      DSPATR(HI)
     A                                  7 69'Catch'
     A                                      DSPATR(HI)
     A                                  8  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  6'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 17'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 32'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 43'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 54'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 58'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 69'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 28'Opt'
     A                                      DSPATR(HI)
     A                                  8 24'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 24'Stk'
     A                                      DSPATR(HI)
     A                                  8 50'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 50'Stk'
     A                                      DSPATR(HI)
     A                                  8 76'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 76'Stk'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20051017  205641  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  2 46'Position to code . . .'
     A            PTINP1        10A  B  2 70
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
