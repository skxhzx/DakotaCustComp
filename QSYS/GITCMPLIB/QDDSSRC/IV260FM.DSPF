     A*%%TS  SD  20060712  125326  ROCKY       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IV260FM   Inventory page assignment  - Display
     A*  11 July 2006
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
510  A*    07/11/06  RH  V5.10
     A*      - Created using IV360
     A*      - Uses Pop up window to enter inventory date and team.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA11
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A*%%TS  SD  19941014  075724  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'IV260.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 30'Inventory Page Assignment'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940811  153641  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 41'Change to department  . .'
     A            PTINP2         5A  B  3 68
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  2 41'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 68EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WOWHSE         3A  O  2  2
     A                                  2  8'-'
     A            WHDESC        30A  O  2 10
     A            FLD001         1A  O  1  2
     A            WDCODE         5A  O  3  2
     A                                  3  8'-'
     A            WDDESC        30A  O  3 10
     A                                  4 41'Position to slot  . . . .'
     A            PTINP3        12A  B  4 68
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20060712  125326  ROCKY       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1FDSP        12A  O 11  7
     A            W1TDSP        12A  O 11 21
     A            IVATEM    R        O 11 35REFFLD(IVAREC/IVATEM *LIBL/INVPASN)
     A            IVAPAG    R        O 11 47REFFLD(IVAREC/IVAPAG *LIBL/INVPASN)
     A                                      EDTCDE(4)
     A*           W1DATE         6Y 0O 11 33EDTCDE(Y)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060712  125116  ROCKY       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10  7'From Slot   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'To Slot     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35'Team      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Page '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Pop-up Window to enter inventory date and team for page assign.
     A*
     A          R WINDOW1
500dDA*                                     WINDOW(9 20 6 30 *NOMSGLIN)
500dMA                                      WINDOW(9 20 10 30 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT &WDWTITLE) -
     A                                      *CENTER)
     A                                      WDWTITLE((*TEXT 'F5=Refresh  F12=Ca-
     A                                      ncel') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WDWTITLE      28A  P
     A*
     A*
     A          R WDWFMT1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                  2  2'Inventory date:'
     A            WWIDTE         6Y 0B  2 18EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  3  2'Inventory team:'
     A            WWTEAM        10A  B  3 18
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    Window Error Message Subfile Record.
     A*
     A          R MSGREC1                   SFL
     A                                      SFLMSGRCD(05)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Window Error Message Subfile Control Record.
     A*
     A          R MSGCTL1                   SFLCTL(MSGREC1)
     A                                      WINDOW(WINDOW1)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(05)
     A                                      CLRL(1)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940811  113933  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(07)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A*%%TS  SD  19941014  082552  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for'
     A                                      COLOR(BLU)
     A            DELLN1        10A  O    +1COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
