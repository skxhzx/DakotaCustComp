     A*%%TS  SD  20140507  114806  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG176FM    Staging Export View Purchase Order Detail
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            PEDPO     R        O  8  9REFFLD(PEDREC/PEDPO *LIBL/EXPSPOD)
     A            PEDSEQ    R        O  8 19REFFLD(PEDREC/PEDSEQ *LIBL/EXPSPOD)
     A                                      EDTCDE(Z)
     A            PEDITM    R        O  8 27REFFLD(PEDREC/PEDITM *LIBL/EXPSPOD)
     A            S1OQTY        15A  O  8 43
     A            S1RQTY        15A  O  8 59
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140507  114806  BFC         REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Purchase Order Detail Info'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'   PO#   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19' Seq '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 27'     Item      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 43'  Ordered Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 59' Received Qty  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20140410  154635  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG176.02'
     A                                  1 25'View Purchase Order Detail Info'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Full Detail Information'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            PEDWHS    R        O  5 18REFFLD(PEDREC/PEDWHS *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            PEDBATID  R        O  5 54REFFLD(PEDREC/PEDBATID *LIBL/EXPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  6  2'PO #  . . . . .'
     A            PEDPO     R        O  6 18REFFLD(PEDREC/PEDPO *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            PEDADDTS  R        O  6 54REFFLD(PEDREC/PEDADDTS *LIBL/EXPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  7  2'PO Seq #  . . .'
     A            PEDSEQ    R        O  7 18REFFLD(PEDREC/PEDSEQ *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            PEDSTSTS  R        O  7 54REFFLD(PEDREC/PEDSTSTS *LIBL/EXPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  9  2'Status/Message '
     A            PEDSTATUS R        O  9 18REFFLD(PEDREC/PEDSTATUS *LIBL/EXPSP-
     A                                      OD)
     A                                      DSPATR(HI)
     A            PEDTEXT   R        O  9 24REFFLD(PEDREC/PEDTEXT *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 13  2'Item  . . . . .'
     A            PEDITM    R        O 13 18REFFLD(PEDREC/PEDITM *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 13 34'Qty Ordered . . . .'
     A            S2QTY         20A  O 13 54DSPATR(HI)
     A                                 14  2'Vendor Item . .'
     A            PEDVIT    R        O 14 18REFFLD(PEDREC/PEDVIT *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 14 34'Spcl Order Code . .'
     A            PEDSPC    R        O 14 54REFFLD(PEDREC/PEDSPC *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 16  2'PO Message  . .'
     A            PEDPOM    R        O 16 18REFFLD(PEDREC/PEDPOM *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 17  2'Misc 1  . . . .'
     A            PEDMS1    R        O 17 18REFFLD(PEDREC/PEDMS1 *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 18  2'Misc 2  . . . .'
     A            PEDMS2    R        O 18 18REFFLD(PEDREC/PEDMS2 *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 19  2'Misc 3  . . . .'
     A            PEDMS3    R        O 19 18REFFLD(PEDREC/PEDMS3 *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 20  2'Misc 4  . . . .'
     A            PEDMS4    R        O 20 18REFFLD(PEDREC/PEDMS4 *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 21  2'Misc 5  . . . .'
     A            PEDMS5    R        O 21 18REFFLD(PEDREC/PEDMS5 *LIBL/EXPSPOD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
