     A*%%TS  SD  20110515  171917  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LT310FM    View LICACTIVE file - Display
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
600 AA*    02/25/09  DAS  6.00  P#00045
     A*      - Created.
     A*
600aAA*    06/03/09  DAS  6.00a
     A*      - Added W1LCNS to selection window.
     A*
600bAA*    06/12/09  DAS  6.00b
     A*      - Changed fields on subfiles
     A*
610aAA*    08/24/09  DAS  6.10a
     A*      - Added %generic% to some parms.
     A*
610bAA*    08/24/09  DAS  6.10a
     A*      - Added SCREEN2 as a prompt screen for displaying
     A*        special slots.
     A*
610CAA*    09/02/09  CWM  6.10C
     A*      - Added message subfile processing and error checking for
     A*        SC2.
610dAA*    10/22/10  RH   6.10d
     A*      - Enh: add 'XDK' to screen for slot type.
     A*      - Rename LT310fm to LT31002
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20110515  171917  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1VAL1         3A  O  8  6
     A            W1VAL2         3   O  8 14
     A            W1QTY1         9Y 0O  8 21EDTCDE(J)
     A            W1QTY2         9Y 0O  8 35EDTCDE(J)
     A            W1QTY3         9Y 0O  8 49EDTCDE(J)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20110515  171917  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 28'View Slot Summary for Item'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WOCOL1         6A  O  7  5DSPATR(HI)
     A                                      DSPATR(UL)
     A            WOCOL2         6   O  7 13DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 21'   Normal  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 35'Breakdown 1'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'Breakdown 2'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20110502  181418  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20110502  181418  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View Active Licenses'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SCREEN2
     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LT310.02'
     A                                  1 31'License Information'
     A                                      DSPATR(HI)
     A                                  7  9'Warehouse . . . .'
     A            W2WHSE         3Y 0B  7 28
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  9  9'Department  . . .'
     A            W2WHDP         5A  B  9 28CHECK(ER)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  9 37'*ALL'
     A                                 11  9'Slot type . . . .'
     A            W2TYPE         3   B 11 28
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 11 37'RCV, STG, OSS, USR, XDK'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R FKEY1
     A*%%TS  SD  20110502  181418  DAVE        REL-V6R1M0  5761-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20110513  074011  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(3 17 16 45)
     A                                  1 15'Selection Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Warehouse . .'
     A            W1WHSE         3Y 0B  3 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  4  1'Item  . . . .'
     A            W1ITEM        15A  B  4 15DSPATR(HI)
     A                                  6  1'Aisles  . . .'
     A            W1ASL1         3   B  6 15
     A            W1ASL2         3   B  6 20
     A            W1ASL3         3   B  6 25
     A            W1ASL4         3   B  6 30
     A            W1ASL5         3   B  6 35
     A                                  7  2'(Everything else will go into ''ot-
     A                                      her'')'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
