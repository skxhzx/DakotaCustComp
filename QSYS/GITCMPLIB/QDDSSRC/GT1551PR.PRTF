     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OI2851PR    Import FTP orders - Exceptions Ver 4
     A*  21 January 2009
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    01/21/09  MLB  5.20
     F*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD11
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HDR1        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'OI285'
     A          R HEAD21
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    80
     A                                      'Stop'
     A                                    88
     A                                      'Truck'
     A*
     A          R HEAD31
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHORD IMPFORDH4)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHTYP IMPFORDH4)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHCUS IMPFORDH4)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHCNM IMPFORDH4)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHRTE IMPFORDH4)
     A            OIHEST    R        O    +3
     A                                      REFFLD(OIHEST IMPFORDH4)
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHTRK IMPFORDH4)
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*
     A          R HEAD41
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Original'
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Item'
     A          R DETAIL1
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDITM IMPFORDD4)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDQ1  IMPFORDD4)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDQ2  IMPFORDD4)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDQ3  IMPFORDD4)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDWH2 IMPFORDD4)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDOR2 IMPFORDD4)
     A                                      EDTCDE(4)
     A            OIDOIT    R        O    +2
     A                                      REFFLD(OIDOIT IMPFORDD4)
     A            DETMSG        20A  O   106
     A          R SUBTTL1
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ11         7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ21         7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ31         7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A          R GRNDTTL1
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer order Exceptions:'
     A                                      SPACEB(001)
     A            GRORD1         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail line Exceptions   :'
     A                                      SPACEB(001)
     A            GRLIN1         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
