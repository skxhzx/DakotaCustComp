     A*%%TS  SD  20000310  112402  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI210FM    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
413 AA*   03/09/00  DAS  4.13
     A*     - Added qty fill options.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000310  112402  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PCK120.01'
     A                                  1 21'Initialize Slots for Whse 900 Pick-
     A                                       Test'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Place on job queue  . . . . . . .'
     A            W1JOBQ         1A  B  8 39
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 49'Y=Yes, N=No'
730aAA                                  9  4'Warehouse . . . . . . . . . . . .'
730aAA            W1WHSE         3Y 0B  9 39EDTCDE(4)
730aAA N25N90                               DSPATR(HI)
730aAA  25                                  DSPATR(RI)
730aAA N25N90                               DSPATR(UL)
730aAA  05                                  DSPATR(PC)
730aAA  90                                  DSPATR(PR)
     A                                 13  6'This program will create items bas-
     A                                      ed on the slots for'
     A                                 14  6'warehouse 900. One item will be cr-
     A                                      eated for each pick'
     A                                 15  6'slot in the fixed section and for -
     A                                      each level 1 slot in'
     A                                 16  6'the pir section.'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
