     A*%%TS  SD  20140411  164349  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG125FM    Staging Import View Regular/Will-call Order Header
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140407  152129  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OIHSTATUS R        H      REFFLD(OIHREC/OIHSTATUS *LIBL/IMPSO-
     A                                      RDH)
     A            OIHTEXT   R        H      REFFLD(OIHREC/OIHTEXT *LIBL/IMPSORD-
     A                                      H)
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            OIHRTE    R        O  8  9REFFLD(OIHREC/OIHRTE *LIBL/IMPSORDH)
     A            OIHEST    R        O  8 15REFFLD(OIHREC/OIHEST *LIBL/IMPSORDH)
     A                                      EDTCDE(Z)
     A            OIHORD    R        O  8 21REFFLD(OIHREC/OIHORD *LIBL/IMPSORDH)
     A                                      EDTCDE(Z)
     A            OIHCUS    R        O  8 29REFFLD(OIHREC/OIHCUS *LIBL/IMPSORDH)
     A                                      EDTCDE(Z)
     A            OIHCNM    R        O  8 41REFFLD(OIHREC/OIHCNM *LIBL/IMPSORDH)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140411  164349  BFC         REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 23'View Route Import Order Header Inf-
     A                                      o'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 15'Stop '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 21' Order '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 29' Customer  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'             Customer Name        -
     A                                           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20140411  164349  BFC         REL-V7R1M0  5770-WDS
     A  98                                  ALARM
     A                                  1  2'SG125.02'
     A                                  1 23'View Route Import Order Header Inf-
     A                                      o'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Header Information - Page 1'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            OIHWHS    R        O  5 18REFFLD(OIHREC/OIHWHS *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            OIHBATID  R        O  5 54REFFLD(OIHREC/OIHBATID *LIBL/IMPSOR-
     A                                      DH)
     A                                      DSPATR(HI)
     A                                  6  2'Route ID  . . .'
     A            OIHRTEID  R        O  6 18REFFLD(OIHREC/OIHRTEID *LIBL/IMPSOR-
     A                                      DH)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            OIHADDTS  R        O  6 54REFFLD(OIHREC/OIHADDTS *LIBL/IMPSOR-
     A                                      DH)
     A                                      DSPATR(HI)
     A                                  7  2'Order ID  . . .'
     A            OIHORDID  R        O  7 18REFFLD(OIHREC/OIHORDID *LIBL/IMPSOR-
     A                                      DH)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            OIHSTSTS  R        O  7 54REFFLD(OIHREC/OIHSTSTS *LIBL/IMPSOR-
     A                                      DH)
     A                                      DSPATR(HI)
     A                                  9  2'Status  . . . .'
     A            OIHSTATUS R        O  9 18REFFLD(OIHREC/OIHSTATUS *LIBL/IMPSO-
     A                                      RDH)
     A                                      DSPATR(HI)
     A            OIHTEXT   R        O  9 24REFFLD(OIHREC/OIHTEXT *LIBL/IMPSORD-
     A                                      H)
     A                                      DSPATR(HI)
     A                                 13  2'Route . . . . .'
     A            OIHRTE    R        O 13 18REFFLD(OIHREC/OIHRTE *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 13 34'Order . . . . . . .'
     A            OIHORD    R        O 13 54REFFLD(OIHREC/OIHORD *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 14  2'Stop  . . . . .'
     A            OIHSTP    R        O 14 18REFFLD(OIHREC/OIHSTP *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 14 34'Order Type  . . . .'
     A            OIHTYP    R        O 14 54REFFLD(OIHREC/OIHTYP *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 15  2'Extended Stop .'
     A            OIHEST    R        O 15 18REFFLD(OIHREC/OIHEST *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 15 34'Truck . . . . . . .'
     A            OIHTRK    R        O 15 54REFFLD(OIHREC/OIHTRK *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 17  2'Customer  . . .'
     A            OIHCUS    R        O 17 18REFFLD(OIHREC/OIHCUS *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A            OIHCNM    R        O 17 30REFFLD(OIHREC/OIHCNM *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20140411  164349  BFC         REL-V7R1M0  5770-WDS
     A  98                                  ALARM
     A                                  1  2'SG125.03'
     A                                  1 23'View Route Import Order Header Inf-
     A                                      o'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Header Information - Page 2'
     A                                      DSPATR(HI)
     A                                  6  2'Address 1 . . .'
     A            OIHCA1    R        O  6 18REFFLD(OIHREC/OIHCA1 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                  7  2'Address 2 . . .'
     A            OIHCA2    R        O  7 18REFFLD(OIHREC/OIHCA2 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                  8  2'City/State/Zip '
     A            OIHCCT    R        O  8 18REFFLD(OIHREC/OIHCCT *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A            OIHCST    R        O  8 39REFFLD(OIHREC/OIHCST *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A            OIHCZP    R        O  8 42REFFLD(OIHREC/OIHCZP *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 10  2'Misc Info . . .'
     A            OIHMSC    R        O 10 18REFFLD(OIHREC/OIHMSC *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 11  2'Misc 1  . . . .'
     A            OHMMS1    R        O 11 18REFFLD(OIHREC/OHMMS1 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 12  2'Misc 2  . . . .'
     A            OHMMS2    R        O 12 18REFFLD(OIHREC/OHMMS2 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 13  2'Misc 3  . . . .'
     A            OHMMS3    R        O 13 18REFFLD(OIHREC/OHMMS3 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 14  2'Misc 4  . . . .'
     A            OHMMS4    R        O 14 18REFFLD(OIHREC/OHMMS4 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 15  2'Misc 5  . . . .'
     A            OHMMS5    R        O 15 18REFFLD(OIHREC/OHMMS5 *LIBL/IMPSORDH)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
