     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OP510FM   PIR options - General Employee Productivity Options
     A*  12 July 1994
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
530a A*  06/07/09  GJA    5.30a
     A*      Enh:  Added options
     A*      Voice Module? ....ability to use LABELDO
     A*      Zone or Department....keep track of task by zone or dept
     A*      Tote/Box/Drop functionality?...
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP510.01'
     A                                  1 22'General Employee Productivity Opti-
     A                                      ons'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Manually assign employee numbers  -
     A                                      . . . . .'
     A            W1MAN          1A  B  7 49
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N21N90                               DSPATR(HI)
     A                                  8  4'Next computer assigned employee nu-
     A                                      mber  . .'
     A            W1NXTE         5Y 0B  8 49
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A                                 23  2'F3=Exit F5=Refresh F12=Cancel'
     A                                      COLOR(BLU)
530aAA                                  9  4'Voice Module? . . . . . . . . . . -
530aAA                                      . . . . .'
530aAA                                  9 61'(Y or N)'
530aAA            W1VYN          1A  B  9 49
530aAA  23                                  DSPATR(RI)
530aAA N23N90                               DSPATR(UL)
530aAA  03                                  DSPATR(PC)
530aAA  90                                  DSPATR(PR)
530aAA N23N90                               DSPATR(HI)
530aAA                                 10  4'Zone or Department. . . . . . . . -
530aAA                                      . . . . .'
530aAA                                 10 61'(Z or D)'
530aAA            W1ZORD         1A  B 10 49
530aAA  24                                  DSPATR(RI)
530aAA N24N90                               DSPATR(UL)
530aAA  04                                  DSPATR(PC)
530aAA  90                                  DSPATR(PR)
530aAA N24N90                               DSPATR(HI)
530aAA                                  7 61'(Y or N)'
530aAA                                 11  4'Tote/Box/Drop functionality?. . . -
530aAA                                      . . . . .'
530aAA                                 11 61'(Y or N)'
530aAA            W1TBD          1A  B 11 49
530aAA  25                                  DSPATR(RI)
530aAA N25N90                               DSPATR(UL)
530aAA  05                                  DSPATR(PC)
530aAA  90                                  DSPATR(PR)
530aAA N25N90                               DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
