     A*%%TS  SD  20090128  025331  CHUCKM      REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  PS191FM  Pallet List - Display
     A*  07 August 2006
     A*  Jeffrey Jahnke
     A*
     A*  This was a copy of ZH120FM
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    01/28/09  CWM  5.20
     A*      - Enh: Added support for entry of host stop.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20060807  184809  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'PS191 '
     A                                  1 32'Label Maintenance '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20060809  144545  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A            W1TRUK        40A  O  2  2
     A                                  2 46'Pallet:'
     A            $KPAL#         7Y 0O  2 54EDTCDE(Z)
     A                                  3  2'Route:'
     A            $KRTE          5A  O  3  9
     A                                  3 16'Route Id:'
     A            $KRTID         5A  O  3 26
     A                                  3 33'Zone:'
     A            $KSSZN         6   O  3 39
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20090128  025331  CHUCKM      REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            LWLBL#    R        O 11  2REFFLD(LWREC/LWLBL# *LIBL/WORKLBL)
     A                                      EDTCDE(Z)
520 DA*           LWSTOP    R        O 11 10REFFLD(LWREC/LWSTOP *LIBL/WORKLBL)
520 MA            LWSTOP    R        H      REFFLD(LWREC/LWSTOP *LIBL/WORKLBL)
520 AA            DSPSTP         3Y 0O 11 10EDTCDE(4)
     A            LWITEM    R        O 11 14REFFLD(LWREC/LWITEM *LIBL/WORKLBL)
     A            W1DESC        18A  O 11 30
     A            OHCORD         7Y 0O 11 49EDTCDE(Z)
     A            LWORD     R        O 11 57REFFLD(LWREC/LWORD *LIBL/WORKLBL)
     A                                      EDTCDE(Z)
     A            LWSEQ     R        O 11 65REFFLD(LWREC/LWSEQ *LIBL/WORKLBL)
     A                                      EDTCDE(Z)
     A            LWQALC    R        O 11 71REFFLD(LWREC/LWQALC *LIBL/WORKLBL)
     A                                      EDTCDE(Z)
     A            LWASL2         3A  O 11 77
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060809  150040  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9  2'Label'
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9 10
     A                                  9 49'Customer'
     A                                      DSPATR(HI)
     A                                  9 71'Qty'
     A                                      DSPATR(HI)
     A                                 10  2'Number '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 10'Stp'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Item Number    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Item Desc         '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 49'Order #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 57'Ord Id '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 65'Seq# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 71'Alloc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 77'Out'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
