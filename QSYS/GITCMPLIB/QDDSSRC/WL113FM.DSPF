     A*%%TS  SD  19980706  131121  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  WL113FM   Aisle maintenance - Copy aisle layout
     A*  06 July 1998
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'WL113.01'
     A                                  1 33'Aisle Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 39'Copy'
     A                                      DSPATR(HI)
     A                                  4  5'From Warehouse:'
     A            W1FMWH        40A  O  4 21
     A                                  5 10'From Dept:'
     A            W1FWHD        60A  O  5 21
     A                                  6  9'From Aisle:'
     A            W1FASL         3   O  6 21
     A*
     A                                 10  6'Type the warehouse number, departm-
     A                                      ent and aisle.'
     A*
     A                                 13  7'To Warehouse:'
     A            W1TWHS         3Y 0B 13 21EDTCDE(3)
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 14 12'To Dept:'
     A            W1TWHD         5A  B 14 21
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 11'To Aisle:'
     A            W1TASL         3   B 15 21
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
