     A*%%TS  SD  20040506  120053  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  TB122FM      RF - Tote and Bundle Count maintenance
     A*  09 April 2010
     A*  Retha Davis
     A*
     A********************************************************************
     A*  Revisions:
     A*
     A*  04/09/10  RBD  5.30
     A*    - Created.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'TB122.01'
     A                                  2  2'Tote and Bundle Mnt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2' Warehouse:'
     A            W1WHSE         3Y 0B  4 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  3'Scan trn#:'
     A            W1TRN#         7Y 0B  5 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CF01
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'TB122.02'
     A                                  2  2'Tote and Bundle Mnt'
     A                                      DSPATR(HI)
     A                                  3  2'Trn#'
     A                                      DSPATR(HI)
     A            W2TRN#    R        O  3  7REFFLD(LBTRN# LABEL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            W2SZON    R        O  3 15REFFLD(TBSZON TOTEBUND)
     A                                      DSPATR(HI)
     A                                  4  2'Tote count  :'
     A            W2QTOT    R        B  4 16REFFLD(TBQTOT TOTEBUND)
     A                                      EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Bundle count:'
     A            W2QBUN    R        B  5 16REFFLD(TBQBUN TOTEBUND)
     A                                      EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  1'F1=Zone cmpl for pal'
     A                                      COLOR(BLU)
     A                                  7  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                  7  9'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*  16 line screens.
     A*
     A          R SCREEN61
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'TB122.01'
     A                                  2  2'Tote and Bundle Mnt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2' Warehouse:'
     A            W1WHSE         3Y 0B  4 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  3'Scan trn#:'
     A            W1TRN#         7Y 0B  6 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15 11'F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN62
     A  98                                  ALARM
     A                                      CF01
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'TB122.02'
     A                                  2  2'Tote and Bundle Mnt'
     A                                      DSPATR(HI)
     A            W2FILLER       1A  B  3  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  4  2'Trn#'
     A                                      DSPATR(HI)
     A            W2TRN#    R        O  4  7REFFLD(LBTRN# LABEL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            W2SZON    R        O  4 15REFFLD(TBSZON TOTEBUND)
     A                                      DSPATR(HI)
     A                                  6  2'Tote count  :'
     A            W2QTOT    R        B  6 16REFFLD(TBQTOT TOTEBUND)
     A                                      EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  2'Bundle count:'
     A            W2QBUN    R        B  8 16REFFLD(TBQBUN TOTEBUND)
     A                                      EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  1'F1=Zone cmpl for pal'
     A                                      COLOR(BLU)
     A                                 15  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15  9'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Records for 16 line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
