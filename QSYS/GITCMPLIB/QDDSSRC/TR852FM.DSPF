     A*%%TS  SD  20120606  153036  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR852FM    Trax - Stop Level Options - Display
     A*
     A*  Retha Davis
     A*
     A*  Revisions
     A*
640 AA*    03/16/12  RBD  6.40
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20120425  115139  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTPRTE    R        O  8  5REFFLD(TTPREC/TTPRTE *LIBL/TTSTP)
     A            TTPRTI    R        O  8 11REFFLD(TTPREC/TTPRTI *LIBL/TTSTP)
     A            TTPSTP    R        O  8 17REFFLD(TTPREC/TTPSTP *LIBL/TTSTP)
     A            W1CNAM        40A  O  8 20
     A            TTPCCE    R        O  8 62REFFLD(TTPREC/TTPCCE *LIBL/TTSTP)
     A            TTPSIG    R        O  8 66REFFLD(TTPREC/TTPSIG *LIBL/TTSTP)
     A            TTPPIV    R        O  8 70REFFLD(TTPREC/TTPPIV *LIBL/TTSTP)
     A            TTPPAL    R        O  8 74REFFLD(TTPREC/TTPPAL *LIBL/TTSTP)
     A            TTPBUL    R        O  8 78REFFLD(TTPREC/TTPBUL *LIBL/TTSTP)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20120425  121238  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 32'Stop Level Options'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        76A  O  2  3
     A            SCOPT1        38A  O  4  2COLOR(BLU)
     A            LEGND1        38A  O  4 42COLOR(BLU)
     A            SCOPT2        38A  O  5  2COLOR(BLU)
     A            LEGND2        38A  O  5 42COLOR(BLU)
     A                                  6  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 11'RteID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 17'St'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 20'Name                              -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 61'CCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 65'SIG'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 69'PIV'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 73'PAL'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 77'BUL'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F9=Select/Sort  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20120606  153036  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                  1 13'Stop Level Options'
     A                                      DSPATR(HI)
     A                                  2 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  4  1'Order  . . . . . .'
     A            W1SORT         1A  B  4 20DSPATR(HI)
     A                                  4 27'Ascend, Descend'
     A                                  5  1'Order by . . . . .'
     A            W1OBY          1A  B  5 20DSPATR(HI)
     A                                  5 27'1 = Route'
     A                                  6 27'2 = RteID'
     A                                  8  1'Warehouse  . . . .'
     A            W1WHSE         3Y 0B  8 20
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A                                  9  1'Overrides only . .'
     A            W1OVER         1A  B  9 20
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                  9 30'Y = Yes'
     A                                 10  1'Route  . . . . . .'
     A            W1RTE          5A  B 10 20
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                 10 27'*ALL = All'
     A                                 11  1'Stop . . . . . . .'
     A            W1STOP         3Y 0B 11 20
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A                                 11 30'0 = All'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R DELREC
     A*%%TS  SD  20120316  135545  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  1  2'TR852'
     A                                  1 20'Stop Level Options'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Press F10 to confirm your choice f-
     A                                      or 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choice.'
     A                                      COLOR(BLU)
     A                                  6  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 11'RteID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 17'St'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 20'Name                              -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TTPRTE    R        O  8  5
     A            TTPRTI    R        O  8 11
     A            TTPSTP    R        O  8 17
     A            W1CNAM    R        O  8 20
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
