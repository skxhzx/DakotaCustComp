     A*%%TS  SD  20120917  171054  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR320FM    View Trax at route level - Display
     A*  Retha Davis
     A*
     A*  Revisions
     A*
640 AA*    07/18/11  RBD  6.40
     A*      - Created.
     A*
640aAA*    02/22/12  RBD  6.40a
     A*      - Shrank some columns and added new column for Total
     A*        Pieces.
     A*      - Removed non-scan and returns parms from sort/select
     A*        as they were not being used.
     A*      - Use ind 84 for sflend instead of 90.
     A*
640bAA*    09/13/12  RBD  6.40b
     A*      - Added Route Printed Date to selection criteria.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20120222  164035  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            #FRTE     R        O  8  5REFFLD(TTRREC/TTRRTE *LIBL/TTRTE)
     A            W1UNAM        10A  O  8 11
     A            #FS       R        O  8 23REFFLD(TTRREC/TTRS *LIBL/TTRTE)
     A                                      EDTCDE(4)
     A            #FSD      R        O  8 30REFFLD(TTRREC/TTRSD *LIBL/TTRTE)
     A                                      EDTCDE(4)
     A            W1QDLV         6Y 0O  8 35EDTCDE(4)
     A            W1QDLN         6Y 0O  8 43EDTCDE(4)
     A            W1QCRT         6Y 0O  8 50EDTCDE(4)
     A            W1QPRT         6Y 0O  8 57EDTCDE(4)
     A            W1TPCS         6Y 0O  8 65EDTCDE(4)
     A            W1STRD         6Y 0O  8 73EDTWRD('  /  /  ')
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20120917  171054  BFC         REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 32'View Trax Routes'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 22'Total'
     A                                      DSPATR(HI)
     A                                  6 29'Stops'
     A                                      DSPATR(HI)
     A                                  6 36'Pcs'
     A                                      DSPATR(HI)
     A                                  6 43'Pcs'
     A                                      DSPATR(HI)
     A                                  6 52'Curr / Prev'
     A                                      DSPATR(HI)
     A                                  6 65'Total'
     A                                      DSPATR(HI)
     A                                  6 73'Trax Dlv'
     A                                      DSPATR(HI)
     A                                  7  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'Driver    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 22'Stops'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 29'Dlvrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 36'Dlvrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 43'NonScn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 51'Pcs Returned'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'Pcs   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 73'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SFL2                      SFL
     A*%%TS  SD  20110719  104844  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            #FRTE     R        O  8  5REFFLD(TTRREC/TTRRTE *LIBL/TTRTE)
     A            W1TRUK    R        O  8 11REFFLD(RHREC/RHTRUK *LIBL/RTEHED)
     A            #FSRT     R        O  8 22REFFLD(TTRREC/TTRSRT *LIBL/TTRTE)
     A                                      EDTWRD(' 0:  :  ')
     A            #FFIT     R        O  8 32REFFLD(TTRREC/TTRFIT *LIBL/TTRTE)
     A                                      EDTWRD(' 0:  :  ')
     A            #FRTI     R        O  8 41REFFLD(TTRREC/TTRRTI *LIBL/TTRTE)
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
     A*%%TS  SD  20110719  164215  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
640aDA**90                                  SFLEND(*MORE)
640aMA  84                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 32'View Trax Routes'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  6 22'Delivery'
     A                                      DSPATR(HI)
     A                                  6 32'Delivery'
     A                                      DSPATR(HI)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 22'Start   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 32'Finish  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'Rte ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R SCREEN2
     A*%%TS  SD  20120913  135731  RETHA       REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TR320.02'
     A                                  1 31'View Trax Routes'
     A                                      DSPATR(HI)
     A                                  7  9'Warehouse . . . .'
     A            W2WHSE         3Y 0B  7 28
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  9  9'Route . . . . . .'
     A            W2RTE          5A  B  9 28
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  9 40'*ALL'
640bAA                                 11  9'Route Prtd Date .'
640bAA            W2PRTD         6Y 0B 11 28
640bAA N26                                  DSPATR(HI)
640bAA  26                                  DSPATR(RI)
640bAA N26                                  DSPATR(UL)
640bAA  06                                  DSPATR(PC)
640bAA                                      EDTCDE(Z)
     A                                 11 40'MMDDYY or 0=All'
     A                                 13  9'Trax Dlvry Date .'
     A            W2DLVD         6Y 0B 13 28
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
640bAA                                      EDTCDE(Z)
     A                                 13 40'MMDDYY or 0=All'
     A                                 15  9'Driver  . . . . .'
     A            W2USR         10A  B 15 28
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 15 40'Driver Name or *ALL'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F11=Next view  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20120222  124943  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 17 16 45)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order  . . . . . .'
     A            W1SORT         1A  B  3 20DSPATR(HI)
     A                                  3 25'Ascending, Descending'
     A                                  4  1'Order by . . . . .'
     A            W1OBY          1A  B  4 20DSPATR(HI)
     A                                  4 25'1 = Route'
     A                                  5 25'2 = Delivery Date'
     A                                  7  1'Warehouse  . . . .'
     A            W1WHSE         3Y 0B  7 20
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A                                  8  1'Route  . . . . . .'
     A            W1RTE          5A  B  8 20
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  8 32'*ALL'
640bAA                                  9  1'Route Prtd Date  .'
640bAA            W1PRTD         6Y 0B  9 20
640bAA N26                                  DSPATR(HI)
640bAA  26                                  DSPATR(RI)
640bAA N26                                  DSPATR(UL)
640bAA  06                                  DSPATR(PC)
640bAA                                      EDTCDE(Z)
     A                                  9 32'0=All'
640bMA                                 10  1'Trax Dlvry Date  .'
     A            W1DLVD         6Y 0B 10 20
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
640bAA                                      EDTCDE(Z)
     A                                 10 32'0=All'
     A                                 11  1'Driver . . . . . .'
     A            W1USR         10A  B 11 20
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 11 32'*ALL'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
