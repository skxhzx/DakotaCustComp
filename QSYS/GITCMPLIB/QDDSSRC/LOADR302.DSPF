     A*%%TS  SD  20160728  075732  THOMAS      REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*
     A*  LOADR3FM Truck Loader - Pallet Loading Screen.
     A*  May 24, 2006
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
650aAA*  01/19/15  RBD  6.50a
     A*    - Increased ldcube field size from 5,2 to 7,3.
     A*
700aAA*  07/28/16  TAB  7.00a
     A*    - Added voice ID to screen
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A                                      CA13
     A                                      CF10
     A                                      CF17
     A                                      CF18
     A          R TITLE
     A*%%TS  SD  20060524  151435  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'LOADR3'
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 31'Pallet Loading Detail'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A* Position to record
     A*
     A*----------------------------------------------------------------
     A          R POSTNREC
     A*%%TS  SD  20071005  125129  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 46'Change to warehouse . . .'
     A            PTINP1         3Y 0B  1 73EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  2 46'Position to pallet id . .'
     A  24        PTINP2         7Y 0B  2 73
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
     A  25        PTINP3         7Y 0B  2 73
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      CHECK(ER)
     A  33                              3  2'HACCP Plt in exccess of'
     A                                      DSPATR(HI)
     A  33                              3 26'Red=45 Min'
     A                                      COLOR(RED)
     AO 33                              3 38'Yellow=30 Min'
     A                                      COLOR(YLW)
     AO 33                              3 52'on dock.'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display
     A*
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070226  172810  BFCTEST1    REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LDPLID         7Y 0O 11  6EDTCDE(Z)
     A            W1STAT         7A  O 11 14
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  32                                  DSPATR(BL)
     A            LDTRKZ         6A  O 11 22
     A            LDLDSQ         2S 0O 11 30
     A            STOPR1         2A  O 11 34
     A                                 11 37'-'
     A            STOPR2         2A  O 11 39
     A            LDPCS          5Y 0O 11 43EDTWRD('  0  ')
650aDA**          LDCUBE         5Y 2O 11 49EDTWRD('  0.  ')
650aMA            LDCUBE         7Y 3O 11 49EDTWRD('   0.   ')
650aDA**          LDENDT         6Y 0O 11 57EDTWRD('  :  :  ')
650aMA            LDENDT         6Y 0O 11 58EDTWRD('  :  :  ')
     A            W1FLAG         2Y 0O 11 68EDTCDE(Z)
     A  28                                  COLOR(RED)
     A  28                                  DSPATR(BL)
     A            LDRTID         5A  O 11 72
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070226  172810  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9  3
     A                                 10 29'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'Load'
     A                                      DSPATR(HI)
650aDA**                               10 57'Time    '
650aMA                                 10 58'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'PltId  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 67'Flag'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700aDA**                                9 14'Pallet'
700aAA                                  9 14'Trans'
     A                                      DSPATR(HI)
     A                                 10 14'Status '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
650aDA**                                9 57'Load'
650aMA                                  9 58'Load'
     A                                      DSPATR(HI)
650aMA                                 10 49'Cubes   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Stops  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 22'Dept  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Rte Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display - view 2
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R VW2SFL                    SFL
     A*%%TS  SD  20070326  110727  BFCTEST1    REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LDPLID         7Y 0O 11  7EDTCDE(Z)
700a A            W1UVID         4Y 0O 11 15
     A            LDTRKZ         6A  O 11 23
     A            LDLDSQ         2S 0O 11 31
     A            STOPR1         2A  O 11 36
     A                                 11 39'-'
     A            STOPR2         2A  O 11 41
     A            LDPCS          5Y 0O 11 46EDTCDE(Z)
     A            W1STRD         6Y 0O 11 53EDTCDE(Y)
     A            LDSTRT         6Y 0O 11 63EDTWRD('  :  :  ')
     A            LDENDT         6  0O 11 73EDTWRD('  :  :  ')
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20160728  075732  THOMAS      REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
700a A                                  9 15'Voice'
700a A                                      DSPATR(HI)
     A                                  9 30'Load'
     A                                      DSPATR(HI)
     A                                  9 53'Started'
     A                                      DSPATR(HI)
     A                                  9 63'Started'
     A                                      DSPATR(HI)
     A                                  9 73'Ending'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'PltId  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A
700a A                                 10 15'Id     '
700a A                                      DSPATR(HI)
700a A                                      DSPATR(UL)
     A                                 10 23'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Stops  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Pcs  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Date    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 73'Time    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Subfile for list display - view 3
     A*
     A*----------------------------------------------------------------
     A          R VW3SFL                    SFL
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A*----------------------------------------------------------------
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*
     A* Error Message Subfile Records
     A*
     A*----------------------------------------------------------------
     A          R MSGREC                    SFL
     A*----------------------------------------------------------------
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*----------------------------------------------------------------
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R OPTREC
     A*%%TS  SD  20071005  125650  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 50'Rte#:'
     A                                      COLOR(YLW)
     A            OPTRTE         5A  O  1 58COLOR(YLW)
     A                                  1 66'Door#:'
     A                                      COLOR(YLW)
     A            OPTDOR         3A  O  1 73COLOR(YLW)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A*    Command lines
     A*
     A*----------------------------------------------------------------
     A          R CMDREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A*----------------------------------------------------------------
     A          R MORREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A*    Delete display instructions
     A*
     A*----------------------------------------------------------------
     A          R DELREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A*
     A* Records to clear different portions of a panel
     A*
     A*----------------------------------------------------------------
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A*----------------------------------------------------------------
     A          R CLR03END
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*----------------------------------------------------------------
     A*
     A*    Clear lines between title and subfile list
     A*
     A*----------------------------------------------------------------
     A          R CLR0306
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*----------------------------------------------------------------
     A*
     A          R CMDDEL
     A*
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
