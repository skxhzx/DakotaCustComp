     A*%%TS  SD  20170309  112440  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   P.O. Box 1957
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WD112FM   Simple table maintenance - Change screen
     A*  20 February 1993
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    07/06/94  DAS
     A*      - Added warehouse code and description.
     A*
417 AA*    05/04/04  DAS  4.17
     A*      - Added F9=Auto Assign.
     A*
710aAA*    02/14/17  JCJ  7.10a
     A*      - Renamed to RG12201.
710bAA*    01/04/18  MLB  7.10b
     A*      - Renamed to RG12202.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Cheney Brothers
     A*
CBI AA*    12/18/07  CBI-JGD
     A*      Added new field "W1TRLS" to Screen1 record to
     A*      hold trailer size input.
     A*
CBIaAA*    12/16/08  CBI-JGD
     A*      Added new field "W1CADC" to Screen1 record to
     A*      identify route being processed as a CADEC or
     A*      non CADEC' route.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: Coastal Sunbelt Produce
     A*
CSP AA*    01/04/18  MLB  P#01852
     A*      - Revised pgm to add screen 2 to confirm update of route
     A*        to Closed status when truck template = MISTAKE.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA09
     A          R SCREEN1
     A*%%TS  SD  20170309  112440  BFC         REL-V7R1M0  5770-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'RH122.01'
     A                                  1 30'Work with Open Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Truck Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Route:'
     A            $PRTE          5A  O  5 13
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10 50'F9=Auto Assign'
     A                                      COLOR(BLU)
     A                                 10  4'Truck  . . . . . . . .'
     A            W1TRUK        10A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N21N90                               DSPATR(HI)
     A N21N90                               DSPATR(UL)
CBI AA*
     A                                 12  4'Trailer size . . . . .'
     A  44                                  DSPATR(ND)
     A            W1TRLS        10A  B 12 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  44
     AO 90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  44                                  DSPATR(ND)
     A*
     A                                 14  4'Is this a Cadec route?'
     A  44                                  DSPATR(ND)
     A            W1CADC         1A  B 14 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  44
     AO 90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  44                                  DSPATR(ND)
     A                                 14 50'Y=Yes, N=No'
     A  44                                  DSPATR(ND)
     A*
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
CSP AA*
CSP AA          R SCREEN2
CSP AA                                      CF22
CSP AA  98                                  ALARM
CSP AA                                      CLRL(23)
CSP AA                                      OVERLAY
CSP AA                                  1  2'RH122.02'
CSP AA                                  1 27'Route Template Update'
CSP AA                                      DSPATR(HI)
CSP AA                                  1 72DATE
CSP AA                                      EDTCDE(Y)
CSP AA                                  2 27'Change Truck Template'
CSP AA                                      DSPATR(HI)
CSP AA            $PWHSE         3Y 0O  4  2EDTCDE(3)
CSP AA                                  4  6'-'
CSP AA            WHDESC        30A  O  4  8
CSP AA                                  6  2'Route:'
CSP AA            RHRTE          5A  O  6  9
CSP AA                                  6 15'-'
CSP AA            $PRTID         5A  O  6 17
CSP AA                                  8  9'Are you sure you want to change th-
CSP AA                                      e Truck Template to MISTAKE for ALL-
CSP AA                                                 orders on this route?'
CSP AA                                 12  9'Press F22 to change Truck Template-
CSP AA                                       for ALL Orders on route'
CSP AA                                 14  9'Press F3/F12 to cancel without cha-
CSP AA                                      nging Truck Template'
CSP AA                                 23  2'F3=Exit  F5=Refresh  F22=Chg Templ-
CSP AA                                      ate  F12=Cancel'
CSP AA                                      COLOR(BLU)
CSP AA*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
