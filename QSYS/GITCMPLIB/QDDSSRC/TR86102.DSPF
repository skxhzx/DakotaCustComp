     A*%%TS  SD  20130207  131418  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  TR861FM  Trax - Stop Flags detail (One whse) - Display
     A*  06 Jan 2011
     A*  Retha Davis
     A*
     A*  Revisions:
     A*
640 AA*    01/06/11  RBD  6.40
     A*      - Created.
     A*
650aAA*    02/07/13  RBD  6.50a
     A*      - TTSTPFLG file has changed.  Removed type and icon.
     A*        Added order, unique Id, and display time.
     A*
650bAA*    02/19/13  RBD  6.50a
     A*      - Changed display of order to customer order.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      CF07
     A          R TITLE
     A*%%TS  SD  20130207  131418  RETHA       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                  1  2'TR861'
     A                                  1 25'Work with Trax Stop Flags Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20130207  122059  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                      CLRL(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 39'Truck:'
     A            W1TRUK        10A  O  1 46
     A                                  1 58'Rte:'
     A            W1RTE          5A  O  1 63
     A                                  1 69'RteId:'
     A            W1RTID         5A  O  1 76
     A                                  2  2'Stop:'
     A            W1STOP         2Y 0O  2  8EDTCDE(3)
650bDA**                                2 12'Order:'
650bMA                                  2 12'Cust Ord:'
650bDA**          W1CORD         7Y 0O  2 19EDTCDE(3)
650bMA            W1CORD         7Y 0O  2 22EDTCDE(3)
650bDA**                                2 28'Cust:'
650bMA                                  2 31'Cust:'
650bDA**          W1CNAM        40A  O  2 34
650bMA            W1CNAM        40A  O  2 37
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20130207  122059  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1CHAR         1   O 11  6
     A            W1UID         12   O 11 10
     A            W1DSP          1   O 11 24
     A            W1ALE1        53   O 11 28
     A            W1ALE2        47   O 12 28
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20130207  122059  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLMODE(&DSPMODE)
     A  51                                  SFLDROP(CF07)
     A N51                                  SFLFOLD(CF07)
     A            DSPMODE        1A  H      TEXT('Hidden: Display Mode')
     A            FLD001         1A  O  9  3
     A                                  9 23'Disp'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  5'Flag'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'Unique ID   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Alert                             -
     A                                                         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for list display - View 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20130207  122059  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1CHAR         1A  O 11  6
     A            W2DATE         6  0O 11 10EDTCDE(Y)
     A            W2TIME         6  0O 11 20EDTWRD(' 0:  :  ')
     A            W2FILLR       25   O 12  1
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20130207  122059  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                      SFLMODE(&DSPMODE)
     A  51                                  SFLDROP(CF07)
     A N51                                  SFLFOLD(CF07)
     A            DSPMODE        1A  H      TEXT('Hidden: Display Mode')
     A            FLD002         1A  O  9  3
     A                                  9 11'Add/Chg'
     A                                      DSPATR(HI)
     A                                  9 21'Add/Chg'
     A                                      DSPATR(HI)
     A                                 10  2'Op'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Flag'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 20'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press F10 to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
