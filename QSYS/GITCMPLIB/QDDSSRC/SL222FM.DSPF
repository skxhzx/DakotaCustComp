     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SL222.01'
     A                                  1 30'Delete Group of Slots'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  8 51'1 - 999'
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5   B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  9 51'*ALL, Code'
     A                                 10  4'Aisle  . . . . . . . . . . .'
     A            W1ASLE         4   B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                 10 65'(Y=Yes, N=No)'
     A                                      DSPATR(ND)
     A                                 10 51'*ALL, Code'
     A                                 11  4'Location . . . . . . . . . .'
     A            W1FLOC         3Y 0B 11 34EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 11 39'to'
     A            W1TLOC         3Y 0B 11 43EDTCDE(4)
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 11 51'1 - 999'
     A                                 12  4'Level  . . . . . . . . . . .'
     A            W1FLVL         2Y 0B 12 35EDTCDE(4)
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                 12 39'to'
     A            W1TLVL         2Y 0B 12 43EDTCDE(4)
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A                                 12 51'1 - 99'
     A                                 13  4'Hand stack . . . . . . . . .'
     A            W1HSTK         4   B 13 34
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A                                 13 51'*ALL'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL222.02'
     A                                  1 30'Delete Group of Slots'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  8 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP    R        O  9 34REFFLD(W1WHDP *SRC)
     A                                 10  4'Aisle  . . . . . . . . . . .'
     A            W1ASLE    R        O 10 34REFFLD(W1ASLE *SRC)
     A                                 11  4'Location . . . . . . . . . .'
     A            W1FLOC    R        O 11 34REFFLD(W1FLOC *SRC)
     A                                      EDTCDE(4)
     A                                 11 39'to'
     A            W1TLOC    R        O 11 43REFFLD(W1TLOC *SRC)
     A                                      EDTCDE(4)
     A                                 12  4'Level  . . . . . . . . . . .'
     A            W1FLVL    R        O 12 35REFFLD(W1FLVL *SRC)
     A                                      EDTCDE(4)
     A                                 12 39'to'
     A            W1TLVL    R        O 12 43REFFLD(W1TLVL *SRC)
     A                                      EDTCDE(4)
     A                                 13  4'Hand stack . . . . . . . . .'
     A            W1HSTK    R        O 13 34REFFLD(W1HSTK *SRC)
     A                                 16 16'You are about to delete all the sl-
     A                                      ots that match the'
     A                                 17 16'above criteria.'
     A                                 19 16'Please make sure you know what you-
     A                                       are doing.'
     A                                 21 31'Press F10 to continue.'
     A                                 23  2'F12=Cancel'
     A          R SCREEN3
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL222.03'
     A                                  1 30'Delete Group of Slots'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                  6 13'to cancel your request.'
     A                                  9 17'If you are sure, press F22 to dele-
     A                                      te the slots.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
    DA*                                     SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
