     A*%%TS  SD  20121005  162303  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  IT612FM    Same Slot Breakdown Label xref - change
     A*  20 February 2012
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    10/05/12  JCJ  6.40a
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20121005  162303  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'IT612.01'
     A                                  1 23'Same Slot Breakdown X-Ref Maintena-
     A                                      nce'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Item Number. . . . . .'
     A            W1ITEM        15A  B  8 28
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                  9  4'Print Combo Label. . .'
     A            W1COMBO        1A  B  9 28
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90
     AON91                                  DSPATR(PR)
     A N27N90                               DSPATR(HI)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
