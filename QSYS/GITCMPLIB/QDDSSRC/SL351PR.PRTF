     A****************************************************************
     A*
     A* SL351PR   Unverified zero slot with virtual slots info
     A* 01 September, 1999
     A* Hemant Kapadia
     A*
     A* 09/01/99  HNK  4.12
     A*  - Created
     A* 12/01/04  RH   5.00
     A*  - Changed to use SL351A work file and display zero slot with
     A*    related slots.
510a A* 11/29/07  RH   5.10a
     A*  - Enh: Add item# and description to Detail2 for zero slot.
640a A* 09/27/11  RH   6.40a
     A*  - ENH: Add license
650a A* 08/02/13  RH   6.50a
     A*  - ENH: Add extra line before Detail2.
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    56
     A                                      DFT('Unverified Zero Slot +
500 MA                                           with Related Slot Info')
500 DA*                                          with Virtual Slot Info')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL351')
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A*           TYPE          11A  O    62
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    15
     A                                      DFT('Status')
     A                                      UNDERLINE
     A                                    23
500 MA                                      DFT('Related Slot')
500 DA*                                     DFT('Virtual Slot')
     A                                      UNDERLINE
     A                                    37
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    54
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    86
     A                                      DFT('Available Qty')
     A                                      UNDERLINE
640aAA                                   102
640aMA                                      DFT('License        ')
640aAA                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail L1 line.
     A*
     A          R DETAIL2
650aDA*                                     SPACEB(001)
650aMA                                      SPACEB(002)
     A            OQDISP    R              1REFFLD(WKDISP SL351A)
     A            OQSTAT    R             16REFFLD(WKSTAT SL351A)
510a A            SLITEM    R        O    37REFFLD(SLITEM SLOT)
510a A            ITDESC    R        O    54REFFLD(ITDESC PIRITEM)
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
500 DA*           OQVDSP    R        O    23REFFLD(VSDISP SL351)
500 MA            OQRDSP    R        O    23REFFLD(WKRDSP SL351A)
     A            SLITEM    R        O    37REFFLD(SLITEM SLOT)
     A            ITDESC    R        O    54REFFLD(ITDESC PIRITEM)
     A            W1QTY         10A  O    86
640aMA            LCNS          15A  O   102
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('SL351: End of Report ')
     A*
