     A****************************************************************
     A*
     A* ER352PR   Transaction analysis by task for an empl(Summary)
     A* 28 July, 1999
     A* Hemant Kapadia
     A*
     A* Revisions:
     A* 07/28/99  HNK  4.12
     A* - Created
     A*
     A* 09/18/09  GJA  6.00
     A* - Cleaned up and revamped for suspended transactions.
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    56
     A                                   125
     A                                      'ER352'
     A                                      SPACEA(001)
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(001)
     A            TMERNG        22A  O    56
530aAA                                      SPACEA(003)
530aMA            MRGTXT        45A  O     2
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            EREMP#    R             +1REFFLD(EREMP# ER350)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A                                      SPACEA(002)
     A  92                                75
     A                                      '(Continued)'
     A*
     A                                     1'   Shift:'
     A            ERSHFT    R             +1REFFLD(ERSHFT ER350)
     A            WKSHFT        40        +1
     A                                      SPACEA(002)
     A*
     A                                     1'    Crew:'
     A            ERCREW    R             +1REFFLD(ERCREW ER350)
     A            WKCREW        40        +1
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    31'|'
     A                                    32'<-----------Transactions----------+
     A                                      ->'
     A                                    68'|'
     A                                    69'<--------------Pieces------------>'
     A                                   103'|'
     A                                      SPACEA(001)
     A                                    16'Direct'
     A                                    25'Indir'
     A                                    31'|'
     A                                    33'Number'
     A                                    42'Trans'
     A                                    49'Std Tran'
     A                                    59'%of Std'
     A                                    68'|'
     A                                    71'Number'
     A                                    79'Pieces'
     A                                    87'Std Pcs'
     A                                    96'%of Std'
     A                                      SPACEA(001)
     A*
     A                                     1'Dept '
     A                                      UNDERLINE
     A                                     8'Task'
     A                                      UNDERLINE
     A                                    19'Hrs'
     A                                      UNDERLINE
     A                                    27'Hrs'
     A                                      UNDERLINE
     A                                    31'|'
     A                                    33'of Trn'
     A                                      UNDERLINE
     A                                    41'Per Hr'
     A                                      UNDERLINE
     A                                    49'Per Hour'
     A                                      UNDERLINE
     A                                    59'Tran/Hr'
     A                                      UNDERLINE
     A                                    68'|'
     A                                    71'of PCS'
     A                                      UNDERLINE
     A                                    79'Per Hr'
     A                                      UNDERLINE
     A                                    87'Per Hr'
     A                                      UNDERLINE
     A                                    96'Pcs/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(002)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A*
     A* Print detail line
     A*
     A          R SUMAR1
     A                                      SPACEA(001)
     A            S#WHD          5         1
     A            S#TSK          6         8
     A            T#DHRS         5S 3     16EDTCDE(4)
     A            T#IHRS         5S 3     24EDTCDE(4)
     A*
     A                                    31'|'
     A            S#TRN          5S 0     34EDTCDE(4)
     A            T#TPH          7S 3     40EDTCDE(4)
     A            S#TPH          5S 0     51EDTCDE(4)
     A            TPHPCN         5S 2     60EDTCDE(4)
     A*
     A                                    68'|'
     A            S#PCS          5S 0     72EDTCDE(4)
     A            T#PPH          7S 3     78EDTCDE(4)
     A            S#PPH          5S 0     88EDTCDE(4)
     A            PPHPCN         5S 2     97EDTCDE(4)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          12A        1
     A            T#DTOT         7S 3     14EDTCDE(4)
     A            T#ITOT         7S 3     22EDTCDE(4)
     A            T#TTOT         7S 0     32EDTCDE(4)
     A            T#PTOT         7S 0     70EDTCDE(4)
     A                                      SPACEA(001)
     A                                     1'Average:'
     A            A#TCNT         5S 2     60EDTCDE(4)
     A            A#PCNT         5S 2     97EDTCDE(4)
     A                                      SPACEA(002)
     A*
     A* Print totals
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD          12A        1
     A            G#DTOT         7S 3     14EDTCDE(4)
     A            G#ITOT         7S 3     22EDTCDE(4)
     A            G#TTOT         7S 0     32EDTCDE(4)
     A            G#PTOT         7S 0     70EDTCDE(4)
     A                                      SPACEA(001)
     A                                     1'Average:'
     A            A#TCNT         5S 2     60EDTCDE(4)
     A            A#PCNT         5S 2     97EDTCDE(4)
     A                                      SPACEA(002)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* Draw 1 Line
     A*
     A          R DRAW1
     A                                      TEXT('DRAW ONE LINE')
     A                                      SPACEA(001)
     A                                     1'----------------------------------+
     A                                       ----------------------------------+
     A                                       ----------------------------------'
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'Note: Direct task includes idle ti+
     A                                       me.'
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER352: End of Report '
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'No records found for employee'
     A*
