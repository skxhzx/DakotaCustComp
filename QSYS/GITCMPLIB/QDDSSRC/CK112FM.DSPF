     A*%%TS  SD  20011102  103744  HEMANT      REL-V4R2M0  5769-PW1
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  CK112FM      RF Checker Detail
     A*  09 October, 2001
     A*  Hemant Kapadia
     A*
     A*  Notes:
     A*       8 X 20 Screen for hand held scanners
     A*
     A********************************************************************
     A*
     A*
     A*  Program Modification History
     A*
     A*    10/09/01  HNK  4.16
     A*      - Created
     A*
416A A*    04/29/02  HNK  4.16A
     A*      - REMOVE WOROW AND WOCOL.  ADD CHECK(FE)
     A*
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A                                      CA03
     A                                      CA12
     A                                      CA16
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
416ADA**                                    CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
416ADA**          WOROW          3S 0H
416ADA**          WOCOL          3S 0H
     A                                  1  2'CK112.01'
     A                                  1 12'Checker Detail'
     A                                      DSPATR(HI)
     A                                  2  1'Rte:'
     A            W1RTE          5A  O  2  6
     A                                  2 12'/'
     A            W1RTID         5A  O  2 14
     A                                  3  1'Plt:'
     A            W1PLT          2  0O  3  6EDTCDE(3)
     A                                  3  9'TtlQt:'
     A            W1TQTY         5Y 0O  3 16EDTCDE(3)
     A                                  4  1'Scanned Qty:'
     A            W1SQTY         5Y 0O  4 14EDTCDE(3)
     A                                  5  1'Scan label:'
416AMA            W1LBLA         7A  B  5 13CHECK(ER)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
416AAA                                      CHECK(FE)
     A                                  7  1'F12=Cncl F16=Bal F20=Comp'
     A                                      COLOR(BLU)
     A********************************************************************
     A          R SCREEN2
     A*%%TS  SD  20011102  103744  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'CK112.02'
     A                                  1 12'Qty Entry'
     A                                      DSPATR(HI)
     A                                  2  1'Rte:'
     A            W2RTE          5A  O  2  6
     A                                  2 12'/'
     A            W2RTID         5A  O  2 14
     A                                  3  1'Plt:'
     A            W2PLT          2Y 0O  3  6EDTCDE(3)
     A                                  3  9'ItmQt:'
     A            W2TQTY         5Y 0O  3 16EDTCDE(3)
     A                                  4  1'Itm:'
     A            W2ITM         40A  O  4  6
     A                                  5  1'Qty:'
     A            W2QTY          5Y 0B  5  6EDTCDE(4)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A            W2BULK         5   O  5 13
     A********************************************************************
     A          R SCREEN3
     A*%%TS  SD  20011010  115533  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CA12
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'CK112.03'
     A                                  1 12'Checker Close'
     A                                      DSPATR(HI)
     A                                  2  1'Rte:'
     A            W3RTE          5A  O  2  6
     A                                  2 12'/'
     A            W3RTID         7S 0O  2 14
     A                                  3  1'Plt:'
     A            W3PLT          2Y 0O  3  6EDTCDE(3)
     A                                  3  9'TtlQt:'
     A            W3TQTY         5Y 0O  3 16EDTCDE(3)
     A                                  4  1'Scanned Qty:'
     A            W3SQTY         5Y 0O  4 14EDTCDE(3)
     A                                  7  1'F12=Cncl F20=Comp'
     A                                      COLOR(BLU)
     A                                  5  1'If you are sure, press'
     A                                      DSPATR(HI)
     A                                  6  1'F20 again to complete.'
     A                                      DSPATR(HI)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
