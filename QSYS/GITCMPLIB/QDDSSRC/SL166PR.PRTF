     A****************************************************************
     A*
     A* SL166PR   RF Verify zero slot exception report.
     A* 02 April, 1997
     A* Roxanne Harrison
     A*
     A*  Revisions
     A*  08/31/00  RH  4.14a
     A*   - Add detail2 to list all slots associated with exception
     A*     slot.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A*                                   52
     A*                                     DFT('Verify Zero Slot Exception ')
     A*                                   79
     A*                                     DFT('Report ')
     A            RPTTTL        40        47
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL166')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                     3
     A                                      DFT('Slot')
     A                                    20
     A                                      DFT('User')
414aDA*                                   34
414aMA                                    35
     A                                      DFT('Date')
414a A                                    46
414a A                                      DFT('Item')
414a A                                    65
414a A                                      DFT('Description')
414a A                                   100
414a A                                      DFT('Entered date')
     A*%%***********************************************************************
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
414aDA*                                     SPACEB(001)
414aMA                                      SPACEB(002)
     A            VXDISP    R        O     3
     A                                      REFFLD(VXDISP VSLEXCP)
     A            VXUSER    R        O    +5
     A                                      REFFLD(VXUSER VSLEXCP)
     A            GENDTE         6S 0O    +5
     A                                      EDTCDE(Y)
414a A            WOITEM    R        O    46
414a A                                      REFFLD(SLITEM SLOT2)
414a A            WODESC    R        O    65
414a A                                      REFFLD(ITDESC PIRITEM)
414a A            WOENTD         6S 0O   100
414a A                                      EDTCDE(Y)
414a A*
414a A          R DETAIL2
414a A*%%***********************************************************************
414a A                                      SPACEB(001)
414a A            WODISP    R        O     6
414a A                                      REFFLD(VXDISP VSLEXCP)
414a A            WOITEM    R        O    46
414a A                                      REFFLD(SLITEM SLOT2)
414a A            WODESC    R        O    65
414a A                                      REFFLD(ITDESC PIRITEM)
414a A            WOENTD         6S 0O   100
414a A                                      EDTCDE(Y)
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total slots for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A          R TOTAL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL166: End of Report ')
     A*
