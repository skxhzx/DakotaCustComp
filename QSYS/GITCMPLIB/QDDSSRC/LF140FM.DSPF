     A*%%TS  SD  20000408  100809  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LF140FM    Label format set maintenance: Script - Display
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
414 AA*    04/06/00  DAS  4.14
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This is a special display file. The option input field has
     A*   been hidden, the option lines have been removed and the
     A*   subfile has been moved up with 4 new lines added.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20000407  165940  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'LF140.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Script Display'
     A                                      DSPATR(HI)
     A                                  1 27'Label Format Set Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000408  100809  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  H
     A            LFDSEQ    R        O  7  2REFFLD(LFDREC/LFDSEQ *LIBL/LBLFMTD)
     A                                      EDTCDE(4)
     A            LFDSTR    R        O  7  6REFFLD(LFDREC/LFDSTR *LIBL/LBLFMTD)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000408  095851  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  6  2'Seq'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6  6'Script                            -
     A                                                                         -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20000408  100809  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 54'Position to seq . . .'
     A            PTDMY2        78A  O  3  2
     A            PTINP1         3Y 0B  2 77EDTCDE(4)
     A                                  2  2'Label format set/type:'
     A            $KSET          6A  O  2 25
     A            $KFMT          8A  O  2 34
     A                                  2 32'/'
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
