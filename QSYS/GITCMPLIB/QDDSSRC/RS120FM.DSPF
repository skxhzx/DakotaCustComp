     A*%%TS  SD  20020216  173433  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RS120FM  Route compartment summary - Display
     A*  16 February 2002
     A*  Max Blacknik
     A*
     A*  Revisions:
     A*
416 AA*    02/16/02  MLB  4.16
     A*      - Created.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'RS120 '
     A                                  1 27'Route Compartment Summary'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  3  2
     A            WHCODE         3Y 0O  1 13EDTCDE(3)
     A                                  1 17'-'
     A            WHDESC        30A  O  1 19
     A                                  1  2'Warehouse:'
     A                                  2  2'Route    :'
     A            $KRTE          5A  O  2 13
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A  85                                  SFLNXTCHG
     A            RCCOMP    R        O 11  3REFFLD(RCREC/RCCOMP *LIBL/RTECSUM)
     A            RCDRDP    R        O 11  7REFFLD(RCREC/RCDRDP *LIBL/RTECSUM)
     A            RCPCKC    R        O 11 13REFFLD(RCREC/RCPCKC *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            RCPCKW    R        O 11 24REFFLD(RCREC/RCPCKW *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            RCPCKP    R        O 11 35REFFLD(RCREC/RCPCKP *LIBL/RTECSUM)
     A                                      EDTCDE(3)
     A            W1DRDP    R        O 11 44REFFLD(RCREC/RCDRDP *LIBL/RTECSUM)
     A            W1PCKC    R        O 11 50REFFLD(RCREC/RCPCKC *LIBL/RTECSUM)
     A                                      EDTCDE(4)
     A            W1PCKW    R        O 11 61REFFLD(RCREC/RCPCKW *LIBL/RTECSUM)
     A                                      EDTCDE(4)
     A            W1PCKP    R        O 11 72REFFLD(RCREC/RCPCKP *LIBL/RTECSUM)
     A                                      EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  8  7'------ Compartment Summary -------'
     A                                      DSPATR(HI)
     A                                  8 44'---------- Door Summary ----------'
     A                                      DSPATR(HI)
     A                                  9  7'Door'
     A                                      DSPATR(HI)
     A                                  9 44'Door'
     A                                      DSPATR(HI)
     A                                 10  2'Comp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 24'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 35'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 44'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 50'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 61'Weight    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 72'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A*N81                                  ERASE(VW2SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A*           RECNO2         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A*N81                                  ERASE(VW3SFL)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A*           RECNO3         4S 0H      SFLRCDNBR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A                                  1  2'Press Enter to return.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
