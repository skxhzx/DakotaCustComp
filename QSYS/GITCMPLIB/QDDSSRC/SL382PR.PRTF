     A****************************************************************
     A*
     A* SL382PR   Slots with negative quantity.
     A* 16 July, 2001
     A* Roxanne Harrison
     A*
     A* 07/16/01  RH   4.15
     A*  - Created
640a A* 09/27/11  RH   6.40a
     A*  - ENH: Add license, replace transfer and receive qty.
GLP AA*  10/02/13  RH   GREENLEAF PRODUCE
     A*    - CHANGED OBJECT NOT SOURCE TO LPI=4, CPI=5
     A*    - LINES PER PAGE =50, OVERFLOW=44
650a A* 05/05/14  RBD  6.50a
     A*  - ENH: Added slot type selection heading.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    53
     A                                      DFT('Slots with negative +
     A                                           quantity')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL382')
     A*
     A                                     3
     A                                      DFT('Department: ')
     A                                      SPACEB(002)
     A            WKWHDP         5   O    15
650aAA            WKTYPE        19   O    55
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A*           TYPE          11A  O    62
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    18
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    50
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    64
     A                                      DFT('Available')
     A                                      UNDERLINE
     A                                    76
     A                                      DFT('Stock ')
     A                                      UNDERLINE
     A                                    88
     A                                      DFT('Allocated')
     A                                      UNDERLINE
     A                                   100
     A                                      DFT('Pick ')
     A                                      UNDERLINE
     A                                   112
640aMA                                      DFT('License        ')
640aDA*                                     DFT('Transfer')
     A                                      UNDERLINE
640aDA*                                  124
640aDA*                                     DFT('Receive ')
640aDA*                                     UNDERLINE
     A*%%***********************************************************************
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Section: ')
     A            WOSTYP         5A  O    10
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A  92        WKITEM    R        O     1REFFLD(WKITEM SL381A)
     A  92        ITDESC    R        O    18REFFLD(ITDESC PIRITEM)
     A            WKDISP    R        O    50REFFLD(WKDISP SL381A)
     A            W1AVL         10A  O    64
     A            W1STK         10A  O    76
     A            W1ALC         10A  O    88
     A            W1PCK         10A  O   100
640aMA            LCNS          15A  O   112
640aDA*           W1TFR         10A  O   112
640aDA*           W1RCV         10A  O   124
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('SL382: End of Report ')
     A*
