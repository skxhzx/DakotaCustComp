     A*%%TS  SD  20060619  063132  ROBIN       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO110AFM   PO maint. (one whse) - display
     A*  08 OCT.1996
     A*  HEMANT KAPADIA
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
218 AA*    01/28/97  RH
     A*      - Change heading to P.O. Receiving.
     A*
412 AA*    09/16/99  HNK
     A*      - Display delivery date.
     A*
413 AA*    11/09/99  HNK
     A*      - Disable Display delivery date.
     A*
414 AA*    09/11/00  HNK
     A*      - Position cursor to PO instead of option line.
     A*
414aAA*    10/18/00  DAS  4.14a
     A*      - Changed warehouse position cursor indicator from 01
     A*        to 03.
     A*
416 AA*    10/26/01  MLB  4.16
     A*      - Added F7=Create Super P/O.
     A*      - Added F24=More keys support.
416aAA*    02/13/02  MLB  4.16a
     A*      - Added CA06 Fkey - Add P/O.
500aAA*    04/21/05  RH   5.00a
     A*      - Added F11 key=Received only or all
510A A*    06/19/06  RMC  5.10
     A*      - Enh: Create window format to force entry of destination
     A*        storage location if trying to export the PO.
     A*        PACKERS only.
610b A*    08/18/09  GJA  6.10b
     A*      - Enh: Added F23 for extended options
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      CF04
     A                                      CA09
     A                                      CF07
     A                                      CF11
     A                                      CF24
610bAA                                      CF23
     A          R TITLE
     A*%%TS  SD  19961008  140359  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'PO110A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
218 AA                                  1 35'PO Receiving'
218 DA*                                 1 28'PO Header File Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20000905  211716  KENNETH     REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            FLD002         1A  O  1  2
     A                                  2 47'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 71EDTCDE(4)
     A  21                                  DSPATR(RI)
414 DA* 01                                  DSPATR(PC)
414aAA  03                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3 47'Position to PO  . . . .'
     A            PTINP2         9A  B  3 71
414 AA                                      DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000820  220321  KENNETH     REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
414 DA* 20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PHPO      R        O 11  7REFFLD(PHREC/PHPO *LIBL/POHDR)
     A            PHVEN     R        O 11 18REFFLD(PHREC/PHVEN *LIBL/POHDR)
     A                                      EDTCDE(Z)
     A            W1VNAM        20A  O 11 30
     A            W1STAT        16A  O 11 65
     A            W1DATE         6A  O 11 51
     A                                 11 58'/'
     A            W1TIME         4A  O 11 60
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000820  214738  KENNETH     REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
416aDA*                                     CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10  7'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'Status          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Vendor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Name                '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51' Date  / Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
510A A          R WINDOW1
510A A*%%TS  SD  20060619  063132  ROBIN       REL-V5R3M0  5722-WDS
510A A                                      WINDOW(9 41 3 31 *NOMSGLIN)
510A A                                      WDWBORDER((*DSPATR RI))
510A A                                  1  3'Destination storage location'
510A A                                  2  3'must be entered. . .'
510A A            W1DEST         4Y 0B  2 24EDTCDE(4)
