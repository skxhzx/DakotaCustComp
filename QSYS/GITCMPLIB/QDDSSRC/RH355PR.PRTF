     A****************************************************************
     A*
     A* RH355PR   Daily Route Weight Report
     A* 23 February, 2013
     A* Ginger Andrews
     A*
     A* - Created 6.50
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3EDTCDE(Y)
     A            RPTTTL        60        13
     A*
     A                                    73'Page'
     A            PAGE           4S 0O    78EDTCDE(Z)
     A*
     A            TIME           6S 0O     3EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O    25
     A                                    77'RH355'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(001)
     A            TMERNG        22A  O    25
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'Route'
     A                                      UNDERLINE
     A                                     7'RouteID'
     A                                      UNDERLINE
     A                                    15'Truck'
     A                                      UNDERLINE
     A                                    27'Weight'
     A                                      UNDERLINE
     A                                    37'Plt'
     A                                      UNDERLINE
     A                                    42'Printed'
     A                                      UNDERLINE
     A                                    62'Exported'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            RHRTE          5         1
     A            RHRTID         5         8
     A            RHTRUK        10        15
     A            WKWGT          7S 2     27EDTCDE(3)
     A            WKPLT          2S 0     37EDTCDE(3)
     A            RHPRTD         8S 0     42EDTWRD('    /  /  ')
     A            RHPRTT         6S 0     53EDTWRD('  :  :  ')
     A            RHEXPD         8S 0     62EDTWRD('    /  /  ')
     A            RHEXPT         6S 0     73EDTWRD('  :  :  ')
     A*
     A          R TOTAL1
     A                                      SPACEA(001)
     A            TLWGT          7S 2     27EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1' '
     A*
     A* Draw 1 Line
     A*
     A          R DRAW1
     A                                      TEXT('DRAW ONE LINE')
     A                                      SPACEA(001)
     A                                     1'-----------------------------------
     A                                      ------------------------------'
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1'RH355: End of Report '
     A*
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1'No records found'
     A*
