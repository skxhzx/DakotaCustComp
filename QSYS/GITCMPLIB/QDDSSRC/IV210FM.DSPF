     A*%%TS  SD  19960215  133618  ROCKY       REL-V3R1M0  5763-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   845 W. Roosevelt Rd.  #8 - 51
     A*   West Chiago, Il. 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  IV210FM   Inventory page maintenance.
     A*
     A*
     A*  Revisions
510aAF*    07/26/06  RH   5.10a
     F*      - ENH:Add dept and slot.
510bAF*    06/08/07  RH   5.10b
     F*      - Enh:Add F7 to call sheet lookup
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960215  133618  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
510b A                                      CF07
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IV210.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Inventory Sheet Maintenance'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt F7=Lookup'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  7 28
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  9  4'Sheet  . . . . . . . .'
     A            W1PAGE         5Y 0B  9 28
     A  22                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                      EDTCDE(4)
510a A                                 11  4'OR'
510a A                                 13  4'Department . . . . . .'
510a A            W1WHDP         5A  B 13 28
510a A N24                                  DSPATR(HI)
510a A  24                                  DSPATR(RI)
510a A  04                                  DSPATR(PC)
510a A                                 15  4'Slot . . . . . . . . .'
510a A            W1DISP        12A  B 15 28CHECK(ER)
510a A N23                                  DSPATR(HI)
510a A  23                                  DSPATR(RI)
510a A  03                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
