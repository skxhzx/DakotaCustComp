     A*%%TS  SD  20090819  091437  BFC         REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Multilingual Version
     A*   Every screen has a corresponding Spanish screen.
     A*   All Spanish screens are at the end.
     A*   Any changes must be made to both screens.
     A*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A********************************************************************
     A*
     A* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A* NOTE: ADDED 3RD SET OF SCREENS FOR 16 X 20 DISPLAY.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS IF NECESSARY.
     A*
     A*
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A**************************************************************************
     A*  IT253FM   AUTO Inventory transfer -  RF Mini Screen
     A*  Revisions:
     A*
     A*    05/09/17  LMC  7.20
     A*     - Created from a copy of IT25206.
     A*
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000204  175958  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT253.01'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W1ITEM        55A  O  2  8
     A*                                     DSPATR(PR)
     A            W1LBLS        12A  O  3  2DSPATR(HI)
     A            W1ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Aval:'
     A            W1CURQ        30A  O  4  8
     A            W1PEND         4A  O  4 40
     A  42                                  DSPATR(RI)
     A                                  5  2'Qty:'
     A            W1QT01         5Y 0B  5  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A            W1UM1          5A  O  5 14
     A  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B  5 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O  5 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 36
     A  93                                  DSPATR(ND)
     A                                  6  2'Item:'
     A            W1ITM         15   B  6  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A            W1PSLT        12A  O  6 25DSPATR(HI)
     A                                  7  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.02'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W2ITEM        55A  O  2  8
     A            W2LBLS        12A  O  3  2DSPATR(HI)
     A            W2ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Qty:'
     A            W2CURQ        30A  O  4  8
     A                                  5  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B  5 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                  5 16'Wt:'
     A N60                                  DSPATR(ND)
     A            W2CWT          9Y 2B  5 20DSPATR(UL)
     A N60                                  DSPATR(ND)
     A N60                                  DSPATR(PR)
     A  02 60                               DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  2'To slot:'
     A            W2DISP        12A  B  6 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A            W2PSLT        12A  O  6 24DSPATR(HI)
     A                                  7  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.03'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  1 26'- Verify Slot'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W3ITEM        55A  O  2  8
     A            W3LBLS        12A  O  3  2DSPATR(HI)
     A            W3ITMS        55A  O  3 15DSPATR(HI)
     A                                  5  2'Slot phys. empty:'
     A            W3ZERO         1A  B  5 20CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  5 23'(Y/N)'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A* Lot/License plate scan window.
     A          R WDWLLP
     A                                      OVERLAY
     A                                      WINDOW(1 2 6 20)
     A                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
     A                                      ENTER)
     A                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
     A                                      l') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WWTXT         18   O  2  1
     A            WWLLP#        15   B  3  1DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A*  Window message subfile formats.
     A          R MSGRECW                   SFL
     A                                      SFLMSGRCD(5)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Window Error message subfile control record
     A*
     A          R MSGCTLW                   SFLCTL(MSGRECW)
     A                                      WINDOW(WDWLLP)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLRW
     A                                      WINDOW(WDWLLP)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(5)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRBTM
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(16)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* 16-line screens
     A*
     A          R SCREEN61
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.61'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W61ITM        65A  O  3  8
     A            W61DSC        30A  O  4  8
     A            W1LBLS        12A  O  5  2DSPATR(HI)
     A            W1ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
     A            W1PEND         4A  O  9  8
     A N42                                  DSPATR(ND)
     A  42                                  DSPATR(RI)
     A                                 10  2'Qty:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A            W1UM1          5A  O 10 14
     A  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 10 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 10 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 10 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 10 36
     A  93                                  DSPATR(ND)
     A                                 12  2'Item:'
     A            W1ITM         15   B 12  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN62
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.62'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W62ITM        65A  O  3  8
     A            W62DSC        30A  O  4  8
     A            W2LBLS        12A  O  5  2DSPATR(HI)
     A            W2ITMS        55A  O  5 15DSPATR(HI)
     A            W2PSLT        12A  O  6  2DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Qty:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                 12  2'Wt:'
     A N60                                  DSPATR(ND)
     A            W2CWT          9Y 2B 12  6DSPATR(UL)
     A N60                                  DSPATR(ND)
     A N60                                  DSPATR(PR)
     A  02 60                               DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2'To slot:'
     A            W2DISP        12A  B 13 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 15  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN63
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.63'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  2 13'Verify Slot'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W63ITM        65A  O  3  8
     A            W63DSC        30A  O  4  8
     A            W3LBLS        12A  O  5  2DSPATR(HI)
     A            W3ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  9  2'Slot phys. empty:'
     A            W3ZERO         1A  B  9 20CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A            WOYN           5A  O  9 23
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R SCREEN71
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.71'
     A                                  1 11'Inv Transfer'
     A                                      DSPATR(HI)
     A            W61ITM        65A  O  3  1
     A            W71DS1        15A  O  4  1
     A            W71DS2        15A  O  5  1
     A            W1LBLS        12A  O  6  2DSPATR(HI)
     A            W1ITMS        55A  O  6 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Qty:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            W1UM1          5A  O 10 14
     A            W1QT02         3Y 0B 11  8EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 11 14
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 12  8EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 12 14
     A  93                                  DSPATR(ND)
     A                                 13  1'Item'
     A            W1ITM         15   B 13  6
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN72
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.72'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A            W62ITM        65A  O  3  1
     A            W72DS1        15A  O  4  1
     A            W72DS2        15A  O  5  1
     A            W2LBLS        12A  O  6  2DSPATR(HI)
     A            W2ITMS        55A  O  6 15DSPATR(HI)
     A            W2PSLT        12A  O  7  2DSPATR(HI)
     A                                  8  2'From:'
     A            WOFROM        12A  O  8  8
     A                                  9  2'Aval:'
     A            W1CURQ        30A  O  9  8
     A                                 10  2'Qty:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                 12  2'Wt:'
     A N60                                  DSPATR(ND)
     A            W2CWT          9Y 2B 12  6DSPATR(UL)
     A N60                                  DSPATR(ND)
     A N60                                  DSPATR(PR)
     A  02 60                               DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  1'To slot'
     A            W2DISP        12A  B 13  9CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 15  2'F9=OSS F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN73
     A*%%TS  SD  20090819  091437  BFC         REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.73'
     A                                  1 12'Inv Transfer'
     A                                      DSPATR(HI)
     A                                  2  8'Verify Slot'
     A                                      DSPATR(HI)
     A*                                 3  2'Item:'
     A            W63ITM        65A  O  3  1
     A*           W63DSC        30A  O  4  8
     A            W73DS1        15A  O  4  1
     A            W73DS2        15A  O  5  1
     A            W3LBLS        12A  O  6  2DSPATR(HI)
     A            W3ITMS        55A  O  6 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  9  2'Slot phys empty:'
     A            W3ZERO         1A  B  9 19CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A            WOYN           5A  O 10  2
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 12  2' '
     A*
     A*****************************************************************
     A*
     A* Error Message Subfile Records for 16-line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A*
     A*****************************************************************
     A* Spanish Screens
     A*****************************************************************
      *----------------------------------------------------------------
     A          R SCRN1SP
     A*%%TS  SD  20000204  175958  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT253.01'
     A                                  1 12'Trans. de Inv.'
     A                                      DSPATR(HI)
     A                                  2  2'Prdc:'
     A            W1ITEM        55A  O  2  8
     A            W1LBLS        12A  O  3  2DSPATR(HI)
     A            W1ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Disp:'
     A            W1CURQ        30A  O  4  8
     A                                  5  2'Ctd:'
     A            W1QT01         5Y 0B  5  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A            W1UM1          5A  O  5 14
     A                                      DSPATR(ND)
     A            W1QT02         3Y 0B  5 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O  5 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 36
     A  93                                  DSPATR(ND)
     A                                  6  2'Prdc:'
     A            W1ITM         15   B  6  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                  7  2'F12=Cancelar F20=Aceptar aviso'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SCRN2SP
     A*%%TS  SD  20090522  115344  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.02'
     A                                  1 12'Trans. de Inv.'
     A                                      DSPATR(HI)
     A                                  2  2'Prdc:'
     A            W2ITEM        55A  O  2  8
     A            W2LBLS        12A  O  3  2DSPATR(HI)
     A            W2ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Ctd:'
     A            W2CURQ        30A  O  4  8
     A                                  5  2'Aju codigo:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B  5 14DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                  5 16'Wt:'
     A N60                                  DSPATR(ND)
     A            W2CWT          9Y 2B  5 20DSPATR(UL)
     A N60                                  DSPATR(ND)
     A N60                                  DSPATR(PR)
     A  02 60                               DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  2'A slot :'
     A            W2DISP        12A  B  6 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  7  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R SCRN3SP
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.03'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  1 26'-Verificar Slot'
     A                                      DSPATR(HI)
     A                                  2  2'Prdc:'
     A            W3ITEM        55A  O  2  8
     A            W3LBLS        12A  O  3  2DSPATR(HI)
     A            W3ITMS        55A  O  3 15DSPATR(HI)
     A                                  5  2'Slot vacio fsico:'
     A            W3ZERO         1A  B  5 20CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  5 23'(Y/N)'
     A                                  7  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* 16-line screens
     A*
      *----------------------------------------------------------------
     A          R SCRN61SP
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.61'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Prdc:'
     A            W61ITM        65A  O  3  8
     A            W61DSC        30A  O  4  8
     A            W1LBLS        12A  O  5  2DSPATR(HI)
     A            W1ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'De  :'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Disp:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Ctd:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  41                                  DSPATR(ND)
     A            W1UM1          5A  O 10 14
     A                                      DSPATR(ND)
     A            W1QT02         3Y 0B 10 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 10 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 10 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 10 36
     A  93                                  DSPATR(ND)
     A                                 12  2'Prdc:'
     A            W1ITM         15   B 12  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancelar F20=Aceptar aviso'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A          R SCRN62SP
     A*%%TS  SD  20090522  115344  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT253.62'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Prdc:'
     A            W62ITM        65A  O  3  8
     A            W62DSC        30A  O  4  8
     A            W2LBLS        12A  O  5  2DSPATR(HI)
     A            W2ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'De  :'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Disp:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Ctd:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Aju codigo:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 14DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                 12  2'Wt:'
     A N60                                  DSPATR(ND)
     A            W2CWT          9Y 2B 12  6DSPATR(UL)
     A N60                                  DSPATR(ND)
     A N60                                  DSPATR(PR)
     A  02 60                               DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2'A slot :'
     A            W2DISP        12A  B 13 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 15  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R SCRN63SP
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT253.63'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  2 13'Verificar slot'
     A                                      DSPATR(HI)
     A                                  3  2'Prdc:'
     A            W63ITM        65A  O  3  8
     A            W63DSC        30A  O  4  8
     A            W3LBLS        12A  O  5  2DSPATR(HI)
     A            W3ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'De  :'
     A            WOFROM        12A  O  7  8
     A*                                 9  2'Slot vacio fisicamente. .'
     A*           W3ZERO         1A  B  9 28CHECK(ER)
     A                                  9  2'Slot vacio fsico:'
     A            W3ZERO         1A  B  9 20CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A            WOYN           5A  O  9 23
     A                                 15  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R WEXTSTG
     A*%%TS  SD  20090522  114956  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(1 2 6 26)
     A                                      WDWTITLE((*TEXT 'Off Site  Storage'-
     A                                      ) *LEFT)
     A                                  2  2'Storage'
     A            W9STG          1A  B  2 15
     A                                  3  2'Printer'
     A            W9DEV         10A  B  3 15DSPATR(RI)
     A                                  5  2'F12=Cancel'
     A                                      DSPATR(HI)
     A          R WDUMMY                     ASSUME
     A                                  1  2' '
