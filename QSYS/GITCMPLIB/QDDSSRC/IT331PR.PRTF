     A****************************************************************
     A*
     A* IT331PR   Warehouse book by slot
     A* 26 August, 1994
     A* Hemant Kapadia
     A*
     A****************************************************************
     A* Revisions:
     A*
417 AA* 03/22/04  MLB  4.17
     A*   - Added 3 item type columns to report.
500 AA* 03/21/05  RH   5.00
     A*   - ENH: 417 Change missed in update, add as 5.00
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    55
     A                                      DFT('Warehouse Book by Slot')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT331')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A            TYPE          11A  O    63
     A  91                                75
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Slot')
417 AA                                    +9
417 AA                                      DFT('Typ')
417 DA*                                  +13
417 MA                                    +1
     A                                      DFT('Item')
     A                                   +10
     A                                      DFT('Quantity')
     A                                    46
     A                                      DFT('Slot')
417 AA                                    59
417 AA                                      DFT('Typ')
     A                                    63
     A                                      DFT('Item')
     A                                    77
     A                                      DFT('Quantity')
     A                                    91
     A                                      DFT('Slot')
417 AA                                   104
417 AA                                      DFT('Typ')
     A                                   108
     A                                      DFT('Item')
     A                                   122
     A                                      DFT('Quantity')
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1SLOT        12A  O     1
417 AA            C1TYPE         1A  O    15
     A            C1ITEM        15A  O    18
     A            C1QTY         10A  O    32
     A*
     A            C2SLOT        12A  O    46
417 AA            C2TYPE         1A  O    60
     A            C2ITEM        15A  O    63
     A            C2QTY         10A  O    77
     A*
     A            C3SLOT        12A  O    91
417 AA            C3TYPE         1A  O   105
     A            C3ITEM        15A  O   108
     A            C3QTY         10A  O   122
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT331: End of Report ')
     A*
