     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. ZH14001 COPY AND NAME ZH14002
     A*----------------------------------------------------------------
     A*
     A*  ZH140FM  Zone maintenance
     A*  07 August 2006
     A*  Jeffrey Jahnke
     A*
     A*  This was a copy of ZH120FM
     A*
     A*  Revisions:
     A*
530 AA*    11/09/09  JCJ  5.30
     A*      - Moved POSTNREC/OPTREC into DSPSFC.
     A*        Re: Webfacing had problem with double read.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'ZH140 '
     A                                  1 33'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20060809  143458  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 44'Position to zone  . . . .'
     A            PTINP3         6A  B  1 70
     A            W1TRUK        40A  O  2  2
     A                                  3  2'Route:'
     A            $KRTE          5A  O  3  9
     A                                  3 16'Route Id:'
     A            $KRTID         5A  O  3 26
     A                                  3 33'Truck:'
     A            $KTRUK        10A  O  3 40
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20060807  122245  JEFF        REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ZHZONE    R        O 11  7REFFLD(ZHREC/ZHZONE *LIBL/ZONEH)
     A            ZHDESC    R        O 11 15REFFLD(ZHREC/ZHDESC *LIBL/ZONEH)
     A            ZHCOMP    R        O 11 52REFFLD(ZHREC/ZHCOMP *LIBL/ZONEH)
     A            ZHPZON    R        O 11 60REFFLD(ZHREC/ZHPZON *LIBL/ZONEH)
     A            WZTYPE         6   O 11 68
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060807  122245  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  3 44'Position to zone  . . . .'
     A            PTINP3         6A  B  3 70
     A            W1TRUK        40A  O  4  2
     A                                  5  2'Route:'
     A            $KRTE          5A  O  5  9
     A                                  5 16'Route Id:'
     A            $KRTID         5A  O  5 26
     A                                  5 33'Truck:'
     A            $KTRUK        10A  O  5 40
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A            FLD001         1A  O  9  3
     A                                  9 47'Truck'
     A                                      DSPATR(HI)
     A                                  9 60'Parent'
     A                                      DSPATR(HI)
     A                                  9 68'Zone'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Zone  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 15'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Compartment'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 60'Zone  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Type  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
