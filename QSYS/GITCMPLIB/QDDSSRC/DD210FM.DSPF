     A*%%TS  SD  19980120  130432  ROCKY       REL-V3R1M0  5763-PW1
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  DD210FM      Create Item Tie/Highs - Options
     A*  20 January 1998
     A*  Roxanne Harrison
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
500 AA*    - 08/31/04  MLB  5.00
     A*       - Enh: Add aisle selection to tie/high process.
     A*         Move lines 11 to 19 down 1 line.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19980120  130432  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'DD210.01'
     A                                  1 30'Create Item Tie/Highs'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 54'1-999'
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 54'Name, *ALL'
     A                                 10  4'Section  . . . . . . . . . .'
     A            W1STYP         1A  B 10 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 54'F=Fixed, P=Pir, B=Both'
500 AA                                 11  4'Aisle  . . . . . . . . . . .'
500 AA            W1AISL         4A  B 11 34
500 AA N31N90                               DSPATR(HI)
500 AA  31                                  DSPATR(RI)
500 AA N31N90                               DSPATR(UL)
500 AA  11                                  DSPATR(PC)
500 AA  90                                  DSPATR(PR)
500 AA                                 11 54'Aisle, *ALL'
500 MA                                 12  4'Item number  . . . . . . . .'
500 MA            W1ITEM        15A  B 12 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 12 54'Name, *ALL'
500 MA                                 13  4'New definitions only . . . .'
500 MA            W1NEWD         1A  B 13 34
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 13 54'(Y=Yes, N=No)'
500 MA                                 15  4'  Slot types defined . . . .'
500 MA            W1TDEF         1A  B 15 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 15 54'(Y=Yes, N=No)'
500 MA                                 17  4'  Slot types item occupies .'
500 MA            W1SLOT         1A  B 17 34
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 17 54'(Y=Yes, N=No)'
500 MA                                 18  4'Clear work file  . . . . . .'
500 MA            W1CLRF         1A  B 18 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 18 54'(Y=Yes, N=No)'
500 MA                                 19  4'Place on job queue ? . . . .'
500 MA            W1JOBQ         1A  B 19 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 MA                                 19 54'(Y=Yes, N=No)'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
500 MA                                 14  4'Create from:'
500 MA                                 16  4'-OR-'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
