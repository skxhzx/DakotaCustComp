     A*%%TS  SD  20200114  125521  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  GS122FM   GS1 Weight Entry. - Display
     A*  10 January 2020
     A*  Lynn McMahhon
     A*
     A*  Revisions
     A*
740 AA*  01/10/20  LMC  7.40
     A*    - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A N92                                  CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20200114  125521  LYNNM       REL-V7R2M0  5770-WDS
     A  85                                  SFLNXTCHG
     A            W1ENT         50A  H
     A            W1SEQ          4Y 0O  8  2EDTCDE(3)
     A            W1UCI         48A  O  8  8
     A            W1WGTLBS       6Y 0B  8 58EDTCDE(Z)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20200114  124916  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0014)
     A                                      CF07
     A                                      CF20
     A                                      CF10
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'GS122.03'
     A                                  1 24'GS1 Weight Entry Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3  2'Item:'
     A            $PITEM        15A  O  3  8
     A            WODESC        15A  O  3 24
     A                                  3 62'P/O:'
     A            WOPO           9A  O  3 68
     A                                  4 26'Weights Entered:'
     A            WOCW#          7Y 0O  4 50EDTCDE(3)
     A                                  4 60'Total:'
     A            WOTCW          9Y 2O  4 68EDTCDE(3)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  6'Weight'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            W1KGLB         3A  O  7  1
     A            W1WGT          7A  B  7  6CHECK(ER)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 14'Assume 2 decimals.'
     A                                      COLOR(BLU)
     A            DATETP         9A  O  7 35
     A            DATEIN         6Y 0B  7 45EDTCDE(Z)
     A                                  7 54'How Many'
     A            GS1MUL         5Y 0B  7 64EDTCDE(Z)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79A  O  1  2COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
