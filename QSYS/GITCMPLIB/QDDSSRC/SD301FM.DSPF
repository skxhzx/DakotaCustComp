     A*%%TS  SD  20061204  102110  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  SD301FM   Print Check Digit Exceptions - Options
     A*  22 November  2006
     A*  Randy Haselhorst
     A*
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
510aAA*    11/22/06  JCJ  5.10a
     A*      - Enh: Added date prompts
     A*
     A*----------------------------------------------------------------
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20061204  102110  JEFF        REL-V5R3M0  5722-WDS
     A                                      CF04
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(23)
     A                                      OVERLAY
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A                                  1  2'SD301.01'
     A                                  1 27'Print Check Digit Exceptions'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 53'1-999'
     A*
     A                                  8  4'Department . . . . . . . . .'
     A            W1DEPT         5A  B  8 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 53'Dept, *ALL'
     A*
     A                                  9  4'Employee . . . . . . . . . .'
     A            W1EMP          5Y 0B  9 34EDTCDE(Z)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 41'or'
     A                                      DSPATR(HI)
     A            W1ALL          4A  B  9 44
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  9 53'Emp, *ALL'
     A                                 10  4'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B 10 34EDTCDE(4)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 41'to'
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0B 10 44EDTCDE(4)
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 53'Date (MMDDYY)'
     A                                 12  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 12 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 53'Y=Yes, N=No'
     A                                 13  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 13 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 53'Name, *SYSVAL, *JOB'
     A                                 14  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 14 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 53'Character value, *STD'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
