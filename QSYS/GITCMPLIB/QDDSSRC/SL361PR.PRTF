     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL361PR    Slot Cube for warehouse - Print
     A*
     A*  Revisions
     A*
     A*    07/10/01  HNK  4.15
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL361')
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A            TYPE           5A  O    63
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    17
     A                                      DFT('Slot')
     A                                    24
     A                                      DFT('Pallet')
     A                                    63
     A                                      DFT('Pallet')
     A*
     A                                     1
     A                                      DFT('Dept.')
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                     8
     A                                      DFT('Section')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Type')
     A                                      UNDERLINE
     A                                    24
     A                                      DFT('Type')
     A                                      UNDERLINE
     A                                    33
     A                                      DFT('Length')
     A                                      UNDERLINE
     A                                    44
     A                                      DFT('Width')
     A                                      UNDERLINE
     A                                    54
     A                                      DFT('Height')
     A                                      UNDERLINE
     A                                    63
     A                                      DFT('Position')
     A                                      UNDERLINE
     A                                    74
     A                                      DFT('Cube')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            SLWHDP    R        O     1
     A                                      REFFLD(SLWHDP SLOT)
     A            SLSTYP    R        O    11
     A                                      REFFLD(SLSTYP SLOT)
     A            SDCODE    R        O    18
     A                                      REFFLD(SDCODE SLOTDEF)
     A            SDPAL     R        O    24
     A                                      REFFLD(SDPAL  SLOTDEF)
     A            SDLEN     R        O    33
     A                                      REFFLD(SDLEN  SLOTDEF)
     A                                      EDTCDE(3)
     A            SDWDTH    R        O    44
     A                                      REFFLD(SDWDTH SLOTDEF)
     A                                      EDTCDE(3)
     A            SDHGT     R        O    54
     A                                      REFFLD(SDHGT  SLOTDEF)
     A                                      EDTCDE(3)
     A            SDPOS     R        O    65
     A                                      REFFLD(SDPOS  SLOTDEF)
     A                                      EDTCDE(3)
     A            T#CUBE        11S 3O    70
     A                                      EDTCDE(J)
     A*
     A* Print totals
     A*
     A**        R TOTAL1
     A**                                    SPACEB(001)
     A**          T#CUBE         9S 3O    62
     A**                                    EDTCDE(J)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD          30A       38
     A            T#CUBE        11S 3O    70
     A                                      EDTCDE(J)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL361: End of Report ')
     A*
