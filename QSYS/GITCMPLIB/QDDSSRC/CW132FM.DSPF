     A*%%TS  SD  20061103  160926  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CW132FM   UCI Catch Weight Maint. - Display
     A*  14 JUNE 2006
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
510 AA*  06/14/06  JCJ  5.10
     A*    - Created.
     A*
510bAA*  10/05/06  RAH  5.10b
     A*    - Added support for 16x20 screens
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A N92                                  CA12
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DETAIL1                   SFL
     A*%%TS  SD  20060628  071125  JEFF        REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1SEQ          4Y 0O  9  2EDTCDE(3)
     A            W1UCI2        10A  O  9  7
     A            W1CWGT         7Y 0B  9 18EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R HEAD1                     SFLCTL(DETAIL1)
     A*%%TS  SD  20060707  113720  JEFF        REL-V5R3M0  5722-WDS
     A  *DS3                                SFLLIN(0003)
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0040)
     A                                      OVERLAY
     A  98                                  ALARM
     A                                      CF20
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'CW132.03'
     A                                  1 24'P/O Catch Weight Entry Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 42'License:'
     A            $PLCNS        15A  O  2 51
     A                                  3  2'Item:'
     A            $PITEM        15A  O  3  8
     A            WODESC        20A  O  3 24
     A                                  3 46'Qty:'
     A            WOPQTY         5Y 0O  3 51EDTCDE(3)
     A                                  3 63'P/O:'
     A            WOPO           9A  O  3 68
     A                                  4 28'Catch Weights Entered:'
     A            WOCW#          7Y 0O  4 51EDTCDE(3)
     A                                  4 61'Total:'
     A            WOTCW          9Y 2O  4 68EDTCDE(3)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  7'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 18'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 33'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 44'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 59'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 70'Catch Wt.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  2'UCI:'
     A            W1UCI         10A  B  7  7CHECK(ER)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A N17
     AO 16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 19'Wgt:'
     A            W1WGT         50A  B  7 24CHECK(ER)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 24'Assume 2 decimals'
     A                                      COLOR(BLU)
     A*
     A*  Clear screen
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* Command line
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A                                  1  2'F5=Refresh'
     A                                      COLOR(BLU)
     A                                  1 14'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 26'F20=Accept warning'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
510b A*   Below are the 16 line screens
     A*****************************************************************
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R D16AIL1                   SFL
     A*%%TS  SD  20061005  123649  RANDY       REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            W1SEQ          4  0H
     A            W1UCI2        10A  O 10  3
     A            W1CWGT         7Y 0B 10 14EDTCDE(3)
     A  39                                  DSPATR(RI)
     A N39N90                               DSPATR(UL)
     A  19                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R H16D1                     SFLCTL(D16AIL1)
     A*%%TS  SD  20061103  160926  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0162)
     A                                      SFLPAG(0005)
     A                                      CF20
     A  98                                  ALARM
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  1  2'CW132.03'
     A                                  1 11'PO CW Ent'
     A                                      DSPATR(HI)
     A            $PWHSE         3Y 0O  2  2EDTCDE(4)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  3  2'Item:'
     A                                      DSPATR(HI)
     A            $PITEM        15A  O  3  8
     A            WODESC        20A  O  3 24
     A                                  4  2'Qty:'
     A                                      DSPATR(HI)
     A            WOPQTY         5Y 0O  4  7EDTCDE(3)
     A                                  4 13'P/O:'
     A                                      DSPATR(HI)
     A            WOPO           9A  O  4 18
     A                                  5  2'Tot:'
     A                                      DSPATR(HI)
     A            WOTCW          9Y 2O  5  7EDTCDE(3)
     A                                  5 18'Lcns:'
     A                                      DSPATR(HI)
     A            $PLCNS        15A  O  5 24
     A                                  6  2'CW Ent:'
     A                                      DSPATR(HI)
     A            WOCW#          7Y 0O  6 10EDTCDE(3)
     A                                  7  2'Type changes, Enter'
     A                                      COLOR(BLU)
     A                                  8  3'UCI Code  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 14'Catch Wt.'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            W1UCI         10A  B  9  3CHECK(ER)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A N17
     AO 16                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1WGT         50A  B  9 14CHECK(ER)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* Command line
     A*
     A          R C16REC
     A*%%TS  SD  20061005  123649  RANDY       REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A                                  1  2'F12=Cnl'
     A                                      COLOR(BLU)
     A                                  1 10'F20=Acpt Wrn'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R M16CLR
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
