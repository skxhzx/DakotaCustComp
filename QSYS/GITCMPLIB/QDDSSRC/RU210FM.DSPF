     A*%%TS  SD  20040525  124930  BFC2        REL-V5R2M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RU210FM      RF - Runner Flag Assignment
     A*  30 April 2004
     A*  Max Blacknik
     A*
     A********************************************************************
     A*  Revisions:
     A*
417 AA*  04/30/04  MLB  4.17
     A*    - Created.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20040525  124930  BFC2        REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.01'
     A                                  1 11'Flag Assignment'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Scan emp#:'
     A            W1EMP#         5Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  5  3'Printer'
     A            W1DEV         10A  B  5 11
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  3'Form'
     A            W1FORM        10A  B  6 11
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.02'
     A                                  1 11'Flag Assignment'
     A                                      DSPATR(HI)
     A                                  3  3'Emp:'
     A            W2EMP#    R        O  3  8REFFLD(USEMP# PIRUSER)
     A                                      EDTCDE(4)
     A            W2LNAM    R        O  3 14REFFLD(USLNAM PIRUSER)
     A                                  4  3'Last lbl#:'
     A            W2LLBL         7Y 0O  4 14
     A                                      EDTCDE(3)
     A                                  5  3'Nbr scanned:'
     A            W2NRSN         5Y 0O  5 16EDTCDE(3)
     A                                  6  3'Scan lbl#:'
     A            W2LBL#         7Y 0B  6 14CHECK(ER)
     A                                      EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.03'
     A                                  1 11'Flag Assignment'
     A                                  3  2'Lbl#'
     A            W3LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A            WOITEM        50A  O  4  2
     A                                  5  1'Qty shrt/out'
     A            W3QYSO    R        B  5 14REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(3)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O  5 20REFFLD(LBUCOD LABEL)
     A                                  7  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                  7  9'Enter=Continue'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN4
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.04'
     A                                  1 11'Flag Assignment'
     A                                  3  2'Press ENTER'
     A                                  4  2'to create'
     A                                  5  2'Runner Pick List.'
     A                                  7  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*  16 line screens.
     A*
     A          R SCREEN61
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU210.01'
     A                                  1 11'Flag Assignment'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Scan emp#:'
     A            W1EMP#         5Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  6  3'Printer'
     A            W1DEV         10   B  6 11
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  3'Form'
     A            W1FORM        10   B  7 11
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15 11'F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN62
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU210.02'
     A                                  1 11'Flag Assignment'
     A                                      DSPATR(HI)
     A                                  3  3'Emp:'
     A            W2EMP#    R        O  3  8REFFLD(USEMP# PIRUSER)
     A                                      EDTCDE(4)
     A            W2LNAM    R        O  3 14REFFLD(USLNAM PIRUSER)
     A                                  5  3'Last lbl#:'
     A            W2LLBL         7Y 0O  5 14
     A                                      EDTCDE(3)
     A                                  7  3'Nbr scanned:'
     A            W2NRSN         5Y 0O  7 16EDTCDE(3)
     A                                  9  3'Scan lbl#:'
     A            W2LBL#         7Y 0B  9 14CHECK(ER)
     A                                      EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15  9'Enter=Continue'
     A                                      COLOR(BLU)
     A          R SCREEN63
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.03'
     A                                  1 11'Flag Assignment'
     A                                  3  2'Lbl#'
     A            W3LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(3)
     A            WOITEM        50A  O  5  2
     A                                  7  1'Qty shrt/out'
     A            W3QYSO    R        B  7 14REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(3)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O  7 20REFFLD(LBUCOD LABEL)
     A                                 15  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 15  9'Enter=Continue'
     A                                      COLOR(BLU)
     A                                  7  1'Shrt/out qty'
     A*
     A          R SCREEN64
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU210.04'
     A                                  1 11'Flag Assignment'
     A                                  3  2'Press ENTER'
     A                                  4  2'to create'
     A                                  5  2'Runner Pick List.'
     A                                 15  1'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Records for 16 line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
