     A****************************************************************
     A*
     A* SL333PR   Slot listing with item/description
     A* 06 March, 2000
     A* Roxanne Harrison
     A*
     A* 03/06/00  RH   4.13
     A*  - Created
     A*
     A* 11/30/02  HNK  4.16
     A*  - Report date added.
530a A* 12/17/09  RH   5.30a
     A*  - ENH: Add printed line in place of Seq and Type
SFG  A* 04/05/11  GJA  SFG
     A*  - NOTE:  COMPILE WITH DRAWER(0002)
640aAA* 04/19/11  RH   6.40a
     A*  - ENH: Add total line for available qty.
640B A* 08/22/12  GJA  6.40B
     A*  - ENH: Add LICENSE enter date.
650a A* 04/24/13  GJA  6.50a
     A*  - ENH: Add pick slot
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
416 AA            UDATE          6S 0O     3EDTCDE(Y)
     A                                    52
     A                                      DFT('Slot Listing')
     A                                    65
     A                                      DFT('with Item/Desc')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL333')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    86
     A                                      DFT('Available')
530aMA N65                               101
     A                                      DFT('Pick')
     A                                   111
     A                                      DFT('Pick')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    30
     A                                      DFT('License')
     A                                      UNDERLINE
     A                                    58
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    86
     A                                      DFT('  Qty    ')
     A                                      UNDERLINE
530aMA N65                               101
     A                                      DFT('Seq ')
     A                                      UNDERLINE
530aMA N65                               106
     A                                      DFT('Type')
     A                                      UNDERLINE
     A                                   111
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A*
650ADA*                                  117
650ADA*                                     DFT('Status')
650AMA                                   114
650AMA                                      DFT('Sts')
     A                                      UNDERLINE
650aAA                                   118
650aAA                                      DFT('Pick slot')
650aAA                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail L1 line.
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            SLDISP    R        O     1REFFLD(SLDISP SLOT1)
SGCaMA            SLITEM    R        O    14REFFLD(SLITEM SLOT1)
SGCaMA            LCNS          15A  O    30
640bAA            LAENTD         6S 0O    46EDTWRD('  /  /  ')
SGCaMA            ITDESC    R        O    58REFFLD(ITDESC PIRITEM)
SGCaMA            W1QTY         10A  O    89
530aMA N65        SLPSEQ    R        O   100REFFLD(SLPSEQ SLOT1)
     A                                      EDTCDE(Z)
530aMA N65        SLSDEF    R        O   107REFFLD(SLSDEF SLOT1)
530aMA  65                           O   100'__________'
     A            SLPICK    R        O   112REFFLD(SLPICK SLOT1)
     A*
650ADA*           SLSTAT    R        O   120REFFLD(SLSTAT SLOT1)
     A            SLSTAT    R        O   115REFFLD(SLSTAT SLOT1)
     A*
650aAA            PCKSLT    R        O   118REFFLD(SLDISP SLOT1)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
640aAA* Print total line
640aAA*
640aAA          R TOTAL1
640aAA                                      SPACEB(001)
640AAA                                    78
640AAA                                      DFT('TOTAL:')
640aAA            W1TQTY        15A  O    86
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('SL333: End of Report ')
     A*
