     A*%%TS  SD  19990910  121239  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PT170AFM   Letdown maint.   (one whse) - Display screen
     A*  26 January 1998
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*  09/10/99  HNK  4.12
     A*   -Transaction generated date and time added.
     A*  04/20/01  RH   4.15c
     A*   -Status added.
     A*
500 AA*  12/16/04  HNK/MLB  5.00
     A*   -Enh: Department, item added to display.
530aAA*  02/24/10  RH       5.30a
     A*   -Fix: Remove OPTLN2, not used, causing OPTLN to not display
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19941109  073516  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'PT170A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 22'Close Open Letdown Transactions'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19941109  073516  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 43'Position to transaction . .'
     A                                  2 43'Change to warehouse . . . .'
     A            PTINP1         3Y 0B  2 72EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            FLD001         1A  O  1  2
     A            PTINP2         7Y 0B  3 72EDTCDE(4)
     A            FLD002         1   O  4  2
     A            WOWHSE        40   O  2  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990910  121239  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
500 MA            PTTRN#    R        O 11  5REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
500 DA*           W1TYPE        30A  O 11 20
500 MA            W1TYPE        20A  O 11 13
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
500 AA            W1WHDP         5A  O 11 34
500 AA            W1ITEM        15A  O 11 40
500 MA            W1GDTE         6Y 0O 11 56EDTCDE(Y)
500 MA            W1GTIM         6Y 0O 11 65EDTWRD('  :  :  ')
500 MA            W1STAT         5A  O 11 74
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990910  121239  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
500 DA*                                10 20'Description                   '
500 MA                                 10 13'Description         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 DA*                                10  7'Transaction'
500 MA                                 10  5'Trans# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 MA                                 10  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 AA                                 10 34'Dept '
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
500 AA                                 10 40'Item           '
500 AA                                      DSPATR(HI)
500 AA                                      DSPATR(UL)
500 MA                                 10 56'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 MA                                 10 65'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500 MA                                 10 74'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19941013  210313  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75   O  2  2COLOR(BLU)
530aDA**          OPTLN2        75   O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A*%%TS  SD  19941109  110010  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 9=Quick close.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
