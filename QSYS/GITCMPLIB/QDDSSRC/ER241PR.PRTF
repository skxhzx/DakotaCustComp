     A****************************************************************
     A*
     A* ER241PR   Task report for an employee
     A* 09 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
413  A*  NOTE: THIS PROGRAM IS NOW REPLACED BY ER371PR
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER241'
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(WKEMP# ER241)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1REFFLD(WKNAME ER241)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    16'Direct'
     A                                    25'Indirect'
     A                                    87'% Total'
     A                                      SPACEA(001)
     A*
     A                                     3'  Date  '
     A                                      UNDERLINE
     A                                    16' Hours'
     A                                      UNDERLINE
     A                                    25'   Hours'
     A                                      UNDERLINE
     A                                    37' Hours'
     A                                      UNDERLINE
     A                                    48'Task  '
     A                                      UNDERLINE
     A                                    55'Description                  '
     A                                      UNDERLINE
     A                                    87'  Hours'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            DIRTOT         5S 2     16EDTCDE(3)
     A            INDTOT         5S 2     27EDTCDE(3)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            HOURS          5S 2     38EDTCDE(3)
     A            WKCODE    R             48REFFLD(WKCODE ER241)
     A            TKDESC    R             56REFFLD(TKDESC TASK)
     A            PCNHRS         5S 2     88EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          10A        1
     A            T#DTOT         7S 2     14EDTCDE(3)
     A            T#ITOT         7S 2     25EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Print summary
     A*
     A          R SUMHD1
     A                                     1'Summary:'
     a                                      SPACEA(001)
     A                                    87'% Total'
     A                                      SPACEA(001)
     A                                    37' Hours'
     A                                      UNDERLINE
     A                                    48'Task  '
     A                                      UNDERLINE
     A                                    55'Description                  '
     A                                      UNDERLINE
     A                                    87'  Hours'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A          R SUMAR1
     A            S#HRS          5S 2     38EDTCDE(3)
     A            S#TSK     R             48REFFLD(WKCODE ER241)
     A            S#DSC     R             56REFFLD(TKDESC TASK)
     A            S#PCN          5S 2     88EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER241: End of Report '
     A*
