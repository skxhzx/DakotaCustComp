     A*%%***********************************************************************
     A*
     A*   ER341PR  Print Letdown/Replenishment transactions
     A*   02 July, 1999
     A*   Hemant Kapadia
     A*
650a A*  10/05/14  GJA  6.50a
     A*      -Enh: Added transactions per hour
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                   125
     A                                      DFT('ER341')
     A                                      SPACEA(001)
     A*
     A* Print employee number and name
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(003)
     A            TMERNG        22A  O    52
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            PTEMP#    R             +1REFFLD(PTEMP# ER341)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('From slot  ')
     A                                      UNDERLINE
     A                                    23
     A                                      DFT('To slot    ')
     A                                      UNDERLINE
     A                                    36
     A                                      DFT('ITEM #         ')
     A                                      UNDERLINE
     A                                    52
     A                                      DFT('Qty  ')
     A                                      UNDERLINE
     A                                    58
     A                                      DFT('UOM  ')
     A                                      UNDERLINE
     A                                    65
     A                                      DFT('Start Dt')
     A                                      UNDERLINE
     A                                    75
     A                                      DFT('Start Tm')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('End Date')
     A                                      UNDERLINE
     A                                    95
     A                                      DFT('End Time')
     A                                      UNDERLINE
     A                                   105
     A                                      DFT('Task')
     A                                      UNDERLINE
     A                                   113
     A                                      DFT('Minutes')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
     A                                      REFFLD(PTTRN# *LIBL/ER341)
     A            LBDISP    R        O    10REFFLD(LBDISP *LIBL/ER341)
     A            LBDSP2    R        O    23REFFLD(LBDSP2 *LIBL/ER341)
     A            LBITEM    R        O    36REFFLD(LBITEM *LIBL/ER341)
     A            LBQPCK    R        O    52EDTCDE(3)
     A                                      REFFLD(LBQPCK *LIBL/ER341)
     A            LBUCOD    R        O    58
     A                                      REFFLD(LBUCOD *LIBL/ER341)
     A            W1SDTE         6  0O    65EDTCDE(Y)
     A            W1STIM         6  0O    75EDTWRD('  :  :  ')
     A            W1EDTE         6  0O    85EDTCDE(Y)
     A            W1ETIM         6  0O    95EDTWRD('  :  :  ')
     A            PTTASK    R        O   105REFFLD(PTTASK *LIBL/ER341)
     A            PTDMIN    R        O   113EDTCDE(3)
     A                                      REFFLD(PTDMIN *LIBL/ER341)
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Total transactions:')
530aAA            TRNCNT         5  0O    21EDTCDE(3)
530aAA            TRNPCK         7  0O    50EDTCDE(3)
530aAA            TRNMIN         7  0O   111EDTCDE(3)
650aAA            TRNHR          7  2O   121EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('ER341: End of Report')
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'No records found for employee'
     A*
