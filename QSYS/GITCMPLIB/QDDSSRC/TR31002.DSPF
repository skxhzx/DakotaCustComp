     A*%%TS  SD  20110317  133949  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR310FM   View TRAX log - Display
     A*  Retha Davis
     A*
     A*  Revisions
     A*
530 AA*    06/23/10  RBD  5.30
     A*      - Created.
     A*
640 AA*    02/14/12  RBD  6.40
     A*      - Fixed scrolling.
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A  20                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE       75A  O  6  5
     A            SFLHIDE      200   H
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
640 DA**                                    SFLSIZ(0016)
640 MA                                      SFLSIZ(0034)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 34'View Trax Log'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  3  2
     A            SCOPT2        75A  O  4  2
     A            SFLHEAD       79A  O  5  1
     A*
     A          R SFL2                      SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  6  2
     A  20                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE2      75A  O  6  5
     A            SFLHIDE      200   H
     A*
     A          R SF2CTL                    SFLCTL(SFL2)
640 DA**                                    SFLSIZ(0016)
640 MA                                      SFLSIZ(0034)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      SFLCSRRRN(&CSRRRN)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 34'View Trax Log'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  3  2
     A            SCOPT2        75A  O  4  2
     A            SFLHEAD2      79A  O  5  1
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F11=Next view  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20110317  133949  RETHA       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 20 16 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'From date . .'
     A            W1DATE         6Y 0B  3 15EDTCDE(3)
     A                                      DSPATR(HI)
     A                                  3 24'MMDDYY'
     A                                  4  1'Warehouse . .'
     A            W1WHSE         3Y 0B  4 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                  5  1'Route . . . .'
     A            W1RTE          5A  B  5 15DSPATR(HI)
     A                                  5 24'*ALL'
     A                                  6  1'Route id  . .'
     A            W1RTID         5A  B  6 15DSPATR(HI)
     A                                  6 24'*ALL'
     A                                  8  1'Errors only .'
     A            W1ERRONLY      1A  B  8 15DSPATR(HI)
     A                                  8 24'Y,N'
     A                                 10  1'Downloaded & not'
     A                                 11  2'uploaded . .'
     A            W1DWNUP        1A  B 11 15DSPATR(HI)
     A                                 11 24'Y,N'
     A                                 13  1'Order by  . .'
     A            W1ORDERBY      1A  B 13 15DSPATR(HI)
     A                                 13 24'1=Rte/Try'
     A                                 14 24'2=Creation date'
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
