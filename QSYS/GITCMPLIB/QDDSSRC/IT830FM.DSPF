     A*%%TS  SD  20010424  162943  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT830FM   Change item quantities
     A*  18 April 2001
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
415 AA*    04/18/01  DAS  4.15
     A*      - Created
     A*
415aAA*    04/24/01  DAS  4.15a
     A*      - Added window PASWRD
     A*      - Added quantities for open label values.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010418  135629  DAVE3       REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'IT810.01'
     A                                  1 31'Change Item Quantities'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Item number . . . . . . . . .'
     A            W1ITEM        15A  B  9 35
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20010424  162943  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF13
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'IT830.02'
     A                                  1 31'Change Item Quantities'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  3  2'Warehouse            :'
     A            ITWHSE         3Y 0O  3 25EDTCDE(4)
     A            WHDESC        30A  O  3 29
     A            W2LBL1        22A  O  4  2
     A            W2HEAD        55A  O  4 25
     A            W2LBLX        22A  O  5  2
     A            W2HEDX        55A  O  5 25
     A                                  7  2'Press Enter to return.'
     A                                      COLOR(BLU)
     A                                  9 23'Normal'
     A                                  9 41'Breakdown 1'
     A                                  9 59'Breakdown 2'
     A            W2UM1          5A  O 10 23
     A            W2UM2          5A  O 10 44
     A            W2UM3          5A  O 10 62
     A                                 11 18'------------------'
     A                                 11 40'--------------'
     A                                 11 58'--------------'
     A                                 12  4'Stock . . . .'
     A            W2STK1    R        B 12 18REFFLD(IQREC/IQSTK1 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STK2    R        B 12 40REFFLD(IQREC/IQSTK2 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STK3    R        B 12 58REFFLD(IQREC/IQSTK3 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13  4'Allocated . .'
     A            W2ALC1    R        B 13 18REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOALC1    R        O 13 28REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A            W2ALC2    R        B 13 40REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOALC2    R        O 13 48REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A            W2ALC3    R        B 13 58REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOALC3    R        O 13 66REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                 14  4'Inactive  . .'
     A            W2HLD1    R        B 14 18REFFLD(IQREC/IQHLD1 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2HLD2    R        B 14 40REFFLD(IQREC/IQHLD2 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2HLD3    R        B 14 58REFFLD(IQREC/IQHLD3 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 15  4'Unknown . . .'
     A            WOUNK1    R        O 15 28REFFLD(IQREC/IQALC1 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A            WOUNK2    R        O 15 48REFFLD(IQREC/IQALC2 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A            WOUNK3    R        O 15 66REFFLD(IQREC/IQALC3 *LIBL/ITEMQTY)
     A                                      EDTCDE(L)
     A                                 17  4'Available . .'
     A            W2AVL1         7Y 0O 17 18EDTCDE(L)
     A            W2AVL2         5Y 0O 17 40EDTCDE(L)
     A            W2AVL3         5Y 0O 17 58EDTCDE(L)
     A                                 23  2'F5=Refresh  F12=Cancel  F13=Linked-
     A                                       items'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20010416  132325  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'IT830.03'
     A                                  1 35'ItemQty Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Warehouse            :'
     A            ITWHSE         3Y 0O  3 25EDTCDE(4)
     A            WHDESC        30A  O  3 29
     A            W2LBL1        22A  O  4  2
     A            W2HEAD        55A  O  4 25
     A            W2LBLX        22A  O  5  2
     A            W2HEDX        55A  O  5 25
     A                                 11 27'Press F10 to Confirm Update'
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A*%%TS  SD  20010424  095629  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
