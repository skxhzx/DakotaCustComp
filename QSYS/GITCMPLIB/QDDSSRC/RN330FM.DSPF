     A*%%TS  SD  20130612  121301  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  RN330FM    Return Customer Order - Select/Verify
     A*  11 June 2013
     A*  Lynn McMahon
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650  A*    06/11/13  LMC  6.50
     A*      - Created.
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20130612  102139  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                  1  2'RN330.01'
     A                                  1 27'Return Customer Order Lookup'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  4'Customer Id'
     A                                      DSPATR(HI)
     A            W1CUSTID       6A  O  4 16
     A                                  4 24'Item'
     A                                      DSPATR(HI)
     A            W1ITEM        15A  O  4 29
     A                                  4 46'Quantity'
     A                                      DSPATR(HI)
     A            W1QTY          5Y 0O  4 55EDTCDE(Z)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20120509  170716  LYNNM       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD005         1A  O  1  2
     A            WHCODE         3Y 0O  2  3EDTCDE(4)
     A                                  2  7'-'
     A                                      DSPATR(ND)
     A            WHDESC        30A  O  2  9
     A                                  2 46'Pos to Frequency. . . .'
     A            PTINP1         1A  B  2 70
     A            FLD002         1A  O  3  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20130612  121301  LYNNM       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1CORD         7Y 0O 11  7EDTCDE(Z)
     A            S1QTY          5Y 0O 11 35EDTCDE(Z)
     A            S1UDTE         6Y 0O 11 42EDTCDE(Y)
     A            S1ITEM        15   O 11 17
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20130612  121301  LYNNM       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9  7'Customer'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  7'Order# '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 42'  Date  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 33'Quantity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 17'Ordered'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R WINDOW1
     A*%%TS  SD  20130612  082434  LYNNM       REL-V6R1M0  5761-WDS
     A                                      WINDOW(6 20 16 42)
     A                                  1 10'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  4  1'Warehouse . .'
     A            W1WHSE         3Y 0B  4 15DSPATR(HI)
     A                                      EDTCDE(3)
     A                                      DSPATR(PR)
     A                                  5  1'Customer. . .'
     A            W1CUSTID       6A  B  5 15DSPATR(HI)
     A                                  6  1'Item. . . . .'
     A            W1ITEM        15A  B  6 15DSPATR(HI)
     A                                 15  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  1'Quantity. . .'
     A            W1QTY          5Y 0B  7 15DSPATR(HI)
     A                                      EDTCDE(3)
