     A*%%TS  SD  19940708  113503  HEMANT      REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  ST112FM   Pick Zone Selector Maintenance - Change screen
     A*  05 August, 1998
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940708  113503  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'ST112.01'
     A                                  1 28'Pick Zone Selector '
     A                                      DSPATR(HI)
     A                                  1 47'Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  8 29EDTCDE(4)
     A  90
     AO 91                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90N91                            DSPATR(HI)
     A N26N90N91                            DSPATR(UL)
     A                                  9  4'User code  . . . . . .'
     A            W1USER        10A  B  9 29
     A  90
     AO 91                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 10  4'Truck  . . . . . . . .'
     A            W1TRUK        10A  B 10 29
     A  90
     AO 91                                  DSPATR(PR)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90N91                            DSPATR(HI)
     A N24N90N91                            DSPATR(UL)
     A                                 11  4'Pick Zone  . . . . . .'
     A            W1ZONE         6A  B 11 29
     A  90
     AO 91                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                 12  4'Default Selector(Y,N).'
     A            W1DFLT         1A  B 12 29
     AO 90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                 23  2'F4=Prompt F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
