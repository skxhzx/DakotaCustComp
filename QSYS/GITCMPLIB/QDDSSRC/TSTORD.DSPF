     A*%%TS  SD  20020520  135951  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTORD      Test *ORD subcommands of DRIITEMADJ interface
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    05/17/02  DAS  4.16
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20020211  114104  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTORD.1'
     A                                  1 14'Test DRIORD Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Subcommand type  . . . .'
     A            W1TYPE         1A  B  5 30
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 34'1 = Order header'
     A                                  6 34'2 = Order detail'
     A          R SCREEN2
     A*%%TS  SD  20020520  135951  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTORD.2'
     A                                  1 26'Test DRIORD Interface - Header'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Subcommand . . . . .'
     A            W2SCMD         2Y 0B  3 25EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 29'1=*CRTRTE'
     A                                  3 40'2=*ADDHDR'
     A                                  3 52'3=*CHGHDR'
     A                                  3 63'4=*DELORD'
     A                                  4  4'Dakota call  . . . .'
     A            W2DKTA         1A  B  4 25VALUES('Y' 'N')
     A                                  4 28'11=*GETHDR'
     A                                  4 39'12=*GETHDRWCO'
     A                                  4 62'14=*DELRTE'
     A                                  6  1'Send'
     A                                      DSPATR(UL)
     A            S2WHSE         1A  B  8  2
     A                                  8  6'Warehouse  . . . . .'
     A            W2WHSE         3Y 0B  8 27
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S2OID          1A  B  9  2
     A                                  9  6'Order id . . . . . .'
     A            W2OID          7Y 0B  9 27
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S2RID          1A  B 10  2
     A                                 10  6'Route id . . . . . .'
     A            W2RID          5A  B 10 27
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A            S2TYPE         1A  B 11  2
     A                                 11  6'Type . . . . . . . .'
     A            W2TYPE         1A  B 11 27
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A            S2CORD         1A  B 12  2
     A                                 12  6'Customer order # . .'
     A            W2CORD         7Y 0B 12 27
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S2RTE          1A  B 13  2
     A                                 13  6'Route  . . . . . . .'
     A            W2RTE          5A  B 13 27
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A                                 20  6'Program  . . . . . .'
     A            W2PGM         10A  B 20 27
     A                                 21  6'Return code  . . . .'
     A            W2RTN         10A  B 21 27
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20020520  135951  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTORD.3'
     A                                  1 25'Test DRIORD Interface - Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Subcommand . . . . .'
     A            W3SCMD         2Y 0B  3 25EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 29'1=*GETDTL'
     A                                  3 40'2=*ADDDTL'
     A                                  3 51'3=*CHGDTL'
     A                                  3 62'4=*DELDTL'
     A                                  4  4'Dakota call  . . . .'
     A            W3DKTA         1A  B  4 25VALUES('Y' 'N')
     A                                  6  1'Send'
     A                                      DSPATR(UL)
     A            S3WHSE         1A  B  8  2
     A                                  8  6'Warehouse  . . . . .'
     A            W3WHSE         3Y 0B  8 27
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S3OID          1A  B  9  2
     A                                  9  6'Order id . . . . . .'
     A            W3OID          7Y 0B  9 27
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S3SEQ          1A  B 10  2
     A                                 10  6'Seq #  . . . . . . .'
     A            W3SEQ          5Y 0B 10 27
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            S3ITEM         1A  B 11  2
     A                                 11  6'Item . . . . . . . .'
     A            W3ITEM        15A  B 11 27
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A            S3OQTY         1A  B 12  2
     A                                 12  6'Ordered quantity . .'
     A            W3OQ1          5Y 0B 12 27EDTCDE(M)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A            W3OQ2          5Y 0B 12 35EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A            W3OQ3          5Y 0B 12 43EDTCDE(M)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A            S3PQTY         1A  B 13  2
     A                                 13  6'Picked quantity  . .'
     A            W3PQ1          5Y 0B 13 27EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A            W3PQ2          5Y 0B 13 35EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A            W3PQ3          5Y 0B 13 43EDTCDE(M)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A                                 20  6'Program  . . . . . .'
     A            W3PGM         10A  B 20 27
     A                                 21  6'Return code  . . . .'
     A            W3RTN         10A  B 21 27
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
