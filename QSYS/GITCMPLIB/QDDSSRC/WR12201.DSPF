     A*%%TS  SD  20100419  135541  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  WR122FM   REPACK Priority Replenishment update
     A*  Jeffrey Jahnke
     A*
     A*  Revisions
     A*
530 AA*    04/19/10  JCJ  5.30
     A*      - Created
640 AA*    01/20/11  JCJ  6.40
     A*      - Do not allow qty file to be changed.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  20080908  105056  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'WR122.01'
     A                                  1 25'REPACK Replenishment Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Replenishment type . .'
     A            W1PRRPLT       1A  O  7 28
     A                                  8  4'Select Flag. . . . . .'
     A            W1PRFLAG       1A  B  8 28
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 52'D=Delete, S=Sequenced'
     A                                  9  4'To Slot. . . . . . . .'
     A            W1PRTDIS      12A  O  9 28
     A                                  9  4'From Slot. . . . . . .'
     A            W1PRFDIS      12A  O  9 28
     A                                 10  4'Item . . . . . . . . .'
     A            W1PRITEM      15A  O 10 28
     A                                 11  4'Parent Quantity Needed'
     A            W1PRQTY        3Y 0O 11 28EDTCDE(3)
640 D *           W1PRQTY        3Y 0B 11 28EDTCDE(3)
640 D * 22                                  DSPATR(RI)
640 D *N22N90                               DSPATR(UL)
640 D * 02                                  DSPATR(PC)
640 D * 90                                  DSPATR(PR)
     A                                 11 52'Enter new quantity needed'
     A                                 12  4'Add Date . . . . . . .'
     A            W1ADDDT       10A  O 12 28
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
