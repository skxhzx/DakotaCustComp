     A****************************************************************
     A*
     A* ER281PR   Employee management report for a crew (Detail)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
413  A*  NOTE: This program is not used any more.
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER281'
     A*
     A* Print crew code and description
     A*
     A          R CRHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'CREW:'
     A            WKCREW    R             +1REFFLD(WKCREW ER281)
     A            CRDESC    R             +1REFFLD(CRDESC CREW)
     A  94                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    76'Direct'
     A                                    88'Cases'
     A                                   105'Cube'
     A                                   119'Weight'
     A                                   128'Cases'
     A                                      SPACEA(001)
     A*
     A                                     2'  Date  '
     A                                      UNDERLINE
     A                                    11'Employee                       '
     A                                      UNDERLINE
     A                                    44'Task  '
     A                                      UNDERLINE
     A                                    51'Description                  '
     A                                      UNDERLINE
     A                                    76' Hours'
     A                                      UNDERLINE
     A                                    84' Selected'
     A                                      UNDERLINE
     A                                   100' Selected'
     A                                      UNDERLINE
     A                                   116' Selected'
     A                                      UNDERLINE
     A                                   127'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A            GTDATE         6S 0      2EDTCDE(Y)
     A  93                                75
     A                                      '(Continued)'
     A*
     A* Print employee line
     A*
     A          R EPHEAD
     A                                      SPACEA(001)
     A            WKEMP#    R             14REFFLD(WKEMP# ER281)
     A                                       EDTCDE(Z)
     A            WKNAME    R             20REFFLD(WKNAME ER281)
     A            DIRTOT         5S 2     76EDTCDE(3)
     A            WKPTOT    R             84REFFLD(WKPTOT ER281)
     A                                      EDTCDE(1)
     A            WKCTOT    R             95REFFLD(WKCTOT ER281)
     A                                      EDTCDE(1)
     A            WKWTOT    R            111REFFLD(WKWTOT ER281)
     A                                      EDTCDE(1)
     A            CASEPH         5S 0    128EDTCDE(3)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKCODE    R             44REFFLD(WKCODE ER281)
     A            TKDESC    R             51REFFLD(TKDESC TASK)
     A            HOURS          5S 2     76EDTCDE(3)
     A            WKPCS     R             87REFFLD(WKPCS  ER281)
     A                                      EDTCDE(1)
     A            WKCUBE    R             98REFFLD(WKCUBE ER281)
     A                                      EDTCDE(1)
     A            WKSWGT    R            113REFFLD(WKSWGT ER281)
     A                                      EDTCDE(1)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#DTOT         7S 2     74EDTCDE(3)
     A            T#PTOT         7S 0     84EDTCDE(1)
     A            T#CTOT        11S 3     95EDTCDE(1)
     A            T#WTOT        11S 2    111EDTCDE(1)
     A            T#CSPH         7S 0    126EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER281: End of Report '
     A*
