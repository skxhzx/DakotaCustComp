     A*%%TS  SD  19960215  133618  ROCKY       REL-V3R1M0  5763-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   845 W. Roosevelt Rd.  #8 - 51
     A*   West Chiago, Il. 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  IV266FM   Inventory page assignment by slot page
     A*
     A*
     A*  Revisions
510aAF*    05/25/07  RH   5.10a
     F*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960215  133618  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IV266.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30'Inventory Page Assignment -
     A                                       By Slot Page'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . .'
     A            W1WHSE         3Y 0B  7 28
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  9  4'Inventory date . . . .'
     A            W1IDTE         6Y 0B  9 28
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                 11  4'Team . . . . . . . . .'
     A            W1TEAM        10A  B 11 28
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A                                 13  4'Department . . . . . .'
     A            W1WHDP         5A  B 13 28
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A*                                13  4'Aisle. . . . . . . . .'
     A*           W1ASLE         4A  B 13 28
     A*N23                                  DSPATR(HI)
     A* 23                                  DSPATR(RI)
     A* 03                                  DSPATR(PC)
     A                                 15  4'From Slot  . . . . . .'
     A            W1FDSP        12A  B 15 28
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                 17  4'To Slot  . . . . . . .'
     A            W1TDSP        12A  B 17 28
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
