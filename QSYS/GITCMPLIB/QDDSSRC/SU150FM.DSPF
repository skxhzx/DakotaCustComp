     A*%%TS  SD  19990820  133332  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SU150FM    Clear & reset files for start up - Options
     A*  19 August 1999
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970917  063708  DAVE        REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SU150.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Y=Yes, N=No'
     A                                  1 21'Clear & Reset Files for Start Up'
     A                                      DSPATR(HI)
     A                                 10 14'This program will clear -
     A                                      records from files:'
     A                                 12 14'CROSSDK, EXPORD*, EXPPO*, IMPORD*,-
     A                                       IMPPO*, ITEMQTY, LABEL,'
     A                                 13 14'LICENSE, ORD*, PLT*, PIRORD#,-
     A                                      PIRRTE#, PIRTRAN, PODTL, POHDTL,'
     A                                 14 14'POHDR, POHHDR, RTE*, SLOT, UPC,-
     A                                       VSLOT, RTE*, WORKLBL, and WORKZON.'
     A          R SCREEN2
     A*%%TS  SD  19990820  133332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SU150.02'
     A                                  1 23'Clear & Reset Files for Start Up'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  7 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ    R        O  8 34REFFLD(W1JOBQ *SRC)
     A                                  8 58'Y=Yes, N=No'
     A                                 10 14'This program will clear records fr-
     A                                      om files:'
     A                                 12 14'CROSSDK, EXPORD*, EXPPO*, IMPORD*,-
     A                                       IMPPO*, ITEMQTY, LABEL,'
     A                                 13 14'LICENSE, ORD*, PLT*, PIRORD#,PIRRT-
     A                                      E#, PIRTRAN, PODTL, POHDTL,'
     A                                 14 14'POHDR, POHHDR, RTE*, SLOT, UPC, VS-
     A                                      LOT, RTE*, WORKLBL, and WORKZON.'
     A                                 16 16'You are about to reset or clear re-
     A                                      cords from the files'
     A                                 17 16'listed above.'
     A                                 19 16'Please make sure you know what you-
     A                                       are doing.'
     A                                 21 31'Press F10 to continue.'
     A          R SCREEN3
     A*%%TS  SD  19990820  133332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SU150.03'
     A                                  1 23'Clear & Reset Files for Start Up'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  7 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ    R        O  8 34REFFLD(W1JOBQ *SRC)
     A                                  8 58'Y=Yes, N=No'
     A                                 10 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                 11 13'to cancel your request.'
     A                                 14 17'If you are sure, press F22 to rese-
     A                                      t the files.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
