     A*%%TS  SD  20100119  103106  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. TM110A01 COPY AND NAME TM110A02
     A*----------------------------------------------------------------
     A*
     A*  TM110AFM   Truck maintenance (one whse) - Display
     A*  22 May, 1997
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
530 AA*    05/26/06  DAS  5.30
     A*      - Moved POSTNREC/OPTREC into DSPSFC.
     A*        Re: Webfacing had problem with double read.
     A*
530aAA*    01/18/10  JCJ  5.30a
     A*      - Added screen2. Moved THBULK & THBAT to screen 2.
     A*      - Added F10 to dspctl and f11 to vw2sfc rcdfmts.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'TM110A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 31'Truck Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19970602  155152  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A                                  2 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 47'Position to truck . .'
     A            PTINP2        10A  B  3 69
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970522  144429  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            THTRUK    R        O 11  7REFFLD(THREC/THTRUK *LIBL/TRUCKH)
     A            THDESC    R        O 11 19REFFLD(THREC/THDESC *LIBL/TRUCKH)
     A            TFRZ           3Y 0O 11 51EDTCDE(4)
     A            TREF           3Y 0O 11 56EDTCDE(4)
     A            TDRY           3Y 0O 11 61EDTCDE(4)
530aD ***         THBULK    R        O 11 70REFFLD(THREC/THBULK *LIBL/TRUCKH)
530aD ***         THBAT     R        O 11 77REFFLD(THREC/THBAT *LIBL/TRUCKH)
530aAA            THZTMP    R        O 11 67REFFLD(THREC/THZTMP *LIBL/TRUCKH)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
530aAA                                      CF10
530aAA                                      CF11
     A                                      CF16
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
530 AA            WHCODE         3Y 0O  4  2EDTCDE(3)
530 AA                                  4  6'-'
530 AA            WHDESC        30A  O  4  8
530 AA                                  4 47'Change to warehouse . . .'
530 AA            PTINP1         3Y 0B  4 74EDTCDE(4)
530 AA  21                                  DSPATR(RI)
530 AA  01                                  DSPATR(PC)
530 AA                                  5 47'Position to truck . .'
530 AA            PTINP2        10A  B  5 69
530 AA  22                                  DSPATR(RI)
530 AA  02                                  DSPATR(PC)
530 AA                                  6  2'Type options, press Enter.'
530 AA                                      COLOR(BLU)
530 AA            OPTLN1        75A  O  7  2COLOR(BLU)
530 AA            OPTLN2        75A  O  8  2COLOR(BLU)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 19'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 53'- Pallets -'
     A                                      DSPATR(HI)
     A                                 10 52'Frz'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 57'Ref'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Dry'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
530aD ***                               9 68'Bulk '
530aD ***                                   DSPATR(HI)
530aD ***                              10 68'Picks'
530aD ***                                   DSPATR(HI)
530aD ***                                   DSPATR(UL)
530aD ***                               9 75'Batch'
530aD ***                                   DSPATR(HI)
530aD ***                              10 75'Picks'
530aD ***                                   DSPATR(HI)
530aD ***                                   DSPATR(UL)
530aAA                                  9 67'Zone '
530aAA                                      DSPATR(HI)
530aAA                                 10 67'Template  '
530aAA                                      DSPATR(HI)
530aAA                                      DSPATR(UL)
530aAA*****************************************************************
530aAA*
530aAA* SUBFILE FOR LIST DISPLAY - VIEW 2
530aAA*
530aAA          R VW2SFL                    SFL
     A*%%TS  SD  19970714  170646  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
530AaA            THTRUK    R        O 11  7REFFLD(THREC/THTRUK *LIBL/TRUCKH)
530aAA            THBULK    R        O 11 21REFFLD(THREC/THBULK *LIBL/TRUCKH)
530aAA            THBAT     R        O 11 28REFFLD(THREC/THBAT *LIBL/TRUCKH)
530aAA*
530aAA          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
530aAA                                      CF10
530aAA                                      CF11
     A                                      CF16
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            WHCODE         3Y 0O  4  2EDTCDE(3)
     A                                  4  6'-'
     A            WHDESC        30A  O  4  8
     A                                  4 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  4 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 47'Position to truck . .'
     A            PTINP2        10A  B  5 69
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  8  2COLOR(BLU)
     A            OPTLN1        75A  O  7  2COLOR(BLU)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Truck     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 19'Bulk '
     A                                      DSPATR(HI)
     A                                 10 19'Picks'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 26'Batch'
     A                                      DSPATR(HI)
     A                                 10 26'Picks'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
