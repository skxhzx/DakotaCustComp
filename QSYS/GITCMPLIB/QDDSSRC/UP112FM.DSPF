     A*%%TS  SD  19970424  100243  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UP112FM   UPDFILES file - Change Screen
     A*  24 April, 1997
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970424  100243  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'UP112.01'
     A                                  1 29'UPDFILES File Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Update Type (U,N,R,P).'
     A            W1TYPE         1A  B  7 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A*N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  7 45'U = Update'
     A                                  8 45'N = New   '
     A                                  9 45'R = Replace'
     A                                 10 45'P = Program'
     A                                 11  4'New File . . . . . . .'
     A            W1NEWF        10A  B 11 28
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Old File . . . . . . .'
     A            W1OLDF        10A  B 12 28
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  4'File Type (P,L,D). . .'
     A            W1FTYP         1A  B 13 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Conversion Program . .'
     A            W1PGM         10A  B 14 28
     A                                 15  4'Before/After . . . . .'
     A            W1BA           1A  B 15 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  4'Copied . . . . . . . .'
     A            W1CPY          1A  B 16 28
     A*N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  4'Logicals Deleted . . .'
     A            W1DLTL         1A  B 17 28
     A                                 18  4'Physical Deleted . . .'
     A            W1DLTP         1A  B 18 28
     A                                 19  4'Renamed  . . . . . . .'
     A            W1RNM          1A  B 19 28
     A                                 20  4'Logicals Created . . .'
     A            W1CRTL         1A  B 20 28
     A                                 21  4'Converted. . . . . . .'
     A            W1CVT          1A  B 21 28
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
