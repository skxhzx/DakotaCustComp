     A*%%TS  SD  20040514  173715  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  LNC112FM  License info code maintenance - Change
     A*  23 March 2004
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
417 AA*    03/23/04  DAS  4.17
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040514  173715  DAVE        REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'LNC112.01'
     A                                  1 26'License Info Code Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Code . . . . . . . . .'
     A            W1CODE         8A  B  9 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N21N90                               DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A                                 10  4'Description  . . . . .'
     A            W1DESC        30A  B 10 28CHECK(LC)
     A                                 12  4'Type . . . . . . . . .'
     A            W1TYPE         1A  B 12 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A                                 12 63'A=Alpha, N=Numeric'
     A                                 13  4'Decimal places . . . .'
     A            W1DEC          1Y 0B 13 28EDTCDE(3)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
