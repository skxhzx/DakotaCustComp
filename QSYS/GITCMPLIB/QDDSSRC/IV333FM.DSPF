     A*%%TS  SD  19980715  165607  ROCKY       REL-V3R1M0  5763-PW1
     A********************************************************************
520 AA* ***************************************************************
520 AA* *****  If the position of any slot entry field is changed *****
520 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
520 AA* *****  program.                                           *****
520 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   845 W Roosevelt Rd.  #8 - 51
     A*   West Chicago, IL 60185
     A*   (630) 562-0385
     A*----------------------------------------------------------------
     A*
     A*  IV333FM      RF Inventory entry - Virtual slot
     A*  21 November, 2006
     A*  Roxanne Harrison
     A*----------------------------------------------------------------
     A*  Revisions
     A*    11/21/06  RH   5.10
     A*      - Created using IV213FM
510b A*    12/19/06  RH   5.10b
     A*      - Remove F12 from Screens 4 & 5.
510c A*    05/23/07  RH   5.10c
     A*      - Enh: Add Screen6 to allow item to be keyed in to add
520aAA*    11/20/08  MLB  5.20a
     A*      - Enh: Added hidden fields WOROW and WOCOL.
     A*        Added function CSRLOC to SCREEN 2.
     A*        Both of the above added for api program APICVTSLT
610aAA*    09/21/09  DAS  6.10a
     A*      - Added screen 7 to display item info.
530aAA*    11/02/09  RH   5.30a
     A*      - Added MFG item # to screen 6.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
510b A*                                     CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  19980715  165607  ROCKY       REL-V3R1M0  5763-PW1
510b A                                      CF12
     A  98                                  ALARM
     A*                                     CLRL(23)
     A                                      CLRL(07)
520aAA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
520aAA            WOROW          3  0H
520aAA            WOCOL          3  0H
     A                                  1  2'IV333.01'
     A*                                 1 12'Inv. Entry'
     A*                                     DSPATR(HI)
     A                                  1 12'Add Item'
     A                                      DSPATR(HI)
     A                                  4  2'Slot:'
     A            W1SLOT        12A  B  4  8CHECK(ER)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
    DA*                                     CF04
500b A                                      CF08
500b A                                      CF10
510b A                                      CF12
     A  98                                  ALARM
     A                                  1  2'IV333.02'
     A                                  1 12'Add Item'
     A                                      DSPATR(HI)
     A                                  3  1'UPC'
     A            W2UPCD        50A  B  3  5CHECK(ER)
     A  30                                  DSPATR(RI)
     A                                  4  2'-OR-'
     A                                  5  1'Lic'
     A            W2LCN         15A  B  5  5CHECK(ER)
     A  30                                  DSPATR(RI)
     A                                  7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
500b A***                                   CF08
510b A                                      CF12
     A  98                                  ALARM
     A                                  1  2'IV333.03'
     A                                  1 12'Add Item'
     A                                      DSPATR(HI)
     A*                                 2 35'Virtual Slot'
     A*                                     DSPATR(HI)
     A                                  3  1'Qty:'
     A            W3SQT1         5Y 0B  3  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            W3UM1          5A  O  3 14
     A            W3SQT2         3Y 0B  4  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90N92                               DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  92                                  DSPATR(ND)
     A            W3UM2          5A  O  4 14
     A  92                                  DSPATR(ND)
     A            W3SQT3         3Y 0B  5  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90N93                               DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  93                                  DSPATR(ND)
     A            W3UM3          5A  O  5 14
     A  93                                  DSPATR(ND)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN4
     A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
500b A**                                    CF08
     A  98                                  ALARM
     A                                  1  2'IV333.04'
     A                                  1 12'Add Item'
     A                                      DSPATR(HI)
     A*                                 2 35'Virtual Slot'
     A*                                     DSPATR(HI)
     A                                  3  1'Pos:'
     A            W4SPOS         3Y 0B  3  6EDTCDE(L)
     A  90 45                               DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  45                                  DSPATR(ND)
     A                                  7  2'F3=Exit               '
500b A                                      COLOR(BLU)
500b A          R SCREEN5
500b A                                      CF08
500b A  98                                  ALARM
500b A                                      CLRL(07)
500b A                                      OVERLAY
500b A                                  1  2'IV333.05 '
500b A                                  1 12'Add Item'
500b A                                  3  1'CtWt:'
500b A            W5CWGT         7Y 2B  3  7EDTCDE(M)
500b A  29                                  DSPATR(RI)
500b A  09                                  DSPATR(PC)
500b A N29                                  DSPATR(UL)
500b A                                  7  2'F8=Ovrrid             '
500b A                                      COLOR(BLU)
510c A          R SCREEN6
     A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
    DA*                                     CF04
     A                                      CF08
     A                                      CF10
     A                                      CF12
     A  98                                  ALARM
     A                                  1  2'IV333.2A'
     A                                  1 12'Add Item'
     A                                      DSPATR(HI)
     A                                  3  1'Item'
     A            W6ITEM        15A  B  3  6CHECK(ER)
     A  30                                  DSPATR(RI)
530aAA                                  4  1'- OR -'
530AAA                                  5  1'Mfg#'
530aAA            W6VIT#        15A  B  5  6CHECK(ER)
530aAA  31                                  DSPATR(RI)
     A                                  7  2'F3/F12=Exit F10=Excpt.'
     A                                      COLOR(BLU)
610aAA          R SCREEN7
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CF12
     A                                      OVERLAY
     A                                  1  2'IV333.07 '
     A                                  1 12'Add Item'
     A                                  3  2'Item:'
     A            W7ITEM        15A  O  3  8
     A                                  4  2'Desc:'
     A            W7ITM1        15A  O  4  8
     A            W7ITM2        15A  O  5  8
414aAA            W7ITM3        15A  O  6  8
JORa A                                  7  2'F3/F12=Exit'
     A                                      COLOR(BLU)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A**                                    CF04
510b A                                      CF12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV333.01'
     A                                  1 12'Prdc. Inv'
     A                                      DSPATR(HI)
     A                                  4  2'Slot:'
     A            W1SLOT        12A  B  4  8CHECK(ER)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                  7  2'F3/F12=Salir '
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
500b A                                      CF08
500b A                                      CF10
510b A                                      CF12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV333.02'
     A                                  1 12'Prdc. Inv'
     A                                      DSPATR(HI)
     A                                  3  1'UPC'
     A            W2UPCD        50A  B  3  5CHECK(ER)
     A  30                                  DSPATR(RI)
     A                                  4  2'-OR-'
     A                                  5  1'Lic'
     A            W2LCN         15A  B  5  5CHECK(ER)
     A  30                                  DSPATR(RI)
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
510b A                                      CF12
     A                                      OVERLAY
     A                                  1  2'IV333.03'
     A                                  1 12'Prdc. Inv'
     A                                      DSPATR(HI)
     A                                  3  1'Ctd:'
     A            W3SQT1         5Y 0B  3  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            W3UM1          5A  O  3 14
     A            W3SQT2         3Y 0B  4  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90N92                               DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  92                                  DSPATR(ND)
     A            W3UM2          5A  O  4 14
     A  92                                  DSPATR(ND)
     A            W3SQT3         3Y 0B  5  6EDTCDE(M)
     A  90                                  DSPATR(PR)
     A N90N93                               DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  93                                  DSPATR(ND)
     A            W3UM3          5A  O  5 14
     A  93                                  DSPATR(ND)
     A                                  7  2'F3/F12=Salir '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN4SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV333.04'
     A                                  1 12'Prdc. Inv'
     A                                      DSPATR(HI)
     A                                  3  1'Pos:'
     A            W4SPOS         3Y 0B  3  6EDTCDE(L)
     A  90 45                               DSPATR(PR)
     A N90                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  45                                  DSPATR(ND)
     A                                  7  2'F3/F12=Salir '
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN5SP
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IV333.05'
     A                                  1 12'Prdc. Inv'
     A                                      DSPATR(HI)
     A                                  3  1'CtWt:'
     A            W5CWGT         7Y 2B  3  7EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29                                  DSPATR(UL)
     A                                  7  2'F3/F12=Salir F8=Ovrd'
     A                                      COLOR(BLU)
510c A          R SCRN6SP
510c A*%%TS  SD  19960520  102458  DAVE3       REL-V3R1M0  5763-PW1
510c A                                      OVERLAY
510c A                                      CLRL(07)
510cDA*                                     CF04
510c A                                      CF08
510c A                                      CF10
510c A                                      CF12
510c A  98                                  ALARM
510c A                                  1  2'IV333.2A'
510c A                                  1 12'Prdc. Inv'
510c A                                      DSPATR(HI)
510c A                                  3  1'Item'
510c A            W6ITEM        15A  B  3  6CHECK(ER)
510c A  30                                  DSPATR(RI)
530aAA                                  4  1'- OR -'
530aAA            W6VIT#        15A  B  5  6CHECK(ER)
530aAA  31                                  DSPATR(RI)
510c A                                  7  2'F3/F12=Salir F10=Excpc.'
510c A                                      COLOR(BLU)
610aAA          R SCRN7SP
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CF12
     A                                      OVERLAY
     A                                  1  2'IV333.07 '
     A                                  1 12'Prdc. Inv'
     A                                  3  2'Prdc:'
     A            W7ITEM        15A  O  3  8
     A                                  4  2'Desc:'
     A            W7ITM1        15A  O  4  8
     A            W7ITM2        15A  O  5  8
     A            W7ITM3        15A  O  6  8
     A                                  7  2'F3/F12=Salir F10=Excpc.'
     A                                      COLOR(BLU)
     A*
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
