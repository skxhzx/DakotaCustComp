     A*%%TS  SD  20030730  093945  HEMANT      REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  CW110AFM   Catch weight maint. (one whse) - Display
     A*  21 July 1999
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*  07/29/03 HNK 4.17
417 AA*    - Remove CW flag and add number of catch weights entered
     A*      count.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A*%%TS  SD  19990722  152847  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'CW110A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 29'Catch Weight Maintenance'
     A                                      DSPATR(HI)
     A                                  5  2'Trans #:'
     A            $PTRN#         7Y 0O  5 11EDTCDE(3)
     A                                  3  2'Whse:'
     A            LBWHSE         3Y 0O  3  8EDTCDE(3)
     A            WHDESC        30A  O  3 14
     A                                  5 28'Route:'
     A            W6RTE          5A  O  5 35
     A                                  4  2'Zone:'
     A            PSSZON         6A  O  4  8
     A            W6ZDSC        30A  O  4 16
     A                                  5 49'Pallet type:'
     A            W6PTYP         5A  O  5 62
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940809  140259  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A*                                     SLNO(06)
     A*                                 1 35'Position to item . .'
     A*           PTINP3        15A  B  1 62
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20030729  154444  HEMANT      REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*           SLAISL    R        O 11  7REFFLD(SLREC/SLAISL *LIBL/SLOT)
     A*           SLLOC     R        O 11 14REFFLD(SLREC/SLLOC *LIBL/SLOT)
     A*                                     EDTCDE(4)
     A*           SLRLVL    R        O 11 20REFFLD(SLREC/SLRLVL *LIBL/SLOT)
     A*                                     EDTCDE(4)
     A*           SLHAND    R        O 11 27REFFLD(SLREC/SLHAND *LIBL/SLOT)
     A*           SLSTYP    R        O 11 35REFFLD(SLREC/SLSTYP *LIBL/SLOT)
     A*           SLDISP    R        O 11 41REFFLD(SLREC/SLDISP *LIBL/SLOT)
     A*           SLDESG    R        O 11 62REFFLD(SLREC/SLDESG *LIBL/SLOT)
     A*           SLACTV    R        O 11 75REFFLD(SLREC/SLACTV *LIBL/SLOT)
     A*           SLSDEF    R        O 11 55REFFLD(SLREC/SLSDEF *LIBL/SLOT)
     A            W6LBL#         7S 0H
     A**          W6LINE         3Y 0O 11  2EDTCDE(3)
     A            W6ITEM        15A  O 11  6
     A            W6SEQ          4Y 0O 11 22EDTCDE(3)
     A            W6STOP         2Y 0O 11 28EDTCDE(3)
     A            W6DISP        12A  O 11 32
417 AA            W6CW#          3  0O 11 47EDTCDE(Z)
     A            W6QPCK         5Y 0O 11 53EDTCDE(3)
     A            W6STAT         1A  O 11 61
     A            W6QTY          5Y 0O 11 66EDTCDE(3)
     A            WOUM           5A  O 11 72
     A            W6MRG          1A  O 11 79
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20030730  093945  HEMANT      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 27'Stop'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 32'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 66'Picked     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 59'Closed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Qty  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 22'Seq#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 22'Item'
     A                                      DSPATR(HI)
     A                                 10 78'Mrg'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 44'No of CW'
     A                                      DSPATR(HI)
     A                                 10 45'Entered'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940808  162133  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
