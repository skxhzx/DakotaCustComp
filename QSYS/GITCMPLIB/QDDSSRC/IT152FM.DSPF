     A*%%TS  SD  19991125  074540  DAVE        REL-V4R2M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/25/99  DAS  4.13
     A*     - Moved lines down to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
413aAA*    03/30/00  HNK  4.13a
     A*     - Replaced by IT352FM.
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19991125  074540  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF08
     A                                      CF20
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT152.01'
     A                                  1 32'Inventory Transfer'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse  :'
     A            WOWHSE         3A  O  4 15
     A            WHDESC        30A  O  4 22
     A                                  5  2'Slot Dept. :'
     A            WDCODE         5A  O  5 15
     A            WDDESC        30A  O  5 22
     A                                  6  2'Slot       :'
     A            FMDISP        12A  O  6 15
     A                                  7  2'Slot type  :'
     A            W1STYP         1A  O  7 15
     A                                  8  2'Status     :'
     A            SSDESC        30A  O  8 15
     A                                  9  2'Item       :'
     A            W1ITEM        55A  O  9 15
     A                                 11  2'Quantity   :'
     A            W1CURQ        30A  O 11 15
     A                                 12  2'Item Dept. :'
     A            W1WHDP         5A  O 12 15
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 17  4'Transfer to slot . . .'
     A            W1DISP        12A  B 17 28CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 16  4'Quantity . . . . . . .'
     A            W1QT01         5Y 0B 16 28EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            W1UM1          5A  O 16 34
     A            W1QT02         3Y 0B 16 42EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 16 46
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 16 54EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 16 58
     A  93                                  DSPATR(ND)
     A                                 22  2'F4=Prompt  F5=Refresh  F8=View "To-
     A                                      " item/slot info.  F12=Cancel   '
     A                                      COLOR(BLU)
     A                                 23  2'F20=Accept warning  F21=Tie/High m-
     A                                      aintenance        '
     A                                      COLOR(BLU)
     A            W1LBLS        12   O 10  2DSPATR(HI)
     A            W1ITMS        55   O 10 15DSPATR(HI)
     A          R SCREEN2
     A*%%TS  SD  19991125  072426  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF08
     A                                      CF20
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT152.01'
     A                                  1 32'Inventory Transfer'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse :'
     A            WOWHSE         3A  O  4 14
     A                                  5  2'Department:'
     A            WDCODE         5A  O  5 14
     A                                  6  2'Slot      :'
     A            FMDISP        12A  O  6 14
     A                                  7  2'Slot type :'
     A            W1STYP         1A  O  7 14
     A                                 10  2'Quantity  :'
     A            W1CURQ        30A  O 10 14
     A                                 12  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A            W1ITEM        55A  O  9 14
     A                                 14  4'Quantity . . . . . . .'
     A            W2QTY          7A  B 14 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            W1USTR        17A  O 14 40
     A                                 22  2'F4=Prompt  F5=Refresh  F8=View "To-
     A                                      " item/slot info.  F12=Cancel   '
     A                                      COLOR(BLU)
     A                                 23  2'F20=Accept warning  F21=Tie/High m-
     A                                      aintenance        '
     A                                      COLOR(BLU)
     A                                  9  2'Item      :'
     A                                  8  2'Status    :'
     A            SSDESC        30A  O  8 14
     A            WHDESC        30A  O  4 21
     A            WDDESC        30A  O  5 21
     A                                 15  4'Transfer to slot . . .'
     A            W1DISP        12A  B 15 28CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
