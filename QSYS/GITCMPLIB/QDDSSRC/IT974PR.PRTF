     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  IT974PR    Item Quantity Compare Report - By Whse/Dept/Item
     A*  24 November 2013
     A*  Rod Roller
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
650  A*    11/24/14  RTR  6.50
     A*      - Created from IT972.
650b A*    01/20/15  RTR  6.50b
     A*      - Added count to footer.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A            RDATE          6S 0      1
     A                                      EDTCDE(Y)
     A            W1HEAD        60A  O    30
     A                                   121
     A                                      'Page'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
      *
     A            RTIME          6S 0      1
     A                                      EDTWRD('0  :  :  ')
     A            WHHEAD        40A  O    40
     A                                   121
     A                                      'IT974'
     A                                      SPACEA(002)
     A                                    53
     A                                      'Q u a n t i t y  1'
     A                                    77
     A                                      'Q u a n t i t y  2'
     A                                   101
     A                                      'Q u a n t i t y  3'
     A                                      SPACEA(001)
     A                                     2
     A                                      'Item'
     A                                    18
     A                                      'Description'
     A                                    52
     A                                      'Host'
     A                                    58
     A                                      'Dakota'
     A                                    68
     A                                      'Diff'
     A                                    76
     A                                      'Host'
     A                                    82
     A                                      'Dakota'
     A                                    90
     A                                      'Diff'
     A                                    98
     A                                      'Host'
     A                                   104
     A                                      'Dakota'
     A                                   113
     A                                      'Diff'
     A                                   120
     A                                      'Pick Slot'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            IQITEM    R        O     2
     A                                      REFFLD(ITITEM PIRITEM)
     A            ITDESC    R        O    18
     A                                      REFFLD(ITDESC PIRITEM)
     A            ICTQH1    R        O    49
     A                                      REFFLD(ICTQH1 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK1    R        O    57
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF1     R        O    65
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            ICTQH2    R        O    73
     A                                      REFFLD(ICTQH2 IMPFCQTY)
     A                                      EDTCDE(L)
     A            IQSTK2    R        O    81
     A                                      REFFLD(IQSTK2 ITEMQTY)
     A                                      EDTCDE(L)
     A            DIFF2     R        O    87
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(L)
     A            ICTQH3    R        O    95
     A                                      REFFLD(ICTQH3 IMPFCQTY)
     A                                      EDTCDE(M)
     A            IQSTK3    R        O   103
     A                                      REFFLD(IQSTK3 ITEMQTY)
     A                                      EDTCDE(M)
     A            DIFF3     R        O   109
     A                                      REFFLD(IQSTK1 ITEMQTY)
     A                                      EDTCDE(M)
     A            SLDISP    R        O   119
     A                                      REFFLD(SLDISP SLOT)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT974: End of Report')
650bAA                                    31
650bAA                                      DFT('Differences found: ')
650bAA            RECCNT    R        O    51
650bAA                                      REFFLD(IQSTK1 ITEMQTY)
650bAA                                      EDTCDE(L)
     A*
