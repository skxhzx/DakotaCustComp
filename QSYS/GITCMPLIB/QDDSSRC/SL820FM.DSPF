     A*%%TS  SD  19941116  112303  HEMANT      REL-V2R1M1  5738-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941116  101336  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SL820.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 10  4'Slot number . . . . . . . . .'
     A            W1DISP        12A  B 10 35
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 30'Change Slot Quantities'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  B  9 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  19941116  112303  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CHGINPDFT
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  9  2'Press Enter to return.'
     A                                      COLOR(BLU)
     A                                  1  2'SL820.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 30'Change Slot Quantities'
     A                                      DSPATR(HI)
     A                                  4  3'Warehouse:'
     A            ITWHSE         3Y 0O  4 14EDTCDE(4)
     A            WHDESC        30A  O  4 19
     A                                 13  4'Stock . . . . . . .'
     A                                 14  4'Allocated . . . . .'
     A                                 15  4'Picked  . . . . . .'
     A                                 19  4'Available . . . . .'
     A            W2AVL1         7Y 0O 19 25EDTCDE(N)
     A            W2AVL2         5Y 0O 19 42EDTCDE(N)
     A            W2AVL3         5Y 0O 19 58EDTCDE(N)
     A            W2UM1          5A  O 11 28
     A            W2UM2          5A  O 11 43
     A            W2UM3          5A  O 11 59
     A                                 10 28'Normal'
     A                                 10 40'Breakdown 1'
     A                                 10 56'Breakdown 2'
     A                                 12 25'----------'
     A                                 12 40'-----------'
     A                                 12 56'-----------'
     A                                  5  2'Department:'
     A            SLWHDP         5A  O  5 14
     A                                  6  8'Slot:'
     A            SLDISP        12A  O  6 14
     A                                 16  4'Transfer  . . . . .'
     A                                 17  4'Receive . . . . . .'
     A                                  7  8'Item:'
     A            W2HEAD        60A  O  7 14
     A                                 21  4'(Qty available = (Stock + Receive)-
     A                                       - (Allocated + Picked + Transfer)'
     A            W2STK1         5Y 0B 13 28EDTCDE(O)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2ALC1         5Y 0B 14 28EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2PCK1         5Y 0B 15 28EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2TFR1         5Y 0B 16 28EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2RCV1         5Y 0B 17 28EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2STK2         3Y 0B 13 45EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2ALC2         3Y 0B 14 45EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2PCK2         3Y 0B 15 45EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2TFR2         3Y 0B 16 45EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2RCV2         3Y 0B 17 45EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2STK3         3Y 0B 13 61EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2ALC3         3Y 0B 14 61EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2PCK3         3Y 0B 15 61EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2TFR3         3Y 0B 16 61EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2RCV3         3Y 0B 17 61EDTCDE(O)
     A                                      DSPATR(UL)
     A            W2DUMY         1   O  2  2
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
