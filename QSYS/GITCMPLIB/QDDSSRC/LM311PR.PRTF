     A****************************************************************
     A*
     A* LM311PR   Lumper report
     A* 02 January, 2003
     A* Hemant Kapadia
     A*
     A* 01/02/03  HNK  4.16
     A*  - Created
     A*
417 AA* 03/25/03  HNK  4.17
     A*  - ENH:Add palletized cost.
     A*
417aAA* 05/27/03  HNK  4.17a
     A*  - Display monetary value instead of "Y" or "N" for palletized.
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    56
     A                                      DFT('Lumper Information Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('LM311')
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    67
     A                                      DFT('Qty')
     A                                    74
     A                                      DFT('Qty')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('PO Number')
     A                                      UNDERLINE
     A                                    12
     A                                      DFT('Date Recv')
     A                                      UNDERLINE
     A                                    23
     A                                      DFT('Vendor                  ')
     A                                      UNDERLINE
     A                                    64
     A                                      DFT('Ordered')
     A                                      UNDERLINE
     A                                    73
     A                                      DFT('Recv')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('Palletized')
     A                                      UNDERLINE
417 AA                                    91
417 AA                                      DFT('Total Cost')
417 AA                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            LMPO      R        O     1REFFLD(LMPO   LM311)
     A            W1RDTE         6S 0O    12EDTCDE(Y)
     A            W1VNDR        40A  O    23
     A            W1QOR1         7S 0O    63EDTCDE(Z)
     A            W1QRC1         7S 0O    70EDTCDE(Z)
417aAA            W1PALT         5S 2O    81EDTCDE(3)
417aDA**          LMPALT    R        O    83REFFLD(LMPALT LM311)
417 AA            W1COST         7S 2O    91EDTCDE(3)
     A*
417 AA* Print detail line
417 AA*
417 AA          R TOTAL1
417 AA                                      SPACEB(001)
417aMA                                    55
417aDA**                                  70
417aDA**                                    DFT('Total Cost')
417aMA                                      DFT('Totals:')
417aAA            T#QTY          9S 0O    68EDTCDE(Z)
417 AA            T#COST         9S 2O    89EDTCDE(3)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
417 AA                                     1
417 AA                                      DFT('Palletized cost: '    )
417 AA            $PPALY         5S 2     22EDTCDE(3)
417 AA                                      SPACEA(001)
417 AA                                     1
417 AA                                      DFT('Non Palletized cost: '    )
417 AA            $PPALN         5S 2     22EDTCDE(3)
417 AA                                      SPACEA(001)
     A                                     1
     A                                      DFT('LM311: End of Report ')
     A*
