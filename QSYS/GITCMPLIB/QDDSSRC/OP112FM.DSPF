     A*%%TS  SD  19960701  170507  DAVE3       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*
     A*  OP110FM   PIR options - Picking
     A*  22 August 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R SCREEN1
     A*%%TS  SD  19960701  170507  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'OP112.01'
     A                                  1 33'Picking Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'General options'
     A                                      DSPATR(HI)
     A                                  8  5'Department  . . . . . . . . . .'
     A            W1WHDP         5A  B  8 38
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                  9  5'Bulk pick definition  . . . . .'
     A            W1BPCK         3Y 0B  9 38EDTCDE(3)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 42'% of full pallet'
409 AA                                 10  5'Allow manual put away . . . . .'
     A  92                                  DSPATR(ND)
409 AA            W1MPUT         1A  B 10 38
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N32N90
     AON92                                  DSPATR(UL)
     A  90
     AO 92                                  DSPATR(PR)
     A  92                                  DSPATR(ND)
     A                                 11  3'PIR options'
     A                                      DSPATR(HI)
     A                                 12  5'Maximum cube  . . . . . . . . .'
     A            W1PCUB         5Y 2B 12 38
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W1PBRK         3A  B 13 38
     A  33N90                               DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  3'Fixed options'
     A                                      DSPATR(HI)
     A                                 16  5'Pallets on fork lift  . . . . .'
     A            W1FPAL         1Y 0B 16 38EDTCDE(3)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  5'Maximum cube per pallet . . . .'
     A            W1FCUB         5Y 2B 17 38
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N28N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 18  5'Maximum stops per pallet  . . .'
     A            W1FSTP         2Y 0B 18 38
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                      EDTCDE(3)
     A N29N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 19  5'Include PIR items in cube . . .'
     A            W1INCL         1A  B 19 38
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 20  5'Pallet building method  . . . .'
     A            W1FBLD         1Y 0B 20 38
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 21  5'Previous pallets to search  . .'
     A            W1SRCH         2Y 0B 21 38EDTCDE(3)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A                                  9 63'0-100'
409 AA                                 10 63'Y, N'
409 AA  92                                  DSPATR(ND)
     A                                 12 63'0-999.99'
     A                                 13  5'Aisle to start new pallet . . .'
     A                                 13 63'Code'
     A                                 16 63'1-9'
     A                                 17 63'.01-999.99'
     A                                 18 63'1-99'
     A                                 19 63'Y, N'
     A                                 20 63'1=Piece, 2=Stop'
     A                                 21 63'1-99'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
