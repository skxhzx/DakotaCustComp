     A*%%TS  SD  20070905  172922  LYNNM       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RH132FM   Route Notes maintenance
     A*  05 September 2007
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
     A*    09/05/07  LMC
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA09
     A          R SCREEN1
     A*%%TS  SD  20070905  172922  LYNNM       REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'RH132.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 30'Work with Route Notes'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Route:'
     A            $PRTE          5A  O  5 13
     A            W1NT01        75A  B  9  3
     A  90                                  DSPATR(PR)
     A            W1NT02        75A  B 10  3
     A  90                                  DSPATR(PR)
     A            W1NT03        75A  B 11  3
     A  90                                  DSPATR(PR)
     A            W1NT04        75A  B 12  3
     A  90                                  DSPATR(PR)
     A            W1NT05        75A  B 13  3
     A  90                                  DSPATR(PR)
     A            W1NT06        75A  B 14  3
     A  90                                  DSPATR(PR)
     A            W1NT07        75A  B 15  3
     A  90                                  DSPATR(PR)
     A            W1NT08        75A  B 16  3
     A  90                                  DSPATR(PR)
     A            W1NT09        75A  B 17  3
     A  90                                  DSPATR(PR)
     A            W1NT10        75A  B 18  3
     A  90                                  DSPATR(PR)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
