     A*%%TS  SD  20051103  125529  BOBP        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RN320FM   Print Customer Returns Analysis - Options
     A*  28 August 2002
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
416 AA*  08/28/02  HNK  4.16
     A*    - Created
     A*
500bAA*  10/31/05  RLP  5.00b
     A*    - ENH: Re-enable/modify w1detl line, also adding *ALL or
     A*           specific return code to options.
     A*    - ENH: adding new report RN326 to print when (S)ummary is
     A*           entered for w1detl field, will default to 'S' and
     A*           will print current report RN325 when (D)etail is
     A*           entered for W1DETL.
     A*
     A*----------------------------------------------------------------
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20051103  125529  BOBP        REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'RN320.01'
     A            RPTTTL        50A  O  1 16DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  5'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  7 35EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  8  5'Date range . . . . . . . . .'
     A            W1FMDT         6Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 42'to'
     A            W1TODT         6Y 0B  8 45
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8 54'Date (MMDDYY)'
     A                                 10  5'Crew code  . . . . . . . . .'
     A            W1CREW         6A  B 10 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 54'*ALL, crew code'
     A                                 11  2'or'
     A                                      DSPATR(HI)
     A                                 11  5'Employee number  . . . . . .'
     A            W1EMP#         5Y 0B 11 35EDTCDE(M)
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13  5'Returns code . . . . . . . .'
     A            W1RCOD         4A  B 13 35
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 54'*All, return code'
     A                                 15  5'Detail or Summary  . . . . .'
500bDA*                                     DSPATR(ND)
     A            W1DETL         1A  B 15 35
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 54'(D)etail,(S)ummary,(B)oth'
500bDA*                                     DSPATR(ND)
     A                                 16  5'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 16 35
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 54'Y=Yes, N=No'
     A                                 17  5'Printer device . . . . . . .'
     A            W1DEV         10A  B 17 35
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 54'Name, *SYSVAL, *JOB'
     A                                 18  5'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 18 35
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 54'Character value, *STD'
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
