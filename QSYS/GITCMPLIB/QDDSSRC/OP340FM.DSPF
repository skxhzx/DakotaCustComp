     A*%%TS  SD  20060103  135518  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP340FM   PIR Options - Interleaving
     A*  28 October 2005
     A*  Jeff Janhnke
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    10/28/05  MLB  5.00
     A*      - Created.
     A*
500aAA*    10/28/05  MLB  5.00a
     A*      - Enh: Add te RUNNER entries (Y/N).
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20060103  135518  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP340.01'
     A                                  1 28'Interleaving Flag Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Allow interleaving Replenishments -
     A                                      with Putaways. . .'
     A            W1INTL         1A  B  6 57
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 66'Y=Yes, N=No'
     A                                  7  4'Maximum number of Replenishments a-
     A                                      llowed . . . . . .'
     A            W1MAXR         2Y 0B  7 57
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                  9  4'From slot must reside in following-
     A                                       area:'
     A            W1FSAB         2Y 0B 11  8
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 11 11'aisles before current aisle.'
     A            W1FSAA         2Y 0B 12  8
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 12 11'aisles after current aisle.'
     A            W1FSBB         3Y 0B 13  7
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 13 11'slots towards front of whse from c-
     A                                      urrent location.'
     A            W1FSBA         3Y 0B 14  7
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 14 11'slots towards rear of whse from cu-
     A                                      rrent location.'
     A                                 16  4'To slot must reside in following a-
     A                                      rea:'
     A            W1TSAB         2Y 0B 18  8
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 18 11'aisles before current aisle.'
     A            W1TSAA         2Y 0B 19  8
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 19 11'aisles after current aisle.'
     A            W1TSBB         3Y 0B 20  7
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 20 11'slots towards front of whse from c-
     A                                      urrent location.'
     A            W1TSBA         3Y 0B 21  7
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 21 11'slots towards rear of whse from cu-
     A                                      rrent location.'
     A*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
