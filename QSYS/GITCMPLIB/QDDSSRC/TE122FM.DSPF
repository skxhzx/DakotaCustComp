     A*%%TS  SD  19960123  081358  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TE122FM   Manual transaction maintenance - Change screen
     A*  22 January, 1996
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960123  081358  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TE122.01'
     A                                  1 26'Manual Transaction Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  4'Employee:'
     A            USEMP#    R        O  9 15REFFLD(USREC/USEMP# *LIBL/PIRUSER)
     A                                      EDTCDE(4)
     A            W1NAME        40A  O  9 21
     A                                 10  3'Task code:'
     A            PTTASK    R        O 10 14REFFLD(PTREC/PTTASK *LIBL/PIRTRAN)
     A            TKDESC    R        O 10 21REFFLD(TKREC/TKDESC *LIBL/TASK)
     A                                 12  3'Start date . . . . . . .'
     A            W1SDTE         6Y 0B 12 29EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(CS)
     A  02                                  DSPATR(PC)
     A                                 12 45'MMDDYY'
     A                                 13  3'Start time . . . . . . .'
     A            W1SHRS         2Y 0B 13 29EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(CS)
     A  03                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 13 32':'
     A            W1SMIN         2Y 0B 13 34EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(CS)
     A  04                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W1SAP          1   B 13 38
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(CS)
     A  05                                  DSPATR(PC)
     A                                 13 45'HH:MM A,P'
     A                                 15  3'End date . . . . . . . .'
     A            W1EDTE         6Y 0B 15 29EDTCDE(4)
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(CS)
     A  06                                  DSPATR(PC)
     A                                 15 45'MMDDYY'
     A                                 16  3'End time . . . . . . . .'
     A                                 16 45'HH:MM A,P'
     A            W1EHRS         2Y 0B 16 29EDTCDE(4)
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(CS)
     A  07                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                 16 32':'
     A            W1EMIN         2Y 0B 16 34EDTCDE(4)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(CS)
     A  08                                  DSPATR(PC)
     A                                      CHECK(FE)
     A            W1EAP          1   B 16 38
     A  29                                  DSPATR(RI)
     A N29                                  DSPATR(CS)
     A  09                                  DSPATR(PC)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
