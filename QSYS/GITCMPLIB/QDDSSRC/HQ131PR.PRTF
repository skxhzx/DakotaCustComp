     A****************************************************************
     A*
     A* HQ131PR   Compare client host system qty to BFC item qty
     A* 20 Nov, 2007
     A* Chuck Manny
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A** 1ST LINE
     A*
     A            UDATE          6S 0O     3EDTCDE(Y)
     A                                    55DFT('Quantity Compare Report')
     A*
     A                                   121DFT('Page:')
     A*
     A            PAGE           4S 0O   126EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A** 2ND LINE
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('HQ131')
     A                                      SPACEA(001)
     A            GBREAK       132A  O     1
     A*
     A*************************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A** 1ST LINE
     A*
     A                                    01
     A                                      DFT('Pick')
     A*
     A                                    15
     A                                      DFT('Item')
     A*
     A                                    32
     A                                      DFT('Item')
     A*
     A                                    74
     A                                      DFT('Client')
     A                                    82
     A                                      DFT('Client')
     A                                    90
     A                                      DFT('Dakota')
     A                                    99
     A                                      DFT('Dakota')
     A                                   110
     A                                      DFT('Difference')
     A*
     A                                      SPACEA(001)
     A*
     A** 2ND LINE
     A*
     A                                    01
     A                                      DFT('Slot')
     A*
     A                                    15
     A                                      DFT('Number')
     A*
     A                                    32
     A                                      DFT('Description')
     A*
     A                                    74
     A                                      DFT('Cases ')
     A                                    82
     A                                      DFT('Eaches')
     A                                    90
     A                                      DFT('Cases ')
     A                                    99
     A                                      DFT('Eaches')
     A*
     A                                      SPACEA(001)
     A*
     A** 3RD LINE
     A*
     A            GBREAK       132A  O     1
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line  1
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            R#SLOT        12A  O     1
     A            R#ITEM        15A  O    15
     A            R#DESC        30A  O    32
     A            IJTQH1         7S 0O    73EDTCDE(Z)
     A            IJTQH2         7S 0O    81EDTCDE(Z)
     A            IQSTK1         7S 0O    89EDTCDE(L)
     A            IQSTK2         5S 0O   100EDTCDE(L)
     A            GQTYCMP        8S 0O   112EDTCDE(Z)
     A*
     A*%%***********************************************************************
     A*
     A* Print Detail Line 2
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            R#SLOT        12A  O     1
     A            R#ITEM        15A  O    15
     A            R#DESC        30A  O    32
     A                                    74'HOST ITEM NOT FOUND IN ITEM QTY '
     A                                   106'FILE. DELETE'
     A*
     A*%%***********************************************************************
     A*
     A* Print Detail Line 3
     A*
     A          R DETAIL3
     A                                      SPACEB(001)
     A            R#SLOT        12A  O     1
     A            R#ITEM        15A  O    15
     A            R#DESC        30A  O    32
     A                                    74'DAKOTA ITEM NOT FOUND IN HOST '
     A                                   104'COMPARE FILE'
     A*
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('HQ131: End of Report ')
     A*
