     A*%%***********************************************************************
     A*
     A*   ER513DPR  Employee Activity Report Slot,Date
     A*   07 Sept, 2001
     A*   Hemant Kapadia
     F*
416 AF*    09/07/01  HNK  4.16
     F*      - Created.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +42
     A                                      DFT('Item/Slot Activity Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O   +43
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   124
     A                                      DFT('ER513D')
     A            WHSE          30A  O    52
     A                                      SPACEB(001)
     A                                    58'  By Slot/Date'
     A                                      SPACEB(001)
     A                                    50'Item:'
     A                                      SPACEB(001)
     A            ITEM          15A  O    56
     A                                    74'Slot:'
     A            DISP          12A  O    80
     A                                    93'Task:'
     A            TASK           6A  O    99
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('From Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('To Slot')
     A                                      UNDERLINE
     A                                    27
     A                                      DFT('  Date  ')
     A                                      UNDERLINE
     A                                    36
     A                                      DFT('Item     ')
     A                                      UNDERLINE
     A                                    47
     A                                      DFT('Description         ')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('Type    ')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('Qty  ')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('UOM  ')
     A                                      UNDERLINE
     A                                    91
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    99
     A                                      DFT('Employee            ')
     A                                      UNDERLINE
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A  92        WKDISP    R        O     1
     A                                      REFFLD(WKREC/WKDISP *LIBL/ER513A)
     A  92        WKDSP2    R        O    14
     A                                      REFFLD(WKREC/WKDSP2 *LIBL/ER513A)
     A  91        W1DATE         6S 0O    27
     A                                      EDTCDE(Y)
     A            W1ITEM        10A  O    36
     A            W1DESC        20A  O    47
     A            W1TYPE        10A  O    68
     A            W1QTY          5S 0O    79
     A                                      EDTCDE(K)
     A            WKUOM     R        O    85
     A                                      REFFLD(WKREC/WKUOM *LIBL/ER513A)
     A            WKTRN#    R        O    91EDTCDE(3)
     A                                      REFFLD(WKREC/WKTRN# *LIBL/ER513A)
     A            WKEMP#    R        O    99EDTCDE(3)
     A                                      REFFLD(WKREC/WKEMP# *LIBL/ER513A)
     A            W1NAME        20A  O   105
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'NO RECORDS MATCH ENTERED CRITERIA'
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('ER513D: End of Report')
     A*
