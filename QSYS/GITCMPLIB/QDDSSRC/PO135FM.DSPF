     A*%%TS  SD  19991008  143610  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO135FM    PO Receiving Log Entries - Display screen
     A*  08 October 1999
     A*  Max Blacknik
     A*
     A*  PO135FM - 4.13a
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PO135.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 28'Item Receiving Log Entries'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            W1WHSE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            W1DESC        30A  O  1  8
     A                                  2  2'Item number .'
     A            WOITEM        65A  O  2 16
     A                                  3  2'Purchase order .'
     A            W1PO           9   O  3 19
     A            W1VNAM        40   O  3 29
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1RCDT         6Y 0O 11  6EDTCDE(Y)
     A            W1RCTM         6Y 0O 11 15EDTWRD('0 :  :  ')
     A            W1DEV         10A  O 11 24
     A            W1USER        10A  O 11 35
     A            W1RQTY        17A  O 11 46
     A            W1RCST        16A  O 11 64
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9  7'Receive'
     A                                      DSPATR(HI)
     A                                  9 15'Receive'
     A                                      DSPATR(HI)
     A                                  9 35'Received'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15' Time   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'Workstn   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35'   By     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Qty Received     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Status         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A                                  3  1' '
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
