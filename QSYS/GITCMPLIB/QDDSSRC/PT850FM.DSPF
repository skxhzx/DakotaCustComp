     A*%%TS  SD  20060823  162511  JOSED       REL-V5R3M0  5722-WDS
     *----------------------------------------------------------------
     *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     *   BFC Software, Inc.
     *   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     *   West Chicago, IL  60185
     *   (630) 562-0375
     *----------------------------------------------------------------
     *
     *  PT850FM      Reset Selection Transaction to Open status.
     *  25 August 2006
     *  Jose Duque
     *
     *----------------------------------------------------------------
     *  Revisions:
     *
510 A*    08/25/06  JD   5.10
     *      - Created.
     *
     *----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A*----------------------------------------------------------------
     A          R SCREEN1
     A*%%TS  SD  20060823  162511  JOSED       REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'PT850.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 22'Reset Transaction to Open Status'
     A                                      DSPATR(HI)
     A                                  9  5'Transaction number . . . . .'
     A            W1TRN#         7Y 0B  9 35
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A                                 12  5'Multi-Tran User ID . . . . .'
     A            W1EMP#         5Y 0B 12 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A                                 12 52'User processing multi-trans'
     A*
     A                                 23  5'F3=Exit  F12=Cancel'
     A*----------------------------------------------------------------
     A          R CLRSCR
     A*----------------------------------------------------------------
     A                                      CLRL(*ALL)
     A*
     A*----------------------------------------------------------------
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*----------------------------------------------------------------
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*----------------------------------------------------------------
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*
     A*----------------------------------------------------------------
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
