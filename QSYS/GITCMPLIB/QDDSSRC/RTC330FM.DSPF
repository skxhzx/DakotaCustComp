     A*%%TS  SD  20190816  104144  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A* *******************************************
     A* *****  RSTDSP value must be set *YES  *****
     A* *******************************************
     A*----------------------------------------------------------------
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  RTC330FM   RTC  -  RF Mini Screen
     A*  25 March, 2019
     A*  Roxanne Harrison
     A*
     A*
     A*  Revisions
730 AA*    03/25/19  LMC  7.30
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19970205  123210  ROCKY       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'RTC330.01'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Dept'
     A            W1DEPT        10A  B  4  7
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(UL)
     A  02
     AON20                                  DSPATR(PC)
     A N22                                  DSPATR(HI)
     A                                  7  2'F3/F12=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20190325  130708  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CA07
     A                                      OVERLAY
     A                                  1  2'RTC330.02'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  2  2'1. Scope    3. Item'
     A                                  3  2'2. Age      4. Slot'
     A                                  5  2'Selection:'
     A            W2CTYP         1A  B  5 13CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20190816  104144  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.03'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Scope Id:'
     A            W3SCOPE        3Y 0B  3 12EDTCDE(4)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  20190816  104144  LYNNM       REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                      CF04
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'RTC330.04'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Page'
     A            W4PAGE         5Y 0B  3  8EDTCDE(4)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN5
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'RTC330.05'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Adj Code:'
     A            W5ADJCD        2A  B  3 12CHECK(ER)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN6
     A*%%TS  SD  20190516  100424  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.06'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  2  2'Oldest:'
     A            W6DAYS        10A  O  2 10
     A                                  4  2'Minimum age:'
     A            W6DAY          3Y 0B  4 14EDTCDE(4)
     A                                  5  2'Display Order:'
     A            W6ORDER        1A  B  5 17CHECK(ER)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A                                  5 19'1-Oldest/Slot'
     A                                  6 19'2-Slot'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN7
     A*%%TS  SD  20190516  102149  LYNNM       REL-V7R2M0  5770-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.07'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W7ITEM        15A  B  3  8
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN8
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.08'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Slot:'
     A            W8DISP        12A  B  3  8
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN9
     A*%%TS  SD  20190520  144727  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.09'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Goto Slot:'
     A            W9SLOT        12A  O  3 13
     A                                  5  2'Scan Slot:'
     A            W9DISP        12A  B  5 13
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN10
     A*%%TS  SD  20190401  093007  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.10'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  2  2'Lcns:'
     A            W10LCNS       15A  O  2  8
     A                                  3  2'Slot:'
     A            W10SLOT       12A  O  3  8
     A                                  4  2'Item:'
     A            W10ITEM       31A  O  4  8
     A            W10ITEMA      15A  O  5  8
     A            W10ITEMB      15A  O  6  8
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN11
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.11'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W11ITEM       15A  O  3  8
     A                                  4  2'UCI:'
     A            W11UCI        50A  B  4  8
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                  5  2'LCNS:'
     A            W11LCNS       15A  B  5  8
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.12'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Qty:'
     A            W12QTY1        5  0B  3  7EDTCDE(4)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A            W12UOM1        5   O  3 13
     A            W12STK1        6   O  3 19
720aAA                                      DSPATR(HI)
     A            W12QTY2        3  0B  4  7EDTCDE(4)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N35N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W12UOM2        5   O  4 13
     A  92                                  DSPATR(ND)
     A            W12STK2        6   O  4 19
720aAA                                      DSPATR(HI)
     A  92                                  DSPATR(ND)
     A            W12QTY3        3  0B  5  7EDTCDE(4)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A N36N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W12UOM3        5   O  5 13
     A  93                                  DSPATR(ND)
     A            W12STK3        6   O  5 19
720aAA                                      DSPATR(HI)
     A  93                                  DSPATR(ND)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN13
     A                                      CA07
     A                                      CF08
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.13'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'CtWt:'
     A            W13CWGT        7Y 2B  3  8EDTCDE(4)
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A            W13LBKG        3   O  3 17
     A  40                              7  2'F7=Kilogram F8=Override F12=Cancel'
     A                                      COLOR(BLU)
     A N40                              7  2'F7=LBS F8=Override F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN14
     A*%%TS  SD  20190521  122826  LYNNM       REL-V7R2M0  5770-WDS
     A                                      CF06
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.14'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  7  2'Enter=Continue F6=Add'
     A                                      COLOR(BLU)
     A                                  3  2'No more items found in'
     A                                  4  2'Slot '
     A            W14DISP       12A  O  4  9
     A          R SCREEN15
     A*%%TS  SD  20190411  104444  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.15'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  3  2'Add Lic:'
     A                                  4  5'-OR-'
     A                                  5  6'UCI:'
     A            W15LCNS       15   B  3 11
     A            W15UCI        50   B  5 11
     A          R SCREEN16
     A*%%TS  SD  20190506  092325  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.16'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  3  2'Add Item'
     A                                  4  5'-OR-'
     A                                  5  6'Mfg#:'
     A            W16ITEM       15A  B  3 12
     A            W16MFG#       15A  B  5 12
     A          R SCREEN17
     A*%%TS  SD  20190521  090323  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.17'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  7  2'Enter=Continue'
     A                                      COLOR(BLU)
     A                                  3  2'No more slots for'
     A            W17TEXT       30A  O  4  2
     A          R SCREEN18
     A*%%TS  SD  20190423  111814  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CA07
     A                                      OVERLAY
     A                                  1  2'RTC330.18'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  2  2'Count does not match System Count'
     A                                  3  2'3 = Scan All UCI'
     A                                  5  2'Selection:'
     A            W18TYPE        1A  B  5 13CHECK(ER)
     A                                      DSPATR(PR)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23                                  DSPATR(UL)
     A                                  7  2'Enter to scan  F12=Recount'
     A                                      COLOR(BLU)
     A                                  4  2'4 = Empty - No UCI to Scan'
     A          R SCREEN19
     A                                      CF22
     A  98                                  ALARM
     A                                      OVERLAY
     A                                  1  2'RTC330.19'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  2  2'# of UCI:'
     A            UCIQTY         5  0O  2 12EDTCDE(Z)
     A                                  4  1'Scan UCI'
     A            W19UCI       100A  B  4 10
     A                                      CHECK(ER)
     A N21                                  DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  7  1'F12=Cancel  F22=Done'
     A                                      COLOR(BLU)
     A          R SCREEN20
     A*%%TS  SD  20190429  144410  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'RTC330.20'
     A                                  1 12'RT Count'
     A                                      DSPATR(HI)
     A                                  3  2'Qty:'
     A            W12QTY1        5Y 0B  3  7EDTCDE(4)
     A                                      DSPATR(PR)
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A            W12UOM1        5A  O  3 13
     A            W12STK1        6A  O  3 19DSPATR(HI)
     A            W12QTY2        3Y 0B  4  7EDTCDE(4)
     A                                      DSPATR(PR)
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A N35N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A            W12UOM2        5A  O  4 13
     A  92                                  DSPATR(ND)
     A            W12STK2        6A  O  4 19DSPATR(HI)
     A  92                                  DSPATR(ND)
     A            W12QTY3        3Y 0B  5  7EDTCDE(4)
     A                                      DSPATR(PR)
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A N36N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A            W12UOM3        5A  O  5 13
     A  93                                  DSPATR(ND)
     A            W12STK3        6A  O  5 19DSPATR(HI)
     A  93                                  DSPATR(ND)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
