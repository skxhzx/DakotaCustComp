     A*%%TS  SD  19960430  153113  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT112FM   Item maintenance - Change screen
     A*  09 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
213 AF*    05/22/96  HNK  2.13
     F*      - Size of W1HIGH field changed to 2.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960430  153113  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TH112.01'
     A                                  1 34'Item Tie High'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A            DUMMY1         1A  B  3  4DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  4  4'Warehouse . . . . . . . .'
     A            WHCODE         3S 0O  4 31
     A                                  4 35'-'
     A            WHDESC        30A  O  4 37
     A                                  5  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  5 31
     A            W1DESC        30A  O  5 48
     A                                  6  4'Package size  . . . . . .'
     A            W1PLEN         5Y 2O  6 31EDTCDE(3)
     A                                  6 38'X'
     A            W1PWDT         5Y 2O  6 40EDTCDE(3)
     A                                  6 47'X'
     A            W1PHGT         5Y 2O  6 49EDTCDE(3)
     A                                  7  4'Pallet  . . . . . . . . .'
     A            W1PAL          6A  O  7 31
     A                                  8  4'Overhang  . . . . . . . .'
     A            W1HANG         3Y 2O  8 31EDTCDE(3)
     A                                  9  4'Tie X High  . . . . . . .'
     A            W1TIE          3Y 0B  9 31EDTCDE(3)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 35'X'
213 MA            W1HIGH         2Y 0B  9 37EDTCDE(3)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 34'xxxxx'
     A                                 14 34'x   x'
     A                                 15 34'x A x'
     A                                 16 34'x   x'
     A                                 17 34'x   x'
     A                                 18 34'xxxxx'
     A                                 14 45'xxxxxxxxxxx'
     A                                 15 45'x    B    x'
     A                                 16 45'xxxxxxxxxxx'
     A            R1            10A  O 12 12
     A            R2            10A  O 13 12
     A            R3            10A  O 14 12
     A            R4            10A  O 15 12
     A            R5            10A  O 16 12
     A            R6            10A  O 17 12
     A            R7            10A  O 18 12
     A            R8            10A  O 19 12
     A            R9            10A  O 20 12
     A            R10           10A  O 21 12
     A                                 14 68'XXXXX'
     A                                 15 68'X C X'
     A                                 16 68'XXXXX'
     A            ALONG          5Y 2O 16 27EDTCDE(4)
     A            AWIDE          5Y 2O 19 33EDTCDE(4)
     A            CLONG          5Y 2O 15 74EDTCDE(4)
     A            CWIDE          5Y 2O 17 68EDTCDE(4)
     A            BLONG          5Y 2O 17 48EDTCDE(4)
     A            BWIDE          5Y 2O 15 57EDTCDE(4)
     A                                 11 12'1234567890'
     A                                 12 10'1'
     A                                 13 10'2'
     A                                 14 10'3'
     A                                 15 10'4'
     A                                 16 10'5'
     A                                 17 10'6'
     A                                 18 10'7'
     A                                 19 10'8'
     A                                 20 10'9'
     A                                 21  9'10'
     A            PLDESC    R        O  7 48REFFLD(PLREC/PLDESC *LIBL/PALLET)
     A            PLLEN     R        O  7 38REFFLD(PLREC/PLLEN *LIBL/PALLET)
     A                                      EDTCDE(3)
     A                                  7 42'X'
     A            PLWDTH    R        O  7 44REFFLD(PLREC/PLWDTH *LIBL/PALLET)
     A                                      EDTCDE(3)
     A            W1PLWD         3Y 0O 22 15EDTCDE(3)
     A                                 22 12'|-'
     A                                 22 19'--|'
     A                                 12  5'---'
     A                                 13  5' | '
     A                                 14  5' | '
     A                                 15  5' | '
     A            W1PLLN         3Y 0O 17  5EDTCDE(3)
     A                                 19  5' | '
     A                                 20  5' | '
     A                                 21  5'---'
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960416  163237  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TH112.02'
     A                                  1 34'Item Tie High'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            WHCODE         3S 0O  6 31
     A                                  6 35'-'
     A            WHDESC        30A  O  6 37
     A                                  7  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  7 31
     A                                  8  4'Description . . . . . . .'
     A            W1DESC        30A  O  8 31
     A                                 22  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  19960416  163237  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'TH112.03'
     A                                  1 34'Item Tie High'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            WHCODE         3S 0O  6 31
     A                                  6 35'-'
     A            WHDESC        30A  O  6 37
     A                                  7  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  7 31
     A                                  8  4'Description . . . . . . .'
     A            W1DESC        30A  O  8 31
     A                                 22  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
