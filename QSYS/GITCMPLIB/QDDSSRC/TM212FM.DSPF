     A*%%TS  SD  20000515  153805  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TM212FM   Truck compartment code maintenance - Change
     A*  15 May 2000
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A* Revisions
     A*
     A*   05/15/00  DAS  4.14
     A*     - Renamed from TP112FM because EPM uses TP programs.
     A*
416 AA*   02/08/02  MLB  4.16
     A*     - Added new field to screen 1 W1DRDP.
     A*     - Added warehouse / description to screen 1.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20000515  153805  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
416 AA                                  4  2'Warehouse:'
416 AA            $PWHSE         3Y 0O  4 13EDTCDE(3)
416 AA                                  4 17'-'
416 AA            WHDESC        30A  O  4 19
416 DA*                                 5  2'Type changes, press Enter.'
416 MA                                  6  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'TM212.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  4'Code . . . . . . . . .'
     A                                  9  4'Description  . . . . .'
     A            W1CODE         1A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A            W1DESC        15A  B  9 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416 AA                                 10  4'Dock door department .'
416 AA            W1DRDP         5A  B 10 28
416 AA  23                                  DSPATR(RI)
416 AA N23N90                               DSPATR(UL)
416 AA  03                                  DSPATR(PC)
416 AA  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 22'Truck Compartment Code Table Maint-
     A                                      enance'
     A                                      DSPATR(HI)
     A          R CLRSCR
     A                                      CLRL(*ALL)
