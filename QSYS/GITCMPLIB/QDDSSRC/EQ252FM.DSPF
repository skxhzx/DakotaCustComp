     A*%%TS  SD  19940714  131348  HEMANT      REL-V2R1M1  5738-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  19940714  131348  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'EQ252.01'
     A                                  1 25'Equipment Misc Question Maint'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Type:'
     A            WOTDSC        30A  O  3  8
     A                                  7  4'Status (A/I) . . . . .'
     A            W1STAT         1A  B  7 28
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  4'Question Level . . . .'
     A            W1ULVL         4A  B  8 28
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Question type  . . . .'
     A            W1UTYP         3A  B  9 28
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Check in/out (I/O) . .'
     A            W1IO           1A  B 10 28
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
