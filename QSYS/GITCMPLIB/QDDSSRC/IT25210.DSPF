     A*%%TS  SD  20090819  091437  BFC         REL-V5R4M0  5722-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A********************************************************************
     A*
415 AA* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
610 AA* NOTE: ADDED 3RD SET OF SCREENS FOR 16 X 20 DISPLAY.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS IF NECESSARY.
     A*
     A*
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A**************************************************************************
     A*  IT252FM   Inventory transfer -  RF Mini Screen
     A*  Revisions:
     A*
     A*    11/25/99  DAS  4.13
     A*     - Moved lines down to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
413 AA*    02/07/00  HNK  4.13
     A*     - Screen 1 Slot number removed. Item number added instead.
     A*     - Screen 2 added for slot number input.
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCREEN2.
     A*      - Both of the above added for api program APICVTSLT
     A*
415 AA*    05/16/01  DAS  4.15
     A*     - Created SCREEN61-62, a 16-line version of SCREEN1-2.
     A*     - Created message screens for 16-line displays. They all
     A*       start with M16.
     A*
415aAA*    07/11/01  DAS  4.15a
     A*     - Revised SCREEN61-62 to only clear 16 lines.
     A*
416aAA*    12/03/01  MLB  4.16a
     A*     - Added new screen formats for verifying zero slots.
     A*     - Added SCREEN3 AND SCREEN63.
416CAA*    04/16/02  RH   4.16C
     A*     - Screen 2 adjustment code added if item # changed.
416nAA*    07/25/02  RH   4.16n
     A*     - Screen63, shorten 'Physically empty' so it will fit
     A*       on 20 position screen.
     A*
417 AA*    03/05/03  MLB  4.17
     A*     - Fix: Change line# to 11 from 5 for adj. text/field
     A*       on SCREEN 62 format.
417aAA*    05/20/03  MLB  4.17a
     A*      - Enh: Added lot/license plate scan window (WDWLLP)
     A*        Added error message subfile to window (WDWERR)
     A*
500 AA*   05/26/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   08/10/06  RH   5.10a
     A*     - Enh: Add pick slot to screen 2 and screen62
600aAA*   05/22/09  DAS  6.00a
     A*     - Renamed to IT25201.
     A*     - Added F9=OSS to gernerate OSS label/slot/lcns
     A*       to screen 2 and 62.
     A*     - Changed F20=Accept warning to F20=WOvr.
     A*     - Added window to display when F9 is pressed.
610aAA*   08/04/09  RH   6.10a
     A*     - Renamed to IT25202.
     A*     - Added SCREEN71, 72, 73 for 16 X 20 display.
610bAA*   08/19/09  RH   6.10b
     A*     - Change Y,N to output field to allow 1,9 in place of Y/N
530aAA*   10/21/09  RH   5.30a
     A*     - Renamed to IT25203.
     A*     - Enh: Add cw field to screen 2 if item changed and is cw.
530cAA*   11/05/09  RH   5.30c
     A*     - Enh: Add pick slot to screen 1
650aAA*   07/24/13  RH   6.50a
     A*     - Enh: Rename to IT25204, add F8=Slot to IT252.02
650b A*   07/30/13  RH   6.50b
     A*     - Fix: Add F8=Slot to IT252.62 and spanish
700a A*   05/05/15  LMC  7.00a
     A*     - Enh: Rename to IT25205
     A*     - Enh: Added the ability to protect normal qty entry
700b A*   03/07/16  RH   7.00b
     A*     - Enh: Rename to IT25206 Add W1PEND if item has pending qty
700c A*   07/27/16  RH   7.00c
     A*     - Enh: Rename to IT25207 Add F10=Excp to create Item Except
710a A*   05/23/17  LMC  7.10a
     A*     - Enh: Rename to IT25208 Add Print field to outside storage
     A*       screen - will be defaulted to N for base code.
720  A*   03/26/18  KDE  7.20
     A*     - Enh: ADD F11 TO TOGGLE BETWEEN PICK SLOTS
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000204  175958  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT252.01'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W1ITEM        55A  O  2  8
     A            W1LBLS        12A  O  3  2DSPATR(HI)
     A            W1ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Aval:'
     A            W1CURQ        30A  O  4  8
700b A            W1PEND         4A  O  4 40
700b A  42                                  DSPATR(RI)
     A                                  5  2'Qty:'
     A            W1QT01         5Y 0B  5  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O  5 14
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B  5 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O  5 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 36
     A  93                                  DSPATR(ND)
     A                                  6  2'Item:'
413 AA            W1ITM         15   B  6  8
413 AA  28                                  DSPATR(RI)
413 AA N28                                  DSPATR(UL)
413 AA  08                                  DSPATR(PC)
413 AA N28                                  DSPATR(HI)
530c A            W1PSLT        12A  O  6 25DSPATR(HI)
740aMA                                  7  2'F4=Lookup F12=Cancel F20=Acpt warn'
740aDA*                                 7  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
650aAA                                      CF08
600aAA                                      CF09
700CAA                                      CF10
720 AA                                      CF11
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT252.02'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W2ITEM        55A  O  2  8
     A            W2LBLS        12A  O  3  2DSPATR(HI)
     A            W2ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Qty:'
     A            W2CURQ        30A  O  4  8
     A                                  5  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B  5 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                  5 16'Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B  5 20DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
     A                                  6  2'To slot:'
     A            W2DISP        12A  B  6 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A            W2PSLT        12A  O  6 24DSPATR(HI)
650aDA*                                 7  2'F9=Oss F20=WOvr F12=Cancel'
700cDA*                                 7  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
700cMA                                  7  2'F8=Slt F9=Oss F10=Excp F20=WOvr +
720 DA*                                      F12=Cncl'
720 AA                                       F11=NextPck F12=Cncl'
     A                                      COLOR(BLU)
416aAA*
416aAA          R SCREEN3
416aAA  98                                  ALARM
416aAA                                      CLRL(07)
416aAA                                      CSRLOC(WOROW      WOCOL)
416aAA                                      OVERLAY
416aAA            WOROW          3S 0H
416aAA            WOCOL          3S 0H
416aAA                                  1  2'IT252.03'
416aAA                                  1 12'Inv. Transfer'
416aAA                                      DSPATR(HI)
416aAA                                  1 26'- Verify Slot'
416aAA                                      DSPATR(HI)
416aAA                                  2  2'Item:'
416aAA            W3ITEM        55A  O  2  8
416aAA            W3LBLS        12A  O  3  2DSPATR(HI)
416aAA            W3ITMS        55A  O  3 15DSPATR(HI)
416nD**                                 5  2'Slot physically empty . .'
416nD**           W3ZERO         1A  B  5 28CHECK(ER)
416nMA                                  5  2'Slot phys. empty:'
416nMA            W3ZERO         1A  B  5 20CHECK(ER)
416aAA                                      DSPATR(UL)
416aAA  01                                  DSPATR(PC)
416aAA  21                                  DSPATR(RI)
416aAA                                  5 23'(Y/N)'
416aAA                                  7  2'F12=Cancel'
416aAA                                      COLOR(BLU)
417aAA*
417aAA* Lot/License plate scan window.
417aAA          R WDWLLP
417aAA                                      OVERLAY
417aAA                                      WINDOW(1 2 6 20)
417aAA                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
417aAA                                      ENTER)
417aAA                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
417aAA                                      l') *BOTTOM)
417aAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
417aAA                                          '))
417aAA            WWTXT         18   O  2  1
417aAA            WWLLP#        15   B  3  1DSPATR(HI)
417aAA                                      DSPATR(UL)
417aAA*
417aAA*****************************************************************
417aAA*  Window message subfile formats.
417aAA          R MSGRECW                   SFL
417aAA                                      SFLMSGRCD(5)
417aAA            #MSGK                     SFLMSGKEY
417aAA            #PGMQ                     SFLPGMQ
417aAA*
417aAA*    Window Error message subfile control record
417aAA*
417aAA          R MSGCTLW                   SFLCTL(MSGRECW)
417aAA                                      WINDOW(WDWLLP)
417aAA  98                                  ALARM
417aAA                                      LOCK
417aAA                                      OVERLAY
417aAA                                      SFLSIZ(2) SFLPAG(1)
417aAA N97                                  SFLCLR
417aAA  97                                  SFLDSP
417aAA  97                                  SFLEND
417aAA*
417aAA          R MSGCLRW
417aAA                                      WINDOW(WDWLLP)
417aAA                                      OVERLAY
417aAA                                      LOCK
417aAA                                      SLNO(5)
417aAA                                      CLRL(1)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRBTM
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(16)
     A          R CLRSCR
     A                                      CLRL(*ALL)
415 AA*****************************************************************
415 AA*
415 AA* 16-line screens
415 AA*
     A          R SCREEN61
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
415aDA**                                    CLRL(*ALL)
415aMA                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT252.61'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W61ITM        65A  O  3  8
     A            W61DSC        30A  O  4  8
     A            W1LBLS        12A  O  5  2DSPATR(HI)
     A            W1ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
700b A            W1PEND         4A  O  9  8
700b A N42                                  DSPATR(ND)
700b A  42                                  DSPATR(RI)
     A                                 10  2'Qty:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O 10 14
700aAA  41                                  DSPATR(ND)
     A            W1QT02         3Y 0B 10 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 10 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 10 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 10 36
     A  93                                  DSPATR(ND)
     A                                 12  2'Item:'
     A            W1ITM         15   B 12  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A          R SCREEN62
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
650bAA                                      CF08
600aAA                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT252.62'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A                                  3  2'Item:'
     A            W62ITM        65A  O  3  8
     A            W62DSC        30A  O  4  8
     A            W2LBLS        12A  O  5  2DSPATR(HI)
     A            W2ITMS        55A  O  5 15DSPATR(HI)
     A            W2PSLT        12A  O  6  2DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Qty:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                 12  2'Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B 12  6DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
530aDA*                                12  2'To slot:'
530aDA*           W2DISP        12A  B 12 11CHECK(ER)
530aMA                                 13  2'To slot:'
530aMA            W2DISP        12A  B 13 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
650bDA*                                15  2'F9=OSS F20=WOvr F12=Cancel'
700cDA*                                15  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
700cMA                                 15  2'F8=Slt F9=Oss F10=Excp F20=WOvr +
700cMA                                       F12=Cncl'
     A                                      COLOR(BLU)
416aAA*
416aAA          R SCREEN63
416AAA  98                                  ALARM
416aMA                                      CLRL(15)
416aAA                                      OVERLAY
416aAA                                  1  2'IT252.63'
416aAA                                  1 12'Inv. Transfer'
416aAA                                      DSPATR(HI)
416aAA                                  2 13'Verify Slot'
416aAA                                      DSPATR(HI)
416aAA                                  3  2'Item:'
416aAA            W63ITM        65A  O  3  8
416aAA            W63DSC        30A  O  4  8
416aAA            W3LBLS        12A  O  5  2DSPATR(HI)
416aAA            W3ITMS        55A  O  5 15DSPATR(HI)
416aAA                                  7  2'From:'
416aAA            WOFROM        12A  O  7  8
416nDA*                                 9  2'Slot physically empty . .'
416nDA*           W3ZERO         1A  B  9 28CHECK(ER)
416nMA                                  9  2'Slot phys. empty:'
416nMA            W3ZERO         1A  B  9 20CHECK(ER)
416aAA                                      DSPATR(UL)
416aAA  01                                  DSPATR(PC)
416aAA  21                                  DSPATR(RI)
610bDA*                                 9 23'(Y,N)'
610bMA            WOYN           5A  O  9 23
416aAA                                 15  2'F12=Cancel'
416aAA                                      COLOR(BLU)
416aAA*
610aAA*
610a A          R SCREEN71
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT252.71'
     A                                  1 11'Inv Transfer'
     A                                      DSPATR(HI)
     A**                                3  2'Item:'
     A            W61ITM        65A  O  3  1
     A**          W61DSC        30A  O  4  8
     A            W71DS1        15A  O  4  1
     A            W71DS2        15A  O  5  1
     A            W1LBLS        12A  O  6  2DSPATR(HI)
     A            W1ITMS        55A  O  6 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Aval:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Qty:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            W1UM1          5A  O 10 14
     A            W1QT02         3Y 0B 11  8EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 11 14
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 12  8EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 12 14
     A  93                                  DSPATR(ND)
     A                                 13  1'Item'
     A            W1ITM         15   B 13  6
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
610a A          R SCREEN72
     A*%%TS  SD  20090522  113402  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT252.72'
     A                                  1 12'Inv. Transfer'
     A                                      DSPATR(HI)
     A**                                3  2'Item:'
     A            W62ITM        65A  O  3  1
     A*           W62DSC        30A  O  4  8
     A            W72DS1        15A  O  4  1
     A            W72DS2        15A  O  5  1
     A            W2LBLS        12A  O  6  2DSPATR(HI)
     A            W2ITMS        55A  O  6 15DSPATR(HI)
     A            W2PSLT        12A  O  7  2DSPATR(HI)
     A                                  8  2'From:'
     A            WOFROM        12A  O  8  8
     A                                  9  2'Aval:'
     A            W1CURQ        30A  O  9  8
     A                                 10  2'Qty:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Adj. code:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 13DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                 12  2'Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B 12  6DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
530aDA*                                12  1'To slot'
530aDA*           W2DISP        12A  B 12  9CHECK(ER)
530aMA                                 13  1'To slot'
530aMA            W2DISP        12A  B 13  9CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 15  2'F9=OSS F20=WOvr F12=Cancel'
     A                                      COLOR(BLU)
610aAA*
     A          R SCREEN73
     A*%%TS  SD  20090819  091437  BFC         REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT252.73'
     A                                  1 12'Inv Transfer'
     A                                      DSPATR(HI)
     A                                  2  8'Verify Slot'
     A                                      DSPATR(HI)
     A*                                 3  2'Item:'
     A            W63ITM        65A  O  3  1
     A*           W63DSC        30A  O  4  8
     A            W73DS1        15A  O  4  1
     A            W73DS2        15A  O  5  1
     A            W3LBLS        12A  O  6  2DSPATR(HI)
     A            W3ITMS        55A  O  6 15DSPATR(HI)
     A                                  7  2'From:'
     A            WOFROM        12A  O  7  8
     A                                  9  2'Slot phys empty:'
     A            W3ZERO         1A  B  9 19CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
610bDA*                                10  2'(Y,N)'
     A            WOYN           5A  O 10  2
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 12  2' '
610aAA*
415 AA*****************************************************************
415 AA*
415 AA* Error Message Subfile Records for 16-line screens
415 AA*
415 AA*    Error Message Subfile Record
415 AA*
415 AA          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
415 AA*
415 AA*    Error Message Subfile Control Record
415 AA*
415 AA          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
415 AA          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 A *----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  20000204  175958  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT252.01'
     A                                  1 12'Trans. de Inv.'
     A                                      DSPATR(HI)
     A                                  2  2'Prdc:'
     A            W1ITEM        55A  O  2  8
     A            W1LBLS        12A  O  3  2DSPATR(HI)
     A            W1ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Disp:'
     A            W1CURQ        30A  O  4  8
     A                                  5  2'Ctd:'
     A            W1QT01         5Y 0B  5  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O  5 14
700aAA                                      DSPATR(ND)
     A            W1QT02         3Y 0B  5 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O  5 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B  5 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O  5 36
     A  93                                  DSPATR(ND)
     A                                  6  2'Prdc:'
413 AA            W1ITM         15   B  6  8
413 AA  28                                  DSPATR(RI)
413 AA N28                                  DSPATR(UL)
413 AA  08                                  DSPATR(PC)
413 AA N28                                  DSPATR(HI)
     A                                  7  2'F12=Cancelar F20=Aceptar aviso'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
     A          R SCRN2SP
     A*%%TS  SD  20090522  115344  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
650bAA                                      CF08
600aAA                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT252.02'
     A                                  1 12'Trans. de Inv.'
     A                                      DSPATR(HI)
     A                                  2  2'Prdc:'
     A            W2ITEM        55A  O  2  8
     A            W2LBLS        12A  O  3  2DSPATR(HI)
     A            W2ITMS        55A  O  3 15DSPATR(HI)
     A                                  4  2'Ctd:'
     A            W2CURQ        30A  O  4  8
     A                                  5  2'Aju codigo:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B  5 14DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                  5 16'Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B  5 20DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
     A                                  6  2'A slot :'
     A            W2DISP        12A  B  6 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
650bDA*                                 7  2'F9=OSS F20=WOvr F20=Cancelar'
700cDA*                                 7  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
700cMA                                  7  2'F8=Slt F9=Oss F10=Excp F20=WOvr +
700cMA                                       F12=Cncl'
     A                                      COLOR(BLU)
500 A *----------------------------------------------------------------
500 AA          R SCRN3SP
416aAA  98                                  ALARM
416aAA                                      CLRL(07)
416aAA                                      CSRLOC(WOROW      WOCOL)
416aAA                                      OVERLAY
416aAA            WOROW          3S 0H
416aAA            WOCOL          3S 0H
416aAA                                  1  2'IT252.03'
416aAA                                  1 12'Trans. de Inv'
416aAA                                      DSPATR(HI)
416aAA                                  1 26'-Verificar Slot'
416aAA                                      DSPATR(HI)
416aAA                                  2  2'Prdc:'
416aAA            W3ITEM        55A  O  2  8
416aAA            W3LBLS        12A  O  3  2DSPATR(HI)
416aAA            W3ITMS        55A  O  3 15DSPATR(HI)
416nD**                                 5  2'Slot vacio fisicamente. .'
416nD**           W3ZERO         1A  B  5 28CHECK(ER)
416nMA                                  5  2'Slot vacio fsico:'
416nMA            W3ZERO         1A  B  5 20CHECK(ER)
416aAA                                      DSPATR(UL)
416aAA  01                                  DSPATR(PC)
416aAA  21                                  DSPATR(RI)
416aAA                                  5 23'(Y/N)'
416aAA                                  7  2'F12=Cancelar'
416aAA                                      COLOR(BLU)
415 AA*****************************************************************
415 AA*
415 AA* 16-line screens
415 AA*
500 A *----------------------------------------------------------------
     A          R SCRN61SP
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
415aDA**                                    CLRL(*ALL)
415aMA                                      CLRL(15)
     A                                      OVERLAY
     A                                  1  2'IT252.61'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Prdc:'
     A            W61ITM        65A  O  3  8
     A            W61DSC        30A  O  4  8
     A            W1LBLS        12A  O  5  2DSPATR(HI)
     A            W1ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'De  :'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Disp:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Ctd:'
     A            W1QT01         5Y 0B 10  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
700aAA  41                                  DSPATR(PR)
700aAA  41                                  DSPATR(ND)
     A            W1UM1          5A  O 10 14
700aAA                                      DSPATR(ND)
     A            W1QT02         3Y 0B 10 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W1UM2          5A  O 10 26
     A  92                                  DSPATR(ND)
     A            W1QT03         3Y 0B 10 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W1UM3          5A  O 10 36
     A  93                                  DSPATR(ND)
     A                                 12  2'Prdc:'
     A            W1ITM         15   B 12  8
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A                                 15  2'F12=Cancelar F20=Aceptar aviso'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
     A          R SCRN62SP
     A*%%TS  SD  20090522  115344  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
650bAA                                      CF08
600aAA                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(15)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT252.62'
     A                                  1 12'Trans. de Inv'
     A                                      DSPATR(HI)
     A                                  3  2'Prdc:'
     A            W62ITM        65A  O  3  8
     A            W62DSC        30A  O  4  8
     A            W2LBLS        12A  O  5  2DSPATR(HI)
     A            W2ITMS        55A  O  5 15DSPATR(HI)
     A                                  7  2'De  :'
     A            WOFROM        12A  O  7  8
     A                                  8  2'Disp:'
     A            W1CURQ        30A  O  8  8
     A                                 10  2'Ctd:'
     A            W2CURQ        30A  O 10  8
     A                                 11  2'Aju codigo:'
     A N59                                  DSPATR(ND)
     A            W2ADJ          2A  B 11 14DSPATR(UL)
     A N59                                  DSPATR(ND)
     A N59                                  DSPATR(PR)
     A  02 59                               DSPATR(PC)
     A  22                                  DSPATR(RI)
530a A                                 12  2'Wt:'
530a A N60                                  DSPATR(ND)
530a A            W2CWT          9Y 2B 12  6DSPATR(UL)
530a A N60                                  DSPATR(ND)
530a A N60                                  DSPATR(PR)
530a A  02 60                               DSPATR(PC)
530a A  23                                  DSPATR(RI)
530a A                                      EDTCDE(4)
530aDA*                                12  2'A slot :'
530aDA*           W2DISP        12A  B 12 11CHECK(ER)
530aMA                                 13  2'A slot :'
530aMA            W2DISP        12A  B 13 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
650bDA*                                15  2'F9=OSS F20=WOvr F12=Cancelar'
700cDA*                                15  2'F8=Slt F9=Oss F20=WOvr F12=Cancel'
700cMA                                  7  2'F8=Slt F9=Oss F10=Excp F20=WOvr +
700cMA                                       F12=Cncl'
     A                                      COLOR(BLU)
500 A *----------------------------------------------------------------
500 AA          R SCRN63SP
416AAA  98                                  ALARM
416aMA                                      CLRL(15)
416aAA                                      OVERLAY
416aAA                                  1  2'IT252.63'
416aAA                                  1 12'Trans. de Inv'
416aAA                                      DSPATR(HI)
416aAA                                  2 13'Verificar slot'
416aAA                                      DSPATR(HI)
416aAA                                  3  2'Prdc:'
416aAA            W63ITM        65A  O  3  8
416aAA            W63DSC        30A  O  4  8
416aAA            W3LBLS        12A  O  5  2DSPATR(HI)
416aAA            W3ITMS        55A  O  5 15DSPATR(HI)
416aAA                                  7  2'De  :'
416aAA            WOFROM        12A  O  7  8
416nDA*                                 9  2'Slot vacio fisicamente. .'
416nDA*           W3ZERO         1A  B  9 28CHECK(ER)
416nMA                                  9  2'Slot vacio fsico:'
416nMA            W3ZERO         1A  B  9 20CHECK(ER)
416aAA                                      DSPATR(UL)
416aAA  01                                  DSPATR(PC)
416aAA  21                                  DSPATR(RI)
610bDA*                                 9 23'(Y,N)'
610bMA            WOYN           5A  O  9 23
416aAA                                 15  2'F12=Cancelar'
416aAA                                      COLOR(BLU)
     A*****************************************************************
     A          R WEXTSTG
     A*%%TS  SD  20090522  114956  DAVE        REL-V6R1M0  5761-WDS
     A                                      WINDOW(1 2 6 26)
     A                                      WDWTITLE((*TEXT 'Off Site  Storage'-
     A                                      ) *LEFT)
     A                                  2  2'Storage'
     A            W9STG          1A  B  2 15
710aDA*                                 3  2'Printer'
710aDA*           W9DEV         10A  B  3 15DSPATR(RI)
710aAA                                  3  2'Print (Y/N)'
710aAA            W9PRT          1A  B  3 15DSPATR(RI)
710aMA                                  4  2'Printer'
710aMA            W9DEV         10A  B  4 15DSPATR(RI)
     A                                  5  2'F12=Cancel'
     A                                      DSPATR(HI)
600aAA          R WDUMMY                     ASSUME
600aAA                                  1  2' '
