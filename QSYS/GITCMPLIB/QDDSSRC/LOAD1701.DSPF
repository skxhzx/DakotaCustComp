     A*%%TS  SD  20070912  100919  BFCTEST1    REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD17FM  Truck Load Module - Comments.
     A*  January 19, 2007
     A*  Jose Duque
     A*
     A*
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. LOAD17FM COPY AND NAME LOAD1701
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*  06/30/10  JCJ  5.30
     A*    - Increase W1TRL# field size from 6 to 8.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    ##/##/##  XXX-XXX
     A*      Text . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A*         Select comment type screen
     A*----------------------------------------------------------------
     A          R WINDW1
      *%%TS  SD  20070912  085909  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      WINDOW(1 15 21 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
      *
     A                                  2  2'Loadr17.01'
      *
     A                                  2 16'Comment Detail Entry'
     A                                      DSPATR(HI)
      *
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  4 20'Trailer:'
530 D ***         W1TRL#         6A  O  4 29DSPATR(HI)
530 MA            W1TRL#         8A  O  4 29DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  4 40'Door#:'
     A            W1DOOR         3A  O  4 47DSPATR(HI)
     A                                      DSPATR(UL)
      *
     A                                  6  2'Comment by:'
      *
     A            W1LDR#         5Y 0O  6 15DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
      *
     A            W1USER        28A  O  6 23DSPATR(HI)
      *
     A                                  9  2'Select type:'
      *
     A                                  9 16'1=Select'
      *
     A                                 11  2'Transportation . . . '
     A            W1TRNP         2A  B 11 24
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
      *
     A                                 12  2'Trailer Condition  .'
     A            W1TRLR         2A  B 12 24
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
      *
     A                                 13  2'Loader . . . . . . .'
     A            W1LODR         2A  B 13 24
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
      *
     A                                 14  2'Loading Delays . . .'
     A            W1DLAY         2A  B 14 24
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
      *
     A                                 15  2'Other  . . . . . . .'
     A            W1OTHR         2A  B 15 24
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
      *
     A                                 20  2'F3=Exit'
     A                                      COLOR(BLU)
      *
     A                                 20 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A*         Create route comment screen
     A*----------------------------------------------------------------
     A          R WINDW2
     A*%%TS  SD  20070912  100919  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      WINDOW(1 15 21 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A*
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A*
     A                                  2  2'Loadr17.02'
     A*
     A            W1TITL        26A  O  2 16DSPATR(HI)
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 20'Trailer:'
530 D ***         W1TRL#         6A  O  4 29DSPATR(HI)
530 MA            W1TRL#         8A  O  4 29DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 40'Door#:'
     A            W1DOOR         3A  O  4 47DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  6  2'Comment by:'
     A*
     A            W1LDR#         5Y 0O  6 15DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            W1USER        28A  O  6 23DSPATR(HI)
     A                                  8  2'Date:'
     A            W1DATE         6Y 0O  8  8DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  8 21'Time:'
     A            W1TIME         6Y 0O  8 27DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A  26 27                           9  2'Runner #:'
     A N26 27                           9  2'Loader #:'
     A  27        W1EMP#         5Y 0B  9 12
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A*
     A  27 31                           9 21'Dept:'
     A  27 31     W1DEPT         5A  B  9 27
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A N25                             11  2'Reason'
     A N25                             12  2'code:'
     A N25        W1RCDE         2A  B 12  9
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A*
     A N25        W1REAS        35A  O 12 14DSPATR(HI)
     A*
     A                                 14  2'Comment:'
     A            W1COM1        35A  B 14 11
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A            W1COM2        35A  B 15 11
     A N23N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A            W1COM3        35A  B 16 11
     A N23N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A            W1COM4        35A  B 17 11
     A N23N90                               DSPATR(HI)
     A  90                                  DSPATR(PR)
     A*
     A  28 90                          19 42'More'
     A*
     A N28 90                          19 42'Bottom'
     A*
     A                                 20  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A                                 20 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A  90                             20 24'Enter=Comments'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
