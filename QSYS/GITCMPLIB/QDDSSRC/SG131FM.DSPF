     A*%%TS  SD  20140410  154635  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG131FM    Staging Import View Purchase Order Detail
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            PIDPO     R        O  8  9REFFLD(PIDREC/PIDPO *LIBL/IMPSPOD)
     A            PIDSEQ    R        O  8 19REFFLD(PIDREC/PIDSEQ *LIBL/IMPSPOD)
     A                                      EDTCDE(Z)
     A            PIDITM    R        O  8 27REFFLD(PIDREC/PIDITM *LIBL/IMPSPOD)
     A            S1QTY         15A  O  8 43
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140409  152842  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Purchase Order Detail Info'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'   PO#   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19' Seq '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 27'     Item      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 43'  Ordered Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20140410  154635  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG131.02'
     A                                  1 25'View Purchase Order Detail Info'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Full Detail Information'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            PIDWHS    R        O  5 18REFFLD(PIDREC/PIDWHS *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            PIDBATID  R        O  5 54REFFLD(PIDREC/PIDBATID *LIBL/IMPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  6  2'PO #  . . . . .'
     A            PIDPO     R        O  6 18REFFLD(PIDREC/PIDPO *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            PIDADDTS  R        O  6 54REFFLD(PIDREC/PIDADDTS *LIBL/IMPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  7  2'PO Seq #  . . .'
     A            PIDSEQ    R        O  7 18REFFLD(PIDREC/PIDSEQ *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            PIDSTSTS  R        O  7 54REFFLD(PIDREC/PIDSTSTS *LIBL/IMPSPO-
     A                                      D)
     A                                      DSPATR(HI)
     A                                  8  2'Update Type . .'
     A            PIDTYP    R        O  8 18REFFLD(PIDREC/PIDTYP *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                  9  2'Status/Message '
     A            PIDSTATUS R        O  9 18REFFLD(PIDREC/PIDSTATUS *LIBL/IMPSP-
     A                                      OD)
     A                                      DSPATR(HI)
     A            PIDTEXT   R        O  9 24REFFLD(PIDREC/PIDTEXT *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 13  2'Item  . . . . .'
     A            PIDITM    R        O 13 18REFFLD(PIDREC/PIDITM *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 13 34'Qty Ordered . . . .'
     A            S2QTY         20A  O 13 54DSPATR(HI)
     A                                 14  2'Vendor Item . .'
     A            PIDVIT    R        O 14 18REFFLD(PIDREC/PIDVIT *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 14 34'Spcl Order Code . .'
     A            PIDSPC    R        O 14 54REFFLD(PIDREC/PIDSPC *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 15  2'Ovr Item Desc .'
     A            PIDODS    R        O 15 18REFFLD(PIDREC/PIDODS *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 16  2'PO Message  . .'
     A            PIDPOM    R        O 16 18REFFLD(PIDREC/PIDPOM *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 17  2'Misc 1  . . . .'
     A            PIDMS1    R        O 17 18REFFLD(PIDREC/PIDMS1 *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 18  2'Misc 2  . . . .'
     A            PIDMS2    R        O 18 18REFFLD(PIDREC/PIDMS2 *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 19  2'Misc 3  . . . .'
     A            PIDMS3    R        O 19 18REFFLD(PIDREC/PIDMS3 *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 20  2'Misc 4  . . . .'
     A            PIDMS4    R        O 20 18REFFLD(PIDREC/PIDMS4 *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 21  2'Misc 5  . . . .'
     A            PIDMS5    R        O 21 18REFFLD(PIDREC/PIDMS5 *LIBL/IMPSPOD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
