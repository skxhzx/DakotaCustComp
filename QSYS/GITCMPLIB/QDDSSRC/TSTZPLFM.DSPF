     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2023 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TSTZPL      Test ZPL printing
     A*
     A*  Revisions
     A*
770 AA*    04/30/23  DAS  7.70
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CF07
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTZPL.1'
     A                                  1 25'Test ZPL Printing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Printer Device:'
     A            W1DEV         10A  B  3 18
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  3 31'Form:'
     A            W1FORM        10A  B  3 37
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  4  2'Path:'
     A            W1PATH        70   B  4  8 CHECK(LC)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A**                                4  2'ZPL:'
     A                                  5  2'ZPL.+....1....+....2....+....3....-
     A                                      +....4....+....5....+....6....+....-
     A                                      7....+'
     A            W1ZPL1        75   B  6  2 CHECK(LC)
     A            W1ZPL2        75   B  7  2 CHECK(LC)
     A            W1ZPL3        75   B  8  2 CHECK(LC)
     A            W1ZPL4        75   B  9  2 CHECK(LC)
     A            W1ZPL5        75   B 10  2 CHECK(LC)
     A            W1ZPL6        75   B 11  2 CHECK(LC)
     A            W1ZPL7        75   B 12  2 CHECK(LC)
     A            W1ZPL8        75   B 13  2 CHECK(LC)
     A            W1ZPL9        75   B 14  2 CHECK(LC)
     A            W1ZPL10       75   B 15  2 CHECK(LC)
     A            W1ZPL11       75   B 16  2 CHECK(LC)
     A            W1ZPL12       75   B 17  2 CHECK(LC)
     A            W1ZPL13       75   B 18  2 CHECK(LC)
     A            W1ZPL14       75   B 19  2 CHECK(LC)
     A            W1ZPL15       75   B 20  2 CHECK(LC)
     A            W1ZPL16       75   B 21  2 CHECK(LC)
     A                                 23  2'F3=Exit F7=Examples F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTZPL.3'
     A                                  1 25'Test ZPL Printing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'~WC  - Print Configuration Label'
     A                                  4  2'~CC^ - Change to ^ command'
     A                                  5  2'^PWa - Print Width (a = dots) '
     A                                  6  2'^JC  - Set Media Sensor Calibratio-
     A                                      n (This prints 10+ labels)'
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7  2'^MNa,b - Media Tracking (N=Continu-
     A                                      ous, Y/W/M=Non-continuous) (b not u-
     A                                      sed) '
     A                                  8  2'~PH  - Slew to home postion (print-
     A                                       blank label) (Can also use ^PH)'
     A                                  9  2'^LHx,y  - Home Position'
     A                                 10  2'^LLy - Label length (y=dots)'
     A                                 11  2'^Afo,h,w - Font f=Font code, o=Ori-
     A                                      entation (Normal/Inverted), h=Heigh-
     A                                      t, w=Width'
     A                                 12  2'^BYw,r,h - Barcode dflt  w=module -
     A                                      wdth in dots, r=wide/narrow bar rat-
     A                                      io, h=hgt'
     A                                 13  2'^B3o,e,h,f,g - Code39  o=orientati-
     A                                      on (N/I), e=chk dgt (Y/N), h=height-
     A                                      , f, g)'
     A                                 14  2'^B7o,h,s,c,r,t = PDF417 o=orient, -
     A                                      h=hgt, s=security, c=columns, r=row-
     A                                      s, t=trunc'
     A                                 15  2'^GBw,h,t,c,r  - Box w=wdth, h=hght-
     A                                      , t=thickness, c=color(B,W), r=roun-
     A                                      ding(0-8)'
     A                                 16  2'^PQq,p,r,o,e - Print Qty q=qty'
     A                                 17  2'^FR - Field reverse'
     A                                 18  2'^POa - Print orientation  a=orient-
     A                                      ation (N/I)'
     A          R SCREEN3
     A*%%TS  SD  20080909  163344  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTZPL.2'
     A                                  1 25'Test ZPL Printing'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Printer device:'
     A            W2DEV         10A  B  4 18
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  4 31'Form:'
     A            W2FORM        10A  B  4 37
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                 12  1'Path:'
     A            W2PATH        80   B 13  1
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                 23  2'F3=Exit F7=Examples F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
