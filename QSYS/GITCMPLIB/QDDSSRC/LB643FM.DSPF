     A*%%TS  SD  20041014  180814  BFCTEST1    REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  OR643FM    Reprint labels audit. (one whse) - Display
     A*  October 14, 2004   / Cheney
     A*
     A*  Revisions
     A*
700aAA*     11/18/16  NLK  7.00
     A*       - Cheney OR643 to LB643 -- OR643 is used by BFC
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A                                      CA21
     A*----------------------------------------------------------------
     A          R TITLE
     A*----------------------------------------------------------------
     A*%%TS  SD  19940706  172535  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
700aD *                                 1**2'OR643.01'
700aMA                                  1  2'LB643.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'List Display'
     A                                      DSPATR(HI)
     A                                  1 27'Reprint Labels Audit View/Print '
     A                                      DSPATR(HI)
     A*
     A*
     A* POSITION TO RECORD
     A*----------------------------------------------------------------
     A          R POSTNREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19940711  163426  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            PTDMY1        78A  O  1  2
     A            LGWHSE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
     A                                  3 43'Change to warehouse . . .'
     A            PTINP1         3Y 0B  3 70EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  4 43'Position to log date  . .'
     A            PTINP2         6Y 0B  4 70EDTCDE(Y)
     A  02                                  DSPATR(PC)
     A            BLANKS        80A     5  1
     A*
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*----------------------------------------------------------------
     A*%%TS  SD  20041014  180814  BFCTEST1    REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W1USER    R        O 11  2REFFLD(LGUSER LABELOG)
     A            W1EMP#    R        O 11 18REFFLD(LGEMP# LABELOG)
     A            W1RTE     R        O 11 28REFFLD(LGRTE LABELOG)
     A            W1ZONE    R        O 11 37REFFLD(LGZONE LABELOG)
     A            W1GDTE         8Y 0O 11 46EDTCDE(Y)
     A            W1TIME    R        O 11 59REFFLD(LGTIME LABELOG)
     A                                      EDTWRD('  :  :  ')
     A*
     A*
     A*----------------------------------------------------------------
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20041014  135541  BFCTEST1    REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2' User Id  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                 10 16'Employee#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                 10 28'Route#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                 10 37'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                 10 46' Log Date '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 59'Log Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*
     A* ERROR MESSAGE SUBFILE RECORD
     A*----------------------------------------------------------------
     A          R MSGREC                    SFL
     A*----------------------------------------------------------------
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*
     A*
     A* ERROR MESSAGE SUBFILE CONTROL RECORD
     A*----------------------------------------------------------------
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*----------------------------------------------------------------
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*
     A* COMMAND LINES
     A*----------------------------------------------------------------
     A          R CMDREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*
     A* 'MORE.../BOTTOM' MESSAGE LINE
     A*----------------------------------------------------------------
     A          R MORREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*
     A*
     A* CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*----------------------------------------------------------------
     A          R CLR0306
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
