     A*%%TS  SD  20060427  092434  BFCTEST1    REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LU137FM  Print deleted flag info - options
     A*  June 26, 2006
     A*  Jose Duque
     A*
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20060427  092434  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A*
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A*
     A                                  1  2'LU137.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A*
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AO 24                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A                                 11  4'Start date . . . . . . . . .'
     A            W1FMDT         6Y 0B 11 34EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 48'Date (MMDDYY)'
     A*
     A                                 12  4'End date . . . . . . . . . .'
     A            W1TODT         6Y 0B 12 34EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 48'Date (MMDDYY)'
     A*
     A                                 17  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 17 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 23  4'F3=Exit   F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 24'Print Deleted Flag Information'
     A                                      DSPATR(HI)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
