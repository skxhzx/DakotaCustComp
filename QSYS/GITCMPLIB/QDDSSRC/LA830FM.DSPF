     A*%%TS  SD  20090714  042236  CHUCKM      REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LA830FM   Change Active License
     A*  06 May 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
600 AA*    06/30/09  CWM  6.00
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SCREEN1
     A*%%TS  SD  20090710  063043  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'LA830.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 73DSPATR(HI)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 30'Change Active License'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A N21N90                               DSPATR(HI)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'License . . . . . . . . . . .'
     A            W1LCNS        15A  B  9 35
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A N22N90                               DSPATR(HI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A          R SCREEN2
     A*%%TS  SD  20090714  042236  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LA830.02'
     A                                  1 35'Active License'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            W2WHSE         3Y 0O  3 20
     A                                  4  4'License . . . .'
     A            W2LCNS        15A  B  4 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  8 57'Display .'
     A            W2DISP        12A  B  8 68DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  8  4'Item. . . . . .'
     A            W2ITEM        15A  B  8 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  3  4'Warehouse . . .'
     A            W2QTY1         5Y 0B  6 54DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A            W2QTY2         5Y 0B  6 64DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A            W2QTY3         5Y 0B  6 75DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                 23  2'F3=Exit  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  6  4'Org License . .'
     A            W2OLCN        15A  B  6 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  8 37'Item Type. . . .'
     A            W2ITYP         1A  B  8 54DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2SITM        15A  B  9 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2SITY         1A  B  9 54DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  9  4'Slot Item . . .'
     A                                  9 37'Slot Item Type .'
     A            W2CODE         1A  B  4 71DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2AREA        15A  B 11 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 37'Dept. .'
     A            W2WHDP         5A  B 13 45DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 11 37'Aisle .'
     A            W2AISL         3A  B 11 45DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2LOC          3Y 0B 11 55DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                 11 60'Lvl .'
     A            W2LVL          2Y 0B 11 66DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                 11 69'H/S. . .'
     A            W2HAND         2A  B 11 78DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2POS          3S 0B 13 77DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 51'Pick Priority .'
     A            W2PRTY         1Y 0B 13 67DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13  4'Order/Ord Seq .'
     A            W2ORD          7Y 0B 13 20DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A            W2SEQ          5Y 0B 13 30DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                  6 37'License Qty. .1)'
     A                                  6 61'2)'
     A                                  6 72'3)'
     A                                 11 49'Loc .'
     A                                 11  4'Area. . . . . .'
     A                                  3 37'Date Entered . .'
     A            W2ENTD         8S 0B  3 54DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4 37'Expiration Date.'
     A            W2EXPD         8S 0B  4 54DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 16  4'Timestamp . . .'
     A            W2CHGT          Z  O 16 20
     A            W2CHGU          Z  O 17 20
     A                                 17  4'UC Timestamp. .'
     A                                 19  4'Chg Pgm/Emp . .'
     A            W2CGPG        10A  O 19 20
     A            W2CGEM         5S 0O 19 32
     A                                 20  4'Employee User .'
     A            W2EMUS        10A  O 20 20
     A                                 20 51'Current User. .'
     A            W2CRUS        10A  O 20 67
     A                                 13 69'Plt Pos'
     A                                  4 64'Code .'
     A                                 19 51'Job User. . . .'
     A                                 16 51'Change Job. . .'
     A                                 17 51'Chg Job Nbr . .'
     A            W2CJUS        10A  O 19 67
     A            W2CGJB        10A  O 16 67
     A            W2CGJN        10A  O 17 67
     A                                  9 57'Base Slot'
     A            W2BASE        12A  B  9 68DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOTYPE         6A  O  2 73DSPATR(HI)
     A*
     A          R SCREEN3
     A*%%TS  SD  20090710  063043  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LA830.03'
     A                                  1 32'License Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 73DSPATR(HI)
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A                                 11 27'Press F10 to Confirm Update'
     A            W3WHSE         3S 0O  4 18
     A                                  4  4'Warehouse . .'
     A                                  4 23'License . .'
     A            W3LCNS        15A  O  4 35
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A*%%TS  SD  20010424  095629  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
