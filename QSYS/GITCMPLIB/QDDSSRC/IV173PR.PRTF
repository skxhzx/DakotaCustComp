     A****************************************************************
     A*
     A* IV173PR   Inventory variance re-count sheet by Aisle
     A* 24 September, 2006
     A* Roxanne Harrison
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Client Revisions:
     A*
VIP AA*    09/25/06  JCJ  VIP A
     A*      - Add Column1 for VIP
CWa AA*    03/25/08  RH   CWa
     A*      - Add detail2 for City Wholesale
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*                                     SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A                                    57'Variance: '
     A*                                     SPACEB(001)
     A            PMVAR          5S 0     +1EDTCDE(3)
     A                                    57'Aisle: '
     A                                      SPACEB(001)
     A            $PASL          4A       +1
     A          R SBHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1'Warehouse:'
     A            WKSWHS    R             +1REFFLD(WKSWHS IV151)
     A                                      EDTCDE(3)
     A* 93                                75
     A*                                     '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A                                     1'Department:'
     A            WKSWHD    R             +1REFFLD(WKSWHD IV151)
     A* 92                                75
     A*                                     '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A                                     1'Section:'
     A            WKSSTP    R             +1REFFLD(WKSSTP IV151)
     A  91                                75
     A                                      '(Continued)'
     A*
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
416b A                                    11'Excp/'
     A                                    69'- Current -'
     A                                    82'- Counted -'
     A                                   108'- Re-count -'
     A*
     A                                     1'Slot#'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    11'Pick'
     A                                      UNDERLINE
     A                                    17'Page #'
     A                                      UNDERLINE
     A                                    23'Item #'
     A                                      UNDERLINE
     A                                    39'Description'
     A                                      UNDERLINE
     A                                    70'Norm'
     A                                      UNDERLINE
     A                                    77'Bk1'
     A                                      UNDERLINE
     A                                    82'Norm'
     A                                      UNDERLINE
     A                                    89'Bk1'
     A                                      UNDERLINE
416a A*                                   80'Bk2'
416a A*                                     UNDERLINE
     A                                    93'Team      '
     A                                      UNDERLINE
     A                                   104'Norm'
     A                                      UNDERLINE
     A                                   119'Bk1'
     A                                      UNDERLINE
416a A*                                  118'Bk2'
416a A*                                     UNDERLINE
VIP  A*
VIP  A*
VIP  A*
VIP  A* Print column headings
VIP  A*
VIP  A          R COLUMN1
VIP  A                                      SPACEB(001)
VIP  A                                      SPACEA(001)
VIP  A                                    11'Excp/'
VIP  A                                    69'- Current -'
VIP  A                                    82'- Counted -'
VIP  A                                   108'- Re-count -'
VIP  A*
VIP  A                                     1'Slot#'
VIP  A                                      UNDERLINE
VIP  A                                      SPACEB(001)
VIP  A                                    11'Pick'
VIP  A                                      UNDERLINE
VIP  A                                    17'Page #'
VIP  A                                      UNDERLINE
VIP  A                                    23'Item #'
VIP  A                                      UNDERLINE
VIP  A                                    39'Description'
VIP  A                                      UNDERLINE
VIP  A                                    70'Norm'
VIP  A                                      UNDERLINE
VIP  A                                    77'Bk1'
VIP  A                                      UNDERLINE
VIP  A                                    82'Norm'
VIP  A                                      UNDERLINE
VIP  A                                    89'Bk1'
VIP  A                                      UNDERLINE
VIP  A*                                   80'Bk2'
VIP  A*                                     UNDERLINE
VIP  A                                    93'Team      '
VIP  A                                      UNDERLINE
VIP  A                                   104'Count1'
VIP  A                                      UNDERLINE
VIP  A                                   119'Count2'
VIP  A                                      UNDERLINE
VIP  A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKSDSP    R              1REFFLD(WKSDSP IV173)
     A            WOPICK         1A       14
416b A            WOEXCP         1A       15
     A            WKSPAG    R             17REFFLD(WKSPAG IV173)
     A                                      EDTCDE(3)
     A N81        WKSITM    R             23REFFLD(WKSITM IV173)
     A N81        IDESC         30A       38
     A            WKSTK1    R             66REFFLD(WKSQT1 IV173)
     A                                      EDTCDE(K)
     A            WKSTK2    R             74REFFLD(WKSQT2 IV173)
     A                                      EDTCDE(K)
     A*           WKSQT3    R             79REFFLD(WKSQT3 IV173)
     A*                                     EDTCDE(K)
     A            WKSQT1    R             79REFFLD(WKSQT1 IV173)
     A                                      EDTCDE(K)
     A            WKSQT2    R             89REFFLD(WKSQT2 IV173)
     A                                      EDTCDE(K)
     A            WKSEBY    R             93REFFLD(WKSEBY IV173)
     A            WKBQT1        12A      104
     A                                      UNDERLINE
     A            WKBQT2        12A      119
     A                                      UNDERLINE
     A*           WKBQT3        12A      118
     A*                                     UNDERLINE
     A            IFLG1          1A      131
     A            IFLG2          1A      132
     A*
     A* Print detail line
CWa  A          R DETAIL2
CWa  A                                      SPACEA(001)
CWa  A N81        PDESC         15A       38
     A*
     A* Print detail line
     A*
     A          R DETAIL3
     A                                      SPACEA(001)
     A            WKBQT1    R            103REFFLD(WKBQT1)
     A                                      UNDERLINE
     A            WKBQT2    R            118REFFLD(WKBQT2)
     A                                      UNDERLINE
     A*
     A          R DETAIL4
     A                                      SKIPB(062)
     A                                      SPACEA(001)
     A                                     1
     A                                      'First Re-counted by'
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
     A                                    +5
510  A                                      'Second Re-counted by'
510  A                                    +2
510  A                                      '          '
510  A                                      UNDERLINE
510  A                                    +5
     A                                      'Keyed by'
     A                                    +2
     A                                      '          '
     A                                      UNDERLINE
     A*                                   +5
     A*                                     'Verified by'
     A*                                   +2
     A*                                     '          '
     A*                                     UNDERLINE
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A* Skip 2 Line
     A*
     A          R SKIP2
     A                                      TEXT('SKIP TWO LINE')
     A                                      SPACEA(002)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'IV173: End of Report '
     A*
