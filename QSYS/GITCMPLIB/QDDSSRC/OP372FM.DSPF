     A*%%TS  SD  20070612  113114  DAVE        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP372FM   PIR Options - Check digit entry
     A*  12 June 2007
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    06/12/07  DAS  5.10
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070612  113114  DAVE        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'OP372.01'
     A                                  1 28'Check Digit Entry Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Digit reading method to use  . . .'
     A            W1RORD         1A  B  5 40
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 52'S=Single method, R=Random'
     A                                  7  6'For Single choose 1, for Random ch-
     A                                      oose 2 or more ...'
     A            WOLCR         20A  O  9  8
     A                                  9 29':'
     A            W1LCR          1A  B  9 31
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WORCL         20A  O  9 38
     A                                  9 59':'
     A            W1RCL          1A  B  9 61
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            WOLRC         20A  O 10  8
     A  81                                  DSPATR(ND)
     A                                 10 29':'
     A  81                                  DSPATR(ND)
     A            W1LRC          1A  B 10 31
     A  81                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A            WORLC         20A  O 10 38
     A  81                                  DSPATR(ND)
     A                                 10 59':'
     A  81                                  DSPATR(ND)
     A            W1RLC          1A  B 10 61
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A            WOCLR         20A  O 11  8
     A  81                                  DSPATR(ND)
     A                                 11 29':'
     A  81                                  DSPATR(ND)
     A            W1CLR          1A  B 11 31
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A            WOCRL         20A  O 11 38
     A  81                                  DSPATR(ND)
     A                                 11 59':'
     A  81                                  DSPATR(ND)
     A            W1CRL          1A  B 11 61
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A                                 13  4'Code to read . . . . . . . . . . .'
     A  82                                  DSPATR(ND)
     A            W1RCOD         1A  B 13 40
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  82                                  DSPATR(ND)
     A                                 13 52'S=Single code, R=Random'
     A  82                                  DSPATR(ND)
     A                                 15  6'For Single choose 1, for Random ch-
     A                                      oose 2 or more ...'
     A  82                                  DSPATR(ND)
     A            WORCD1        23   O 17  8
     A  82                                  DSPATR(ND)
     A                                 17 32':'
     A  82                                  DSPATR(ND)
     A            W1RCD1         1   B 17 34
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  82                                  DSPATR(ND)
     A            WORCD2        23   O 18  8
     A  83                                  DSPATR(ND)
     A                                 18 32':'
     A  83                                  DSPATR(ND)
     A            W1RCD2         1   B 18 34
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  83                                  DSPATR(ND)
     A            WORCD3        23   O 19  8
     A  84                                  DSPATR(ND)
     A                                 19 32':'
     A  84                                  DSPATR(ND)
     A            W1RCD3         1   B 19 34
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  84                                  DSPATR(ND)
     A                                 21  9'New values will take affect when-
     A                                       the voice servers are restarted'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
