     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OR610PPR   Truck build - Exception Report - Customer Pick Slot
     A*  11 September 2005
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*
     A* Revisions
     A*
500 AA*   09/11/05  MLB  5.00
     A*     - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +31
     A                                      'Pick Customer Orders - Allocation -
     A                                      Exception Report'
     A                                   125
     A                                      'Page'
     A            PAGE           3S 0O   130
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            HEDLN2        60A  O   +26
     A                                   125
     A                                      'OR610P'
     A*
     A          R HEAD2
     A*
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                   120
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    71
     A                                      'Warehouse'
     A                                    +2
     A                                      'Slot'
     A                                     1
     A                                      SPACEB(001)
     A                                      'Order #'
     A                                    10
     A                                      'Customer name'
     A                                    43
     A                                      'Item'
     A                                    60
     A                                      'Ordered'
     A                                    71
     A                                      'Allocated'
     A                                    +2
     A                                      'Allocated'
     A                                    +2
     A                                      'Message'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*
     A                                      SPACEB(001)
     A            OHCORD    R        O     1
     A                                      REFFLD(ODREC/ODORD *LIBL/ORDD)
     A                                      EDTCDE(3)
     A            P1CNAM        30A  O    +2
     A            ODITEM    R        O    +3
     A                                      REFFLD(ODREC/ODITEM *LIBL/ORDD)
     A            P1QORD        10A  O    +2
     A            P1QALC        10A  O    +1
     A            P1QPCK        10A  O    +1
     A            P1MSG         40A  O    +1
     A*
     A          R DETAIL2
     A*
     A                                      SPACEB(001)
     A            DSCLN         80A  O     1
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'OR610P: End of Report '
     A                                      SPACEB(002)
     A*
