     A*%%TS  SD  20070914  085639  ROCKY       REL-V5R4M0  5722-WDS
     A*****************************************************************
     A*
     A*  IT951FM  Receipts lookup by item
     A*  24 May, 2005
     A*  Hemant Kapadia
     A*
     A*  Revisions:
510 AA*    11/13/06  RH   5.10
     A*     - Enh: Added Usy by date, Shelf life, Dist & User days
510baA*    11/20/06  RAH   5.10b
     A*     - Enh: Changed to NOT use fields from POMFR file.
510caA*    09/14/07  RH    5.10c
     A*     - Enh: Add qty remaining field when put away is to multiple
     A*       slots.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'IT951.01'
     A                                  1 31'Receipts Lookup by Item'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20050525  110312  HEMANT      REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1        78A  O  1  2
     A                                  2 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A            W1DESC        40   O  3  2
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070914  085639  ROCKY       REL-V5R4M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PORPO          9A  O 11  6
     A            W1RDTE         6Y 0O 11 17EDTCDE(Y)
     A            W1EXPD         6Y 0O 11 45EDTCDE(Y)
     A            W1MFDT         6Y 0O 11 55EDTCDE(Y)
     A            PORLIC        15A  O 11 65
     A            W1QORD         5Y 0O 11 27EDTCDE(Z)
     A            W1QRCV         5Y 0O 11 33EDTCDE(Z)
510c A            W1QRMN         5Y 0O 11 39EDTCDE(Z)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20070914  085639  ROCKY       REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            FLD001         1A  O  9  3
     A                                 10  6'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Rec Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 27'Qty'
     A                                      DSPATR(HI)
     A                                 10 27'Order'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'Qty'
     A                                      DSPATR(HI)
     A                                 10 33'Recv'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
510c A                                  9 39'Qty '
510c A                                      DSPATR(HI)
510c A                                 10 39'Rmain'
510c A                                      DSPATR(HI)
510c A                                      DSPATR(UL)
     A                                 10 45'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 55'Mfg Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 65'License Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20050525  135818  HEMANT      REL-V5R3M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
510bmA            PORPO          9A  O 11  7
     A            W2VEN#        11Y 0O 11 17EDTCDE(Z)
     A            W2VENM        30A  O 11 30
510bmA            LBTRN#         7Y 0O 11 62EDTCDE(Z)
     A            LBLBL#         7Y 0O 11 71EDTCDE(Z)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20050525  135818  HEMANT      REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Vendor     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Vendor name                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'Label #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
510bmA            PORPO          9   O 11  7
     A            USEMP#         5Y 0O 11 17EDTCDE(Z)
     A            W3ENAM        30   O 11 23
510  A            W3USDT         6Y 0O 11 54EDTCDE(Y)
510  A            W3MDAY         3Y 0O 11 63EDTCDE(Z)
510  A            W3UDAY         3Y 0O 11 70EDTCDE(Z)
510  A            W3DDAY         3Y 0O 11 77EDTCDE(Z)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20061113  095428  ROCKY       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'PO Number'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 17'Empl#'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Employee name                 '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Use By  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Shelf'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'User'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Dist'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
