     A*%%TS  SD  20140120  115604  RETHA       REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  LOADR1FM Truck Loader - Route Status Summary
     A*  May 24, 2006
     A*  Jose Duque
     A*
     A*  Revisions:
     A*
530 AA*  11/12/09  RBD  5.30
     A*    - Increase w1flag field size from 2-0 to 3-0.
530bAA*  02/09/09  RH   5.30b
     A*    - Fix: Change position to , should be Route not Rt ID.
650aAA*  01/17/14  RBD  6.50a
     A*    - Enh: Added display of trailer#.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA06
     A                                      CA07
     A                                      CA05
     A                                      CA08
     A                                      CA10
     A                                      CA11
     A                                      CA12
     A                                      CA13
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A          R TITLE
     A*%%TS  SD  20060524  082853  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'LOADR1.01'
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 31'Route Status Summary'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20071005  133521  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A*
     A*>          PTDMY1        78A  O  1  2
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  1 75EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A*
530bDA*                                 2 48'Position to route id  . .'
530bMA                                  2 48'Position to route . . . .'
     A            PTINP2         5A  B  2 75
     A  02                                  DSPATR(PC)
     A  33                              3  2'HACCP Plt in exccess of'
     A                                      DSPATR(HI)
     A  33                              3 26'Red=45 Min'
     A                                      COLOR(RED)
     A*>                                    DSPATR(BL)
     A  33                              3 38'Yellow=30 Min'
     A                                      COLOR(YLW)
     A  33                              3 52'on dock.'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20140120  115604  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            LHRTID         5A  O 11 13
     A            W1STAT         8A  O 11 20
     A  31                                  COLOR(YLW)
     A  32                                  COLOR(RED)
     A  32                                  DSPATR(BL)
     A            LHTRLR         8A  O 11 29
650aMA            W1STRD         6Y 0O 11 39EDTWRD('  /  /  ')
650aDA**          LHSTRT         6Y 0O 11 40EDTWRD('  :  :  ')
     A            W1STRT         4Y 0O 11 48EDTWRD('  :  ')
650aMA            W1CLSD         6Y 0O 11 55EDTWRD('  /  /  ')
650aDA**          LHCLST         6Y 0O 11 60EDTWRD('  :  :  ')
     A            W1CLST         4Y 0O 11 64EDTWRD('  :  ')
530 DA***         W1FLAG         2Y 0O 11 71EDTCDE(Z)
     A            W1FLAG         3Y 0O 11 71EDTCDE(Z)
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A            W1DOOR         3A  O 11 77
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20140120  115604  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 13'RteID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A                                 10 20'Status '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 29'Trailer '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Door'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'Flg'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 42'Started'
     A                                      DSPATR(HI)
     A                                 10 39'  Date   Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 59'Closed'
     A                                      DSPATR(HI)
     A                                 10 55'  Date   Time '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W2STRD         6Y 0O 11 14EDTCDE(Y)
     A            LHSTRT    R        O 11 23REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2ENDD         6Y 0O 11 33EDTCDE(Y)
     A            LHENDT    R        O 11 42REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2LODT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2SUP         10   O 11 63
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 14'---- Started ----'
     A                                      DSPATR(HI)
     A                                 10 33'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'----- Ended -----'
     A                                      DSPATR(HI)
     A                                 10 52'Load Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Supervisor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
