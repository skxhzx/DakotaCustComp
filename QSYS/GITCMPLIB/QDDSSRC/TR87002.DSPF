     A*%%TS  SD  20141120  120235  RETHA       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  TR870FM   Trax Merged Routes maintenance - Display
     A*  27 December, 2011
     A*  Retha Davis
     A*
     A*  Revisions
     A*    12/27/11  RBD  6.40
     A*      - Created.
     A*
650 AA*    11/18/14  RBD  6.50
     A*      - Changed screen format to TR87001.
     A*      - Added display of multi route group Id in header when it's
     A*        part of a multi route.  Also display 'Multi Route' in the
     A*        subfile for each child designated as such, in other words,
     A*        those which exist in TTGRP.
     A*
720aAA*    01/24/18  RBD  7.20a
     A*      - Changed screen format to TR87002.
     A*      - Commented out multi route group Id for parent
     A*        and multi route flag for children.
     A*      - Fixed TITLE overlay problem.
     A*
730 AA*    12/21/19  MLB  7.30
     A*      - Enh: Revised pgm to allow lowercase entry for route
     A*        number.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'TR870.01'
     A                                  1 19'Trax Merged Routes Maintenance'
     A                                      DSPATR(HI)
     A                                  1 50'List Display'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
720aAA                                  2  2' '
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20141120  120235  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
720aDA**                                    SLNO(03)
720aMA                                      SLNO(04)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 45'Change to Warehouse . . . .'
     A            PTINP1         3Y 0B  1 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                  2  8'Parent Route . . . . .'
     A            PTRTE          5A  O  2 31
     A                                  2 45'Change to Parent Route  . .'
     A            PTINP2         5A  B  2 74
730 AA                                      CHECK(LC)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                  3  8'Parent Route Id  . . .'
     A            PTRTI          5A  O  3 31
     A                                  3 55'and Route Id  . .'
     A            PTINP3         5A  B  3 74
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
720aDA**                                4  8'Multi Route Group Id .'
720aDA**          PTGGID         5Y 0O  4 31EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20111228  141925  RETHA       REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            TTMCRT    R        O 12 12REFFLD(TTMREC/TTMCRT *LIBL/TTMERG)
     A            TTMCRI    R        O 12 26REFFLD(TTMREC/TTMCRI *LIBL/TTMERG)
720aDA**          W1MULT        11   O 12 33
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20141118  161940  RETHA       REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0009)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 11  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  8'Child Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 22'Child Route Id'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20111227  144614  RETHA       REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A            DUMMY1         1A  O  1  2
     A                                  2  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  3  2COLOR(BLU)
     A            DUMMY2         1A  O  4  2
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
