500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*****************************************************************
     A*
     A*  OR650AFM  Will call RF selection - Display
     A*  27 August 1998
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
500 AA*   06/05/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970828  062326  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'OR650A'
     A                                  1 30'Will Call RF Selection'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Position to priority  . .'
     A            PTINP2         1Y 0B  3 75EDTCDE(4)
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSCORD    R        O 11  7REFFLD(OSREC/OSCORD *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            OSTRAN    R        O 11 19REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            W1PRTY    R        O 11 35REFFLD(OSREC/OSPRTY *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            W1NDTE         6Y 0O 11 40EDTCDE(Y)
     A            OSNTIM    R        O 11 50REFFLD(OSREC/OSNTIM *LIBL/ORDSLCT)
     A                                      EDTWRD(' 0:  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  7'Customer'
     A                                      DSPATR(HI)
     A                                 10  7'Order # '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Transaction'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 30'Priority'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 40'----- Needed -----'
     A                                      DSPATR(HI)
     A                                 10 40'  Date  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 50'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSTRAN    R        O 11  9REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            OSRTE     R        O 11 21REFFLD(OSREC/OSRTE *LIBL/ORDSLCT)
     A            OSZONE    R        O 11 28REFFLD(OSREC/OSZONE *LIBL/ORDSLCT)
     A            W2PCS          5Y 0O 11 37EDTCDE(3)
     A            W2CUBE         9Y 3O 11 46EDTCDE(3)
     A            W2SWGT         9Y 2O 11 59EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Transaction'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 21'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 28'Zone  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 37'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Cube      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 59'Weight    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSTRAN    R        O 11  9REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            OSRTE     R        O 11 21REFFLD(OSREC/OSRTE *LIBL/ORDSLCT)
     A            OSZONE    R        O 11 28REFFLD(OSREC/OSZONE *LIBL/ORDSLCT)
     A            W1PRTY    R        O 11 42REFFLD(OSREC/OSPRTY *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            W1NDTE         6Y 0O 11 47EDTCDE(Y)
     A            OSNTIM    R        O 11 57REFFLD(OSREC/OSNTIM *LIBL/ORDSLCT)
     A                                      EDTWRD(' 0:  :  ')
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Transaction'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 21'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 28'Zone  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 37'Priority'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 47'----- Needed -----'
     A                                      DSPATR(HI)
     A                                 10 47'  Date  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 57'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Pick items.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R TITLESP
     A*%%TS  SD  19970828  062326  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'OR650A'
     A                                  1 30'Will Call RF Selection'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
500 AA*----------------------------------------------------------------
500 AA          R POSTNRSP
     A*%%TS  SD  19970604  101512  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  3 48'Posicion de prioridad . .'
     A            PTINP2         1Y 0B  3 75EDTCDE(4)
     A            PTDMY1        78A  O  1  2
     A                                  2 48'Cambiar a almacen . . . .'
     A            PTINP1         3Y 0B  2 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            WHCODE         3Y 0O  2  2EDTCDE(3)
     A                                  2  6'-'
     A            WHDESC        30A  O  2  8
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSCORD    R        O 11  7REFFLD(OSREC/OSCORD *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            OSTRAN    R        O 11 19REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            W1PRTY    R        O 11 35REFFLD(OSREC/OSPRTY *LIBL/ORDSLCT)
     A                                      EDTCDE(3)
     A            W1NDTE         6Y 0O 11 40EDTCDE(Y)
     A            OSNTIM    R        O 11 50REFFLD(OSREC/OSNTIM *LIBL/ORDSLCT)
     A                                      EDTWRD(' 0:  :  ')
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opc'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  7'# pedido'
     A                                      DSPATR(HI)
     A                                 10  7'de client'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Transaccion'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 30'Priorida'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 40'--- Necesitado ---'
     A                                      DSPATR(HI)
     A                                 10 40' Fecha  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 50'  Hora  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  3
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
500 AA*----------------------------------------------------------------
500 AA          R VW2SFLSP                  SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSTRAN    R        O 11  9REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            OSRTE     R        O 11 21REFFLD(OSREC/OSRTE *LIBL/ORDSLCT)
     A            OSZONE    R        O 11 28REFFLD(OSREC/OSZONE *LIBL/ORDSLCT)
     A            W2PCS          5Y 0O 11 37EDTCDE(3)
     A            W2CUBE         9Y 3O 11 46EDTCDE(3)
     A            W2SWGT         9Y 2O 11 59EDTCDE(3)
     A*
500 AA*----------------------------------------------------------------
500 AA          R VW2SFCSP                  SFLCTL(VW2SFLSP)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opc'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Transaccion'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 21'Ruta '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 28'Zona  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 37'Piezas'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Cubo      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 59'Peso      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
500 AA*----------------------------------------------------------------
500 AA          R VW3SFLSP                  SFL
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            OSTRAN    R        O 11  9REFFLD(OSREC/OSTRAN *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            OSRTE     R        O 11 21REFFLD(OSREC/OSRTE *LIBL/ORDSLCT)
     A            OSZONE    R        O 11 28REFFLD(OSREC/OSZONE *LIBL/ORDSLCT)
     A            W1PRTY    R        O 11 42REFFLD(OSREC/OSPRTY *LIBL/ORDSLCT)
     A                                      EDTCDE(4)
     A            W1NDTE         6Y 0O 11 47EDTCDE(Y)
     A            OSNTIM    R        O 11 57REFFLD(OSREC/OSNTIM *LIBL/ORDSLCT)
     A                                      EDTWRD(' 0:  :  ')
500 AA*----------------------------------------------------------------
500 AA          R VW3SFCSP                  SFLCTL(VW3SFLSP)
     A*%%TS  SD  19980828  154956  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opc'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Transaccion'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 21'Ruta '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 28'Zona  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 37'Priorida'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 47'--- Necesitado ---'
     A                                      DSPATR(HI)
     A                                 10 47' Fecha  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 57'  Hora  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R OPTRECSP
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
500 AA*----------------------------------------------------------------
500 AA          R CMDRECSP
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
500 AA*----------------------------------------------------------------
500 AA          R DELRECSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Oprima ENTER para confirmar selecc-
     A                                      iones por 4=Borrar.'
     A                                      COLOR(BLU)
     A                                  4  2'Oprima F12 para volver y cambiar -
     A                                       selecciones.'
     A                                      COLOR(BLU)
     A*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R CMDDELSP
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
500 AA*----------------------------------------------------------------
500 AA          R OP1RECSP
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Oprima ENTER para confirmar selecc-
     A                                      iones por 1=Productos de pick.'
     A                                      COLOR(BLU)
     A                                  4  2'Oprima F12 para volver y cambiar s-
     A                                       elecciones.'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500  A          R CMDOP1SP
     A*%%TS  SD  19970625  090327  DAVE        REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancelar'
     A                                      COLOR(BLU)
