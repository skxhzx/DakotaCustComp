     A*%%TS  SD  20061214  132816  BFCTEST1    REL-V5R3M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*  RU310BFM              RF - Runner Clear Flags
     A*  December 14, 2006     Copy of RU310FM
     A*  Jose G. Duque
     A*
     A********************************************************************
     A*  Revisions:
     A*
417 AA*  05/25/04  MLB  4.17
     A*    - Created.
     A*
510 AA*  07/01/06  MLB  5.10
     A*    Fix: Revised screen 3, screen 63 to convert qty found field
     A*    to character from numeric. This was done to prevent users
     A*    from pressing Enter without entering zero or qty found for
     A*    shorted item.
     A*    Note for Cheney Bros: This mod was added to the ver 4.17 of
     A*    the dspf and program. No ver 5.00 updates are included.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20061214  132816  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.01'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Flag trn#:'
     A            W1TRN#         7Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(Z)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.02'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  3'Nbr scanned:'
     A            W2NRSN         5Y 0O  3 16EDTCDE(3)
     A                                  4  3'Slot:'
     A            W2DISP        12   O  4  9
     A                                  5  3'Label nbr:'
     A            W2LBL#         7Y 0B  5 14CHECK(ER)
     A                                      EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  7 14'F9=Skip'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A                                      CA03
     A                                      CA07
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.03'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  2  2'Lbl#'
     A            W3LBL#    R        O  2  7REFFLD(LBLBL# LABEL)
     A                                  3  2'Slot:'
     A            W3DISP        12A  O  3  8
     A            WOITEM        50A  O  4  2
     A                                  5  2'Qty short/out'
     A            W3QYSO    R        O  5 16REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A                                  6  2'Qty found:'
510 DA*           W3QYFD    R        B  6 13REFFLD(RUQYFD RUNNER)
510 MA            W3QYFD         5A  B  6 13
510 DA*                                     EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O  6 19REFFLD(LBUCOD LABEL)
     A                                  7  1'F7=Slots'
     A                                      COLOR(BLU)
     A                                  7 11'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*  16 line screens.
     A*
     A          R SCREEN61
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU310.01'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2' Warehouse:'
     A            W1WHSE         3Y 0B  3 14EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  3'Scan trn#:'
     A            W1TRN#         7Y 0B  4 14CHECK(ER)
     A                                      EDTCDE(M)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN62
     A  98                                  ALARM
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A                                      CLRL(15)
     A                                  1  2'RU310.02'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  3'Nbr scanned:'
     A            W2NRSN         5Y 0O  3 16EDTCDE(3)
     A                                  5  3'Slot:'
     A            W2DISP        12A  O  5  9
     A                                  7  3'Label nbr:'
     A            W2LBL#         7Y 0B  7 14CHECK(ER)
     A                                      EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 15  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 15 14'F9=Skip'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN63
     A                                      CA03
     A                                      CA07
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                  1  2'RU310.03'
     A                                  1 11'Clear Flags'
     A                                      DSPATR(HI)
     A                                  3  2'Lbl#'
     A            W3LBL#    R        O  3  7REFFLD(LBLBL# LABEL)
     A                                      EDTCDE(3)
     A                                  5  2'Slot:'
     A            W3DISP        12A  O  5  8
     A            WOITEM        50A  O  7  2
     A                                  9  2'Qty short/out'
     A            W3QYSO    R        O  9 16REFFLD(RUQYSO RUNNER)
     A                                      EDTCDE(3)
     A                                 11  2'Qty found:'
510 DA*           W3QYFD    R        B 11 13REFFLD(RUQYFD RUNNER)
510 MA            W3QYFD         5A  B 11 13
510 DA*                                     EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W3UCOD    R        O 11 19REFFLD(LBUCOD LABEL)
     A                                 15  1'F7=Slots'
     A                                      COLOR(BLU)
     A                                 15 11'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*
     A* Error Message Subfile Records for 16 line screens
     A*
     A*    Error Message Subfile Record
     A*
     A          R M16REC                    SFL
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
