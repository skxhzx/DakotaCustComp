     A*%%TS  SD  20030916  144731  ROCKY       REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  IT322FM   View warehouse book by item.
     A*  11 December 1993
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions:
414aAA*    08/25/00  RRH  4.14a
     A*      - Change SFLSIZ to 13 so rollup/down will work.
     A*        Screen only displays 13 records not 26.
415aAA*    03/26/01  RRH  4.15a
     A*      - Add From date to screen.
417aAA*    09/16/03  RH   4.17a
     A*      - Add Buyer to screen, remove entered date to make room.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19941028  115201  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 34'Expired Items'
     A                                      DSPATR(HI)
     A                                  1  2'IT392'
     A            WSHEAD        11A  O  2 35
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19941028  115201  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTINP1        15A  B  4 66
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
415aAA            WOFROM         6Y 0O  1 46EDTCDE(Y)
     A                                  1 56'Thru'
     A            WODATE         6Y 0O  1 62EDTCDE(Y)
415ADA*                                 1 41'Thru'
415ADA*           WODATE         6Y 0O  1 46EDTCDE(Y)
     A            WOPREV        35A  O  2 41
     A                                  4 41'Position to item  . . .'
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20030916  144731  ROCKY       REL-V5R2M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            IMBNBR    R        O  9  2REFFLD(OQREC/IMBNBR IT391)
     A                                      EDTCDE(4)
     A            SLITEM        15A  O  9  9
     A            W1DESC        15A  O  9 26
     A            SLDISP        12A  O  9 48
     A            QTY           10A  O  9 71
     A            SLWHDP         5A  O  9 42
417aDA*           W1ENTD         6Y 0O  9 53EDTCDE(Y)
     A            W1EXPD         6Y 0O  9 61EDTCDE(Y)
     A*
414  A***                                   SFLSIZ(0026)
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20030916  144731  ROCKY       REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  8  2'Buyer#'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8  9'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 26'Description    '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 48'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 71'Qty       '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 42'Dept '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
417a A*                                 8 53'Entered '
417a A*                                     DSPATR(UL)
417a A*                                     DSPATR(HI)
     A                                  8 61'Expired '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
