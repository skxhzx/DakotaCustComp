     A*%%***********************************************************************
     A*
     A*   EQ516PR  Print/Reprint equip. work orders
     A*   17 March, 2003   V4.17
     A*   Roxanne Harrison
     A*
     A*   Revision
     A*
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                   125
     A                                      DFT('EQ516')
     A                                      SPACEA(001)
     A*
     A* Print work order header
     A*
     A          R EWHEAD
     A                                      SPACEB(001)
     A                                     1'Warehouse:'
     A            EWWHSE    R             +1REFFLD(EWWHSE EQ516)
     A                                      EDTCDE(Z)
     A*                                     SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Work Order:'
     A            EWWRK#    R             +1REFFLD(EWWRK# EQ516)
     A                                      EDTCDE(Z)
     A                                    +3'Check'
     A            W1IO           3A       +1
     A            W1RPRT         9A      +10
     A  92                                75
     A                                      '(Continued)'
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('Employee:')
     A                                      SPACEB(002)
     A            ETEMP#    R             +1REFFLD(ETEMP# EQUIPT)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('Created Date:')
     A            W1GDTE         8A  O    16
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('Equipment ID:')
     A            ETCODE    R             +1REFFLD(ETCODE EQUIPT)
     A            WKEDSC        30A  O    +2
     A                                      SPACEA(002)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                     1'EXCEPTIONS                        '
     A                                      UNDERLINE
     A                                    35'   '
     A                                      UNDERLINE
     A                                    40'COMPLETED BY        '
     A                                      UNDERLINE
     A                                    63'DATE      '
     A                                      UNDERLINE
     A                                      SPACEA(001)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A            W1QSTN    R              1REFFLD(EUQSTN EQUIPQ)
     A            W1ANSR    R             +5REFFLD(EAANSR EQUIPA)
     A            W1BY          20        40UNDERLINE
     A            W1BYDT        10        63UNDERLINE
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('Completed by:')
     A            W1BY          25   O    +1
     A                                      UNDERLINE
     A                                    +2
     A                                      DFT('Date:')
     A            W1DT          15   O    +1
     A                                      UNDERLINE
     A
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('EQ516: End of Report')
     A*
