     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  BX310FM    View AMPBOX file - Display
     A*  NANCY KOELZ
     A*
     A*  Revisions
     A*
700 AA*    03/29/16  NLK  7.00
     A*      - Created.
     A*
700aAA*    05/04/16  NLK  7.00a
     A*      - ENH: GLP asked for different fields on the screens
     A*
700bAA*    05/09/16  NLK  7.00b
     A*      - ENH: rmv BoxID and add Cust Order# as a selection
     A*      - ENH: Allow F8 and F9 to call BX311
     A*
700cAA*    06/14/16  NLK  7.00c
     A*      - ENH: increased box# from 3.0 to 5.0
     A*
700dAA*    07/20/16  NLK  7.00d
     A*      - ENH: Add BX300 to see where AMP reduction needed
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
700bAA                                      CF08
700bAA                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      100A  H
     A            OPTION         2A  B  7  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
700DMA            #BBOX     R     Y  O  7  5REFFLD(ABXREC/ABXBOX# *LIBL/AMPBOX)
700cMA            #BCDGT         3Y 0O  7 11
700cMA            #BTRN     R     Y  O  7 17REFFLD(ABXREC/ABXATRN# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
700cMA            #BEMP     R     Y  O  7 26REFFLD(ABXREC/ABXAEMP# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
700cMA            OHCNAM        40   O  7 32
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20090612  110233  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View AMP Box Information'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A            SCHEAD        22A  O  2 33
     A                                      DSPATR(HI)
     A                                  3 35'for'
     A                                      DSPATR(HI)
     A            W1DT           8  0O  3 40EDTWRD('    -  -  ')
     A                                      DSPATR(HI)
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A                                  6  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700cMA                                  6  5'Box #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700cMA                                  6 11'CkDgt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700cMA                                  6 17'Tranactn'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700cMA                                  6 26'Empl '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
700cMA                                  6 32'Customer   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
      ***********************
     A          R WINDOW1
     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'BX310.01'
     A                                  1 25'View AMP Box Information'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
      *
     A                                  3 15'View BOX Information'
     A                                      DSPATR(HI)
     A                                  3 37'/  RePrint BOX LABEL'
     A                                      DSPATR(HI)
     A                                  5 15'Date. . . . . .'
     A            W1DT           8  0B  5 35
     A                                  5 50'CCYYMMDD'
     A                                  7 15'Warehouse . . .'
     A            W1WHSE         3  0B  7 35
     A                                  7 50'999'
     A                                  9 15'Box # . . . . .'
700cMA            W1BOX#         5A  B  9 35
700cMA                                  9 50'99999, *ALL'
     A                                 11 15'Cust Order# . .'
     A            W1CORD         7A  B 11 35
     A                                 11 50'9999999, *ALL'
     A                                 13 15'Transaction . .'
     A            W1TRAN         7A  B 13 35
     A                                 13 50'9999999, *ALL'
     A                                 15 15'Customer #. . . '
     A            W1CUST        11A  B 15 35
     A                                 15 50'99999999999, *ALL'
      ***
     A                                 23  5'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'BX310.03'
     A                                  1 31'View AMP Box Information'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A**                                  **REFFLD(OHREC/OHCNAM *LIBL/ORDH)
     A            H1CUST    R        O  4 14REFFLD(ABXREC/ABXACUST *LIBL/AMPBOX)
     A                                      EDTCDE(4)
     A                                  4  3'Customer #'
     A                                      DSPATR(HI)
      *
     A            OHCNAM        40   O  5  3
     A                                      DSPATR(HI)
     A                                  5 50'Tran#'
     A                                      DSPATR(HI)
     A            #BTRN     R     Y  O  5 56REFFLD(ABXREC/ABXATRN# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  7  3'Warehouse . . .'
     A            W1WHSE    R        O  7 21REFFLD(ABXREC/ABXWHSE *LIBL/AMPBOX)
     A                                  7 29'Box # . . . . .'
     A            #BBOX     R        O  7 45REFFLD(ABXREC/ABXBOX# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
     A                                  7 54'Box ID . . . .'
     A            H1BXID    R        O  7 69REFFLD(ABXREC/ABXBOXID *LIBL/AMPBOX)
     A                                      EDTCDE(4)
      ***
     A                                  9 29'CheckDigit. . .'
     A            #BCDGT         3  0O  9 45
     A                                  9 54'Status Date. .'
     A            OHSDTE         8  0O  9 69
      ***
     A                                 11  3'Cust Order #. .'
     A            AXCORD    R        O 11 21REFFLD(AXREC/AXCORD *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A                                 11 29'Order ID. . . .'
     A            AXAORD    R        O 11 45REFFLD(AXREC/AXAORD *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A                                 11 54'ROUTE. . . . .'
700aAA            ARTE      R        O 11 69REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A***         OHRTE     R        O 12 69REFFLD(OHREC/OHRTE *LIBL/ORDH)
      ***
     A                                 13  3'Label # . . . .'
     A            AXALBL#   R        O 13 21REFFLD(AXREC/AXALBL# *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A                                 13 29'SLOT. . . . . .'
     A            AXADISP   R        O 13 45REFFLD(AXREC/AXADISP *LIBL/AMPXREF)
      ***
     A                                 15  3'** DELIVER ROUTE INFORMATION **'
     A                                      DSPATR(HI)
     A                                 16  3'ROUTE . . . . .'
700aAA            DRTE      R        O 16 21REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A***         AXDRTID   R        O 17 21REFFLD(AXREC/AXDRTID *LIBL/AMPXREF)
     A                                 16 29'Order ID. . . .'
     A            AXDORD    R        O 16 45REFFLD(AXREC/AXDORD *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A                                 16 54'LABEL #. . . .'
     A            AXDLBL#   R        O 16 69REFFLD(AXREC/AXDLBL# *LIBL/AMPXREF)
     A                                      EDTCDE(4)
      ***
     A                                 23  5'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
      ***********************
     A          R SCREEN3
     A*%%TS  SD  20090824  110153  DAVE        REL-V6R1M0  5761-WDS
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'BX310.04'
     A                                  1 31'View AMP Box Information'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  3 31'   REPRINT BOX LABEL   '
     A                                      DSPATR(HI)
     A                                  6  5'Box # . . . . . . '
     A            #BBOX     R        O  6 27REFFLD(ABXREC/ABXBOX# *LIBL/AMPBOX)
     A                                  7  5'Date. . . . . . . '
     A            ADDDT         10A  O  7 27
     A                                  8  5'Printer device . .'
     A            W1DEV         10A  B  8 27
     A                                  9  5'Form Type. . . . .'
     A            W1FORM        10A  B  9 27
      ***
     A                                 23  5'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R FKEY1
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
700dAA  56                             22  2'Report of AMP Route QTY > -
700dAA                                      Delv Rt QTY exist for selected date'
700dAA  56                                  COLOR(RED)
700dAA  56                                  DSPATR(HI)
     A                                 23  2'F3=Exit  F5=Refresh  -
700bAA                                       F8=Items Not Boxed  -
700bAA                                       F9=Items Not on DelvRt  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R FKEY6
     A*%%TS  SD  20050105  171209  DAVE        REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A*****
     A          R MKEY1
     A                                      OVERLAY
     A            W1MSG1        75A  O 24  3
     A  55                                  DSPATR(HI)
     A N55                                  DSPATR(ND)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A************ITEMS IN THE BOX*************************************
     A          R SFL6                      SFL
     A*%%TS  SD  20090225  173813  DAVE        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SF6HIDE      100A  H
     A            #FITEM    R        O  8  2REFFLD(AXREC/AXITEM *LIBL/AMPXREF)
     A            ITDESC        30A  O  8 18
     A            LBQALC         5  0O  8 49EDTCDE(4)
     A            LBQPCK         5  0O  8 55EDTCDE(4)
     A            #FCORD    R     Y  O  8 63REFFLD(AXREC/AXCORD *LIBL/AMPXREF)
     A                                      EDTCDE(4)
     A*
     A          R SF6CTL                    SFLCTL(SFL6)
     A*%%TS  SD  20090612  110233  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 31'View ITEMS in Box Selected'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
      *
     A                                  2 35'for'
     A                                      DSPATR(HI)
     A            W1DT           8  0O  2 40EDTWRD('    -  -  ')
     A                                      DSPATR(HI)
700cMA                                  4 10'Box #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #BBOX     R        O  5 10REFFLD(ABXREC/ABXBOX# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
     A                                  4 20' Tran# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            #BTRN     R     Y  O  5 20REFFLD(ABXREC/ABXATRN# *LIBL/AMPBOX)
     A                                      EDTCDE(4)
      *
     A                                  3 61' Route  '
     A                                      DSPATR(HI)
     A                                  4 60'*AMP*'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 66'Delv'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A**          DARTID    R        O  5 59REFFLD(AXREC/AXARTID *LIBL/AMPXREF)
     A**          DDRTID    R        O  5 65REFFLD(AXREC/AXDRTID *LIBL/AMPXREF)
700aAA            ARTE      R        O  5 60REFFLD(RHREC/RHRTE *LIBL/RTEHED)
700aAA            DRTE      R        O  5 66REFFLD(RHREC/RHRTE *LIBL/RTEHED)
      *
     A                                  6 50'Quantity'
     A                                      DSPATR(HI)
     A                                  7  2'ITEM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 18'Description'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'ALLOC'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 56'PICK'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 63'CustOrd'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
