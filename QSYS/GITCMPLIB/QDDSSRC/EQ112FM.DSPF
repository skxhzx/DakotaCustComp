     A*%%TS  SD  19940711  163703  HEMANT      REL-V2R1M1  5738-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EQ112FM   Equipment table maintenance - Change screen
     A*  02 November, 1995
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions
     F*    12/23/02  RH   4.17
     F*      - EQUIP file changed, add Equipment type W1TYPE
417bAA*    04/28/04  RH   4.17b
     A*      - Increase length of equipment code to 8.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19940711  163703  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
417  A                                      CF04
     A  98                                  ALARM
     A                                  1  2'EQ112.01'
     A                                  1 27'Equipment Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Code . . . . . . . . .'
417bDA*           W1CODE         6A  B 10 28
417b A            W1CODE         8A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Description  . . . . .'
     A            W1DESC        30A  B 11 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12  4'Speed factor . . . . .'
     A            W1SFCT         3Y 0B 12 28EDTCDE(3)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 32'%'
417  A                                 13  4'Equipment type . . . .'
417  A            W1TYPE         6A  B 13 28
417  A  24                                  DSPATR(RI)
417  A N24N90                               DSPATR(UL)
417  A  04                                  DSPATR(PC)
417  A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
