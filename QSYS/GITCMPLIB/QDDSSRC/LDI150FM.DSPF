     A*%%TS  SD  20000224  162220  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LDI150FM   Create test po from std item import file - Options
     F*  24 February 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000224  162220  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CF04
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LDI150.01'
     A                                  1 16'Create Test P.O.''s from Standard -
     A                                      Item Import File'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  4'Import file version  . . . .'
     A            W1VER          1A  B  7 34
     A N22N99                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 53'1, 2'
     A                                  9  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B  9 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 53'Y=Yes, N=No'
     A                                 13  7'This program creates P.O.''s from -
     A                                      the items in the Standard'
     A                                 14  7'Item Import file. A separate P.O. -
     A                                      will be created for each'
     A                                 15  7'department/section combination. Se-
     A                                      parate P.O.''s will be'
     A                                 16  7'created for items with no average -
     A                                      stock on hand verses items'
     A                                 17  7'with an average stock on hand. The-
     A                                       department and section'
     A                                 18  7'come from the item master file. A -
     A                                      separate P.O. will be'
     A                                 19  7'created for items not found in the-
     A                                       item master file.'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
