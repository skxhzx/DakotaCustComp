     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UC225PR    FTP Import Item UPC Code Updates - Post
     A*  30 August 2004
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    08/30/04  MLB  5.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A            IMPDTE         6S 0O     1EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      DFT('Page')
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A
     A            IMPTIM         6S 0O     1EDTWRD('0  :  :  ')
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      DFT('UC225')
     A                                     2
     A                                      DFT('Item           ')
     A                                      SPACEB(002)
     A                                    19
     A                                      DFT('Description')
     A                                    50
     A                                      DFT('UPC Code')
     A                                    90
     A                                      DFT('Message')
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A  40        UCITEM    R        O     2
     A                                      REFFLD(UCREC/UCITEM *LIBL/IMPFUPC)
     A  40        ITDESC    R        O    19
     A                                      REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A  40        UCUPC     R        O    50
     A                                      REFFLD(UCREC/UCUPC  *LIBL/IMPFUPC)
     A            DETMSG        40A  O    90
     A*
     A          R GRNDTTL
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      DFT('Grand Totals')
     A                                     4
     A                                      DFT('Total UPC codes imported  . . -
     A                                      . .:')
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A*                                     SPACEB(002)
     A                                     4
     A                                      DFT('Invalid item numbers  . . . . -
     A                                      . .:')
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid UPC codes . . . . . . -
     A                                      . .:')
     A                                      SPACEB(001)
     A            BADUPC         7S 0O    39
     A                                      EDTCDE(J)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('UC225: End of Report ')
