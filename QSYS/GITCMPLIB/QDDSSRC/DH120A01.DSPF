     A*%%TS  SD  20020416  052355  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  DH120A01   Dock door matrix by date/pass - Display
     A*  16 April 2002
     A*  Dave Sommerville
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
416 AA*    04/16/02  DAS  4.16
     A*      - Created.
     A*
520 AA*    09/22/09  MLB  5.20
     A*      - Enh: Added new Change To field, for assign type A or M.
     A*        This new field will be used as a filter to display
     A*        either automatic assigned doors or only manual assigned
     A*        doors.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA11
     A                                      CA12
     A                                      CA04
     A          R TITLE
     A*%%TS  SD  20011106  163259  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'DH120A.01'
     A                                  1 33'Dock Door Matrix'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOHEAD        50   O  2 16DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20011105  120804  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 48'Change to warehouse . . .'
     A            PTINP1         3Y 0B  1 75EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01N20                               DSPATR(PC)
520 AA                                  2 48'Position to assign type .'
520 AA            PTINP6         1A  B  2 75VALUES(' ' 'A' 'M')
520 AA                                  2 78'A/M'
520 DA*                                 2 48'Position to pass  . . . .'
520 MA                                  3 48'Position to pass  . . . .'
520 DA*           PTINP2         3Y 0B  2 75EDTCDE(4)
520 MA            PTINP2         3Y 0B  3 75EDTCDE(4)
520 DA* 40                              3 48'Position to door  . . . .'
520 AA                                  4  2'Type options, press Enter.'
520 AA                                      COLOR(BLU)
520 MA  40                              4 48'Position to door  . . . .'
520 DA* 40        PTINP3         3Y 0B  3 75EDTCDE(4)
520 MA  40        PTINP3         3Y 0B  4 75EDTCDE(4)
520 DA*N40                              3 48'Position to department  .'
520 MA N40                              4 48'Position to department  .'
520 DA*N40        PTINP4         5A  B  3 75
520 MA N40        PTINP4         5A  B  4 75
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20020416  052355  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            W1PASS         3Y 0O 11  2EDTCDE(Z)
     A            OPT1           2A  B 11  7
     A  20
     AO 22                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1INF1        10A  O 11 10
     A            OPT2           2A  B 11 22
     A  23                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1INF2        10A  O 11 25
     A            OPT3           2A  B 11 37
     A  24                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1INF3        10A  O 11 40
     A            OPT4           2A  B 11 52
     A  25                                  DSPATR(PC)
     A N25N90                               DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1INF4        10A  O 11 55
     A            OPT5           2A  B 11 67
     A  26                                  DSPATR(PC)
     A N26N90                               DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1INF5        10A  O 11 70
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  1'Pass'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WODOR1        10A  O  9 10DSPATR(HI)
     A            WOMAX1        10A  O 10 10DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WODOR2        10A  O  9 25DSPATR(HI)
     A            WOMAX2        10A  O 10 25DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WODOR3        10A  O  9 40DSPATR(HI)
     A            WOMAX3        10A  O 10 40DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 51'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WODOR4        10A  O  9 55DSPATR(HI)
     A            WOMAX4        10A  O 10 55DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            WODOR5        10A  O  9 70DSPATR(HI)
     A            WOMAX5        10A  O 10 70DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
520 DA*                                 1  2'Type options, press Enter.'
520 DA*                                     COLOR(BLU)
520 DA*N40                              1 48'Position to door  . . . .'
520 MA N40                              2 48'Position to door  . . . .'
520 DA*N40        PTINP5         3Y 0B  1 75EDTCDE(4)
520 MA N40        PTINP5         3Y 0B  2 75EDTCDE(4)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
