     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  ER711FM    Current Selection Display
     A*
     A*  Revisions
     A*
640a A*    12/29/10  GJA  6.40
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA12
     A                                      CA05
     A                                      CA03
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            W1EMP#         5  0O  8  2
     A            W1NAME        16A  O  8  8
     A            W1DTRN         5Y 0O  8 25EDTCDE(3)
     A            W1DPCS         6Y 0O  8 31EDTCDE(J)
     A            W1NVPC         6Y 0O  8 40EDTCDE(J)
     A            W1CTRN         7Y 0O  8 49EDTCDE(3)
     A            W1TPCS         6Y 0O  8 57EDTCDE(J)
     A            W1CPCS         6Y 0O  8 66EDTCDE(J)
     A            W1RTID         5   O  8 75
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      CF08
     A                                      CF16
     A                                      OVERLAY
     A                                      SFLCSRRRN(&@CSRPO)
     A  26                                  SFLDSP
     A N27                                  SFLDSPCTL
     A  27                                  SFLCLR
     A N28                                  SFLEND(*MORE)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0013)
     A            @CSRPO         5  0H
     A            POSNBR         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'ER711.01'
     A                                  1 26'View Current Selection'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  2  2USER
     A                                  2 32'List Display'
     A                                      DSPATR(HI)
     A            WHSE           3Y 0O  4  3EDTCDE(4)
     A                                  4  7'-'
     A            WHDESC        30A  O  4  9
     A            FLD001         1A  O  6  3
     A                                  6 21'--DAILY'
     A                                      DSPATR(HI)
     A            TDPCS          7Y 0O  6 29EDTCDE(J)
     A                                  6 49'--CURRENT'
     A                                      DSPATR(HI)
     A                                  5 40'Non   '
     A                                      DSPATR(HI)
     A                                  6 40'Voice '
     A                                      DSPATR(HI)
     A                                  7  2'     Employee        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 25'Trans'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 31'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 40'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'Trans'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 58'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 66'Total '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aAA                                  7 75'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
