     A*%%TS  SD  20081022  185200  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTSLOTQTY  Test DRIITEMADJ interface.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    12/26/01  DAS  4.16
     A*      - Created
     A*
416aAA*    01/30/02  DAS  4.16a
     A*      - Added "Dakota" field.
     A*      - Added "Send" fields.
     A*      - Added Status, Exp Date, Priority and Program fields.
     A*      - Moved things around.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20081022  185200  DAVE        REL-V6R1M0  5761-WDS
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF13
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTSLOTQTY.1'
     A                                  1 25'Test DRISLOT Quantity Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Adjustment type  . .'
     A            W1TYPE         2Y 0B  3 25EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  3 28'1=*ADJUST'
     A                                  3 38'2=*STOCK'
     A                                  3 47'3=*RECEIVE'
     A                                  3 58'4=*ALLOCATE'
     A                                  3 70'5=*PICK'
     A                                  4  5'6=*TFROUT'
     A                                  4 15'7=TFRIN'
     A                                  4 23'8=*ADJUSTORD'
     A                                  4 36'9=*ALC2PCK'
     A                                  4 47'10=*PCK2ALC'
     A                                  4 59'11=*CLSRCV'
     A                                  4 70'12=*CLSPICK'
     A                                  5  5'13=*CLRTFROUT'
     A                                  5 19'14=*CLSTFRIN'
     A                                  5 32'15=*RCVIN'
     A                                  5 42'16=*RCVOUT'
     A                                  5 53'17=*STGIN'
     A                                  5 63'18=*STGOUT'
     A                                  6  1'Send'
     A                                      DSPATR(UL)
     A                                  6  7'19=*USRIN'
     A                                  6 17'20=*USROUT'
     A            WSCODE         1A  B  7  2VALUES('Y' 'N')
     A                                  7  6'Adjustment code  . .'
     A            W1CODE         2A  B  7 27
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A            WSWHSE         1A  B  8  2
     A                                  8  6'Warehouse  . . . . .'
     A            W1WHSE         3Y 0B  8 27
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSWHDP         1A  B  9  2
     A                                  9  6'Department . . . . .'
     A            W1WHDP         5A  B  9 27
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                  9 55'Dakota call  . . . .'
     A            W1DKTA         1A  B  9 76VALUES('Y' 'N')
     A            WSDISP         1A  B 10  2
     A                                 10  6'Slot . . . . . . . .'
     A            W1SLOT        12A  B 10 27
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A            WSITEM         1A  B 11  2
     A                                 11  6'Item . . . . . . . .'
     A            W1ITEM        15A  B 11 27
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A            WSSTAT         1A  B 12  2
     A                                 12  6'Status . . . . . . .'
     A            W1STAT         2A  B 12 27
     A  36                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
     A            WSEXPD         1A  B 13  2
     A                                 13  6'Expiration date  . .'
     A            W1EXPD         8Y 0B 13 27
     A  34                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 13 49'YYYYMMDD'
     A            WSPRTY         1A  B 14  2
     A                                 14  6'Priority . . . . . .'
     A            W1PRTY         1Y 0B 14 27
     A  35                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A                                      EDTCDE(Z)
     A            WSQTY          1A  B 16  2
     A                                 16  6'Adjust quantity  . .'
     A            W1QTY1         5Y 0B 16 27EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A            W1QTY2         3Y 0B 16 35
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A            W1QTY3         3Y 0B 16 41
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A                                 16 49'Note: For negative qtys put'
     A            WSORG          1A  B 17  2
     A                                 17  6'Original quantity  .'
     A            W1ORG1         5Y 0B 17 27EDTCDE(M)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            W1ORG2         3Y 0B 17 35
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A            W1ORG3         3Y 0B 17 41
     A  33                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A                                 17 55'minus sign on the right'
     A            WSCWTA         1A  B 18  2
     A                                 18  6'Catch weight . . . .'
     A            W1CWTA         9Y 2B 18 27
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A                                      EDTCDE(M)
     A            WSMEMO         1A  B 19  2
     A                                 19  6'Memo . . . . . . . .'
     A            W1MEMO        30A  B 19 27CHECK(LC)
     A                                 20  6'Program  . . . . . .'
     A            W1PGM         10A  B 20 27
     A                                 21  6'Return code  . . . .'
     A            W1RTN         10A  B 21 27
     A                                 23  2'F4=Prompt F8=Whse qtys F9=Slot qty-
     A                                      s F10=Slot F11=Item F13=View prty r-
     A                                      pl'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
