     A*%%TS  SD  20140411  000320  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG181FM    Staging Import View Regular/Will-call Order Detail
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140411  000320  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OEDSTSTS  R        H      REFFLD(OEDREC/OEDSTSTS *LIBL/EXPSOR-
     A                                      DD)
     A            OEDSTATUS R        H      REFFLD(OEDREC/OEDSTATUS *LIBL/EXPSO-
     A                                      RDD)
     A            OEDTEXT   R        H      REFFLD(OEDREC/OEDTEXT *LIBL/EXPSORD-
     A                                      D)
     A            OEDQ1     R        H      REFFLD(OEDREC/OEDQ1 *LIBL/EXPSORDD)
     A            OEDQ2     R        H      REFFLD(OEDREC/OEDQ2 *LIBL/EXPSORDD)
     A            OEDQ3     R        H      REFFLD(OEDREC/OEDQ3 *LIBL/EXPSORDD)
     A            OEDQP1    R        H      REFFLD(OEDREC/OEDQP1 *LIBL/EXPSORDD)
     A            OEDQP2    R        H      REFFLD(OEDREC/OEDQP2 *LIBL/EXPSORDD)
     A            OEDQP3    R        H      REFFLD(OEDREC/OEDQP3 *LIBL/EXPSORDD)
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            OEDORD    R        O  8  9REFFLD(OEDREC/OEDORD *LIBL/EXPSORDD)
     A                                      EDTCDE(Z)
     A            OEDSEQ    R        O  8 17REFFLD(OEDREC/OEDSEQ *LIBL/EXPSORDD)
     A                                      EDTCDE(Z)
     A            OEDITM    R        O  8 25REFFLD(OEDREC/OEDITM *LIBL/EXPSORDD)
     A            S1QTY         15A  O  8 41
     A            S1QTYP        15A  O  8 57
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140411  000320  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 29'View Route Order Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9' Order '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 17' Seq '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 25'     Item      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 41'  Ordered Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 57'  Picked Qty   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit   F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20140411  000320  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG181.02'
     A                                  1 29'View Route Order Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Detail Information - Page 1'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            OEDWHS    R        O  5 18REFFLD(OEDREC/OEDWHS *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            OEDBATID  R        O  5 54REFFLD(OEDREC/OEDBATID *LIBL/EXPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  6  2'Order ID  . . .'
     A            OEDORDID  R        O  6 18REFFLD(OEDREC/OEDORDID *LIBL/EXPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            OEDADDTS  R        O  6 54REFFLD(OEDREC/OEDADDTS *LIBL/EXPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  7  2'Order Seq # . .'
     A            OEDSEQ    R        O  7 18REFFLD(OEDREC/OEDSEQ *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            OEDSTSTS  R        O  7 54REFFLD(OEDREC/OEDSTSTS *LIBL/EXPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  9  2'Status/Message '
     A            OEDSTATUS R        O  9 18REFFLD(OEDREC/OEDSTATUS *LIBL/EXPSO-
     A                                      RDD)
     A                                      DSPATR(HI)
     A            OEDTEXT   R        O  9 24REFFLD(OEDREC/OEDTEXT *LIBL/EXPSORD-
     A                                      D)
     A                                      DSPATR(HI)
     A                                 13  2'Order . . . . .'
     A            OEDORD    R        O 13 18REFFLD(OEDREC/OEDORD *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 14  2'Item  . . . . .'
     A            OEDITM    R        O 14 18REFFLD(OEDREC/OEDITM *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 15  2'Qty Ordered . .'
     A            S2QTY         20A  O 15 18DSPATR(HI)
     A                                 16  2'Qty Picked  . .'
     A            S2QTYP        20A  O 16 18DSPATR(HI)
     A                                 18  2'Corresp Whse  .'
     A            OEDWH2    R        O 18 18REFFLD(OEDREC/OEDWH2 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 19  2'Corresp Order .'
     A            OEDOR2    R        O 19 18REFFLD(OEDREC/OEDOR2 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 20  2'Original Item .'
     A            OEDOIT    R        O 20 18REFFLD(OEDREC/OEDOIT *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 21  2'Special Status '
     A            OEDSPC    R        O 21 18REFFLD(OEDREC/OEDSPC *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20140410  205131  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG181.03'
     A                                  1 29'View Route Order Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Detail Information - Page 2'
     A                                      DSPATR(HI)
     A                                  6  2'Misc 1  . . . .'
     A            OEDMS1    R        O  6 18REFFLD(OEDREC/OEDMS1 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                  7  2'Misc 2  . . . .'
     A            OEDMS2    R        O  7 18REFFLD(OEDREC/OEDMS2 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                  8  2'Misc 3  . . . .'
     A            OEDMS3    R        O  8 18REFFLD(OEDREC/OEDMS3 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                  9  2'Misc 4  . . . .'
     A            OEDMS4    R        O  9 18REFFLD(OEDREC/OEDMS4 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 10  2'Misc 5  . . . .'
     A            OEDMS5    R        O 10 18REFFLD(OEDREC/OEDMS5 *LIBL/EXPSORDD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
