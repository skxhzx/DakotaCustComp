     A*%%TS  SD  19960621  105851  DAVE3       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO150FM   Manual Receiving - Change screen
     A*  09 July 1994
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
219  F*    03/11/97  RH   2.19
     F*      - Add Catch weight to Screen 3 and Screen 4.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960621  105851  DAVE3       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PO150.01'
     A                                  1 33'Manual Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'General Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0B  6 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  7  4'P.O. number . . . . . . .'
     A            W1PO           9A  B  7 31
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8  4'Vendor number . . . . . .'
     A            W1VEN         11Y 0B  8 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9  4'Vendor name . . . . . . .'
     A            W1VNAM        40A  B  9 31
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 10  4'Printer device  . . . . .'
     A            W1DEV         10A  B 10 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Printer form  . . . . . .'
     A            W1FORM        10A  B 11 31
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19941110  144329  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PO150.02'
     A                                  1 33'Manual Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0O  6 31EDTCDE(4)
     A                                  7  4'P.O. number . . . . . . .'
     A            W1PO           9A  O  7 31
     A                                  8  4'Vendor number . . . . . .'
     A            W1VEN         11Y 0O  8 31EDTCDE(4)
     A                                  9  4'Vendor name . . . . . . .'
     A            W1VNAM        40A  O  9 31
     A                                 10  4'Printer device  . . . . .'
     A            W1DEV         10A  O 10 31
     A                                 11  4'Printer form  . . . . . .'
     A            W1FORM        10A  O 11 31
     A                                 13  4'Item number . . . . . . .'
     A            W2ITEM        15A  B 13 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Vendor item number  . . .'
     A            W2VITM        15A  B 14 31
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19941110  144329  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PO150.02'
     A                                  1 33'Manual Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0O  6 31EDTCDE(4)
     A                                  7  4'P.O. number . . . . . . .'
     A            W1PO           9A  O  7 31
     A                                  8  4'Vendor number . . . . . .'
     A            W1VEN         11Y 0O  8 31EDTCDE(4)
     A                                  9  4'Vendor name . . . . . . .'
     A            W1VNAM        40A  O  9 31
     A                                 10  4'Printer device  . . . . .'
     A            W1DEV         10A  O 10 31
     A                                 11  4'Printer form  . . . . . .'
     A            W1FORM        10A  O 11 31
     A                                 13  4'Item number . . . . . . .'
     A            W2ITEM        15A  O 13 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                 14  4'Vendor item number  . . .'
     A            W2VITM        15A  O 14 31
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                 15  4'Quantity received . . . .'
     A            W2QT01         5Y 0B 15 31EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A N23N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            W2UM1          5A  O 15 37
     A            W2QT02         3Y 0B 15 45EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A N31N90                               DSPATR(UL)
     A  94                                  DSPATR(ND)
     A  94
     AO 90                                  DSPATR(PR)
     A            W2UM2          5A  O 15 49
     A  94                                  DSPATR(ND)
     A            W2QT03         3Y 0B 15 57EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93
     AO 90                                  DSPATR(PR)
     A            W2UM3          5A  O 15 61
     A  93                                  DSPATR(ND)
     A                                 16  4'Expiration date . . . . .'
     A            W2EXPD         6Y 0B 16 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(4)
219 AA                                 17  4'Catch weight received . .'
     A            W2DWGT         7Y 2B 17 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 50'Average (Y/N) . . . . . .'
     A            W2AVG          1A  B 17 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  19941110  144329  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PO150.02'
     A                                  1 33'Manual Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            W1WHSE         3Y 0O  6 31EDTCDE(4)
     A                                  7  4'P.O. number . . . . . . .'
     A            W1PO           9A  O  7 31
     A                                  8  4'Vendor number . . . . . .'
     A            W1VEN         11Y 0O  8 31EDTCDE(4)
     A                                  9  4'Vendor name . . . . . . .'
     A            W1VNAM        40A  O  9 31
     A                                 10  4'Printer device  . . . . .'
     A            W1DEV         10A  O 10 31
     A                                 11  4'Printer form  . . . . . .'
     A            W1FORM        10A  O 11 31
     A                                 13  4'Item number . . . . . . .'
     A            W2ITEM        15A  B 13 31
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Vendor item number  . . .'
     A            W2VITM        15A  B 14 31
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Quantity received . . . .'
     A            W4QTY          7   B 15 31
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16  4'Expiration date . . . . .'
     A            W2EXPD         6Y 0B 16 31
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  96                                  DSPATR(ND)
     A  96
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(4)
219 AA                                 17  4'Catch weight received . .'
     A            W2DWGT         7Y 2B 17 31
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 50'Average (Y/N) . . . . . .'
     A            W2AVG          1A  B 17 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
