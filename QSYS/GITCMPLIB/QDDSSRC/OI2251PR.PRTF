     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI2251PR    Import customer orders - Print Exceptions
     A*  07 June 2001
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
416 AA*    06/07/01  MLB  4.16
     F*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD11
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HDR1        50A  O   +32
     A                                   122
     A                                      DFT('Page')
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      DFT('OI225')
     A          R HEAD21
     A                                      SPACEB(002)
     A                                     3
     A                                      DFT('Order')
     A                                    19
     A                                      DFT('Customer')
     A                                     3
     A                                      DFT('Number')
     A                                      SPACEB(001)
     A                                    +2
     A                                      DFT('Type')
     A                                    19
     A                                      DFT('Number')
     A                                    29
     A                                      DFT('Customer Name')
     A                                    72
     A                                      DFT('Route')
     A                                    79
     A                                      DFT('Stop')
     A                                    85
     A                                      DFT('Truck')
     A*
     A          R HEAD31
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHORD *LIBL/IMPORDH)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHREC/OIHTYP *LIBL/IMPORDH)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHCUS *LIBL/IMPORDH)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHCNM *LIBL/IMPORDH)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHRTE *LIBL/IMPORDH)
     A            OIHSTP    R        O    +3
     A                                      REFFLD(OIHSTP *LIBL/IMPORDH)
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHTRK *LIBL/IMPORDH)
     A            HEDMSG        20A  O   106
     A  81                               127'(CONT)'
     A                                      HIGHLIGHT
     A*
     A          R HEAD41
     A                                    53
     A                                      DFT('----- Quantity -----')
     A                                      SPACEB(002)
     A                                    +2
     A                                      DFT('Corresponding')
     A                                    +2
     A                                      DFT('Original')
     A                                     4
     A                                      DFT('Item')
     A                                      SPACEB(001)
     A                                    21
     A                                      DFT('Description')
     A                                   +21
     A                                      DFT('Normal')
     A                                    +2
     A                                      DFT('Brk 1')
     A                                    +2
     A                                      DFT('Brk 2')
     A                                    +3
     A                                      DFT('Whse')
     A                                    +3
     A                                      DFT('Order')
     A                                    +2
     A                                      DFT('Item')
     A          R DETAIL1
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDITM *LIBL/IMPORDD)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDREC/OIDQ1 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ2 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ3 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDREC/OIDWH2 *LIBL/IMPORDD)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDREC/OIDOR2 *LIBL/IMPORDD)
     A                                      EDTCDE(4)
     A            OIDOIT    R        O    +2
     A                                      REFFLD(OIDREC/OIDOIT *LIBL/IMPORDD)
     A            DETMSG        20A  O   106
     A          R SUBTTL1
     A                                      SPACEB(001)
     A                                    53
     A                                      DFT('------')
     A                                    +1
     A                                      DFT('------')
     A                                    +1
     A                                      DFT('------')
     A            SUBQ11         7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ21         7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ31         7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A          R GRNDTTL1
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      DFT('Grand Totals')
     A                                     4
     A                                      DFT('Customer orders:')
     A                                      SPACEB(001)
     A            GRDORD         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Detail lines   :')
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Pre-existing customer orders  -
     A                                         :')
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Pre-existing detail lines     -
     A                                         :')
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid item numbers          -
     A                                         :')
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid breakdown quantities  -
     A                                         :')
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid Cross dock P.O.s      -
     A                                         :')
     A                                      SPACEB(001)
     A            BADPO          7S 0O    39
     A                                      EDTCDE(J)
