     A*%%TS  SD  20011101  101020  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TSTPRTYRPL  Test DRIPRTYRPL interface.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    10/29/01  DAS  4.16
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20011101  101020  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTITEMADJ.1'
     A                                  1 28'Test DRIITEMADJ Interface'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Adjustment type  . . . .'
     A            W1TYPE         1A  B  7 30
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  7 37'1 = *CRTRPL'
     A                                  8 37'2 = *CRTRPLDKA'
     A                                  9 37'3 = *REMOVE'
     A                                 11  4'Warehouse  . . . . . . .'
     A            W1WHSE         3S 0B 11 30
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                 12  4'Department . . . . . . .'
     A            W1WHDP         5A  B 12 30
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A                                 13  4'Slot . . . . . . . . . .'
     A            W1SLOT        12A  B 13 30
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A                                 14  4'Item . . . . . . . . . .'
     A            W1ITEM        15A  B 14 30
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A                                 15  4'Quantity needed  . . . .'
     A            W1QTYN         7Y 0B 15 30EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A                                 16  4'Total warehouse qty  . .'
     A            W1QTYW         7S 0B 16 30
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A                                 17  4'Fill to needed qty . . .'
     A            W1FILL         1A  B 17 30
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A                                 19  4'Return code  . . . . . .'
     A            W1RTN         10A  O 19 30
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F1-
     A                                      0=View priority repl.  12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
