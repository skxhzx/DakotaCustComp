     A*%%***********************************************************************
     A*%%TS  RD  19970828  065827  DAVE        REL-V3R1M0  5763-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OI315PR    Import FTP orders - Detail Ver 6
     A*  14 October 2009
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    10/14/09  MLB  5.20
     F*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'OI315'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A          R HEAD2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    80
     A                                      'Stop'
     A                                    88
     A                                      'Truck'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD3
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHORD IMPFORDH6)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHTYP IMPFORDH6)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHCUS IMPFORDH6)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHCNM IMPFORDH6)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHRTE IMPFORDH6)
     A            OIHEST    R        O    +3
     A                                      REFFLD(OIHEST IMPFORDH6)
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHTRK IMPFORDH6)
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD4
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Original'
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Item'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 002
     A*%%CL 001
     A*%%***********************************************************************
     A          R DETAIL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDITM IMPFORDD6)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDQ1  IMPFORDD6)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDQ2  IMPFORDD6)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDQ3  IMPFORDD6)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDWH2 IMPFORDD6)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDOR2 IMPFORDD6)
     A                                      EDTCDE(4)
     A            OIDOIT    R        O    +2
     A                                      REFFLD(OIDOIT IMPFORDD6)
     A            DETMSG        20A  O   106
     A*%%***********************************************************************
     A          R DETAIL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            STSMSG        33A  O     6
     A                                    +2
     A                                      DFT('Host Route/Order#/Line#:')
     A            OIHRTE    R        O    +1
     A                                      REFFLD(OIHRTE IMPFORDH6)
     A                                    +1
     A                                      DFT('/')
     A            OIHORD    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIHORD IMPFORDH6)
     A                                    +1
     A                                      DFT('/')
     A            OIDSEQ    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIDSEQ IMPFORDD6)
     A                                    +4
     A                                      DFT('FSWMS RouteId/Order#:')
     A            PRTRTE    R        O    +1
     A                                      REFFLD(OHRTE  ORDH)
     A                                    +1
     A                                      DFT('/')
     A            PRTORD    R        O    +1
     A                                      EDTCDE(4)
     A                                      REFFLD(OIDORD IMPFORDD6)
     A*%%***********************************************************************
     A*%%SS
     A*%%SN ITDESC     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
     A*%%SN OIDQ1      99999
     A*%%SN OIDQ2      99999
     A*%%SN OIDQ3      99999
     A*%%***********************************************************************
     A          R SUBTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ1          7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ2          7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ3          7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A          R GRNDTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer orders:'
     A                                      SPACEB(001)
     A            GRDORD         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail lines   :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      DFT('Invalid Cross dock P.O.s      -
     A                                         :')
     A                                      SPACEB(001)
     A            BADPO          7S 0O    39
     A                                      EDTCDE(J)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%SN GRDORD     9999999
     A*%%CL 001                   :
     A*%%SN GRDLIN     9999999
     A*%%CL 002
     A*%%SN PREOH      9999999
     A*%%CL 001
     A*%%SN PREOD      9999999
     A*%%CL 001
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(*libl/OI315PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(66       132      *N     )
     A*%%***********************************************************************
