     A*%%TS  SD  20061219  091350  JEFF        REL-V5R3M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------
     A*
     A*  TAX110FM   Harbor Tax Right - Options
     A*  07 July 2006
     A*  Robin Clark
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    07/07/06  RMC
     A*      - Created. Copied from RED110FM
     A*
510 AA*    12/17/06  JCJ  510 A
     A*      - Enh: add Warehouse processing and $pcmd selection.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20061217  155032  JEFF        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TAX110.01'
     A                                  1 33'Tax Right Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WHCODE         3Y 0O  3  2EDTCDE(3)
     A                                  3  6'-'
     A            WHDESC        30A  O  3  8
510 AA                                  3 48'Change to warehouse . . .'
510 AA            PTINP1         3Y 0B  3 75EDTCDE(4)
510 AA  23                                  DSPATR(RI)
510 AA  03                                  DSPATR(PC)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Options'
     A                                      DSPATR(HI)
     A                                  9  5'Days to keep export data  . . .'
     A            W1TXHS         3Y 0B  9 38EDTCDE(3)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 59'1 - 999 days'
510 AA                                 11  5'Tax Right file Processing . . .'
510 AA            W1PCMD         8   B 11 38
510 AA  22                                  DSPATR(RI)
510 AA N22N90                               DSPATR(UL)
510 AA  02                                  DSPATR(PC)
510 AA  90                                  DSPATR(PR)
510 AA                                 11 59'*CREATE, *SEND, *NONE'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
