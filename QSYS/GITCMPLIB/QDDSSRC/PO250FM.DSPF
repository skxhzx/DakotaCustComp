     A*%%TS  SD  20181012  094557  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A********************************************************************
     A*
     A* NOTE: MUST COMPILE WITH RSTDSP = *YES
     A*
     A*
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A**************************************************************************
     A*  PO250FM   Box Labeler Print  -  RF Screen
     A*  Revisions:
     A*
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20181009  154944  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'PO250.01'
     A                                  1 12'Print Labels'
     A                                      DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A                                  3  2'Lic#'
     A                                  4  2'Item'
     A            W1LCNS        15A  B  3  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A            W1ITEM        15A  B  4  7
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A          R SCREEN2
     A*%%TS  SD  20181009  112732  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'PO250.02'
     A                                  1 12'Print Label'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W2ITEMDS      70A  O  2  8
     A                                  4  2'PO#.....:'
     A                                  5  2'Rcv Date:'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A            W2PO           9A  B  4 12
     A  90                                  DSPATR(PR)
     A            W2RCVDT        6Y 0B  5 12EDTCDE(Y)
     A  91                                  DSPATR(PR)
     A          R SCREEN3
     A*%%TS  SD  20181012  094557  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A N92                                  CA04
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'PO250.03'
     A                                  1 12'Print Labels'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W2ITEMDS      70A  O  2  8
     A                                  6  2'Printer.:'
     A            W3PRINT       10A  B  6 12CHECK(ER)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                  7  2'F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A            W3UOM          2A  B  4 12
     A  92                                  DSPATR(PR)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A                                  4  2'Uom.....:'
     A                                  5  2'Quantity:'
     A            W3QTY          5Y 0B  5 12EDTCDE(Z)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                  7 25' '
     A                                  7 27' '
     A                                  7 29' '
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRBTM
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(16)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R WDUMMY                     ASSUME
     A                                  1  2' '
     A          R SCREEN4
     A*%%TS  SD  20181009  163758  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'PO250.04'
     A                                  1 12'Print Labels'
     A                                      DSPATR(HI)
     A                                  2  2'Item:'
     A            W2ITEMDS      70A  O  2  8
     A            W4CONF         1A  B  5 11CHECK(ER)
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  4  2'Did you get your labels?'
     A                                  3  2'Gtin:'
     A            W4GTIN        14A  O  3  8
