     A*%%TS  SD  20150108  104705  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  OE152FM    Customer/Item Amazon File Maintenance - change
     A*  January 8, 2015
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
650a A*    01/08/15  LMC  6.50a
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20150108  104705  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OE152.01'
     A                                  1 24'Customer/Item Amazon Information'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Customer Number. . . .'
     A            W1CUST        11A  B  8 28
     A N21N90                               DSPATR(HI)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 10  4'Department . . . . . .'
     A            W1WHDP         5A  B 10 28
     A N22N90                               DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Item Number. . . . . .'
     A            W1ITEM        15A  B  9 28
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 12  4'Description. . . . . .'
     A            W1DESC        60A  B 12 28
     A N24N90                               DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 11  4'Customer Item. . . . .'
     A            W1CITM        15A  B 11 28
     A N25N90                               DSPATR(HI)
     A N25N90                               DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
