     A*%%TS  SD  20160908  161329  JEFF        REL-V7R2M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20160908  154441  JEFF        REL-V7R2M0  5770-WDS
     A                                      OVERLAY
     A                                  1  2'OR904.01'
     A                                  1 32'Pallet Number Lookup'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20110316  131010  JEFF        REL-V6R1M0  5761-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD005         1A  O  1  2
     A            WHCODE         3Y 0O  2  3EDTCDE(4)
     A                                  2  7'-'
     A            WHDESC        30A  O  2  9
     A                                  2 46'Position to label # . .'
     A            PTINP1        10A  B  2 70
     A            FLD002         1A  O  3  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20160908  161329  JEFF        REL-V7R2M0  5770-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PSPAL#         7Y 0O 11  8EDTCDE(Z)
     A            PSPLID         7Y 0O 11 17EDTCDE(Z)
     A            PSCOMP         1A  O 11 30
     A            PSSZON         6A  O 11 39
     A            HSSTP1         3Y 0O 11 47EDTCDE(Z)
     A            HSSTP2         3Y 0O 11 51EDTCDE(Z)
     A            PSPCS          5Y 0O 11 56EDTCDE(Z)
     A            PSCUBE         7Y 3O 11 64EDTWRD('    .   ')
     A            PSTYPE         1A  O 11 75
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20160908  155617  JEFF        REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  9  8'Pallet'
     A                                      DSPATR(HI)
     A                                  9 17'Pallet'
     A                                      DSPATR(HI)
     A                                  9 26'Truck'
     A                                      DSPATR(HI)
     A                                  9 47'Stop'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  8'Number '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 17'Id     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 26'Compartment'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 39'Zone  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Range  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 56'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'Cubes   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
