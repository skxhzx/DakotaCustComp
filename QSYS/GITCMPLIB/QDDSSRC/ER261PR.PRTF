     A****************************************************************
     A*
     A* ER261PR   Task report for a shift
     A* 12 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER261'
     A*
     A* Print shift code and description
     A*
     A          R SHHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'SHIFT:'
     A            WKSHFT    R             +1REFFLD(WKSHFT ER261)
     A            SHDESC    R             +1REFFLD(SHDESC SHIFT)
     A  95                                75
     A                                      '(Continued)'
     A*
     A* Print crew code and description
     A*
     A          R CRHEAD
     A                                      SPACEA(002)
     A                                     1'CREW:'
     A            WKCREW    R             +1REFFLD(WKCREW ER261)
     A            CRDESC    R             +1REFFLD(CRDESC CREW)
     A  94                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    11'Employee'
     A                                    62'Direct'
     A                                    70'In dir'
     A                                   122'% Total'
     A                                      SPACEA(001)
     A*
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    11'  Number'
     A                                      UNDERLINE
     A                                    20'Name                              '
     A                                      UNDERLINE
     A                                    62' Hours'
     A                                      UNDERLINE
     A                                    70' Hours'
     A                                      UNDERLINE
     A                                    78' Hours'
     A                                      UNDERLINE
     A                                    85'Task  '
     A                                      UNDERLINE
     A                                    92'Description                  '
     A                                      UNDERLINE
     A                                   122'  Hours'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A  93                                75
     A                                      '(Continued)'
     A*
     A* Print employee line
     A*
     A          R EPHEAD
     A                                      SPACEA(001)
     A            WKEMP#    R             14REFFLD(WKEMP# ER261)
     A                                       EDTCDE(Z)
     A            WKNAME    R             20REFFLD(WKNAME ER261)
     A            DIRTOT         5S 2     62EDTCDE(3)
     A            INDTOT         5S 2     70EDTCDE(3)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            HOURS          5S 2     78EDTCDE(3)
     A            WKCODE    R             85REFFLD(WKCODE ER261)
     A            TKDESC    R             92REFFLD(TKDESC TASK)
     A            PCNHRS         5S 2    123EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#DTOT         7S 2     60EDTCDE(3)
     A            T#ITOT         7S 2     68EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Print summary
     A*
     A          R SUMHD1
     A                                     1'Summary:'
     a                                      SPACEA(001)
     A                                    87'% Total'
     A                                      SPACEA(001)
     A                                    37' Hours'
     A                                      UNDERLINE
     A                                    48'Task  '
     A                                      UNDERLINE
     A                                    55'Description                  '
     A                                      UNDERLINE
     A                                    87'  Hours'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A          R SUMAR1
     A            S#HRS          5S 2     38EDTCDE(3)
     A            S#TSK     R             48REFFLD(WKCODE ER261)
     A            S#DSC     R             56REFFLD(TKDESC TASK)
     A            S#PCN          5S 2     88EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER261: End of Report '
     A*
