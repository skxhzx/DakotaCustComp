     A*%%TS  SD  20120502  101334  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software  Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*  HB112FM    Question Type  File Maintenance - change
     A*  20 February 2012
     A*  Lynn McMahon
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*
640aAA*    02/20/12  LMC  6.40a
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20120502  083652  LYNNM       REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'HB112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  9  4'Code . . . . . . . . .'
     A                                 10  4'Description  . . . . .'
     A            W1TYPE         3A  B  9 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A            W1DESC        25A  B 10 28CHECK(LC)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 23'HACCP Question Type File Maintenan-
     A                                      ce'
     A                                      DSPATR(HI)
     A                                  8  4'Level. . . . . . . . .'
     A                                 11  4'Lookup/Select Program.'
     A            W1LOOK        10A  B 11 28
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1LEVL         4A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A            W1UCTL         1A  B 13 28
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      VALUES('Y' 'N')
     A                                 13  4'User Controlled  Y/N .'
     A                                 12  4'Entry NO Validation. .'
     A            W1ENTRY        1A  B 12 28
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      VALUES('Y' 'N')
     A                                 14  4'Weeks of History . . .'
     A            W1WKHST        3Y 0B 14 28EDTCDE(Z)
     A            SVWKHST        3Y 0B 14 34EDTCDE(Z)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  20120502  101334  LYNNM       REL-V6R1M0  5761-WDS
     A                                      CF10(10 'Accept change of driver')
     A                                      WINDOW(9 33 11 40)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  1' W A R N I N G : '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  1'Reducing the weeks of history to k-
     A                                      eep.'
     A                                      DSPATR(HI)
     A                                  5  2'If you wish to do so, enter a reas-
     A                                      on'
     A                                      DSPATR(HI)
     A                                  6  2'and press F10, otherwise press F12'
     A                                      DSPATR(HI)
     A            W1REASON      30A  B  8 10
     A                                  8  2'Reason:'
     A                                 10  1'F10=Accept  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
