     A****************************************************************
     A*
     A* LOAD30PR   Route Loading Comments report - By 'written by'
     A* September 11, 2007
     A* Jose Duque
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    57
     A                                      DFT('Route Loading Comments')
     A*>                                 120
     A*>                                    DFT('Page')
     A*>          PAGE           4S 0O   124
     A*>                                    EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    53
     A                                   125
     A                                      DFT('Loadr30')
     A                                      SPACEA(002)
     A*
     A          R HEAD2
     A            FRMDTE         6S 0O    55EDTCDE(Y)
     A                                    65
     A                                      DFT('Thru')
     A            TODTE          6S 0O    71EDTCDE(Y)
     A                                      SPACEA(002)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    19
     A                                      DFT('Runner/')
     A                                    31
     A                                      DFT('Comment')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Rte #')
     A                                     8
     A                                      DFT('Trailer #')
     A                                    19
     A                                      DFT('Loader')
     A                                    31
     A                                      DFT('Written by')
     A                                    43
     A                                      DFT('Date')
     A                                    53
     A                                      DFT('Type')
     A                                    59
     A                                      DFT('Reason Code Desc')
     A                                    96
     A                                      DFT('Comment')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A  15        LCHRTE    R              1REFFLD(LCHRTE RTECOMM)
      *
     A  15        LCHTRL    R              8REFFLD(LCHTRL RTECOMM)
      *
     A  15        COMMBY    R             19REFFLD(USCODE PIRUSER)
      *
     A  15        WRITTN    R             31REFFLD(USCODE PIRUSER)
      *
     A  15        DATE           6S 0O    43EDTCDE(Y)
      *
     A  15        TYPE           4A       53
      *
     A  15        LCSDSC    R             59REFFLD(LCSDSC LDRCOMC)
      *
     A            COMMNT    R             96REFFLD(COMMNT RTECOMM)
     A                                      SPACEA(001)
     A*
     A*----------------------------------------------------------------
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('Loadr30: End of Report ')
     A*
