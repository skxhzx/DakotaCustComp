     A*%%TS  SD  20071008  102936  LYNNM       REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  MF132FM    Manufacturer code items maintenance
     A*  Lynn McMahon
     A*
     A*  Revisions
     A*
     A*    09/25/07  LMC  5.10
     A*      - Created
510aAA*    10/02/07  LMC  5.10a
     A*      - Fix: Changed mfr# field size to agree with vendor size
510bAA*    10/08/07  LMC  5.10b
     A*      - Enh: Added upc code
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20070926  073502  LYNNM       REL-V5R4M0  5722-WDS
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20071008  102936  LYNNM       REL-V5R4M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'MF132.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  8  3'Manufacturer # . . . .'
     A                                 11  3'Manufacturer Item. . .'
     A            W1IMIT        15A  B 11 27
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 23  2'F3=Exit  F4=Lookup  F5=Refresh  F1-
     A                                      2=Cancel'
     A                                      COLOR(BLU)
     A                                  1 29'Manufacturer Code Items'
     A                                      DSPATR(HI)
     A                                  9  3'Definition # . . . . .'
     A            W1IDIT        15A  B 13 27
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A                                      DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 13  3'Dakota Item. . . . . .'
     A                                  8 39'-'
     A            MFNAME        40A  O  8 41
     A            MFIMF#        11Y 0O  8 27EDTCDE(4)
     A            W1IDD#         2S 0O  9 27
     A                                 23 46' '
     A                                 12  3'UPC. . . . . . . . . .'
     A            W1UPC         20A  B 12 27
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
