     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  UI340FM   UCI Inventory entry - Stuff it - RF Mini Screen
     A*  31 March,  2007
     A*  Jeffrey Jahnke
     A*
     A*  UI340FM - 5.10
     A*
     A*  Revisions
     A*
510 AA*    03/31/07  JCJ  5.10 A
     A*      - Created using UI330FM
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*----------------------------------------------------------------
     A          R SCREEN1
     A*%%TS  SD  20070331  155246  JEFF        REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'UI340.01 '
     A                                  1 13'UCI Inv Entry'
     A                                  3  2'Dept.'
     A            W1WHDP         5A  B  3  8
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A* 01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Slot '
      *           W1DISP        12A  B  4  8CHECK(ER)
      * 22                                  DSPATR(RI)
      * 02
      * 20                                  DSPATR(PC)
      *N22                                  DSPATR(HI)
     A            W1DSP1         7A  B  4  8
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02
     AO 20                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A            W1DSP2         5A  B  4 16
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  6  2'Lcn'
     A            W1LCN         15   B  6  8CHECK(ER)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F3=Exit  '
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R SCREEN2
     A*%%TS  SD  20070401  162147  JEFF        REL-V5R3M0  5722-WDS
     A                                      CF14
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'UI340.02 '
     A                                  1 13'UCI Inv Entry'
     A                                  3  1'UCI:'
     A            W2UCI         10A  B  3  6CHECK(ER)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A                                  5  6'Count:'
     A            W2COUNT        3Y 0O  5 13EDTCDE(Z)
     A                                  7  2' F14=Done'
     A                                      COLOR(BLU)
     *****************************************************************
     *
     * Error message subfile records
     *
     *    Error message subfile record
     *
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     *
     *    Error message subfile control record
     *
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
