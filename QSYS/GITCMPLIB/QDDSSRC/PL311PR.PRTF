     A****************************************************************
     A*
     A* PL311PR   Pallet utilization report by length.
     A* 12 April, 1996
     A* Hemant Kapadia
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      DFT('PL311')
     A                                      SPACEA(001)
     A*
     A          R PLHEAD
     A                                      SPACEA(002)
     A                                     1'  Pallet code:'
     A            PKPAL     R        O    +1REFFLD(PKPAL  PACKAGE)
     A  93                                75
     A                                      DFT('(Continued)')
     A*
     A          R OHHEAD
     A                                      SPACEA(001)
     A                                     1'     Overhang:'
     A            PKHANG    R        O    +1REFFLD(PKHANG PACKAGE)
     A                                      EDTCDE(3)
     a                                    +1'Inches'
     A  92                                75
     A                                      DFT('(Continued)')
     A*
     A          R LNHEAD
     A                                      SPACEA(001)
     A                                     1'Pallet length:'
     A            PKLEN     R        O    +1REFFLD(PKLEN  PACKAGE)
     A                                      EDTCDE(3)
     a                                    +1'Inches'
     A  91                                75
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1'Pkg.Width'
     A                                    16'Sq.Inch'
     A*
     A                                     1'in Inches'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    16'Per Pkg'
     A                                      UNDERLINE
     A                                    27'Tie Qty'
     A                                      UNDERLINE
     A                                    39'Usage%'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            PKWDTH    R        O     1REFFLD(PKWDTH PACKAGE)
     A                                      EDTCDE(3)
     A            SQINCH         9S 2O    11EDTCDE(3)
     A            PKTIE     R        O    31REFFLD(PKTIE  PACKAGE)
     A                                      EDTCDE(3)
     A            PKUPCT    R        O    39REFFLD(PKUPCT PACKAGE)
     A                                      EDTCDE(3)
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A       93
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('PL311: End of Report ')
     A*
