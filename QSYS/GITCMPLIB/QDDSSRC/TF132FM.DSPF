     A*%%TS  SD  19960412  093155  HEMANT      REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TF132.01'
     A                                  1 28'Transfer Order Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Transfer number . . . . . .'
     A            W1DNUM         7Y 0O  7 31EDTCDE(4)
     A                                  8  3'Picking warehouse . . . . .'
     A            W1HFWH         3Y 0O  8 31EDTCDE(4)
     A            W1FDSC        30   O  8 36
     A                                  9  3'Requesting warehouse  . . .'
     A            W1HTWH         3Y 0O  9 31EDTCDE(4)
     A            W1TDSC        30   O  9 36
     A                                 11  3'Item number . . . . . . . .'
     A            W1DITM        15   B 11 31
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19960412  093155  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TF132.02'
     A                                  1 28'Transfer Order Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  3'Transfer number . . . . . .'
     A            W2DNUM         7Y 0O  7 31EDTCDE(4)
     A                                  8  3'Picking warehouse . . . . .'
     A            W2HFWH         3Y 0O  8 31EDTCDE(4)
     A            W2FDSC        30A  O  8 36
     A                                  9  3'Requesting warehouse  . . .'
     A            W2HTWH         3Y 0O  9 31EDTCDE(4)
     A            W2TDSC        30A  O  9 36
     A                                 10  3'Item number . . . . . . . .'
     A            W2DITM        15A  O 10 31
     A                                 12  3'Quantity ordered  . . . . .'
     A            W2DQO1         5Y 0B 12 31EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  61
     AO 90                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A            W2DUM1         2A  O 12 37
     A            W2DQO2         5Y 0B 12 41EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  62
     AO 90                                  DSPATR(PR)
     A  62                                  DSPATR(ND)
     A            W2DUM2         2A  O 12 47
     A            W2DQO3         5Y 0B 12 51EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  63
     AO 90                                  DSPATR(PR)
     A  63                                  DSPATR(ND)
     A            W2DUM3         2A  O 12 57
     A                                 14  3'Picking warehouse:'
     A                                 15  3'Quantity available  . . . .'
     A            W2DQH1         5Y 0O 15 31EDTCDE(3)
     A  61                                  DSPATR(ND)
     A            W2HUM1         2   O 15 37
     A  61                                  DSPATR(ND)
     A            W2DQH2         5Y 0O 15 41EDTCDE(3)
     A  62                                  DSPATR(ND)
     A            W2HUM2         2   O 15 47
     A  62                                  DSPATR(ND)
     A            W2DQH3         5Y 0O 15 51EDTCDE(3)
     A  63                                  DSPATR(ND)
     A            W2HUM3         2   O 15 57
     A  63                                  DSPATR(ND)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
