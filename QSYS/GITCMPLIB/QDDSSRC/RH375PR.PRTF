     A****************************************************************
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A* RH375PR   Truck Builder Daily Loading Worksheet.
     A* 18 July 2002
     A* Max Blacknik
     A*
     A****************************************************************
     A*
     A*  Revisions:
     A*
416 AA*    07/18/02  MLB  4.16
     A*       - Created.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    52
     A                                      DFT('Daily Loading Worksheet')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    49
     A                                   125
     A                                      DFT('RH375')
     A                                      SPACEA(001)
     A*
     A                                    51
     A                                      DFT('Delivery date:')
     A            DLVDT          8S 0O    66EDTWRD('0 /  /    ')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(003)
     A  91                               120
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                     1'Route'
     A                                      UNDERLINE
     A                                    17'Warehouse'
     A                                      UNDERLINE
     A                                    27'Door'
     A                                      UNDERLINE
     A                                    32'Dr O/F'
     A                                      UNDERLINE
     A                                    42'Cube'
     A                                      UNDERLINE
     A                                    50'Batches'
     A                                      UNDERLINE
     A                                    63'Fork'
     A                                      UNDERLINE
     A                                    73'ReRoutes'
     A                                      UNDERLINE
     A                                    87'Checker'
     A                                      UNDERLINE
     A                                   100'Loader'
     A                                      UNDERLINE
     A                                   112'Pallets'
     A                                      UNDERLINE
     A                                   125'Time'
     A                                      UNDERLINE
     A                                      SPACEA(001)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1RTE          5A  O     1
     A            C1WHDP         5A  O    17
     A            C1CUBE         9S 3O    37EDTCDE(4)
     A            C1DOOR         3S 0O    27EDTCDE(4)
     A            C1OVRF         1A  O    34
     A*
     A* Print DASH1
     A*
     A          R DASH1
     A*
     A                                      SPACEA(001)
     A                                    17'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------'
     A*
     A* Print DASH2
     A*
     A          R DASH2
     A*
     A                                      SPACEA(002)
     A                                     1'==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('RH375: End of Report ')
     A*
