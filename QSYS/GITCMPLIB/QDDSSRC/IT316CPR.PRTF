     A*%%***********************************************************************
     A*%%TS  RD  19930715  163657  DAVE        REL-V2R1M1  5738-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A*
     A*   IT316CPR  Inventory adjustments report
     A*   09 November, 1994
     A*   Hemant Kapadia
     A*
     A*    06/10/96  RH   2.13
     A*      - Create using IT316PR.  Print by Slot, Date.             d
410 AF*    12/07/98  RH   4.10
     F*      - Add Adjustment code report.
415aAF*    06/16/01  RH   4.15a
     F*      - Add WOLINK to show if item is linked.
415bAF*    06/14/01  RH   4.15b
     F*      - Add total gain/loss.
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +42
     A                                      DFT('Inventory Adjustment Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            W1FROM         6S 0O   +43
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TO           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                   124
     A                                      DFT('IT316C')
     A            WHSE          30A  O    52
     A                                      SPACEB(001)
     A                                    60'By Slot/Date'
     A                                      SPACEB(001)
410 DA*                                   50'Item:'
410 MA                                    10'Item:'
     A                                      SPACEB(001)
410 DA*           ITEM          15A  O    56
410 MA            ITEM          15A  O    16
415aAA            WOLINK         6A  O    32
415aMA                                    40'Slot:'
415aMA            DISP          12A  O    46
415aAA                                    61'Adjustment code:'
415aAA            CODE           4A  O    78
410 DA*                                   74'Slot:'
410 DA*                                   34'Slot:'
410 DA*           DISP          12A  O    80
410 DA*           DISP          12A  O    40
410 DA*                                   55'Adjustment code:'
410 DA*           CODE           4A  O    72
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%SD  999999
     A*%%SD  9999
     A*%%CL 001
     A*%%SD  999999
     A*%%SD  999999
     A*%%SD  999999
     A*%%CL 001
     A*%%SD  XXXXX
     A*%%CL 002
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('Date')
     A                                      UNDERLINE
     A                                    24
     A                                      DFT('Time')
     A                                      UNDERLINE
     A                                    30
     A                                      DFT('Item Number')
     A                                      UNDERLINE
     A                                    46
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    77
     A                                      DFT('User')
     A                                      UNDERLINE
     A                                    88
     A                                      DFT('Quantity')
     A                                      UNDERLINE
     A                                    98
     A                                      DFT('Catch Wt Adj.')
     A                                      UNDERLINE
     A                                   112
     A                                      DFT('Code')
     A                                      UNDERLINE
     A                                   117
     A                                      DFT('Memo')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  92        IADISP    R        O     1
     A                                      REFFLD(IAREC/IADISP *LIBL/ITEMADJ)
     A  91        W1DATE         6S 0O    14
     A                                      EDTCDE(Y)
     A            W1TIME         4S 0O    24
     A                                      EDTWRD('  :  ')
     A            IAITEM    R        O    +1
     A                                      REFFLD(IAREC/IAITEM *LIBL/ITEMADJ)
     A            ITDESC    R        O    +1
     A                                      REFFLD(ITDESC PIRITEM)
     A            IABY      R        O    +1
     A                                      REFFLD(IAREC/IABY *LIBL/ITEMADJ)
     A            W1QTY         10A  O    +1
     A            IACWTA    R        O    +1
     A                                      REFFLD(IAREC/IACWTA *LIBL/ITEMADJ)
     A                                      EDTCDE(K)
     A            IACODE    R        O   114
     A                                      REFFLD(IAREC/IACODE *LIBL/ITEMADJ)
     A            W1MEM1        15A  O   117
     A*%%***********************************************************************
     A          R DETAIL2
     A                                      SPACEA(001)
     A            W1MEM2        15A  O   117
     A*%%***********************************************************************
     A*%%SS
     A*%%SD  XXXXXXXXXXXXXXX
     A*%%SD  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
     A*%%SD  999999
     A*%%SD  9999
     A*%%SD  XXXXXXXXXXXX
     A*%%SD  XXXXXXXXXX
     A*%%SD  99999
     A*%%SD  XX
     A*%%SD  XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
     A*%%CL 001
     A*%%***********************************************************************
214 AA*
214 AA* Print detail line3
214 AA*
214 AA          R DETAIL3
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
214 AA                                      SPACEB(001)
214 AA                                      SPACEA(001)
214 AA                                     1'NO RECORDS MATCH ENTERED CRITERIA'
415b A*%%***********************************************************************
415b A*
415b A          R TOTAL1
415b A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
415b A                                      SPACEB(001)
415b A                                      SPACEA(001)
415b A                                    71'      *Gain/Loss'
415b A            TQTY          10A  O    +1
     A*%%***********************************************************************
     A*
415b A          R TOTAL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
415b A                                      SPACEB(001)
415b A                                      SPACEA(001)
415b A                                    71'*Total Gain/Loss'
415b A            TLQTY         10A  O    +1
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT316C: End of Report')
     A*%%***********************************************************************
     A*
