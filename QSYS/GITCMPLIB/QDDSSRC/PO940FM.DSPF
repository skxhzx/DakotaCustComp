     A*%%TS  SD  19970310  111009  ROCKY       REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO940FM   UPC PO detail by item - Full screen
     A*  31 January, 1997
     A*  Roxanne Harrison/Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19940919  164934  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'PO940.01'
     A                                  1 31'PO Detail Lookup by item'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            FILL1         78A  O  2  2
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19970131  085846  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD001        75A  O  1  2
     A                                  2  2'Warehouse:'
     A                                      DSPATR(HI)
     A            WOWHSE        35A  O  2 13
     A                                  5 34'Position to item  . . .'
     A            PTINP1        15A  B  5 59
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19970131  114520  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 13  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PDITEM    R        O 13  6REFFLD(PDREC/PDITEM *LIBL/PODTL1)
     A            PDSEQ     R        O 13 53REFFLD(PDREC/PDSEQ *LIBL/PODTL1)
     A                                      EDTCDE(4)
     A            ITDESC    R        O 13 22REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19940922  163714  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 12  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 22'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 53'Line #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R DSPSFL1                   SFL
     A*%%TS  SD  19970131  114520  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 13  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PDITEM    R        O 13  6REFFLD(PDREC/PDITEM *LIBL/PODTL1)
     A            IXDSC1    R        O 13 22REFFLD(IXREC/IXDSC1 *LIBL/ITEMDSC)
     A            PDSEQ     R        O 13 63REFFLD(PDREC/PDSEQ *LIBL/PODTL1)
     A                                      EDTCDE(4)
     A            IXDSC2    R        O 14 22REFFLD(IXREC/IXDSC2 *LIBL/ITEMDSC)
     A*
     A          R DSPSFC1                   SFLCTL(DSPSFL1)
     A*%%TS  SD  19970310  111009  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0005)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 12  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 22'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 63'Line #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940922  163714  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(08)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19931115  111104  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            W1CMD         75   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
