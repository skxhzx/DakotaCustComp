     A*%%TS  SD  20070827  154615  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOADR7FM  Truck Load Module - Route Summary History - Options
     A*  November 8, 2004
     A*  Jose Duque
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20070827  154615  JOSED       REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A*
     A                                  1  2'LOADR7.01'
     A*
     A                                  1 22'Route Summary History - View/Print'
     A                                      DSPATR(HI)
     A*
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A*
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A*
     A                                  8  4'Warehouse  . . . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 40EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 10  4'Route #  . . . . . . . . . . . . .'
     A            W1RTE#         5A  B 10 40
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'Code, *All'
     A*
     A                                 11  4'From date  (MMDDYY). . . . . . . .'
     A            W1FRDT         6Y 0B 11 40EDTCDE(4)
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 12  4'To date    (MMDDYY). . . . . . . .'
     A            W1TODT         6Y 0B 12 40EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 13  4'Order report . . . . . . . . . . .'
     A            W1SORT         1A  B 13 40
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 49'1 = Route'
     A                                 14 49'2 = Loader'
     A*
     A                                 17  4'View or print  . . . . . . . . . .'
     A            W1VP           1A  B 17 40
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 51'(V=View, P=Print)'
     A*
     A                                 18  4'Place on job queue ? . . . . . . .'
     A            W1JOBQ         1A  B 18 40
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                 18 51'(Y=Yes, N=No)'
     A*
     A                                 19  4'Printer device . . . . . . . . . .'
     A            W1DEV         10A  B 19 40
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 20  4'Form type  . . . . . . . . . . . .'
     A            W1FORM        10A  B 20 40
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                 15 49'3 = Loader-Plt Dtl'
     A                                 16 49'4 = Loader-Rte Loading Summ '
