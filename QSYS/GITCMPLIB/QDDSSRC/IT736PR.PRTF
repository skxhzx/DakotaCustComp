     A*------------------------------------------------------------------------*
     A*
     A*   IT736PR  FTP Export item quantities/license - Exceptions
     A*   18 October 2011
     A*   Max Blacknik
     A*
     A*------------------------------------------------------------------------*
     A*
640 AA*    10/18/11  MLB  6.40
     A*      - Enh: Added exception report to list out any items where
     A*        the warehouse qty does not match the total qty in the whse.
     A*
     A**************************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            RDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    44
     A                                      DFT('FTP Export Item Whse Qty/Licen-
     A                                      se - Exceptions')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            RTIME          6S 0O     3
     A                                      EDTWRD('0  :  :  ')
     A                                      SPACEB(001)
     A            HEDLN2        60A  O   +26
     A                                   125
     A                                      DFT('IT736')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      DFT('Item Number')
     A                                    17
     A                                      DFT('Description')
     A                                    49
     A                                      DFT('Slot Stkl')
     A                                    59
     A                                      DFT('Slot Stk2')
     A                                    69
     A                                      DFT('Slot Stk3')
     A                                    79
     A                                      DFT('Lic Qty 1')
     A                                    89
     A                                      DFT('Lic Qty 2')
     A                                    99
     A                                      DFT('Lic Qty 3')
     A                                   110
     A                                      DFT('Error Message')
     A                                      SPACEA(001)
     A*
     A                                     3
     A                                      DFT('License')
     A                                    19
     A                                      DFT('Orig. License')
     A*
     A* Print detail lines
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            QEITEM    R        O     1
     A                                      REFFLD(QEITEM EXPFQTY)
     A            ITDESC    R        O    17
     A                                      REFFLD(ITDESC PIRITEM)
     A            SLSTK1    R        O    50EDTCDE(J)
     A                                      REFFLD(SLSTK1 SLOT)
     A            SLSTK2    R        O    61EDTCDE(K)
     A                                      REFFLD(SLSTK2 SLOT)
     A            SLSTK3    R        O    71EDTCDE(K)
     A                                      REFFLD(SLSTK3 SLOT)
     A*
     A            ERMSG1        23A  O   110
     A*
     A*
     A          R DETAIL2
     A                                      SPACEA(001)
     A            LALCNS    R        O     3
     A                                      REFFLD(LALCNS LICACTIVE)
     A            LAOLCNS   R        O    19
     A                                      REFFLD(LAOLCNS LICACTIVE)
     A            LAQTY1    R        O    80EDTCDE(J)
     A                                      REFFLD(LAQTY1 LICACTIVE)
     A            LAQTY2    R        O    90EDTCDE(K)
     A                                      REFFLD(LAQTY2 LICACTIVE)
     A            LAQTY3    R        O   100EDTCDE(K)
     A                                      REFFLD(LAQTY3 LICACTIVE)
     A*
     A* Print license totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                    63
     A                                      DFT('* License Totals')
     A*
     A            TLLIC1         5S 0O    80EDTCDE(J)
     A            TLLIC2         5S 0O    90EDTCDE(J)
     A            TLLIC3         5S 0O   100EDTCDE(J)
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT736: End of Report')
     A*
