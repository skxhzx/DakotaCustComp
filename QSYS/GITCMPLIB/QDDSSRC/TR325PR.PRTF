     A****************************************************************
     A*
     A* TR325PR   Trax route by stop detail - Print
     A* 12 October, 2011
     A* Ginger Andrews
     A*
     A* Revisions:
     A*
     A* 10/12/11  GJA  6.40
     A*  - Created
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    41
     A                                      'Trax Route Stop Detail'
     A                                   123
     A                                      'Page'
     A            PAGE           4S 0O   127
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIMECUR        6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            SCHEAD        58A  O    26
     A                                   126
     A                                      'TR325'
     A                                      SPACEA(002)
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A  91                               120
     A                                      '(Continued)'
     A*
     A* Print column headings for Date order.
     A*
     A          R COLUMNS1
     A                                      SPACEA(001)
     A
     A                                    15
     A                                      'Pieces'
     A                                    27
     A                                      'Pieces'
     A                                    39
     A                                      'Curr / Prev'
     A                                    54
     A                                      'Scan'
     A                                    62
     A                                      'Delivery'
     A                                    81
     A                                      'Delivery'
     A                                      SPACEA(001)
     A                                     2
     A                                      'Stop '
     A                                      UNDERLINE
     A                                    10
     A                                      'Dlv'
     A                                      UNDERLINE
     A                                    15
     A                                      'Delivered'
     A                                      UNDERLINE
     A                                    27
     A                                      'NonScanned'
     A                                      UNDERLINE
     A                                    39
     A                                      'Pcs Returned'
     A                                      UNDERLINE
     A                                    54
     A                                      'Dlvy'
     A                                      UNDERLINE
     A                                    62
     A                                      'Start date/time'
     A                                      UNDERLINE
     A                                    81
     A                                      'Finish date/time'
     A                                      UNDERLINE
     A                                   101
     A                                      'Memo        '
     A                                      UNDERLINE
     A*
     A* Print detail line for Date order
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            TTPSTP    R        O     2REFFLD(TTPREC/TTPSTP *LIBL/TTSTP)
     A            W1PDLV         1A  O    11
     A            W1QDLV         6  0O    15EDTCDE(4)
     A            W1QDLN         6  0O    27EDTCDE(4)
     A            W1QCRT         6  0O    39EDTCDE(4)
     A            W1QPRT         6  0O    46EDTCDE(4)
     A            W1PSCN         1A  O    56
     A            W1STRD         6  0O    62EDTWRD('  /  /  ')
     A            TTPSRT    R        O    71REFFLD(TTPREC/TTPSRT *LIBL/TTSTP)
     A                                      EDTWRD('  :  :  ')
     A            W1FIND         6  0O    81EDTWRD('  /  /  ')
     A            TTPFIT    R        O    90REFFLD(TTPREC/TTPFIT *LIBL/TTSTP)
     A                                      EDTWRD('  :  :  ')
     A            TTPMEM    R        O   101REFFLD(TTPREC/TTPMEM *LIBL/TTSTP)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     2
     A                                      'Total Stops:'
     A            WKCOUNT       10  0O    16
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'TR325: End of Report '
     A*
