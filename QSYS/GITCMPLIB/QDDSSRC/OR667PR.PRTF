     A****************************************************************
     A*
     A* OR667PR   Truck Pallet detail report Loader/Merge Compartments:
     A*           1,2,C,H,I,O,X
     A*
     A* July 10, 2006
     A* Jose Duque
     A*
     A* NOTE: This print file is a copy of 'OR664PR'
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            TITLE         40A  O    21
     A*                                                                )
     A                                    72
     A                                      'Page'
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    26
     A                                    76
     A                                      'OR667'
     A*
     A            BATCH         30A  O    26
     A                                      SPACEB(001)
     A            PSTRUK    R        O    60REFFLD(PSTRUK OR667)
     A            $RTYPE         8A  O    73
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A            P#DESC        80A  O     1
     A  92                                72
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(002)
     A                                     1
     A                                      'Stop'
     A                                     7
     A                                      'Customer'
     A                                    19
     A                                      'Name'
     A                                    49
     A                                      'Pieces'
     A                                    66
     A                                      'Cube'
     A                                    76
     A                                      'Weight'
     A                                    84
     A                                      'Transaction#'
     A*
     A          R COLUMND
     A                                      SPACEB(002)
     A                                     1
     A                                      'Plt'
     A                                      UNDERLINE
     A                                     6
     A                                      'Slot        '
     A                                      UNDERLINE
     A                                    21
     A                                      'Quantity'
     A                                      UNDERLINE
     A                                    31
     A                                      'Item #'
     A                                      UNDERLINE
     A                                    39
     A                                      'Description                   '
     A                                      UNDERLINE
     A                                    72
     A                                      'Cube/Pack  '
     A                                      UNDERLINE
     A                                    86
     A                                      'Wt/Brand'
     A                                      UNDERLINE
     A                                    98
     A                                      'Loader '
     A                                      UNDERLINE
     A*
     A* Print detail line  (For Detail Report only)
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
     A                                     2'Customer #:'
     A            CUST#         11A  O    14
     A            CSNAME        35A  O    26
     A                                    63'Order #:'
     A            OHCORD    R        O    73REFFLD(OHCORD ORDH)
     A*
     A*  Print item detail (For Detail Report only)
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            PAL#           3S 0O     1
     A*           PSPAL#    R        O     1
     A*                                     REFFLD(PSPAL# OR660)
     A                                      EDTCDE(Z)
     A            LBDISP    R        O     6REFFLD(LBDISP LABEL)
     A*           T#QPCK         7S 0O    46
     A            T#QPCK         7S 0O    19
     A                                      EDTCDE(Z)
     A*           LBUCOD         2A  O    54
     A            LBUCOD         2A  O    27
     A*           LBITEM    R        O     7
     A            ITEM#          6A  O    31
     A*           LBITEM    R        O    36
     A*                                     REFFLD(LBITEM OR660)
     A            DESCI1        30A  O    39
     A            T#CUBE        10S 3O    70
     A                                      EDTCDE(2)
     A            T#WGHT         8S 2O    84
     A                                      EDTCDE(2)
     A                                    98'_______'
     A*
     A*  Print item pack description (For Detail Report only)
     A*
     A          R DETAIL3
     A                                      SPACEB(001)
     A            DESCI2        30A  O    39
     A            ITPDSC    R        O    72
     A                                      REFFLD(ITPDSC OR667)
     A            BRAND         10A  O    86
     A*
     A*  Print CW entry line for quantity picked.
     A*
     A          R CWLIN
     A                                      SPACEB(001)
     A            CWLN1          8A  O    21
     A            CWLN2          8A  O    33
     A            CWLN3          8A  O    45
     A            CWLN4          8A  O    57
     A            CWLN5          8A  O    69
     A            CWLN6          8A  O    81
     A            CWLN7          8A  O    93
     A*
     A*  Stop totals (For Detail Report only)
     A*
     A          R TOTALD
     A                                      SPACEB(001)
     A            $RPD          25A  O    20
     A            T#QPCK         7S 0O    48
     A                                      EDTCDE(Z)
     A            T#CUBE        11S 3O    56
     A                                      EDTCDE(2)
     A            T#WGHT        11S 2O    70
     A                                      EDTCDE(2)
     A*
     A* Print totals
     A*
     A*  Stop totals (For Summary Report only)
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            LBSTOP    R        O     1
     A                                      REFFLD(LBSTOP OR667)
     A                                      EDTCDE(Z)
     A            CUST#     R        O     7
     A                                      REFFLD(CUST#  *SRC)
     A            CSNAME    R        O    19
     A                                      REFFLD(CSNAME *SRC)
     A            T#QPCK    R        O    48
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    56
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
410 MA            T#WGHT    R        O    70
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Pallet totals (For Both Detail and Summary)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    48
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    56
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
     A            T#WGHT    R        O    70
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Department totals (For Both Detail and Summary)
     A*
     A          R TOTAL3
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    48
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    56
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
     A            T#WGHT    R        O    70
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Route totals (For Both Detail and Summary)
     A*
     A          R TOTAL4
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    48
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    56
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
     A            T#WGHT    R        O    70
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                     1
     A                                      'OR667: End of Report '
     A*
