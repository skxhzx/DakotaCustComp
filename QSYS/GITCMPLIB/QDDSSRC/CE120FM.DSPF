     A*%%TS  SD  20070425  131124  BFC         REL-V5R2M0  5722-WDS
     A**************************************************************************
     A*  Revisions:
     A*
     A*    10/17/01  HNK  4.16
     A*     - Created
510 AA*    11/01/06  JCJ  5.10
     A*     - Fix: Increased w1item from 7a to 10a.  Added warhouse selection
     A*            to screen1.
510aAA*    02/27/07  JCJ  5.10a
     A*     - Enh: Renamed CWENTRY to SCREEN1 & SPAKERLB to SCREEN3.
510bAA*    04/25/07  JCJ  5.10b
     A*     - Enh: Added Screen4 for license scanning.
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*****************************************************************
     A          R SCREEN1
     A*%%TS  SD  20070227  162110  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  3'CE120.01 CW Entry        '
     A                                      DSPATR(HI)
     A                                  4  3'Warehouse'
     A            W1WHSE         3Y 0B  4 14
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5  3'Scan Tran#'
     A            W1TRAN         7Y 0B  5 14EDTCDE(Z)
510jD ***                               7  3'F3=Exit'
510jMC                                  6  3'F3=Exit'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
510bAA            W1MSG         40A     7  1
510bAA                                      DSPATR(HI)
     A                                  8  1' '
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R CWESFL                    SFL
     A*%%TS  SD  20061101  072014  JEFF        REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            W2OPT          2A  B  4  2
510 MA            W2ITEM        10A  O  4  5
     A            W2DESC         9A  O  4 16
     A            W2LBL#         7Y 0O  4 32EDTCDE(3)
     A            W2QRMN         5Y 0O  4 26EDTCDE(3)
     A            RECNOX         3S 0O  4 40
     A*
     A          R CWECTL                    SFLCTL(CWESFL)
     A*%%TS  SD  20061101  072014  JEFF        REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  1  3'CE120.02 CW Entry'
     A                                      DSPATR(HI)
     A                                  2  3'Scan BFC Label'
     A            W2SCNL         7Y 0B  2 20EDTCDE(Z)
     A                                  3  5'Item'
     A                                  3 16'Desc'
     A                                  3 27'Qty'
     A                                  3 32'Bfc Lbl'
     A*****************************************************************
     A*
     A          R CWCMDLINE
     A                                      LOCK
     A                                      OVERLAY
510bD ***                                   SLNO(08)
510bMA                                      SLNO(07)
510jD ***                               1  2'F12=Cancel'
510jMC                                  1  2'F12=Cnl  Opt 1=UCI 2=Lic 16=CtWgt'
     A                                      COLOR(BLU)
     A                                      DSPATR(HI)
510bAA            W2MSG         40A     2  2
510bAA                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCREEN3
     A*%%TS  SD  20070227  111155  JEFF        REL-V5R3M0  5722-WDS
     A                                      CA16
510bAA                                      CA17
     A                                      CA20
     A*                                     OVERLAY
     A                                  1  3'CE120.03 CW Entry        '
     A                                      DSPATR(HI)
510bA * NOTE all these lines moved up 1 line
510bMA                                  2  1'Item:'
510bMA            W3ITEM        15A  O  2  7
510bMA            W3DESC        16A  O  2 24
510bMA                                  3  2'Qty:'
510bMA            W3QRMN         5Y 0O  3  7EDTCDE(3)
510bMA                                  3 15'BFC Lbl:'
510bMA            W2HLBL         7Y 0O  3 24EDTCDE(Z)
510bMA                                  4  2' UCI #:'
510bMA            W10LBL        10A  B  4 10CHECK(ER)
510bMA                                  5  2'Tot CW Scn:'
510bMA            W3SCAN         5Y 0O  5 14EDTCDE(3)
510bMA                                  6  2'F12=Cncl F16=Dtl'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
510bD ***                               8  2'F20=Complete'
510bAA                                  7  2'F17=Scan Lic# F20=Complete'
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
510bAA            W3MSG         40A     8  2
510bAA                                      DSPATR(HI)
     A*
510bAA          R SCREEN4
510bAA*%%TS  SD  20070425  131124  BFC         REL-V5R2M0  5722-WDS
510bAA                                      CA16
510bAA                                      CA20
510bAA*                                     OVERLAY
510bAA                                  1  3'CE120.04 CW Entry        '
510bAA                                      DSPATR(HI)
510bAA                                  3  1'Item:'
510bAA            W3ITEM        15A  O  3  7
510bAA            W3DESC        16A  O  3 24
510bAA                                  4  2'Qty:'
510bAA            W4QRMN         5Y 0O  4  7EDTCDE(3)
510bAA                                  4 15'BFC Lbl:'
510bAA            W2HLBL         7Y 0O  4 24EDTCDE(Z)
510bAA                                  5  2' Lcn #:'
510bAA            W4LCN#        15A  B  5 10CHECK(ER)
510bAA                                  6  2'Tot CW Scn:'
510bAA            W4SCAN         5Y 0O  6 14EDTCDE(3)
510bD ***                               7  2'F12=Cncl F16=Dtl'
510bMA                                  7  2'F12=Cnl F16=Dtl F20=Complete'
510bAA                                      DSPATR(HI)
510bAA                                      COLOR(BLU)
510bD ***                               8  2'F20=Complete'
510bMA            W4MSG         40A     8  2
510bAA                                      DSPATR(HI)
510bD ***                                   COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(9)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
