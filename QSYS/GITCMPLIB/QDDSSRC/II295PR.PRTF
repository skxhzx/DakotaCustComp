     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  II295PR    FTP Import Item Updates 4 - Print
     A*  23 February 2007
     A*  Dave Sommerville
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
510 AA*    02/23/07  DAS  5.10
     A*      - Created.
510aAA*    05/08/07  MLB  5.10a
     A*      - Fix: Change report to specify new field name for
     A*        report date. Keyword DATE is session or job date
     A*        and does not always yield the correct date.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
510 DA*                                    1
510 DA*                                     DATE
510 MA            RDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
     A                                      'II295'
     A                                      SPACEB(002)
     A                                     2
     A                                      'Item           '
     A                                    19
     A                                      'Description'
     A                                   100
     A                                      'Message'
     A*
     A          R DETAIL
     A                                      SPACEB(001)
     A            IUITEM    R        O     2
     A                                      REFFLD(IUREC/IUITEM *LIBL/IMPITEM)
     A            IUDESC    R        O    19
     A                                      REFFLD(IUREC/IUDESC *LIBL/IMPITEM)
     A            DETMSG        30A  O   100
     A*
     A          R GRNDTTL
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Detail lines   :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A*                                     SPACEB(002)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'New designation codes            :'
     A                                      SPACEB(001)
     A            NEWDSG         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'New activity zone codes          :'
     A                                      SPACEB(001)
     A            NEWACT         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'New build codes                  :'
     A                                      SPACEB(001)
     A            NEWBLD         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'New unit of measure codes        :'
     A                                      SPACEB(001)
     A            NEWUM          7S 0O    39
     A                                      EDTCDE(J)
     A*
