     A*%%TS  SD  20000310  150721  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS160FM   Truck Build split stops. - Display screen
     A*  01 December 1999
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
413 AA*    03/10/00  DAS  4.13
     A*      - Rearranged list fields
     A*      - Added pallet cube and max pallet cube fields.
     A*      - Duplicated stop on right side of list.
     A*
520 AA*    01/23/09  CWM  5.20
     A*      Enh: Added support for 3 digit stop numbers.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions: City Line
     A*
CLD AA*    01/13/10  MLB  CLD
     A*      - Revised display file to highlight the following
     A*        fields: Row, Column, Stop Cube and Pallet Cube.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'PS160.01'
     A                                  1 31'Work with Split Stops'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Truck Detail'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  1  2'Warehouse:'
     A            WOWHSE        35A  O  1 13
     A                                  1 52'Route:'
     A            WORTE          5A  O  1 59
     A                                  2 52'Position to Compartment '
     A            PTINP1         1A  B  2 77
     A                                  3 52'Position to Stop . . . .'
520 DA*           PTINP2         2Y 0B  3 77EDTCDE(4)
520 MA            PTINP2         3Y 0B  3 77EDTCDE(4)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20000310  150721  DAVE        REL-V4R4M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1COMP         1A  O 11  7
520 DA*           W1STOP         3Y 0O 11 10EDTCDE(4)
520 MA            W1STOP         3Y 0H
520 AA            X1STOP         3Y 0O 11 10EDTCDE(4)
     A            W1ROW          2Y 0O 11 15EDTCDE(4)
CLD AA  51                                  DSPATR(HI)
     A            W1COL          2Y 0O 11 20EDTCDE(4)
CLD AA  51                                  DSPATR(HI)
     A            W1UNLD         2Y 0O 11 24EDTCDE(4)
     A            W1PCS          5Y 0O 11 28EDTCDE(3)
     A            W1CUBE         7Y 3O 11 44EDTCDE(3)
CLD AA  51                                  DSPATR(HI)
     A            W1SWGT         7Y 2O 11 35EDTCDE(3)
     A            W1BULK         1A  O 11 72
     A                                 11 18'/'
     A            W1PCUB         7Y 3O 11 53EDTWRD('    .   ')
CLD AA  51                                  DSPATR(HI)
     A            W1MCUB         5Y 2O 11 62EDTWRD('   .  ')
520 DA*           W1STP2         3  0O 11 76EDTCDE(4)
520 MA            W1STP2         3  0H
520 AA            X1STP2         3  0O 11 76EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20000310  150721  DAVE        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0030)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A  01                                  SFLEND(*MORE)
     A N01                                  SFLCLR
     A                                  9 23'Unld'
     A                                      DSPATR(HI)
     A                                  9 28'Stop'
     A                                      DSPATR(HI)
     A                                  9 35'Stop'
     A                                      DSPATR(HI)
     A                                  9 44'Stop'
     A                                      DSPATR(HI)
     A                                  9 71'Incl'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Cmp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 10'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Row/Col'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Seq '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 28'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 35'Weight  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 44'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 71'Bulk'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 53'Cube    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 53'Pallet'
     A                                      DSPATR(HI)
     A                                 10 62'Max Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 62'Pallet'
     A                                      DSPATR(HI)
     A                                 10 76'Stop'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
