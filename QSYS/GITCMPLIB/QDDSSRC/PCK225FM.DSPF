     A*%%TS  SD  19970731  064040  DAVE3       REL-V3R1M0  5763-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*----------------------------------------------------------------
     A*****  COMPILE WITH DEFER WRITE EQUAL TO *NO *****
     A*----------------------------------------------------------------
     A*
     A*  PCK224FM   Create random test orders for warehouse 900 - WORKLBL
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
650aAA*    10/28/13  DAS  6.50a
     A*      - Added warehouse
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SCREEN1
     A*%%TS  SD  19970731  064040  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PCK225.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
650aDA**                                1 19'Create Random Test Orders for Ware-
650aDA**                                    house 900'
650aMA                                  1 19'Create Random Test Orders for Ware-
650aMA                                      house'
     A                                      DSPATR(HI)
650aAA            W1WHSE         3S 0O  1 59
650aAA                                      DSPATR(HI)
     A                                  8 20'Compartment'
     A                                      DSPATR(UL)
     A                                  8 34'Max Cube'
     A                                      DSPATR(UL)
     A                                  8 46'Cube'
     A                                      DSPATR(UL)
     A                                  8 53'Pieces'
     A                                      DSPATR(UL)
     A                                  9 23'Dry'
     A                                 10 23'Ref'
     A                                 11 23'Frz'
     A            WODRY          5Y 0O  9 36EDTCDE(3)
     A            W1DRYC         5Y 0O  9 45EDTCDE(3)
     A            W1DRYP         5Y 0O  9 54EDTCDE(3)
     A            WOREF          5Y 0O 10 36EDTCDE(3)
     A            W1REFC         5Y 0O 10 45EDTCDE(3)
     A            W1REFP         5Y 0O 10 54EDTCDE(3)
     A            WOFRZ          5Y 0O 11 36EDTCDE(3)
     A            W1FRZC         5Y 0O 11 45EDTCDE(3)
     A            W1FRZP         5Y 0O 11 54EDTCDE(3)
     A                                 15 31'Orders:'
     A            W1ORD          5Y 0O 15 39EDTCDE(3)
     A                                  4 25'Route:'
     A                                  4 40'Truck:'
     A            $PRTE          5A  O  4 32
     A            $PTRUK        10A  O  4 47
     A                                 21 31'Press ENTER to Exit'
     A                                      DSPATR(HI)
     A N98                                  DSPATR(ND)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
