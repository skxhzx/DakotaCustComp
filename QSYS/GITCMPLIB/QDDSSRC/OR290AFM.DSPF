     A*%%TS  SD  20051228  143447  BFCROCKY    REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR290AFM   Open WORKSOS SOS rec  - Display screen
     A*  28 December 2005
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*  12/28/05  RH   5.00
     A*   -Created using PT160A
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19960524  101928  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 27'Open WORKSOS Single Order'
     A                                  1  2'OR290AFM '
     A*           WOHEAD        50   O  1 16DSPATR(HI)
     A*           W1SCRN         9   O  1  2
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19960524  105804  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 43'Position to transaction . .'
     A                                  2 43'Change to warehouse . . . .'
     A            PTINP1         3Y 0B  2 72EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A            FLD001         1A  O  1  2
     A            PTINP2         7Y 0B  3 72EDTCDE(4)
     A            FLD002         1A  O  4  2
     A            WOWHSE        40   O  2  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20051228  143447  BFCROCKY    REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
416aDA*           W1TYPE        30A  O 11 20
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            WKSTRN    R        O 11  7REFFLD(WKSREC/WKSTRN *LIBL/WORKSOS)
     A                                      EDTCDE(3)
     A            WKSUSR    R        O 11 18REFFLD(WKSREC/WKSUSR *LIBL/WORKSOS)
     A            WKSWHD    R        O 11 31REFFLD(WKSREC/WKSWHD *LIBL/WORKSOS)
     A            WKSDSP    R        O 11 38REFFLD(WKSREC/WKSDSP *LIBL/WORKSOS)
     A            WKSPCK    R        O 11 55REFFLD(WKSREC/WKSPCK *LIBL/WORKSOS)
     A                                      EDTCDE(3)
     A            WKSLBL    R        O 11 63REFFLD(WKSREC/WKSLBL *LIBL/WORKSOS)
     A                                      EDTCDE(3)
     A            WKSPLI    R        O 11 72REFFLD(WKSREC/WKSPLI *LIBL/WORKSOS)
     A                                      EDTCDE(3)
     A***         W1GTIM         6Y 0O 11 68EDTWRD('  :  :  ')
     A***         W1STAT         5A  O 11 72
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20051228  143447  BFCROCKY    REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 18'User      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 31'Dept.'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 38'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'Picked'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Label  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Plt Lic'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19941013  210313  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75   O  3  2COLOR(BLU)
     A            OPTLN1        75   O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A*%%TS  SD  19941109  110048  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 9=Quick close.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
