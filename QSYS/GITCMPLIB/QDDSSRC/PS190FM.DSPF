     A*%%TS  SD  20090128  015654  CHUCKM      REL-V6R1M0  5761-WDS
     A*****************************************************************
     A*
     A*  PS190FM  Pallet List - Display
     A*  07 August 2006
     A*  Jeffrey Jahnke
     A*
     A*  This was a copy of ZH120FM
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    01/28/09  CWM  5.20
     A*      - Enh: Added support for entry of host stop.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20060807  143238  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'PS190 '
     A                                  1 32'Pallet Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20060809  144039  JEFF        REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  1 44'Position to pallet. . . .'
     A            PTINP3         7Y 0B  1 70EDTCDE(Z)
     A            W1TRUK        40A  O  2  2
     A                                  3  2'Route:'
     A            $KRTE          5A  O  3  9
     A                                  3 16'Route Id:'
     A            $KRTID         5A  O  3 26
     A                                  3 33'Zone:'
     A            $KSSZN         6A  O  3 39
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20090128  015654  CHUCKM      REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PSPLID    R        O 11  7REFFLD(PSREC/PSPLID *LIBL/PLTSUM)
     A                                      EDTCDE(Z)
     A            PSPAL#    R        O 11 16REFFLD(PSREC/PSPAL# *LIBL/PLTSUM)
     A                                      EDTCDE(Z)
520 DA*           PSSTP1    R        O 11 25REFFLD(PSREC/PSSTP1 *LIBL/PLTSUM)
520 MA            PSSTP1    R        H      REFFLD(PSREC/PSSTP1 *LIBL/PLTSUM)
520 AA            HSSTP1         3Y 0O 11 25
     A                                      EDTCDE(Z)
520 DA*           PSSTP2    R        O 11 29REFFLD(PSREC/PSSTP2 *LIBL/PLTSUM)
520 MA            PSSTP2    R        H      REFFLD(PSREC/PSSTP2 *LIBL/PLTSUM)
520 AA            HSSTP2         3Y 0O 11 29
     A                                      EDTCDE(Z)
     A            PSPCS     R        O 11 34REFFLD(PSREC/PSPCS *LIBL/PLTSUM)
     A                                      EDTCDE(Z)
     A            PSCUBE    R        O 11 42REFFLD(PSREC/PSCUBE *LIBL/PLTSUM)
     A                                      EDTCDE(L)
     A            PSTYPE    R        O 11 53REFFLD(PSREC/PSTYPE *LIBL/PLTSUM)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20090128  015654  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD001         1A  O  9  3
     A                                  9  7'Pallet'
     A                                      DSPATR(HI)
     A                                  9 16'Pallet'
     A                                      DSPATR(HI)
     A                                  9 25'Stop'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Id     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Number '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 25'Range  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 34'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Cubes   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 52'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19970610  164925  DAVE        REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
