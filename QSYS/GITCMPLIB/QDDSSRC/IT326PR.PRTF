     A****************************************************************
     A*
     A* IT326PR   Warehouse book by item/desig
     A* 09 September, 2007
     A* Roxanne Harrison
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3EDTCDE(Y)
     A                                    52
     A                                      DFT('Warehouse Book by Item/Desg')
     A*                                   65
     A*                                     DFT('with Item/Desc')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT326')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*                                   14
     A*                                     DFT('Pick')
     A                                    25
     A                                      DFT('Pick')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    14
     A                                      DFT('Desg')
     A                                    20
     A                                      DFT('Type')
     A                                    25
     A                                      DFT('Slot')
     A*
     A                                    34
     A                                      DFT('Status')
     A                                      UNDERLINE
     A                                    56
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    73
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                   105
     A                                      DFT('Available Qty')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail L1 line.
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            SLDISP    R        O     1REFFLD(SLDISP IT326)
     A            SLDESG    R        O    14REFFLD(SLDESG IT326)
     A            SLSDEF    R        O    21REFFLD(SLSDEF IT326)
     A            SLPICK    R        O    26REFFLD(SLPICK IT326)
     A*
     A            SLSTAT    R        O    34REFFLD(SLSTAT IT326)
     A            ITITEM    R        O    56REFFLD(ITITEM IT326)
     A            ITDESC    R        O    73REFFLD(ITDESC PIRITEM)
     A            W1QTY         10A  O   105
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('IT326: End of Report ')
     A*
