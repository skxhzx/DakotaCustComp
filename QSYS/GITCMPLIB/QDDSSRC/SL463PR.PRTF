     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SL463PR    Slot Statistics - Print
     A*
     A*  Revisions
     A*
650 AA*    05/02/13  RBD  6.50
     A*      - Created for Dierks Waukesha.
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL463')
     A*
     A          R RPHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    82'Assg   Unasg  Total'
     A                                   103'Used   Avail  Total'
     A                                      SPACEA(001)
     A                                    17'Slt'
     A                                    55'Slot Dimensions'
     A                                    76'Cap'
     A                                    82'Pick   Pick   Pick'
     A                                   103'Resrv  Resrv  Resrv'
     A                                   124'Total'
     A                                      SPACEA(002)
     A                                     1'Department      Typ'
     A                                    21'Description'
     A                                    55'L'
     A                                    62'W'
     A                                    69'H'
     A                                    76'Cube'
     A                                    82'Slots  Slots  Slots'
     A                                   103'Slots  Slots  Slots  Slots'
     A*
     A* Print L1
     A*
     A          R TOTAL1
     A                                      SPACEA(001)
     A            DEPSEC        11A  O     1
     A            SLSDEF    R        O    17REFFLD(SLSDEF SLOT)
     A            SDDESC    R        O    21REFFLD(SDDESC SLOTDEF)
     A            SDLEN     R        O    52REFFLD(SDLEN  SLOTDEF)
     A                                      EDTCDE(3)
     A            SDWDTH    R        O    59REFFLD(SDWDTH SLOTDEF)
     A                                      EDTCDE(3)
     A            SDHGT     R        O    66REFFLD(SDHGT  SLOTDEF)
     A                                      EDTCDE(3)
     A            CAPCUB         6S 1O    73
     A                                      EDTCDE(3)
     A            T#PASG         6S 0O    81
     A                                      EDTCDE(4)
     A            T#PUNA         6S 0O    88
     A                                      EDTCDE(4)
     A            T#PTOT         6S 0O    95
     A                                      EDTCDE(4)
     A            T#RUSD         6S 0O   102
     A                                      EDTCDE(4)
     A            T#RAVA         6S 0O   109
     A                                      EDTCDE(4)
     A            T#RTOT         6S 0O   116
     A                                      EDTCDE(4)
     A            T#TOT          6S 0O   123
     A                                      EDTCDE(4)
     A*
     A* Print L2
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            DSTOT         18A  O     1
     A            T#PASG         6S 0O    81
     A                                      EDTCDE(4)
     A            T#PUNA         6S 0O    88
     A                                      EDTCDE(4)
     A            T#PTOT         6S 0O    95
     A                                      EDTCDE(4)
     A            T#RUSD         6S 0O   102
     A                                      EDTCDE(4)
     A            T#RAVA         6S 0O   109
     A                                      EDTCDE(4)
     A            T#RTOT         6S 0O   116
     A                                      EDTCDE(4)
     A            T#TOT          6S 0O   123
     A                                      EDTCDE(4)
     A*
     A* Print L3
     A*
     A          R TOTAL3
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            DEPTOT        12A  O     1
     A            T#PASG         6S 0O    81
     A                                      EDTCDE(4)
     A            T#PUNA         6S 0O    88
     A                                      EDTCDE(4)
     A            T#PTOT         6S 0O    95
     A                                      EDTCDE(4)
     A            T#RUSD         6S 0O   102
     A                                      EDTCDE(4)
     A            T#RAVA         6S 0O   109
     A                                      EDTCDE(4)
     A            T#RTOT         6S 0O   116
     A                                      EDTCDE(4)
     A            T#TOT          6S 0O   123
     A                                      EDTCDE(4)
     A*
     A* Print LR
     A*
     A          R GRDTOT
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'GRAND TOTALS'
     A            T#PASG         6S 0O    81
     A                                      EDTCDE(4)
     A            T#PUNA         6S 0O    88
     A                                      EDTCDE(4)
     A            T#PTOT         6S 0O    95
     A                                      EDTCDE(4)
     A            T#RUSD         6S 0O   102
     A                                      EDTCDE(4)
     A            T#RAVA         6S 0O   109
     A                                      EDTCDE(4)
     A            T#RTOT         6S 0O   116
     A                                      EDTCDE(4)
     A            T#TOT          6S 0O   123
     A                                      EDTCDE(4)
     A*
     A* Print a line of hyphens
     A*
     A          R HYPLIN
     A            HYPHEN       132A  O     1
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL463: End of Report ')
     A*
