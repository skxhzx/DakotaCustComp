     A*%%TS  SD  19990726  055146  DAVE        REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LDI130FM   Import slots from Layout Designer - Options
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990726  055146  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'LDI130.01'
     A                                  1 24'Add/Update Slots from LAYSLOT File'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01
     AO 21                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  4'Delete FS-WMS slot not'
     A                                  9  6'found in LAYSLOT file  . .'
     A            W1DEL          1A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02
     AO 22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B 11 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 58'Y=Yes, N=No'
     A                                 14  9'This program will update the SLOT -
     A                                      file from the Layout Designer'
     A                                 15  9'slot import file, LAYSLOT. If a sl-
     A                                      ot does not exist it will be'
     A                                 16  9'added, otherwise it will be update-
     A                                      d with the Item Number, Status'
     A                                 17  9'code, Slot Definition code and Des-
     A                                      ignation code. Also, there is'
     A                                 18  9'an option above that allows the us-
     A                                      er to determine if slots'
     A                                 19  9'should be deleted if they do not e-
     A                                      xist in the LAYSLOT file.'
     A                                 21 16'** This Program Does Not Create An-
     A                                      y Table Codes **'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
