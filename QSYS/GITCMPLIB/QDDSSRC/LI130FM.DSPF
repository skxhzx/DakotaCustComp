500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LI130FM   Pallet License Plate Date Info - View RF (Full Screen)
     A*  16 October, 2006
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*  10/16/06  RH  5.10
     A*   - Created
500 AA*   06/06/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20030123  133629  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LI130.01'
     A                                  1 31'License Date Information'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'General Information:'
     A                                      DSPATR(HI)
     A                                  4  5'License . . . . . . .'
     A            W1LCNS        15A  O  4 28
     A                                  5  5'Transaction . . . . .'
     A            W1TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  6  5'Item  . . . . . . . .'
     A            W1ITEM        49A  O  6 28
     A                                 10  3'Date Information:'
     A                                      DSPATR(HI)
     A                                 11  5'Keyed date  . . . . .'
     A            W1INDT         6Y 0O 11 28EDTCDE(Y)
     A                                 12  5'Expiration date . . .'
     A            W1EXPD         6Y 0O 12 28EDTCDE(Y)
     A                                 13  5'Date type . . . . . .'
     A            W1DTYP         8A  O 13 28
     A                                 14  5'Date sensitive  . . .'
     A            W1DFLG         1A  O 14 28
     A                                 15  5'Manufacture date. . .'
     A            W1MFLG         1A  O 15 28
     A                                 16  5'Mfg shelf life days .'
     A            W1MDAY         3Y 0O 16 28EDTCDE(3)
     A                                 17  5'User days to sell . .'
     A            W1UDAY         3Y 0O 17 28EDTCDE(3)
     A                                 18  5'Dist days to sell . .'
     A            W1DDAY         3Y 0O 18 28EDTCDE(3)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'LI130.01'
     A                                  1 31'Informacion de Lic'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  2  2DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DSPATR(PC)
     A                                  3  3'Informacion General:'
     A                                      DSPATR(HI)
     A                                  4  5'Licencia. . . . . . .'
     A            W1LCNS        15A  O  4 28
     A                                  5  5'Transaccion . . . . .'
     A            W1TRN#         7Y 0O  5 28EDTCDE(3)
     A                                  6  5'Producto. . . . . . .'
     A            W1ITEM        49A  O  6 28
     A                                 10  3'Informacion de Fcha:'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
