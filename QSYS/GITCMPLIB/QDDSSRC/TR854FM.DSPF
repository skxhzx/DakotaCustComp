     A*%%TS  SD  20120531  113418  RETHA       REL-V6R1M0  5761-WDS
     A********************************************************************
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------
     A*
     A*  TR854FM   TRAX - Stop Level Options - Change
     A*  31 May 2012
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    05/31/12  RBD  6.40
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20120531  113418  RETHA       REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TR854.01'
     A                                  1 29'Trax Stop Level Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Whse'
     A            WHCODE         3Y 0O  3  8EDTCDE(3)
     A                                  3 12'-'
     A            WHDESC        30A  O  3 14
     A                                  4  2'Route'
     A            W1RTE          5   O  4  8
     A                                  4 15'Route Id'
     A            W1RTID         5   O  4 24
     A                                  5  2'Stop'
     A            W1STOP         3  0O  5  8
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  2'Credit card enabled .............'
     A            W1TCCE         1X  B  9 36
     A N38N90                               DSPATR(UL)
     A  38                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 38'Y/N'
     A                                 10  2'Signature enabled ...............'
     A            W1TSIG         1X  B 10 36
     A N39N90                               DSPATR(UL)
     A  39                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 38'Y/N'
     A                                 11  2'Print invoice ...................'
     A            W1TPIV         1X  B 11 36
     A N42N90                               DSPATR(UL)
     A  42                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 38'Y/N'
     A                                 12  2'Pallets enabled .................'
     A            W1PALE         1X  B 12 36
     A N53N90                               DSPATR(UL)
     A  53                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 38'Y/N'
     A                                 13  2'Bulk items enabled ..............'
     A            W1TBUL         1X  B 13 36
     A N56N90                               DSPATR(UL)
     A  56                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 38'Y/N'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
