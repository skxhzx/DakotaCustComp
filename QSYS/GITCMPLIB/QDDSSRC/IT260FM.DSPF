     A*%%TS  SD  20000126  144804  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT260FM   Inventory transfer with item number change 8x40
     A*  26 January 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000126  144804  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'IT260.01'
     A                                  1 12'Inv. Tfr to New Item Number'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Dept'
     A            W1WHDP         5A  B  4  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N03                                  DSPATR(PC)
     A                                  5  2'Slot'
     A            W1DISP        12A  B  5  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000126  093953  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT260.02'
     A                                  1 12'Inv. Tfr to New Item Number'
     A                                      DSPATR(HI)
     A                                  2  2'Slot:'
     A            W1DISP        12A  O  2  8
     A                                  3  2'Item:'
     A            W2ITEM        55A  O  3  8
     A            W2LBLS        12A  O  4  2DSPATR(HI)
     A            W2ITMS        55A  O  4 15DSPATR(HI)
     A                                  5  2'Aval:'
     A            W2CURQ        30A  O  5  8
     A                                  6  2'Qty:'
     A            W2QTY1         5Y 0B  6  8EDTCDE(4)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            W2UM1          5A  O  6 14
     A            W2QTY2         3Y 0B  6 22EDTCDE(4)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
     A            W2UM2          5A  O  6 26
     A  92                                  DSPATR(ND)
     A            W2QTY3         3Y 0B  6 32EDTCDE(4)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
     A            W2UM3          5A  O  6 36
     A  93                                  DSPATR(ND)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20000126  093953  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT260.03'
     A                                  1 12'Inv. Tfr to New Item Number'
     A                                      DSPATR(HI)
     A                                  2  2'Slot:'
     A            W1DISP        12A  O  2  8
     A                                  3  2'Item:'
     A            W2ITEM        55A  O  3  8
     A                                  4  2'Qty:'
     A            W2TFRQ        30A  O  4  8
     A                                  5  2'New Item:'
     A            W3ITEM        15A  B  5 12DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                  6  2'New Dept:'
     A            W3WHDP         5A  B  6 12DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  22                                  DSPATR(RI)
     A                                  6 19'Slot:'
     A            W3DISP        12A  B  6 25CHECK(ER)
     A                                      DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                  7  2'F12=Cancel F20=Accept warning'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
