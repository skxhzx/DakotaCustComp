     A****************************************************************
     A*
     A* UI320PR
     A* 16 January 2015
     A* Lynn McMahon
     A*
     A* Revisions:
     A*
650 AA*    01/16/15  LMC  6.50
     A*      - Created
700aAA*    02/17/16  LMC  7.00a
     A*      - Added total count to TOTAL1
     A****************************************************************
     A*    Client Custom Revision -- Inter County Baker
ICBa A*    03/09/17   LMC  ICBa
     A*      - Enh: add DETAIL2 - ICB needs lot# from licinfo
     A*             add COLUMNS2
     A*
     A****************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    31
     A                                      'Received Weights'
     A                                    85
     A                                      'Page'
     A            PAGE           4S 0O    89
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            SCHEAD        58A  O    16
     A                                    88
     A                                      'UI320'
     A                                      SPACEA(001)
     A          R DESC1
     A            SCDESC        58A  O    16
     A*
     A                                      SPACEA(002)
     A          R HEAD2
     A                                      SPACEA(001)
     A  91                                70
     A                                      '(Continued)'
     A*
     A* Print column headings for Date order.
     A*
     A          R COLUMNS1
     A                                      SPACEA(001)
     A
     A                                     1
     A                                      'License       '
     A                                      UNDERLINE
     A                                    17
     A                                      'Weight     '
     A                                      UNDERLINE
     A                                    30
     A                                      'Scanned/Entry                     -
     A                                                                   '
     A                                                                         -
     A                                      UNDERLINE
ICBaAA          R COLUMNS2
ICBaAA                                      SPACEA(001)
ICBaAA
ICBaAA                                     1
ICBaAA                                      'License       '
ICBaAA                                      UNDERLINE
ICBaAA                                    17
ICBaAA                                      'Weight     '
ICBaAA                                      UNDERLINE
ICBaAA                                    30
ICBaAA                                      'Lot#                              -
ICBaAA                                                                   '
ICBaAA                                                                         -
ICBaAA                                      UNDERLINE
     A*
     A* Print detail line for receiving weights
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            #UILCNS       15A  O     1
     A            #WEIGHT       12A  O    17
     A            #UIUCIA      100A  O    30
     A
ICBaAA          R DETAIL2
ICBaAA                                      SPACEA(001)
ICBaAA            #UILCNS       15A  O     1
ICBaAA            #WEIGHT       12A  O    17
ICBaAA            #LNLOT        30A  O    30
     A*
     A**************************************************************************
     A*
     A**************************************************************************
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     2
     A                                      'Total Weight:'
     A            TOTWGT        10  2O    16
     A                                      EDTCDE(3)
700aAA                                    32
700aAA                                      'Total Count:'
700aAA            TOTCNT         5  0O    45
700aAA                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A*                                    1
     A*                                     ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'UI320: End of Report '
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(PIRWORK/UI320PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(*N       132      *N     )
     A*%%***********************************************************************
     A*
