     A*%%TS  SD  20040115  172339  DAVE        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI210FM    Import customer orders - Options
     A*  03 August 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040115  172339  DAVE        REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'PCK130.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B  8 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 58'Y=Yes, N=No'
730aAA                                  9  4'Warehouse  . . . . . . . . .'
730aAA            W1WHSE         3Y 0B  9 34EDTCDE(4)
730aAA N25N90                               DSPATR(HI)
730aAA  25                                  DSPATR(RI)
730aAA N25N90                               DSPATR(UL)
730aAA  05                                  DSPATR(PC)
730aAA  90                                  DSPATR(PR)
     A                                  1 21'Clear Files for Warehouse 900 Pick-
     A                                       Test'
     A                                      DSPATR(HI)
     A                                 12 14'This program will clear warehouse -
     A                                      900 records from files:'
     A                                 14 14'PIRTRAN, LABEL, PIRORD#, ORD*, IMP-
     A                                      ORD*, EXPORD*, PIRRTE#,'
     A                                 15 14'RTE*, PLT*, WORKLBL, WORKZON, CROS-
     A                                      SDK, POHDR, PODTL,'
     A                                 16 14'IMPPO*, EXPPO*, POHHDR, POHDTL, LI-
     A                                      CENSE and VSLOT.'
     A                                 18 14'See program PCK135 for other files'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
