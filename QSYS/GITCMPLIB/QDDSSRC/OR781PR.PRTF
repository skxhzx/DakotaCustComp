     A****************************************************************
     A*
     A* OR781PR   Substituted Items Report
     A* 08 December, 2010
     A* Roxanne Harrison
     A*
     A*--------------------------------------------------------------*
     A* 12/08/10  RH   5.30
     A*  - Created using HC321
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    56
     A                                      DFT('Substituted Items Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('OR781')
     A                                      SPACEB(001)
     A            W1FRMDT        6S 0O    56
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            W1TODT         6S 0O    +2
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A*
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                               119
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                    71
     A                                      DFT('Picked Quantity')
     A                                    99
     A                                      DFT('Status')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Route')
     A                                      UNDERLINE
     A                                     9
     A                                      DFT('Rt ID')
     A                                      UNDERLINE
     A                                    17
     A                                      DFT('Cust Ord#')
     A                                      UNDERLINE
     A                                    28
     A                                      DFT('Original Item  ')
     A                                      UNDERLINE
     A                                    48
     A                                      DFT('Substitute Item')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('Normal')
     A                                      UNDERLINE
     A                                    75
     A                                      DFT('Brk 1 ')
     A                                      UNDERLINE
     A                                    82
     A                                      DFT('Brk 2 ')
     A                                      UNDERLINE
     A                                    94
     A                                      DFT('Date    ')
     A                                      UNDERLINE
     A                                   104
     A                                      DFT('Time    ')
     A                                      UNDERLINE
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RHRTE     R        O     1REFFLD(RHRTE   OR781)
     A            RHRTID    R        O     9REFFLD(RHRTID  OR781)
     A            OHCORD    R        O    17REFFLD(OHCORD  OR781)
     A            ODOITM    R        O    28REFFLD(ODOITM  OR781)
     A            ODITEM    R        O    48REFFLD(ODITEM  OR781)
     A            ODQPK1    R        O    68REFFLD(ODQPK1  OR781)
     A                                      EDTCDE(3)
     A            ODQPK2    R        O    75REFFLD(ODQPK2  OR781)
     A                                      EDTCDE(3)
     A            ODQPK3    R        O    82REFFLD(ODQPK3  OR781)
     A                                      EDTCDE(3)
     A**          RHSTSD    R        O    85REFFLD(RHSTSD  OR781)
     A            W1STSD         6S 0O    94
     A                                      EDTCDE(Y)
     A            RHSTST    R        O   104REFFLD(RHSTST  OR781)
     A                                      EDTWRD('  :  :  ')
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A*           ODOITM    R        O    25REFFLD(ODOITM  OR781)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('OR781: End of Report ')
     A*
