     A*%%TS  SD  20040429  152237  BFC2        REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  EQ610FM    Equipment - Yard Status - Display
     A*  10 February 2004
     A*  Max Blacknik
     A*
     A*  SK110FM - 1.00
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    02/10/04  MLB  4.17
     A*      - Created.
417bAA*    04/28/04  RH   4.17b
     A*      - Increase length of equipment code to 8.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'EQ610.01'
     A                                  1 30'Equipment Yard Status'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD - Code Sequence
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2 49'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 73EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  3 49'Position to code  . . .'
417b A            PTINP8         8A  B  3 73
417b A*           PTINP2         6A  B  3 74
     A  02                                  DSPATR(PC)
     A                                  4  2' '
     A*
     A* POSITION TO RECORD - Type/Description Sequence
     A*
     A          R POSTNRC2
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2 39'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 65EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  3 39'Position to Type  . . . .'
     A            PTINP2         6A  B  3 65
     A  04                                  DSPATR(PC)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI)
     A                                  4 51'Description .'
     A            PTINP3        15A  B  4 65
     A  05                                  DSPATR(PC)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI)
     A*
     A* POSITION TO RECORD - Type/Status Sequence
     A*
     A          R POSTNRC3
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  1  2EDTCDE(3)
     A                                  1  6'-'
     A            WHDESC        30A  O  1  8
     A                                  2 49'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 74EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                  3 49'Position to Type  . . .'
     A            PTINP2         6A  B  3 74
     A  04                                  DSPATR(PC)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI)
     A                                  4 61'Status  . .'
     A            PTINP4         1A  B  4 74
     A  05                                  DSPATR(PC)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI)
     A*
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  21                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            EQCODE    R        O 11  6REFFLD(EQCODE EQUIP)
417b A* The following fields move 2 space because EQCODE increased.
     A            EQDESC    R        O 11 18REFFLD(EQDESC EQUIP)
     A            EQTYPE    R        O 11 50REFFLD(EQTYPE EQUIP)
     A            W1STAT        15A  O 11 58
     A            ESDOOR    R        O 11 75REFFLD(ESDOOR EQUIPYS)
     A                                      EDTCDE(4)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20040429  152237  BFC2        REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            FLD001         1A  O  9  2
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Code    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417b A* The following fields move 2 space because EQCODE increased.
     A                                 10 18'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 50'Type  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 58'Status         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 75'Door'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79A  O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
