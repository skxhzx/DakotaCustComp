     A****************************************************************
     A*
     A* TS271PR   Employee management report for an employee(Detail)
     A* 19 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'TS271'
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(WKEMP# TS271)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1REFFLD(WKNAME TS271)
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    50'Direct'
     A                                    61'Cases'
     A                                    76'Cube'
     A                                    90'Weight'
     A                                    99'Cases'
     A                                      SPACEA(001)
     A*
     A                                     1'  Date  '
     A                                      UNDERLINE
     A                                    11'Task  '
     A                                      UNDERLINE
     A                                    18'Description                  '
     A                                      UNDERLINE
     A                                    50' Hours'
     A                                      UNDERLINE
     A                                    57' Selected'
     A                                      UNDERLINE
     A                                    71' Selected'
     A                                      UNDERLINE
     A                                    87' Selected'
     A                                      UNDERLINE
     A                                    98'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A                                      SPACEA(002)
     A            GTDATE         6S 0      1EDTCDE(Y)
     A            DIRTOT         5S 2     50EDTCDE(3)
     A            WKPTOT    R             57REFFLD(WKPTOT TS271)
     A                                      EDTCDE(1)
     A            WKCTOT    R             67REFFLD(WKCTOT TS271)
     A                                      EDTCDE(1)
     A            WKWTOT    R             82REFFLD(WKWTOT TS271)
     A                                      EDTCDE(1)
     A            CASEPH         5S 0     99EDTCDE(3)
     A            LINES          5S 0    105EDTCDE(3)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKCODE    R             11REFFLD(WKCODE TS271)
     A            TKDESC    R             18REFFLD(TKDESC TASK)
     A            HOURS          5S 2     50EDTCDE(3)
     A            WKPCS     R             60REFFLD(WKPCS  TS271)
     A                                      EDTCDE(1)
     A            WKCUBE    R             70REFFLD(WKCUBE TS271)
     A                                      EDTCDE(1)
     A            WKSWGT    R             84REFFLD(WKSWGT TS271)
     A                                      EDTCDE(1)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          10A        1
     A            T#DTOT         7S 2     48EDTCDE(3)
     A            T#PTOT         7S 0     57EDTCDE(1)
     A            T#CTOT        11S 3     67EDTCDE(1)
     A            T#WTOT        11S 2     82EDTCDE(1)
     A            T#CSPH         7S 0     97EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'TS271: End of Report '
     A*
