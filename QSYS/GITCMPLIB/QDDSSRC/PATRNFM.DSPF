     A*%%TS  SD  19960425  110843  HEMANT      REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PATRNFM   Pattern display
     A*  25 April, 1996
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960425  110843  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PATRN.01'
     A                                  1 33'Pattern Display'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  4'Warehouse . . . . . . . .'
     A            WHCODE         3Y 0O  5 31EDTCDE(3)
     A                                  5 35'-'
     A            WHDESC        30A  O  5 37
     A                                  6  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  6 31
     A            W1DESC        30A  O  6 48
     A                                  7  4'Package size  . . . . . .'
     A            W1PLEN         5Y 2O  7 31EDTCDE(3)
     A                                  7 38'X'
     A            W1PWDT         5Y 2O  7 40EDTCDE(3)
     A                                  7 47'X'
     A            W1PHGT         5Y 2O  7 49EDTCDE(3)
     A                                  8  4'Pallet  . . . . . . . . .'
     A            W1PAL          6A  O  8 31
     A                                  9  4'Overhang  . . . . . . . .'
     A            W1HANG         3Y 2O  9 31EDTCDE(3)
     A                                  9 41'Tie X High  . . . . . . .'
     A            W1TIE          3Y 0O  9 67EDTCDE(3)
     A                                  9 71'X'
     A            W1HIGH         3Y 0O  9 73EDTCDE(3)
     A                                 13 34'xxxxx'
     A                                 14 34'x   x'
     A                                 15 34'x A x'
     A                                 16 34'x   x'
     A                                 17 34'x   x'
     A                                 18 34'xxxxx'
     A                                 14 45'xxxxxxxxxxx'
     A                                 15 45'x    B    x'
     A                                 16 45'xxxxxxxxxxx'
     A            R1            10A  O 12 12
     A            R2            10A  O 13 12
     A            R3            10A  O 14 12
     A            R4            10A  O 15 12
     A            R5            10A  O 16 12
     A            R6            10A  O 17 12
     A            R7            10A  O 18 12
     A            R8            10A  O 19 12
     A            R9            10A  O 20 12
     A            R10           10A  O 21 12
     A                                 14 68'XXXXX'
     A                                 15 68'X C X'
     A                                 16 68'XXXXX'
     A            ALONG          5Y 2O 16 27EDTCDE(4)
     A            AWIDE          5Y 2O 19 33EDTCDE(4)
     A            CLONG          5Y 2O 15 74EDTCDE(4)
     A            CWIDE          5Y 2O 17 68EDTCDE(4)
     A            BLONG          5Y 2O 17 48EDTCDE(4)
     A            BWIDE          5Y 2O 15 57EDTCDE(4)
     A                                 24  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 11 12'1234567890'
     A                                 12 10'1'
     A                                 13 10'2'
     A                                 14 10'3'
     A                                 15 10'4'
     A                                 16 10'5'
     A                                 17 10'6'
     A                                 18 10'7'
     A                                 19 10'8'
     A                                 20 10'9'
     A                                 21  9'10'
     A            PLDESC    R        O  8 48REFFLD(PLREC/PLDESC *LIBL/PALLET)
     A            PLLEN     R        O  8 38REFFLD(PLREC/PLLEN *LIBL/PALLET)
     A                                      EDTCDE(3)
     A                                  8 42'X'
     A            PLWDTH    R        O  8 44REFFLD(PLREC/PLWDTH *LIBL/PALLET)
     A                                      EDTCDE(3)
     A            W1PLWD         3Y 0O 22 15EDTCDE(3)
     A                                 22 12'|-'
     A                                 22 19'--|'
     A                                 12  5'---'
     A                                 13  5' | '
     A                                 14  5' | '
     A                                 15  5' | '
     A            W1PLLN         3Y 0O 17  5EDTCDE(3)
     A                                 19  5' | '
     A                                 20  5' | '
     A                                 21  5'---'
     A            DUMMY1         1A  B  4  4DSPATR(ND)
     A                                      DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  19960425  110843  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PATRN.02'
     A                                  1 33'Pattern Display'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            WHCODE         3S 0O  6 31
     A                                  6 35'-'
     A            WHDESC        30A  O  6 37
     A                                  7  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  7 31
     A                                  8  4'Description . . . . . . .'
     A            W1DESC        30A  O  8 31
     A                                 22  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R SCREEN3
     A*%%TS  SD  19960425  110843  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PATRN.03'
     A                                  1 33'Pattern Display'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse . . . . . . . .'
     A            WHCODE         3S 0O  6 31
     A                                  6 35'-'
     A            WHDESC        30A  O  6 37
     A                                  7  4'Item  . . . . . . . . . .'
     A            W1ITEM        15A  O  7 31
     A                                  8  4'Description . . . . . . .'
     A            W1DESC        30A  O  8 31
     A                                 22  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
