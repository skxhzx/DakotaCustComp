     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*  SL420FM - Available pick slots by aisle
650 AA*          - Added option to print overflow slots.
     A*  Hemant Kapadia
     A*  21 January, 2002
     A*
     A*  Created 4.16
     A*
     A*  Revisions
     A*
     A*  04/08/05  RLP  5.00
     A*     - added "view" option
     A*
650 AA*  05/02/13  RBD  6.50
     A*     - Added option to print overflow slots.
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL420.01'
650 DA**          RPTTTL        40A  O  1 21DSPATR(HI)
650 MA            RPTTTL        45A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  6 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 49'0-999'
     A                                  7  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  7 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 49'Name, *ALL'
     A                                  9  4'Aisle  . . . . . . . . . . .'
     A            W1AISL         4A  B  9 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 49'Name, *ALL'
     A            W1STYP         1A  B  8 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
650 AA                                 10  4'Pick or Overflow . . . . . .'
650 AA            W1PICK         1A  B 10 34
650 AA N25N90                               DSPATR(HI)
650 AA  25                                  DSPATR(RI)
650 AA N25N90                               DSPATR(UL)
650 AA  05                                  DSPATR(PC)
650 AA  90                                  DSPATR(PR)
650 AA                                 10 49'P=Pick or O=Overflow'
     A                                 11  4'View or print  . . . . . . .'
500 DA*                                     DSPATR(ND)
     A            W1VP           1A  B 11 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 DA*                                     DSPATR(ND)
     A                                 11 49'V=View, P=Print'
500 DA*                                     DSPATR(ND)
     A                                 12  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 12 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 49'Y=Yes, N=No'
     A                                 13  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 13 34
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 14 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Section  . . . . . . . . . .'
     A                                  8 49'F=Fixed,P=PIR'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
