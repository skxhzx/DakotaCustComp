     A*%%TS  SD  20011105  122718  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  DK112FM   Simple table maintenance - Change screen
     A*  05 November 2001
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    11/05/01  DAS  4.16
     A*      - Created.
     A*
417 AA*    08/12/03  MLB  4.17
     A*      - Fix: Corrected title on SCREEN1 display.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20011105  122718  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'DK112.01'
417 DA*                                 1 22'Warehouse Department Table Mainten-
417 DA*                                     ance'
417 MA                                  1 22'      Dock Door Table Maintenance -
417 MA                                          '
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse:'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Department . . . . . .'
     A            W1WHDP         5A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                 11  4'Door number  . . . . .'
     A            W1DOOR         3Y 0B 11 28EDTCDE(3)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N22N90N91                            DSPATR(HI)
     A N22N90N91                            DSPATR(UL)
     A                                      RANGE(0 999)
     A                                 12  4'Maximum cube . . . . .'
     A            W1MAXC         5Y 0B 12 28EDTCDE(3)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                      RANGE(0 99999)
     A                                 13  4'Status . . . . . . . .'
     A            W1STAT         1A  B 13 28
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A  90
     A N24N90                               DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A                                 13 44'A=Active, I=Inactive'
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
