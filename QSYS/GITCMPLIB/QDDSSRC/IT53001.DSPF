     A*%%TS  SD  20120514  114153  BFC         REL-V7R1M0  5770-WDS
     A********************************************************************
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*  Revisions:
     A*
520 AA*  05/26/09  MLB  5.20
     A*    - Created.
640 AA*  05/11/12  RTR  6.40
     A*    - Added sort option
640bAA*  05/14/12  RTR  6.40b
     A*    - Added selection option
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ALTHELP
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      HELP
     A          R SCREEN1
     A*%%TS  SD  20120511  004101  BFC         REL-V7R1M0  5770-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT530.01'
     A                                  1 19'Recommended Transfer from Outside -
     A                                      Storage'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . . . . .-
     A                                       .'
     A            W1WHSE         3Y 0B  8 42EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department . . . . . . . . . . . .-
     A                                       .'
     A            W1WHDP         5A  B  9 42
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 57'Dept, *ALL'
     A                                 10  4'Days supply to have on-hand  . . .-
     A                                       .'
     A            W1DYSP         3Y 0B 10 42EDTCDE(4)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 57'1-999'
     A                                 11  4'Outside storage slot name  . . . .-
     A                                       .'
     A            W1OSSL        12A  B 11 42
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
640bAA                                 12  4'Use Order or Sales Demand  . . . .-
640bAA                                       .'
640bAA            W1SEL          4A  B 12 42
640bAA N29N90                               DSPATR(HI)
640bAA  29                                  DSPATR(RI)
640bAA N29N90                               DSPATR(UL)
640bAA  09                                  DSPATR(PC)
640bAA  90                                  DSPATR(PR)
640bAA                                 12 57'*ORD, *SLS'
640 AA                                 13  4'Sort by Department and . . . . . .-
640 AA                                       .'
640 AA            W1SORT         5A  B 13 42
640 AA N28N90                               DSPATR(HI)
640 AA  28                                  DSPATR(RI)
640 AA N28N90                               DSPATR(UL)
640 AA  08                                  DSPATR(PC)
640 AA  90                                  DSPATR(PR)
640 AA                                 13 57'*ITEM, *SLOT'
     A                                 15  4'Printer device . . . . . . . . . .-
     A                                       .'
     A            W1DEV         10A  B 15 42
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 57'*JOB, printer device'
     A                                 16  4'Form type  . . . . . . . . . . . .-
     A                                       .'
     A            W1FORM        10A  B 16 42
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 57'*STD, form type'
     A                                 17  4'Place on job queue ? . . . . . . .-
     A                                       .'
     A            W1JOBQ         1A  B 17 42
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 57'Y=Yes, N=No'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
