     *----------------------------------------------------------------
     *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     *   BFC Software, Inc.
     *   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     *   West Chicago, IL  60185
     *   (630) 562-0375
     *----------------------------------------------------------------
     * UI313PR    Warehouse UCI exception listing
     * February  24, 2007
     * Jeffrey Jahnke
     *
     *----------------------------------------------------------------
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    33
     A                                      DFT('Warehouse UCI Exception --
                                            Report')
     A                                   118DFT('Page')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                   123DFT('UI311')
     A*
     A          R HEAD2
     A                                      SPACEB(002)
     A*
     A                                     8
     A                                      DFT('Report Criteria:')
     A                                    27
     A                                      DFT('Warehouse:')
     A            $WHSEA         3A  O    39
     A                                    45DFT('Department:')
     A            $DEPT          5A  O    58
     A                                    66DFT('Item:')
     A            $ITEM         15A  O    73
     A                                      SPACEA(002)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     1DFT('Whse')
     A                                   + 2DFT('Dept ')
     A                                   + 2DFT('Item           ')
     A                                   + 2DFT('UCI#      ')
     A                                   + 2DFT('Slot#       ')
     A                                   + 2DFT('Aisle')
     A                                   + 2DFT('Loc')
     A                                   + 2DFT('Rack')
     A                                   + 2DFT('Handstack')
     A                                   + 2DFT('Sel')
     A                                   + 2DFT('Date      ')
     A                                   + 2DFT('Time    ')
     A                                   + 2DFT('Ent By    ')
     A                                   + 2DFT('Excp')
     A                                      SPACEA(001)
      *
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            UIEWHS    R              1REFFLD(UIEWHS UIIEXCP)
     A            UIEWHD    R            + 3REFFLD(UIEWHD UIIEXCP)
     A            UIEITM    R            + 2REFFLD(UIEITM UIIEXCP)
     A            UIEUCI    R            + 2REFFLD(UIEUCI UIIEXCP)
     A            UIEDSP    R            + 2REFFLD(UIEDSP UIIEXCP)
     A            UIEASL    R            + 2REFFLD(UIEASL UIIEXCP)
     A            UIELOC    R            + 4REFFLD(UIELOC UIIEXCP)
     A            UIELVL    R            + 3REFFLD(UIELVL UIIEXCP)
     A            UIEHND    R            + 6REFFLD(UIEHND UIIEXCP)
     A            UIESTP    R            + 7REFFLD(UIESTP UIIEXCP)
     A            UIEEDT    R            + 3REFFLD(UIEEDT UIIEXCP)
                                            EDTWRD('    /  /  ')
     A            UIEETM    R            + 2REFFLD(UIEETM UIIEXCP)
                                            EDTWRD('  :  :  ')
     A            UIEEBY    R            + 2REFFLD(UIEEBY UIIEXCP)
     A            UIEECD    R            + 3REFFLD(UIEECD UIIEXCP)
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R TOTALS
     A                                      SPACEB(002)
     A                                     5DFT('Total Items Printed:')
     A            COUNT          4S 0O    26
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A                                     5DFT('UI313: End of Report')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R NOTOTAL
     A                                      SPACEB(002)
     A                                     5DFT('No items to report')
     A                                      SPACEA(002)
     A                                     5DFT('UI313: End of Report')
