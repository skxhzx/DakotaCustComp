     A*%%TS  SD  20090511  074817  BFC         REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*---------------------------------------------------------------
     A*
     A*  IT330FM   Warehouse book by slot
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
     A*   08/20/99  HNK  4.12
     A*     - SIDE OPTION ADDED
     A*
414 AA*    07/20/00  HNK  4.14
     A*      - Slot type option added.(P=Pick, O=Overflow, B=Both)
     A*      - Indicators changed.
     A*
520aAA*    03/31/09 CWM 5.20A
     A*      - Add support for one time report distribution flag.
     A******************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20090511  074817  BFC         REL-V5R3M0  5722-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT330.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10  4'Section  . . . . . . . . . .'
     A            W1SECN         1A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'(P=PIR, F=Fixed, B=Both)'
     A                                 11  4'Type of slot . . . . . . . .'
     A            W1PICK         1A  B 11 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 49'(P=Pick, O=Overflow, B=Both)'
     A                                 12  4'Slot side  . . . . . . . . .'
     A            W1SIDE         1A  B 12 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 49'(O=Odd, E=Even, B=Both)'
     A                                 14  4'View or print  . . . . . . .'
     A            W1VP           1A  B 14 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 49'(V=View, P=Print)'
     A                                 15  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 15 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 49'(Y=Yes, N=No)'
520aDA*                                16  4'Printer device . . . . . . .'
520aDA*           W1DEV         10A  B 16 34
520aDA*N28N90                               DSPATR(HI)
520aDA* 28                                  DSPATR(RI)
520aDA*N28N90                               DSPATR(UL)
520aDA* 08                                  DSPATR(PC)
520aDA* 90                                  DSPATR(PR)
520aDA*                                17  4'Form type  . . . . . . . . .'
520aDA*           W1FORM        10A  B 17 34
520aDA*N29N90                               DSPATR(HI)
520aDA* 29                                  DSPATR(RI)
520aDA*N29N90                               DSPATR(UL)
520aDA* 09                                  DSPATR(PC)
520aDA* 90                                  DSPATR(PR)
520aAA*
520 aA                                 17  4'One Time Print Definition. .'
520 aA            W1POVR         1A  B 17 34
520 aA N29N90                               DSPATR(HI)
520 aA  29                                  DSPATR(RI)
520 aA  09                                  DSPATR(PC)
520 aA  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
