     A*%%TS  SD  19990907  191649  DAVE        REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PS142FM   Truck pallet detail  - Move  screen
     A*  24 June 1997
     A*  Roxanne Harrison
     A*
     A*  Revisions
404 AA*    08/19/97  RH   4.04
     A*      - F20 accept warning.
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19990907  191649  DAVE        REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A                                      CF20
     A  98                                  ALARM
     A                                  1  2'PS152.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Stop Detail'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2  0O  7 19EDTCDE(4)
     A                                 15  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A            W1STOP         3Y 0O  8 19EDTCDE(4)
     A                                 10  2'Item           :'
     A                                 11  2'Total pieces   :'
     A            W1PCS          5Y 0O 11 19EDTCDE(4)
     A                                 12  2'Total cube     :'
     A            W1CUBE         9Y 3O 12 19EDTCDE(4)
     A                                 13  2'Total weight   :'
     A            W1WGHT         9Y 2O 13 19EDTCDE(4)
     A                                 17  3'Move to Row . . .'
     A            W1ROW          2Y 0B 17 21EDTCDE(4)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 28'Column  . . .'
     A            W1COL          1Y 0B 17 42EDTCDE(4)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F5=Refresh  F12=Cancel  F20=Accept-
     A                                       warning'
     A                                      COLOR(BLU)
     A                                  7 23'Row:'
     A                                  7 32'Column:'
     A            WOROW          2  0O  7 28
     A            WOCOL          2  0O  7 40
     A                                  8  2'Stop           :'
     A            W1ITEM        15   O 10 19
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PS142.01'
     A                                  1 30'Work with Built Routes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Pallet Detail'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse      :'
     A            $PWHSE         3Y 0O  4 13EDTCDE(3)
     A                                  4 17'-'
     A            WHDESC        30A  O  4 19
     A                                  5  2'Route          :'
     A            WORTE          5A  O  5 19
     A                                  6  2'Compartment    :'
     A            WOCOM         15A  O  6 19
     A                                  7  2'Unload Sequence:'
     A            WOSEQ          2Y 0O  7 19EDTCDE(4)
     A                                  9  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
