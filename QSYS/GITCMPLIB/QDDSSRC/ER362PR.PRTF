     A****************************************************************
     A*
     A* ER362PR   Management report for a selector (Summary)
     A* 22 March, 2000
     A* Hemant Kapadia
     A*
     A* 03.22.00  HNK  4.13
     A*  Created
     A*
     A* Note: Replaces ER272PR
     A*
     A* Revisions:
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER362'
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(PTEMP# ER361)
     A                                      EDTCDE(Z)
     A            WKNAME        40A       +1
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*                                    1'Start'
     A                                    41'Cases'
     A                                    55'Direct'
     A                                    64'Indirect'
     A                                    76'Cases'
     A                                      SPACEA(001)
     A*
     A                                     1' Dept '
     A                                      UNDERLINE
     A                                    11'Task  '
     A                                      UNDERLINE
     A                                    19'Description'
     A                                      UNDERLINE
     A                                    41'Selected'
     A                                      UNDERLINE
     A                                    56'Hours'
     A                                      UNDERLINE
     A                                    67'Hours'
     A                                      UNDERLINE
     A                                    75'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            WKWHDP    R              2REFFLD(WKWHDP ER361A)
     A*                                     EDTCDE(Y)
     A            TKCODE    R             11REFFLD(PTTASK ER361)
     A            W1DESC        20A       19
     A            T#PTOT         7S 0     41EDTCDE(1)
     A            T#DTOT         7S 2     52EDTCDE(1)
     A            T#ITOT         7S 2     63EDTCDE(1)
     A            T#CSPH         7S 0     72EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            T#PTOT         7S 0     41EDTCDE(1)
     A            T#DTOT         7S 2     52EDTCDE(1)
     A            T#ITOT         7S 2     63EDTCDE(1)
     A            T#CSPH         7S 0     72EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER362: End of Report '
     A*
