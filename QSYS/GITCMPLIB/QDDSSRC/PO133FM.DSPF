     A*%%TS  SD  20000121  135409  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PO133FM   Item Receiving Log Detail - Display
     A*  07 October 1999
     A*  Max Blacknik
     A*
     A*  PO133FM - 4.13
     A*
     A*  Revisions
     A*    01/20/00  MLB  4.13a
     A*      - Changed length of W7SQNR from 3,0 to 5,0.
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*
     A*****************************************************************
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF07
     A                                      CF08
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF21
     A                                      CF24
     A  98                                  ALARM
     A                                  1  2'PO133.01'
     A                                      DSPATR(PC)
     A                                  1 35'PO Receiving'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  6  2'PO number . .'
     A            W1PO           9A  O  6 16
     A                                  7  2'Vendor  . . .'
     A            W1VEN#        11Y 0O  7 16EDTCDE(4)
     A            W1VNAM        40A  O  7 29
     A                                  8  2'Item number .'
     A            WOITEM        65A  O  8 16
     A                                  9  2'Department  .'
     A            W1WHDP         5A  O  9 16
     A            W1DPDS        30A  O  9 23
     A                                 10  2'Section . . .'
     A            W1STYP         1A  O 10 16
     A                                 10 22'Item Type:'
     A            W1TYPE         1A  O 10 33
     A                                 11  2'Designation . .'
     A            W1DESG         5A  O 11 18
     A            W1DSDS        32A  O 11 24
     A            W1DSCD         6A  O 11 57
     A                                 12  2'Activity zone .'
     A            W1ACTV         2A  O 12 18
     A            W1ACTD        30A  O 12 21
     A                                 13  2'Build code  . .'
     A            W1BLD          2A  O 13 18
     A            W1BLDD        30A  O 13 21
     A                                 14  2'Quantity received . . . . .'
     A            W1QRC1         5Y 0O 14 31EDTCDE(4)
     A            W1UM1          5A  O 14 37
     A            W1QRC2         5Y 0O 14 43EDTCDE(4)
     A  94                                  DSPATR(ND)
     A            W1UM2          5A  O 14 49
     A  94                                  DSPATR(ND)
     A            W1QRC3         5Y 0O 14 55EDTCDE(4)
     A  93                                  DSPATR(ND)
     A            W1UM3          5A  O 14 61
     A  94                                  DSPATR(ND)
     A                                 15  2'Expiration date . . . . . .'
     A            W1EXPD         6Y 0O 15 31EDTCDE(Y)
     A                                 16  2'Slot Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 17'Tie   /   High'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 37'Slot Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 16 52'Tie   /   High'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W1SDF1         2A  O 17  5
     A            W1TIE1         4Y 0O 17 18EDTCDE(4)
     A                                 17 24'/'
     A            W1HI1          2Y 0O 17 28EDTCDE(4)
     A            W1SDF6         2A  O 17 40
     A            W1TIE6         4Y 0O 17 53EDTCDE(4)
     A                                 17 58'/'
     A            W1HI6          2Y 0O 17 63EDTCDE(4)
     A            W1SDF2         2A  O 18  5
     A            W1TIE2         4Y 0O 18 18EDTCDE(4)
     A                                 18 24'/'
     A            W1HI2          2Y 0O 18 28EDTCDE(4)
     A            W1SDF7         2A  O 18 40
     A            W1TIE7         4Y 0O 18 53EDTCDE(4)
     A                                 18 58'/'
     A            W1HI7          2Y 0O 18 63EDTCDE(4)
     A            W1SDF3         2A  O 19  5
     A            W1TIE3         4Y 0O 19 18EDTCDE(4)
     A                                 19 24'/'
     A            W1HI3          2Y 0O 19 28EDTCDE(4)
     A            W1SDF8         2A  O 19 40
     A            W1TIE8         4Y 0O 19 53EDTCDE(4)
     A                                 19 58'/'
     A            W1HI8          2Y 0O 19 63EDTCDE(4)
     A            W1SDF4         2A  O 20  5
     A            W1TIE4         4Y 0O 20 18EDTCDE(4)
     A                                 20 24'/'
     A            W1HI4          2Y 0O 20 28EDTCDE(4)
     A            W1SDF9         2A  O 20 40
     A            W1TIE9         4Y 0O 20 53EDTCDE(4)
     A                                 20 58'/'
     A            W1HI9          2Y 0O 20 63EDTCDE(4)
     A            W1SDF5         2A  O 21  5
     A            W1TIE5         4Y 0O 21 18EDTCDE(4)
     A                                 21 24'/'
     A            W1HI5          2Y 0O 21 28EDTCDE(4)
     A            W1SDF0         2A  O 21 40
     A            W1TIE0         4Y 0O 21 53EDTCDE(4)
     A                                 21 58'/'
     A            W1HI0          2Y 0O 21 63EDTCDE(4)
     A*
     A*
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'PO133.02'
     A                                  1 34'System Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  6  2'* Error - System options record no-
     A                                      t found.'
     A N41                                  DSPATR(ND)
     A  41                                  DSPATR(RI)
     A  41                                  COLOR(RED)
     A                                  7  4'Using EPM module  . . . . . . . . -
     A                                      . . . . .'
     A            W2EPM          1A  O  7 49
     A                                  7 69'Y=Yes, N=No'
     A                                  8  4'Using RF module . . . . . . . . . -
     A                                      . . . . .'
     A            W2RF           1A  O  8 49
     A                                  8 69'Y=Yes, N=No'
     A                                  9  4'Capture catch weights at pick clos-
     A                                      ing . . .'
     A            W2CAPC         1A  O  9 49
     A                                  9 69'Y=Yes, N=No'
     A                                 10  4'Individule catch weight entry  . .-
     A                                        . . . .'
     A            W2ICW          1A  O 10 49
     A                                 10 69'Y=Yes, N=No'
     A                                 11  4'Perform catch weight variance chec-
     A                                      k . . . .'
     A            W2WRNC         1A  O 11 49
     A                                 11 69'Y=Yes, N=No'
     A                                 12  4'Catch weight Low/High variance per-
     A                                      centage .'
     A            W2RG1C         3Y 1O 12 49
     A                                 12 53'%'
     A            W2RG2C         3Y 1O 12 55
     A                                 12 59'%'
     A                                 13  4'Print letdown labels  . . . . . . -
     A                                      . . . . .'
     A            W2LETD         1A  O 13 49
     A                                 13 69'Y=Yes, N=No'
     A                                 14  4'Using Import item fields  . . . . -
     A                                      . . . . .'
     A            W2IMPI         1A  O 14 49
     A                                 14 69'Y=Yes, N=No'
     A                                 15  4'Allow multiple pick slots for fixe-
     A                                      d items .'
     A            W2MPCK         1A  O 15 49
     A                                 15 69'Y=Yes, N=No'
     A                                 16  4'Number of days to keep receiving l-
     A                                      ogs . . .'
     A            W2RCDY         1Y 0O 16 49
     A                                 17  4'Number of days to keep picking log-
     A                                      s . . . .'
     A            W2PKDY         1Y 0O 17 49
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R SCREEN3
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO133.03'
     A                                  1 32'Receiving Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  6  2'* Error - Receiving options record-
     A                                       not found.'
     A N42                                  DSPATR(ND)
     A  42                                  DSPATR(RI)
     A  42                                  COLOR(RED)
     A                                  7  3'General options'
     A                                      DSPATR(HI)
     A                                  8  5'Program to print receiving labels -
     A                                      . . .'
     A            W3PROG        10A  O  8 46
     A                                  9  5'Ideal slot usage percentage . . . -
     A                                      . . .'
     A            W3UPCT         3Y 0O  9 46
     A                                 10  5'Use generic license (Y,N) . . . . -
     A                                      . . .'
     A            W3PLCN         1A  O 10 46
     A                                 12  3'PIR search priorities (1,2,3,4)'
     A                                      DSPATR(HI)
     A                                 13  5'Minimum number of slots . . . . . -
     A                                      . . .'
     A            W3PDEF         1Y 0O 13 46
     A                                 14  5'Same build code . . . . . . . . . -
     A                                      . . .'
     A            W3PBLD         1Y 0O 14 46
     A                                 15  5'Same activity zone  . . . . . . . -
     A                                      . . .'
     A            W3PACT         1Y 0O 15 46
     A                                 16  5'Same aisle  . . . . . . . . . . . -
     A                                      . . .'
     A            W3PASL         1Y 0O 16 46
     A                                 16 51'Force same aisle (Y,N). . .'
     A            W3PFRC         1A  O 16 79
     A                                 17  3'Fixed search priorities (1,2,3,4)'
     A                                      DSPATR(HI)
     A                                 18  5'Same aisle as picking slot  . . . -
     A                                      . . .'
     A            W3FASL         1Y 0O 18 46
     A                                 19  5'Same side of aisle as picking slot-
     A                                        . .'
     A            W3FSID         1Y 0O 19 46
     A                                 20  5'Minimum number of slots . . . . . -
     A                                      . . .'
     A            W3FDEF         1Y 0O 20 46
     A                                 21  5'Highest level . . . . . . . . . . -
     A                                      . . .'
     A            W3FLVL         1Y 0O 21 46
     A                                 21 51'Auto assign pick slot (Y,N)'
     A            W3APCK         1A  O 21 79
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R SCREEN4
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO133.04'
     A                                  1 34'Activity Zones'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  8  2'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  9'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 17'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 25'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 33'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 41'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 49'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 57'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 65'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 73'Zone'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W4AZ01         2   O 10  3
     A            W4AZ02         2   O 10 10
     A            W4AZ03         2   O 10 18
     A            W4AZ04         2   O 10 26
     A            W4AZ05         2   O 10 34
     A            W4AZ06         2   O 10 42
     A            W4AZ07         2   O 10 50
     A            W4AZ08         2   O 10 58
     A            W4AZ09         2   O 10 66
     A            W4AZ10         2   O 10 74
     A  46                             11  3'No activity zones found during rec-
     A                                      eiving.'
     A                                      DSPATR(HI)
     A            W4AZ11         2   O 12  3
     A            W4AZ12         2   O 12 10
     A            W4AZ13         2   O 12 18
     A            W4AZ14         2   O 12 26
     A            W4AZ15         2   O 12 34
     A            W4AZ16         2   O 12 42
     A            W4AZ17         2   O 12 50
     A            W4AZ18         2   O 12 58
     A            W4AZ19         2   O 12 66
     A            W4AZ20         2   O 12 74
     A            W4AZ21         2   O 14  3
     A            W4AZ22         2   O 14 10
     A            W4AZ23         2   O 14 18
     A            W4AZ24         2   O 14 26
     A            W4AZ25         2   O 14 34
     A            W4AZ26         2   O 14 42
     A            W4AZ27         2   O 14 50
     A            W4AZ28         2   O 14 58
     A            W4AZ29         2   O 14 66
     A            W4AZ30         2   O 14 74
     A            W4AZ31         2   O 16  3
     A            W4AZ32         2   O 16 10
     A            W4AZ33         2   O 16 18
     A            W4AZ34         2   O 16 26
     A            W4AZ35         2   O 16 34
     A            W4AZ36         2   O 16 42
     A            W4AZ37         2   O 16 50
     A            W4AZ38         2   O 16 58
     A            W4AZ39         2   O 16 66
     A            W4AZ40         2   O 16 74
     A            W4AZ41         2   O 18  3
     A            W4AZ42         2   O 18 10
     A            W4AZ43         2   O 18 18
     A            W4AZ44         2   O 18 26
     A            W4AZ45         2   O 18 34
     A            W4AZ46         2   O 18 42
     A            W4AZ47         2   O 18 50
     A            W4AZ48         2   O 18 58
     A            W4AZ49         2   O 18 66
     A            W4AZ50         2   O 18 74
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*
     A          R SCREEN5
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO133.05'
     A                                  1 34'Build Codes'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  8  2'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8  9'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 17'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 25'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 33'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 41'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 49'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 57'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 65'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 73'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W5BC01         2A  O 10  3
     A            W5BC02         2A  O 10 10
     A            W5BC03         2A  O 10 18
     A            W5BC04         2A  O 10 26
     A            W5BC05         2A  O 10 34
     A            W5BC06         2A  O 10 42
     A            W5BC07         2A  O 10 50
     A            W5BC08         2A  O 10 58
     A            W5BC09         2A  O 10 66
     A            W5BC10         2A  O 10 74
     A  47                             11  3'No build codes found during receiv-
     A                                      ing.'
     A                                      DSPATR(HI)
     A            W5BC11         2A  O 12  3
     A            W5BC12         2A  O 12 10
     A            W5BC13         2A  O 12 18
     A            W5BC14         2A  O 12 26
     A            W5BC15         2A  O 12 34
     A            W5BC16         2A  O 12 42
     A            W5BC17         2A  O 12 50
     A            W5BC18         2A  O 12 58
     A            W5BC19         2A  O 12 66
     A            W5BC20         2A  O 12 74
     A            W5BC21         2A  O 14  3
     A            W5BC22         2A  O 14 10
     A            W5BC23         2A  O 14 18
     A            W5BC24         2A  O 14 26
     A            W5BC25         2A  O 14 34
     A            W5BC26         2A  O 14 42
     A            W5BC27         2A  O 14 50
     A            W5BC28         2A  O 14 58
     A            W5BC29         2A  O 14 66
     A            W5BC30         2A  O 14 74
     A            W5BC31         2A  O 16  3
     A            W5BC32         2A  O 16 10
     A            W5BC33         2A  O 16 18
     A            W5BC34         2A  O 16 26
     A            W5BC35         2A  O 16 34
     A            W5BC36         2A  O 16 42
     A            W5BC37         2A  O 16 50
     A            W5BC38         2A  O 16 58
     A            W5BC39         2A  O 16 66
     A            W5BC40         2A  O 16 74
     A            W5BC41         2A  O 18  3
     A            W5BC42         2A  O 18 10
     A            W5BC43         2A  O 18 18
     A            W5BC44         2A  O 18 26
     A            W5BC45         2A  O 18 34
     A            W5BC46         2A  O 18 42
     A            W5BC47         2A  O 18 50
     A            W5BC48         2A  O 18 58
     A            W5BC49         2A  O 18 66
     A            W5BC50         2A  O 18 74
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Subfile for list display.
     A*
     A          R DSPSFL6                   SFL
     A            W6SR01         6   O  8  2
     A            W6SR02         6   O  8  9
     A            W6SR03         6   O  8 16
     A            W6SR04         6   O  8 23
     A            W6SR05         6   O  8 30
     A            W6SR06         6   O  8 37
     A            W6SR07         6   O  8 44
     A            W6SR08         6   O  8 51
     A            W6SR09         6   O  8 58
     A            W6SR10         6   O  8 65
     A            W6SR11         6   O  8 72
     A*
     A          R DSPSFC6                   SFLCTL(DSPSFL6)
     A*%%TS  SD  19991007  092052  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0140)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND(*MORE)
     A                                  1  2'PO133.06'
     A                                  1 29'Subroutines Processed'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  7  2'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 16'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 23'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 30'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 37'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 44'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 51'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 58'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 65'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 72'Subr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for list display.
     A*
     A          R DSPSFL7                   SFL
     A*%%TS  SD  20000121  135409  DAVE        REL-V4R4M0  5769-PW1
     A            W7SDEF         2A  O  8  2
     A            W7TIE          4Y 0O  8  5EDTCDE(L)
     A            W7HIGH         2Y 0O  8 11EDTCDE(L)
     A            W7PQTY         5Y 0O  8 15EDTCDE(L)
     A            W7POS          3Y 1O  8 24EDTCDE(L)
     A            W7POSF         2Y 0O  8 33EDTCDE(L)
     A            W7PLTS         3Y 0O  8 41EDTCDE(L)
     A            W7SLTS         3Y 0O  8 49EDTCDE(L)
     A            W7SQTY         5Y 0O  8 56EDTCDE(L)
     A            W7PCTU         6Y 3O  8 63EDTCDE(L)
     A            W7TQTY         7Y 0O  8 72EDTCDE(L)
     A                                  9  3'Sort seq:'
     A            W7SRTS         1A  O  9 13
     A                                  9 16'Ptr:'
     A            W7OCUR         1A  O  9 21
     A                                  9 25'Pass:'
     A            W7SQNR         5Y 0O  9 31EDTCDE(3)
     A                                  9 38'Empty pct:'
     A            W7PCTE         6Y 3O  9 49EDTCDE(L)
     A*
     A*
     A          R DSPSFC7                   SFLCTL(DSPSFL7)
     A                                      SFLSIZ(0170)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  01N56                               SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A  01                                  SFLEND(*MORE)
     A                                      SFLDROP(CF11)
     A                                  1  2'PO133.07'
     A                                  1 29'Tie/High Calculations'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Receiving Log'
     A                                      DSPATR(HI)
     A                                  4  2'Received date/time:'
     A            W1RCDT         6Y 0O  4 22EDTCDE(Y)
     A            W1RCTM         6Y 0O  4 31EDTWRD(' 0:  :  ')
     A                                  4 42'By:'
     A            W1RCBY        10A  O  4 46
     A                                  4 59'Workstn:'
     A            W1RCJB        10A  O  4 68
     A                                  5  2'Warehouse . .'
     A            W1WHSE         3Y 0O  5 16EDTCDE(4)
     A            W1WHDS        30A  O  5 21
     A                                  6 16'Qty/'
     A                                      DSPATR(HI)
     A                                  6 22'Pallets'
     A                                      DSPATR(HI)
     A                                  6 32'Full'
     A                                      DSPATR(HI)
     A                                  6 39'Pallets'
     A                                      DSPATR(HI)
     A                                  6 47'Slots'
     A                                      DSPATR(HI)
     A                                  6 54'Qty Fill'
     A                                      DSPATR(HI)
     A                                  6 63'Pct Slot'
     A                                      DSPATR(HI)
     A                                  6 72'Tot Qty'
     A                                      DSPATR(HI)
     A                                  7  2'Def'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  6'Tie'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 11'Hi'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 15'Pallet'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 31'Pallets'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 39'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 47'Needed'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 54'One Slot'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 72'Fit Slot'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 63'  Used  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 22' /Slot '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC6
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN6        79   O  2  2COLOR(BLU)
     A*
     A          R CMDREC7
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN7        79   O  2  2COLOR(BLU)
     A*
     A*
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A            #PGMQ                     SFLPGMQ(10)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            CMDLN1        79   O  2  2COLOR(BLU)
