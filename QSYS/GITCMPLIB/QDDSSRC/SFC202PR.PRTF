     A****************************************************************
     A*
     A* SFC202PR  PIR Piece count by employee (Summary)
     A* 14 September, 2000
     A* Hemant Kapadia
     A*
     A* Revisions:
     A* 09/14/00  HNK  4.14
     A* - Created
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'SFC202'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(001)
     A            TMERNG        22A  O    56
     A*
     A* Print report headings
     A*
     A          R HEAD3
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(002)
     A                                    56'Employee:'
     A            $PEMP#         5A  O    66
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A            PTEMP#    R              1REFFLD(PTEMP# SFC202)
     A                                      EDTCDE(Z)
     A            WKNAME        30         8
     A                                      SPACEA(001)
     A*
     A* Print summary totals
     A*
     A          R SMHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'Summary totals:'
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'Empl#'
     A                                      UNDERLINE
     A                                     8'Employee name                '
     A                                      UNDERLINE
     A                                    40'Dept '
     A                                      UNDERLINE
     A                                    46'Aisle'
     A                                      UNDERLINE
     A                                    53'Pieces'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R SUMAR1
     A                                      SPACEA(001)
     A            S#WHDP         5        40
     A            S#AISL         3        47
     A            S#PCS          7S 0     52EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          25A        1
     A            T#PTOT         9S 0     50EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* Draw 1 Line
     A*
     A          R DRAW1
     A                                      TEXT('DRAW ONE LINE')
     A                                      SPACEA(001)
     A                                     1'----------------------------------+
     A                                       ------------------------------'
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'SFC202: End of Report '
     A*
