     A*%%TS  SD  20091110  133953  JEFF        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave #203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. ZT112FM COPY AND NAME ZT11201
     A*----------------------------------------------------------------
     A*
     A*  ZT112FM   Zone maintenance - Change screen
     A*  31 December, 2009
     A*  Jeffrey Jahnke
     A*
     A*  This is a copy of TM11201
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*    05/19/12  JCJ  6.40a
     A*    - Enh: Renamed from ZT112FM to ZT11201.
     A*           Added Heavy to light sort option.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20091110  133953  JEFF        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'ZT112.01'
     A                                  1 28'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 28'  Detail Entry  '
     A                                      DSPATR(HI)
     A            W1HDG1         8O  O  2 72
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 24EDTCDE(3)
     A                                  5 28'-'
     A            WHDESC        30A  O  5 30
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  3'Zone. . . . . . . . . . . . . .'
     A            W1HTRU        10A  B  9 35
     A N21N90N91                            DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90N91                            DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A                                 10  3'Description . . . . . . . . . .'
     A            W1HDES        30A  B 10 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
640aD *                                11  3'Allow bulk picks  . . . . . . .'
     A            W1HBUL         1A  B 11 35
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
640aMA N90
640aMAO 90                                  DSPATR(PR)
640aMA                                      DSPATR(ND)
417  A*                                12  3'Allow batch picks . . . . . . .'
     A            W1HBAT         1A  B 12 35
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A N90
     AO 90                                  DSPATR(PR)
     A                                      DSPATR(ND)
640aAA                                 13  3'Build Pallets with Heavy Items '
640aAA                                 14  3'on bottom for split stops . . .'
640aAA            W1SORT         1A  B 14 35
640aAA N25N90N91                            DSPATR(HI)
640aAA  25                                  DSPATR(RI)
640aAA N25N90N91                            DSPATR(UL)
640aAA  05                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
640aAA                                 14 40'Y=Yes, N=No'
640aMA                                 16  3'Zone template . . . . . . . . .'
640aMA  89                                  DSPATR(ND)
640aMA            W1ZTMP        10A  B 16 35
640aMA N25N90                               DSPATR(HI)
640aMA  25                                  DSPATR(RI)
640aMA N25N90                               DSPATR(UL)
640aMA  05                                  DSPATR(PC)
640aMA  89
640aMAO 90                                  DSPATR(PR)
640aMA  89                                  DSPATR(ND)
     A                                 19  3'Truck names starting with an *'
     A                                      DSPATR(HI)
     A                                 19 34'will be considered a zone template'
     A                                      DSPATR(HI)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A                                      OVERLAY
     A                                      CLRL(23)
     A  98                                  ALARM
     A                                  1  2'ZT112.02'
     A                                  1 28'Zone Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 24EDTCDE(3)
     A                                  5 28'-'
     A            WHDESC        30   O  5 30
     A                                  7  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
