     A*%%TS  SD  20070613  080725  DAVE        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*
     A*  TSTGETCDGT  Test program GETCHKDGT
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    06/13/07  DAS  5.10
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070613  080725  DAVE        REL-V5R3M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTGETCHKD.1'
     A                                  1 30'Test Program GETCHKDGT'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Command  . . . . . . . .'
     A            W1CMD          8A  B  5 30
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  5 51'n/a'
     A                                  7  4'Warehouse  . . . . . . .'
     A            W1WHSE         3S 0B  7 30
     A                                  8  4'Department . . . . . . .'
     A            W1WHDP         5A  B  8 30
     A                                  9  4'Slot . . . . . . . . . .'
     A            W1SLOT        12A  B  9 30
     A                                 11  4'Return code  . . . . . .'
     A            W1RETURN      10A  O 11 30
     A                                 12  4'Codes  . . . . . . . . .'
     A            W1CODES        1  0O 12 30
     A                                 13  4'Digits . . . . . . . . .'
     A            W1DIGITS       1  0O 13 30
     A                                 14  4'Order type . . . . . . .'
     A            W1OTYPE        1   O 14 30
     A                                 15  4'Order descriptions . . .'
     A            W1ORDER1       6   O 15 30
     A            W1ORDER2       6   O 15 38
     A            W1ORDER3       6   O 15 46
     A                                 16  4'Code type  . . . . . . .'
     A            W1CTYPE        1   O 16 30
     A                                 17  4'Code description . . . .'
     A            W1CODE         6   O 17 30
     A                                 18  4'Check digit  . . . . . .'
     A            W1CHKDIGIT     3  0O 18 30
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
