     A*%%TS  SD  20070926  223752  JEFF        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*  Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*  BFC Software, Inc.
     A*  245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*  West Chicago, IL  60185
     A*  (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  RMTOUTQDS
     A*  December 26, 2007
     A*  Jeffrey Jahnke
     A*
     A*  Revisions:
     A*
510 AA*  12-26-2007  JCJ   5.10
     A*    - Copied of the internet
     A*
     A*----------------------------------------------------------------
     A*     Written By:  Bryan Dietz with substantial Subfile help from Tom Cham
     A*     Dec. 2001
     A*
     A*-------------------------------------------------------------------------
     A* 12/05/01             Work with Remote Writers(WRKRMTWTR)            USRN
     A* 10:28:45                                                            RMTO
     A*   Type options, press Enter.
     A*  1=Start Remote Writer  2=Chg Outq  3=Hold Outq   4=End Writer
     A*  5=Work Writer  6=Release Outq  8=Work Outq  99=Ping
     A*
     A*                 Nbr Outq                        Remote     WSCST    Writ
     A*Opt Outq        Splf Status  IP Address          Queue      Type     Stat
     A*__  LPR5           0 RLSD  1.2.3.4              USRNAME    *HPIID
     A*__  LPR6           0 RLSD  5.6.7.8              USRNAME    *HPIID
     A*__  LPR7           0 RLSD  9.10.11.12           USRNAME    *HPIID
     A*__  LPR8           0 RLSD  12.34.56.78          USRNAME    *HPIID
     A*__  LPR9           0 RLSD  10.1.1.1             USRNAME    *HPIID
     A*__  RMTAS400       0 RLSD  10.2.2.2             CORP_IN               STR
     A*
     A*
     A*
     A*  F3/F12=Exit  F4=Prompt  F5=Refresh  F6=CrtOutq  F9=Command Line  F10=QS
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ALWGPH
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A N01 01                               CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      HELP
     A          R WWM01004
     A                                      RTNCSRLOC(*RECNAME &BCURRD &BCURFD -
     A                                      &BCURPS)
     A                                      CSRLOC(BCRROW     BCRCOL)
     A                                      OVERLAY
     A  02                                  INVITE
     A            BCURRD        10A  H
     A            BCURFD        10A  H
     A            BCURPS         4S 0H
     A            BCRROW         3S 0H
     A            BCRCOL         3S 0H
      *************************************************************
     A          R WWM01000                  SFL
     A            O#LIB         10   H
     A            O#WTR         10   H
     A            BOPT           2Y 0B  9  2DSPATR(PC)
     A  15                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A            O#OUTQ        10   O    +1
     A            O#NUMFLS       4  0O    +1EDTCDE(1)
     A            O#OSTAT4       4   O    +1
     A            O#RMTSYS      20   O    +2
     A            O#PRTQ        10   O    +1
     A            O#MFG         10   O    +1
     A            O#WSTAT4       4   O    +1
     A*************************************************************
     A          R WWM01001                  SFLCTL(WWM01000)
     A*%%TS  SD  20011128  135210  BDIETZ      REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      RTNCSRLOC(*RECNAME &BCURRD &BCURFD -
     A                                      &BCURPS)
     A                                      CSRLOC(BCRROW     BCRCOL)
     A  02                                  INVITE
     A                                      OVERLAY
     A  03                                  SFLDSP
     A  04                                  SFLDSPCTL
     A  05                                  SFLCLR
     A  06                                  SFLEND(*MORE)
     A            BDRRN          4S 0H      SFLRCDNBR(CURSOR)
     A            BCURRD        10A  H
     A            BCURFD        10A  H
     A            BCURPS         4S 0H
     A            BCRROW         3S 0H
     A            BCRCOL         3S 0H
     A                                  3  4'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  1'Opt'
     A                                      DSPATR(HI)
     A                                  8 22'Status'
     A                                      DSPATR(HI)
     A                                  7 18'Nbr'
     A                                      DSPATR(HI)
     A                                  8 17'Splf'
     A                                      DSPATR(HI)
     A                                  7 22'Outq'
     A                                      DSPATR(HI)
     A                                  8  5'Outq'
     A                                      DSPATR(HI)
     A                                  4  3'1=Start Remote Writer'
     A                                      COLOR(BLU)
     A                                    +2'2=Chg Outq'
     A                                      COLOR(BLU)
     A                                    +2'3=Hold Outq '
     A                                      COLOR(BLU)
     A                                    +2'4=End Writer'
     A                                      COLOR(BLU)
     A                                  5  3'5=Work Writer'
     A                                      COLOR(BLU)
     A                                    +2'6=Release Outq'
     A                                      COLOR(BLU)
     A                                    +2'8=Work Outq'
     A                                      COLOR(BLU)
     A                                    +2'99=Ping'
     A                                      COLOR(BLU)
     A                                  7 50'Remote'
     A                                      DSPATR(HI)
     A                                  8 50'Queue'
     A                                      DSPATR(HI)
     A                                  7 61'WSCST'
     A                                      DSPATR(HI)
     A                                  8 61'Type'
     A                                      DSPATR(HI)
     A                                  7 70'Writer'
     A                                      DSPATR(HI)
     A                                  8 30'IP'
     A                                      DSPATR(HI)
     A                                  8 33'Address'
     A                                      DSPATR(HI)
     A                                  8 70'Status'
     A                                      DSPATR(HI)
     A*************************************************************
     A          R WWMTOP
     A*
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A            RRN            4S 0   2 12
     A                                  1 70USER
     A            BPRGM         10A  O  2 70
     A                                  1 23'Work with Remote Writers(WRKRMTWTR-
     A                                      )'
     A                                      DSPATR(HI)
     A*************************************************************
     A          R WWMNODTA
     A                                      OVERLAY
     A                                 11  6'(No Remote Writers to Display)'
     A                                      COLOR(BLU)
     A*************************************************************
     A          R WWMSGSFL                  SFL
     A                                      TEXT('MESSAGE SUBFILE')
     A                                      SFLMSGRCD(24)
     A            BMSGK                     SFLMSGKEY
     A            BPGMQ                     SFLPGMQ(10)
     A*************************************************************
     A          R WWMSGCTL                  SFLCTL(WWMSGSFL)
     A*%%TS  SD  20070926  223752  JEFF        REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A                                      TEXT('MESSAGE SUBFILE CONTROL RECOR-
     A                                      D')
     A  11                                  LOCK
     A  11                                  FRCDTA
     A                                      OVERLAY
     A N12                                  SFLDSP
     A N12                                  SFLDSPCTL
     A N12                                  SFLINZ
     A N12                                  SFLEND
     A            BPGMQ                     SFLPGMQ(10)
     A                                 22  3'F3/F12=Exit'
     A                                      COLOR(BLU)
     A                                 22 16'F4=Prompt'
     A                                      COLOR(BLU)
     A                                 22 27'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 22 39'F6=CrtOutq'
     A                                      COLOR(BLU)
     A                                      DSPATR(ND)
     A                                 22 51'F9=Command Line'
     A                                      COLOR(BLU)
     A                                 22 68'F10=QSPL Sbs'
     A                                      COLOR(BLU)
