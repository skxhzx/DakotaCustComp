     A*%%TS  SD  19991124  154713  DAVE        REL-V4R2M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/23/99  DAS  4.13
     A*     - Moved warehouse and item headings up one line.
     A*     - Changed hard code 'Item' and 'Base' labels to output fields
     A*       that will be initialized in the IT810 program.
417a A*    03/11/04  RH   4.17a
     A*     - Add F9 to screen 2 to view pending slot quantity detail
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19941019  134850  HEMANT      REL-V2R1M1  5738-PW1
     A                                      CF04
     A                                      CF09
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SL810.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 10  4'Slot number . . . . . . . . .'
     A            W1DISP        12A  B 10 35
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 31'View Slot Quantities'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  B  9 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  19991124  154713  DAVE        REL-V4R2M0  5769-PW1
     A                                      CHGINPDFT
417a A                                      CF09
     A                                      CF13
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL810.02'
     A                                  1 31'View Slot Quantities'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            W2DUMY         1A  I  2  2DSPATR(PC)
     A                                      DSPATR(PR)
     A                                  3  2'Warehouse            :'
     A            ITWHSE         3Y 0O  3 25EDTCDE(4)
     A            WHDESC        30A  O  3 29
     A                                  4  2'Department           :'
     A            SLWHDP         5A  O  4 25
     A                                  5  2'Slot                 :'
     A            SLDISP        12A  O  5 25
     A            W2LBL1        22   O  6  2
     A            W2HEAD        55A  O  6 25
     A            W2LBLX        22   O  7  2
     A            W2HEDX        55   O  7 25
     A                                  9  2'Press Enter to return.'
     A                                      COLOR(BLU)
     A                                 10 28'Normal'
     A                                 10 40'Breakdown 1'
     A                                 10 56'Breakdown 2'
     A            W2UM1          5A  O 11 28
     A            W2UM2          5A  O 11 43
     A            W2UM3          5A  O 11 59
     A                                 12 25'----------'
     A                                 12 40'-----------'
     A                                 12 56'-----------'
     A                                 13  4'Stock . . . . . . .'
     A            SLSTK1    R        O 13 28REFFLD(SLREC/SLSTK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLSTK2    R        O 13 45REFFLD(SLREC/SLSTK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLSTK3    R        O 13 61REFFLD(SLREC/SLSTK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                 14  4'Allocated . . . . .'
     A            SLALC1    R        O 14 28REFFLD(SLREC/SLALC1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLALC2    R        O 14 45REFFLD(SLREC/SLALC2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLALC3    R        O 14 61REFFLD(SLREC/SLALC3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                 15  4'Picked  . . . . . .'
     A            SLPCK1    R        O 15 28REFFLD(SLREC/SLPCK1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLPCK2    R        O 15 45REFFLD(SLREC/SLPCK2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLPCK3    R        O 15 61REFFLD(SLREC/SLPCK3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                 16  4'Transfer  . . . . .'
     A            SLTFR1    R        O 16 28REFFLD(SLREC/SLTFR1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLTFR2    R        O 16 45REFFLD(SLREC/SLTFR2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLTFR3    R        O 16 61REFFLD(SLREC/SLTFR3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                 17  4'Receive . . . . . .'
     A            SLRCV1    R        O 17 28REFFLD(SLREC/SLRCV1 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLRCV2    R        O 17 45REFFLD(SLREC/SLRCV2 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A            SLRCV3    R        O 17 61REFFLD(SLREC/SLRCV3 *LIBL/SLOT)
     A                                      EDTCDE(O)
     A                                 19  4'Available . . . . .'
     A            W2AVL1         7Y 0O 19 25EDTCDE(N)
     A            W2AVL2         5Y 0O 19 42EDTCDE(N)
     A            W2AVL3         5Y 0O 19 58EDTCDE(N)
     A                                 21  4'(Qty available = (Stock + Receive)-
     A                                       - (Allocated + Picked + Transfer)'
     A                                 23  2'F5=Refresh  F9=Pending detail  -
     A                                       F12=Cancel  F13=Linked items'
417aDA*                                23  2'F5=Refresh  F12=Cancel  F13=Linked-
417aDA*                                      items'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
