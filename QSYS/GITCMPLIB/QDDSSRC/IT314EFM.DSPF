     A*%%TS  SD  20031023  160051  ROCKY       REL-V5R2M0  5722-WDS
     A*****************************************************************
     A*
     A*  IT314EFM  View item/slot activity report - item/close dt/trans
     A*  20 February 2006
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*  Roxanne Harrison
     A*
     A*  Revisions:
     A*
     A*    02/20/06  RH   5.00
     A*      - Created .
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19941109  162934  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 30' Item/Slot Activity   '
     A                                      DSPATR(HI)
     A                                  1  2'IT314E'
     A                                  2 30'  By Item/Close dt/Trans'
     A                                      DSPATR(HI)
     A            W1FMDT         6Y 0O  3 30EDTCDE(Y)
     A                                      DSPATR(HI)
     A            W1TODT         6Y 0O  3 44EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  3 40'to'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  19960612  144124  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(04)
     A                                      OVERLAY
     A            WOWHSE         3A  O  1  2
     A                                  1  8'-'
     A            WHDESC        30A  O  1 10
     A            W1ITEM        15A  O  2  8
     A*                                 2 30'Slot:'
     A*           W1DISP        12A  O  2 36
     A            W1LINK        18A  O  3  8
     A                                  3 41'Position to item  . . .'
     A            PTINP1        15A  B  3 65
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  2  2'Item:'
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960703  101742  ROCKY       REL-V3R1M0  5763-PW1
     A  85                                  SFLNXTCHG
     A            WKITEM    R        O  9  2REFFLD(WKREC/WKITEM *LIBL/IT313A)
     A            W1DESC        20A  O  9 18
     A            W1DATE         6Y 0O  9 39EDTCDE(Y)
     A            WKDISP    R        O  9 48REFFLD(WKREC/WKDISP *LIBL/IT313A)
     A            W1TYPE         8A  O  9 61
     A            QTY            5Y 0O  9 70EDTCDE(K)
     A            WKUOM     R        O  9 79REFFLD(WKREC/WKUOM *LIBL/IT313A)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960703  101742  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO          4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 39'Created '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 48'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 61'Type   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 70'Qty    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 78'UOM'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Description         '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20031023  160051  ROCKY       REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            WKITEM    R        O  9  2REFFLD(WKREC/WKITEM *LIBL/IT313A)
     A            W1CDTE         6Y 0O  9 19EDTCDE(Y)
     A            WKDISP    R        O  9 29REFFLD(WKREC/WKDISP *LIBL/IT313A)
     A            QTY            5Y 0O  9 49EDTCDE(K)
     A            WKTYPE    R        O  9 44REFFLD(WKREC/WKTYPE *LIBL/IT313A)
     A            WKUOM     R        O  9 59REFFLD(WKREC/WKUOM *LIBL/IT313A)
     A            WKTRN#    R        O  9 63REFFLD(WKREC/WKTRN# *LIBL/IT313A)
     A                                      EDTCDE(3)
     A            WKLBL#    R        O  9 72REFFLD(WKREC/WKLBL# *LIBL/IT313A)
     A                                      EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20031023  160051  ROCKY       REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO2         4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 19'Closed  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 29'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 43'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 48'Qty     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 58'UOM'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 63'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 72'Label #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20031023  160051  ROCKY       REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            WKITEM    R        O  9  2REFFLD(WKREC/WKITEM *LIBL/IT313A)
     A            W1DESC        20A  O  9 18
     A            W1DATE         6Y 0O  9 39EDTCDE(Y)
     A            WKDISP    R        O  9 48REFFLD(WKREC/WKDISP *LIBL/IT313A)
     A            WKTYPE    R        O  9 62REFFLD(WKREC/WKTYPE *LIBL/IT313A)
     A            WKTRN#    R        O  9 66REFFLD(WKREC/WKTRN# *LIBL/IT313A)
     A                                      EDTCDE(3)
     A            WKLBL#    R        O  9 74REFFLD(WKREC/WKLBL# *LIBL/IT313A)
     A                                      EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19960703  093827  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A            RECNO3         4S 0H      SFLRCDNBR
     A                                  8  2'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 18'Description         '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 39'Created '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 48'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 61'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 66'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 74'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
