     A*%%TS  SD  20040130  015837  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  EQ612FM   Equipment - Yard Status - Change screen
     A*  30 January 2004
     A*  Max Blacknik
     A*
     A*  Revisions
     A*
417 AA*    01/30/04  MLB  4.17
     A*      - Created.
417bAA*    04/28/04  RH   4.17b
     A*      - Increase length of equipment code to 8.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040130  015837  MLBLACKNIK  REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'EQ612.01'
     A                                  1 29'Equipment - Yard Status'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 34'Update Status'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse . . :'
     A            $PWHSE         3Y 0O  4 18EDTCDE(3)
     A                                  4 22'-'
     A            WHDESC        30A  O  4 24
     A                                  5  2'Equipment code:'
417b A*           $PCODE         6A  O  5 18
417b A            $PCODE         8A  O  5 18
     A                                  5 27'-'
     A            EQDESC        30A  O  5 29
     A                                  6  2'Equipment type:'
     A            EQTYPE         6A  O  6 18
     A                                  6 25'-'
     A            EYDESC        30A  O  6 27
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  2'Equipment status . . .'
     A            W1LOC          1A  B 10 25
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1LCDS        20A  O 10 29
     A                                 10 50'I=In yard, D=Dock, L=Loaded'
     A                                 11 50'O=Out, N=Out of svc'
     A                                 12  2'If location is dock  .'
     A                                 13  3'Dock door . . . . . .'
     A            W1DOOR         3S 0B 13 25
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17  2'Last changed by  . . .'
     A            W1USER        10A  O 17 25
     A            W1EMP#         5Y 0O 17 36EDTCDE(4)
     A                                 17 42'/'
     A            W1ENAM        35A  O 17 44
     A                                 18  2'Last change date/time.'
     A            ESSDTS    R     Z    18 25REFFLD(ESSDTS EQUIPYS)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
