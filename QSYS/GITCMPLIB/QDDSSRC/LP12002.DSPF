     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
416 AA* ***************************************************************
416 AA* *****  If the position of any slot entry field is changed *****
416 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
416 AA* *****  program.                                           *****
416 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LP120FM   Pallet License Plate - View RF
     A*  12 February 1997
     A*  Dave Sommerville
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
414dAA*    09/13/00  HNK  4.14d
     A*      - Add pick slot on screen 2
     A*
416 AA*    03/28/02  DAS  4.16
     A*      - Added W2ITM2 to top of screen.
     A*        Re: Item number comes last in W2ITEM and would get
     A*            chopped off. AFI relies on item number.
416aAA*    05/23/02  RRH  4.16a
     A*      - Increase W1LCNS and W2LCNS to 15.
     A*
500 AA*   05/31/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   10/29/07  LMC  5.10a
     A*     - 16 x 20 screens for handheld
     A*     - 16 X 20 Screens for spanish have been copied from the
     A*       original spanish screens, no changes were made to them
     A*       at this time, if we have a client that needs them they
     A*       will be reformatted at that time.
600aAA*   03/05/09  DAS  6.00a
     A*     - Revised to work with new license tracking.
     A*     - Renamed to LP12001
     A*     - Removed OVERLAY from screens 1 & 2.
     A*     - Added dummy input field to screen 2.
640aAA*   08/18/11  RH   6.40a
     A*     - Fix: Change to position to not positon to dummy field
     A*       was causing small screens to be moved to left to view.
650aAA*   03/19/14  RH   6.50a
     A*     - Enh: Enable F11=Transfer
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20090306  215928  DAVE        REL-V6R1M0  5761-WDS
600aDA**                                    OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.01'
     A                                  1 13'License'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  5  2'License.'
416aDA*           W1LCNS         7Y 0B  6 11CHECK(ER)
     A            W1LCNS        15A  B  5 11CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
650aAA                                      CF11
600aDA**                                    OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'License'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  1 41DSPATR(ND)
     A                                      DSPATR(PR)
640aDA*                                     DSPATR(PC)
416aDA*                                 2  1'License:'
     A                                  2  1'Licn:'
640aMA                                      DSPATR(PC)
416aDA*           W2LCNS         7Y 0O  2 10EDTCDE(3)
     A            W2LCNS        15A  O  2  7
     A            W2ITM2        15A  O  2 24
     A                                  3  1'Item:'
     A            W2ITEM        49A  O  3  7
     A                                  4  1'Slot:'
     A            W2DISP        12A  O  4  7
     A                                  4 20'Pck Slt:'
     A            W2PSLT        12A  O  4 29
     A                                  5  1'Qty :'
     A            W2QTY         10   O  5  7
     A                                  6  1'Ent :'
     A            W2ENTD         6Y 0O  6  7EDTCDE(Y)
     A                                  6 17'Exp:'
     A            W2EXPD         8   O  6 22
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
650a A                                  7 23'F11=Tfr'
650aAA                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
     A          R SCRN1SP
     A*%%TS  SD  20090306  215928  DAVE        REL-V6R1M0  5761-WDS
600aDA**                                    OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.01'
     A                                  1 13'Licencia'
     A                                      DSPATR(HI)
     A                                  3  2'Alm '
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  5  2'Licencia.'
416aDA*           W1LCNS         7Y 0B  6 12CHECK(ER)
     A            W1LCNS        15A  B  5 12CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
416aDA*                                     EDTCDE(4)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
     A          R SCRN2SP
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
650aAA                                      CF11
600aDA**                                    OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'Licencia'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  1 41DSPATR(ND)
     A                                      DSPATR(PR)
640aDA*                                     DSPATR(PC)
416aDA*                                 2  1'License:'
     A                                  2  1'Licn:'
640aMA                                      DSPATR(PC)
416aDA*           W2LCNS         7Y 0O  2 10EDTCDE(3)
     A            W2LCNS        15A  O  2  7
     A            W2ITM2        15A  O  2 24
     A                                  3  1'Prdc:'
     A            W2ITEM        49A  O  3  7
     A                                  4  1'Slot:'
     A            W2DISP        12A  O  4  7
     A                                  5  1'Ctd :'
     A            W2ENTD         6Y 0O  6  7EDTCDE(Y)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A            W2PSLT        12A  O  4 29
     A                                  4 20'Pck Slt:'
     A            W2QTY         10   O  5  7
     A                                  6  1'Ent :'
     A                                  6 17'Exp:'
     A            W2EXPD         8   O  6 22
650a A                                  7 23'F11=Tfr'
650aAA                                      COLOR(BLU)
     A          R SCREEN61
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
600aDA**                                    OVERLAY
     A                                      CLRL(15)
     A  98                                  ALARM
     A                                  1  2'LP120.61'
     A                                  1 13'License'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  5  2'Lic#'
     A            W1LCNS        15A  B  5  7CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                 15  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN62
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
650aAA                                      CF11
600aDA**                                    OVERLAY
     A                                      CLRL(15)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'License'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  1 41DSPATR(ND)
     A                                      DSPATR(PR)
640aDA*                                     DSPATR(PC)
     A                                  3  1'Lic#'
640aMA                                      DSPATR(PC)
     A            W2LCNS        15A  O  3  6
     A                                  4  1'Item'
     A            W2ITM2        15A  O  4  6
     A            W2ITEM        49A  O  5  6
     A                                  6  1'Slot'
     A            W2DISP        12A  O  6  9
     A                                  7  1'Pck Slt'
     A            W2PSLT        12A  O  7  9
     A                                  8  1'Lic Qty'
     A            W2QTY         10   O  8  9
     A                                  9  1'Ent Dte'
     A            W2ENTD         6Y 0O  9  9EDTCDE(Y)
     A                                 10  1'Exp Dte'
     A            W2EXPD         8   O 10  9
     A                                 15  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
650aAA                                 15 23'F11=Transfer'
650aAA                                      COLOR(BLU)
510aAA*
510aAA* ERROR MESSAGE SUBFILE RECORDS
510aAA*
510aAA*    ERROR MESSAGE SUBFILE RECORD
510aAA*
510aAA          R M16REC                    SFL
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      SFLMSGRCD(16)
510aAA            #MSGK                     SFLMSGKEY
510aAA            #PGMQ                     SFLPGMQ(10)
510aAA*    ERROR MESSAGE SUBFILE CONTROL RECORD
510aAA*
510aAA          R M16CTL                    SFLCTL(M16REC)
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      SFLSIZ(0002)
510aAA                                      SFLPAG(0001)
510aAA  98                                  ALARM
510aAA                                      LOCK
510aAA                                      OVERLAY
510aAA N97                                  SFLCLR
510aAA  97                                  SFLDSP
510aAA  97                                  SFLEND
510aAA          R M16CLR
510aAA*%%TS  SD  20071026  153509  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      LOCK
510aAA                                      SLNO(16)
510aAA                                      CLRL(01)
510aAA*****************************************************************
510aAA* Spanish Screens
510aAA*****************************************************************
510aAA*----------------------------------------------------------------
     A          R SCRN61SP
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
600aDA**                                    OVERLAY
     A                                      CLRL(15)
     A  98                                  ALARM
     A                                  1  2'LP120.01'
     A                                  1 13'Licencia'
     A                                      DSPATR(HI)
     A                                  3  2'Alm '
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  5  2'Licencia.'
     A            W1LCNS        15A  B  5 12CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A*         R SCRN2SP
     A          R SCRN62SP
     A*%%TS  SD  20090307  073354  DAVE        REL-V6R1M0  5761-WDS
650aAA                                      CF11
600aDA**                                    OVERLAY
     A                                      CLRL(07)
     A  98                                  ALARM
     A                                  1  2'LP120.02'
     A                                  1 11'Licencia'
     A                                      DSPATR(HI)
     A            FLD02          1A  B  1 41DSPATR(ND)
     A                                      DSPATR(PR)
640aDA*                                     DSPATR(PC)
416aDA*                                 2  1'License:'
     A                                  2  1'Licn:'
640aMA                                      DSPATR(PC)
416aDA*           W2LCNS         7Y 0O  2 10EDTCDE(3)
     A            W2LCNS        15A  O  2  7
     A            W2ITM2        15A  O  2 24
     A                                  3  1'Prdc:'
     A            W2ITEM        49A  O  3  7
     A                                  4  1'Slot:'
     A            W2DISP        12A  O  4  7
     A                                  5  1'Ctd :'
     A            W2ENTD         6Y 0O  6  7EDTCDE(Y)
     A                                  7  2'F3=Salir  F12=Cancelar'
     A                                      COLOR(BLU)
     A            W2PSLT        12A  O  4 29
     A                                  4 20'Pck Slt:'
     A            W2QTY         10   O  5  7
     A                                  6  1'Ent :'
     A                                  6 17'Exp:'
     A            W2EXPD         8   O  6 22
650aAA                                  7 23'F11=Tfr'
650aAA                                      COLOR(BLU)
