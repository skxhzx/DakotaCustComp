     A*%%TS  SD  20051229  140047  JEFF        REL-V5R3M0  5722-WDS
     A********************************************************************
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A********************************************************************
     A*
     A*  RU120FM      Work with Flag Entries - Display
     A*  06 May 2004
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*  Revisions
417 AA*    05/06/04  MLB  4.17
     A*      - Created.
     A*
500 AA*    03/29/05  MLB  5.00
     A*      - Enh: Added new function key: F16=Select all
     A*        Moved text "Type options..." to POSTNREC format.
     A*        Added new select field to POSTNREC: FLGTYP with
     A*        values of *RPL or *OUT.
500aAA*    07/10/05  MLB  5.00a
     A*      - Enh: Added new select field to POSTNREC: FLGTYP with
     A*        new value of *MRG.
500bAA*    07/27/05  MLB  5.00b
     A*      - Enh: Revised FLGTYP field to remove '*' from list of
     A*        values.
500cAA*    10/26/05  MLB  5.00c
     A*      - Enh: Revised View 3 subfile/subctl formats to add
     A*        new Replenished date/time fields in place of Closed
     A*        date/time.
500dAA*    12/27/05  JCJ/MLB  5.00d
     A*      - Enh: Revise Window 1 format to increase size of window
     A*        to allow wdwfmt 3 and wdwfmt 4 to be displayed. These
     A*        two new formats prompt the user for both a report &
     A*        label printers.
520 AA*    05/30/08  RBD  5.20  P#00060
     A*      - Shortened W1STAT from length 12 to 11 to make room
     A*        for and add W1SM to identify loader merged items
     A*        short or mispick flags (created by loader system pgms,
     A*        RF- Loader9 --> Loader19 ).
650a A*    01/19/15  GJA  6.50a
     A*      - Enh:  Add selector employee number
710a A*    06/05/17  TAB  7.10a
     A*      - Enh:  Add selctor for Box Breaker
     A*              and ALL flag types
720aAA*    01/16/18  RBD  7.20a
     A*      - Created RU12003
     A*      - Color legend when runner flag type is M:
     A*          If a runner flag exists for the child:
     A*            Green : child status is not O,P or R
     A*            Yellow: child status is P
     A*            Red   : child status is O or R
     A*            Pink  : child runner type is U and its flag trans is gt 0
     A*          If no runner flag exists for the child:
     A*            Green : trans status is Ended, Closed or Deleted
     A*            Yellow: trans status is Open or Started
720bAA*    07/03/18  RBD  7.20b
     A*      - Abbreviate Status to make room for adding Route to first screen.
730aAA*    05/07/19  DAS  7.30a
     A*      - Added 5th veiw, originally for Greco.
     A*
     A*-------------------------------------------------------------------
     A*  Client Custom Revisions: Grocery Supply
GSC AA*    01/14/06  MLB  GSC
     A*      - Added two new dspf fmts: VW4SFLGS, VW4SFCGS to display
     A*        additional Grocery Supply info.
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A                                      OVERLAY
     A                                  1  2'RU120'
     A                                  1 30'Work with Flag Entries'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A                                  2  2'Warehouse:'
     A            WOWHSE        40A  O  2 13
     A                                  3 47'Change to warehouse . . .'
     A            PTINP1         3Y 0B  3 73EDTCDE(4)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                  4  2'Dept:'
     A            WOWHDP         5A  O  4  8
     A            WOWHDS        30A  O  4 14
     A                                  4 47'Change to department  . .'
     A            PTINP2         5A  B  4 73
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  5 45'Position to label number  .'
     A            PTINP3         7Y 0B  5 73EDTCDE(4)
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
500 AA                                  6  2'Type options, press Enter.'
500 AA                                      COLOR(BLU)
710aMA                                  6 34'Select flag type:'
710aMA            FLGTYP         3A  B  6 52VALUES('RPL' 'OUT' 'MRG' 'BXB' 'ALL-
710aMA                                      ')
710aMA                                  6 57'RPL, OUT, MRG, BXB, ALL'
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
720aAA            HLDI32         1A  H      TEXT('Hold Ind 32 setting')
720aAA            HLDI33         1A  H      TEXT('Hold Ind 33 setting')
720aAA            HLDI34         1A  H      TEXT('Hold Ind 34 setting')
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1WHDP         5A  O 12  5
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W1ITEM        15A  O 12 11
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W1DESC        30A  O 12 27
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W1QYSO         5Y 0O 12 58EDTCDE(L)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W1UCOD         2A  O 12 65
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
520 DA**          W1STAT        12A  O 12 68
720bDA**          W1STAT        11A  O 12 68
720bMA            W1STAT         5A  O 12 68
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
720bAA            W1RTE          5A  O 12 74
720bAA  32                                  COLOR(YLW)
720bAA  33                                  COLOR(RED)
720bAA  34                                  COLOR(PNK)
520 AA            W1SM           1A  O 12 80
520 AA  31                                  COLOR(RED)
520 AA  31                                  DSPATR(BL)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&SFLRRN)
     A                                      CF06
     A                                      CF10
     A                                      CF11
500 AA                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            SFLRRN         5S 0H
     A                                 10 58'Quantity'
     A                                      DSPATR(HI)
     A                                 11  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  5'Dept '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 11'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 27'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 58'Short/Out'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
720bMA                                 11 68'Stat '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
720bAA                                 11 74'Route'
720bAA                                      DSPATR(HI)
720bAA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display - View 2
     A*
     A          R VW2SFL                    SFL
     A  21                                  SFLNXTCHG
720aAA            HLDI32         1A  H      TEXT('Hold Ind 32 setting')
720aAA            HLDI33         1A  H      TEXT('Hold Ind 33 setting')
720aAA            HLDI34         1A  H      TEXT('Hold Ind 34 setting')
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2ITEM        15A  O 12  6
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W2DISP        12   O 12 23
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W2LBL#         7Y 0O 12 37EDTCDE(4)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W2OTRN         7Y 0O 12 46EDTCDE(4)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
710aMA            W2TRN#         7Y 0O 12 54EDTCDE(3)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
710aMA            W2RTE          5A  O 12 62
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
710aMA            W2RTID         5A  O 12 68
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
710aAA            W2TYPE         3A  O 12 74
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&SFLRRN)
     A                                      CF06
     A                                      CF10
     A                                      CF11
500 AA                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            SFLRRN         5S 0H
710aMA                                 10 46'Origin'
     A                                      DSPATR(HI)
710bMA                                 10 54'Flag'
     A                                      DSPATR(HI)
710bMA                                 10 68'Route'
     A                                      DSPATR(HI)
     A                                 11  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 23'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 37'Label# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 46'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
710aMA                                 11 54'Tran#  '
710aMA                                      DSPATR(HI)
710aMA                                      DSPATR(UL)
710aMA                                 11 62'Route'
710aMA                                      DSPATR(HI)
710aMA                                      DSPATR(UL)
710aMA                                 11 68' ID  '
710aMA                                      DSPATR(HI)
710aMA                                      DSPATR(UL)
710aAA                                 11 74'Type'
710aAA                                      DSPATR(UL)
710aAA                                      COLOR(WHT)
     A*****************************************************************
     A*
     A* Subfile for List Display - View 3
     A*
     A          R VW3SFL                    SFL
     A  21                                  SFLNXTCHG
720aAA            HLDI32         1A  H      TEXT('Hold Ind 32 setting')
720aAA            HLDI33         1A  H      TEXT('Hold Ind 33 setting')
720aAA            HLDI34         1A  H      TEXT('Hold Ind 34 setting')
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3ITEM        15A  O 12  6
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W3OPND         6Y 0O 12 23EDTCDE(Y)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W3OPNT          T  O 12 32
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
500cAA            W3RPLD         6Y 0O 12 42EDTCDE(Y)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
500cAA            W3RPLT          T  O 12 51
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
500cDA*           W3PRTD         6Y 0O 12 42EDTCDE(Y)
500cMA            W3PRTD         6Y 0O 12 61EDTCDE(Y)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
500cDA*           W3PRTT          T  O 12 51
500cMA            W3PRTT          T  O 12 70
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
500cDA*           W3CLSD         6Y 0O 12 61EDTCDE(Y)
500cDA*           W3CLST          T  O 12 70
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&SFLRRN)
     A                                      CF06
     A                                      CF10
     A                                      CF11
500 AA                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            SFLRRN         5S 0H
     A                                 10 23'---- Opened -----'
     A                                      DSPATR(HI)
500cAA                                 10 42'-- Replenished --'
500cAA                                      DSPATR(HI)
500cDA*                                10 42'---- Printed ----'
500cMA                                 10 61'---- Printed ----'
     A                                      DSPATR(HI)
500cDA*                                10 61'---- Closed -----'
500cDA*                                     DSPATR(HI)
     A                                 11  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 23'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 32'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 42'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 51'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 61'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 70'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Subfile for List Display - View 4
     A*
     A          R VW4SFL                    SFL
     A  21                                  SFLNXTCHG
720aAA            HLDI32         1A  H      TEXT('Hold Ind 32 setting')
720aAA            HLDI33         1A  H      TEXT('Hold Ind 33 setting')
720aAA            HLDI34         1A  H      TEXT('Hold Ind 34 setting')
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W4ITEM        15A  O 12  6
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W4EMPNAM      34   O 12 23
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W4LBL#         7Y 0O 12 60EDTCDE(4)
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W4RTE          5A  O 12 68
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A            W4RTID         5A  O 12 75
720aAA  32                                  COLOR(YLW)
720aAA  33                                  COLOR(RED)
720aAA  34                                  COLOR(PNK)
     A*
     A          R VW4SFC                    SFLCTL(VW4SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&SFLRRN)
     A                                      CF06
     A                                      CF10
     A                                      CF11
500 AA                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            SFLRRN         5S 0H
     A                                 11 23'Employee'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 60'Label# '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 68'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 75' ID  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
GSC AA*****************************************************************
GSC AA*
GSC AA* Subfile for List Display - View 4
GSC AA*
GSC AA          R VW4SFLGS                  SFL
GSC AA  21                                  SFLNXTCHG
GSC AA            OPTION         2A  B 12  2
GSC AA  20                                  DSPATR(PC)
GSC AA N21N90                               DSPATR(UL)
GSC AA  21                                  DSPATR(RI)
GSC AA  90                                  DSPATR(PR)
GSC AA            W4HTR#    R        O 12  6REFFLD(ALREC/ALHTR# *LIBL/ORDLBL)
GSC AA                                      EDTCDE(3)
GSC AA            W4CORD    R        O 12 14REFFLD(OHREC/OHCORD *LIBL/ORDH)
GSC AA                                      EDTCDE(3)
GSC AA            W4CUST    R        O 12 22REFFLD(OHREC/OHCUST *LIBL/ORDH)
GSC AA                                      EDTCDE(3)
GSC AA            W4CNAM        30A  O 12 34
GSC AA            W4DELD         6Y 0O 12 65EDTCDE(Y)
GSC AA            W4STAT         7A  O 12 74
GSC AA*
GSC AA          R VW4SFCGS                  SFLCTL(VW4SFLGS)
GSC AA                                      SFLSIZ(0010)
GSC AA                                      SFLPAG(0009)
GSC AA                                      SFLCSRRRN(&SFLRRN)
GSC AA                                      CF06
GSC AA                                      CF10
GSC AA                                      CF11
GSC AA                                      CF16
GSC AA                                      CF17
GSC AA                                      CF18
GSC AA                                      CF20
GSC AA                                      ROLLDOWN
GSC AA                                      ROLLUP
GSC AA                                      OVERLAY
GSC AA  81                                  SFLDSP
GSC AA  82                                  SFLDSPCTL
GSC AA  83                                  SFLCLR
GSC AA            SFLRRN         5S 0H
GSC AA                                 10  8'Doc'
GSC AA                                      DSPATR(HI)
GSC AA                                 10 15'Order'
GSC AA                                      DSPATR(HI)
GSC AA                                 10 23'Customer'
GSC AA                                      DSPATR(HI)
GSC AA                                 10 65'Delivery'
GSC AA                                      DSPATR(HI)
GSC AA                                 11  1'Opt'
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 11  6' Number'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 11 14' Number'
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 11 22'  Number   '
GSC AA                                      DSPATR(UL)
GSC AA                                      DSPATR(HI)
GSC AA                                 11 34'Name                          '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 11 65'  Date  '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA                                 11 74'Status '
GSC AA                                      DSPATR(HI)
GSC AA                                      DSPATR(UL)
GSC AA*
730aAA*****************************************************************
730aAA*
730aAA* Subfile for List Display - View 5
730aAA*
730aAA          R VW5SFL                    SFL
     A  21                                  SFLNXTCHG
     A            HLDI32         1A  H      TEXT('Hold Ind 32 setting')
     A            HLDI33         1A  H      TEXT('Hold Ind 33 setting')
     A            HLDI34         1A  H      TEXT('Hold Ind 34 setting')
     A            OPTION         2A  B 12  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2ITEM        15A  O 12  5
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W5DESC        24A  O 12 21
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W1QYSO         5Y 0O 12 46EDTCDE(L)
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W1UCOD         2A  O 12 53
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W2RTE          5A  O 12 56
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W2OTRN         7Y 0O 12 62EDTCDE(4)
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W5OTRNS        3   O 12 70
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
     A            W2TRN#         7Y 0O 12 74EDTCDE(3)
     A  32                                  COLOR(YLW)
     A  33                                  COLOR(RED)
     A  34                                  COLOR(PNK)
730aAA*
730aAA          R VW5SFC                    SFLCTL(VW5SFL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      SFLCSRRRN(&SFLRRN)
     A                                      CF06
     A                                      CF10
     A                                      CF11
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            SFLRRN         5S 0H
     A                                 11  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11  5'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 21'Description             '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 46'Quantity'
     A                                      DSPATR(HI)
     A                                 11 46'Short/Out'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11 56'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Origin'
     A                                      DSPATR(HI)
     A                                 11 62'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'Org'
     A                                      DSPATR(HI)
     A                                 11 70'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 74'Flag'
     A                                      DSPATR(HI)
     A                                 11 74'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Pop-up Window to assign employee to Flag Transaction.
     A*
     A          R WINDOW1
500dDA*                                     WINDOW(9 20 6 30 *NOMSGLIN)
500dMA                                      WINDOW(9 20 10 30 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT &WDWTITLE) -
     A                                      *CENTER)
     A                                      WDWTITLE((*TEXT 'F5=Refresh  F12=Ca-
     A                                      ncel') *BOTTOM)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A            WDWTITLE      28A  P
     A*
     A*
     A          R WDWFMT1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                  2  2'Flag employee:'
     A            WWEMP#         5Y 0B  2 17EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                  3  2'Print device :'
     A            WWDEV         10A  B  3 17
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
500dAA  40                                  DSPATR(PR)
     A                                  4  2'Form type ...:'
     A            WWFORM        10A  B  4 17
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A*
     A*
     A          R WDWFMT2
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                  2  2'Transaction#:'
     A            WWTRN#         7Y 0B  2 17EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                  3  2'Print device :'
     A            WWDEV         10A  B  3 17
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
500dAA  40                                  DSPATR(PR)
     A                                  4  2'Form type ...:'
     A            WWFORM        10A  B  4 17
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
500dAA*
500dAA*
500dAA          R WDWFMT3
500dAA                                      WINDOW(WINDOW1)
500dAA                                      OVERLAY
500dAA                                  2  2'Flag employee:'
500dAA            WWEMP#         5Y 0B  2 17EDTCDE(4)
500dAA  24                                  DSPATR(RI)
500dAA N24                                  DSPATR(UL)
500dAA  04                                  DSPATR(PC)
500dAA                                  3  2'Print device :'
500dAA            WWDEV         10A  B  3 17
500dAA  25                                  DSPATR(RI)
500dAA N25                                  DSPATR(UL)
500dAA  05                                  DSPATR(PC)
500dAA                                  4  2'Form type ...:'
500dAA            WWFORM        10A  B  4 17
500dAA  26                                  DSPATR(RI)
500dAA N26                                  DSPATR(UL)
500dAA  06                                  DSPATR(PC)
500dAA                                  6  2'Label device :'
500dAA            WWDEV2        10A  B  6 17
500dAA  27                                  DSPATR(RI)
500dAA N27                                  DSPATR(UL)
500dAA  07                                  DSPATR(PC)
500dAA  40                                  DSPATR(PR)
500dAA                                  7  2'Form type ...:'
500dAA            WWFORM2       10A  B  7 17
500dAA  28                                  DSPATR(RI)
500dAA N28                                  DSPATR(UL)
500dAA  08                                  DSPATR(PC)
500dAA*
500dAA*
500dAA          R WDWFMT4
500dAA                                      WINDOW(WINDOW1)
500dAA                                      OVERLAY
500dAA                                  2  2'Transaction#:'
500dAA            WWTRN#         7Y 0B  2 17EDTCDE(4)
500dAA  24                                  DSPATR(RI)
500dAA N24                                  DSPATR(UL)
500dAA  04                                  DSPATR(PC)
500dAA                                  3  2'Print device :'
500dAA            WWDEV         10A  B  3 17
500dAA  25                                  DSPATR(RI)
500dAA N25                                  DSPATR(UL)
500dAA  05                                  DSPATR(PC)
500dAA                                  4  2'Form type ...:'
500dAA            WWFORM        10A  B  4 17
500dAA  26                                  DSPATR(RI)
500dAA N26                                  DSPATR(UL)
500dAA  06                                  DSPATR(PC)
500dAA                                  6  2'Label device :'
500dAA            WWDEV2        10A  B  6 17
500dAA  27                                  DSPATR(RI)
500dAA N27                                  DSPATR(UL)
500dAA  07                                  DSPATR(PC)
500dAA  40                                  DSPATR(PR)
500dAA                                  7  2'Form type ...:'
500dAA            WWFORM2       10A  B  7 17
500dAA  28                                  DSPATR(RI)
500dAA N28                                  DSPATR(UL)
500dAA  08                                  DSPATR(PC)
500dAA*
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    Window Error Message Subfile Record.
     A*
     A          R MSGREC1                   SFL
     A                                      SFLMSGRCD(05)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Window Error Message Subfile Control Record.
     A*
     A          R MSGCTL1                   SFLCTL(MSGREC1)
     A                                      WINDOW(WINDOW1)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR1
     A                                      WINDOW(WINDOW1)
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(05)
     A                                      CLRL(1)
     A*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
500 DA*                                     SLNO(08)
500 MA                                      SLNO(09)
     A                                      OVERLAY
500 DA*                                 1  2'Type options, press Enter.'
500 DA*                                     COLOR(BLU)
500 DA*           OPTLN1        75A  O  2  2COLOR(BLU)
500 MA            OPTLN1        75A  O  1  2COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    1=Select Display Instructions
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Select for Flag Entries'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A*
     A*    4=Delete Display Instructions
     A*
     A          R DELREC3
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete Flag Entries'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12=Cancel to return to chan-
     A                                      ge your choices.'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
