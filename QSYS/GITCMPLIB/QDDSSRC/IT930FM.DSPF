     A*%%TS  SD  19961219  130507  ROCKY       REL-V3R1M0  5763-PW1
     A********************************************************************
     A*
     A* NOTE: THERE ARE 2 SETS OF SCREENS IN THIS FORMAT. ONE SET IS
     A*       FOR USE WITH 8-LINE DISPLAYS AND THE OTHER FOR 16-LINE
     A*       DISPLAYS.
     A*
     A*       IF CHANGES ARE MADE MAKE SURE THEY ARE MADE TO BOTH
     A*       SETS OF SCREENS.
     A*
     A*
     A********************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  IT930FM    RF MFG# Multiple item - Display screen
     A*  06 May, 2019
     A*  Lynn McMahon
     A*
     A*
     A*  Revisions
     A*
730 AA*  05/06/19  LMC  7.30
     A*    - created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF04
     A          R TITLE
     A*%%TS  SD  19961212  143846  DAVE4       REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'IT930.01'
     A            WOMFG         15A  O  1 11
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A*         R POSTNREC
     A*                                     OVERLAY
     A*                                     LOCK
     A*                                     SLNO(03)
     A*           WHCODE         3Y 0O  1  2EDTCDE(3)
     A*                                 1  6'-'
     A*           WHDESC        30A  O  1  8
     A*           FLD002         1A  O  3  2
     A*                                 2  2'PO #   '
     A*           $KPO           9A  O  2 10
     A*                                 2 41'Position to line . . .'
     A*           PTINP1        15A  B  2 65
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19961219  130507  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            TOUCH          1A  O  4  2
     A            OPTION         1A  B  4  4
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ITITEM    R        O  4  6REFFLD(UPREC/UPITEM *LIBL/UPC)
     A            ITDESC    R        O  4 22REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19961219  130507  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  2  2'1=Select '
     A                                      COLOR(BLU)
     A                                  3  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 22'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL                    SFL
     A*%%TS  SD  19961219  130507  ROCKY       REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            TOUCH          1A  O  4  2
     A            OPTION         1A  B  4  4
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            ITITEM    R        O  4  6REFFLD(UPREC/UPITEM *LIBL/UPC)
     A            ITDESC    R        O  4 22REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A*
     A          R D16SFC                    SFLCTL(D16SFL)
     A*%%TS  SD  19961219  130507  ROCKY       REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  2  2'1=Select '
     A                                      COLOR(BLU)
     A                                  3  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 22'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(08)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS (16-Lines)
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R M16REC                    SFL
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(16)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R M16CTL                    SFLCTL(M16REC)
     A*%%TS  SD  19961212  141543  DAVE4       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A  97                                  SFLDSP
     A N97                                  SFLCLR
     A  97                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A          R M16CLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(16)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(20)
     A            OPTLN1        75A  O  1  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(7)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R C16REC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(15)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A*         R MORREC
     A*                                     LOCK
     A*                                     OVERLAY
     A*                                     SLNO(21)
     A*           MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(2)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A*%%TS  SD  19961212  143846  DAVE4       REL-V3R1M0  5763-PW1
     A*                                     SLNO(01)
     A                                      SLNO(02)
     A                                      OVERLAY
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R VERIFY
     A                                      WINDOW(2 1 5 20)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Are you sure you '
     A                                      COLOR(WHT)
     A                                  3  2'want to check as'
     A                                      COLOR(WHT)
     A                                  4  2'touched ?'
     A                                      COLOR(WHT)
     A            WOVRFY         1A  B  4 12COLOR(WHT)
     A                                      CHECK(ER)
