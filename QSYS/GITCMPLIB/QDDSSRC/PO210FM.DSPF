     A*%%TS  SD  19970312  092018  ROCKY       REL-V3R1M0  5763-PW1
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  PO210FM     UPC RECEIVING
     A*  28 JANUARY, 1997
     A*  Roxanne Harrison/David Sommerville
     A*
     A********************************************************************
     A*
     A*
     A*  Revisions
     A*
416 AF*    05/23/02  RRH  4.16
     F*      - Increase W6LCNS to 15.
     A*
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19961127  091617  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.01'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  8  3'Dock number . . . . . . . .'
     A            W1DOCK         3Y 0B  8 32EDTCDE(4)
     A*N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  3'UPC Code  . . . . . . . . .'
     A            W1UPC         20A  B  9 32
218 AA                                      CHECK(ER)
     A*N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  19970312  092018  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.02'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  6  3'Line #'
     A            WOLINE         5Y 0O  6 11EDTCDE(4)
     A                                  6 40'Preferred Tie/High'
     A            WOPTIE         4Y 0O  6 60EDTCDE(4)
     A                                  6 65'/'
     A            WOPHI          2Y 0O  6 67EDTCDE(4)
     A                                  7  3'Item'
     A            WOITEM        65A  O  7 11
     A            WODSC1        40A  O  8 11
     A            WODSC2        40A  O  9 11
     A            WOUM1          6A  O 11  3
     A            W2QT01         5Y 0B 11 12EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A            WOUM2          6A  O 12  3
     A            W2QT02         5Y 0B 12 12EDTCDE(4)
     A  25N92                               DSPATR(RI)
     A  05                                  DSPATR(PC)
     A N25N90N91
     A N92                                  DSPATR(UL)
     A  90
     AO 92                                  DSPATR(PR)
     A            WOUM3          6A  O 13  3
     A            W2QT03         5Y 0B 13 12EDTCDE(4)
     A  30N93                               DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N90N91
     A N93                                  DSPATR(UL)
     A  90
     AO 93                                  DSPATR(PR)
     A                                 14  3'Expiration date . . . . . .'
     A            W2EXDT         6Y 0B 14 31
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  96
     AO 90                                  DSPATR(PR)
     A  96                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 14 50'(MMDDYY)'
     A  96                                  DSPATR(ND)
     A                                 17  3'Catch weight received . .'
     A            W2DWGT         7Y 2B 17 31
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 17 50'Average (Y/N) . . . . . .'
     A            W2AVG          1A  B 17 76
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  99
     AO 90                                  DSPATR(PR)
     A  99                                  DSPATR(ND)
     A                                 23  2'F12=Cancel F20=Accept'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  19970129  154126  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.03'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  6  3'Line #'
     A            WOLINE         5Y 0O  6 11EDTCDE(4)
     A                                  7  3'Item'
     A            WOITEM        65A  O  7 11
     A            WOUM1          6A  O  9  4
     A            WOQT01         5Y 0O  9 13EDTCDE(4)
     A            WOUM2          6A  O 10  4
     A            WOQT02         5Y 0O 10 13EDTCDE(4)
     A            WOUM3          6A  O 11  4
     A            WOQT03         5Y 0O 11 13EDTCDE(4)
219 AA                                 12  4'Total catch weight:'
     A            WOTWGT         9Y 2O 12 24EDTCDE(4)
     A                                 13  4'Pallets:'
     A            WOPLET         5Y 0O 13 13EDTCDE(4)
     A                                 23  1'Press Enter to continue'
     A                                      COLOR(BLU)
     A          R SCREEN4
     A*%%TS  SD  19961127  091617  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.04'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  6  3'UPC:'
     A            WOUPC         20A  O  6 11
     A                                  8  3'LINE  . . . . . . . . . . .'
     A            W4LINE         5Y 0B  8 32EDTCDE(4)
     A*N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  9  3'ITEM  . . . . . . . . . . .'
     A            W4ITEM        15A  B  9 32
     A*N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                 23  2'F4=Prompt F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN5
     A*%%TS  SD  19970129  154126  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.05'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  6  3'Line #'
     A            WOLINE         5Y 0O  6 11EDTCDE(4)
     A                                  7  3'Item'
     A            WOITEM        65A  O  7 11
     A                                  9  4'UPC code'
     A            WOUPC         20A  O  9 16
     A                                 11  4'Verify UPC'
     A            W5UPC         20A  B 11 16CHECK(ER)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN6
     A*%%TS  SD  19970129  154126  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PO210.06'
     A                                  1 35'UPC Receiving'
     A                                  1 72DATE
     A                                  5  3'P.O. '
     A            WOPO           9A  O  5 11
     A                                  6  3'Line #'
     A            WOLINE         5Y 0O  6 11EDTCDE(4)
     A                                  7  3'Item'
     A            WOITEM        65A  O  7 11
     A            W6PCTR         4Y 0O  9  3EDTCDE(4)
     A                                  9  8'of'
     A            W6PAL          4Y 0O  9 11EDTCDE(4)
     A                                  9 16'pallets'
     A                                 10  3'Tie/High'
     A            W6TIE          4Y 0O 10 13EDTCDE(4)
     A                                 10 18'/'
     A            W6HI           2Y 0O 10 20EDTCDE(4)
     A                                 12  3'License . . .'
416aDA*           W6LCNS         7Y 0B 12 18EDTCDE(4)
416a A            W6LCNS        15A  B 12 18
     A                                      CHECK(ER)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
