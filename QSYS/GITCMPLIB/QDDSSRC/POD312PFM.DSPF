     A*%%TS  SD  20080906  083225  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  POD312PFM   View PODP log - View record
     A*  27 October 2008
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    10/27/08  RBD  5.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA09
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080906  083225  DAVE        REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'POD312P.01'
     A                                  1 31'View POD Pallet Log'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  4'Try  . . . . . . . . .'
     A            PDPTRY    R        O  2 27REFFLD(PODPREC/PDPTRY *LIBL/PODLOGP)
     A                                      EDTCDE(3)
     A                                  3  4'Status . . . . . . . .'
     A            PDPSTATUS R        O  3 27REFFLD(PODPREC/PDPSTATUS *LIBL/PODL-
     A                                      OGP)
     A                                  4  4'Code . . . . . . . . .'
     A            PDPERRCODER        O  4 27REFFLD(PODPREC/PDPERRCODE *LIBL/POD-
     A                                      LOGP)
     A                                  5  4'Message  . . . . . . .'
     A            PDPERRMSG R        O  5 27REFFLD(PODPREC/PDPERRMSG *LIBL/PODL-
     A                                      OGP)
     A                                  7  4'Warehouse  . . . . . .'
     A            PDPWHSE   R        O  7 27REFFLD(PODPREC/PDPWHSE *LIBL/PODLOG-
     A                                      P)
     A                                      EDTCDE(3)
     A                                  8  4'Route / Id . . . . . .'
     A            PDPRTE    R        O  8 27REFFLD(PODPREC/PDPRTE *LIBL/PODLOGP)
     A                                  8 33'/'
     A            PDPRTID   R        O  8 35REFFLD(PODPREC/PDPRTID *LIBL/PODLOG-
     A                                      P)
     A                                  9  4'Pallet / Tote Id . . .'
     A            PDPPLID   R        O  9 27REFFLD(PODPREC/PDPPLID *LIBL/PODLOG-
     A                                      P)
     A                                      EDTCDE(3)
     A                                  9 35'/'
     A            PDPUP#    R        O  9 37REFFLD(PODPREC/PDPUP# *LIBL/PODLOG-
     A                                      P)
     A                                      EDTCDE(3)
     A                                 10  4'Stop . . . . . . . . .'
     A            PDPSTOP   R        O 10 27REFFLD(PODPREC/PDPSTOP *LIBL/PODLOG-
     A                                      P)
     A                                      EDTCDE(3)
     A                                 11  4'Order / Id . . . . . .'
     A            PDPCORD   R        O 11 27REFFLD(PODPREC/PDPCORD *LIBL/PODLOG-
     A                                      P)
     A                                      EDTCDE(3)
     A                                 11 39'/'
     A            PDPORD    R        O 11 41REFFLD(PODPREC/PDPORD *LIBL/PODLOGP)
     A                                      EDTCDE(3)
     A                                 12  4'Sent file name . . . .'
     A            PDPFILESNDR        O 12 27REFFLD(PODPREC/PDPFILESND *LIBL/POD-
     A                                      LOGP)
     A                                 13  4'Response file name . .'
     A            PDPFILETRYR        O 13 27REFFLD(PODPREC/PDPFILETRY *LIBL/POD-
     A                                      LOGP)
     A                                 14  4'File Start (Sys/UC)  .'
     A            W1TS1         26A  O 14 27
     A            W1UC1         26A  O 14 55
     A                                 15  4'File End (Sys/UC)  . .'
     A            W1TS2         26A  O 15 27
     A            W1UC2         26A  O 15 55
     A                                 16  4'File user/job  . . . .'
     A            PDPUSER1  R        O 16 27REFFLD(PODPREC/PDPUSER1 *LIBL/PODLO-
     A                                      GP)
     A                                 16 38'/'
     A            PDPJOB1   R        O 16 40REFFLD(PODPREC/PDPJOB1 *LIBL/PODLOG-
     A                                      P)
     A                                 16 51'/'
     A            PDPNBR1   R        O 16 53REFFLD(PODPREC/PDPNBR1 *LIBL/PODLOG-
     A                                      P)
     A                                 17  4'File program . . . . .'
     A            PDPCALL1  R        O 17 27REFFLD(PODPREC/PDPCALL1 *LIBL/PODLO-
     A                                      GP)
     A                                 17 38'/'
     A            PDPPGM1   R        O 17 40REFFLD(PODPREC/PDPPGM1 *LIBL/PODLOG-
     A                                      P)
     A                                 18  4'Send Start (Sys/UC)  .'
     A            W1TS3         26A  O 18 27
     A            W1UC3         26A  O 18 55
     A                                 19  4'Send End (Sys/UC)  . .'
     A            W1TS4         26A  O 19 27
     A            W1UC4         26A  O 19 55
     A                                 20  4'Send user/job  . . . .'
     A            PDPUSER3  R        O 20 27REFFLD(PODPREC/PDPUSER3 *LIBL/PODLO-
     A                                      GP)
     A                                 20 38'/'
     A            PDPJOB3   R        O 20 40REFFLD(PODPREC/PDPJOB3 *LIBL/PODLOG-
     A                                      P)
     A                                 20 51'/'
     A            PDPNBR3   R        O 20 53REFFLD(PODPREC/PDPNBR3 *LIBL/PODLOG-
     A                                      P)
     A                                 21  4'Send program . . . . .'
     A            PDPCALL3  R        O 21 27REFFLD(PODPREC/PDPCALL3 *LIBL/PODLO-
     A                                      GP)
     A                                 21 38'/'
     A            PDPPGM3   R        O 21 40REFFLD(PODPREC/PDPPGM3 *LIBL/PODLOG-
     A                                      P)
     A                                 22  4'Status (Sys/UC)  . . .'
     A            W1TS9         26A  O 22 27
     A            W1UC9         26A  O 22 55
     A                                 23  2'F3=Exit  F5=Refresh  F7=View sent -
     A                                      file  F9=View response file  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
