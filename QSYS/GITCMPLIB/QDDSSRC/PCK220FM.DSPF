     A*%%TS  SD  20110729  102122  DAVE        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PCK220FM   Create random test orders for warehouse 900.
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416 AA*    07/17/02  DAS  4.16
     A*      - Added FTP option.
     A*      - Added 'S' type.
     A*
640aAA*    07/21/11  DAS  6.40
     A*      - Added Batch Pick option
     A*
640bAA*    07/29/11  DAS  6.40b
     A*      - Changed FTP option into an Import Method option.
     A*
650aAA*    10/28/13  DAS  6.50a
     A*      - Added warehouse
     A*
700aAA*    01/30/16  DAS  7.00a
     A*      - Added AMP pick option
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A          R SCREEN1
     A*%%TS  SD  20110729  102122  DAVE        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PCK220.01'
     A                                  1 19'Create Random Test Orders for Ware-
     A                                      house 900'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A**                                3  2'Type changes, press Enter.'
     A**                                    COLOR(BLU)
     A                                  3  4'Route  . . . . . . . . . . .'
     A            W1RTE          5A  B  3 34
     A                                  4  4'Truck  . . . . . . . . . . .'
     A            W1TRUK        10A  B  4 34
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  4'Import method  . . . . . . .'
     A            W1FTP          1A  B  5 34
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                  5 44'F=FTP (PCKTEST), S=Staging'
     A                                  6  4'Order type . . . . . . . . .'
     A            W1TYPE         1A  B  6 34
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                  6 44''' ''=Normal, ''S''=Single, ''W''=-
     A                                      Willcall'
     A                                  7  4'Batch pick . . . . . . . . .'
     A            W1BPCK         1A  B  7 34
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                  7 44'Y=Yes, N=No'
     A                                  8  4'AMP pick . . . . . . . . . .'
     A            W1APCK         1   B  8 34
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28                                  DSPATR(UL)
     A  08                                  DSPATR(PC)
     A            W1ACOD         3   B  8 36
     A                                  8 44'Y=Yes, N=No'
     A                                  8 56'(hmmsc4 pos 13)         '
     A                                  9  4'Items per order  . . . . . .'
     A                                  9 34'Min'
     A            W1ITM1         3Y 0B  9 38EDTCDE(4)
     A                                  9 43'Max'
     A            W1ITM2         3Y 0B  9 47EDTCDE(4)
     A                                 10  4'Pieces per item  . . . . . .'
     A                                 10 34'Min'
     A            W1PCS1         3Y 0B 10 38EDTCDE(4)
     A                                 10 43'Max'
     A            W1PCS2         3Y 0B 10 47EDTCDE(4)
     A                                 11  4'Maximum comparment cube  . .'
     A                                 11 34'Dry'
     A            W1DRY          5Y 0B 11 38EDTCDE(4)
     A                                 11 45'Ref'
     A            W1REF          5Y 0B 11 49EDTCDE(4)
     A                                 11 56'Frz'
     A            W1FRZ          5Y 0B 11 60EDTCDE(4)
     A                                 12  4'Populate WORKLBL file  . . .'
     A            W1LBL          1A  B 12 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 44'Y=Yes, N=No'
     A                                 13  4'Place on job queue . . . . .'
     A            W1JOBQ         1A  B 13 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 44'Y=Yes, N=No'
650aAA                                 14  4'Warehouse  . . . . . . . . .'
650aAA            W1WHSE         3Y 0B 14 34EDTCDE(4)
     A                                 16  4'This program will create random or-
     A                                      ders for the above route. The recor-
     A                                      ds'
     A                                 17  4'will be written to IMPORDH and IMP-
     A                                      ORDD. The number of items in each o-
     A                                      rder'
     A                                 18  4'and quantity for each item is rand-
     A                                      om. Orders are created until compar-
     A                                      tments'
     A                                 19  4'are filled. The items are randomly-
     A                                       selected from the WORKLBL file. Th-
     A                                      is'
     A                                 20  4'should be populated after 1) It ha-
     A                                      s been cleared or 2) Changes have b-
     A                                      een'
     A                                 21  4'made to the slot file.'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh  F9-
     A                                      =Calculate cube from truck  F12=Can-
     A                                      cel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
