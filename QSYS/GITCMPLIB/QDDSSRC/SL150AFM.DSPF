     A*%%TS  SD  20021028  141213  ROCKY       REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL150AFM   Verify zero slots (one whse) - Display
     A*  21 July 1994
     A*  Dave Sommerville
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
416  A*    06/12/02  RH   4.16
     A*      - Add F9 to verify any multiple virtual slots.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA04
     A                                      CA09
     A          R TITLE
     A*%%TS  SD  19941014  075724  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'SL150A.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 32'Verify Zero Slots'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940811  153641  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  3 41'Change to department  . .'
     A            PTINP2         5A  B  3 68
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A                                  2 41'Change to warehouse . . .'
     A            PTINP1         3Y 0B  2 68EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WOWHSE         3A  O  2  2
     A                                  2  8'-'
     A            WHDESC        30A  O  2 10
     A            FLD001         1A  O  1  2
     A            WDCODE         5A  O  3  2
     A                                  3  8'-'
     A            WDDESC        30A  O  3 10
     A                                  4 41'Position to slot  . . . .'
     A            PTINP3        12A  B  4 68
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940811  151313  DAVE        REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLSTYP    R        O 11 24REFFLD(SLREC/SLSTYP *LIBL/SLOT)
     A            SLDISP    R        O 11  7REFFLD(SLREC/SLDISP *LIBL/SLOT)
     A            SLDESG    R        O 11 39REFFLD(SLREC/SLDESG *LIBL/SLOT)
     A            SLACTV    R        O 11 52REFFLD(SLREC/SLACTV *LIBL/SLOT)
     A            SLSDEF    R        O 11 31REFFLD(SLREC/SLSDEF *LIBL/SLOT)
     A            SLSTAT    R        O 11 74REFFLD(SLREC/SLSTAT *LIBL/SLOT)
     A            SLBLD     R        O 11 61REFFLD(SLREC/SLBLD *LIBL/SLOT)
     A            SLPICK    R        O 11 68REFFLD(SLREC/SLPICK *LIBL/SLOT)
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19940811  151313  DAVE        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            FLD001         1A  O  9  2
     A                                 10  7'Display     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'Section'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 36'Area       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 49'Zone    '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 59'Build'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 66'Pick'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 72'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 36'Designation'
     A                                      DSPATR(HI)
     A                                  9 49'Activity'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
416b A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20021028  141213  ROCKY       REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLDISP    R        O 11  7REFFLD(SLREC/SLDISP *LIBL/SLOT)
     A            SLITEM    R        O 11 21REFFLD(SLREC/SLITEM *LIBL/SLOT)
     A            W1DESC        30A  O 11 38
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20021028  141213  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A            FLD002         1A  O  9  3
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Display     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 38'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940811  113933  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(07)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A*%%TS  SD  19941014  082552  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Verify.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
