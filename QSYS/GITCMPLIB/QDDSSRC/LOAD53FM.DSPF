     A*%%TS  SD  20081230  101452  BFCTEST1    REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*  LOAD53FM  Truck Load Module - Trailer Equipment Detail
     A*  October 30, 2008
     A*  Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/15/11  JGD  6.40
     F*      - Created
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A*         Loading trailer equipment
     A*----------------------------------------------------------------
     A          R WINDW1
     A*%%TS  SD  20081230  101452  BFCTEST1    REL-V5R4M0  5722-WDS
     A                                      WINDOW(1 2 21 72)
     A  98                                  ALARM
     A                                      CLRL(20)
     A                                      OVERLAY
     A*
     A                                  2  2'Loadr53'
     A*
     A                                  2 23'Trailer Equipment Detail Entry'
     A                                      DSPATR(HI)
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 11DSPATR(HI)
     A*
     A                                  4 19'LoaderID:'
     A            W1LDR#         5S 0O  4 29DSPATR(HI)
     A*
     A                                  4 36'Loader:'
     A            W1USER        28A  O  4 44DSPATR(HI)
     A*
     A                                  5  2'Door #:'
     A            W1DOOR         3A  O  5 11DSPATR(HI)
     A*
     A                                  5 19'Trailer #:'
     A            W1TRL#         8A  O  5 30DSPATR(HI)
     A*
     A                                  5 40'Date:'
     A            W1DATE         6Y 0O  5 46DSPATR(HI)
     A                                      EDTCDE(Y)
     A*
     A                                  5 57'Time:'
     A            W1STRT         6Y 0O  5 63DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  6  2'Route Driver:'
     A            W1DNAM        22A  O  6 16DSPATR(HI)
     A*
     A                                  6 40'Empl #:'
CBI AA            W1EMP#         4Y 0B  6 48
CBI AA N20N90                               DSPATR(HI)
CBI AA  20                                  DSPATR(RI)
CBI AA N20N90                               DSPATR(UL)
CBI AA  10                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A*
     A                                  7 55'Quantity'
     A                                  8  2'Loaded         '
     A                                      DSPATR(UL)
     A*
     A                                  8 27'Unloaded       '
     A                                      DSPATR(UL)
     A*
     A                                  8 53'Short'
     A                                      DSPATR(UL)
     A*
     A                                  8 61'Over '
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A*            LOADER TRAILER EQUIPMENT SCREEN
     A* (Fields names ending in '1' and '2' identify 'Loaded' and
     A* 'Unloaded' columns respectively on screen).
     A*----------------------------------------------------------------
     A                                  9  2'Blankets:'
     A            W1BK1          4A  B  9 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                  9 27'Blankets:'
     A            W1BK2          4A  B  9 43
CBI AA N21N90                               DSPATR(HI)
CBI AA  21                                  DSPATR(RI)
CBI AA N21N90                               DSPATR(UL)
CBI AA  01                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO1          4A  B  9 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO1B         4A  B  9 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 10  2'Bulk Heads:'
     A            W1BH1          4A  B 10 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 10 27'Bulk Heads:'
     A            W1BH2          4A  B 10 43
CBI AA N22N90                               DSPATR(HI)
CBI AA  22                                  DSPATR(RI)
CBI AA N22N90                               DSPATR(UL)
CBI AA  02                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO2          4A  B 10 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO2B         4A  B 10 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 11  2'Load Bars:'
     A            W1LB1          4A  B 11 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 11 27'Load Bars:'
     A            W1LB2          4A  B 11 43
CBI AA N23N90                               DSPATR(HI)
CBI AA  23                                  DSPATR(RI)
CBI AA N23N90                               DSPATR(UL)
CBI AA  03                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO3          4A  B 11 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO3B         4A  B 11 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 12  2'IC Bins-Small:'
     A            W1ICS1         4A  B 12 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 12 27'IC Bins-Small:'
     A            W1ICS2         4A  B 12 43
CBI AA N24N90                               DSPATR(HI)
CBI AA  24                                  DSPATR(RI)
CBI AA N24N90                               DSPATR(UL)
CBI AA  04                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO4          4A  B 12 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO4B         4A  B 12 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 13  2'IC Bins-Medium:'
     A            W1ICM1         4A  B 13 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 13 27'IC Bins-Medium:'
     A            W1ICM2         4A  B 13 43
CBI AA N25N90                               DSPATR(HI)
CBI AA  25                                  DSPATR(RI)
CBI AA N25N90                               DSPATR(UL)
CBI AA  05                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO5          4A  B 13 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO5B         4A  B 13 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 14  2'IC Bins-Large:'
     A            W1ICL1         4A  B 14 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 14 27'IC Bins-Large:'
     A            W1ICL2         4A  B 14 43
CBI AA N26N90                               DSPATR(HI)
CBI AA  26                                  DSPATR(RI)
CBI AA N26N90                               DSPATR(UL)
CBI AA  06                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO6          4A  B 14 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO6B         4A  B 14 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 15  2'SF Bins:'
     A            W1SFB1         4A  B 15 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 15 27'SF Bins:'
     A            W1SFB2         4A  B 15 43
CBI AA N27N90                               DSPATR(HI)
CBI AA  27                                  DSPATR(RI)
CBI AA N27N90                               DSPATR(UL)
CBI AA  07                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO7          4A  B 15 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO7B         4A  B 15 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 16  2'Staged Pallet:'
     A            W1SPL1         4A  B 16 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 16 27'Staged Pallet:'
     A            W1SPL2         4A  B 16 43
CBI AA N28N90                               DSPATR(HI)
CBI AA  28                                  DSPATR(RI)
CBI AA N28N90                               DSPATR(UL)
CBI AA  08                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO8          4A  B 16 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO8B         4A  B 16 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 17  2'Total Pallets:'
     A            W1TPL1         4A  B 17 18DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A                                 17 27'Total Pallets:'
     A            W1TPL2         4A  B 17 43
CBI AA N29N90                               DSPATR(HI)
CBI AA  29                                  DSPATR(RI)
CBI AA N29N90                               DSPATR(UL)
CBI AA  09                                  DSPATR(PC)
CBI AA  90                                  DSPATR(PR)
     A*
     A            W1SO9          4A  B 17 53DSPATR(HI)
     A                                      DSPATR(PR)
     A*
     A            W1SO9B         4A  B 17 61DSPATR(HI)
     A                                      DSPATR(PR)
     A*----------------------------------------------------------------
     A                                 19  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A                                 19 12'F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A                                 19 25'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A            ERRMSG        50A  O 20  2DSPATR(HI)
     A*----------------------------------------------------------------
     A*         Verification window
     A*----------------------------------------------------------------
     A          R WINDW2
     A*%%TS  SD  20060627  164443  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(19 52 2 24 *MSGLIN)
     A*                                     WDWBORDER((*COLOR PNK) (*CHAR '    -
     A*                                         '))
     A*
     A                                  1  1'COMFIRM UPDATE:'
     A*                                     COLOR(PNK)
     A*
     A            YN             1A  B  1 17
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A  70                                  ERRMSG('Wrong value entered.')
     A
     A                                  1 19'(Y/N)'
     A*                                     COLOR(PNK)
     A*                                     DSPATR(HI)
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
