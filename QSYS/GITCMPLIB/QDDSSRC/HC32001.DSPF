     A*%%TS  SD  20021101  142428  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  HC320FM   History HACCP report
     A*  23 April, 2008
     A*  Roxanne Harrison
     A*
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*  04/23/08  RH   5.20
     A*   - Created using HC310
     A*
530 AA*  01/23/10  MLB  5.30
     A*   - Enh: Added from/to date range selection to report.
640aAA*  09/30/11  RH   6.40a
     A*   - Enh: Added PO Vend # selection to report.
     A*     Rename format to HC32001
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R SCREEN1
     A*%%TS  SD  20021101  142428  HEMANT      REL-V4R2M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'HC320.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'PO number  . . . . . . . . .'
     A            W1PO           9   B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
530 AA                                  9 53'P/O# or *ALL'
530 AA                                 10  4'Date range . . . . . . . . .'
530 AA            W1FMDT         6Y 0B 10 34EDTCDE(4)
530 AA N28N90                               DSPATR(HI)
530 AA  28                                  DSPATR(RI)
530 AA N28N90                               DSPATR(UL)
530 AA  08                                  DSPATR(PC)
530 AA  90                                  DSPATR(PR)
530 AA                                 10 42'to'
530 AA            W1TODT         6Y 0B 10 46EDTCDE(4)
530 AA N29N90                               DSPATR(HI)
530 AA  29                                  DSPATR(RI)
530 AA N29N90                               DSPATR(UL)
530 AA  09                                  DSPATR(PC)
530 AA  90                                  DSPATR(PR)
530 AA                                 10 53'MMDDYY'
640aAA                                 11  4'PO Vendor #  . . . . . . . .'
640aAA            W1VEN         11Y 0B 11 34EDTCDE(4)
640aAA N23N90                               DSPATR(HI)
640aAA  23                                  DSPATR(RI)
640aAA N23N90                               DSPATR(UL)
640aAA  03                                  DSPATR(PC)
640aAA  90                                  DSPATR(PR)
     A*                                12  4'View or print  . . . . . . .'
     A*           W1VP           1A  B 12 34
     A*N24N90                               DSPATR(HI)
     A* 24                                  DSPATR(RI)
     A*N24N90                               DSPATR(UL)
     A* 04                                  DSPATR(PC)
     A* 90                                  DSPATR(PR)
     A*                                12 46'V=View, P=Print'
     A                                 13  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B 13 34
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 13 46'Y=Yes, N=No'
     A                                 14  4'Printer device . . . . . . .'
     A            W1DEV         10A  B 14 34
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15  4'Form type  . . . . . . . . .'
     A            W1FORM        10A  B 15 34
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
