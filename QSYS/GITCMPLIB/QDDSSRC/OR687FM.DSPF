     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR687FM
     A*  04 April, 2006
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
500 AA*    04/04/06  RH   5.00
     A*      - Created
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCREEN1 and SCREEN2
     A*      - Both of the above added for api program APICVTSLT
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  3'OR687.01'
     A                                  1 16'Bulk Drop '
     A                                      DSPATR(HI)
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A*                                 2  4'Type changes, press Enter.'
     A*                                     COLOR(BLU)
     A                                  2  3'Warehouse:'
     A            W1WHSE         3Y 0O  2 14EDTCDE(4)
     A                                  2 21'Dept:'
     A            W1WHDP         5A  O  2 27
     A                                  3  2'Slot:'
     A            W1DISP        12A  O  3  8
     A                                  3 21'Item:'
     A            W1ITEM        15A  O  3 27
     A                                  4  2'Truck:'
     A            W1TRUK         5A  O  4  9
     A                                  4 22'Plt#'
     A            W1UNLD         2Y 0O  4 27
     A                                  4 30'Rte#'
     A            W1RTE          5A  O  4 35
     A                                  5  3'Drop (Y/N):'
     A            W1DROP         1A  B  5 15
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 21'Qty:'
     A            W1DRPQ         7Y 0O  5 27EDTCDE(3)
     A                                  7  3'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'OR687.02'
     A                                  1 12'Bulk Drop'
     A                                      DSPATR(HI)
     A                                  2  2'Whse'
     A            W2WHSE         3Y 0O  2  7EDTCDE(4)
     A                                  2 11'Dept.'
     A            W2WHDP         5A  O  2 17
     A                                  3  2'Item'
     A            W2ITEM        15A  O  3  7
     A                                  7  2'F12=Cancel '
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
