     A*%%TS  SD  20000622  142739  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
    AA* ***************************************************************
    AA* *****  If the position of any slot entry field is changed *****
    AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
    AA* *****  program.                                           *****
    AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OR685FM   RF Bulk Drop - View RF Screen
     A*  03 April, 2006
     A*  Roxanne Harrison
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCREEN1 and SCREEN2
     A*      - Both of the above added for api program APICVTSLT
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000622  142739  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A****                                  CLRL(23)
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'OR685.01'
     A                                  1 12'RF Bulk Drop'
     A                                      DSPATR(HI)
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Warehouse . . .'
     A            W1WHSE         3Y 0B  5 19
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  6  2'Department  . .'
     A            W1WHDP         5A  B  6 19
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  3'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000622  142739  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'OR685.02'
     A                                  1 12'Bulk Drop'
     A                                      DSPATR(HI)
     A                                  2  2'Whse'
     A            W2WHSE         3Y 0O  2  7EDTCDE(4)
     A                                  2 11'Dept.'
     A            W2WHDP         5A  O  2 17
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
