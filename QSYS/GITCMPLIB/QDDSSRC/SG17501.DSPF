     A*%%TS  SD  20140410  230055  RODR        REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG175FM    Staging Export View Purchase Order Header
     A*  Rod Roller
     A*
     A*  Revisions
     A*
700aAA*    08/12/16  RTR  7.00a
     A*      - Added prompt for RP=Reset PO option.
     A*      - Changed to SG17501.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140410  225530  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            PEHPO     R        O  8  9REFFLD(PEHREC/PEHPO *LIBL/EXPSPOH)
     A            PEHVEN    R        O  8 19REFFLD(PEHREC/PEHVEN *LIBL/EXPSPOH)
     A                                      EDTCDE(Z)
     A            S1NAME        30A  O  8 31
     A            PEHDTE    R        O  8 62REFFLD(PEHREC/PEHDTE *LIBL/EXPSPOH)
     A                                      EDTCDE(Y)
     A            PEHTIM    R        O  8 71REFFLD(PEHREC/PEHTIM *LIBL/EXPSPOH)
     A                                      EDTWRD('0  :  ')
     A            PEHAMP    R        O  8 77REFFLD(PEHREC/PEHAMP *LIBL/EXPSPOH)
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140410  225530  RODR        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 25'View Purchase Order Header Info'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'   PO#   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 19'  Vendor#  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 31'         Vendor Name          '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 62'Deliv Date/Time '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit  F5=Refresh  -
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20140410  230055  RODR        REL-V6R1M0  5761-WDS
     A  98                                  ALARM
     A                                  1  2'SG175.02'
     A                                  1 25'View Purchase Order Header Info'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 29'Full Header Information'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            PEHWHS    R        O  5 18REFFLD(PEHREC/PEHWHS *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            PEHBATID  R        O  5 54REFFLD(PEHREC/PEHBATID *LIBL/EXPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  6  2'PO #  . . . . .'
     A            PEHPO     R        O  6 18REFFLD(PEHREC/PEHPO *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                  6 34'Added Timestamp . .'
     A            PEHADDTS  R        O  6 54REFFLD(PEHREC/PEHADDTS *LIBL/EXPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  7 34'Status Timestamp  .'
     A            PEHSTSTS  R        O  7 54REFFLD(PEHREC/PEHSTSTS *LIBL/EXPSPO-
     A                                      H)
     A                                      DSPATR(HI)
     A                                  9  2'Status  . . . .'
     A            PEHSTATUS R        O  9 18REFFLD(PEHREC/PEHSTATUS *LIBL/EXPSP-
     A                                      OH)
     A                                      DSPATR(HI)
     A            PEHTEXT   R        O  9 24REFFLD(PEHREC/PEHTEXT *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 13  2'Vendor  . . . .'
     A            PEHVEN    R        O 13 18REFFLD(PEHREC/PEHVEN *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            PEHVNM    R        O 13 30REFFLD(PEHREC/PEHVNM *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 14  2'Delivery D/T  .'
     A            PEHDTE    R        O 14 18REFFLD(PEHREC/PEHDTE *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            PEHTIM    R        O 14 25REFFLD(PEHREC/PEHTIM *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A            PEHAMP    R        O 14 30REFFLD(PEHREC/PEHAMP *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 16  2'Misc 1  . . . .'
     A            PEHMM1    R        O 16 18REFFLD(PEHREC/PEHMM1 *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 17  2'Misc 2  . . . .'
     A            PEHMM2    R        O 17 18REFFLD(PEHREC/PEHMM2 *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 18  2'Misc 3  . . . .'
     A            PEHMM3    R        O 18 18REFFLD(PEHREC/PEHMM3 *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 19  2'Misc 4  . . . .'
     A            PEHMM4    R        O 19 18REFFLD(PEHREC/PEHMM4 *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 20  2'Misc 5  . . . .'
     A            PEHMM5    R        O 20 18REFFLD(PEHREC/PEHMM5 *LIBL/EXPSPOH)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
700aAA          R WINDOW2
700aAA                                      WINDOW(7 27 6 24)
700aAA                                      CA08
700aAA                                  2  1'Are you sure you want to'
700aAA                                      DSPATR(HI)
700aAA  41N42                           3  5'reset this PO?'
700aAA                                      DSPATR(HI)
700aAA  42N41                           3  3'clear these errors?'
700aAA                                      DSPATR(HI)
700aAA  41N42                           5  2'F8=Reset'
700aAA                                      COLOR(BLU)
700aAA  42N41                           5  2'F8=Clear'
700aAA                                      COLOR(BLU)
700aAA                                  5 14'F12=Cancel'
700aAA                                      COLOR(BLU)
700aAA          R WINDOW3
700aAA                                      CA08
700aAA                                      WINDOW(7 25 6 28)
700aAA                                  2  2'You are not authorized to'
700aAA                                      DSPATR(HI)
700aAA                                      COLOR(RED)
700aAA                                  3  7'use this option.'
700aAA                                      DSPATR(HI)
700aAA                                      COLOR(RED)
700aAA                                  5  2'Press any key to continue.'
700aAA                                      COLOR(BLU)
