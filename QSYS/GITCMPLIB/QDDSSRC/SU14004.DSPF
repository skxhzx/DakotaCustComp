     A*%%TS  SD  20010417  082213  DAVE3       REL-V4R4M0  5769-PW1
415aAA* ***************************************************************
415aAA* *****  If the position of any slot entry field is changed *****
415aAA* *****  you must change the WOROW/WOCOL values in the RPG  *****
415aAA* *****  program.                                           *****
415aAA* ***************************************************************
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  SU14001 Re-name  RF Set slot item/qty
     A*  SU140FM      RF Set slot item/qty
     A*  14 July, 1999
     A*  Roxanne Harrison
     A*
     A*  Notes:
     A*       Created using SL160
     A*       8 X 20 Screen for hand held scanners
     A*       Program to be used for start up only.
     A*
     A********************************************************************
     A*
     A*
     A*  Program Modification History
     A*
413 AA*    12/13/99  RH
     A*      - Position cursor to W1SCAN after first time.
     A*
415 AA*    03/28/01  DAS  4.15
     A*      - Changed SCREEN2 to SCREEN99 for history.
     A*      - Created new SCREEN2 to get item number either by
     A*        entering UPC, item number or home slot.
     A*      - Created new SCREEN3 to get quantities.
     A*
415aAA*    03/29/01  DAS  4.15a
     A*      - Added WOROW/WOCOL for positioning in SCREEN1.
     A*
415bAA*    04/17/01  DAS  4.15b
     A*      - Added W1SCAN to SCREEN2 & 3. Moved existing lines
     A*        down accordingly.
     A*
500 AA*    01/10/05  DAS  5.00
     A*      - Added W2LIC to SCREEN2, conditioned by ind 81 because
     A*        it is only displayed as a custom change.
     A*
600 AA*    04/28/08  DAS  6.00  P#00046
     A*     - License Tracking
     A*
600aAA*    04/28/08  DAS  6.00a P#00046
     A*      - Added SCREEN4 for capturing expiration date.
     A*      - Was 500 change in Jordano's version.
     A*
530a A*    01/28/10  GJA  5.30a
     A*      - Added MFR item# to screen 2.
610a A*    11/03/10  GJA  6.10a
     A*      - Added ability to collect pack date.
610b A*    12/28/10  RH   6.10b
     A*      - Rename SU140FM to SU14001 for 610 modifications.
650a A*    09/02/14  RH   6.50a
     A*      - Rename SU14001 to SU14002 to capture UPC code.
700a A*    11/02/16  LMC  7.00a
     A*      - Rename SU14002 to SU14003 to capture breakdown UPC.
740a A*    03/16/20  RH   7.40a
     A*      - Rename SU14003 to SU14004 Add W2FLD1 for Lic/Slt field
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20010417  082213  DAVE3       REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'SU140.01'
     A                                  1 12'Set Slot Item/qty'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  8EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept:'
     A            W1WHDP         5A  B  4  8
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A N99                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Scan:'
     A            W1SCAN        12A  B  5  8CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AO 99                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  20010417  082213  DAVE3       REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CA03
     A                                      CA12
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SU140.02'
     A                                  1 12'Set Slot Item/qty'
     A                                      DSPATR(HI)
     A            W1SCAN        12   O  2  2
740aDA***                               3  1'Lic:'
740aDA***                               3  1'Slt:'
74oaMA            W2FLD1         4A  O  3  1
500 AA N81                                  DSPATR(ND)
500 AA            W2LIC         15A  B  3  6CHECK(ER)
500 AA  23                                  DSPATR(RI)
500 AA N23N90                               DSPATR(UL)
500 AA  03                                  DSPATR(PC)
500 AA  90
500 AAON81                                  DSPATR(PR)
500 AA N81                                  DSPATR(ND)
     A                                  4  1'UPC:'
     A            W2UPC         50A  B  4  6CHECK(ER)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  1'Itm:'
     A            W2ITEM        15A  B  5  6CHECK(ER)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  90                                  DSPATR(PR)
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
530aAA                                  6  1'MFR:'
530aAA            W2MFRI        15A  B  6  6CHECK(ER)
530aAA  24                                  DSPATR(RI)
530aAA  04                                  DSPATR(PC)
530aAA N24N90                               DSPATR(UL)
530aAA  90                                  DSPATR(PR)
     A          R SCREEN3
     A*%%TS  SD  20010417  082213  DAVE3       REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SU140.03'
     A                                  1 12'Set Slot Item/qty'
     A                                      DSPATR(HI)
     A            WOITEM        65A  O  3  1
     A            WOUM1          5A  O  4  1
     A            W2QT01         5Y 0B  4  7EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
     A            WOUM2          5A  O  5  1
     A  95                                  DSPATR(ND)
     A            W2QT02         3Y 0B  5  7EDTCDE(4)
     A  25                                  DSPATR(RI)
     A  95                                  DSPATR(ND)
     A N25N95                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  95                                  DSPATR(PR)
     A            WOUM3          5A  O  6  1
     A  96                                  DSPATR(ND)
     A            W2QT03         3Y 0B  6  7EDTCDE(4)
     A  26                                  DSPATR(RI)
     A  96                                  DSPATR(ND)
     A N26N96                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  96                                  DSPATR(PR)
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
     A            W1SCAN        12   O  2  2
     A          R SCREEN4
     A*%%TS  SD  20050330  133056  DAVE        REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SU140.04'
     A                                  1 12'Set Slot Item/qty'
     A                                      DSPATR(HI)
     A            W1SCAN        12A  O  2  2
     A            WOITEM        65A  O  3  1
610bMA            WODTE          9A  O  5  1
610bDA*                                 5  1'Exp Dte:'
610bMA            W4EXPD         6Y 0B  5 11EDTCDE(4)
610bDA*           W4EXPD         6Y 0B  5 10EDTCDE(4)
     A  24                                  DSPATR(RI)
     A  04                                  DSPATR(PC)
     A N24                                  DSPATR(UL)
610bMA                                  5 20'MDY'
610bDA*                                 5 17'MDY'
     A                                  7  1'F12=Cancel'
     A                                      COLOR(BLU)
650a A          R SCREEN5
650a A*%%TS  SD  20050330  133056  DAVE        REL-V5R3M0  5722-WDS
650a A                                      CA03
650a A                                      CA12
650aAA                                      CF10
650a A  98                                  ALARM
650a A                                      CLRL(07)
650a A                                      OVERLAY
650a A                                  1  2'SU140.05'
650a A                                  1 12'Set Slot Item/qty'
650a A                                      DSPATR(HI)
650a A            W1SCAN        12A  O  2  2
650a A            WOITEM        65A  O  3  1
650a A                                  5  1'UPC:'
650a A            W5UPC         50   B  5  6
650a A  29                                  DSPATR(RI)
650a A N29N90                               DSPATR(UL)
650a A  09                                  DSPATR(PC)
650a A  90                                  DSPATR(PR)
650a A                                  7  1'F12=Cancel F10=WOvr'
650a A                                      COLOR(BLU)
415 DA**        R SCREEN2
415 MA          R SCREEN99
     A*%%TS  SD  19990820  152013  ROCKY       REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'SU140.02'
     A                                  1 12'Set Slot Item/qty'
     A                                  4  1'Itm:'
     A            W2ITEM        15A  B  4  6
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  1'Qty:'
     A            W2QT01         5Y 0B  5  6EDTCDE(M)
     A  26                                  DSPATR(RI)
     A  06                                  DSPATR(PC)
     A N26                                  DSPATR(UL)
     A            W2QT02         3Y 0B  5 13EDTCDE(M)
     A  27                                  DSPATR(RI)
     A  07                                  DSPATR(PC)
     A N27                                  DSPATR(UL)
     A            W2QT03         3Y 0B  5 18EDTCDE(M)
     A  28                                  DSPATR(RI)
     A  08                                  DSPATR(PC)
     A N28                                  DSPATR(UL)
     A                                  7  2'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
700aAA          R SCREEN5B
700aAA*%%TS  SD  20161004  161650  LYNNM       REL-V7R2M0  5770-WDS
700aAA                                      CA03
700aAA                                      CA12
700aaA  98                                  ALARM
700aAA                                      CLRL(07)
700aAA                                      OVERLAY
700aAA                                  1  2'SU140.05B'
700aAA                                  1 12'Set Slot Item/qty'
700aAA                                      DSPATR(HI)
700aAA            W1SCAN        12A  O  2  2
700aAA            WOITEM        65A  O  3  1
700aAA            W5BUPC1       50A  B  5  6
700aAA  15                                  DSPATR(RI)
700aAA N15N90                               DSPATR(UL)
700aAA  15                                  DSPATR(PC)
700aAA  90
700aAAO 38                                  DSPATR(PR)
700aaA  38                                  DSPATR(ND)
700aAA                                  7  2'F12=Cancel'
700aAA                                      COLOR(BLU)
700aAA                                  2 12'Breakdown UPC Collection'
700aAA                                      DSPATR(HI)
700aAA            W5BUPC2       50A  B  6  6
700aaA  16                                  DSPATR(RI)
700aAA N16N90                               DSPATR(UL)
700aAA  16                                  DSPATR(PC)
700aAA  90
700aaAO 39                                  DSPATR(PR)
700aAA  39                                  DSPATR(ND)
700aAA            W5BUOM1        2   B  5  2DSPATR(PR)
700aAA  38                                  DSPATR(ND)
700aAA            W5BUOM2        2   B  6  2DSPATR(PR)
700aAA  39                                  DSPATR(ND)
