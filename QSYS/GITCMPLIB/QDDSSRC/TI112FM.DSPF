     A*%%TS  SD  20040505  162428  HEMANT      REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  TI112FM   slot Tie/High - RF Mini Screen
     A*  05 May, 2004
     A*  Hemant Kapadia
     A*
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20040505  162428  HEMANT      REL-V5R2M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'TI112.01'
     A                                  1 12'Tie/High'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  8
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'Dept.'
     A            W1WHDP         5A  B  4  8
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Slot '
     A            W1DISP        12A  B  5  8CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'TI112.02'
     A                                  1 12'Tie/High'
     A                                      DSPATR(HI)
     A                                  2  2'Slot '
     A            W1DISP        12A  O  2  8
     A                                  4  2'Slot type:'
     A            W2SDEF         2   B  4 13
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Tie/Hi:'
     A            W2TIE          3Y 0B  5 10EDTCDE(4)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 14'/'
     A            W2HIGH         3Y 0B  5 16EDTCDE(4)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7  2'F12=Cancel  F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
