     A*%%TS  SD  20010115  153528  KENNETH     REL-V4R2M0  5769-PW1
     A*****************************************************************
     A*
     A*  SCD322FM  View Slots with Check Digits
     A*  07 January 2001
     A*  Kenneth Elder
     A*
     A*  Revisions:
     A*
417 AA*    04/15/04  DAS  4.17
     A*      - Changed size of WSHEAD to 50.
510 AA*    09/12/07  RBD  5.10
     A*      - Revise to use field name changes from SCD321.
     A*        SCDWHS changed to SWKWHS
     A*        SCDWHD changed to SWKWHD
     A*        SCDDSP changed to SWKDSP
     A*        SCDDGT changed to SWKDGT
640A A*    06/21/12  LMC  6.40A
     A*      - Revise to allow for 5 pos check digit. Use different
     A*        subfile if options chkdgt field OP5Num  = 'Y'
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010107  181620  KENNETH     REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 28'Slots with Check Digits'
     A                                      DSPATR(HI)
     A                                  1  2'SCD322'
417 DA**          WSHEAD        11A  O  2 35
417 MA            WSHEAD        50A  O  2  2
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20010107  215206  KENNETH     REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            WDCODE         5A  O  2  2
     A            WHDESC        30A  O  1 10
     A            WDDESC        30A  O  2 10
     A                                  1 41'Change to warehouse   .'
     A                                  2 41'Change to department  .'
     A                                  1  8'-'
     A                                  2  8'-'
     A                                  3 41'Position to slot  . . .'
     A            PTINP2         5A  B  2 66
     A  32                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A            PTINP1         3Y 0B  1 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A            WOWHSE         3A  O  1  2
     A            PTINP5        12A  B  3 66
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20010115  153528  KENNETH     REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
510 MA            SWKDSP        12   O 10  2
     A            SLPICK         1   O 10 16
640aDA*           SWKDGT         3  0O 10 21
640aMA            SWKDGT1        3  0O 10 21
640aAA            SWKDGT2        3  0O 10 25
640aAA            SWKDGT3        3  0O 10 29
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20010115  153528  KENNETH     REL-V4R2M0  5769-PW1
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                      SFLSIZ(0078)
640aDA*                                     SFLPAG(0036)
640aMA                                      SFLPAG(0024)
     A                                      SFLLIN(0004)
640aDA*                                 9  2'Slot        Pick  ChkDgt  Slot    -
640aDA*                                         Pick  ChkDgt  Slot        Pick -
640aDA*                                      ChkDgt'
640aMA                                  9  2'Slot        Pick   Check Digit    -
640aMA                                      Slot        Pick   Check Digit'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
640aAA          R DSPSFL1                   SFL
640aAA*%%TS  SD  20010115  153528  KENNETH     REL-V4R2M0  5769-PW1
640aAA  21                                  SFLNXTCHG
640aAA            SWKDSP        12   O 10  2
640aAA            SLPICK         1   O 10 16
640aAA            SWKDGT5        5  0O 10 20
640aAA*
640aAA          R DSPSFC1                   SFLCTL(DSPSFL1)
640aAA*%%TS  SD  20010115  153528  KENNETH     REL-V4R2M0  5769-PW1
640aAA  09                                  CF09
640aAA                                      ROLLDOWN
640aAA                                      ROLLUP
640aAA                                      OVERLAY
640aAA  01                                  SFLDSP
640aAA  01                                  SFLDSPCTL
640aAA N01                                  SFLCLR
640aAA                                      SFLSIZ(0078)
640aAA                                      SFLPAG(0036)
640aAA*                                     SFLPAG(0024)
640aAA                                      SFLLIN(0004)
640aAA                                  9  2'Slot        Pick  ChkDgt   Slot   -
640aAA                                           Pick  ChkDgt   Slot        Pic-
640aAA                                      k  ChkDgt'
640aAA*                                 9  2'Slot        Pick   Check Digit    -
640aAA*                                     Slot        Pick   Check Digit'
640aAA                                      DSPATR(HI)
640aAA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19931214  171949  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  2'F3=Exit'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
