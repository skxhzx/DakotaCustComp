     A*%%TS  SD  20040913  160241  ROCKY       REL-V5R2M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  EX150FM   Clear Catch Weight Exceptions (one whse) - Display
     A*  13 September 2004
     A*  Roxanne Harrison
     A*
     A*  SK110FM - 1.00
     A*
     A*  Revisions
     A*
417 AA*    09/13/04  RH   4.17
     A*      - Created
     A*
     A*
     A*----------------------------------------------------------------
     F*  Client Custom Revisions
     F*
CBI AF*    03/22/06  CBI-JGD
     F*      Added indicators to signal error when unauthorized
     F*      user try to delete an exception.
     A*
CBIaAA*    11/04/14  CBIa-JGD
     A*      Added function CA09 to display screen sorted
     A*      in 2 different formats; By item or by date.
     F*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
CBIaAA*>                                    CA09
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  20010926  151134  DAVE        REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2'EX150.01'
     A                                  1 25'Clear Catch Weight Exceptions'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 36'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A            WHCODE         3Y 0O  2  2
     A                                  2  8'-'
     A            WHDESC        30A  O  2 10
     A                                  2 41'Change to warehouse . .'
     A            PTINP1         3Y 0B  2 66EDTCDE(4)
     A  31                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A                                  3 41'Position to item  . . .'
     A            PTINP2        15   B  3 66
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20040913  160241  ROCKY       REL-V5R2M0  5722-WDS
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
CBI AA  19
CBI MAO 20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
CBI AA  19
CBI MAO 21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1ITEM        15A  O 11  6
     A            W1TRN#         7Y 0O 11 23EDTCDE(Z)
     A            W1WGT          7Y 2O 11 32EDTCDE(3)
     A            W1RNGL         7Y 2O 11 42EDTCDE(3)
     A            W1RNGH         7Y 2O 11 52EDTCDE(3)
     A            W1STAT         6A  O 11 62
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20040913  160241  ROCKY       REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
CBIaAA                                      CA09
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 42'Low Wt. '
     A                                      DSPATR(HI)
     A                                  9 52'High Wt.'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 32'Weight  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'Range   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'Range   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Status'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
CBI AA  19
CBI MAO 20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
CBI AA  19
CBI MAO 21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2PDSC    R        O 11  5REFFLD(ITREC/ITPDSC *LIBL/PIRITEM)
     A            W2DESC    R        O 11 21REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A            W2GDTE         6Y 0O 11 52EDTCDE(Y)
     A            W2GTIM    R        O 11 61REFFLD(EXREC/EXGTIM *LIBL/ITMEXCP)
     A                                      EDTWRD(' 0:  :  ')
     A            W2GBY     R        O 11 70REFFLD(EXREC/EXGBY *LIBL/ITMEXCP)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
CBIaAA                                      CA09
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 52'---- Created ----'
     A                                      DSPATR(HI)
     A                                  9 70'Created'
     A                                      DSPATR(HI)
     A                                 10  1'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  5'Size           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 21'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 52'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 61'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 70'By        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  85                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
CBI AA  19
CBI MAO 20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
CBI AA  19
CBI MAO 21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3TASK    R        O 11  7REFFLD(EXREC/EXTASK *LIBL/ITMEXCP)
     A            W3ERDS        25A  O 11 15
     A            W3USER    R        O 11 42REFFLD(EXREC/EXUSER *LIBL/ITMEXCP)
     A            W3CDTE         6Y 0O 11 54EDTCDE(Y)
     A            W3CTIM    R        O 11 64REFFLD(EXREC/EXCTIM *LIBL/ITMEXCP)
     A                                      EDTWRD(' 0:  :  ')
417 AA            W3TRN#         7Y 0O 11 73EDTCDE(Z)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
CBIaAA                                      CA09
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9 42'Closed'
     A                                      DSPATR(HI)
     A                                  9 54'----- Closed -----'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'Error Description        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'By        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 54'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 64'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417 AA                                 10 73'Tran#  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Clear exception.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
     A*    Clear lines in subfile body.
     A*
     A          R CLR1120
     A                                      OVERLAY
     A                                      SLNO(11)
     A                                      CLRL(10)
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*         R DELREC
     A          R OP1REC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 1=Allocate & Build.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A          R CMDOP1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
