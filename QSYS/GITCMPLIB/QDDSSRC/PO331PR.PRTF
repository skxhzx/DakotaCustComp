     A*%%***********************************************************************
     A*
     A*   PO331PR  PO Receiving Receipt - Wide version
     A*   January 16, 2019
     A*   Rod Roller
     A*
     A*  NOTE: compile at 132 columns
     A*
     A*  Revisions
     A*
     A*  01/16/19  RTR  7.30
     A*      - Created from PO330PR, initially for Earl's Organic.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    45
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   127
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    50
     A                                   122
     A                                      DFT('PO331')
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A*%%***********************************************************************
     A                                      SPACEA(002)
     A                                     6
     A                                      DFT('PO:')
     A            PHPO           9A  O    10
     A                                    24
     A                                      DFT('Status:')
     A            P#DESC        11A  O    32
     A            P#MEMO        15A  O    50
     A                                      SPACEA(001)
     A                                     2DFT('Vendor:')
     A            PHVNAM        40   O    10
     A                                      SPACEA(001)
     A            VADR1         70   O    10
     A                                      SPACEA(001)
     A            VADR2         40   O    10
     A                                      SPACEA(001)
     A            VAL39          9A  O    20BARCODE(CODE128 *NOHRI)
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  91                                60DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Seq #')
     A                                      UNDERLINE
     A                                     7
     A                                      DFT('Item Number    ')
     A                                      UNDERLINE
     A                                    23
     A                                      DFT('Description             ')
     A                                      UNDERLINE
     A                                    49
     A                                      DFT(' UOM      ')
     A                                      UNDERLINE
     A                                    61
     A                                      DFT(' Weight  ')
     A                                      UNDERLINE
     A                                    71
     A                                      DFT(' Ordered ')
     A                                      UNDERLINE
     A                                    82
     A                                      DFT('Received ')
     A                                      UNDERLINE
     A            COLTXT1       10A  O    95
     A                                      UNDERLINE
     A            COLTXT2       10A  O   106
     A                                      UNDERLINE
     A            COLTXT3       15A  O   117
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PDSEQ     R        O     1REFFLD(PDSEQ PODTL)
     A                                      EDTCDE(Z)
     A            PDITEM    R        O     7
     A                                      REFFLD(PDITEM PODTL)
     A            W1DESC        25A  O    23
     A            W1UOM         10A  O    49
     A            W1WT          10A  O    60
     A            W1ORD         10A  O    71
     A            W1RCV         10A  O    82
     A  62                                93'*'
     A            W1TXT1        10A  O    95
     A            W1TXT2        10A  O   106
     A            W1TXT3        15A  O   117
     A*
     A* Print tie high
     A*
     A          R DETAIL4
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    10'Mfr#:'
     A            ITVIT#        15   O    16
     A            ITWHDP         5   O    35
     A                                    40'Tie High:'
     A            IDTIE          4  0O    50EDTCDE(Z)
     A                                    55'x'
     A            IDHIGH         2  0O    57EDTCDE(Z)
     A                                    60'Plt:'
     A            PUTPLT         3  0O    64EDTCDE(Z)
     A            ITUM1          2   O    68
     A*
     A          R DETAIL5
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    10'Mfr#:'
     A            ITVIT#        15   O    16
     A            ITWHDP         5   O    35
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            W1EXCP        24   O    23
     A            W1QEXC        10A  O    48
     A*%%***********************************************************************
     A* Print CLAMS harvest data
     A*
     A          R DETAIL3
     A*
     A                                      SPACEA(001)
     A            CLAMS1        75   O     7
     A                                      SPACEA(001)
     A            CLAMS2        75   O     7
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* Print total line
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                     1DFT('Date:    ')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Receiver:')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Shipper: ')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Lumper: ')
     A                                    10DFT('___________________')
     A*%%***********************************************************************
     A          R TOTAL1A
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A                                     1DFT('Container')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Period/Wk')
     A                                    10DFT('___________________')
     A*%%***********************************************************************
     A*
     A          R TOTAL1PFC
     A                                      SPACEB(002)
     A                                      SPACEA(002)
     A                                    41DFT('Pallets:')
     A            TOTPLT         4  0O    51EDTCDE(Z)
     A                                      SPACEA(002)
     A                                     1DFT('Dock#')
     A                                     7DFT('_____')
     A                                    13DFT('Arrival Time:')
     A                                    27DFT('_________')
     A                                    37DFT('Trailer Temp:')
     A                                    51DFT('_________')
     A                                    63DFT('Prdt Temp:')
     A                                    74DFT('_________')
     A                                      SPACEA(002)
     A                                     1DFT('Temp Gun#')
     A                                    11DFT('_________')
     A                                    21DFT('Receipt#')
     A                                    30DFT('_________')
     A                                    40DFT('Pallets In:')
     A                                    51DFT('_______')
     A                                    59DFT('Brkdwn Amt:')
     A                                    70DFT('_________')
     A*
     A          R TOTAL1EOP
     A*%%***********************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                     1DFT('Date:    ')
     A                                    11DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Receiver:')
     A            T1TXT1        40A  O    11
     A                                      SPACEA(002)
     A                                     1DFT('QC:      ')
     A                                    11DFT('[ ]')
     A                                      SPACEA(002)
     A*
     A* Print accumulated totals
     A*
     A          R TOTAL2
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                    40DFT('Totals:')
     A                                    71DFT('__________')
     A                                    82DFT('__________')
     A                                      SPACEA(001)
     A            W1OTOT        10A  O    74
     A            W1RTOT        10A  O    85
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('PO331: End of Report')
     A*
     A          R FOOT1S
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     5'Short________'
     A                                    20'Over_________'
     A                                    35'Damage_______'
     A                                    50'Pest Free____'
     A                                    65'Clean________'
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('PO331: End of Report')
     A*%%***********************************************************************
     A*
