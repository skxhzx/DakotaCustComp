     A*%%TS  SD  20060726  142219  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD4BFM  Truck Load Module - Pallet History View Screen.
     A*  January 26, 2007
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*                  WEBFACING REQUIREMENTS
     A*
     A* WHEN MAKING CHANGES COPY THIS MEMBER TO THE NEXT SEQUENTIAL
     A* NUMBER AND MAKE CHANGES TO THAT VERSION.
     A*
     A* I.E. LOAD4BFM COPY AND NAME LOAD4B01
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*  06/30/10  JCJ  5.30
     A*    - Increase W1TRL# field size from 6 to 8.
     A*
     A*----------------------------------------------------------------
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    ##/##/##  XXX-XXX
     A*      Text . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A*>                                    WINDOW(8 1 12 50)
     A          R WINDW1
     A*%%TS  SD  20060726  142219  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(6 15 14 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  2  2'Loadr4B'
     A* 24                              2 14'Loader Assignment Change'
     A* 24                                  DSPATR(HI)
     A*
     A                                  2 17'Pallet View'
     A                                      DSPATR(HI)
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 35'Door#:'
     A            W1DOOR         3A  O  4 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A*>91 No Display if 1st Time       6  2'Loader:'
     A                                  6  2'Loader:'
     A            W1LDR#         5Y 0O  6 10DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*N91 No Display if 1st Time           DSPATR(ND)
     A*
     A            W1USER        28A  O  6 19DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  8  2'Pallet Id:'
     A            W1PLT#         5Y 0O  8 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A*
     A                                 10  2'Trailer #:'
530 D ***         W1TRL#         6A  O 10 14DSPATR(UL)
530 MA            W1TRL#         8A  O 10 14DSPATR(UL)
     A                                      DSPATR(HI)
     A*
     A                                 12  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 12 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
