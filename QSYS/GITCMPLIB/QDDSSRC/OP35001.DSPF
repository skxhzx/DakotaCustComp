     A*%%TS  SD  20051223  114318  JEFF        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP350FM   PIR Options - Inventory count
     A*  15 December 2006
     A*  Roxanne Harrison
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
510 AA*    12/15/06  RH   5.10
     A*      - Created.
510aAA*    05/22/07  RH   5.10a
     A*      - Enh: Add scan UPC/Lic option to add item  Y/N
     A*        Add key in item option to add item        Y/N
     A*        Can only be Y if OPCADD option to add item is Y
     A*
720aAA*    01/09/17  LMC  7.20a
     A*      - Enh: Add option to display current inventory qty.
     A*        Renamed display file OP350FM to OP35001.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20051223  114318  JEFF        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP350.01'
     A                                  1 34'Inventory Count Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  4  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  4'Scan UPC to confirm item counted .-
     A                                       . . . . . . . . .'
     A            W1CUPC         1A  B  7 57
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 66'Y=Yes, N=No'
     A                                  8  4'RF Add item to slot  . . . . . . .-
     A                                       . . . . . . . . .'
     A            W1CADD         1A  B  8 57
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 66'Y=Yes, N=No'
510a A                                  9  4'Scan UPC/Lic to add item . . . . .-
510a A                                       . . . . . . . . .'
510a A            W1CSCN         1A  B  9 57
510a A  24                                  DSPATR(RI)
510a A N24N90                               DSPATR(UL)
510a A  04                                  DSPATR(PC)
510a A  90                                  DSPATR(PR)
510a A                                  9 66'Y=Yes, N=No'
510a A                                 10  4'Key in item to add item  . . . . .-
510a A                                       . . . . . . . . .'
510a A            W1CKIN         1A  B 10 57
510a A  25                                  DSPATR(RI)
510a A N25N90                               DSPATR(UL)
510a A  05                                  DSPATR(PC)
510a A  90                                  DSPATR(PR)
510a A                                 10 66'Y=Yes, N=No'
     A                                 11  4'Capture catch weight . . . . . . .-
     A                                       . . . . . . . . .'
     A            W1CCWT         1A  B 11 57
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 66'Y=Yes, N=No'
720aAA            W1DQTY         1A  B 12 57
720aAA  26                                  DSPATR(RI)
720aAA N26N90                               DSPATR(UL)
720aAA  06                                  DSPATR(PC)
720aAA  90                                  DSPATR(PR)
720aAA                                 12 66'Y=Yes, N=No'
720aAA                                 12  4'Show Current Inventory Quantity. .-
720aAA                                       . . . . . . . . .'
     A*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
