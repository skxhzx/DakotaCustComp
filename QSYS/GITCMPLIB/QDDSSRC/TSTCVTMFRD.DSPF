     A*%%TS  SD  20070926  151001  DAVE        REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*
     A*  TSTCVTMFRD  Test program CVTMFRDTE
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
510 AA*    09/26/07  DAS  5.10
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20070926  151001  DAVE        REL-V5R4M0  5722-WDS
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'TSTCVTMFRD.1'
     A                                  1 30'Test Program CVTMFRDTE'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Command  . . . . . . . .'
     A            W1CMD         10A  B  5 30
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                  5 51'n/a'
     A                                  7  4'Mfr number . . . . . . .'
     A            W1MFR#        10S 0B  7 30
     A                                  8  4'Mfr item number  . . . .'
     A            W1MFRITEM     15A  B  8 30
     A                                  9  4'Dakota item number . . .'
     A            W1DKTITEM     15A  B  9 30
     A                                 10  4'Mfr def# . . . . . . . .'
     A            W1MFRDEF#      2S 0B 10 30
     A                                 12 39'1'
     A                                 12 49'2'
     A                                 13 30'123456789012345678901234567890'
     A                                 14  4'Code to decipher . . . .'
     A            W1CODE        30A  B 14 30
     A                                 17  4'Return date (CMD)  . . .'
     A            W1DATECMD      8S 0O 17 30
     A                                 18  4'Return date (YMD)  . . .'
     A            W1DATEMDY      6S 0O 18 30
     A                                 19  4'Return code  . . . . . .'
     A            W1RETURN      10A  O 19 30
     A                                 20  4'Return message:'
     A            W1MSG         60   B 20 20
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
