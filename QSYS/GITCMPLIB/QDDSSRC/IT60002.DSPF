     A*%%TS  SD  20150602  130046  ROCKY       REL-V6R1M0  5761-WDS
500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A********************************************************************
     A*   Copyright (C) 1991 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (312) 790-8383                                                 *
     A********************************************************************
     A*
     A*  IT600FM      RF Item data collection
     A*  26 February, 2001
     A*  Hemant Kapadia
     A*
     A*  Notes:
     A*       Created using SU140
     A*       8 X 20 Screen for hand held scanners
     A*
     A********************************************************************
     A*
     A*
     A*  Program Modification History
     A*
415 AA*    02/26/01  HNK
     A*      - Created
     A*
415aAA*    03/12/01  HNK  4.15a
     A*      - Position cursor to slot.
     A*
415bAA*    03/29/01  DAS  4.15b
     A*      - Added F10 to screen 2.
     A*
500 AA*   05/31/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
510aAA*   07/16/07  RH   5.10
     A*     - Add Screen 4 to enter tie/hi.
640aAA*   01/27/11  RBD  6.40a
     A*     - Add Screen 5 to enter shipped weight.
700aAA*   06/02/15  RH   7.00a
     A*     - Rename to IT6002, Add license field to get item #
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA05
     A          R SCREEN1
     A*%%TS  SD  20150602  130046  ROCKY       REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT600.01'
     A                                  1 12'Item Data Collection'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                  3  2'Whse:'
     A            W1WHSE         3Y 0B  3  8EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  2'Dept:'
     A            W1WHDP         5A  B  4  8
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'Slot:'
     A            W1DISP        12A  B  5  8CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON99                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  2'Lic#:'
     A            W1LCNS        15A  B  6  8CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A********************************************************************
     A          R SCREEN2
     A                                      CA03
     A                                      CA12
415bAA                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.02'
     A                                  1 12'Item Data Collection'
     A                                      DSPATR(HI)
     A                                  3  1'Itm:'
     A            W2ITEM        15   O  3  6
     A                                  3 23'Slot:'
     A            W2DISP        12   O  3 29
     A            W2DESC        30   O  4  1
     A                                  5  1'UPC:'
     A            W2UPC         50   B  5  6
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  1'Mfg code:'
     A            W2MFGC        15   B  6 11
415bDA**                                7  2'F12=Cancel'
415bMA                                  7  2'F12=Cancel F10=WOvr'
     A                                      COLOR(BLU)
     A********************************************************************
     A          R SCREEN3
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.03'
     A                                  1 12'Item Data Collection'
     A                                      DSPATR(HI)
     A                                  2  1'Itm:'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A            W3ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W3DISP        12   O  2 29
     A            W3DESC        30   O  3  1
     A                                  4  1'Length'
     A                                  4  8'Width'
     A                                  4 15'Height'
     A            W3ULEN         5  2B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A            W3UWDT         5  2B  5  8EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A            W3UHGT         5  2B  5 15EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  6  1'Recalculate cube:'
     A            W3CALC         1   B  6 19
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A********************************************************************
510  A          R SCREEN4
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.04'
     A                                  1 12'Item Data Collection'
     A                                      DSPATR(HI)
     A                                  2  1'Itm:'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A            W4ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W4DISP        12   O  2 29
     A            W4DESC        30   O  3  1
     A                                  4  1'Tie '
     A                                  4  8'High'
     A            W4TIE          5Y 0B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                  5  5'/'
     A            W4HI           2Y 0B  5  8EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A*
640aAA*****************************************************************
640aAA          R SCREEN5
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.05'
     A                                  1 12'Item Data Collection'
     A                                      DSPATR(HI)
     A                                  2  1'Itm:'
     A                                  7  2'F12=Cancel'
     A                                      COLOR(BLU)
     A            W5ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W5DISP        12   O  2 29
     A            W5DESC        30   O  3  1
     A                                  4  1'Shipping Weight (2 dec)'
     A            W5SWGT         7Y 2B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A*
     A*****************************************************************
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
     A            WOROW          3  0H
     A            WOCOL          3  0H
     A                                  1  2'IT600.01'
     A                                  1 12'Coleccion de datos de prdc'
     A                                      DSPATR(HI)
     A                                  7  2'F3=Salir'
     A                                      COLOR(BLU)
     A                                  3  7'Almc:'
     A            W1WHSE         3Y 0B  3 13EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4  7'Dept:'
     A            W1WHDP         5A  B  4 13
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
415aMA  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5  2'     Slot:'
     A            W1DISP        12A  B  5 13CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
415aMAON99                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A********************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A                                      CA03
     A                                      CA12
415bAA                                      CF10
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.02'
     A                                  1 12'Coleccion de datos de prdc'
     A                                      DSPATR(HI)
     A                                  3  1'Prd:'
     A            W2ITEM        15   O  3  6
     A                                  3 23'Slot:'
     A            W2DISP        12   O  3 29
     A            W2DESC        30   O  4  1
     A                                  5  1'UPC:'
     A            W2UPC         50   B  5  6
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6  1'Cod Mnfc:'
     A            W2MFGC        15   B  6 11
415bDA**                                7  2'F12=Cancel'
415bMA                                  7  2'F12=Cancelar F10=TSob'
     A                                      COLOR(BLU)
     A********************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN3SP
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.03'
     A                                  1 12'Coleccion de datos de prdc'
     A                                      DSPATR(HI)
     A                                  2  1'Prd:'
     A                                  7  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A            W3ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W3DISP        12   O  2 29
     A            W3DESC        30   O  3  1
     A                                  4  1'Longtd'
     A                                  4  8'Ancho'
     A                                  4 15'Altura'
     A            W3ULEN         5  2B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A            W3UWDT         5  2B  5  8EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A            W3UHGT         5  2B  5 15EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                  6  1'Recalcular cubo :'
     A            W3CALC         1   B  6 19
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A********************************************************************
510 AA*----------------------------------------------------------------
510 AA          R SCRN4SP
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.04'
     A                                  1 12'Coleccion de datos de prdc'
     A                                      DSPATR(HI)
     A                                  2  1'Prd:'
     A                                  7  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A            W4ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W4DISP        12   O  2 29
     A            W4DESC        30   O  3  1
     A                                  4  1'Tie '
     A                                  4  8'High'
     A            W4TIE          5Y 0B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                  5  5'/'
     A            W4HI           2Y 0B  5  8EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A*
640aAA*----------------------------------------------------------------
640aAA          R SCRN5SP
     A                                      CA03
     A                                      CA12
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      OVERLAY
     A                                  1  2'IT600.05'
     A                                  1 12'Coleccion de datos de prdc'
     A                                      DSPATR(HI)
     A                                  2  1'Prd:'
     A                                  7  2'F12=Cancelar'
     A                                      COLOR(BLU)
     A            W5ITEM        15   O  2  6
     A                                  2 23'Slot:'
     A            W5DISP        12   O  2 29
     A            W5DESC        30   O  3  1
     A                                  4  1'Peso del envio (2 dec)'
     A            W5SWGT         7Y 2B  5  1EDTCDE(4)
     A  90                                  DSPATR(PR)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
