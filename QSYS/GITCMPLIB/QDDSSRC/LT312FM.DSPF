     A*%%TS  SD  20090723  032738  CHUCKM      REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LT31201   Change Active License
     A*  22 July 2009
     A*  Chuck Manny
     A*
     A*  Revisions
     A*
600 AA*    07/22/09  CWM  6.00
     A*      - Created
     A*
610a A*    09/29/09  GJA  6.10a
     A*      - Enh:  added error handling for screen
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SCREEN1
     A*%%TS  SD  20090723  032738  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'LT312.01'
     A                                  1 26'Active License FIFO Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            W2WHSE         3Y 0O  4 19
     A                                  4 25'License'
     A            W2LCNS        15A  O  4 33
     A                                  9 54'Display . .'
     A            W2DISP        12A  O  9 66
     A                                  9  3'Item. . . . . .'
     A            W2ITEM        15A  O  9 19
     A                                  4  3'Warehouse . . .'
     A            W2QTY1         5Y 0O  6 52EDTCDE(M)
     A            W2QTY2         5Y 0O  6 62EDTCDE(M)
     A            W2QTY3         5Y 0O  6 73EDTCDE(M)
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A            W2OLCN        15A  O  4 64
     A                                  9 35'Item Type. . . .'
     A            W2ITYP         1A  O  9 52
     A            W2SITM        15A  O 10 19
     A            W2SITY         1A  O 10 52
     A                                 10  3'Slot Item . . .'
     A                                 10 35'Slot Item Type .'
     A            W2CODE         1A  O 11 52
     A            W2AREA        15A  O  7 19
     A                                 11  3'Department. . .'
     A            W2WHDP         5A  O 11 19
     A                                  7 35'Aisle'
     A            W2AISL         3A  O  7 41
     A            W2LOC          3Y 0O  7 54EDTCDE(Z)
     A                                  7 59'Level'
     A            W2LVL          2Y 0O  7 65EDTCDE(Z)
     A                                  7 70'H/S .'
     A            W2HAND         2A  O  7 76
     A            W2POS          3S 0O 11 66
     A                                 19  3'Pick Priority .'
     A            W2PRTY         1Y 0B 19 19
610aAA N25N90                               DSPATR(HI)
610aAA  25                                  DSPATR(RI)
610aAA N25N90                               DSPATR(UL)
610aAA  05                                  DSPATR(PC)
610aAA  90                                  DSPATR(PR)
610aDA* 90                                  DSPATR(PR)
610aDA*                                     DSPATR(UL)
610aDA* 25                                  DSPATR(RI)
610aDA* 25                                  DSPATR(PC)
     A                                  6  3'Order/Ord Seq .'
     A            W2ORD          7Y 0O  6 19EDTCDE(Z)
     A            W2SEQ          5Y 0O  6 27EDTCDE(Z)
     A                                  6 59'2)'
     A                                  6 70'3)'
     A                                  7  3'Area. . . . . .'
     A                                 19 22'Date Entered. .'
     A            W2ENTD         8S 0B 19 38
610aAA N26N90                               DSPATR(HI)
610aAA  26                                  DSPATR(RI)
610aAA N26N90                               DSPATR(UL)
610aAA  06                                  DSPATR(PC)
610aAA  90                                  DSPATR(PR)
     A                                 20 38'YYYYMMDD'
     A                                 19 49' Exp Date. . . .'
     A            W2EXPD         8S 0B 19 66
610aAA N27N90                               DSPATR(HI)
610aAA  27                                  DSPATR(RI)
610aAA N27N90                               DSPATR(UL)
610aAA  07                                  DSPATR(PC)
610aAA  90                                  DSPATR(PR)
     A                                 20 66'YYYYMMDD'
     A                                 13  3'Timestamp . . .'
     A            W2CHGT          Z  O 13 19
     A            W2CHGU          Z  O 14 19
     A                                 14  3'UC Timestamp. .'
     A                                 16  3'Chg Pgm/Emp . .'
     A            W2CGPG        10A  O 16 19
     A            W2CGEM         5S 0O 16 31
     A                                 17  3'Employee User .'
     A            W2EMUS        10A  O 17 19
     A                                 17 50'Current User. .'
     A            W2CRUS        10A  O 17 66
     A                                 11 54'Pallet Pos.'
     A                                 11 35'Code . . . . . .'
     A                                 16 50'Job User. . . .'
     A                                 13 50'Change Job. . .'
     A                                 14 50'Chg Job Nbr . .'
     A            W2CJUS        10A  O 16 66
     A            W2CGJB        10A  O 13 66
     A            W2CGJN        10A  O 14 66
     A                                 10 54'Base Slot .'
     A            W2BASE        12A  O 10 66
     A            WOTYPE         6A  O  2 73
     A                                  4 51'Original Lic'
     A                                  7 45'Location'
     A                                  6 49'1)'
     A                                  6 35'License'
     A                                  6 45'Qty'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
