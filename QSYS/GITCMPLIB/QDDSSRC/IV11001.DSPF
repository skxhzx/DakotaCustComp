     A*%%TS  SD  20160801  112426  MIKER       REL-V7R2M0  5770-WDS
     A*
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*
     A*
     A*  Revisions
     A*
414 AA*   05/10/00  RH   4.14
     A*    - Add Aisle Cycle Count option and reason code.
415 AA*   07/06/01  RH   4.15
     A*    - Add slots option (pick/overflow/both)
     A*    - Change order of existing fields on screen.
416 AA*   09/20/01  RH   4.16
     A*    - Add designation code option (code/all)
416aAA*   05/01/02  RH   4.16a
     A*    - Change "Aisle items" to 'Scope items"
500aAA*   02/23/05  RH   5.00a
     A*    - Add side of aisle to selection.
510aAA*   08/15/06  RH   5.10a
     A*    - Add Exclude Catch weight items.
520aAA*   04/04/08  RH   5.20a
     A*    - Add Item category to selection.
520bAA*   11/24/08  RH   5.20b
     A*    - Enh: Add item vendor # to selection.
700aAA*   08/01/16  MR   7.00a
     A*    - Enh: Create IV11001 and add "Item" to
     A*           Inventory scope screen.
     A*
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20160801  112426  MIKER       REL-V7R2M0  5770-WDS
     A                                      CF04
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD)
     A                                      CLRL(23)
     A                                      OVERLAY
     A            CSRRCD        10A  H      TEXT('Cursor rcd fmt name')
     A            CSRFLD        10A  H      TEXT('Cursor field name')
     A                                  1  2'IV110.01'
     A                                  1 28'Define Scope of Inventory'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Select/Unselect  . . . . . .'
     A            W1TYPE         1A  B  5 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 50'S=Select, U=Unselect'
     A                                  6  4'Slots  . . . . . . . . . . .'
     A            W1SLTS         1A  B  6 34
     A N34N90                               DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A                                  6 50'P=Pick, O=Overflow, B=Both'
416a A*                                 8  4'Export Aisle Items only. . .'
     A                                  7  4'Export Scope Items only. . .'
     A            W1PEXP         1A  B  7 34
     A N31N90                               DSPATR(HI)
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A                                  7 50'Y,N'
     A                                  8  4'Aisle Cycle Count(Y/N) . . .'
     A            W1CYCL         1A  B  8 34
     A N32N90                               DSPATR(HI)
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A                                  8 39'/'
     A            W1ADJ          2A  B  8 43
     A N33N90                               DSPATR(HI)
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A                                  8 50'Adjustment code'
     A                                  9  4'Exclude CW items . . . . . .'
     A            W1EXCW         1A  B  9 34
     A N37N90                               DSPATR(HI)
     A  37                                  DSPATR(RI)
     A N37N90                               DSPATR(UL)
     A  17                                  DSPATR(PC)
     A  91                                  DSPATR(PR)
     A                                  9 50'Y,N'
700aMA                                 10  4'Warehouse / Department . . .'
     A            W1WHSE         3Y 0B 10 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aMA                                 10 50'1-999/Name, *ALL'
700aDA*                                11  4'Department . . . . . . . . .'
700aAA                                 10 38'/'
700aMA            W1WHDP         5A  B 10 40
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aDA*                                11 50'Name, *ALL'
700aMA                                 11  4'Designation code . . . . . .'
700aMA            W1DESG         5A  B 11 34
     A N35N90                               DSPATR(HI)
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
700aMA                                 11 50'Name, *ALL'
700aMA                                 12  4'Category code  . . . . . . .'
700aMA            W1CAT         10A  B 12 34
700AMA N38N90                               DSPATR(HI)
700AMA  38                                  DSPATR(RI)
700AMA N38N90                               DSPATR(UL)
700AMA  18                                  DSPATR(PC)
700AMA  90                                  DSPATR(PR)
700AMA                                 12 50'Name, *ALL'
700aMA                                 13  4'Item vendor #  . . . . . . .'
700aMA            W1VEN         10Y 0B 13 34EDTCDE(4)
700AMA N39N90                               DSPATR(HI)
700AMA  39                                  DSPATR(RI)
700AMA N39N90                               DSPATR(UL)
700AMA  19                                  DSPATR(PC)
700AMA  90                                  DSPATR(PR)
700AMA                                 13 50'0 - 9999999999'
700aAA                                 14  4'Item . . . . . . . . . . . .'
700aAA            W1ITM         15A  B 14 34
700aAA N40N90                               DSPATR(HI)
700aAA  40                                  DSPATR(RI)
700aAA N40N90                               DSPATR(UL)
700aAA  20                                  DSPATR(PC)
700aAA  90                                  DSPATR(PR)
700aAA                                 14 50'Item #, *ALL'
     A                                 15  4'Side of aisle  . . . . . . .'
     A            W1SIDE         5A  B 15 34
     A N36N90                               DSPATR(HI)
     A  36                                  DSPATR(RI)
     A N36N90                               DSPATR(UL)
     A  16                                  DSPATR(PC)
     A                                 15 50'*BOTH, ODD, EVEN'
     A                                 16  4'Section  . . . . . . . . . .'
     A            W1STYP         1A  B 16 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 50'P=PIR, F=Fixed, B=Both'
     A                                 17 15'- OR -                      '
     A                                 18  4'Aisle  . . . . . . . . . . .'
     A            W1ASLE         4A  B 18 34
     A N29N90                               DSPATR(HI)
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A                                 18 65'(Y=Yes, N=No)'
     A                                      DSPATR(ND)
     A                                 18 50'*ALL, Code'
     A                                 19  4'Location . . . . . . . . . .'
     A            W1FLOC         3Y 0B 19 34EDTCDE(4)
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A                                 19 39'to'
     A            W1TLOC         3Y 0B 19 43EDTCDE(4)
     A N26N90                               DSPATR(HI)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A                                 19 50'1 - 999'
     A                                 20  4'Level  . . . . . . . . . . .'
     A            W1FLVL         2Y 0B 20 35EDTCDE(4)
     A N27N90                               DSPATR(HI)
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A                                 20 39'to'
     A            W1TLVL         2Y 0B 20 43EDTCDE(4)
     A N28N90                               DSPATR(HI)
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A                                 20 50'1 - 99'
     A                                 21  4'Hand stack . . . . . . . . .'
     A            W1HSTK         4A  B 21 34
     A N30N90                               DSPATR(HI)
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A                                 21 50'*ALL'
     A                                 23  2'F3=Exit   F4=Prompt   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*%%TS  SD  19960130  094217  DAVE3       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
