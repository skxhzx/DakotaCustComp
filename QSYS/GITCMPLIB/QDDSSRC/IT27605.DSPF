     A*%%TS  SD  20180518  083731  KENNETHE    REL-V7R2M0  5770-WDS
500aAA*----------------------------------------------------------------
500aAA*   Multilingual Version
500aAA*   Every screen has a corresponding Spanish screen.
500aAA*   All Spanish screens are at the end.
500aAA*   Any changes must be made to both screens.
500aAA*   If necessary, contact appropriate person to do translation.
414cAA* ***************************************************************
414cAA* *****  If the position of any slot entry field is changed *****
414cAA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414cAA* *****  program.                                           *****
414cAA* ***************************************************************
     A*
     A********************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  IT276FM      RF Specific Slot Replenishment - Display.
     A*  25 July 1997
     A*  Hemant Kapadia
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
414aAA*     08/04/00  MLB  4.14a
     A*       - Changed SCANREC to change wording from "Scan slot" to
     A*         "From slot".
414bAA*     08/08/00  MLB  4.14b
     A*       - Added new option to screen POSTNREC - 1=Select.
     A*       - Moved 9=View qty to line 3.
     A*       - Changed length of W1ITEM from 20 to 65.
414cAA*    08/28/00  MLB  4.14c
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCANREC.
     A*      - Both of the above added for api program APICVTSLT
     A*      - Changed indicator for SFLNXTCHG from 21 to 85.
     A*
416 AA*     10/15/01  MLB  4.16
     A*       - Changed MSGCLR, MSGREC to display msgs on line 9 instead
     A*         of 24.
416aAA*    12/18/01  MLB  4.16a
     A*      - Revised heading on DSPSFC to move W1PICK field next to
     A*        W1STAT
416bAA*    12/18/01  MLB  4.16b
     A*      - Added new window format WDWMSG to display warning messages.
416cAA*    12/28/01  MLB  4.16c
     A*      - Added new subfile option X=Slot error to POSTNREC.
416dAA*    02/01/02  MLB  4.16d
     A*      - Added F8 function key to call license plate info.
     A*
417 AA*    05/16/03  MLB  4.17
     A*      - Enh: Added 16-line support to display.
417aAA*    05/16/03  MLB  4.17a
     A*      - Enh: Added lot/license plate scan window (WDWLLP)
417bAA*    04/05/04  MLB  4.17b
     A*      - Enh: Moved scan field and text 2 spaces right to
     A*        make room for new field to highlight breakdown item.
     A*
500 AA*    03/15/05  MLB  5.00
     A*      - Enh: Added F10=Out to display.
     A*
500aAA*   05/28/06  JCJ  5.00a
     A*     - Merged Spanish screens from PIRS.
510aAA*   10/31/07  LMC  5.10a
     A*     - Added additional 16 x 20 screens.
510bAA*   12/11/07  LMC  5.10b
     A*     - Added F11 = transfer.
600aAA*   01/29/09  DAS  6.00a
     A*     - Deleted unused screens.
610aAA*   08/27/09  RH   6.10a
     A*     - Renamed IT27601,
     A*     - Change Y/N to OUTPUT field, allow 1/9 to be displayed
     A*       for Saval.
610bAA*   11/04/10  DAS  6.10b
     A*     - Renamed IT27602
     A*     - Added second view to all list displays to include
     A*       the license. Controlled by F9.
     A*
650 AA*   05/01/13  MLB  6.50
     A*     - Fix: Renamed IT27603
     A*     - Added *in81 to SFLDSP keyword to fix CPF5013 error
     A*       when program attempted to display subfile with no
     A*       records. Found at Greenleaf Produce. Problem discovered
     A*       when user attempted to replenish type B item and the
     A*       parent item did not have any slots.
     A*
700 AA*    10/02/08  JGD 7.00
     A*      ADDED NEW OPTION TEXT 'S=STUCK PLT' TO POSTNREC RECORD.
     A*      SOURCE COPIED FROM PIR650.
     A*
730 AA*    11/15/19  MLB  7.30
     A*      - Enh: Added CHECK(ER) to field W1SCAN on fmt SCANREC.
     A*        Attribute lost when license scan update added.
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19970725  165203  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'IT276.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Replenishment Slots'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A          R SCANREC
     A*%%TS  SD  20180518  083731  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1  2'IT276.01'
730 MA            W1SCAN        15   B  1 31CHECK(ER)
     A                                  1 18'Frm Slt/Lcns'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19990618  135332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Item:'
414bDA*           W1ITEM        20A  O  1  8
414bMA            W1ITEM        65A  O  1  8
414bDA*                                 1 31'9=View Qty'
     A                                  2  2' '
414bAA                                  2  4'1=Select'
414bAA                                       COLOR(BLU)
414bAA                                  2 14'9=View Qty'
414bAA                                       COLOR(BLU)
416cAA                                  2 26'X=Slot Error'
416cAA                                       COLOR(BLU)
700 AA  22                              2 40'S=STUCK PLT'
700 AA                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
414cMA  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416aMA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
416bAA                                      TEXT('Hidden field: SVEXPD')
416aDA*           SLSTAT    R        O  5 31REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
416aMA            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
416aDA*           SLPICK         1A  O  5 54
416aMA            SLPICK         1A  O  5 33
416aDA*           W1PRTY    R        O  5 61REFFLD(SLREC/SLPRTY *LIBL/SLOT)
416aMA            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
416aDA*                                 4 58'Pck Prity'
416aMA                                  4 53'Pck Prity'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA*                                 4 53'Pick'
416aDA*                                     DSPATR(HI)
416aDA*                                     DSPATR(UL)
416aDA*                                 4 30'Stat'
416aMA                                  4 30'St P'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL2                   SFL
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1LCNS        15   O  5  6
     A            SLDISP        12A  O  5 22
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            W1QTY         10A  O  5 53
     A          R DSPSFC2                   SFLCTL(DSPSFL2)
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 35'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 53'Avail Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417 AA*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(08)
     A            CMDLN1        79   O  1  2COLOR(BLU)
417 AA*
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
416 DA*                                     SFLMSGRCD(24)
416 MA                                      SFLMSGRCD(09)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
416 DA*                                     SLNO(24)
416 MA                                      SLNO(09)
     A                                      CLRL(1)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A          R SC16REC
     A*%%TS  SD  20180518  083731  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1  2'IT276.01'
     A                                  2  1'Fr Slt/Lcn'
     A            W1SCAN        15   B  2 12
     A          R SC16RESP
     A*%%TS  SD  20180518  083731  KENNETHE    REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A            SCANHD         6A  O  1 11
     A                                  1 18'Slot De  :'
     A            W1DISP        12A  B  1 29CHECK(ER)
     A                                  1  2'IT276.01'
417 AA*****************************************************************
417 AA*
417 AA* SUBFILE FOR LIST DISPLAY (16-Lines)
417 AA*
417 AA          R D16SFL                    SFL
417 AA  85                                  SFLNXTCHG
510aDA*           OPTION         2A  B  5  2
510aMA            OPTION         2A  B  6  2
417 AA N21N90                               DSPATR(UL)
417 AA  21                                  DSPATR(RI)
417 AA  90                                  DSPATR(PR)
417 AA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
417 AA                                      TEXT('Hidden field: SVEXPD')
510aDA*           SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
510aMA            SLSTAT    R        O  6 29REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
510aDA*           SLPICK         1A  O  5 33
510aMA            SLPICK         1A  O  6 32
510aDA*           W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
510aMA            W1PRTY    R        O  6 55REFFLD(SLREC/SLPRTY *LIBL/SLOT)
510aDA*           W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
510aMA            W1EXPD         6Y 0O  6 34EDTWRD('  /  /  ')
510aDA*           W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
510aMA            W1ENTD         6Y 0O  6 43EDTWRD('  /  /  ')
510aDA*           SLDISP        12A  O  5  6
510aMA            SLDISP        12A  O  6  5
510aDA*           W1QTY         10A  O  5 19
510aMA            W1QTY         10A  O  6 18
417 AA*
417 AA          R D16SFC                    SFLCTL(D16SFL)
510aDA*                                     SFLSIZ(0010)
510aMA                                      SFLSIZ(0008)
510aDA*                                     SFLPAG(0010)
510aMA                                      SFLPAG(0008)
417 AA                                      ROLLDOWN
417 AA                                      ROLLUP
417 AA                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
417 AA  01                                  SFLDSPCTL
417 AA N01                                  SFLCLR
510aDA*                                 4  2'Opt'
510aMA                                  5  2'Op'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
510aDA*                                 4  6'Slot        '
510aMA                                  5  5'Slot        '
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
510aDA*                                 4 53'Pck Prity'
510aMA                                  5 52'Pck Prity'
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
510aDA*                                 4 44'Ent Date'
510aMA                                  5 43'Ent Date'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
510aDA*                                 4 35'Exp Date'
510aMA                                  5 34'Exp Date'
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
510aDA*                                 4 30'St P'
510aMA                                  5 29'St P'
417 AA                                      DSPATR(UL)
417 AA                                      DSPATR(HI)
510aDA*                                 4 19'Avail Qty'
510aMA                                  5 18'Avail Qty '
417 AA                                      DSPATR(HI)
417 AA                                      DSPATR(UL)
     A*
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFL2                   SFL
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  6  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1LCNS        15   O  6  5
     A            SLDISP        12A  O  6 21
     A            W1EXPD         6Y 0O  6 34EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  6 43EDTWRD('  /  /  ')
     A            W1QTY         10A  O  6 52
     A          R D16SFC2                   SFLCTL(D16SFL2)
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  5  2'Op'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  5'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 21'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 34'Exp Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 43'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 52'Avail Qty '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
417 AA*
417 AA*****************************************************************
417 AA*
417 AA* Error Message Subfile Records for 16-line screens
417 AA*
417 AA*    Error Message Subfile Record
417 AA*
417 AA          R M16REC                    SFL
417 AA                                      SFLMSGRCD(15)
417 AA            #MSGK                     SFLMSGKEY
417 AA            #PGMQ                     SFLPGMQ
417 AA*
417 AA*    Error Message Subfile Control Record
417 AA*
417 AA          R M16CTL                    SFLCTL(M16REC)
417 AA  98                                  ALARM
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SFLSIZ(2) SFLPAG(1)
417 AA N97                                  SFLCLR
417 AA  97                                  SFLDSP
417 AA  97                                  SFLEND
417 AA          R M16CLR
417 AA                                      OVERLAY
417 AA                                      LOCK
417 AA                                      SLNO(15)
417 AA                                      CLRL(1)
411 DA*                                     SLNO(22)
411 DA*           CMDLN1        79   O  2  2COLOR(BLU)
417 AA*
417 AA*    COMMAND LINES
417 AA*
417 AA          R C16REC
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SLNO(16)
417 AA            CMDLN1        79   O  1  2COLOR(BLU)
417 AA*
417 AA          R MORREC16
417 AA                                      LOCK
417 AA                                      OVERLAY
417 AA                                      SLNO(14)
417 AA            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*
409 AA*    CLEAR ALL LINES IN SUBFILE LIST
     A*
409 AA          R CLR0802
     A                                      OVERLAY
     A                                      SLNO(08)
     A                                  1  2' '
     A                                  2  2' '
     A*    COMMAND LINES
     A*
416bAA          R WDWMSG
416bAA                                      WINDOW(1 2 6 20)
416bAA*
416bAA                                      WDWTITLE((*TEXT 'Replenishment Erro-
416bAA                                      r') *CENTER)
416bAA                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
416bAA                                      l') *BOTTOM)
416bAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
416bAA                                          '))
416bAA            WOTXT1        20A  O  1  1
416bAA            WOTXT2        20A  O  2  1
416bAA            WOTXT3        20A  O  3  1
416bAA            WOTXT4        20A  O  4  1
416bAA                               O  5  1'Continue:'
416bAA N40                                  DSPATR(ND)
416bAA            WOYES          1A  B  5 11COLOR(WHT)
416bAA**                                    VALUES('Y' 'N')
610aAA                                      VALUES('Y' 'N' '1' '9')
416bAA N40                                  DSPATR(ND)
416bAA N40                                  DSPATR(PR)
416bAA**                             O  5 13'(Y/N)'
610aMA            WOYN           5A  O  5 13
416bAA N40                                  DSPATR(ND)
     A*
417aAA* Lot/License plate scan window.
417aAA          R WDWLLP
417aAA                                      WINDOW(1 2 6 20)
417aAA                                      WDWTITLE((*TEXT 'Scan Lot/Lic#') *C-
417aAA                                      ENTER)
417aAA                                      WDWTITLE((*TEXT 'F3=Exit  F12=Cance-
417aAA                                      l') *BOTTOM)
417aAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
417aAA                                          '))
417aAA            WWTXT         18   O  2  1
417aAA            WWLLP#        15   B  3  1DSPATR(HI)
417aAA                                      DSPATR(UL)
500aAA*
500aAA*****************************************************************
500aAA* Spanish Screens
500aAA*****************************************************************
500aA *----------------------------------------------------------------
500aAA          R TITLESP
     A*%%TS  SD  19970725  165203  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  2'IT276.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Slots de reaprovisionamiento'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
500aA *----------------------------------------------------------------
500aAA          R SCANRESP
     A*%%TS  SD  19990618  100251  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(01)
414cAA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414cAA            WOROW          3  0H
414cAA            WOCOL          3  0H
417bAA            SCANHD         6A  O  1 11
414aDA*                                 1 16'Slot escn:'
417bDA*                                 1 16'Slot De  :'
417bMA                                  1 18'Slot De  :'
417bDA*           W1DISP        12A  B  1 27CHECK(ER)
417bMA            W1DISP        12A  B  1 29CHECK(ER)
     A                                  1  2'IT276.01'
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
500aA *----------------------------------------------------------------
500aAA          R POSTNRSP
     A*%%TS  SD  19990618  135332  ROCKY       REL-V4R2M0  5769-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(02)
     A                                  1  2'Prdc:'
414bDA*           W1ITEM        20A  O  1  8
414bMA            W1ITEM        65A  O  1  8
414bDA*                                 1 31'9=Ver Ctd '
     A                                  2  2' '
414bAA                                  2  4'1=Seleccionar'
414bAA                                       COLOR(BLU)
414bAA                                  2 18'9=Ver Ctd'
414bAA                                       COLOR(BLU)
416cAA                                  2 28'X=Error de slot'
416cAA                                       COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
500aA *----------------------------------------------------------------
500aAA          R DSPSFLSP                  SFL
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
414cMA  85                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
416aMA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
416bAA                                      TEXT('Hidden field: SVEXPD')
416aDA*           SLSTAT    R        O  5 31REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
416aMA            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
416aDA*           SLPICK         1A  O  5 54
416aMA            SLPICK         1A  O  5 33
416aDA*           W1PRTY    R        O  5 61REFFLD(SLREC/SLPRTY *LIBL/SLOT)
416aMA            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            SLDISP        12A  O  5  6
     A            W1QTY         10A  O  5 19
     A*
500aA *----------------------------------------------------------------
500aAA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  19990618  132659  ROCKY       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
416aDA*                                 4 58'Pck Prird'
416aMA                                  4 53'Pck Prird'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 44'Fcha ent'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 35'Fcha exp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
416aDA*                                 4 53'Pick'
416aDA*                                     DSPATR(HI)
416aDA*                                     DSPATR(UL)
416aDA*                                 4 30'Esta'
416aMA                                  4 30'P St'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 19'Ctd Dispo'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A*----------------------------------------------------------------
     A          R DSPSFLSP2                 SFL
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1LCNS        15   O  5  6
     A            SLDISP        12A  O  5 22
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            W1QTY         10A  O  5 53
     A*----------------------------------------------------------------
     A          R DSPSFCSP2                 SFLCTL(DSPSFLSP2)
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 35'Fcha exp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 44'Fcha ent'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 53'Ctd Dispo '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500aAA*
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
500aA *----------------------------------------------------------------
500aAA          R OPTRECSP
     A*%%TS  SD  19940914  135501  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(09)
     A                                  1  2'Escriba opciones, oprima ENTER.'
     A                                      COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A*    COMMAND LINES
     A*
500aA *----------------------------------------------------------------
500aAA          R WDWMSGSP
416bAA                                      WINDOW(1 2 6 20)
416bAA*
416bAA                                      WDWTITLE((*TEXT 'Error de Reap.    -
416bAA                                       ') *CENTER)
416bAA                                      WDWTITLE((*TEXT 'F3=Salir F12=Cance-
416bAA                                      lar') *BOTTOM)
416bAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
416bAA                                          '))
416bAA            WOTXT1        20A  O  1  1
416bAA            WOTXT2        20A  O  2  1
416bAA            WOTXT3        20A  O  3  1
416bAA            WOTXT4        20A  O  4  1
416bAA                               O  5  1'Continuar:'
416bAA N40                                  DSPATR(ND)
416bAA            WOYES          1A  B  5 12COLOR(WHT)
416bAA                                      VALUES('Y' 'N')
416bAA N40                                  DSPATR(ND)
416bAA N40                                  DSPATR(PR)
416bAA                               O  5 14'(Y/N)'
416bAA N40                                  DSPATR(ND)
     A*
417aAA* Lot/License plate scan window.
500aA *----------------------------------------------------------------
500aAA          R WDWLLPSP
417aAA                                      WINDOW(1 2 6 20)
417aAA                                      WDWTITLE((*TEXT 'Escn Lot/#Lic') *C-
417aAA                                      ENTER)
417aAA                                      WDWTITLE((*TEXT 'F3=Salir  F12=Canc-
417aAA                                      elar') *BOTTOM)
417aAA                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
417aAA                                          '))
417aAA            WWTXT         18   O  2  1
417aAA            WWLLP#        15   B  3  1DSPATR(HI)
417aAA                                      DSPATR(UL)
510aAA*
510aAA* POSITION TO RECORD
510aAA*
510aAA          R PO16NREC
510aAA*%%TS  SD  20071030  145326  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      LOCK
510aAA                                      SLNO(03)
510aAA                                  1  2'Item:'
510aAA            W1ITEM        65A  O  1  8
510aAA                                  2  1' '
510aAA                                  2  3'1=Sel'
510aAA                                      COLOR(BLU)
510aAA                                  2  9'9=Qty'
510aAA                                      COLOR(BLU)
510aAA                                  2 15'X=Err'
510aAA                                      COLOR(BLU)
510aAA          R PO16NRSP
510aAA*%%TS  SD  20071031  071806  LYNNM       REL-V5R4M0  5722-WDS
510aAA                                      OVERLAY
510aAA                                      LOCK
510aAA                                      SLNO(02)
510aAA                                  1  2'Prdc:'
510aAA            W1ITEM        65A  O  1  8
510aAA                                  2  2' '
510aAA                                  2  4'1=Seleccionar'
510aAA                                      COLOR(BLU)
510aAA                                  2 18'9=Ver Ctd'
510aAA                                      COLOR(BLU)
510aAA                                  2 28'X=Error de slot'
510aAA                                      COLOR(BLU)
500aAA*****************************************************************
500aAA*----------------------------------------------------------------
500aAA* SUBFILE FOR LIST DISPLAY (16-Lines)
500aAA*
500aAA          R D16SFLSP                  SFL
500aAA  85                                  SFLNXTCHG
500aAA            OPTION         2A  B  5  2
500aAA N21N90                               DSPATR(UL)
500aAA  21                                  DSPATR(RI)
500aAA  90                                  DSPATR(PR)
500aAA            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
500aAA                                      TEXT('Hidden field: SVEXPD')
500aAA            SLSTAT    R        O  5 30REFFLD(SLREC/SLSTAT *LIBL/SLOT3)
500aAA            SLPICK         1A  O  5 33
500aAA            W1PRTY    R        O  5 56REFFLD(SLREC/SLPRTY *LIBL/SLOT)
500aAA            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
500aAA            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
500aAA            SLDISP        12A  O  5  6
500aAA            W1QTY         10A  O  5 19
500aAA*
500aAA          R D16SFCSP                  SFLCTL(D16SFLSP)
500aAA                                      SFLSIZ(0010)
500aAA                                      SFLPAG(0010)
500aaA                                      ROLLDOWN
500aaA                                      ROLLUP
500aAa                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
500aAA  01                                  SFLDSPCTL
500aAA N01                                  SFLCLR
500aAA                                  4  2'Opc'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500aAA                                  4  6'Slot        '
500aAA                                      DSPATR(UL)
500aAA                                      DSPATR(HI)
500aAA                                  4 53'Pck Prird'
500aAA                                      DSPATR(UL)
500aAA                                      DSPATR(HI)
500aAA                                  4 44'Ent Date'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500aAA                                  4 35'Fcha Exp'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500aAA                                  4 30'St P'
500aAA                                      DSPATR(UL)
500aAA                                      DSPATR(HI)
500aAA                                  4 19'Ctd Dispo'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A* SUBFILE FOR LIST DISPLAY (16-Lines)
     A*
     A          R D16SFLSP2                 SFL
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A  85                                  SFLNXTCHG
     A            SVEXPD    R        H      REFFLD(SLREC/SLEXPD *LIBL/SLOT3)
     A                                      TEXT('Hidden field: SVEXPD')
     A            OPTION         2A  B  5  2
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1LCNS        15   O  5  6
     A            SLDISP        12A  O  5 22
     A            W1EXPD         6Y 0O  5 35EDTWRD('  /  /  ')
     A            W1ENTD         6Y 0O  5 44EDTWRD('  /  /  ')
     A            W1QTY         10A  O  5 53
     A          R D16SFCSP2                 SFLCTL(D16SFLSP2)
     A*%%TS  SD  20101104  103355  DAVE        REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
650 DA* 01                                  SFLDSP
650 MA  81                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  4  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  6'License        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 22'Slot        '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 35'Fcha Exp'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 44'Ent Date'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 53'Ctd Dispo '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
