     A*%%TS  SD  20120116  075100  LYNNM       REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  RT750FM   Remove exported Route records - Options
     A*  11 November 1994
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
414aAA*    04/26/00  MLB  4.14a
     A*      - Changed export cutoff from date to number of weeks value.
     A*      - Several misc. screen format changes.
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20120116  075100  LYNNM       REL-V6R1M0  5761-WDS
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A                                  8 33'. . . . .'
     A                                 11  4'Place on job queue ? . . . .'
     A                                 11 33'. . . . .'
     A            W1WHSE         3Y 0B  8 44EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1JOBQ         1A  B 11 44
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 53'(Y=Yes, N=No)'
414aDA*                                 9  4'Exported before  . . . . . .'
414aDA*           W1DATE         6Y 0B  9 34EDTCDE(4)
     A            W1WEEK         4A  B  9 44
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 53'*DFT, 1-9999'
     A                                  9  4'Weeks history to keep  . . .'
     A                                  9 33'. . . . .'
     A            W1RTHS         4Y 0O 10 44EDTCDE(4)
     A                                 10  6'Current number of weeks history  .-
     A                                       .'
414aDA*                                 9 53'(MMDDYY)'
     A            W1SCRN         8A  O  1  2
     A            WOHEAD        60A  O  1 11DSPATR(HI)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
