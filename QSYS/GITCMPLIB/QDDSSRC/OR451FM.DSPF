     A*%%TS  SD  20060524  133514  ROCKY       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  Revisions
     A*    05/23/06  RH   5.00
     A*      - Created
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'OR451.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 31'Open Selection Labels'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  20000719  160818  ROCKY       REL-V4R2M0  5769-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A                                  2 49'Position to code . . .'
     A            PTINP1         7Y 0B  2 72EDTCDE(4)
     A            FLD002         1A  O  3  2
     A                                  2  3'Tran # '
     A            WOTRN#         7Y 0O  2 12EDTCDE(4)
     A            FLD005         1A  O  1  2
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20060524  133514  ROCKY       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A* 90                                  DSPATR(PR)
     A            LBLBL#    R        O 11  6REFFLD(LBREC/LBLBL# *LIBL/LABEL)
     A                                      EDTCDE(4)
     A*           W1ITEM        15A  O 11 12
     A            LBITEM    R        O 11 14REFFLD(LBREC/LBITEM *LIBL/LABEL)
     A*           W1DESC        30A  O 11 28
     A            ITDESC    R        O 11 30REFFLD(ITREC/ITDESC *LIBL/PIRITEM)
     A            W1RTE          5A  O 11 62
     A*           W1RTID         5A  O 11 68
     A            LBRTE     R        O 11 69REFFLD(LBREC/LBRTE *LIBL/LABEL)
     A*           W1STOP         2Y 0O 11 75EDTCDE(4)
     A            LBSTOP    R        O 11 78REFFLD(LBREC/LBSTOP *LIBL/LABEL)
     A                                      EDTCDE(4)
     A*                              O 11 22'/'
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20060524  133514  ROCKY       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      CF06
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                 10  6'Label #'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'Item           '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 30'Description                   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 62'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 69'Rt ID '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 76'Stop'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930217  144739  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'                                  -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930221  155645  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
