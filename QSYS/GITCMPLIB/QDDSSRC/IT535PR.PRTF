     A*-------------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*-------------------------------------------------------------------
     A*   IT535PR  Recommend Transfers from Outside Storage - Report
     A*   26 May 2009
     A*   Max Blacknik
     A*
     A*-------------------------------------------------------------------------
     A*  Revisions
     A*
520 AA*  05/26/09  MLB  5.20
     A*    - Created.
640 AA*  05/10/12  RTR  6.40
     A*    - Added OSS Slot to report.
GLP AA*  10/02/13  RH   GREENLEAF PRODUCE
     A*    - CHANGED OBJECT NOT SOURCE TO LPI=4, CPI=5
     A*    - LINES PER PAGE =50, OVERFLOW=44
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            RDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                   +42
     A                                      DFT('Recommended Outside Storage Tr-
     A                                      ansfers')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            RTIME          6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          40A  O    52
     A                                   124
     A                                      DFT('IT535')
     A*
     A            OSSLOC        40A  O    52
     A                                      SPACEB(001)
     A                                    52'Counts are'
     A                                      SPACEB(001)
     A            HSEL           5A  O    +1
     A                                    +1'Average, Sorted by'
     A            HSORT          9A  O    +1
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                    56
     A                                      DFT('Recommended')
     A            R1DMTX         3A  O    70
     A                                    74
     A                                      DFT('Day')
     A                                    79
     A                                      DFT(' LcL Whse')
     A                                    90
     A                                      DFT(' Rmt Whse')
     A*
     A*
     A                                     1
     A                                      SPACEB(001)
     A*
     A                                      DFT('Dept ')
     A                                      UNDERLINE
     A                                     8
     A                                      DFT('Item Number   ')
     A                                      UNDERLINE
     A                                    25
     A                                      DFT('Description                   -
     A                                      ')
     A                                      UNDERLINE
     A                                    56
     A                                      DFT('X-Fer Qty')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('   Demand')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('  On-Hand')
     A                                      UNDERLINE
     A                                    90
     A                                      DFT('  On-Hand')
     A                                      UNDERLINE
     A                                   101
     A                                      DFT('Pick Slot ')
     A                                      UNDERLINE
     A                                   113
     A                                      DFT('Offsite Slot')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A* Print detail line
     A*
     A          R DETAIL1
     A*
     A                                      SPACEA(001)
     A            ITWHDP    R        O     1
     A                                      REFFLD(ITWHDP PIRITEM)
     A            ITITEM    R        O     8
     A                                      REFFLD(ITITEM PIRITEM)
     A            ITDESC    R        O    25
     A                                      REFFLD(ITDESC PIRITEM)
     A            R1TFRQ         7S 0O    56
     A                                      EDTCDE(J)
     A            R1DEMD         7S 0O    68
     A                                      EDTCDE(J)
     A            R1LOCQ         7S 0O    79
     A                                      EDTCDE(J)
     A            R1RMTQ         7S 0O    90
     A                                      EDTCDE(J)
     A            R1DISP    R        O   101
     A                                      REFFLD(SLDISP SLOT)
     A            R1OSS     R        O   113
     A                                      REFFLD(SLDISP SLOT)
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'No Items Found for Transfer...'
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT535: End of Report')
     A*
