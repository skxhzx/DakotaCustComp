     A*%%***********************************************************************
     A*
     A*   ER331PR  Print Put-away/Returns transactions
     A*   02 July, 1999
     A*   Hemant Kapadia
     A*
     A*   Revision
     A*
     A*   09/18/09  GJA  5.30a
     A*     - Converted to RPGLE, cleaned up and add suspended trans.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    43
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                   125
     A                                      DFT('ER331')
     A                                      SPACEA(001)
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(003)
     A            TMERNG        22A  O    52
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            PTEMP#    R             +1REFFLD(PTEMP# ER331)
     A                                      EDTCDE(Z)
     A            WKNAME        40        +1
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('License')
     A                                      UNDERLINE
     A                                    28
     A                                      DFT('Start Dt')
     A                                      UNDERLINE
     A                                    38
     A                                      DFT('Start Tm')
     A                                      UNDERLINE
     A                                    49
     A                                      DFT('End Date')
     A                                      UNDERLINE
     A                                    60
     A                                      DFT('End Time')
     A                                      UNDERLINE
     A                                    71
     A                                      DFT('Task')
     A                                      UNDERLINE
     A                                    79
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    89
     A                                      DFT('Qty  ')
     A                                      UNDERLINE
     A                                    99
     A                                      DFT('Minutes')
     A                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
     A                                      REFFLD(PTTRN# *LIBL/ER331)
     A            LILCNS    R        O    10
     A                                      REFFLD(LILCNS *LIBL/ER331)
     A            W1SDTE         6  0O    28EDTCDE(Y)
     A            W1STIM         6  0O    38EDTWRD('  :  :  ')
     A            W1EDTE         6  0O    49EDTCDE(Y)
     A            W1ETIM         6  0O    60EDTWRD('  :  :  ')
     A            PTTASK    R        O    71REFFLD(PTTASK *LIBL/ER331)
     A            LBDISP    R        O    79REFFLD(LBDISP *LIBL/ER331)
     A            LBQPCK    R        O    89REFFLD(LBQPCK *LIBL/ER331)
     A                                      EDTCDE(3)
     A            PTDMIN    R        O    99REFFLD(PTDMIN *LIBL/ER331)
     A                                      EDTCDE(3)
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Total transactions:')
     A            TRNCNT         5  0O    25EDTCDE(3)
     A            TRNMIN         7  0O    97EDTCDE(3)
     A            TRNPCK         7  0O    87EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('ER331: End of Report')
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'No records found for employee'
     A*
