     A*%%TS  SD  19960617  164546  ROCKY       REL-V3R1M0  5763-PW1
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960321  151610  ROCKY       REL-V3R1M0  5763-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'IV760.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                 23  2'F3=Exit  F5=Refresh '
     A                                      COLOR(BLU)
     A                                  1 25'Clear Slot Files for Host Count'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
416  A                                  9  4'Department . . . . . . . . .'
416  A            W1WHDP         5A  B  9 34
416  A  23                                  DSPATR(RI)
416  A  03                                  DSPATR(PC)
416  A N23                                  DSPATR(HI)
416  A                                  9 43'*ALL'
     A                                 10  4'Place on job queue ? . . . .'
     A                                      DSPATR(ND)
     A            W1JOBQ         1A  B 10 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                 10 65'(Y=Yes, N=No)'
     A                                      DSPATR(ND)
     A                                 11  4'Clear to retry FTP only  . .'
     A            W1CFTP         1A  B 11 34
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A                                 11 65'(Y=Yes, N=No)'
     A          R SCREEN2
     A*%%TS  SD  19960304  160341  DAVE        REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'IV760.02'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'Clear Slot Files for Host Count'
     A                                      DSPATR(HI)
     A                                 15 31'Press Enter to Exit'
     A                                  6 15'This program can not be run at thi-
     A                                      s time for one of'
     A                                  7 15'the following reasons:'
     A                                  9 19'- Someone is already running this -
     A                                      program.'
     A                                 11 19'- Someone is working with the file-
     A                                      s that are needed.'
     A          R SCREEN3
     A*%%TS  SD  19960304  160341  DAVE        REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CA10
     A                                      CLRL(23)
     A                                  1  2'IV760.03'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'Clear Slot Files for Host Count'
     A                                      DSPATR(HI)
     A                                  5 13'The last time this program was run-
     A                                       it ended abnormally.'
     A                                  8 25'Batch# :'
     A                                  9 25'Date   :'
     A                                 10 25'Time   :'
     A                                 11 25'User   :'
     A                                 12 25'Status :'
     A            W1BAT          7Y 0O  8 34EDTCDE(3)
     A            W1DATE         6Y 0O  9 34EDTCDE(Y)
     A            W1TIME         6Y 0O 10 34EDTWRD('  :  :  ')
     A            W1USER        10A  O 11 34
     A            W1STAT        30A  O 12 34
     A                                 15 21'Press F10 to pickup where it left -
     A                                      off '
     A                                 16 27'F3 to exit and investigate further-
     A                                      .'
     A          R SCREEN4
     A*%%TS  SD  19960617  164546  ROCKY       REL-V3R1M0  5763-PW1
     A  98                                  ALARM
     A                                      CA10
     A                                      CLRL(23)
     A                                  1  2'IV760.04'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1 25'Clear Slot Files for Host Count'
     A                                      DSPATR(HI)
     A                                  6 12'You are about to update your files-
     A                                        before Host Inventory counts.'
     A                                  8 12'Update for warehouse:'
     A            W4WHSE         3Y 0O  8 35EDTCDE(4)
416  A                                  9 12'          Department:'
     A            W4WHDP         5   O  9 35
     A                                 18 33'Press F10 to continue.'
     A                                 23  2'F3=Exit  F10=Post'
     A                                 11 12'Be certain you want to clear qty -
     A                                      from the slots.'
     A                                 13 12'Make sure all data files are backe-
     A                                      d up.'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
