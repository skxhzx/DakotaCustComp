     A*%%TS  SD  20100924  092126  BFCTEST1    REL-V5R4M0  5722-WDS
     A*------------------------------------------------------------------*
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A********************************************************************
     A*
     A*  LT111FM   RF - Display Pick Slot with less than 5 cases.
     A*  September 17, 2010
     A*  Jose G. Duque
     A*
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    12/01/10  CBI-JGD
CBI  A*      Change heading "< 5" to "< 10"
     A*
     A*    12/03/10  CBI-JGD
CBIa A*      Provide displaying a specific heading based on the
     A*      the fork lift operator shift, if night display heading
     A*      showing "< 5 Cases", if day display heading showing
     A*      "< 10 Cases".
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*----------------------------------------------------------------
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20100921  092346  BFCTEST1    REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                  1  2'LT111.01'
     A            TITLE         35A  O  1 11DSPATR(HI)
     A                                  2  2'1=Select'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A* Subfile for list display
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R DSPSFL                    SFL
     A*%%TS  SD  20100924  092126  BFCTEST1    REL-V5R4M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  4  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1SLOT        12A  O  4  6
     A            W1CURQ        10A  O  4 19
     A            W1TIHI         5Y 0O  4 30EDTCDE(Z)
     A            W1ONHD         5Y 0O  4 38EDTWRD('   0 ')
     A*
     A*----------------------------------------------------------------
     A*----------------------------------------------------------------
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20100924  092126  BFCTEST1    REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0004)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  3  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  6'Pick Slot   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 19'Curr Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 30'T/H Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3 38'OvrStk Qty'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A* SCREEN 3.
     A*----------------------------------------------------------------
     A          R EXCPSCR
     A*%%TS  SD  20000727  075930  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(09)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'LT111.03'
     A                                  1 12'Slot Info.'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W3WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  4  2'Dept.'
     A            W3WHDP         5A  B  4  8
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N22N90                               DSPATR(HI)
     A                                  5  2'Slot '
     A            W3DISP        12A  B  5  8CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*----------------------------------------------------------------
     A          R VW2SFL                    SFL
     A*----------------------------------------------------------------
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W2STRD         6Y 0O 11 14EDTCDE(Y)
     A            LHSTRT    R        O 11 23REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2ENDD         6Y 0O 11 33EDTCDE(Y)
     A            LHENDT    R        O 11 42REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2LODT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2SUP         10   O 11 63
     A*
     A*----------------------------------------------------------------
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0007)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 14'---- Started ----'
     A                                      DSPATR(HI)
     A                                 10 33'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'----- Ended -----'
     A                                      DSPATR(HI)
     A                                 10 52'Load Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Supervisor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*----------------------------------------------------------------
     A          R VW3SFL                    SFL
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A*----------------------------------------------------------------
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*----------------------------------------------------------------
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0007)
CBiaDA*                                     CF10
CBIaDA*                                     CF11
CBIaDA*                                     CF17
CBIaDA*                                     CF18
CBIaDA*                                     CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*----------------------------------------------------------------
     A* Error Message Subfile Records
     A*----------------------------------------------------------------
     A*    Error Message Subfile Record
     A*----------------------------------------------------------------
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(10)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A*    Error Message Subfile Control Record
     A*----------------------------------------------------------------
     A          R MSGCTL                    SFLCTL(MSGREC)
     A*----------------------------------------------------------------
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A*----------------------------------------------------------------
     A          R MSGCLR
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(10)
     A                                      CLRL(1)
     A*----------------------------------------------------------------
     A* Option, Command and EOF Message Lines for Lookup Panels
     A*----------------------------------------------------------------
     A*    Option Lines
     A*----------------------------------------------------------------
     A          R OPTREC
     A*----------------------------------------------------------------
     A*%%TS  SD  20070613  144106  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  1  2'Type 1, press Enter.'
     A                                      COLOR(BLU)
     A*-->        OPTLN2        75A  O  3  2COLOR(BLU)
     A*----------------------------------------------------------------
     A*    Command lines
     A*----------------------------------------------------------------
     A          R CMDREC
     A*----------------------------------------------------------------
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(8)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*-->        CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A*    'MORE.../BOTTOM' Message line
     A*----------------------------------------------------------------
     A          R MORREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      SLNO(9)
     A                                      OVERLAY
     A            MORELN         7A  O  1 38DSPATR(HI)
     A*----------------------------------------------------------------
     A*    DELETE DISPLAY INSTRUCTIONS
     A*----------------------------------------------------------------
     A          R DELREC
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*----------------------------------------------------------------
     A*                        REC NOT MENTIONED IN PGM
     A          R CLR03END
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*----------------------------------------------------------------
     A*    Clear lines between title and subfile list
     A*----------------------------------------------------------------
     A          R CLR0306
     A*----------------------------------------------------------------
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*----------------------------------------------------------------
     A          R CMDDEL
     A*----------------------------------------------------------------
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
