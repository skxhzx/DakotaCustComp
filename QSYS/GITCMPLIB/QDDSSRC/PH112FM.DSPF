     A*%%TS  SD  20080716  212006  DAVE        REL-V5R4M0  5722-WDS
     A**************************************************************************
     A*
     A*  PH112FM  Phrase maintenance - Change
     A*  16 July 2008
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
600 AA*    05/16/08  DAS  6.00  P#00045
     A*      - Created
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF07
     A                                      CF09
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080716  212006  DAVE        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PH112.01'
     A                                  1 29'Phrase Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Phrase id . . . . .'
     A            W1ID      R        B  8 24REFFLD(PHREC/PHID *LIBL/PHRASETBL)
     A                                      EDTCDE(4)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 59'1 - 99999'
     A                                  9  4'Language  . . . . .'
     A            W1LANG    R        B  9 24REFFLD(PHREC/PHLANG *LIBL/PHRASETBL)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 59'EN=English, SP=Spanish'
     A                                 10  4'Group . . . . . . .'
     A            W1GROUP   R        B 10 24REFFLD(PHREC/PHGROUP *LIBL/PHRASETB-
     A                                      L)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11  4'Level . . . . . . .'
     A            W1LEVEL   R        B 11 24REFFLD(PHREC/PHLEVEL *LIBL/PHRASETB-
     A                                      L)
     A                                      EDTCDE(4)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 59'1 - 99'
     A                                 13  4'Phrase  . . . . . .'
     A            W1PHRASE  R        B 13 24REFFLD(PHREC/PHPHRASE *LIBL/PHRASET-
     A                                      BL)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 16  4'XML . . . . . . . .'
     A            W1XML1        50A  O 16 24
     A            W1XML2        50A  O 17 24
     A                                 14 29'Press F7 to Generate XML String'
     A                                 23  2'F5=Refresh  F7=Generate XML  F9=Fo-
     A                                      rmat test  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 19  4'Variables: &EMP, &ID, &ITEM, &LOC,-
     A                                       &LCNS, &LTRN, &PGM, &TRN, &USER'
     A                                 20 15'&V1, &V2, &V3, &V4, &V5'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
