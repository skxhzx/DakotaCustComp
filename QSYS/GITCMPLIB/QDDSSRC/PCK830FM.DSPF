     A*%%TS  SD  20010621  141400  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PCK830FM   Create ITEMSLS and ITEMAVG test records - Options
     A*  21 June 2001
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
415 AA*   06/21/01  DAS  4.15
     A*     - Created.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20010621  141400  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'PCK830.01'
     A                                  1 21'Create ITEMSLS and ITEMAVG Test Re-
     A                                      cords'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'Number of weeks to create . . . .'
     A            W1WKS          2Y 0B  5 39COMP(GT 0)
     A                                      EDTCDE(3)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 49'1-99'
     A                                  7  4'Sales quantity option . . . . . .'
     A            W1OPT1         1A  B  7 39
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 49'1 = Static'
     A                                  8 49'2 = Incrementing'
     A                                  9 49'3 = Random'
     A                                 11  4'Minimum quantity for random . . .'
     A            W1MIN          5Y 0B 11 39COMP(GE 0)
     A                                      EDTCDE(3)
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 49'0-99999'
     A                                 12  4'Maximum quantity for random . . .'
     A            W1MAX          5Y 0B 12 39COMP(GE 0)
     A N24N90                               DSPATR(HI)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 12 49'0-99999'
     A                                 14  4'Place on job queue  . . . . . . .'
     A            W1JOBQ         1A  B 14 39
     A N25N90                               DSPATR(HI)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 49'Y=Yes, N=No'
     A                                 17  6'This program will create ITEMSLS r-
     A                                      ecords and then corresponding'
     A                                 18  6'ITEMAVG records. Static will put t-
     A                                      he same number into every quantity.'
     A                                 19  6'Increment will increment the quant-
     A                                      ity for each record. Random will'
     A                                 20  6'Randomly generate quantities.'
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
