     A*%%TS  SD  20020827  143305  HEMANT      REL-V4R2M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT910FM    Item lookup
     A*  Dave Sommerville
     A*  Hemant Kapadia
     A*
     A*  Revisions
     A*
416 AA*    08/27/02  HNK  4.16
     A*      - Created.
     A*
417 AA*    02/26/03  MLB  4.17
     A*      - Enh: Added CHECK(ER) to Window 1 fields W1DESC, W1ITEM
     A*        W1UPC.
     A*
S88 AA*    07/30/03  HNK  S88
     A*      - CHANGE SIZE OF SFLHIDE FIELD
     A*
     A*----------------------------------------------------------------
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA12
     A                                      CF09
     A*
     A          R SFL1                      SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SFLLINE       75A  O  5  5
S88 MA            SFLHIDE       65A  H
S88 DA*           SFLHIDE       60A  H
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      ROLLUP
     A            RRN1           4S 0H      SFLRCDNBR
     A            SCRNAME        8A  O  1  2
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        40A  O  2 21
     A                                  2 71TIME
     A            SCOPT         75A  O  3  2
     A            SFLHEAD       79A  O  4  1
     A                                  1 31'Item Lookup'
     A                                      DSPATR(HI)
     A*
     A          R FKEY1
     A*
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                    +2'F9=Select/Sort'
     A                                      COLOR(BLU)
     A                                    +2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20020827  143305  HEMANT      REL-V4R2M0  5769-PW1
     A                                      WINDOW(6 20 14 45)
     A                                  1 15'Position To'
     A                                      DSPATR(HI)
     A                                  3  1'Item number .'
     A                                  5  1'UPC code  . .'
     A                                  7  1'Description .'
     A            W1DESC        30A  B  7 15CHECK(LC)
417 AA                                      CHECK(ER)
     A                                      DSPATR(HI)
     A                                 13  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A            W1ITEM        15A  B  3 15DSPATR(HI)
417 AA                                      CHECK(ER)
     A                                      DSPATR(UL)
     A            W1UPC         20A  B  5 15DSPATR(HI)
417 AA                                      CHECK(ER)
     A                                      DSPATR(UL)
     A          R WINDOW2
     A                                      WINDOW(6 20 8 40)
     A                                  1 15'Sort Criteria'
     A                                      DSPATR(HI)
     A                                  4  1'Sort by . . .'
     A            W2SORT         1A  B  4 15DSPATR(HI)
     A                                  4 22'1=Count, 2=Item'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW3
     A                                      WINDOW(6 20 8 40)
     A                                  1 15'Sort Criteria'
     A                                      DSPATR(HI)
     A                                  4  1'Sort by . . .'
     A            W3SORT         1A  B  4 15DSPATR(HI)
     A                                  4 22'1=Count, 2=Emp#'
     A                                  7  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
