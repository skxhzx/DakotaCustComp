     A*%%TS  SD  20090615  141524  BFC         REL-V5R4M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOAD11FM  Truck Load Module - RF Pallet Entry Screen.
     A*  May 24, 2006
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*  06/01/09  RBD  5.20
     A*    - Moved each line up 1 line so that all lines will appear
     A*      on a wrist terminal.
     A*
530 AA*  06/23/10  JCJ  5.30
     A*    - Increase W1TRL# field size from 6 to 8.
     A*
700aAA*   6/01/15  RBD  7.00a
     A*    - Changed format name to LOAD1101.
     A*    - Increased size of entry field which holds the barcode scanned
     A*      on the pallet label. Previously it was the last 2 char of
     A*      route id + pallet id, now it's the full route id + pallet id.
     A*      We're making this change because Coastal has more than 100
     A*      trucks, which means we need more than the last 2 char of
     A*      route id to ensure the barcode is unique for the day's routes.
     A*      By using the full route id + pallet id we can be sure the
     A*      combo field will always be unique, thus making it as fail safe
     A*      as possible that the pallet gets loaded onto the right truck.
     A*
710aAA*   8/24/17  RBD  7.10a
     A*    - Changed format name to LOAD1102.
     A*    - Added 'Sub' to screen title.
     A*
750aAA*  05/04/21  RBD  7.50a
     A*    - Changed format name to LOAD1103.
     A*    - Changed pallet barcode scan from 10 pos to 9 to avoid potential
     A*      conflict with Trax# barcode which is 10 pos. The pallet barcode
     A*      will now consist of last 4 of route id + last 5 of pallet.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*    ##/##/##  XXX-XXX
     A*      Text . . .
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A          R WINDW4
     A*%%TS  SD  20090615  141524  BFC         REL-V5R4M0  5722-WDS
     A                                      WINDOW(1 2 14 26)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  1  2'Loadr11'
710aDA**                                1 10'Flag Loading'
710aMA                                  1 10'Load Flag/Sub'
     A                                      DSPATR(HI)
     A                                  3  2'Rte #'
     A            W1RTE#         5A  O  3  8DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  3 14'Door#:'
     A            W1DOOR         3A  O  3 21DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  5  2'Loader:'
     A            W1LDR#         5Y 0O  5 10DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            W1USER        10A  O  5 16DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  7  2'Pallet Id:'
700aDA**          W1PLT#         7Y 0B  7 14DSPATR(UL)
700aMA**750aD     W1PLT#        10Y 0B  7 14DSPATR(UL)
750aMA            W1PLT#         9Y 0B  7 14DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                  9  2'Trailer #:'
530 D ***         W1TRL#         6A  B  9 14DSPATR(UL)
530 MA            W1TRL#         8A  B  9 14DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 11  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 11 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A*%%TS  SD  20090615  141524  BFC         REL-V5R4M0  5722-WDS
     A                                      SFLMSGRCD(12)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ(10)
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A*%%TS  SD  20090615  141524  BFC         REL-V5R4M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(12)
     A                                      CLRL(01)
     A                                      OVERLAY
     A          R CLRSCR
     A                                      CLRL(*ALL)
