     A*%%TS  SD  19960119  150412  HEMANT      REL-V3R1M0  5763-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA10
     A                                      CA11
     A                                      CA12
     A                                      CA17
     A                                      CA18
     A                                      CA20
     A          R TITLE
     A*%%TS  SD  19960119  150412  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                  1  3'TE120.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A                                  1 26'Manual Transaction Maintenance'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19960105  135724  HEMANT      REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(03)
     A                                  2 48'Position to start date.'
     A            PTDMY1        78A  O  1  2
     A            PTDMY2        78A  O  3  2
     A            PTINP1         6Y 0B  2 73EDTCDE(Z)
     A                                  2  3'User:'
     A            W1UNAM        30A  O  2  9
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19930220  154528  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19960108  083403  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PTTASK    R        O 11  7REFFLD(PTREC/PTTASK *LIBL/PIRTRAN)
     A            W1SDTE         6Y 0O 11 15EDTWRD('  /  /  ')
     A            W1EDTE         6Y 0O 11 35EDTWRD('  /  /  ')
     A            W1HRS          2Y 0O 11 57EDTCDE(3)
     A            W1MIN          2Y 0O 11 61EDTCDE(3)
     A            PTSTIM    R        O 11 25REFFLD(PTREC/PTSTIM *LIBL/PIRTRAN)
     A                                      EDTWRD('  :  :  ')
     A            PTETIM    R        O 11 45REFFLD(PTREC/PTETIM *LIBL/PIRTRAN)
     A                                      EDTWRD('  :  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19960105  135724  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  7'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 25'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'<------Start----->'
     A                                      DSPATR(HI)
     A                                  9 35'<-------End------>'
     A                                      DSPATR(HI)
     A                                 10 35'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 45'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 56'Hrs Min'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 56' Time  '
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  19960108  083403  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PTTASK    R        O 11  6REFFLD(PTREC/PTTASK *LIBL/PIRTRAN)
     A            PTCUBE    R        O 11 14REFFLD(PTREC/PTCUBE *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
     A            PTSWGT    R        O 11 26REFFLD(PTREC/PTSWGT *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
     A            PTPCS     R        O 11 39REFFLD(PTREC/PTPCS *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
     A            W1STDP         5Y 0O 11 58EDTCDE(3)
     A            W1PCNT         5Y 2O 11 65EDTCDE(3)
     A            W1HRS          2Y 0O 11 48EDTCDE(3)
     A            W1MIN          2Y 0O 11 52EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  19960108  083403  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Task  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 14'Total cube'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 26'     Total'
     A                                      DSPATR(HI)
     A                                 10 26'    Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 38' Total'
     A                                      DSPATR(HI)
     A                                 10 38'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 56'Std pcs'
     A                                      DSPATR(HI)
     A                                 10 56' per Hr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 65'  % of'
     A                                      DSPATR(HI)
     A                                 10 65'   Std'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 47'Hrs Min'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 47'  Time '
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  19960105  135724  HEMANT      REL-V3R1M0  5763-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            PTTASK    R        O 11  6REFFLD(PTREC/PTTASK *LIBL/PIRTRAN)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  19960105  135724  HEMANT      REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10  6'Task  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19930220  141828  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(21)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A*    COMMAND LINES
     A*
     A*         R CMDREC
     A          R CMDDEL
     A*%%TS  SD  19930214  153838  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
