     A****************************************************************
     A*
     A* SL311PR   Verified Open slot report.
     A* 01 September, 1994
     A* Hemant Kapadia
415aAA*  06/21/01  RH   4.15a
     A*    - Change column headings.
     A*
640a A*  02/18/11  GJA  6.40
     A*    - Enh:  Add "*" by slot if there are virtuals
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
CBI AA*    10/22/08  CBI-JGD
     A*      Main heading changed to add the word 'Overstock' when
     A*      reporting on overstock slots only.                        '.
     A*
     F*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
CBI AA N20                                52
     A                                      DFT('Verified Open Slot Report')
CBI AA  20                                52
CBI AA                                      DFT('Verified Open Overstock Slot -
     A                                           Report')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL311')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  92                                90
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
415  A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Actv')
     A                                     6
     A                                      DFT('Slot')
     A*
     A                                    26
     A                                      DFT('Actv')
     A                                    31
     A                                      DFT('Slot')
     A*
     A                                    51
     A                                      DFT('Actv')
     A                                    56
     A                                      DFT('Slot')
     A*
     A                                    76
     A                                      DFT('Actv')
     A                                    81
     A                                      DFT('Slot')
     A*
     A                                   101
     A                                      DFT('Actv')
     A                                   106
     A                                      DFT('Slot')
     A*
415  A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Zone')
     A                                     6
     A                                      DFT('Type')
     A                                    11
     A                                      DFT('Slot')
     A*
     A                                    26
     A                                      DFT('Zone')
     A                                    31
     A                                      DFT('Type')
     A                                    36
     A                                      DFT('Slot')
     A*
     A                                    51
     A                                      DFT('Zone')
     A                                    56
     A                                      DFT('Type')
     A                                    61
     A                                      DFT('Slot')
     A*
     A                                    76
     A                                      DFT('Zone')
     A                                    81
     A                                      DFT('Type')
     A                                    86
     A                                      DFT('Slot')
     A*
     A                                   101
     A                                      DFT('Zone')
     A                                   106
     A                                      DFT('Type')
     A                                   111
     A                                      DFT('Slot')
     A*
415  A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1DEF          2A  O     7
     A            C1ACTV         2A  O     2
640aAA            C1VSLT         1A  O    10
     A            C1SLOT        12A  O    11
     A*
     A            C2DEF          2A  O    32
     A            C2ACTV         2A  O    27
640aAA            C2VSLT         1A  O    35
     A            C2SLOT        12A  O    36
     A*
     A            C3DEF          2A  O    57
     A            C3ACTV         2A  O    52
640aAA            C3VSLT         1A  O    60
     A            C3SLOT        12A  O    61
     A*
     A            C4DEF          2A  O    82
     A            C4ACTV         2A  O    77
640aAA            C4VSLT         1A  O    85
     A            C4SLOT        12A  O    86
     A*
     A            C5DEF          2A  O   107
     A            C5ACTV         2A  O   102
640aAA            C5VSLT         1A  O   110
     A            C5SLOT        12A  O   111
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A N21                                 1
     A                                      DFT('Dept Total Slots Printed')
     A  21                                 1
     A                                      DFT('Grand Total Slots Printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          6S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL311: End of Report ')
     A*
