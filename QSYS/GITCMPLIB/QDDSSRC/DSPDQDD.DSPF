     A*----------------------------------------------------------------
     A* DSPDQD - Retrieve Datq Descriptions
     A* By   Craig Rutledge,  10/08/92
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A          R SCREEN
     A                                      CA03
     A                                      CA05(05)
     A                                      PRINT
     A                                  2  2'DSPDQD'
     A                                      COLOR(BLU)
     A                                  2 23'Display Data Queue Description'
     A                                      DSPATR(HI)
     A                                  2 62'jcr'
     A                                      COLOR(BLU)
     A                                  2 72DATE
     A                                      EDTWRD('0  /  /  ')
     A                                      COLOR(BLU)
     A                                  3 72SYSNAME
     A                                      COLOR(BLU)
     A                                  4  2'Data queue: . . . . . . . '
     A            VQNAME        10A  O  4 30DSPATR(HI)
     A                                  4 48'Library:'
     A            VQLIB         10A  O  4 58DSPATR(HI)
     A                                  6  2'Text: . . . . . . . . . .'
     A            VTEXT         50A  O  6 30
     A                                  8  2'Maximum length of entries:'
     A            QLEN           5S 0O  8 30
     A                                 10  2'Sequence: . . . . . . . . '
     A            QSEQUE         6A  O 10 30
     A                                 10 43'*FIFO - First in First out'
     A                                 11 43'*KEYED - Keyed'
     A                                 12 43'*LIFO - Last in First out'
     A  50                             13  2'Key Length: . . . . . . .'
     A  50        QKEY           4S 0O 13 30
     A                                 15  2'Number of entries in queue:'
     A            QNENTY         9S 0O 15 30DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 17  2'Dynamic maximum entries'
     A                                 18  2'before overflow: . . . .'
     A            QMAXEN         9S 0O 18 30DSPATR(HI)
     A  50                             18 43'Include sender name:. . .'
     A  50        QSNDRN         4A  O 18 69
     A                                 20  2'Type . . . . . . . . . .'
     A            DDM_LOCAL      1A  O 20 30
     A                                 21 30'0=Local  1=DDM'
     A                                 24  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 24 20'F5=Refresh'
     A                                      COLOR(BLU)
