     A*%%TS  SD  20061030  140434  JEFF        REL-V5R3M0  5722-WDS
     A**************************************************************************
     A*  Revisions:
     A*
     A*    11/25/99  DAS  4.13
     A*     - Took out department fields. Now just have long string to
     A*       contain whse and dept.
     A*     - Moved lines up to make room for SSB line.
     A*     - Created line to display SSB item if one exists.
     A*
510 AA*    10/30/06  JCJ  5.10
     A*     - Enh: Added cost window
510aAA*    01/19/07  RAH  5.10a
     A*     - Enh: Changed length of cost field from 7,2 to 9,4.
520cAA*  04/24/09  RH   5.20c
     A*    - Enh: Qty and catch weight are both entered, both must
     A*      be the same sign (positve or negative).
     A*      Remaned IT122FM to IT12201 for webfacing.
     A*      Add label for minus sign.
530aAA*  11/02/09  RH   5.30a
     A*    - Enh: Renamed IT12201 format to IT12202.
     A*    - Fix: Change SCREEN1 to display field WODISP in place of
     A*      SLDISP, SLDISP is no longer used with DRI programs.
     A*
650 AA*   02/27/13  MLB  6.50
     A*     - Enh: Changed to use IT12203, to allow for BIR mod.
650bAA*  05/28/14  RH   7.00 (6.50b MISSED IN 6.50 UPDATE)
     A*    - Enh: Renamed IT12203 to IT12204
     A*      Add LIFO field to apply adjustment to newest license,
     A*      default to oldest license (FIFO)
700aAA*  03/23/16  LMC  7.00a
     A*    - Renamed from IT12204 to IT12205
     A*    - Enh: Adding logic for Metric.
     A*    - Moved command keys to program.
     A*
     A*----------------------------------------------------------------
     A* Client Custom Revisions: Packer's
     A*
PCK AA*    02/08/07  JCJ  PCK
     A*     - Enh: made memo field required.
     A*
     A*-------------------------------------------------------------------------
     A*  Client Custom Revisions: BiRite Foodservice
BIR AA*  02/27/13  MLB  BIRa
     A*    - Enh: Created SCREEN1B format for BiRite to display | character
     A*      to identify where p/o nbr should be keyed, if needed.
     A*
     A*****************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19991125  071812  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
700aAA N95                                  CA07
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT122.01'
     A                                  1 30'Inventory Adjustments'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Whse/Dept  :'
     A            WOWHSE        55A  O  4 15
     A                                  5  2'Slot       :'
530aDA*           SLDISP        12A  O  5 15
530aMA            WODISP        12A  O  5 15
     A                                  6  2'Slot type  :'
     A            WOSTYP         5A  O  6 15
     A                                  7  2'Status     :'
     A            SSDESC        30A  O  7 15
     A                                  8  2'Item       :'
     A            WOITEM        55A  O  8 15
     A            WOLBLS        12   O  9  2DSPATR(HI)
     A            WOSITM        55   O  9 15DSPATR(HI)
     A                                 11  2'Available slot qty  :'
     A            W1INCQ        10A  O 11 24
     A                                 11 36'(includes all open transactions)'
     A                                 12  2'Adjustable slot qty :'
     A            W1EXCQ        10A  O 12 24
     A                                 12 36'(excludes open receiving transacti-
     A                                      ons)'
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  4'Adjustment type  . . .'
     A            W1TYPE         1A  B 16 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                 16 59'1 = Increase/decrease'
     A                                 17 59'2 = Replace'
650b A            W1FIFO         1A  B 17 28
650b A  34                                  DSPATR(RI)
650b A N34N90                               DSPATR(UL)
650b A  14                                  DSPATR(PC)
650b A  90                                  DSPATR(PR)
650b A                                 17  4'Adj Oldest Product Y/N'
     A                                 18  4'Quantity . . . . . . .'
     A            W1QT01         5Y 0B 18 28EDTCDE(M)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
520cDA*           W1UM1          5A  O 18 35
520cMA            W1UM1          5A  O 18 34
520cDA*           W1QT02         3Y 0B 18 42EDTCDE(M)
520cMA            W1QT02         3Y 0B 18 41EDTCDE(M)
     A  29                                  DSPATR(RI)
     A  09                                  DSPATR(PC)
     A N29N92                               DSPATR(UL)
     A  92                                  DSPATR(ND)
     A  92                                  DSPATR(PR)
520cDA*           W1UM2          5A  O 18 47
520cMA            W1UM2          5A  O 18 45
     A  92                                  DSPATR(ND)
520dDA*           W1QT03         3Y 0B 18 54EDTCDE(M)
520cMA            W1QT03         3Y 0B 18 51EDTCDE(M)
     A  30                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A N30N93                               DSPATR(UL)
     A  93                                  DSPATR(ND)
     A  93                                  DSPATR(PR)
520cDA*           W1UM3          5A  O 18 59
520cMA            W1UM3          5A  O 18 55
     A  93                                  DSPATR(ND)
520cAA                                 18 61'(-) after #=Decrease'
     A                                 19  4'Catch wt adjustment. .'
     A            W1CWTR         9Y 2B 19 28
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(4)
700aAA            W1KGLB         3A  O 19 40
700aAA  95                                  DSPATR(ND)
520cAA                                 19 61'(-) after #=Decrease'
     A                                 20  4'Reason code  . . . . .'
     A            W1CODE         2A  B 20 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
510 AA                                 20 34'Cost:'
510 AA  94                                  DSPATR(ND)
510aMA            W3COST         9Y 4O 20 40
510aDA*           W3COST         7Y 2O 20 40
510 AA                                      EDTCDE(4)
510 AA  94                                  DSPATR(ND)
     A                                 21  4'Memo . . . . . . . . .'
     A            W1MEMO        30A  B 21 28CHECK(LC)
PCK AA  24                                  DSPATR(RI)
PCK AA  04                                  DSPATR(PC)
PCK AA N24                                  DSPATR(UL)
700aAA            CMDLN1        79A  O 23  2COLOR(BLU)
700aDA*                                23  2'F4=Prompt  F5=Refresh  F9=Slot qua-
700aDA*                                     ntities  F12=Cancel  F20=Accept war-
700aDA*                                     ning'
700aDA*                                     COLOR(BLU)
BIR AA*
BIR AA          R SCREEN1B
BIR AA                                      CF04
700aAA N95                                  CA07
BIR AA                                      CF09
BIR AA                                      CF20
BIR AA  98                                  ALARM
BIR AA                                      CLRL(23)
BIR AA                                      OVERLAY
BIR AA                                  1  2'IT122.01'
BIR AA                                  1 30'Inventory Adjustments'
BIR AA                                      DSPATR(HI)
BIR AA                                  1 72DATE
BIR AA                                      EDTCDE(Y)
BIR AA                                  2 35'Detail Entry'
BIR AA                                      DSPATR(HI)
BIR AA                                  4  2'Whse/Dept  :'
BIR AA            WOWHSE        55A  O  4 15
BIR AA                                  5  2'Slot       :'
BIR AA            WODISP        12A  O  5 15
BIR AA                                  6  2'Slot type  :'
BIR AA            WOSTYP         5A  O  6 15
BIR AA                                  7  2'Status     :'
BIR AA            SSDESC        30A  O  7 15
BIR AA                                  8  2'Item       :'
BIR AA            WOITEM        55A  O  8 15
BIR AA            WOLBLS        12   O  9  2DSPATR(HI)
BIR AA            WOSITM        55   O  9 15DSPATR(HI)
BIR AA                                 11  2'Available slot qty  :'
BIR AA            W1INCQ        10A  O 11 24
BIR AA                                 11 36'(includes all open transactions)'
BIR AA                                 12  2'Adjustable slot qty :'
BIR AA            W1EXCQ        10A  O 12 24
BIR AA                                 12 36'(excludes open receiving transacti-
BIR AA                                      ons)'
BIR AA                                 14  2'Type changes, press Enter.'
BIR AA                                      COLOR(BLU)
BIR AA                                 16  4'Adjustment type  . . .'
BIR AA            W1TYPE         1A  B 16 28
BIR AA  21                                  DSPATR(RI)
BIR AA  01                                  DSPATR(PC)
BIR AA                                 16 59'1 = Increase/decrease'
BIR AA                                 17 59'2 = Replace'
BIR AA                                 18  4'Quantity . . . . . . .'
BIR AA            W1QT01         5Y 0B 18 28EDTCDE(M)
BIR AA  22                                  DSPATR(RI)
BIR AA  02                                  DSPATR(PC)
BIR AA N22                                  DSPATR(UL)
BIR AA            W1UM1          5A  O 18 34
BIR AA            W1QT02         3Y 0B 18 41EDTCDE(M)
BIR AA  29                                  DSPATR(RI)
BIR AA  09                                  DSPATR(PC)
BIR AA N29N92                               DSPATR(UL)
BIR AA  92                                  DSPATR(ND)
BIR AA  92                                  DSPATR(PR)
BIR AA            W1UM2          5A  O 18 45
BIR AA  92                                  DSPATR(ND)
BIR AA            W1QT03         3Y 0B 18 51EDTCDE(M)
BIR AA  30                                  DSPATR(RI)
BIR AA  10                                  DSPATR(PC)
BIR AA N30N93                               DSPATR(UL)
BIR AA  93                                  DSPATR(ND)
BIR AA  93                                  DSPATR(PR)
BIR AA            W1UM3          5A  O 18 55
BIR AA  93                                  DSPATR(ND)
BIR AA                                 18 61'(-) after #=Decrease'
BIR AA                                 19  4'Catch wt adjustment. .'
BIR AA            W1CWTR         9Y 2B 19 28
BIR AA  33                                  DSPATR(RI)
BIR AA N33N90                               DSPATR(UL)
BIR AA  33                                  DSPATR(PC)
BIR AA  95                                  DSPATR(ND)
BIR AA  95
BIR AAO 90                                  DSPATR(PR)
BIR AA                                      EDTCDE(4)
700aAA            W1KGLB         3A  O 19 40
700aAA  95                                  DSPATR(ND)
BIR AA                                 19 61'(-) after #=Decrease'
BIR AA                                 20  4'Reason code  . . . . .'
BIR AA            W1CODE         2A  B 20 28
BIR AA  23                                  DSPATR(RI)
BIR AA  03                                  DSPATR(PC)
BIR AA                                 20 34'Cost:'
BIR AA  94                                  DSPATR(ND)
BIR AA            W3COST         9Y 4O 20 40
BIR AA                                      EDTCDE(4)
BIR AA  94                                  DSPATR(ND)
BIR AA                                 21  4'Memo . . . . . . . . .'
BIR AA            W1MEMO        30A  B 21 28CHECK(LC)
BIR AA  24                                  DSPATR(RI)
BIR AA  04                                  DSPATR(PC)
BIR AA N24                                  DSPATR(UL)
BIR AA                                 22 47'|'
700aAA            CMDLN1        79A  O 23  2COLOR(BLU)
700aDA*                                23  2'F4=Prompt  F5=Refresh  F9=Slot qua-
700aDA*                                     ntities  F12=Cancel  F20=Accept war-
700aDA*                                     ning'
700aDA*                                     COLOR(BLU)
BIR AA*
     A          R SCREEN2
     A*%%TS  SD  19991125  064130  DAVE        REL-V4R2M0  5769-PW1
     A                                      CF04
     A                                      CF09
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'IT122.01'
     A                                  1 30'Inventory Adjustments'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  4  2'Warehouse :'
     A            WOWHSE        55A  O  4 14
     A                                  5  2'Slot      :'
     A            SLDISP        12A  O  5 14
     A                                  6  2'Slot type :'
     A            WOSTYP         5A  O  6 14
     A                                  7  2'Status    :'
     A            SSDESC        30A  O  7 14
     A                                  8  2'Item      :'
     A            WOITEM        55A  O  8 14
     A            WOLBLS        12   O  9  2
     A            WOSITM        55   O  9 15
     A                                 11  2'Available slot qty  :'
     A            W1INCQ        10A  O 11 24
     A                                 11 36'(includes all open transactions)'
     A                                 12  2'Adjustable slot qty :'
     A            W1EXCQ        10A  O 12 24
     A                                 12 36'(excludes open receiving transacti-
     A                                      ons)'
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 16  4'Adjustment type  . . .'
     A            W1TYPE         1A  B 16 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                 16 59'1 = Increase/decrease'
     A                                 17 59'2 = Replace'
     A                                 18  4'Quantity . . . . . . .'
     A            W2QTY          7A  B 18 28
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A            W1USTR        17A  O 18 40
     A                                 19  4'Catch wt adjustment. .'
     A            W1CWTR         9Y 2B 19 28
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  95                                  DSPATR(ND)
     A  95
     AO 90                                  DSPATR(PR)
     A                                      EDTCDE(3)
     A                                 20  4'Reason code  . . . . .'
     A            W1CODE         2A  B 20 28
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A                                 21  4'Memo . . . . . . . . .'
     A            W1MEMO        30A  B 21 28CHECK(LC)
     A                                 23  2'F4=Prompt  F5=Refresh  F9=Slot qua-
     A                                      ntities  F12=Cancel  F20=Accept war-
     A                                      ning'
     A                                      COLOR(BLU)
510 A **************************************************************************
510 A * Cost window                                                    *********
510 A **************************************************************************
510 AA          R SCREEN3
510 AA*%%TS  SD  20061030  140434  JEFF        REL-V5R3M0  5722-WDS
510 AA                                      WINDOW(5 10 6 20)
510 AA                                      OVERLAY
510 AA                                      WDWBORDER((*COLOR RED))
510 AA                                      WDWBORDER((*DSPATR RI))
510 AA                                      WDWBORDER((*CHAR '        '))
510 AA                                      WDWTITLE((*TEXT 'Cost Entry'))
510 AA                                      WDWTITLE((*TEXT 'F3=EXIT') *BOTTOM)
510 AA                                  2  1'Cost:'
510 AA                                      DSPATR(HI)
510 AA            #MSG          10A  O  4  1DSPATR(HI)
510aMA            W3COST         9Y 4B  2  7EDTCDE(L)
510aDA*           W3COST         7Y 2B  2  7EDTCDE(L)
510 AA*
510 AA          R DUMMY
510 AA                                      ASSUME
510 AA                                  5 60' '
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
