     A*%%TS  SD  20001117  125855  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A********************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  QDA100FM     Delete ALL Open P/O's
     A*  17 November 2000
     A*  Max Blacknik
     A*
     A********************************************************************
     A*
     A*  Revisions:
     A*
     A*
     A********************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*
     A*******************************************************************
     A          R SCREEN1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A*                                     OVERLAY
     A                                  1  2'QDA100.01'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 49'(Y=Yes, N=No)'
     A                                 23  2'F3=Exit   F4=Select   F5=Refresh  -
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN2
     A*%%TS  SD  20001117  125855  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(*ALL)
     A*                                     OVERLAY
     A                                  1  2'QDA100.02'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Press F20=CONTINUE to Delete ALL O-
     A                                      pen P/O''s or F3=Exit to Cancel.'
     A                                      COLOR(BLU)
     A                                  8  4'                            '
     A            W2WHSE         3Y 0B  8 34DSPATR(PR ND)
     A                                  9  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3  0O  9 34EDTCDE(4)
     A                                      DSPATR(PC)
     A                                 11  4'Place on job queue ? . . . .'
     A            W1JOBQ         1   O 11 34
     A                                 23  2'F3=Exit   F12=Cancel  F20=Continue'
     A                                      COLOR(BLU)
     A*
     A          R SCREEN3
     A*%%TS  SD  20001117  125855  MLBLACKNIK  REL-V4R4M0  5769-PW1
     A                                      CF20
     A  98                                  ALARM
     A                                      CLRL(23)
     A*                                     OVERLAY
     A                                  1  2'QDA100.03'
     A            RPTTTL        40A  O  1 21DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3 25'****  WARNING **** WARNING  ****'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  4 25'**** Final Chance to Cancel ****'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Press F20=CONTINUE to Delete ALL O-
     A                                      pen P/O''s or F3=Exit to Cancel.'
     A                                      COLOR(BLU)
     A                                  8  4'                            '
     A                                  9  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3  0O  9 34EDTCDE(4)
     A                                      DSPATR(PC)
     A                                 11  4'Place on job queue ? . . . .'
     A            W1JOBQ         1A  O 11 34
     A                                 11 49'(Y=Yes, N=No)'
     A                                 23  2'F3=Exit   F12=Cancel  F20=CONTINUE'
     A                                      COLOR(BLU)
