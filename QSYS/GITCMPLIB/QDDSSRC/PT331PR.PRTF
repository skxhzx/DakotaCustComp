     A*%%***********************************************************************
     A*
     A*   PT331PR  Print Open Put-away transactions
     A*   17 September, 1999
     A*   Hemant Kapadia
     A*
412 AA*  9.17.99 HNK  4.12
     A*  - Created
413 AF*    03/23/00  RH   4.13
     F*      - Add status, W1STAT
     F*      - Correct file from ER331 to PT331
414aAF*    06/28/00  RH   4.14a
     F*      - Add first pick slot, W1PICK
414bAF*    08/04/00  RH   4.14b
     F*      - Add trans date W1GDTE
416 AF*    09/07/01  HNK  4.16
     F*      - Add department.
416aAF*    09/18/01  RH   4.16a
     F*      - Add time to print out.
     A*
417 AF*    05/16/03  MLB  4.17
     F*      - Enh: Revised Detail 1 to support extended license plate
     A*        field.
700 AF*    02/02/16  RH   7.00
     F*      - Enh: Replace status with Item department.
     A*
     A********************************************************************
     A*  Client Custom Revisions:
     A*
700 AA*  12/06/16  JGD  7.00
     A*  - Enh: Add "Act" field (W1ACTV) to report item activity zone
     A*         (99, PR...).
     A*  - Delete output field "LILCNS" (15 char long) and add output
     A*    field "LIC#" (8 char long). Reason: to be able to insert
     A*    W1ACTV field above.
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    47
     A*
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            DTERNG        22A  O    52
     A                                   125
     A                                      DFT('PT331')
     A                                      SPACEA(001)
416 AA                                    52DFT('Department: ')
416 AA            W1WHDP         5A  O    64
416 AA                                      SPACEA(001)
     A*
     A* Print employee number and name
     A*
     A*         R EPHEAD
     A*                                     SPACEB(001)
     A*                                     SPACEA(002)
     A*                                    1'Employee:'
     A*           PTEMP#    R             +1REFFLD(PTEMP# ER331)
     A*                                     EDTCDE(Z)
     A*           WKNAME        40        +1
     A* 92                                75
     A*                                     '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      DFT('Trans #')
     A                                      UNDERLINE
     A                                    10
     A                                      DFT('License')
     A                                      UNDERLINE
700 AA  22                                20'Act'
700 AA  22                                  UNDERLINE
417 DA*                                   20
417 MA                                    26
     A                                      DFT('Item    ')
     A                                      UNDERLINE
417 DA*                                   37
417 MA                                    42
     A                                      DFT('Description')
     A                                      UNDERLINE
417 DA*                                   64
417 MA                                    69
     A                                      DFT('Slot    ')
     A                                      UNDERLINE
416aDA*                                   77
417 DA*                                   76
417 MA                                    82
     A                                      DFT('Quantity')
     A                                      UNDERLINE
416aDA*                                   90
417 DA*                                   88
417 MA                                    92
700aDA**                                    DFT('Status  ')
700aAA                                      DFT('Dept    ')
413 AA                                      UNDERLINE
416aDA*                                  105
416aMA                                   102
414aAA                                      DFT('Pick Slot')
414aAA                                      UNDERLINE
416aDA*                                  119
416aMA                                   115
417 DA*                                     DFT('DATE  ')
417 MA                                      DFT('Date  ')
414bAA                                      UNDERLINE
416aAA                                   125
417 DA*                                     DFT('TIME  ')
417 MA                                      DFT('Time  ')
416aAA                                      UNDERLINE
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            PTTRN#    R        O     1EDTCDE(3)
413 MA                                      REFFLD(PTTRN# *LIBL/PT331)
413 DA*                                     REFFLD(PTTRN# *LIBL/ER331)
700 DA N22        LILCNS    R        O    10
700 DA                                      REFFLD(LILCNS *LIBL/PT331)
413 DA*                                     REFFLD(LILCNS *LIBL/ER331)
700 MA  22        LIC#           8   O    10
700 AA  22        W1ACTV         2   O    20
417 DA*           W1ITEM        15   O    20
417 MA            W1ITEM        15   O    26
417 DA*           W1DESC        25   O    37
417 MA            W1DESC        25   O    42
417 DA*           W1DISP        12   O    64
417 MA            W1DISP        12   O    69
417 DA*           W1QTY          5  0O    76EDTCDE(3)
417 MA            W1QTY          5  0O    84EDTCDE(3)
417 DA*           W1STAT         8   O    88
700aDA*           W1STAT         8   O    92
700aMA            W1WHDP         5   O    92
414aAA            W1PICK        12   O   102
414bAA            W1GDTE         6S 0O   115EDTCDE(Y)
416aAA            W1GTIM         6  0O   125EDTWRD('  :  :  ')
     A*
     A          R TOTAL1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Total transactions:')
     A            TRNCNT         5  0O    25EDTCDE(3)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('PT331: End of Report')
     A*
