     A*%%***********************************************************************
     A*
     A*   PO330PR  PO Receiving Receipt
     A*   22 November, 2004
     A*   Roxanne Harrison
     A*
500 AA*  NOTE: compile at 82 columns
     A*  Revisions
     A*
500 AA*  05/16/05  RLP  5.00
500 AA*      -Enh: Added support for Super PO's
520 AA*  02/03/08  RH   5.20
520 AA*      -Enh: Added catch weight received.
530aAA*  12/30/09  RH   5.30a
530aAA*      -Enh: Added sequence #.
530bAA*  04/29/10  RH   5.30b
530bAA*      -Enh: Added PO qty excptions.
650a A*  09/23/14  GJA  6.50a
     A*      -Enh: Added PO barcode
     A*      **MUST COMPILE DEVTYPE(*AFPDS) FRONTMGN(0.0 0.2)
650b A*  10/03/14  GJA  6.50b
     A*      - Added tiexhigh to report -- DETAIL4
650c A*  01/29/15  LMC  6.50c
     A*      - Added * to the end of the detail line if
     A*      quantity received is different than quantity ordered.
     A*      This requires the quantity ordered/received fields to
     A*      be moved slightly.
650d A*  03/23/15  LMC  6.50d
     A*      - Moved * to pos 80.
650e A*  03/23/15  RH   6.50e
     A*      - Enh: Change SFCa mod to FOOT1S for SFC only
700a A*  08/03/15  GJA  7.00a
     A*      - Enh: Add pallet count to report -- DETAIL4
710a A*  09/06/18  RH   7.10a
     A*      - Enh: Add P#MEMO field to print data from POHDRM
750A A*  05/04/21  RH  MERGE ORIGINAL MOD
     A*  03/28/16  GJA  7.00b
     A*      - Added pick slot to report
     A****************************************************************
CLD AA*  Client Custom Revisions: Cityline
     A*
CLD AA*    09/30/10  RBD  CLD
     A*      - Added format DETAIL3 to print CLAMS harvest data.
     A*      - Compile with COPIES(2).
     A*
     A****************************************************************
EFC AA*  Client Custom Revisions: Eastland
     A*
EFCaAA*    06/06/11  RH   EFCa
     A*      - Added format TOTAL1A to print custom line.
     A*
     A****************************************************************
PFC AA*  Client Custom Revisions: Presto
     A*
PFCaAA*    10/07/14  GJA  PFCa
     A*      - Added format TOTAL1PFC to print custom line.
     A*
     A*      **MUST COMPILE DEVTYPE(*AFPDS) FRONTMGN(0.0 0.2)
     A*
     A****************************************************************
     A*  Client Custom Revisions: Springfield
     A*
SFGa A*    11/28/11  GJA  SFGa
     A*      - Added short/damage/over/pestfree/clean
     A*
SFG AA*  NOTE: COMPILE TO DRAWER(0002)
     A****************************************************************
PANaAA*  Client Custom Revisions: Panos
PANa A*    10/03/17  RH   PANa
    AA*      - Added DETAIL5 to print Vendor Item # only.
     A****************************************************************
     A*  Client Custom Revisions: Accardi
ACCa A*    12/10/18  RH   ACCa
     A*      - create DETAIL1A,remove description.
     A*      - Create DETAIL5A,print 40 desc and vendor item #
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40   O    20
     A*
     A                                    72
     A                                      DFT('Page')
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
500 MA            WHSE          30A  O    25
     A                                    72
     A                                      DFT('PO330')
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A          R HEAD2
     A*%%***********************************************************************
     A                                      SPACEA(002)
500 DA*           WHSE          30A  O     2
500 DA*                                   40
500 MA                                     6
     A                                      DFT('PO:')
500 DA*           PHPO           9A  O    44
500 MA            PHPO           9A  O    10
500 AA                                    24
500 AA                                      DFT('Status:')
500 DA*           P#DESC        11A  O    55
500 MA            P#DESC        11A  O    32
710aAA            P#MEMO        15A  O    50
     A                                      SPACEA(001)
     A                                     2DFT('Vendor:')
     A            PHVNAM        40   O    10
     A                                      SPACEA(001)
     A            VADR1         70   O    10
     A                                      SPACEA(001)
     A            VADR2         40   O    10
650aAA                                      SPACEA(001)
     A            VAL39          9A  O    20BARCODE(CODE128 *NOHRI)
650aAA*           VAL39          9A  O    20BARCODE(PDF417 (*PDF417 5 *MIN 3))
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
500 MA  91                                60DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A*
530aAA                                     1
530aAA                                      DFT('Seq #')
530aAA                                      UNDERLINE
530aDA*                                    1
530aMA                                     7
     A                                      DFT('Item Number    ')
     A                                      UNDERLINE
520  A*                                   19
530aDA*                                   17
530aMA                                    23
     A                                      DFT('Description             ')
     A                                      UNDERLINE
520  A                                    48
520  A                                      DFT('Weight    ')
520  A                                      UNDERLINE
520  A*                                   52
650cDA*                                   60
650cMA                                    59
650cDA*                                     DFT('Ordered')
650cMA                                      DFT('Ordered   ')
     A                                      UNDERLINE
650cDA*                                   72
650cMA                                    70
650cDA*                                     DFT('Received')
650cMA                                      DFT('Received  ')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
530aAA            PDSEQ     R        O     1REFFLD(PDSEQ PODTL)
530aAA                                      EDTCDE(Z)
530aDA*           PDITEM    R        O     1
530aMA            PDITEM    R        O     7
     A                                      REFFLD(PDITEM PODTL)
520  A*           ITDESC    R        O    19
530aDA*           ITDESC    R        O    17
530aDA*                                     REFFLD(ITDESC *LIBL/PIRITEM)
530aMA            W1DESC        25A  O    23
520  A            W1WT           9S 2O    48EDTCDE(3)
520  A*           W1ORD         10A  O    52
650cDA*           W1ORD         10A  O    60
650cMA            W1ORD         10A  O    59
     A*                                     EDTCDE(M)
650cDA*           W1RCV         10A  O    72
650cMA            W1RCV         10A  O    70
650dDA* 62                                81'*'
650dMA  62                                80'*'
     A*
ACCaAA*
ACCaAA* Print detail line
ACCaAA*
ACCaAA          R DETAIL1A
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
ACCaAA            PDSEQ     R        O     1REFFLD(PDSEQ PODTL)
ACCaAA                                      EDTCDE(Z)
ACCaAA            PDITEM    R        O     7
     A                                      REFFLD(PDITEM PODTL)
ACCaAA                                    25'Mfr#:'
ACCaAA            ITVIT#        15   O    31
ACCaAA            W1WT           9S 2O    48EDTCDE(3)
ACCaAA            W1ORD         10A  O    59
     A*                                     EDTCDE(M)
ACCaAA            W1RCV         10A  O    70
ACCaAA  62                                80'*'
ACCaAA*
ACCaAA          R DETAIL5A
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
ACCaAA            W1DESCA       73A  O     7
ACCaAA**                                  60'Mfr#'
ACCaAA**          ITVIT#        15   O    65
     A*
ACCaAA*%%***********************************************************************
650B A* Print tie high
     A*
     A          R DETAIL4
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
750ADA***                                 10'Mfr#:'
750ADA***         ITVIT#        15   O    16
750ADA***         ITWHDP         5   O    35
700bMA            ITWHDP         5   O    10
700bAA            SLDISP        12   O    16
700bMA                                    29'Mfr#:'
700bMA            ITVIT#        15   O    35
     A                                    50'Tie High:'
     A            IDTIE          4  0O    60EDTCDE(Z)
     A                                    64'x'
     A            IDHIGH         2  0O    66EDTCDE(Z)
700aAA                                    70'Plt:'
700aAA            PUTPLT         3  0O    74EDTCDE(Z)
     A            ITUM1          2   O    77
     A*
PANa A          R DETAIL5
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    10'Mfr#:'
     A            ITVIT#        15   O    16
     A            ITWHDP         5   O    35
     A*
     A*
530b A* Print detail line
     A*
530b A          R DETAIL2
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
530bAA            W1EXCP        24   O    23
530bAA            W1QEXC        10A  O    48
     A*%%***********************************************************************
CLD AA* Print CLAMS harvest data
CLD AA*
CLD AA          R DETAIL3
CLD AA*
CLD AA                                      SPACEA(001)
CLD AA            CLAMS1        75   O     7
CLD AA                                      SPACEA(001)
CLD AA            CLAMS2        75   O     7
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* Print total line
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                     1DFT('Date:    ')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Receiver:')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Shipper: ')
     A                                    10DFT('___________________')
     A                                      SPACEA(002)
     A                                     1DFT('Lumper: ')
     A                                    10DFT('___________________')
PFCaAA***************************************************************  *********
PFCaAA          R TOTAL1PFC
PFCa A                                      SPACEB(002)
PFCa A                                      SPACEA(002)
700aAA                                    41DFT('Pallets:')
700aAA            TOTPLT         4  0O    51EDTCDE(Z)
PFCa A                                      SPACEA(002)
PFCa A                                     1DFT('Dock#')
PFCa A                                     7DFT('_____')
PFCa A                                    13DFT('Arrival Time:')
PFCa A                                    27DFT('_________')
PFCa A                                    37DFT('Trailer Temp:')
PFCa A                                    51DFT('_________')
PFCa A                                    63DFT('Prdt Temp:')
PFCa A                                    74DFT('_________')
PFCa A                                      SPACEA(002)
PFCa A                                     1DFT('Temp Gun#')
PFCa A                                    11DFT('_________')
PFCa A                                    21DFT('Receipt#')
PFCa A                                    30DFT('_________')
PFCa A                                    40DFT('Pallets In:')
PFCa A                                    51DFT('_______')
PFCa A                                    59DFT('Brkdwn Amt:')
PFCa A                                    70DFT('_________')
EFCaAA*%%***********************************************************************
EFCaAA          R TOTAL1A
EFCa A                                      SPACEB(002)
EFCa A                                      SPACEA(002)
EFCa A                                     1DFT('Container')
EFCa A                                    10DFT('___________________')
EFCa A                                      SPACEA(002)
EFCa A                                     1DFT('Period/Wk')
EFCa A                                    10DFT('___________________')
     A*%%***********************************************************************
     A*
500 AA* Print accumulated totals
     A*
500 AA          R TOTAL2
     A*%%***********************************************************************
500 AA                                      SPACEB(001)
500 AA                                      SPACEA(002)
500 AA                                    40DFT('Totals:')
520  A*                                   52DFT('__________')
650cDA*                                   60DFT('__________')
650cMA                                    59DFT('__________')
650cDA*                                   72DFT('__________')
650cMA                                    70DFT('__________')
500 AA                                      SPACEA(001)
520  A*           W1OTOT        10A  O    52
650cDA*           W1OTOT        10A  O    60
650cMA            W1OTOT        10A  O    59
650cDA*           W1RTOT        10A  O    72
650cMA            W1RTOT        10A  O    70
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1
     A                                      DFT('PO330: End of Report')
     A*
     A          R FOOT1S
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
SFGaAA                                     5'Short________'
SFGaAA                                    20'Over_________'
SFGaAA                                    35'Damage_______'
SFGaAA                                    50'Pest Free____'
SFGaAA                                    65'Clean________'
SFGaAA                                      SPACEA(002)
     A                                     1
     A                                      DFT('PO330: End of Report')
     A*%%***********************************************************************
     A*
