     A*%%TS  SD  20090401  022642  CHUCKM      REL-V6R1M0  5761-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd. #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  OP330FM   PIR Options - Runner Flag
     A*  28 October 2005
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    10/28/05  MLB  5.00
     A*      - Created.
     A*
500aAA*    10/28/05  MLB  5.00a
     A*      - Enh: Add te RUNNER entries (Y/N).
     A*
500bAA*    02/15/06  JCJ  5.00b
     A*      - Enh: Add to RUNNER entries (Y/N).
     A*
520 AA*    01/20/09  RBD  5.20
     A*      - Enh: Added new *RUNNER entries: OPRSCN, OPRVFS,
     A*        OPRVFQ, AND OPRLOA.
     A*
520AAA*    04/01/09  CWM  5.20A
     A*      - Enh: Put Runner options in same order as OP310.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20090401  022642  CHUCKM      REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'OP330.01'
     A                                  1 34'Runner Flag Options'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'Scan tran# or label# to start pick-
     A                                       . . . . . . . . .'
     A            W1SCN          1A  B  4 57
     A  32                                  DSPATR(RI)
     A N32N90                               DSPATR(UL)
     A  12                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  4 62'1=tran#, 2=label#'
     A                                 16  4'Verify pick slot . . . . . . . . .-
     A                                       . . . . . . . . .'
     A            W1VFS          1A  B 16 57
     A  33                                  DSPATR(RI)
     A N33N90                               DSPATR(UL)
     A  13                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  5 62'Y=Yes, N=No'
     A                                 10  4'Verify qty found . . . . . . . . .-
     A                                       . . . . . . . . .'
     A            W1VFQ          1A  B 10 57
     A  34                                  DSPATR(RI)
     A N34N90                               DSPATR(UL)
     A  14                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  8 62'Y=Yes, N=No'
     A                                 11  4'Using loader module  . . . . . . .-
     A                                       . . . . . . . . .'
     A            W1LOA          1A  B 11 57
     A  35                                  DSPATR(RI)
     A N35N90                               DSPATR(UL)
     A  15                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  7 62'Y=Yes, N=No'
     A                                  6  4'Create Runner entries for shorts/o-
     A                                      uts. . . . . . . .'
     A            W1CRTM         1A  B  6 57
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 10 62'Y=Yes, N=No'
     A            W1UPLR         1A  B  5 57
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  6 62'Y=Yes, N=No'
     A                                 12  4'Replenish flagged items before pri-
     A                                      nt report. . . . .'
     A            W1RPLF         1A  B 12 57
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 11 62'Y=Yes, N=No'
     A                                  7  4'Create Runner entries for Merge re-
     A                                      cords. . . . . . .'
     A            W1MRGF         1A  B  7 57
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 62'Y=Yes, N=No'
     A                                  9  4'Clear Runner flags if qty still sh-
     A                                      ort/out. . . . . .'
     A            W1CLRF         1A  B  9 57
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9 62'Y=Yes, N=No'
     A                                 13  4'Send break message to user when ne-
     A                                      w RUNNER'
     A                                 14  6'task has been assigned . . . . . .-
     A                                       . . . . . . . .'
     A            W1RNRM         1A  B 14 57
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 14 62'Y=Yes, N=No'
     A                                 15  4'Update label quantity at flag clos-
     A                                      e. . . . . . . . .'
     A            W1CLSF         1A  B 15 57
     A  27                                  DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 15 62'Y=Yes, N=No'
     A                                  8  4'Create Runner entry after slot mar-
     A                                      ked out. . . . . .'
     A            W1OUTF         1A  B  8 57
     A  28                                  DSPATR(RI)
     A N28N90                               DSPATR(UL)
     A  08                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 16 62'Y=Yes, N=No'
     A                                 17  4'Print Runner pick list . . . . . .-
     A                                       . . . . . . . . .'
     A            W1RPCK         1A  B 17 57
     A  29                                  DSPATR(RI)
     A N29N90                               DSPATR(UL)
     A  09                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 17 62'Y=Yes, N=No'
     A                                 18  4'Print Runner pick labels . . . . .-
     A                                       . . . . . . . . .'
     A            W1RLBL         1A  B 18 57
     A  30                                  DSPATR(RI)
     A N30N90                               DSPATR(UL)
     A  10                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 18 62'0=No'
     A                                 19 62'1=Zebra Print'
     A                                 20 62'2=Belt Printer'
     A                                 21  4'Allow shorts to be filled with bre-
     A                                      akdown units . . .'
     A            W1RBDU         1A  B 21 57
     A  31                                  DSPATR(RI)
     A N31N90                               DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 21 62'Y=Yes, N=No'
     A*
     A                                 23  2'F3=Exit  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  5  4'Update label with qty picked durin-
     A                                      g flag entry . . .'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
