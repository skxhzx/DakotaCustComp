     A****************************************************************
     A*
     A* LOAD60HPR  Verified Returned Trailer Equipment - Summry Report.
     A*                                                  By Equip Desc
     A* January 22, 2009
     A* Jose G. Duque
     A*
     A*  Revisions
     A*
     F*    03/10/11  JGD  6.40
     F*      - Created
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*----------------------------------------------------------------
     A          R HEAD1
     A*----------------------------------------------------------------
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A                                    16
     A                                      DFT('(Equipment Summary) Returne-
     A                                      d Trailer Equipment')
     A                                    72
     A                                      DFT('Page')
     A            PAGE           4S 0O    76
     A                                      EDTCDE(Z)
      *
     A                                      SPACEA(001)
     A            FRMDTE         6S 0O    24EDTCDE(Y)
      *
     A                                    34
     A                                      DFT('Thru')
      *
     A            TODTE          6S 0O    40EDTCDE(Y)
     A*
     A                                      SPACEA(001)
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    22
     A                                    72
     A                                      DFT('LOADR60H')
     A                                      SPACEA(002)
     A* Print column headings
     A*----------------------------------------------------------------
     A          R COLUMNS
     A*----------------------------------------------------------------
      *
     A                                     1'Equipment'
      *
     A                                    18'Qty'
      *
     A                                    26'Qty'
      *
     A                                      SPACEA(001)
      *
     A                                     1'Description    '
     A                                      UNDERLINE
      *
     A                                    18'Short'
     A                                      UNDERLINE
      *
     A                                    26'Over '
     A                                      UNDERLINE
      *
     A                                    32'Diff   '
     A                                      UNDERLINE
      *----------------------------------------------------------------
     A          R DETAIL1
     A*----------------------------------------------------------------
      *
     A                                      SPACEB(001)
     A            W1DESC        15A        1
      *
     A            W1SHRT         6S 0     17EDTWRD('    0 ')
      *
     A            W1OVER         6S 0     25EDTWRD('    0 ')
      *
     A            W1DIFF         6S 0     32EDTCDE(J)
      *
     A*----------------------------------------------------------------
     A          R TOTAL1
     A*----------------------------------------------------------------
     A                                      SPACEB(001)
     A                                     2
     A                                      DFT('User: ')
     A            SAVUSR         5A  O     8
     A                                    17
     A                                      DFT('Pallet: ')
     A            CNTR1          5S 0O    25
     A                                      EDTCDE(Z)
     A                                    39
     A                                      DFT('Merge: ')
     A            CNTR2          5S 0O    46
     A                                      EDTCDE(Z)
     A                                    60
     A                                      DFT('Flags: ')
     A            CNTR3          5S 0O    67
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*----------------------------------------------------------------
     A          R SKIP1
     A*----------------------------------------------------------------
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*----------------------------------------------------------------
     A          R FOOT1
     A*----------------------------------------------------------------
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('LOADR60H: End of Report   ')
     A*
