     A*%%TS  SD  20000523  171058  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  PT830FM   Change transaction utility
     A*  23 May 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
414 AA*    05/23/00  DAS  4.14
     A*      - Created
     A*
415 AA*    04/24/01  DAS  4.15
     A*      - Added window PASWRD
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000523  171058  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'PT830.01'
     A                                  1 28'Change Transaction Utility'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Transacton number . . . . . .'
     A            W1TRN#         7Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000523  165930  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'PT830.02'
     A                                  1 32'Transaction Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  4  4'Tran#'
     A            W2TRN#         7Y 0O  4 10EDTCDE(Z)
     A                                  6  4'Stat:'
     A            W2STAT         1A  B  6 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOSDSC        30A  O  6 13
     A                                  7  4'Empl:'
     A            W2EMP#         5Y 0B  7 10EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 18'Shft:'
     A            W2SHFT         6A  B  7 24DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 32'Crew:'
     A            W2CREW         6A  B  7 38DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOEDSC        50A  O  8 10
     A                                 10  4'Task:'
     A            W2TASK         6A  B 10 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 18'Cat:'
     A            W2CAT          6A  B 10 23DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 31'Sub:'
     A            W2SUB          6A  B 10 36DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 44'Grp1:'
     A            W2GRP1         1A  B 10 50DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 53'Grp2:'
     A            W2GRP2         1A  B 10 59DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 10 62'RtPo:'
     A            W2RTPO         9A  B 10 68DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 11  4'Whse:'
     A            W2WHSE         3Y 0B 11 10EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 11 17'Whdp:'
     A            W2WHDP         5A  B 11 23DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 11 30'Styp:'
     A            W2STYP         1A  B 11 36DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13  4'Pcs:'
     A            W2PCS          5Y 0B 13  9EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 17'Asl:'
     A            W2AISL         5Y 0B 13 22EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 30'Qty1:'
     A            W2QTY1         5Y 0B 13 36EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 44'Qty2:'
     A            W2QTY2         5Y 0B 13 50EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13 58'Qty3:'
     A            W2QTY3         5Y 0B 13 64EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 14  4'Cube:'
     A            W2CUBE         9Y 3B 14 10EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 14 22'Wgt:'
     A            W2SWGT         9Y 2B 14 27EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 16  4'Generated:'
     A            W2GDTE         8Y 0B 16 15EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2GTIM         6Y 0B 16 27EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 16 46'Rpt Dte  :'
     A            W2RDTE         8  0B 16 57
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 17  4'Started  :'
     A            W2SDTE         8Y 0B 17 15EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STIM         6Y 0B 17 27EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 18  4'Ended    :'
     A            W2EDTE         8Y 0B 18 15EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2ETIM         6Y 0B 18 27EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 18 36'Type:'
     A            W2ETYP         1A  B 18 42DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 19  4'Closed   :'
     A            W2CDTE         8Y 0B 19 15EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2CTIM         6Y 0B 19 27EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 21  4'Direct Min:'
     A            W2DMIN         5Y 0B 21 16EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 21 24'Indirect'
     A                                 21 33'Min:'
     A            W2IMIN         5Y 0B 21 38EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20000523  154917  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'PT830.03'
     A                                  1 32'Transaction Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  8 33'Tran#'
     A            W2TRN#         7  0O  8 39
     A                                 11 27'Press F10 to Confirm Update'
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A*%%TS  SD  20010424  095629  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
