     A*%%TS  SD  20060109  114011  MLBLACKNIK  REL-V5R3M0  5722-WDS
     A*------------------------------------------------------------------*
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*------------------------------------------------------------------*
     A*
     A*  OR647FM  Print Pallet Labels - Display
     A*  Max Blacknik
     A*
     A*------------------------------------------------------------------*
     A*  Revisions
     A*
500 AA*    01/09/06  MLB  5.00
     A*      - Created.
500aAA*    01/27/06  JCJ  5.00a
     A*      - Added new fields to view3
500bAA*    03/13/06  HNK  5.00b
     A*      - Added employee number to view 1,2 and 3
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA08
     A                                      CA12
     A                                      CF16
     A                                      CA07
     A          R TITLE
     A                                      OVERLAY
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  1  2'OR647 '
     A                                  1 32'Pallet Transactions'
     A                                      DSPATR(HI)
     A                                  2 35'List Display'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            PTDMY1         1A  O  1  2
     A                                  2  2'Warehouse:'
     A            WOWHSE        40A  O  2 13
     A                                  3  2'Route:'
     A            WORTE          5A  O  3 13
     A                                  3 43'Position to transaction . .'
     A            PTINP1         7Y 0B  3 72EDTCDE(4)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A* 85                                  SFLNXTCHG
     A          R DSPSFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W1TRN#    R        O 11  7REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
     A            W1HTR#    R        O 11 16REFFLD(PTREC/PTTRN# *LIBL/PIRTRAN)
     A                                      EDTCDE(3)
500bAA            W1EMP#    R        O 11 25REFFLD(PTREC/PTEMP# *LIBL/PIRTRAN)
500bAA                                      EDTCDE(Z)
500bMA            W1DECA         9A  O 11 32
500bMA            W1TCKN         2Y 0O 11 44
500bMA            W1BTYP         1A  O 11 51
500bMA            W1LLTR         2A  O 11 57
500bMA            W1NRPL         1Y 0O 11 64
500bMA            W1GOAL         6Y 0O 11 70EDTWRD('0  :  :  ')
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9  7'Dakota'
     A                                      DSPATR(HI)
     A                                  9 16'Host'
     A                                      DSPATR(HI)
500bAA                                  9 25'Emply'
500bAA                                      DSPATR(HI)
500bMA                                  9 42'Ticket'
     A                                      DSPATR(HI)
500bMA                                  9 50'Brk'
     A                                      DSPATR(HI)
500bMA                                  9 56'Load'
     A                                      DSPATR(HI)
500bMA                                  9 62'Nbr'
     A                                      DSPATR(HI)
500bMA                                  9 72'Goal'
     A                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Doc Nbr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bAA                                 10 25' Nbr '
500bAA                                      DSPATR(HI)
500bMA                                 10 32'DeCA Pull'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 42' Nbr  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 50'Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 56'Ltr '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 62'Labels'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 70'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2TRN#         7Y 0O 11  7EDTCDE(3)
     A            W2HTR#         7Y 0O 11 16EDTCDE(3)
500bAA            W2EMP#    R        O 11 25REFFLD(PTREC/PTEMP# *LIBL/PIRTRAN)
500bAA                                      EDTCDE(Z)
500bMA            W2PCS          5Y 0O 11 33EDTCDE(3)
500bMA            W2CUBE         7Y 3O 11 40EDTCDE(3)
500bMA            W2SWGT         9Y 2O 11 50EDTCDE(3)
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  9  7'Dakota'
     A                                      DSPATR(HI)
     A                                  9 16'Host'
     A                                      DSPATR(HI)
500bAA                                  9 25'Emply'
500bAA                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Trans #'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 16'Doc Nbr'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bAA                                 10 25' Nbr '
500bAA                                      DSPATR(HI)
500bAA                                      DSPATR(UL)
500bMA                                 10 32'Pieces'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 40'    Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
500bMA                                 10 50'    Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
500aAA            W3TRN#         7Y 0O 11  7EDTCDE(3)
500aAA            W3HTR#         7Y 0O 11 16EDTCDE(3)
500bAA            W3EMP#    R        O 11 25REFFLD(PTREC/PTEMP# *LIBL/PIRTRAN)
500bAA                                      EDTCDE(Z)
500bMA            W3QALC         5Y 0O 11 32EDTCDE(3)
500bMA            W3QPCK         5Y 0O 11 41EDTCDE(3)
500bMA            W3QBCK         5Y 0O 11 51EDTCDE(3)
500bMA            W3PFLG         1   O 11 62
500bMA            W3CFLG         1   O 11 72
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10
     A                                      CF11
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
500aAA                                  9  7'Dakota'
500aAA                                      DSPATR(HI)
500aAA                                  9 16'Host'
500aAA                                      DSPATR(HI)
500bAA                                  9 25'Emply'
500bAA                                      DSPATR(HI)
500bMA                                  9 32'Total '
500aAA                                      DSPATR(HI)
500bMA                                  9 41'Actual'
500aAA                                      DSPATR(HI)
500bMA                                  9 51'Total '
500aAA                                      DSPATR(HI)
500bMA                                  9 60'Pick'
500aAA                                      DSPATR(HI)
500bMA                                  9 70'Flag'
500aAA                                      DSPATR(HI)
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
500aAA                                 10  7'Trans #'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500aAA                                 10 16'Doc Nbr'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500bAA                                 10 25' Nbr '
500bAA                                      DSPATR(HI)
500bAA                                      DSPATR(UL)
500bMA                                 10 32'Ord Qty'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500bMA                                 10 41'Pick Qty'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500bMA                                 10 51'Qty Bck'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500bMA                                 10 60'Complete'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
500aAA                                 10 63'Complete'
500aAA                                      DSPATR(HI)
500aAA                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(06)
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            OPTLN2        75A  O  3  2COLOR(BLU)
     A            OPTLN1        75A  O  2  2COLOR(BLU)
     A*                                 4  2' '
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A                                      LOCK
     A                                      SLNO(22)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A            CMDLN2        79   O  2  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(21)
     A                                      OVERLAY
     A            MORELN         7A  O  1 73DSPATR(HI)
     A*
     A*    Delete Display Instructions
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 6=Print.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
