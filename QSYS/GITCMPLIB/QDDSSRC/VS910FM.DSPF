500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A*  VS910FM      SLOT LOOKUP FOR WHSE/DEPT/PHYSICAL SLOT
     F*  03 February, 1998
     F*  Roxanne Harrison
     A*
     A********************************************************************
     A*
     A*  Revisions
     A*
500 AA*   06/04/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A********************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R TITLE
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'VS910.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            FILL1         78A  O  2  2
     A                                  1 31'Virtual slot Lookup'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
     A          R POSTNREC
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD001        75A  O  1  2
     A            WDCODE         5A  O  3 13
     A                                  2  2'Warehouse:'
     A                                      DSPATR(HI)
     A                                  3  7'Dept:'
     A                                      DSPATR(HI)
     A            WOWHSE        35A  O  2 13
     A                                  5 44'Position to slot  . . .'
     A            PTINP1        12A  B  5 69
     A            WDDESC        30A  O  3 20
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 12  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15   O 12  7
     A            W1DESC        30   O 12 24
     A            VSDISP        12A  O 12 55
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 11  2'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  7'Item           '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 24'Description                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 55'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  19940922  164428  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(08)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  2  4'1=Select                          -
     A                                       '
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19931115  111104  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(23)
     A            W1CMD         75   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A            MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR ALL LINES AFTER TITLE
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
500 AA*
500 AA*****************************************************************
500 AA* Spanish Screens
500 AA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R TITLESP
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                  1  2'VS910.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            FILL1         78A  O  2  2
     A                                  1 31'Buscar slot virtual'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* POSITION TO RECORD
     A*
500 AA*----------------------------------------------------------------
500 AA          R POSTNRSP
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(03)
     A                                      OVERLAY
     A            FLD001        75A  O  1  2
     A            WDCODE         5A  O  3 13
     A                                  2  2'Almacen  :'
     A                                      DSPATR(HI)
     A                                  3  7'Dept:'
     A                                      DSPATR(HI)
     A            WOWHSE        35A  O  2 13
     A                                  5 44'Poner a slot  . . . . .'
     A            PTINP1        12A  B  5 69
     A            WDDESC        30A  O  3 20
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFLSP                  SFL
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B 12  3
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SLITEM        15   O 12  7
     A            W1DESC        30   O 12 24
     A            VSDISP        12A  O 12 55
     A*
500 AA*----------------------------------------------------------------
500 AA          R DSPSFCSP                  SFLCTL(DSPSFLSP)
     A*%%TS  SD  19940921  103625  HEMANT      REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A  09                                  CF09
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                 11  2'Opc'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 11  7'Producto       '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 24'Descripcion                   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 55'Slot        '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
500 AA*----------------------------------------------------------------
500 AA          R OPTRECSP
     A*%%TS  SD  19940922  164428  HEMANT      REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      SLNO(08)
     A                                      OVERLAY
     A                                  1  2'Escriba opciones, oprima ENTER'
     A                                      COLOR(BLU)
     A                                  2  4'1=Seleccionar                     -
     A                                       '
     A                                      COLOR(BLU)
