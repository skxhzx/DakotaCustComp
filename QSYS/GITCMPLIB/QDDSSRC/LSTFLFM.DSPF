     A*%%TS  SD  19940207  153849  MLBLACKNIK  REL-V2R3M0  5738-PW1
     A* LSTFLP - LIST FILE LAYOUT
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CF03
     A          R FKEYS
     A                                      OVERLAY
     A                                 24  3'F3=Exit'
     A*
     A* SUBFILE PANEL
     A          R SUBFILE                   SFL
     A                                      TEXT('Subfile body')
     A            TXT20         20A  O 12  4DSPATR(HI)
    DA*                                12 31'P'
    MA            FLTYP          1A    12 31
     A                                      DSPATR(HI)
     A N41                                  DSPATR(ND)
     A            FROM           5Y 0O 12 35EDTCDE(Z)
     A                                      DSPATR(HI)
     A            TO             5Y 0O 12 43EDTCDE(Z)
     A                                      DSPATR(HI)
     A            FLDB           5Y 0O 12 54EDTCDE(Z)
     A                                      DSPATR(HI)
     A            FLDP           2Y 0O 12 60EDTCDE(3)
     A                                      DSPATR(HI)
     A N40                                  DSPATR(ND)
     A            FLDE          10A  O 12 68DSPATR(HI)
     A*
     A* SUBFILE CONTROL
     A          R SUBCTL                    SFLCTL(SUBFILE)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0011)
     A                                      TEXT('Subfile Control')
     A                                      ROLLUP(36 'ROLL UP')
     A                                      OVERLAY
     A N55N56                               SFLDSP
     A N55                                  SFLDSPCTL
     A  55                                  SFLCLR
     A  71                                  SFLEND(*MORE)
     A                                  1  3DATE
     A                                      EDTCDE(Y)
     A                                  1 13TIME
     A                                  1 33'FILE LAYOUT'
     A                                      DSPATR(HI)
     A                                  1 71'LSTFL'
     A                                  3  3'FILE NAME - '
     A            FILE          10A  O  3 16DSPATR(HI)
     A            TEXT          50A  O  3 28DSPATR(HI)
     A                                  4  3'FILE TYPE - '
     A            TYPN           8A  O  4 16DSPATR(HI)
     A                                  5  5'LIBRARY - '
     A            LIB           10A  O  5 16DSPATR(HI)
     A                                  5 28'RECORD FORMAT -'
     A            NAME          10A  O  5 44DSPATR(HI)
     A                                  5 57'FILE LENGTH -'
     A            RLEN           5Y 0O  5 71EDTCDE(Z)
     A                                      DSPATR(HI)
     A                                  6 30'FILE ACCESS -'
     A            ACCES          7A  O  6 44DSPATR(HI)
     A  45                              6 58'KEY LENGTH -'
     A  45        MXKL           3Y 0O  6 71EDTCDE(Z)
     A                                      DSPATR(HI)
     A  45                              7  3'KEY FIELDS -'
     A  45        KEYS          60A  O  7 16DSPATR(HI)
     A  45        KEYS1         60A  O  8 16DSPATR(HI)
     A  45        KEYS2         60A  O  9 16DSPATR(HI)
     A                                 10  4'TEXT'
     A                                      DSPATR(UL)
     A                                 10 36'FROM'
     A                                      DSPATR(UL)
     A                                 10 46'TO'
     A                                      DSPATR(UL)
     A                                 10 55'LENGTH'
     A                                      DSPATR(UL)
     A                                 10 68'FIELD NAME'
     A                                      DSPATR(UL)
