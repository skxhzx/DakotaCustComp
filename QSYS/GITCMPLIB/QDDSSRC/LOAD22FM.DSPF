     A*%%TS  SD  20070613  144106  BFCTEST1    REL-V5R3M0  5722-WDS
     A*****************************************************************
     A*
     A*  LOAD22FM RF Loader Module - Close Route.
     A*  June 13, 2007
     A*  Jose Duque
     A*
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA04
     A                                      CA06
     A                                      CA07
     A                                      CA05
     A                                      CA08
     A                                      CA10
     A                                      CA11
     A                                      CA12
     A                                      CA13
     A                                      CF17
     A                                      CF18
     A                                      CF20
     A          R TITLE
     A*%%TS  SD  20060524  082853  JOSED       REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                  1  2'Loadr22'
     A                                  1 10'Close Route'
     A                                      DSPATR(HI)
     A*****************************************************************
     A*
     A* Position to record
     A*
     A          R POSTNREC
     A*%%TS  SD  20070613  133913  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      SLNO(02)
     A                                      OVERLAY
     A                                  1  2'Route # . .'
     A            PTINP2         5A  B  1 14
     A  02                                  DSPATR(PC)
     A*****************************************************************
     A*
     A* Subfile for list display
     A*
     A          R DSPSFL                    SFL
     A*%%TS  SD  20070111  163408  JOSED       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B  5  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LHRTE     R        O  5  6REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W1STAT         5A  O  5 12
     A            W1DOOR         3A  O  5 18
     A            W1FLAG         2Y 0O  5 71EDTCDE(Z)
     A                                      DSPATR(ND)
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20061129  124046  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0007)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                  4  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  6'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 12'Stat '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4 18'Door'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 2
     A*
     A          R VW2SFL                    SFL
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            LHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            W2STRD         6Y 0O 11 14EDTCDE(Y)
     A            LHSTRT    R        O 11 23REFFLD(RHREC/RHIMPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2ENDD         6Y 0O 11 33EDTCDE(Y)
     A            LHENDT    R        O 11 42REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2LODT    R        O 11 52REFFLD(RHREC/RHBLDT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W2SUP         10   O 11 63
     A*
     A          R VW2SFC                    SFLCTL(VW2SFL)
     A*%%TS  SD  20061108  102705  JOSED       REL-V5R3M0  5722-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0007)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10 14'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 23'  Time  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 14'---- Started ----'
     A                                      DSPATR(HI)
     A                                 10 33'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 42'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 33'----- Ended -----'
     A                                      DSPATR(HI)
     A                                 10 52'Load Time'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 63'Supervisor'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* SUBFILE FOR LIST DISPLAY - VIEW 3
     A*
     A          R VW3SFL                    SFL
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A  21                                  SFLNXTCHG
     A            OPTION         2A  B 11  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W3EXPD         6Y 0O 11 15EDTCDE(Y)
     A            W3STSD         6Y 0O 11 34EDTCDE(Y)
     A            RHRTE     R        O 11  7REFFLD(RHREC/RHRTE *LIBL/RTEHED)
     A            RHEXPT    R        O 11 24REFFLD(RHREC/RHEXPT *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            RHSTST    R        O 11 43REFFLD(RHREC/RHSTST *LIBL/RTEHED)
     A                                      EDTWRD(' 0:  :  ')
     A            W3CUBE         9Y 3O 11 52EDTCDE(3)
     A            W3SWGT         9Y 2O 11 64EDTCDE(3)
     A            W3PCS          5Y 0O 11 76EDTCDE(3)
     A*
     A          R VW3SFC                    SFLCTL(VW3SFL)
     A*%%TS  SD  20000731  115453  HEMANT      REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0007)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A                                 10  2'Opt'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  7'Route'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 15'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 24'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 15'---- Exported ---'
     A                                      DSPATR(HI)
     A                                 10 34'  Date  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 43'  Time  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  9 34'---- Status -----'
     A                                      DSPATR(HI)
     A                                 10 58'Cube'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 68'Weight'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 78'Pcs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*****************************************************************
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
     A*****************************************************************
     A*
     A* OPTION, COMMAND AND EOF MESSAGE LINES FOR LOOKUP PANELS
     A*
     A*    OPTION LINES
     A*
     A          R OPTREC
     A*%%TS  SD  20070613  144106  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(03)
     A                                  1  2'Type 1, press Enter.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A*    Command lines
     A*
     A          R CMDREC
     A*%%TS  SD  19960611  101013  ROCKY       REL-V3R1M0  5763-PW1
     A                                      LOCK
     A                                      SLNO(13)
     A                                      OVERLAY
     A            CMDLN1        79   O  1  2COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' Message line
     A*
     A          R MORREC
     A                                      LOCK
     A                                      SLNO(14)
     A                                      OVERLAY
     A            MORELN         7A  O  1 15DSPATR(HI)
     A*
     A*    DELETE DISPLAY INSTRUCTIONS
     A*
     A          R DELREC
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  3  2'Press Enter to confirm your choice-
     A                                      s for 4=Delete.'
     A                                      COLOR(BLU)
     A                                  4  2'Press F12 to return to change your-
     A                                       choices.'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* Records to clear different portions of a panel
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    Clear all lines after title
     A*
     A          R CLR03END
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A                                  9  2' '
     A                                 10  2' '
     A                                 11  2' '
     A                                 12  2' '
     A                                 13  2' '
     A                                 14  2' '
     A                                 15  2' '
     A                                 16  2' '
     A                                 17  2' '
     A                                 18  2' '
     A                                 19  2' '
     A                                 20  2' '
     A                                 21  2' '
     A*
     A*    Clear lines between title and subfile list
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A                                  4  2' '
     A                                  5  2' '
     A                                  6  2' '
     A                                  7  2' '
     A                                  8  2' '
     A          R CMDDEL
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(22)
     A                                  2  2'F12=Cancel'
     A                                      COLOR(BLU)
