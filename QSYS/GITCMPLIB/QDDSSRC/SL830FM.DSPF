     A*%%TS  SD  20010424  145314  DAVE        REL-V4R4M0  5769-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL830FM   Change slot utility
     A*  23 May 2000
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
414 AA*    05/23/00  DAS  4.14
     A*      - Created
     A*
415 AA*    04/24/01  DAS  4.15
     A*      - Added window PASWRD
     A*      - Added quantities for open label values.
     A*
     A*----------------------------------------------------------------
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000523  171143  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SL830.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                 10  4'Slot number . . . . . . . . .'
     A            W1DISP        12A  B 10 35
     A  23                                  DSPATR(RI)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A                                      CHECK(ER)
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A                                  1 30'Change Slot Quantities'
     A                                      DSPATR(HI)
     A                                  8  4'Warehouse . . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 35EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                  9  4'Department  . . . . . . . . .'
     A            W1WHDP         5A  B  9 35
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A          R SCREEN2
     A*%%TS  SD  20010424  145314  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL830.02'
     A                                  1 35'Slot Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  3  4'Slot:'
     A            WOSTR         38A  O  3 10
     A                                  4  4'Disp:'
     A            W2DISP        12A  B  4 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4 24'Stat:'
     A            W2STAT         2A  B  4 30DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2SDTE         8Y 0B  4 34EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  4 44'/'
     A            W2STIM         6Y 0B  4 46EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5  4'Styp:'
     A            W2STYP         1A  B  5 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5 13'Sdef:'
     A            W2SDEF         2A  B  5 19DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5 23'Desg:'
     A            W2DESG         5A  B  5 29DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5 36'Actv:'
     A            W2ACTV         2A  B  5 42DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5 47'Bld:'
     A            W2BLD          2A  B  5 52DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  5 62'Slfd:'
     A            W2SLFD         8Y 0B  5 68EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6  4'Pick:'
     A            W2PICK         1A  B  6 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 13'Rsvr:'
     A            W2RSRV         1A  B  6 19DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 23'Pseq:'
     A            W2PSEQ         5Y 0B  6 29EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 36'Prty:'
     A            W2PRTY         1Y 0B  6 42DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  6 46'Expd:'
     A            W2EXPD         8Y 0B  6 52EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  6 62'Entd:'
     A            W2ENTD         8Y 0B  6 68DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  7  4'Item:'
     A            W2ITEM        15A  B  7 10DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  7 26'-'
     A            WODESC        48A  O  7 28
     A                                  9 24'Normal'
     A                                  9 40'Breakdown 1'
     A                                  9 56'Breakdown 2'
     A            W2UM1          5A  O 10 24
     A            W2UM2          5A  O 10 43
     A            W2UM3          5A  O 10 59
     A                                 11 21'--------------'
     A                                 11 40'-----------'
     A                                 11 56'-----------'
     A                                 12  4'Stock . . . . .'
     A            W2STK1         5Y 0B 12 21EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STK2         3Y 0B 12 40EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2STK3         3Y 0B 12 56EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 13  4'Allocated . . .'
     A            W2ALC1         5Y 0B 13 21EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOALC1         5Y 0O 13 29EDTCDE(L)
     A            W2ALC2         3Y 0B 13 40EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOALC2         3Y 0O 13 46EDTCDE(L)
     A            W2ALC3         3Y 0B 13 56EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 14  4'Picked  . . . .'
     A            WOALC3         3Y 0O 13 62EDTCDE(L)
     A            W2PCK1         5Y 0B 14 21EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOPCK1         5Y 0O 14 29EDTCDE(L)
     A            W2PCK2         3Y 0B 14 40EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOPCK2         3Y 0O 14 46EDTCDE(L)
     A            W2PCK3         3Y 0B 14 56EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 15  4'Transfer  . . .'
     A            WOPCK3         3Y 0O 14 62EDTCDE(L)
     A            W2TFR1         5Y 0B 15 21EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOTFR1         5Y 0O 15 29EDTCDE(L)
     A            W2TFR2         3Y 0B 15 40EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WOTFR2         3Y 0O 15 46EDTCDE(L)
     A            W2TFR3         3Y 0B 15 56EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                 16  4'Receive . . . .'
     A            WOTFR3         3Y 0O 15 62EDTCDE(L)
     A            W2RCV1         5Y 0B 16 21EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WORCV1         5Y 0O 16 29EDTCDE(L)
     A            W2RCV2         3Y 0B 16 40EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WORCV2         3Y 0O 16 46EDTCDE(L)
     A            W2RCV3         3Y 0B 16 56EDTCDE(L)
     A                                      DSPATR(HI)
     A  90                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WORCV3         3Y 0O 16 62EDTCDE(L)
     A                                 17  4'Unknown . . . .'
     A            WOUNK1         5Y 0O 17 29EDTCDE(L)
     A            WOUNK2         3Y 0O 17 46EDTCDE(L)
     A            WOUNK3         3Y 0O 17 62EDTCDE(L)
     A                                 19  4'Available . .'
     A            W2AVL1         7Y 0O 19 18EDTCDE(J)
     A            W2AVL2         5Y 0O 19 37EDTCDE(J)
     A            W2AVL3         5Y 0O 19 53EDTCDE(J)
     A                                 21  4'(Qty available = (Stock + Receive)-
     A                                       - (Allocated + Picked + Transfer)'
     A                                 23  2'F3=Exit  F4=Prompt'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20000523  144729  DAVE        REL-V4R4M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'SL830.03'
     A                                  1 35'Slot Record'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            WOTYPE         6A  O  2 72DSPATR(HI)
     A                                  4  4'Slot:'
     A            WOSTR         38A  O  4 10
     A                                 23  2'F3=Exit F12=Cancel'
     A                                      COLOR(BLU)
     A                                 11 27'Press F10 to Confirm Update'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A          R PASWRD
     A*%%TS  SD  20010424  095629  DAVE        REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 5 12)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A                                  2  2'Password?'
     A                                      COLOR(WHT)
     A            W1PWRD         8A  B  4  2COLOR(WHT)
