     A****************************************************************
     A*
     A* ER422PR   Total thru-put (Summary)
     A* 03 October, 2000
     A* Hemant Kapadia
     A*
     A* Revisions:
     A* 10/03/00  HNK  4.14
     A* - Created
     A*
     A* 10/06/08  GJA  5.30
     A* - Cleaned up and revamped for suspended transactions
     A*
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER422'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(001)
     A            TMERNG        22A  O    56
530aAA                                      SPACEA(003)
530aMA            MRGTXT        45A  O     2
     A*
     A* Print summary totals
     A*
     A          R SMHEAD
     A                                      SPACEB(005)
     A                                      SPACEA(002)
     A                                     1'Summary totals:'
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*
     A                                     1'Dept/Zone '
     A                                      UNDERLINE
     A                                    13'Section'
     A                                      UNDERLINE
     A                                    21'Task'
     A                                      UNDERLINE
     A                                    30'Pieces'
     A                                      UNDERLINE
     A                                    41'Hours'
     A                                      UNDERLINE
     A                                    50'Pcs/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R SUMAR1
     A                                      SPACEA(001)
     A            S#WHDP         5         1
     A            S#STYP         1        15
     A            S#TASK         6        21
     A            S#PCS          7S 0     29EDTCDE(3)
     A            S#HRS          7S 2     38EDTCDE(3)
     A            S#PCPH         7S 2     48EDTCDE(3)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD           7A        1
     A            T#PTOT         9S 0     27EDTCDE(3)
     A            T#HTOT         9S 2     36EDTCDE(3)
     A            T#CTOT         9S 2     46EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* Draw 1 Line
     A*
     A          R DRAW1
     A                                      TEXT('DRAW ONE LINE')
     A                                      SPACEA(001)
     A                                     1'----------------------------------+
     A                                       ------------------------------'
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'Note: Direct task includes idle ti+
     A                                       me.'
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER422: End of Report '
     A*
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'No records found'
     A*
