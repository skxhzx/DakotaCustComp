     A****************************************************************
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A********************************************************************
     A*
     A* IT286PR   RF Replenishment exception report.
     A* 23 April, 1997
     A* Roxanne Harrison
     A*
     A****************************************************************
     A*
     A*  Revisions:
     A*
414aAA*     09/05/00  MLB  4.14a
     A*       - Revised print positions for existing flds on DETAIL1.
     A*         Added new fields to DETAIL1.
     A*         Revised print positions for existing flds on COLUMN1.
     A*         Added new fields to COLUMN1.
     A*         Added new fields to COLUMN1.
     A*
     A**************************************************************************
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A*                                   52
     A*                                     DFT('RF Replenishment Exception ')
     A*                                   79
     A*                                     DFT('Report ')
     A            RPTTTL        40        47
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('IT286')
     A                                      SPACEA(001)
414aMA            FMDT           6S 0O    55
     A                                      EDTCDE(Y)
     A                                    +2
     A                                      DFT('thru')
     A            TODT           6S 0O    +2
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A*
     A          R HEAD2
414aDA*                                     SPACEA(002)
414aAA            P#DESC        40A  O    47
414aAA                                      SPACEA(001)
414aAA            P#STAT        40A  O    47
414aAA                                      SPACEA(002)
     A*%%***********************************************************************
     A*
     A          R RPHEAD
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A  92                               120
     A                                      DFT('(Continued)')
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A                                      SPACEA(001)
414aAA                                    82
414aAA                                      DFT('* Generated *')
414aAA                                      SPACEA(001)
     A                                     3
     A                                      DFT('Item')
414aAA                                    19
414aAA                                      DFT('Description')
414aMA                                    50
     A                                      DFT('From Slot')
414aMA                                    63
     A                                      DFT('To Slot')
414aMA                                    76
414aMA                                      DFT('Qty')
414aDA*                                     DFT('Quantity')
414aMA                                    82
     A                                      DFT('Date')
414aAA                                    91
414aAA                                      DFT('Time')
414aAA                                    99
414aAA                                      DFT('Gen''d by')
414aAA                                   111
414aAA                                      DFT('Task')
414aAA                                   117
414aAA                                      DFT('Error Desc')
     A*%%***********************************************************************
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            EXITEM    R        O     3
     A                                      REFFLD(EXITEM ITMEXCP)
414aAA            ITDESC    R        O    19
     A                                      REFFLD(ITDESC PIRITEM)
414aMA            EXFDIS    R        O    50
     A                                      REFFLD(EXFDIS ITMEXCP)
414aMA            EXTDIS    R        O    63
     A                                      REFFLD(EXTDIS ITMEXCP)
414aMA            EXQTY     R        O    76
     A                                      REFFLD(EXQTY ITMEXCP)
     A                                      EDTCDE(K)
414aMA            GENDTE         6S 0O    81
     A                                      EDTCDE(Y)
414aAA            EXGTIM    R        O    90
414aAA                                      REFFLD(EXGTIM ITMEXCP)
414aAA                                      EDTWRD(' 0:  :  ')
414aAA            EXGBY     R        O    99
414aAA                                      REFFLD(EXGBY ITMEXCP)
414aAA            EXTASK    R        O   110
414aAA                                      REFFLD(EXTASK ITMEXCP)
414aAA            ERRMSG        15A  O   117
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total exceptions for')
     A            T#DESC        13A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    34
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A          R TOTAL2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT286: End of Report ')
     A*
