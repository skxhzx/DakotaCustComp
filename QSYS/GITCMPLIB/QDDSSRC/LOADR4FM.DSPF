     A*%%TS  SD  20060726  142219  JOSED       REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  LOADR4FM  Truck Load Module - Pallet Entry Screen.
     A*  May 24, 2006
     A*  Jose Duque
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions:
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA12
     A*
     A*----------------------------------------------------------------
     A          R WINDW4
     A*%%TS  SD  20060726  142219  JOSED       REL-V5R3M0  5722-WDS
     A                                      WINDOW(6 15 14 50)
     A                                      WDWBORDER((*DSPATR RI) (*CHAR '    -
     A                                          '))
     A*
     A                                  2  2'Loadr4'
     A*
     A                                  2 17'Pallet Entry'
     A                                      DSPATR(HI)
     A*
     A                                  4  2'Route #:'
     A            W1RTE#         5A  O  4 12DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  4 35'Door#:'
     A            W1DOOR         3A  O  4 43DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  6  2'Loader:'
     A            W1LDR#         5Y 0O  6 10DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A*
     A            W1USER        28A  O  6 19DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A                                  8  2'Pallet Id:'
     A            W1PLT#         5Y 0B  8 14DSPATR(UL)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                      CHECK(ER)
     A*
     A                                 10  2'Trailer #:'
     A            W1TRL#         6A  B 10 14DSPATR(UL)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A*
     A                                 12  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 12 12'F12=Cancel'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
