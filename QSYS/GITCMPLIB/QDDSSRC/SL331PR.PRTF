     A****************************************************************
     A*
     A* SL331PR   Slot listing.
     A* 20 March, 1996
     A* Hemant Kapadia/Roxanne Harrison
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    52
     A                                      DFT('Slot Listing')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('SL311')
     A                                      SPACEA(001)
     A*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A                                    14
     A                                      DFT('Pick')
     A                                    25
     A                                      DFT('Pick')
     A                                    48
     A                                      DFT('Pick')
     A                                    59
     A                                      DFT('Pick')
     A                                    81
     A                                      DFT('Pick')
     A                                    92
     A                                      DFT('Pick')
     A                                   114
     A                                      DFT('Pick')
     A                                   125
     A                                      DFT('Pick')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Slot')
     A                                    14
     A                                      DFT('Seq')
     A                                    20
     A                                      DFT('Type')
     A                                    25
     A                                      DFT('Slot')
     A*
     A                                    34
     A                                      DFT('Slot')
     A                                    48
     A                                      DFT('Seq')
     A                                    54
     A                                      DFT('Type')
     A                                    59
     A                                      DFT('Slot')
     A*
     A                                    68
     A                                      DFT('Slot')
     A                                    81
     A                                      DFT('Seq')
     A                                    87
     A                                      DFT('Type')
     A                                    92
     A                                      DFT('Slot')
     A*
     A                                   101
     A                                      DFT('Slot')
     A                                   114
     A                                      DFT('Seq')
     A                                   120
     A                                      DFT('Type')
     A                                   125
     A                                      DFT('Slot')
     A*
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            C1SLOT        12A  O     1
     A            C1PSEQ         5S 0O    14
     A                                      EDTCDE(Z)
     A            C1DEF          2A  O    21
     A            C1PICK         1A  O    26
     A*
     A            C2SLOT        12A  O    34
     A            C2PSEQ         5S 0O    48
     A                                      EDTCDE(Z)
     A            C2DEF          2A  O    55
     A            C2PICK         1A  O    60
     A*
     A            C3SLOT        12A  O    68
     A            C3PSEQ         5S 0O    81
     A                                      EDTCDE(Z)
     A            C3DEF          2A  O    88
     A            C3PICK         1A  O    93
     A*
     A            C4SLOT        12A  O   101
     A            C4PSEQ         5S 0O   114
     A                                      EDTCDE(Z)
     A            C4DEF          2A  O   121
     A            C4PICK         1A  O   126
     A*
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items for')
     A            T#DESC         5A  O    +1
     A                                    +1
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('Total items printed')
     A                                    +2
     A                                      DFT(':')
     A            T#CLS          5S 0O    25
     A                                      EDTCDE(Z)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('SL331: End of Report ')
     A*
