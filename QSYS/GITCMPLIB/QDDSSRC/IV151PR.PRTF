     A****************************************************************
     A*
     A* IV151PR   Inventory variance report by slot
     A* 12 March, 1996
     A* Hemant Kapadia/Roxanne Harrison
     A*
     A* Revisions:
     A*
217  F*    01/09/97  RH   2.17
     F*      - Add allow item breakdown flag(ITFLG1 & ITFLG2) to print.
416b F*    04/29/02  RH   4.16
     F*      - Comment TOTAL1 line, was giving compile error.
416c F*    05/14/02  RH   4.16c
     F*      - Add field to show if slot has exception.
416d F*    06/03/02  RH   4.16d
     F*      - Shorten description field by 3, quantity was overwriting
510a F*    09/19/06  RH   5.10a
     F*      - Fix: Add program ID to report
510b F*    09/21/06  RH   5.10b
     F*      - Enh: Add COLUMN1, DETAIL1, DETAIL2 to print 80 character
     F*        extended description.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*                                     SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            OPTN1         25A       57
510  A                                   125
510  A                                      'IV151'
     A            OPTN2         25A       57
     A                                      SPACEB(001)
     A                                    57'Variance:'
     A                                      SPACEB(001)
     A            PMVAR          5S 0     +1EDTCDE(3)
     A          R SBHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1'Warhouse:'
     A            WKSWHS    R             +1REFFLD(WKSWHS IV151)
     A                                      EDTCDE(3)
     A  93                                75
     A                                      '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A                                     1'Department:'
     A            WKSWHD    R             +1REFFLD(WKSWHD IV151)
     A  92                                75
     A                                      '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A                                     1'Section:'
     A            WKSSTP    R             +1REFFLD(WKSSTP IV151)
     A  91                                75
     A                                      '(Continued)'
     A*
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    14'Execpt/'
     A                                    70'- Current -'
     A                                    93'- New - '
     A                                   109'- Difference -'
     A                                   125' Allow '
     A*
     A                                     1'Slot#'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    14'   Page #'
     A                                      UNDERLINE
     A                                    24'Item #'
     A                                      UNDERLINE
     A                                    40'Description'
     A                                      UNDERLINE
     A                                    70'Norm'
     A                                      UNDERLINE
     A                                    77'Bk1'
     A                                      UNDERLINE
     A                                    82'Bk2'
     A                                      UNDERLINE
     A                                    90'Norm'
     A                                      UNDERLINE
     A                                    97'Bk1'
     A                                      UNDERLINE
     A                                   102'Bk2'
     A                                      UNDERLINE
     A                                   109'Norm'
416c A                                   116'Bk1'
     A                                   121'Bk2'
     A                                    +1'Bk1'
     A                                    +1'Bk2'
     A                                      SPACEA(002)
510b A          R COLUMN1
510b A                                      SPACEB(001)
510b A                                      SPACEA(001)
510b A                                    14'Execpt/'
510b A                                   116'-Cur-'
510b A                                   122'-New-'
510b A                                   128'-Dif-'
510b A*
510b A                                     1'Slot#'
510b A                                      UNDERLINE
510b A                                      SPACEB(001)
510b A                                    14'   Page'
510b A                                      UNDERLINE
510b A                                    22'Item #'
510b A                                      UNDERLINE
510b A                                    38'Description                    +
510b A                                                                      +
510b A                                                          ' +
510b A                                      UNDERLINE
510b A                                   116'Norm'
510b A                                      UNDERLINE
510b A                                   122'Norm'
510b A                                      UNDERLINE
510b A                                   128'Norm'
510b A                                      SPACEA(002)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKSDSP    R              1REFFLD(WKSDSP IV151)
     A            W1EXCP         1A       14
     A            WKSPAG    R             17REFFLD(WKSPAG IV151)
     A                                      EDTCDE(3)
     A            WKSITM    R             23REFFLD(WKSITM IV151)
416d A*           IDESC         30A       39
416dMA            IDESC         27A       39
     A            WKSTK1    R             67REFFLD(WKSTK1 IV151)
     A                                      EDTCDE(K)
     A            WKSTK2    R             76REFFLD(WKSTK2 IV151)
     A                                      EDTCDE(K)
     A            WKSTK3    R             81REFFLD(WKSTK3 IV151)
     A                                      EDTCDE(K)
     A            WKSQT1    R             87REFFLD(WKSQT1 IV151)
     A                                      EDTCDE(K)
     A            WKSQT2    R             96REFFLD(WKSQT2 IV151)
     A                                      EDTCDE(K)
     A            WKSQT3    R            101REFFLD(WKSQT3 IV151)
     A                                      EDTCDE(K)
     A            WKDQT1         5S 0    106EDTCDE(K)
     A            WKDQT2         3S 0    115EDTCDE(K)
     A            WKDQT3         3S 0    121EDTCDE(K)
     A            IFLG1          1A      126
     A            IFLG2          1A      130
     A*
     A          R DETAIL2
     A                                      SPACEA(001)
     A            WKSDSP    R              1REFFLD(WKSDSP IV151)
     A            WKSPAG    R             17REFFLD(WKSPAG IV151)
     A                                      EDTCDE(3)
     A            WKSITM    R             23REFFLD(WKSITM IV151)
416d A*           IDESC         30A       39
416dMA            IDESC         27A       39
     A            WKSQT1    R             87REFFLD(WKSQT1 IV151)
     A                                      EDTCDE(K)
     A            WKSQT2    R             96REFFLD(WKSQT2 IV151)
     A                                      EDTCDE(K)
     A            WKSQT3    R            101REFFLD(WKSQT3 IV151)
     A                                      EDTCDE(K)
     A            WKDQT1         5S 0    106EDTCDE(K)
     A            WKDQT2         3S 0    115EDTCDE(K)
     A            WKDQT3         3S 0    121EDTCDE(K)
     A            IFLG1          1A      126
     A            IFLG2          1A      130
     A                                      SPACEA(001)
     A            WKITEM    R             23REFFLD(WKSITM IV151)
416d A*           ODESC         30A       39
416dMA            ODESC         27A       39
     A            WKSTK1    R             64REFFLD(WKSTK1 IV151)
     A                                      EDTCDE(K)
     A            WKSTK2    R             73REFFLD(WKSTK2 IV151)
     A                                      EDTCDE(K)
     A            WKSTK3    R             79REFFLD(WKSTK3 IV151)
     A                                      EDTCDE(K)
     A            OFLG1          1A      126
     A            OFLG2          1A      130
     A*
510b A*
510b A          R DETAIL3
510b A                                      SPACEA(001)
510b A            WKSDSP    R              1REFFLD(WKSDSP IV151)
510b A            W1EXCP         1A       14
510b A            WKSPAG    R             16REFFLD(WKSPAG IV151)
510b A                                      EDTCDE(3)
510b A            WKSITM    R             22REFFLD(WKSITM IV151)
510b A            IDESCX        75A       38
510b A            WKSTK1    R            115REFFLD(WKSTK1 IV151)
510b A                                      EDTCDE(K)
510b A            WKSQT1    R            121REFFLD(WKSQT1 IV151)
510b A                                      EDTCDE(K)
510b A            WKDQT1         5S 0    127EDTCDE(K)
     A*
510b A          R DETAIL4
510b A                                      SPACEA(001)
510b A            WKSDSP    R              1REFFLD(WKSDSP IV151)
510b A            WKSPAG    R             16REFFLD(WKSPAG IV151)
510b A                                      EDTCDE(3)
510b A            WKSITM    R             22REFFLD(WKSITM IV151)
510b A            IDESCX        75A       38
510b A            WKSQT1    R            115REFFLD(WKSQT1 IV151)
510b A                                      EDTCDE(K)
510b A            WKDQT1         5S 0    127EDTCDE(K)
510b A                                      SPACEA(001)
510b A            WKITEM    R             22REFFLD(WKSITM IV151)
510b A            ODESCX        77A       38
510b A            WKSTK1    R            121REFFLD(WKSTK1 IV151)
510b A                                      EDTCDE(K)
     A*
     A* Print totals
     A*
416  A*         R TOTAL1
416  A*                                     SPACEB(001)
     A*           $RPD          25A        1
     A*           T#SUNT         7S 2     47EDTCDE(3)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'IV151: End of Report '
     A*
