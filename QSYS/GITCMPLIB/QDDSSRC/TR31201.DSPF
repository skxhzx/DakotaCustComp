     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  TR312FM   View TRAX log - View record
     A*  23 June 2010
     A*  Retha Davis
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
530 AA*    06/23/10  RBD  5.30
     A*      - Created.
     A*
640 AA*    06/12/12  RBD  6.40
     A*      - Added display of handheld serial number.
     A*
     A*----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA09
     A                                      CA12
     A          R SCREEN1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'TR312.01'
     A                                  1 35'View Trax Log'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Try  . . . . . . . . .'
     A            TTLTRY    R        O  3 27REFFLD(TTLREC/TTLTRY *LIBL/TTLOG)
     A                                      EDTCDE(3)
     A                                  4  4'Status . . . . . . . .'
     A            TTLSTATUS R        O  4 27REFFLD(TTLREC/TTLSTATUS TTLOG)
     A                                  5  4'Code . . . . . . . . .'
     A            TTLERRCODER        O  5 27REFFLD(TTLREC/TTLERRCODE TTLOG)
     A                                  6  4'Message  . . . . . . .'
     A            TTLERRMSG R        O  6 27REFFLD(TTLREC/TTLERRMSG TTLOG)
     A                                  8  4'Warehouse  . . . . . .'
     A            TTLWHSE   R        O  8 27REFFLD(TTLREC/TTLWHSE *LIBL/TTLOG)
     A                                      EDTCDE(3)
     A                                  9  4'Route / Id . . . . . .'
     A            TTLRTE    R        O  9 27REFFLD(TTLREC/TTLRTE *LIBL/TTLOG)
     A                                  9 33'/'
     A            TTLRTID   R        O  9 35REFFLD(TTLREC/TTLRTID *LIBL/TTLOG)
     A                                 12  4'Sent file name . . . .'
     A            TTLFILESNDR        O 12 27REFFLD(TTLREC/TTLFILESND TTLOG)
     A                                 14  4'File Start (Sys) . . .'
     A            W1TS1         26A  O 14 27
     A                                 15  4'File End (Sys) . . . .'
     A            W1TS2         26A  O 15 27
     A                                 16  4'File user/job  . . . .'
     A            TTLUSER1  R        O 16 27REFFLD(TTLREC/TTLUSER1 *LIBL/TTLOG)
     A                                 16 38'/'
     A            TTLJOB1   R        O 16 40REFFLD(TTLREC/TTLJOB1 *LIBL/TTLOG)
     A                                 16 51'/'
     A            TTLNBR1   R        O 16 53REFFLD(TTLREC/TTLNBR1 *LIBL/TTLOG)
     A                                 17  4'File program . . . . .'
     A            TTLPGM1   R        O 17 27REFFLD(TTLREC/TTLPGM1 *LIBL/TTLOG)
     A                                 18  4'Download (Sys) . . . .'
     A            W1TS3         26A  O 18 27
640 AA                                 19  4'Unit Serial# . . . . .'
640 AA            W1SER#        30A  O 19 27
     A                                 20  4'Send user/job  . . . .'
     A            TTLUSER3  R        O 20 27REFFLD(TTLREC/TTLUSER3 *LIBL/TTLOG)
     A                                 20 38'/'
     A            TTLJOB3   R        O 20 40REFFLD(TTLREC/TTLJOB3 *LIBL/TTLOG)
     A                                 20 51'/'
     A            TTLNBR3   R        O 20 53REFFLD(TTLREC/TTLNBR3 *LIBL/TTLOG)
     A                                 21  4'Send program . . . . .'
     A            TTLPGM3   R        O 21 27REFFLD(TTLREC/TTLPGM3 *LIBL/TTLOG)
     A                                 22  4'Status (Sys) . . . . .'
     A            W1TS9         26A  O 22 27
     A                                 23  2'F3=Exit  F5=Refresh  F7=View sent -
     A                                      file  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
