     A*%%TS  SD  20000124  114009  DAVE        REL-V4R4M0  5769-PW1
     A* NOTE: This format uses a different version of the error message
     A*       subfiles. The subfile now loads all the messages from the
     A*       programs message queue instead of the program loading
     A*       one message at a time using the message key.
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000124  114009  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'UT220.01'
     A                                  1 14'Add/Remove ''*'' to Right Most Cha-
     A                                      racter of Designation'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE         3Y 0B  8 34EDTCDE(4)
     A N21N90                               DSPATR(HI)
     A  21                                  DSPATR(RI)
     A N21N90                               DSPATR(UL)
     A  01                                  DSPATR(PC)
     A                                  8 51'1 - 999'
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP         5A  B  9 34
     A N22N90                               DSPATR(HI)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A                                  9 51'*ALL, Code'
     A                                 10  4'Add/Remove ''*'' . . . . . . .'
     A            W1ADRM         1A  B 10 34
     A N23N90                               DSPATR(HI)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A                                 10 51'A=Add, R=Remove'
     A                                 23  2'F3=Exit  F4=Prompt  F5=Refresh'
     A                                      COLOR(BLU)
     A                                 14  5'This program will add or remove an-
     A                                       ''*'' to the last character of an'
     A                                 15  5'item''s designation code. An ''*''-
     A                                       in the last character of an item''-
     A                                      s'
     A                                 16  5'designation code tells the receivi-
     A                                      ng program to receive the product'
     A                                 17  5'but not to put it into any slots. -
     A                                      This is for cases where only some'
     A                                 18  5'of the departments are live within-
     A                                       FS-WMS.'
     A          R SCREEN2
     A*%%TS  SD  20000121  155614  DAVE3       REL-V4R4M0  5769-PW1
     A                                      CA10
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT220.02'
     A                                  1 14'Add/Remove ''*'' to Right Most Cha-
     A                                      racter of Designation'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  8  4'Warehouse  . . . . . . . . .'
     A            W1WHSE    R        O  8 34REFFLD(W1WHSE *SRC)
     A                                      EDTCDE(4)
     A                                  9  4'Department . . . . . . . . .'
     A            W1WHDP    R        O  9 34REFFLD(W1WHDP *SRC)
     A                                 10  4'Add/Remove ''*'' . . . . . . .'
     A            W1ADRM    R        O 10 34REFFLD(W1ADRM *SRC)
     A                                 14 10'You are about to change the design-
     A                                      ation code for all the'
     A                                 15 10'slots that match the above criteri-
     A                                      a.'
     A                                 19 10'Please make sure you know what you-
     A                                       are doing.'
     A                                 21 33'Press F10 to continue.'
     A                                 23  2'F12=Cancel'
     A          R SCREEN3
     A*%%TS  SD  20000121  155614  DAVE3       REL-V4R4M0  5769-PW1
     A                                      CA22
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                  1  2'UT220.03'
     A                                  1 14'Add/Remove ''*'' to Right Most Cha-
     A                                      racter of Designation'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  5 13'This is your last chance.  If you -
     A                                      are not sure, press F12'
     A                                  6 13'to cancel your request.'
     A                                  9 17'If you are sure, press F22 to chan-
     A                                      ge the designation code.'
     A                                      DSPATR(HI)
     A                                 23  2'F12=Cancel'
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
    DA*                                     SFLSIZ(2) SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            #PGMQ                     SFLPGMQ(10)
