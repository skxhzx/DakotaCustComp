     A****************************************************************
     A*
     A* HQ121PR   Update pick slot qty derieved from host qty.
     A* 17 April, 2002
     A* Hemant Kapadia
     A*
     A* 04/17/02  HNK
     A*  - Created - Custom for CBI
     A*
510 AA* 12/03/07  MLB  5.10
     A*  - Fix: Revised DETAIL format to correct field names.
640 AA* 03/14/12  LMC  6.40A
     A*  - Enh: Added additional formats for all clients except
     A*    TPC and CBI. Processes a different way, old report formats
     A*    don't make sense for new processing.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3EDTCDE(Y)
     A                                    52
     A                                      DFT('Pick Slot Qty')
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    52
     A                                   125
     A                                      DFT('HQ121')
     A                                      SPACEA(001)
     A*
640aAA          R HEAD11
640aAA                                      TEXT('REPORT HEADING')
640aAA                                      SKIPB(002)
640aAA                                      SPACEA(001)
640aAA*
640aAA            UDATE          6S 0O     3EDTCDE(Y)
640aAA                                    52
640aAA                                      DFT('Import Adjustments')
640aAA                                   122
640aAA                                      DFT('Page')
640aAA            PAGE           4S 0O   126
640aAA                                      EDTCDE(Z)
640aAA                                      SPACEA(001)
640aAA*
640aAA            TIME           6S 0O     3
640aAA                                      EDTWRD('  :  :  ')
640aAA            WHSE          30A  O    52
640aAA                                   125
640aAA                                      DFT('HQ121')
640aAA                                      SPACEA(001)
640aAA*
     A          R HEAD2
     A                                      SPACEA(002)
     A            P#DESC        40A  O    47
     A  91                                90
     A                                      DFT('(Continued)')
     A*
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    74
     A                                      DFT('Overflow')
     A                                    85
     A                                      DFT('Pick Slot')
     A                                      SPACEA(001)
     A                                    96
     A                                      DFT('Overflow')
     A                                   107
     A                                      DFT('Pick Slot')
     A                                      SPACEA(001)
     A                                    01
     A                                      DFT('Pick Slot')
     A                                      UNDERLINE
     A                                    15
     A                                      DFT('Item')
     A                                      UNDERLINE
     A                                    32
     A                                      DFT('Description')
     A                                      UNDERLINE
     A                                    63
     A                                      DFT('Host Qty')
     A*
     A                                    74
     A                                      DFT('Case Qty')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('Case Qty')
     A                                      UNDERLINE
     A*
     A                                    96
510 DA*                                     DFT('Pack Qty')
510 MA                                      DFT('Brk. Qty')
     A                                      UNDERLINE
     A                                   107
510 DA*                                     DFT('Pack Qty')
510 MA                                      DFT('Brk. Qty')
     A                                      UNDERLINE
640aAA*%%***********************************************************************
640aAA*
640aAA* Print column headings adjustments
640aAA*
640aAA          R COLUMNS1
640aAA                                      SPACEB(001)
640aAA                                      SPACEA(001)
640aAA                                     1
640aAA                                      DFT('Item')
640aAA                                      UNDERLINE
640aAA                                    16
640aAA                                      DFT('Description')
640aAA                                      UNDERLINE
640aAA                                    46
640aAA                                      DFT('Host Qty1')
640aAA                                      UNDERLINE
640aAA*
640aAA                                    56
640aAA                                      DFT('Host Qty2')
640aAA                                      UNDERLINE
640aAA                                    66
640aAA                                      DFT('Slot Qty1')
640aAA                                      UNDERLINE
640aAA*
640aAA                                    76
640aAA                                      DFT('Slot Qty2')
640aAA                                      UNDERLINE
640aAA                                    86
640aAA                                      DFT('Slot Qty3')
640aAA                                      UNDERLINE
640aAA                                    96
640aAA                                      DFT('Adj Qty1')
640aAA                                      UNDERLINE
640aAA                                   106
640aAA                                      DFT('Adj Qty2')
640aAA                                      UNDERLINE
640aAA                                   116
640aAA                                      DFT('Adj Qty3')
640aAA                                      UNDERLINE
     A*%%***********************************************************************
     A*
     A* Print detail L1 line.
     A*
     A*
     A*%%***********************************************************************
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            T#DISP        12A  O     1
     A            T#ITEM        15A  O    15
     A            ITDESC    R        O    32REFFLD(ITDESC PIRITEM)
510 DA*           IJQYOH    R        O    63REFFLD(IJQYOH IMPFITMO)
510 MA            IJTQH1    R        O    63REFFLD(IJTQH1 IMPFPQTY)
     A                                      EDTCDE(Z)
     A            OFQTY1         7S 0O    74EDTCDE(Z)
     A            PCKQT1         7S 0O    85EDTCDE(L)
     A            OFQTY2         7S 0O    96EDTCDE(Z)
     A            PCKQT2         7S 0O   107EDTCDE(L)
     A*
640aAA*%%***********************************************************************
640aAA*
640aAA* Print detail adjustment line.
640aAA*
640aAA*
640aAA*%%***********************************************************************
640aAA*
640aAA* Print detail line
640aAA*
640aAA          R DETAIL11
640aAA                                      SPACEB(001)
640aAA            IJITEM    R        O     1REFFLD(ITITEM PIRITEM)
640aAA            ITDESC    R        O    16REFFLD(ITDESC PIRITEM)
640aAA            IJTQH1    R        O    46REFFLD(IJTQH1 IMPFPQTY)
640aAA                                      EDTCDE(Z)
640aAA            IJTQH2    R        O    56REFFLD(IJTQH2 IMPFPQTY)
640aAA                                      EDTCDE(Z)
640aAA            CLQTY1         7S 0O    66EDTCDE(L)
640aAA            CLQTY2         7S 0O    76EDTCDE(L)
640aAA            CLQTY3         7S 0O    86EDTCDE(L)
640aAA            IPQQP1         7S 0O    96EDTCDE(L)
640aAA            IPQQP2         7S 0O   106EDTCDE(L)
640aAA            IPQQP3         7S 0O   116EDTCDE(L)
     A*
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT(' ')
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1
     A                                      DFT('HQ121: End of Report ')
     A*
