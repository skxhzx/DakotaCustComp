     A****************************************************************
     A*
     A* ER372PR   Task report for an employee (Summary)
     A* 24 March, 2000
     A* Hemant Kapadia
     A*
     A* 03.24.00  HNK  4.13
     A*  Created
     A*
     A* Note: There is no ER242PR to replace.
     A*
     A* Revisions:
     A*
     A* 10/08/09  GJA  5.30
     A*  Enh: Cleaned up and revamped for suspended transactions
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   +45
     A            MRGTXT        21A  O    94
     A                                   125
     A                                      'ER372'
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Employee:'
     A            WKEMP#    R             +1REFFLD(PTEMP# ER371)
     A                                      EDTCDE(Z)
     A            WKNAME        40A       +1
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                     1'Start'
     A                                    48'Cases'
     A                                    61'Direct'
     A                                    70'Indirect'
     A                                    82'Num of'
     A                                      SPACEA(001)
     A*
     A                                     1' Date '
     A                                      UNDERLINE
     A                                    11'Dept '
     A                                      UNDERLINE
     A                                    19'Task  '
     A                                      UNDERLINE
     A                                    26'Description'
     A                                      UNDERLINE
     A                                    48'Selected'
     A                                      UNDERLINE
     A                                    61'Hours'
     A                                      UNDERLINE
     A                                    70'Hours'
     A                                      UNDERLINE
     A                                    82'Trans'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*
     A* Print detail
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            W1SDTE         6S 0      1
     A                                      EDTCDE(Y)
     A            WKWHDP    R             11REFFLD(WKWHDP ER371A)
     A            TKCODE    R             19REFFLD(PTTASK ER371)
     A            W1DESC        20A       26
     A            T#PTOT         7S 0     48EDTCDE(1)
     A            T#DTOT         7S 2     61EDTCDE(1)
     A            T#ITOT         7S 2     70EDTCDE(1)
     A            T#TRAN         7S 0     82EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            $RPD          20A        1
     A            T#PTOT         7S 0     48EDTCDE(1)
     A            T#DTOT         7S 2     61EDTCDE(1)
     A            T#ITOT         7S 2     70EDTCDE(1)
     A            T#TRAN         7S 0     82EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER372: End of Report '
     A*
