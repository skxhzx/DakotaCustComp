     A*%%TS  SD  20000128  153211  DAVE        REL-V4R4M0  5769-PW1
     A**************************************************************************
     A*  Revisions:
     A*
     A*    10/19/15  DAS  7.00
     A*     - Created
     A*
     A**************************************************************************
     A*  90/09/26  11:42:01    HEMANTK     REL-R02M00  5728-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000128  153211  DAVE        REL-V4R4M0  5769-PW1
     A                                      CF04
     A                                      CF08
     A                                      CF09
     A                                      CF21
     A  98                                  ALARM
     A                                      CLRL(23)
     A                                      OVERLAY
     A                                  1  2'SL252.01'
     A                                  1 23'Change Pick Slot'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72'Entry'
     A                                      DSPATR(HI)
     A                                  3  2'Warehouse  :'
     A            WOWHSE         3A  O  3 15
     A            WHDESC        30A  O  3 22
     A                                  4  2'Slot Dept. :'
     A            WDCODE         5A  O  4 15
     A            WDDESC        30A  O  4 22
     A                                  5  2'Slot       :'
     A            FMDISP        12A  O  5 15
     A                                  6  2'Slot type  :'
     A            W1STYP         1A  O  6 15
     A                                  7  2'Status     :'
     A            SSDESC        30A  O  7 15
     A                                  8  2'Item Dept. :'
     A            W1WHDP         5A  O  8 15
     A                                  9  2'Item       :'
     A            WOITEM        55A  O  9 15
     A            W1LBLS        12A  O 10  2DSPATR(HI)
     A            W1ITMS        55A  O 10 15DSPATR(HI)
     A                                 11  2'Quantity   :'
     A            W1CURQ        30A  O 11 15
     A                                 14  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 17  4'New Pick slot . . . .'
     A            W1DISP        12A  B 17 27
     A                                      DSPATR(UL)
     A  01                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
     A                                 22  2'F4=Prompt  F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23  2'F8=View slot info'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
