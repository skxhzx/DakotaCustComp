     A*%%TS  SD  19960419  134411  DAVE        REL-V3R1M0  5763-PW1
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  SL112FM   Slot type definition maint. - Change screen
     A*  20 July 1994
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
500 AA*    10/14/04  DAS  5.00
     A*      - Added '*FLOW' to pallet type.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  19960419  134411  DAVE        REL-V3R1M0  5763-PW1
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  1  2'SD112.01'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                 10  4'Code . . . . . . . . .'
     A                                 11  4'Description  . . . . .'
     A                                 12  4'Dimensions . . . . . .'
     A                                 14  4'Pallet positions . . .'
     A            W1CODE         2A  B 10 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A            W1DESC        30A  B 11 28CHECK(LC)
     A  22                                  DSPATR(RI)
     A N22N90                               DSPATR(UL)
     A  02                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1POS          3Y 1B 14 28EDTCDE(3)
     A  26                                  DSPATR(RI)
     A N26N90                               DSPATR(UL)
     A  06                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1LEN          5Y 2B 12 28EDTCDE(3)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1WDTH         5Y 2B 12 37EDTCDE(3)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A            W1HGT          5Y 2B 12 46EDTCDE(3)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 12 35'x'
     A                                 12 44'x'
     A                                 12 62'Len x Wdth x Hght'
     A                                 23  2'F5=Refresh  F4=Prompt  F12=Cancel'
     A                                      COLOR(BLU)
     A                                  1 25'Slot Type Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse:'
     A            $PWHSE         3Y 0O  5 13EDTCDE(3)
     A                                  5 17'-'
     A            WHDESC        30A  O  5 19
     A                                 13  4'Pallet code  . . . . .'
     A            W1PAL          6   B 13 28
     A  27N90                               DSPATR(RI)
     A N27N90                               DSPATR(UL)
     A  07                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
500 DA**                               13 62'Name, *NONE'
500 MA                                 13 62'Name, *FLOW, *NONE'
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
