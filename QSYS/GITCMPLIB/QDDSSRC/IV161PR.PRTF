     A****************************************************************
     A*
     A* IV161PR   Inventory variance report by item
     A* 27 March, 1996
     A* Hemant Kapadia/Roxanne Harrison
     A*
     A* Revisions:
     A*
217  F*    01/09/97  RH   2.17
     F*      - Add allow item breakdown flag(ITFLG1 & ITFLG2) to print.
416a F*    05/01/02  RH   4.16
     F*      - Add variance # to header.
650a F*    05/10/13  RH   6.50a
     F*      - Enh: Add DETAILS to print slots for inventory item
     F*        and linked item with slots.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*                                     SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        40        47
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A                                    57'Variance:'
     A*                                     SPACEB(001)
     A            PMVAR          5S 0     +1EDTCDE(3)
     A            OPTN1         15A       57
     A                                      SPACEB(001)
     A            OPTN2         15A       57
     A                                      SPACEB(001)
     A          R SBHEAD
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A                                     1'Warhouse:'
     A            WKSWHS    R             +1REFFLD(WKSWHS IV161)
     A                                      EDTCDE(3)
     A  93                                75
     A                                      '(Continued)'
     A                                   132' '
     A                                      SPACEA(001)
     A                                     1'Department:'
     A            WKSWHD    R             +1REFFLD(WKSWHD IV161)
     A  92                                75
     A                                      '(Continued)'
     A                                   132' '
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    70'- Current -'
     A                                    93'- New - '
     A                                   109'- Difference -'
     A                                   125' Allow '
     A*
     A                                     1'Item #'
     A                                      UNDERLINE
     A                                      SPACEB(001)
     A                                    17'Description'
     A                                      UNDERLINE
     A                                    48'Slot#'
     A                                      UNDERLINE
     A*                                   14'Page #'
     A*                                     UNDERLINE
     A                                    67'Norm'
     A                                      UNDERLINE
     A                                    74'Bk1'
     A                                      UNDERLINE
     A                                    80'Bk2'
     A                                      UNDERLINE
     A                                    88'Norm'
     A                                      UNDERLINE
     A                                    95'Bk1'
     A                                      UNDERLINE
     A                                   101'Bk2'
     A                                      UNDERLINE
     A                                   108'Norm'
     A                                   115'Bk1'
     A                                   121'Bk2'
     A                                    +1'Bk1'
     A                                    +1'Bk2'
     A                                      SPACEA(001)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEA(001)
     A            WKITEM    R              1REFFLD(WKITEM IV161)
     A            IDESC         30A       17
     A            WKSDSP    R             48REFFLD(WKSDSP IV161)
     A*           WKSPAG    R             14REFFLD(WKSPAG IV151)
     A*           WKTYPE    R             63REFFLD(WKTYPE IV161)
     A N96        WKSTK1    R             64REFFLD(WKSTK1 IV161)
     A                                      EDTCDE(K)
     A  96                                71'--'
     A N96        WKSTK2    R             74REFFLD(WKSTK2 IV161)
     A                                      EDTCDE(K)
     A  96                                75'--'
     A N96        WKSTK3    R             80REFFLD(WKSTK3 IV161)
     A                                      EDTCDE(K)
     A  96                                81'--'
     A N97        WKSQT1    R             86REFFLD(WKSQT1 IV161)
     A                                      EDTCDE(K)
     A  97                                90'--'
     A N97        WKSQT2    R             95REFFLD(WKSQT2 IV161)
     A                                      EDTCDE(K)
     A  97                                96'--'
     A N97        WKSQT3    R            101REFFLD(WKSQT3 IV161)
     A                                      EDTCDE(K)
     A  97                               102'--'
     A            WKDQT1         5S 0    106EDTCDE(K)
     A            WKDQT2         3S 0    115EDTCDE(K)
     A            WKDQT3         3S 0    121EDTCDE(K)
     A            IFLG1          1A      126
     A            IFLG2          1A      130
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                    66'_____'
     A                                    74'___'
     A                                    80'___'
     A                                    87'_____'
     A                                    95'___'
     A                                   101'___'
     A                                   107'_____'
     A                                   115'___'
     A                                   121'___'
     A                                      SPACEA(001)
     A*           $RPD          25A        1
     A            TSTK1          6S       64EDTCDE(J)
     A            TSTK2          4S       72EDTCDE(J)
     A            TSTK3          4S       78EDTCDE(J)
     A            TSQT1          6S       85EDTCDE(J)
     A            TSQT2          4S       93EDTCDE(J)
     A            TSQT3          4S       99EDTCDE(J)
     A            TDQT1          6S      105EDTCDE(J)
     A            TDQT2          4S      113EDTCDE(J)
     A            TDQT3          4S      119EDTCDE(J)
     A                                      SPACEA(002)
     A*
     A          R TOTALS
     A                                      SPACEB(001)
     A*           $RPD          25A        1
     A            WKITEM    R              1REFFLD(WKITEM IV161)
     A            IDESC         30A       17
     A            TSTK1          6S       64EDTCDE(K)
     A            TSTK2          4S       72EDTCDE(K)
     A            TSTK3          4S       78EDTCDE(K)
     A            TSQT1          6S       85EDTCDE(K)
     A            TSQT2          4S       93EDTCDE(K)
     A            TSQT3          4S       99EDTCDE(K)
     A            TDQT1          6S      105EDTCDE(K)
     A            TDQT2          4S      113EDTCDE(K)
     A            TDQT3          4S      119EDTCDE(K)
     A            IFLG1          1A      126
     A            IFLG2          1A      130
     A*                                     SPACEA(001)
650aAA
650aAA          R DETAILS
650aAA                                      SPACEB(001)
650aAA            WOITEM    R              3REFFLD(WKITEM IV161)
650aAA            WOCNT          1A       47
650aAA            WOSDSP    R             51REFFLD(WKSDSP IV161)
650aAA            WOSTK1         6S       64EDTCDE(K)
650aAA            WOSTK2         4S       72EDTCDE(K)
650aAA            WOSTK3         4S       78EDTCDE(K)
650aAA*
650aAA*
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'IV161: End of Report '
     A*
