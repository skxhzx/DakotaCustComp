     A*%%TS  SD  20060213  121924  DAVE        REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  UM112FM   Unit of Measure Table Maintenance - Change screen
     A*  08 July, 1994
     A*  Hemant Kapadia
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
500 AA*    02/13/06  DAS  5.00
     A*      - Added voice description fields.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20060213  121924  DAVE        REL-V5R3M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'UM112.01'
     A                                  1 24'Unit of Measure Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 35'Detail Entry'
     A                                      DSPATR(HI)
     A                                  5  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  4'Code . . . . . . . . .'
     A            W1CODE         2A  B  8 28
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A  90
     AO 91                                  DSPATR(PR)
     A N21N90N91                            DSPATR(HI)
     A N21N90N91                            DSPATR(UL)
     A                                  9  4'Description  . . . . .'
     A            W1DESC        30A  B  9 28CHECK(LC)
     A  90                                  DSPATR(PR)
     A  02                                  DSPATR(PC)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI)
     A                                 10  4'Short'
     A                                 10 10'Description'
     A                                 10 23'.'
     A                                 10 25'.'
     A            W1SHRT         5A  B 10 28CHECK(LC)
     A  90                                  DSPATR(PR)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A                                 12  4'Singular Description .'
     A            W1DSCS        10   B 12 28CHECK(LC)
     A                                 13  4'Plural Description . .'
     A            W1DSCP        10   B 13 28CHECK(LC)
     A                                 23  2'F5=Refresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
