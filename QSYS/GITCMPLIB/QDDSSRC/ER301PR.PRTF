     A****************************************************************
     A*
     A* ER301PR   Employee management report for an employee(RECV)(Detail)
     A* 20 February, 1996
     A* Hemant Kapadia
     A*
     A* Revisions:
     A*
     A*       COMPILE WITH 215 CPI
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        82
     A*
     A                                   207
     A                                      'Page'
     A            PAGE           4S 0O   212
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O   100
     A                                   210
     A                                      'ER301'
     A*%%***********************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A*%%***********************************************************************
     A                                      TEXT('REPORT HEADING')
     A                                      SPACEA(003)
     A            MRGTXT        45A  O     2
     A*%%***********************************************************************
     A*
     A* Print employee number and name
     A*
     A          R EPHEAD
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1
     A                                      'Employee:'
     A            WKEMP#    R             +1
     A                                      REFFLD(WKEMP# ER301)
     A                                      EDTCDE(Z)
     A            WKNAME    R             +1
     A                                      REFFLD(WKNAME ER301)
     A  92                                75
     A                                      '(Continued)'
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A*%%***********************************************************************
     A*
     A                                     1
     A                                      '  Date  '
     A                                      UNDERLINE
     A                                    10
     A                                      '  Time  '
     A                                      UNDERLINE
     A                                    19
     A                                      '   PO    '
     A                                      UNDERLINE
     A                                    29
     A                                      ' Seq '
     A                                      UNDERLINE
     A                                    35
     A                                      'Item           '
     A                                      UNDERLINE
     A                                    51
     A                                      'Item Description'
     A                                      UNDERLINE
     A                                    82
     A                                      '      Pcs'
     A                                      UNDERLINE
     A                                    92
     A                                      '   Weight'
     A                                      UNDERLINE
     A                                   102
     A                                      '     Cube'
     A                                      UNDERLINE
     A                                   112
     A                                      'Vendor Item'
     A                                      UNDERLINE
     A                                   128
     A                                      '    Vendor#'
     A                                      UNDERLINE
     A                                   143
     A                                      'Vendor Name                      '
     A                                      UNDERLINE
     A                                   184
     A                                      ' Trans#'
     A                                      UNDERLINE
     A                                   200
     A                                      'License  '
     A                                      UNDERLINE
     A                                   192
     A                                      'Label '
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A*%%***********************************************************************
     A*
     A* Print date line
     A*
     A          R DTHEAD
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*           GTDATE         6S 0      1EDTCDE(Y)
     A*           DIRTOT         5S 2     50EDTCDE(3)
     A*           WKPTOT    R             57REFFLD(WKPTOT ER301)
     A*                                     EDTCDE(1)
     A*           WKCTOT    R             67REFFLD(WKCTOT ER301)
     A*                                     EDTCDE(1)
     A*           WKWTOT    R             82REFFLD(WKWTOT ER301)
     A*                                     EDTCDE(1)
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A            GTDATE         6S 0      1
     A                                      EDTCDE(Y)
     A            WKRTIM    R             10
     A                                      REFFLD(WKRTIM ER301)
     A                                      EDTWRD('  :  :  ')
     A            WKPO      R             19
     A                                      REFFLD(WKPO ER301)
     A            WKSEQ     R             29
     A                                      REFFLD(WKSEQ ER301)
     A                                      EDTCDE(3)
     A            WKITEM    R             35
     A                                      REFFLD(WKITEM ER301)
     A            WKITMD    R             51
     A                                      REFFLD(WKITMD ER301)
     A            WKPCS     R             82
     A                                      REFFLD(WKPCS  ER301)
     A                                      EDTCDE(1)
     A            WKWGT     R             92
     A                                      REFFLD(WKWGT  ER301)
     A                                      EDTCDE(1)
     A            WKCUBE    R            102
     A                                      REFFLD(WKCUBE ER301)
     A                                      EDTCDE(1)
     A            WKVITM    R            112
     A                                      REFFLD(WKVITM ER301)
     A            WKVEN#    R            128
     A                                      REFFLD(WKVEN# ER301)
     A                                      EDTCDE(1)
     A            WKVENN    R            143
     A                                      REFFLD(WKVENN ER301)
     A            WKTRN#    R            184
     A                                      REFFLD(WKTRN# ER301)
     A                                      EDTCDE(3)
     A            WKLBL#    R            192
     A                                      REFFLD(WKLBL# ER301)
     A            WKLCNS    R            200
     A                                      REFFLD(WKLCNS ER301)
     A*%%***********************************************************************
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A            $RPD          10A        1
     A            DHRS           7S 2     10
     A                                      EDTCDE(1)
     A            T#PTOT         7S 0     82
     A                                      EDTCDE(1)
     A            T#WTOT         7S 0     92
     A                                      EDTCDE(1)
     A            T#CTOT         7S 0    102
     A                                      EDTCDE(1)
     A            T#TTOT         7S 0    182
     A                                      EDTCDE(1)
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A*%%***********************************************************************
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*%%***********************************************************************
     A*
     A* End of report message
     A*
     A          R FOOT1
     A*%%***********************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER301: End of Report '
     A*
