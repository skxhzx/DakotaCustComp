     A****************************************************************
     A*
     A* RU371CPR   Cleared flags report (Loader Flags only).
     A* August 28, 2007
     A* Jose Duque
     A*
     A* NOTE: This report format is copy of RU371PR.
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
CBI AA*    08/06/04  CBI-JGD
     A*      Field 'Stat/Rustat' was changed to 'Emp#/Ptemp#'
     A*
CBIaAA*    12/29/04  CBI-JGD
     A*      Existing column headings 'Qty/Short Out' changed to
     A*      'Flag Qty', 'Tran#' changed to 'Orig/Tran#'. Also,
     A*       two other columns of info were added to report:
     A*      'Found Qty' and 'Flag Tran#'.
     A*
CBIbAA*    03/29/05  CBI-JGD
     A*      Added three new flag time fields, 'W1STRT', 'W1CLST',
     A*      and 'W1DIFF', also move 'flag & found qty fields side
     A*      by side.
     A*
CBIcAA*    11/10/05  CBI-JGD
     A*      Added two records 'DEPTOT' and 'ALLDPT' to report
     A*      items flagged by dept and total items flagged within
     A*      all departments (WDEPT and WALLDP respectvely).
     A*
CBIdAA*    01/15/07  CBI-JGD
     A*      Added field 'RUTYPE' (flag type).
     A*
CBIeAA*    08/28/07  CBI-JGD
     A*      Added 'Clse/Dlt' and fields 'W1LFLG', 'W1FLCD'
     A*
     A*----------------------------------------------------------------
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A*                                     TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A                                    38
     A                                      DFT('Loader Cleared Flags-by Dept')
     A            FRMDTE         6S 0O    71EDTCDE(Y)
     A                                    81
     A                                      DFT('Thru')
     A            TODTE          6S 0O    86EDTCDE(Y)
     A                                   120
     A                                      DFT('Page')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A            WHSE          30A  O    37
     A                                   125
     A                                      DFT('RU371C')
     A                                      SPACEA(002)
     A*
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    51
CBIbAA                                      DFT('Start')
     A                                    61
CBIbAA                                      DFT('Close')
     A                                    78
CBIbMA                                      DFT('Flag')
CBIbMA                                    84
CBIbMA                                      DFT('Found')
CBIbMA                                    91
CBIbMA                                      DFT('Orig')
CBIbMA                                   100
CBIbMA                                      DFT('Flag')
CBIcAA                                   120
CBIcAA                                      DFT('Flag')
CBIeAA                                   128
     A                                      DFT('Clse')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('Dept')
     A                                     8
     A                                      DFT('Item')
     A                                    18
     A                                      DFT('Description')
CBIbAA                                    51
CBIbAA                                      DFT('Time')
CBIbAA                                    61
CBIbAA                                      DFT('Time')
CBIbAA                                    70
CBIbAA                                      DFT('Diff')
CBIbMA                                    79
CBIbMA                                      DFT('Qty')
CBIbMA                                    85
CBIbMA                                      DFT('Qty')
CBIbMA                                    91
     A                                      DFT('Tran#')
CBIbMA                                   100
CBIbMA                                      DFT('Tran#')
      *
CBIbMA                                   107
     A                                      DFT('Route')
CBIbMA                                   114
CBI MA                                      DFT('Emp#')
CBIdAA                                   120
     A                                      DFT('Type')
CBIeAA                                   128
     A                                      DFT('Dlt')
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A*
     A* Print detail line
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            RUWHDP    R              1REFFLD(LBWHDP LABEL)
     A            ITITEM    R              8REFFLD(ITITEM PIRITEM)
     A            ITDESC    R             18REFFLD(ITDESC PIRITEM)
CBIeAA            W1STRT         6S 0     49EDTWRD('0 :  :  ')
CBIeAA            W1CLST         6S 0     59EDTWRD('0 :  :  ')
CBIeAA            W1DIFF         6S 0     68EDTWRD('0 :  :  ')
     A            RUQYSO    R             77REFFLD(LBQPCK LABEL)
     A                                      EDTCDE(Z)
CBIeMA            RUQYFD    R             83REFFLD(RUQYFD RUNNER)
CBIeMA                                      EDTCDE(3)
     A            RUOTRN    R             90REFFLD(TRAN# PIRDICT)
     A            RUTRN#    R             99REFFLD(TRAN# PIRDICT)
     A            RHRTE     R            108REFFLD(RHRTE RTEHED)
CBIeAA            PTEMP#    R            113REFFLD(PTEMP# PIRTRAN)
     A                                       EDTCDE(Z)
CBIdAA            W1TYPE         6A      120
CBIeAA            W1LFLG         1A      126
CBIeAA            W1FLCD         1A      130
CBI DA*           RUSTAT         1A       89
     A*
     A*----------------------------------------------------------------
CBIcAA          R DEPTOT
CBIcAA                                      SPACEB(001)
CBIcAA            WDEPT          5A        1
CBIcAA                                     6
CBIcAA                                      DFT('Items Flagged:')
CBIcAA            WDPTTO         5S 0     21
CBIcAA                                      EDTCDE(Z)
CBIeAA  22                                29'NONE WERE LOADER''S FLAG'
CBIeAA                                      HIGHLIGHT
     A*
     A*----------------------------------------------------------------
CBIcAA          R ALLDPT
CBIcAA                                      SPACEB(001)
CBIcAA                                     1
CBIcAA                                      DFT('Total Items Flagged:')
CBIcAA            WALLDP         5S 0O    22
CBIcAA                                      EDTCDE(Z)
     A*
CBIcAA* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A*----------------------------------------------------------------
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                     1
     A                                      DFT('RU371C: End of Report ')
     A*
