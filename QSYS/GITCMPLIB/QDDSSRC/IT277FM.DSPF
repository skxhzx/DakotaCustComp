500 AA*----------------------------------------------------------------
500 AA*   Multilingual Version
500 AA*   Every screen has a corresponding Spanish screen.
500 AA*   All Spanish screens are at the end.
500 AA*   Any changes must be made to both screens.
500 AA*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
414 AA* ***************************************************************
414 AA* *****  If the position of any slot entry field is changed *****
414 AA* *****  you must change the WOROW/WOCOL values in the RPG  *****
414 AA* *****  program.                                           *****
414 AA* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  IT272FM
     A*  05 February, 1997
     A*  Dave Sommerville
     A*
     A*  SK112FM - 1.01
     A*
     A*  Revisions
     A*
414 AA*    06/22/00  DAS  4.14
     A*      - Added hidden fields WOROW and WOCOL.
     A*      - Added function CSRLOC to SCREEN1 and SCREEN2
     A*      - Both of the above added for api program APICVTSLT
     A*
500 AA*   06/06/06  JCJ  5.00
     A*     - Merged Spanish screens from PIRS.
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  3'IT277.01'
     A                                  1 16'Slot Repl.'
     A                                      DSPATR(HI)
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                  4  3'Warehouse:'
     A            W1WHSE         3Y 0O  4 14EDTCDE(4)
     A                                  4 21'Dept:'
     A            W1WHDP         5A  O  4 27
     A                                  5  3'Pick slot:'
     A            W1TDIS        12A  O  5 14
     A                                  6  3'Item:'
     A            W1ITEM        65A  O  6  9
     A                                  7  3'Qty.:'
     A            W1QTY          7Y 0O  7  9EDTCDE(4)
     A                                  8  1'Scan:'
     A            W1SCAN        12A  B  8  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  3'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'IT270.02'
     A                                  1 12'Slot Repl.'
     A                                      DSPATR(HI)
     A                                  2  2'Whse'
     A            W2WHSE         3Y 0O  2  7EDTCDE(4)
     A                                  2 11'Dept.'
     A            W2WHDP         5A  O  2 17
     A                                  3  2'Item'
     A            W2ITEM        15A  O  3  7
     A                                  4  2'Pk slot'
     A            W2TDIS        12A  O  4 10
     A                                  5  2'Fm slot'
     A            W2FDIS        12A  B  5 10CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  6  2'Quantity'
     A            W2QTY          5Y 0B  6 12EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N25N90                               DSPATR(HI)
     A                                  7  2'F12=Cancel F20=Replenish'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
500aAA*
500aAA*****************************************************************
500aAA* Spanish Screens
500aAA*****************************************************************
500 AA*----------------------------------------------------------------
500 AA          R SCRN1SP
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  3'IT277.01'
     A                                  1 16'Reap. Slot'
     A                                      DSPATR(HI)
     A                                  1 33DATE
     A                                      EDTCDE(Y)
     A                                  3  4'Escriba cambios, oprima Enter.'
     A                                      COLOR(BLU)
     A                                  4  3'Almacen  :'
     A            W1WHSE         3Y 0O  4 14EDTCDE(4)
     A                                  4 21'Dept:'
     A            W1WHDP         5A  O  4 27
     A                                  5  3'Pick slot:'
     A            W1TDIS        12A  O  5 14
     A                                  6  3'Prdc:'
     A            W1ITEM        65A  O  6  9
     A                                  7  3'Ctd.:'
     A            W1QTY          7Y 0O  7  9EDTCDE(4)
     A                                  8  1'Escn:'
     A            W1SCAN        12A  B  8  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A N23N90                               DSPATR(UL)
     A  03                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                 23  3'F3/F12=Salir F5=Refrescar'
     A                                      COLOR(BLU)
500 AA*----------------------------------------------------------------
500 AA          R SCRN2SP
     A*%%TS  SD  20000622  143323  DAVE        REL-V4R4M0  5769-PW1
     A  98                                  ALARM
     A                                      CLRL(07)
414 AA                                      CSRLOC(WOROW WOCOL)
     A                                      OVERLAY
414 AA            WOROW          3  0H
414 AA            WOCOL          3  0H
     A                                  1  2'IT270.02'
     A                                  1 12'Reap. Slot'
     A                                      DSPATR(HI)
     A                                  2  2'Almc'
     A            W2WHSE         3Y 0O  2  7EDTCDE(4)
     A                                  2 11'Dept.'
     A            W2WHDP         5A  O  2 17
     A                                  3  2'Prdc'
     A            W2ITEM        15A  O  3  7
     A                                  4  2'Pk slot'
     A            W2TDIS        12A  O  4 10
     A                                  5  2'De slot'
     A            W2FDIS        12A  B  5 10CHECK(ER)
     A  24                                  DSPATR(RI)
     A N24N90                               DSPATR(UL)
     A  04
     AON32                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N24N90                               DSPATR(HI)
     A                                  6  2'Cantidad'
     A            W2QTY          5Y 0B  6 12EDTCDE(4)
     A  25                                  DSPATR(RI)
     A N25N90                               DSPATR(UL)
     A  05                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N25N90                               DSPATR(HI)
     A                                  7  2'F12=Cancelar F20=Reaprovisionar'
     A                                      COLOR(BLU)
