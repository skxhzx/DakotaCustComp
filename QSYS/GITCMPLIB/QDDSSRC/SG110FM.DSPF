     A*%%TS  SD  20140416  122426  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG110FM    Staging Import/Export Maintenance
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140411  003404  RODR        REL-V6R1M0  5761-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            SBSTST1       19A  O  8  5
     A            SBSTAT1        3A  O  8 25
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            SBTYPE    R        O  8 29REFFLD(SBREC/SBTYPE *LIBL/STGBATCH)
     A            SBWHSE    R        O  8 40REFFLD(SBREC/SBWHSE *LIBL/STGBATCH)
     A                                      EDTCDE(Z)
     A            SBBATID   R        O  8 44REFFLD(SBREC/SBBATID *LIBL/STGBATCH)
     A            SBTEXT1       25A  O  8 56
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140411  173829  BFC         REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF09
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 24'Staging Import/Export Maintenance'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5' Status Timestamp  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 25'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 29'   Type   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 40'Whs'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 44' Batch ID  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 56'       Description       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A*%%TS  SD  20140328  003018  RODR        REL-V6R1M0  5761-WDS
     A                                 23  2'F3=Exit  F5=Refresh  F9=Select/Sor-
     A                                      t  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW1
     A*%%TS  SD  20140416  103327  BFC         REL-V7R1M0  5770-WDS
     A                                      WINDOW(5 18 11 42)
     A                                      CA03
     A                                      CA12
     A                                  1 11'Selection/Sort Criteria'
     A                                      DSPATR(HI)
     A                                  3  1'Order . . . .'
     A            W1SORT         1A  B  3 15DSPATR(HI)
     A                                  3 20'Ascending, Descending'
     A                                  4  1'Order by  . .'
     A            W1OBY          1A  B  4 15DSPATR(HI)
     A                                  4 20'1 = Timestamp'
     A                                  5 20'2 = Type / Timestamp'
     A                                  6  1'Warehouse . .'
     A            W1WHSE         3Y 0B  6 15DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                  6 27'blank for all'
     A                                  7  1'Type  . . . .'
     A            W1TYPE        10A  B  7 15DSPATR(HI)
     A                                  7 27'*ALL or generic%'
     A                                  8  1'Status. . . .'
     A            W1STATUS       4A  B  8 15DSPATR(HI)
     A                                  8 27'*ALL or generic%'
     A                                 10  1'F3=Exit  F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW2
     A*%%TS  SD  20140416  122426  BFC         REL-V7R1M0  5770-WDS
     A                                      WINDOW(7 27 6 24)
     A                                      CA08
     A                                      CA12
     A                                  2  1'Are you sure you want to'
     A                                      DSPATR(HI)
     A  41N42                           3  5'reset this batch?'
     A                                      DSPATR(HI)
     A  42N41                           3  3'clear these errors?'
     A                                      DSPATR(HI)
     A  41N42                           5  2'F8=Reset'
     A                                      COLOR(BLU)
     A  42N41                           5  2'F8=Clear'
     A                                      COLOR(BLU)
     A                                  5 14'F12=Cancel'
     A                                      COLOR(BLU)
     A          R WINDOW3
     A*%%TS  SD  20140416  122426  BFC         REL-V7R1M0  5770-WDS
     A                                      CA08
     A                                      CA12
     A                                      WINDOW(7 25 6 28)
     A                                  2  2'You are not authorized to'
     A                                      DSPATR(HI)
     A                                      COLOR(RED)
     A                                  3  7'use this option.'
     A                                      DSPATR(HI)
     A                                      COLOR(RED)
     A                                  5  2'Press any key to continue.'
     A                                      COLOR(BLU)
