     A*%%TS  SD  20170630  141648  LYNNM       REL-V7R2M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Multilingual Version
     A*   Every screen has a corresponding Spanish screen.
     A*   All Spanish screens are at the end.
     A*   Any changes must be made to both screens.
     A*   If necessary, contact appropriate person to do translation.
     A*----------------------------------------------------------------
     A* ***************************************************************
     A* *****  If the position of any slot entry field is changed *****
     A* *****  you must change the WOROW/WOCOL values in the RPG  *****
     A* *****  program.                                           *****
     A* ***************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2017 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  IT259FM   License Assign/Activate RF
     A*  29 June 2017
     A*  Lynn McMahon
     A*  created using IT250 as a base
     A*
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20170630  141648  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT259.01'
     A                                  1 12'Activate/Assign'
     A                                      DSPATR(HI)
     A                                  3  2'Whse'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Dept'
     A            W1WHDP         5A  B  4  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  5  2'Slot'
     A            W1DISP        12A  B  5  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 25                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(8)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(8)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*
     A*****************************************************************
     A* Spanish Screens
     A*****************************************************************
     A*----------------------------------------------------------------
     A          R SCRN1SP
     A*%%TS  SD  20170629  082222  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT259.01'
     A                                  1 12'Activate/Assign'
     A                                      DSPATR(HI)
     A                                  3  2'Almc'
     A            W1WHSE         3Y 0B  3  7
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  01                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'Dept'
     A            W1WHDP         5A  B  4  7
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A                                  5  2'Slot'
     A            W1DISP        12A  B  5  7CHECK(ER)
     A  23                                  DSPATR(RI)
     A  03
     AO 20                                  DSPATR(PC)
     A N23                                  DSPATR(HI)
     A                                  7  2'F3/F12=Salir F5=Refrescar'
     A                                      COLOR(BLU)
     A          R DSPSFL                    SFL
     A*%%TS  SD  20170629  160133  LYNNM       REL-V7R2M0  5770-WDS
     A  21                                  SFLNXTCHG
     A            OPTION         1A  B  4  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            W2INFO        28A  O  4  4
     A*
     A          R DSPSFC                    SFLCTL(DSPSFL)
     A*%%TS  SD  20170629  160133  LYNNM       REL-V7R2M0  5770-WDS
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0003)
     A                                      ROLLDOWN
     A                                      ROLLUP
     A                                      OVERLAY
     A  01                                  SFLDSP
     A  01                                  SFLDSPCTL
     A N01                                  SFLCLR
     A                                  3  2'O'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  4'License/Slot                '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A          R OPTREC
     A*%%TS  SD  20170630  141648  LYNNM       REL-V7R2M0  5770-WDS
     A                                      LOCK
     A                                      SLNO(01)
     A                                      OVERLAY
     A                                  1  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A*                                 2  4'1=Activate  2=Assign'
     A                                  2  4'1=Activate'
     A                                      COLOR(BLU)
     A*
     A*    COMMAND LINES
     A*
     A          R CMDREC
     A*%%TS  SD  19931115  111104  DAVE        REL-V2R1M1  5738-PW1
     A                                      LOCK
     A                                      OVERLAY
     A                                      SLNO(07)
     A                                  1  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A*
     A*    'MORE.../BOTTOM' MESSAGE LINE
     A*
     A          R MORREC
     A*                                     LOCK
     A*                                     OVERLAY
     A*                                     SLNO(22)
     A*           MORELN         7   O  1 73DSPATR(HI)
     A*****************************************************************
     A*
     A* RECORDS TO CLEAR DIFFERENT PORTIONS OF A PANEL
     A*
     A*     CLRssnn   ss = Starting position
     A*               nn = # of lines to clear or END
     A*
     A*    CLEAR LINES BETWEEN TITLE AND SUBFILE LIST
     A*
     A          R CLR0306
     A                                      OVERLAY
     A                                      SLNO(3)
     A                                  1  2' '
     A                                  2  2' '
     A                                  3  2' '
     A*                                 4  2' '
     A*                                 5  2' '
     A          R SCREEN3
     A*%%TS  SD  20170630  141648  LYNNM       REL-V7R2M0  5770-WDS
     A  98                                  ALARM
     A                                      CLRL(07)
     A                                      CSRLOC(WOROW      WOCOL)
     A                                      OVERLAY
     A            WOROW          3S 0H
     A            WOCOL          3S 0H
     A                                  1  2'IT259.03'
     A                                  4  2'To License'
     A                                  7  2'F3/F12=Exit F5=Refresh'
     A                                      COLOR(BLU)
     A            W3TYPE        10A  O  2  3
     A            W3LCNSNEW     15A  B  5  3
     A            W3LCNSOLD     15A  O  3  3
