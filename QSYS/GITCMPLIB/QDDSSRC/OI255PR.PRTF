     A*%%***********************************************************************
     A*%%TS  RD  19970828  065827  DAVE        REL-V3R1M0  5763-PW1
     A*%%FI+10660100000000000000000000000000000000000000000000000000
     A*%%FI       0000000000000000000000000000000000000000000000000
     A*%%***********************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  OI255PR    Import FTP orders - Print
     A*  26 February 1999
     A*  Dave Sommerville
     A*
     A*  Revisions
     A*
416aAA*    10/05/01  MLB  4.16a
     F*      - Revised to add new fmt DETAIL2 to print item merge info.
416bAA*    10/11/01  MLB  4.16b
     F*      - Revised to print Cross dock error msg. and Bad PO total
417 AA*    08/08/03  HNK  4.17
     F*      - FIX - REPORT ID FROM OI245 TO OI255.
     A*
520 AA*    01/25/09  CWM  5.20
     A*      - CHANGED STOP TO HOST STOP #
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD1
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HEAD        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   121
417 MA                                      'OI255'
417 DA*                                     'OI245'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A          R HEAD2
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 001
     A*%%***********************************************************************
     A                                      SPACEB(002)
     A                                     3
     A                                      'Order'
     A                                    19
     A                                      'Customer'
     A                                     3
     A                                      'Number'
     A                                      SPACEB(001)
     A                                    +2
     A                                      'Type'
     A                                    19
     A                                      'Number'
     A                                    29
     A                                      'Customer Name'
     A                                    72
     A                                      'Route'
     A                                    79
     A                                      'Stop'
     A                                    85
     A                                      'Truck'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD3
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A            OIHORD    R        O     2
     A                                      REFFLD(OIHORD *LIBL/IMPORDH)
     A                                      SPACEB(001)
     A                                      EDTCDE(3)
     A            OIHTYP    R        O    +3
     A                                      REFFLD(OIHREC/OIHTYP *LIBL/IMPORDH)
     A            OIHCUS    R        O    +3
     A                                      REFFLD(OIHCUS *LIBL/IMPORDH)
     A                                      EDTCDE(3)
     A            OIHCNM    R        O    +2
     A                                      REFFLD(OIHCNM *LIBL/IMPORDH)
     A            OIHRTE    R        O    +3
     A                                      REFFLD(OIHRTE *LIBL/IMPORDH)
520 DA*           OIHSTP    R        O    +3
520 DA*                                     REFFLD(OIHSTP *LIBL/IMPORDH)
520 AA            DSPSTP         3  0O    +3
     A                                      EDTCDE(3)
     A            OIHTRK    R        O    +3
     A                                      REFFLD(OIHTRK *LIBL/IMPORDH)
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%***********************************************************************
     A*
     A          R HEAD4
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                    53
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    +2
     A                                      'Corresponding'
     A                                    +2
     A                                      'Original'
     A                                     4
     A                                      'Item'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Description'
     A                                   +21
     A                                      'Normal'
     A                                    +2
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +3
     A                                      'Whse'
     A                                    +3
     A                                      'Order'
     A                                    +2
     A                                      'Item'
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 002
     A*%%CL 001
     A*%%***********************************************************************
     A          R DETAIL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A            OIDITM    R        O     4
     A                                      REFFLD(OIDITM *LIBL/IMPORDD)
     A            ITDESC        30A  O    21
     A            OIDQ1     R        O    +3
     A                                      REFFLD(OIDREC/OIDQ1 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDQ2     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ2 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDQ3     R        O    +1
     A                                      REFFLD(OIDREC/OIDQ3 *LIBL/IMPORDD)
     A                                      EDTCDE(M)
     A            OIDWH2    R        O    +2
     A                                      REFFLD(OIDREC/OIDWH2 *LIBL/IMPORDD)
     A                                      EDTCDE(4)
     A            OIDOR2    R        O    +2
     A                                      REFFLD(OIDREC/OIDOR2 *LIBL/IMPORDD)
     A                                      EDTCDE(4)
     A            OIDOIT    R        O    +2
     A                                      REFFLD(OIDREC/OIDOIT *LIBL/IMPORDD)
     A            DETMSG        20A  O   106
416aAA*%%***********************************************************************
416aAA          R DETAIL2
416aAA*%%***********************************************************************
416aAA*%%RI 00000
416aAA*%%***********************************************************************
416aAA                                      SPACEB(001)
416aAA            STSMSG        33A  O     6
416aAA                                    +2
416aAA                                      DFT('Host Route/Order#/Line#:')
416aAA            OIHRTE    R        O    +1
416aAA                                      REFFLD(OIHRTE *LIBL/IMPORDH)
416aAA                                    +1
416aAA                                      DFT('/')
416aAA            OIHORD    R        O    +1
416aAA                                      EDTCDE(4)
416aAA                                      REFFLD(OIHORD *LIBL/IMPORDH)
416aAA                                    +1
416aAA                                      DFT('/')
416aAA            OIDSEQ    R        O    +1
416aAA                                      EDTCDE(4)
416aAA                                      REFFLD(OIDSEQ *LIBL/IMPORDD)
416aAA                                    +4
416aAA                                      DFT('FSWMS RouteId/Order#:')
416aAA            PRTRTE    R        O    +1
416aAA                                      REFFLD(OHRTE  *LIBL/ORDH)
416aAA                                    +1
416aAA                                      DFT('/')
416aAA            PRTORD    R        O    +1
416aAA                                      EDTCDE(4)
416aAA                                      REFFLD(OIDORD *LIBL/IMPORDD)
     A*%%***********************************************************************
     A*%%SS
     A*%%SN ITDESC     xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
     A*%%SN OIDQ1      99999
     A*%%SN OIDQ2      99999
     A*%%SN OIDQ3      99999
     A*%%***********************************************************************
     A          R SUBTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                    53
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '------'
     A            SUBQ1          7S 0O    52
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ2          7S 0O    +0
     A                                      EDTCDE(3)
     A            SUBQ3          7S 0O    +0
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%***********************************************************************
     A          R GRNDTTL
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                     4
     A                                      'Customer orders:'
     A                                      SPACEB(001)
     A            GRDORD         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail lines   :'
     A                                      SPACEB(001)
     A            GRDLIN         7S 0O    +1
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing customer orders     :'
     A                                      SPACEB(002)
     A            PREOH          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Pre-existing detail lines        :'
     A                                      SPACEB(001)
     A            PREOD          7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid item numbers             :'
     A                                      SPACEB(001)
     A            NOITEM         7S 0O    39
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Invalid breakdown quantities     :'
     A                                      SPACEB(001)
     A            BADBRK         7S 0O    39
     A                                      EDTCDE(J)
416bAA                                     4
416bAA                                      DFT('Invalid Cross dock P.O.s      -
416bAA                                         :')
416bAA                                      SPACEB(001)
416bAA            BADPO          7S 0O    39
416bAA                                      EDTCDE(J)
     A*%%***********************************************************************
     A*%%SS
     A*%%CL 001
     A*%%CL 001
     A*%%SN GRDORD     9999999
     A*%%CL 001                   :
     A*%%SN GRDLIN     9999999
     A*%%CL 002
     A*%%SN PREOH      9999999
     A*%%CL 001
     A*%%SN PREOD      9999999
     A*%%CL 001
     A*%%CL 001
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(PIRWORK/PD215PR)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(66       132      *N     )
     A*%%***********************************************************************
