     A****************************************************************
     A*
     A* OR662PR   Pallet summary report.(Custom for CBI)
     A* 01 Aug, 2003
     A* Hemant Kapadia
     A*
     A*  Revisions:
     A*    08/01/03   HNK
     A*      - Created using OR660PR.
     A*
417 AA*    08/06/03   HNK  4.17
     A*      - Changed P#DESC to 80.
     A*
417aAA*    12/18/03   HNK  4.17a
     A*      - Changed position of report Id
     A*
520 AA*    01/26/09   CWM  5.20
     A*      - Enh: Changed stop from 2 to 3 character.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A*
     A            UDATE          6S 0O     1
     A                                      EDTCDE(Y)
     A            TITLE         40A  O    21
     A*                                                                )
     A                                    72
     A                                      'Page'
     A            PAGE           4S 0O    77
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     1
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            WHSE          30A  O    26
     A*
     A            BATCH         30A  O    26
     A                                      SPACEB(001)
     A            $RTYPE         8A  O    73
417aAA                                    82
     A                                      'OR662'
     A*
     A          R HEAD2
     A                                      SPACEB(002)
417 MA            P#DESC        80A  O     1
417 DA**          P#DESC        70A  O     1
404 DA* 92                                60
410 MA  92                                72
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEB(002)
     A                                     1
     A                                      'Stop'
     A                                     7
     A                                      'Customer'
     A                                    19
     A                                      'Name'
     A                                    47
     A                                      'Pieces'
     A                                    64
     A                                      'Cube'
     A                                    74
     A                                      'Weight'
404 AA                                    82
404 AA                                      'Transaction#'
     A*
     A* Print detail line  (For Detail Report only)
     A*
     A          R DETAIL1
     A                                      SPACEB(002)
520 DA*           LBSTOP    R        O     1
520 DA*                                     REFFLD(LBSTOP OR662)
520 AA            DSPSTP         3  0O     1
     A                                      EDTCDE(Z)
     A            CUST#         11A  O     7
     A            CSNAME        25A  O    19
     A*
     A*  Print item detail (For Detail Report only)
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A            LBITEM    R        O     7
     A                                      REFFLD(LBITEM OR662)
     A            DESCIT        20A  O    23
     A            T#QPCK         7S 0O    46
     A                                      EDTCDE(Z)
     A            LBUCOD         2A  O    54
410 DA*           T#CUBE        11S 3O    54
410 MA            T#CUBE        11S 3O    54
     A                                      EDTCDE(2)
410 DA*           T#WGHT        11S 2O    66
410 MA            T#WGHT        11S 2O    68
     A                                      EDTCDE(2)
410 DA*           LBTRN#    R        O    82REFFLD(LBTRN# LABEL)
410 MA            LBTRN#    R        O    84REFFLD(LBTRN# LABEL)
404 AA                                      EDTCDE(3)
404 AA*
404 AA*  Print item pack description (For Detail Report only)
404 AA*
404 AA          R DETAIL3
404 AA                                      SPACEB(001)
404 AA            ITPDSC    R        O    23
404 AA                                      REFFLD(ITPDSC OR662)
     A*
     A*  Stop totals (For Detail Report only)
     A*
     A          R TOTALD
     A                                      SPACEB(001)
     A            $RPD          25A  O    20
     A            T#QPCK         7S 0O    46
     A                                      EDTCDE(Z)
     A            T#CUBE        11S 3O    54
     A                                      EDTCDE(2)
410 DA*           T#WGHT        11S 2O    66
410 MA            T#WGHT        11S 2O    68
     A                                      EDTCDE(2)
     A*
     A* Print totals
     A*
     A*  Stop totals (For Summary Report only)
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
520 DA*           LBSTOP    R        O     1
520 DA*                                     REFFLD(LBSTOP OR662)
520 AA            DSPSTP    R        O     1
     A                                      EDTCDE(Z)
     A            CUST#     R        O     7
     A                                      REFFLD(CUST#  *SRC)
     A            CSNAME    R        O    19
     A                                      REFFLD(CSNAME *SRC)
     A            T#QPCK    R        O    46
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    54
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
410 DA*           T#WGHT    R        O    66
410 MA            T#WGHT    R        O    68
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Pallet totals (For Both Detail and Summary)
     A*
     A          R TOTAL2
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    46
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    54
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
410 DA*           T#WGHT    R        O    66
410 MA            T#WGHT    R        O    68
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Department totals (For Both Detail and Summary)
     A*
     A          R TOTAL3
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    46
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    54
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
410 DA*           T#WGHT    R        O    66
410 MA            T#WGHT    R        O    68
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A*  Route totals (For Both Detail and Summary)
     A*
     A          R TOTAL4
     A                                      SPACEB(001)
     A            $RPD      R        O    20
     A                                      REFFLD($RPD *SRC)
     A            T#QPCK    R        O    46
     A                                      REFFLD(T#QPCK *SRC)
     A                                      EDTCDE(Z)
     A            T#CUBE    R        O    54
     A                                      REFFLD(T#CUBE *SRC)
     A                                      EDTCDE(2)
410 DA*           T#WGHT    R        O    66
410 MA            T#WGHT    R        O    68
     A                                      REFFLD(T#WGHT *SRC)
     A                                      EDTCDE(2)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(001)
     A                                     1
     A                                      'OR662: End of Report '
     A*
