     A*%%TS  SD  20070518  082216  BFCTEST1    REL-V5R3M0  5722-WDS
     A*----------------------------------------------------------------***
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------***
     A*
     A*  LOAD19FM     RF Loader Module - Loader Merged Items pallet.
     A*  December 4, 2006
     A*  Jose G. Duque
     A*
     A*----------------------------------------------------------------***
     A*  Revisions:
     A*
417 AA*  02/13/04  MLB  4.17
     A*    - Created.
     A*
     A*----------------------------------------------------------------***
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA09
     A*----------------------------------------------------------------***
     A*         Pallet Loading Screen
     A*----------------------------------------------------------------***
     A          R SCREEN1
     A*%%TS  SD  20070412  102635  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA06
     A                                      CA07
     A  98                                  ALARM
     A                                      CLRL(14)
     A                                      OVERLAY
     A*
     A                                  1  2'LOADR19.01'
     A                                  2  2'Merged Plt Load'
     A                                      DSPATR(HI)
     A*
     A                                  4  2'Route:'
     A            W1RTE#         5A  O  4  9
     A*
     A                                  5  2'Door #:'
     A            W1DOOR         3A  O  5 10
     A*
     A                                  6  2'Plt Id:'
     A            W1PLT#         5Y 0O  6 10EDTCDE(Z)
     A*
     A                                  7  2'Item Count:'
     A N21        W1ITMC         3Y 0O  7 14EDTCDE(Z)
     A  21        W1ITC2         1Y 0O  7 14
     A*
     A                                  9  2'Label #:'
     A            W1LBL#         7Y 0B  9 11
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      CHECK(ER)
     A                                      EDTCDE(Z)
     A                                 10  4'OR'
     A                                 11  2'Item #:'
     A            W1ITM#         6A  B 11 11
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A*
     A                                 12  2'Qty:'
     A            W1QTY          7Y 0B 12 11
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 14  2'F3=Exit F6=Remain'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------***
     A*         Items remaining to be scanned screen
     A*----------------------------------------------------------------***
     A          R SCREEN2
     A*%%TS  SD  20070517  170151  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA07
     A  98                                  ALARM
     A                                      CLRL(14)
     A                                      OVERLAY
     A*
     A                                  1  2'LOADR19.02'
     A                                      DSPATR(PC)
     A                                  2  2'Merged Plt Load'
     A                                      DSPATR(HI)
     A*
     A                                  3  2'Route:'
     A            W2RTE#         5A  O  3  9
     A*
     A                                  4  2'Door #:'
     A            W2DOOR         3A  O  4 10
     A*
     A                                  5  2'Plt Id:'
     A            W2PLT#         5Y 0O  5 10EDTCDE(Z)
     A*
     A                                  6  2'Item Count:'
     A            W2ITMC         3Y 0O  6 14EDTCDE(Z)
     A*
     A                                  8  2'Item #:'
     A            W2ITM#         6A  O  8 10DSPATR(HI)
     A*
     A                                  9  2'Desc:'
     A            W2IDSC        13A  O  9  8DSPATR(HI)
     A*
     A                                 10  2'Pack:'
     A            W2PKSZ        13A  O 10  8DSPATR(HI)
     A*
     A                                 11  2'Qty:'
     A            W2QTY          5Y 0O 11  7EDTCDE(Z)
     A                                      DSPATR(HI)
     A*
     A  09        W2FLGQ         3  0O 11 14EDTCDE(Z)
     A                                      COLOR(PNK)
     A  09                             11 18'Flg'
     A  09                                  DSPATR(BL)
     A  09                                  COLOR(RED)
     A*
     A                                 12  2'Stop:'
     A            W2STOP         2Y 0O 12  8EDTCDE(Z)
     A                                      DSPATR(HI)
     A*
     A                                 14  1'F3=Exit F7=Excp'
     A                                      COLOR(BLU)
     A*
     A*----------------------------------------------------------------***
     A*         Item exception screen
     A*----------------------------------------------------------------***
     A          R SCREEN3
     A*%%TS  SD  20070518  082216  BFCTEST1    REL-V5R3M0  5722-WDS
     A                                      CA03
     A  98                                  ALARM
     A                                      CLRL(14)
     A                                      OVERLAY
     A*
     A                                  1  2'LOADR19.03'
     A                                  2  2'Merged Plt Load'
     A                                      DSPATR(HI)
     A*
     A                                  3  2'Route:'
     A            W3RTE#         5A  O  3  9
     A*
     A                                  4  2'Door #:'
     A            W3DOOR         3A  O  4 10
     A*
     A                                  5  2'Plt Id:'
     A            W3PLT#         5Y 0O  5 10EDTCDE(Z)
     A*
     A                                  6  2'Item Count:'
     A            W3ITMC         3Y 0O  6 14EDTCDE(Z)
     A*
     A  08        W3FLAG         1   B  8  2
     A  90                                  DSPATR(PR)
     A  08                              8  4'. . .  F=Flagged'
     A*
     A N08        W3CODE         1A  B  9  2
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A N08                              9  4'. . .  M=Mispick'
     A N08                             10 11'S=Short'
     A*
     A            W3QTY          5Y 0B 12  2
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A  90                                  DSPATR(PR)
     A                                      EDTCDE(Z)
     A                                 12 11'Qty'
     A*
     A                                 14  1'F3=Exit'
     A                                      COLOR(BLU)
     A*----------------------------------------------------------------***
     A          R CLRSCR
     A                                      CLRL(*ALL)
     A*----------------------------------------------------------------***
     A*
     A* Error Message Subfile Records
     A*
     A*    Error Message Subfile Record
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(15)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    Error Message Subfile Control Record
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(15)
     A                                      CLRL(1)
