     A*%%TS  SD  20140411  164553  BFC         REL-V7R1M0  5770-WDS
     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  SG126FM    Staging Import View Regular/Will-call Order Detail
     A*  Rod Roller
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ERRSFL
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A*
     A          R SFL1                      SFL
     A*%%TS  SD  20140411  164553  BFC         REL-V7R1M0  5770-WDS
     A  21                                  SFLNXTCHG
     A            SFLHIDE      110A  H
     A            OIDSTSTS  R        H      REFFLD(OIDREC/OIDSTSTS *LIBL/IMPSOR-
     A                                      DD)
     A            OIDSTATUS R        H      REFFLD(OIDREC/OIDSTATUS *LIBL/IMPSO-
     A                                      RDD)
     A            OIDTEXT   R        H      REFFLD(OIDREC/OIDTEXT *LIBL/IMPSORD-
     A                                      D)
     A            OIDQ1     R        H      REFFLD(OIDREC/OIDQ1 *LIBL/IMPSORDD)
     A            OIDQ2     R        H      REFFLD(OIDREC/OIDQ2 *LIBL/IMPSORDD)
     A            OIDQ3     R        H      REFFLD(OIDREC/OIDQ3 *LIBL/IMPSORDD)
     A            OPTION         2A  B  8  2
     A  20                                  DSPATR(PC)
     A N21N90                               DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  90                                  DSPATR(PR)
     A            S1STAT1        3A  O  8  5
     A  81                                  COLOR(RED)
     A  82                                  DSPATR(HI)
     A            OIDORDID  R        O  8  9REFFLD(OIDREC/OIDORDID *LIBL/IMPSOR-
     A                                      DD)
     A                                      EDTCDE(Z)
     A            OIDORD    R        O  8 17REFFLD(OIDREC/OIDORD *LIBL/IMPSORDD)
     A                                      EDTCDE(Z)
     A            OIDSEQ    R        O  8 25REFFLD(OIDREC/OIDSEQ *LIBL/IMPSORDD)
     A                                      EDTCDE(Z)
     A            OIDITM    R        O  8 33REFFLD(OIDREC/OIDITM *LIBL/IMPSORDD)
     A            S1QTY         15A  O  8 49
     A*
     A          R SF1CTL                    SFLCTL(SFL1)
     A*%%TS  SD  20140411  164553  BFC         REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0034)
     A                                      SFLPAG(0014)
     A                                      ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN)
     A N32                                  SFLDSP
     A N31                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  91                                  SFLEND(*MORE)
     A            RRN1           4S 0H      SFLRCDNBR(CURSOR)
     A            CSRRRN         5S 0H
     A            SCRNAME        8A  O  1  2
     A                                  1 26'View Route Import Order Detail'
     A                                      DSPATR(HI)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            SCHEAD        58A  O  2 12
     A                                  2 71TIME
     A            SCOPT1        75A  O  4  2COLOR(BLU)
     A            SCOPT2        75A  O  5  2COLOR(BLU)
     A                                  7  1'Opt'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  5'Sts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  9'Ordr ID'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 17' Order '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 25' Seq '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 33'     Item      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 49'  Ordered Qty  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A*
     A          R FKEY1
     A                                 23  2'F3=Exit   F12=Cancel'
     A                                      COLOR(BLU)
     A          R SCREEN2
     A*%%TS  SD  20140411  164553  BFC         REL-V7R1M0  5770-WDS
     A  98                                  ALARM
     A                                  1  2'SG126.02'
     A                                  1 26'View Route Import Order Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Detail Information - Page 1'
     A                                      DSPATR(HI)
     A                                  5  2'Warehouse . . .'
     A            OIDWHS    R        O  5 18REFFLD(OIDREC/OIDWHS *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                  5 34'Batch ID  . . . . .'
     A            OIDBATID  R        O  5 54REFFLD(OIDREC/OIDBATID *LIBL/IMPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  6  2'Order ID  . . .'
     A            OIDORDID  R        O  6 18REFFLD(OIDREC/OIDORDID *LIBL/IMPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                  6 34'Added Timestamp . .'
     A            OIDADDTS  R        O  6 54REFFLD(OIDREC/OIDADDTS *LIBL/IMPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  7  2'Order Seq # . .'
     A            OIDSEQ    R        O  7 18REFFLD(OIDREC/OIDSEQ *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                  7 34'Status Timestamp  .'
     A            OIDSTSTS  R        O  7 54REFFLD(OIDREC/OIDSTSTS *LIBL/IMPSOR-
     A                                      DD)
     A                                      DSPATR(HI)
     A                                  9  2'Status/Message '
     A            OIDSTATUS R        O  9 18REFFLD(OIDREC/OIDSTATUS *LIBL/IMPSO-
     A                                      RDD)
     A                                      DSPATR(HI)
     A            OIDTEXT   R        O  9 24REFFLD(OIDREC/OIDTEXT *LIBL/IMPSORD-
     A                                      D)
     A                                      DSPATR(HI)
     A                                 13  2'Order . . . . .'
     A            OIDORD    R        O 13 18REFFLD(OIDREC/OIDORD *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 14  2'Item  . . . . .'
     A            OIDITM    R        O 14 18REFFLD(OIDREC/OIDITM *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 14 34'Qty Ordered . . . .'
     A            S2QTY         20A  O 14 54DSPATR(HI)
     A                                 15  2'Item Desc Ovr .'
     A            OIMDSC    R        O 15 18REFFLD(OIDREC/OIMDSC *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 16  2'Corresp Whse  .'
     A            OIDWH2    R        O 16 18REFFLD(OIDREC/OIDWH2 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 16 34'Print Item Desc . .'
     A            OIMPDS    R        O 16 54REFFLD(OIDREC/OIMPDS *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 17  2'Corresp Order .'
     A            OIDOR2    R        O 17 18REFFLD(OIDREC/OIDOR2 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                 17 34'Request Item Desc .'
     A            OIMRDS    R        O 17 54REFFLD(OIDREC/OIMRDS *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 18  2'Original Item .'
     A            OIDOIT    R        O 18 18REFFLD(OIDREC/OIDOIT *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 18 34'Capture Box Code  .'
     A            OIMBOX    R        O 18 54REFFLD(OIDREC/OIMBOX *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 19  2'PO #  . . . . .'
     A            OIDPO     R        O 19 18REFFLD(OIDREC/OIDPO *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 19 34'Capture Exp Date  .'
     A            OIMEXP    R        O 19 54REFFLD(OIDREC/OIMEXP *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 20  2'Special Status '
     A            OIDSPC    R        O 20 18REFFLD(OIDREC/OIDSPC *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 20 34'Bulk Pick Y/N . . .'
     A            OIDBLK    R        O 20 54REFFLD(OIDREC/OIDBLK *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 21  2'Pick Slot . . .'
     A            OIDSLT    R        O 21 18REFFLD(OIDREC/OIDSLT *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
     A          R SCREEN3
     A*%%TS  SD  20140411  164553  BFC         REL-V7R1M0  5770-WDS
     A  98                                  ALARM
     A                                  1  2'SG126.03'
     A                                  1 26'View Route Import Order Detail'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 25'Full Detail Information - Page 2'
     A                                      DSPATR(HI)
     A                                  6  2'Misc 1  . . . .'
     A            OIMMS1    R        O  6 18REFFLD(OIDREC/OIMMS1 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                  7  2'Misc 2  . . . .'
     A            OIMMS2    R        O  7 18REFFLD(OIDREC/OIMMS2 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                  8  2'Misc 3  . . . .'
     A            OIMMS3    R        O  8 18REFFLD(OIDREC/OIMMS3 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                  9  2'Misc 4  . . . .'
     A            OIMMS4    R        O  9 18REFFLD(OIDREC/OIMMS4 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 10  2'Misc 5  . . . .'
     A            OIMMS5    R        O 10 18REFFLD(OIDREC/OIMMS5 *LIBL/IMPSORDD)
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit  Enter=Next Screen'
     A                                      COLOR(BLU)
