     A*%%TS  SD  20080716  201910  DAVE        REL-V5R4M0  5722-WDS
     A**************************************************************************
     A*
     A*  PH112FM  Phrase maintenance - Change
     A*  16 July 2008
     A*  Dave Sommerville
     A*
     A*  Revisions:
     A*
600 AA*    05/16/08  DAS  6.00  P#00045
     A*      - Created
     A*
     A**************************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CF09
     A                                      CA12
     A          R SCREEN1
     A*%%TS  SD  20080716  201910  DAVE        REL-V5R4M0  5722-WDS
     A                                      OVERLAY
     A                                      CLRL(23)
     A                                      CF04
     A  98                                  ALARM
     A                                  1  2'PH115.01'
     A                                  1 29'Phrase Table Maintenance'
     A                                      DSPATR(HI)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 33'Formatting Test'
     A                                      DSPATR(HI)
     A                                  4  2'Phrase:'
     A            $PPHRASE      50   O  4 10
     A                                  5  2'XML   :'
     A            W1XML1        50A  O  5 10
     A            W1XML2        50A  O  6 10
     A                                  8  2'Type changes, press Enter.'
     A                                      COLOR(BLU)
     A                                 10  4'Var1:'
     A            W1VAR1        30   B 10 10
     A                                 11  4'Var2:'
     A            W1VAR2        30   B 11 10
     A                                 12  4'Var3:'
     A            W1VAR3        30   B 12 10
     A                                 13  4'Var4:'
     A            W1VAR4        30   B 13 10
     A                                 14  4'Var5:'
     A            W1VAR5        30   B 14 10
     A                                 16  4'Id:'
     A            W1ID           5  0B 16  8
     A                                 16 16'Program:'
     A            W1PGM         10   B 16 25
     A                                 16 37'User:'
     A            W1USER        10   B 16 43
     A                                 16 55'Emp#:'
     A            W1EMP#         5  0B 16 61
     A                                 17  4'Whse:'
     A            W1WHSE         3  0B 17 10
     A                                 17 16'Whdp:'
     A            W1WHDP         5   B 17 22
     A                                 17 29'Item:'
     A            W1ITEM        15   B 17 35
     A                                 17 52'Loc:'
     A            W1LOC         15   B 17 57
     A                                 18  4'Trn#:'
     A            W1TRN#         7  0B 18 10
     A                                 18 20'Lcns:'
     A            W1LCNS        15   B 18 26
     A                                 18 43'LTrn#:'
     A            W1LTRN         7  0B 18 50
     A                                 20  4'Result:'
     A            W1FMT1        50   O 20 12DSPATR(HI)
     A            W1FMT2        50   O 21 12DSPATR(HI)
     A                                 23  2'ENTER=Format Phrase  F3=Exit  F5=R-
     A                                      efresh  F12=Cancel'
     A                                      COLOR(BLU)
     A*****************************************************************
     A*
     A* ERROR MESSAGE SUBFILE RECORDS
     A*
     A*    ERROR MESSAGE SUBFILE RECORD
     A*
     A          R MSGREC                    SFL
     A                                      SFLMSGRCD(24)
     A            #MSGK                     SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*
     A*    ERROR MESSAGE SUBFILE CONTROL RECORD
     A*
     A          R MSGCTL                    SFLCTL(MSGREC)
     A  98                                  ALARM
     A                                      LOCK
     A                                      OVERLAY
     A                                      SFLSIZ(2) SFLPAG(1)
     A N97                                  SFLCLR
     A  97                                  SFLDSP
     A  97                                  SFLEND
     A          R MSGCLR
     A                                      OVERLAY
     A                                      LOCK
     A                                      SLNO(24)
     A                                      CLRL(1)
     A          R CLRSCR
     A                                      CLRL(*ALL)
