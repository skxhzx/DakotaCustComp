     A****************************************************************
     A*
     A* ER582PR   Crew Activity report
     A* 23 May, 2011
     A* Ginger Andrews
     A*
     A*
     A* Revisions:
     A*
     A* 05/23/11 GJA 6.40
     A*   - Created
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        10
     A*
     A                                    70
     A                                      'Page'
     A            PAGE           4S 0O    75
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A            DTERNG        22A  O    29
     A                                    73
     A                                      'ER582D'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      SPACEB(001)
     A                                      TEXT('REPORT HEADING')
     A            TMERNG        22A  O    29
     A                                    29'Crew:'
     A                                      SPACEB(001)
     A            WKCREW    R             +1REFFLD(PTCREW ER581)
     A            WKDESC        40A       +1
     A  92                                70'(Continued)'
     A  50                                18'** Pieces Do Not Include +
     A                                      Merge Pickup Pieces **'
     A                                      SPACEB(001)
     A* Print employee number and name
     A*
     A          R CRHEAD
     A                                      SPACEB(001)
     A            W1EMP#         5S 0      1
     A                                      EDTCDE(Z)
     A            W1NAME        25A        8
     A                                      SPACEA(002)
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    14'--------------Sele'
     A                                    32'cted--------------'
     A                                    50'Direct'
     A                                    58'Cases'
     A                                    66'Trans'
     A                                    74'Items'
     A                                      SPACEA(001)
     A*
     A                                    14' Trans'
     A                                      UNDERLINE
     A                                    21' Items'
     A                                      UNDERLINE
     A                                    28' Cases'
     A                                      UNDERLINE
     A                                    35' Short'
     A                                      UNDERLINE
     A                                    42'  Net '
     A                                      UNDERLINE
     A                                    49'  Hrs  '
     A                                      UNDERLINE
     A                                    57' Per/Hr'
     A                                      UNDERLINE
     A                                    65' Per/Hr'
     A                                      UNDERLINE
     A                                    73' Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A* Print detail
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            W1ZONE         5A        1
     A            W1WHDP         5A        7
     A            D#TRTOT        6S 0     13EDTCDE(2)
     A            D#RECK         6S 0     20EDTCDE(2)
     A            D#PTOT         6S 0     27EDTCDE(2)
     A            D#STOT         6S 0     34EDTCDE(2)
     A            D#NTOT         6S 0     41EDTCDE(2)
     A            D#DTOT         6S 2     48EDTCDE(2)
     A            D#CSPH         6S 0     57EDTCDE(2)
     A            D#TRPH         6S 0     65EDTCDE(2)
     A            D#REPH         6S 0     73EDTCDE(2)
     A                                      SPACEA(001)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A                                    13'_________________________+
     A                                      _________________________+
     A                                      ________________'
     A                                      SPACEA(001)
     A            T#TRTOT        6S 0     13EDTCDE(2)
     A            T#RECK         6S 0     20EDTCDE(2)
     A            T#PTOT         6S 0     27EDTCDE(2)
     A            T#STOT         6S 0     34EDTCDE(2)
     A            T#NTOT         6S 0     41EDTCDE(2)
     A            T#DTOT         6S 2     48EDTCDE(2)
     A            T#CSPH         6S 0     57EDTCDE(2)
     A            T#TRPH         6S 0     65EDTCDE(2)
     A            T#REPH         6S 0     73EDTCDE(2)
     A                                      SPACEA(001)
     A* Print grand totals
     A*
     A          R GTOTAL
     A                                    13'_________________________+
     A                                      _________________________+
     A                                      ________________'
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A            G#TRTOT        7S 0     13EDTCDE(2)
     A            G#RECK         7S 0     20EDTCDE(2)
     A            G#PTOT         7S 0     27EDTCDE(2)
     A            G#STOT         7S 0     34EDTCDE(2)
     A            G#NTOT         7S 0     41EDTCDE(2)
     A            G#DTOT         7S 2     49EDTCDE(2)
     A            G#CSPH         7S 0     57EDTCDE(2)
     A            G#TRPH         7S 0     65EDTCDE(2)
     A            G#REPH         7S 0     73EDTCDE(2)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER582D: End of Report '
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER582D: No records found'
     A*
