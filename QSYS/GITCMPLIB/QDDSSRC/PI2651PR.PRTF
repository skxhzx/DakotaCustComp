     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  PI2651PR    Import FTP purchase orders - Exceptions
     A*  18 March 2004
     A*  Max Blacknik
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*  03/18/04  MLB  4.17
     A*    - Created
     A*
510 AA*    06/08/06  MLB  5.10
     A*      - Enh: Added buyer # and name to p/o heading line.
     A*
     A*----------------------------------------------------------------
     A*
     A          R HEAD11
     A                                      SKIPB(002)
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                     1
     A                                      DATE
     A                                      EDTCDE(Y)
     A            W1HDR1        50A  O   +32
     A                                   122
     A                                      'Page'
     A                                     1
     A                                      TIME
     A                                      SPACEB(001)
     A            WHHEAD        40A  O   +37
     A                                   122
     A                                      'PI265'
     A*
     A          R HEAD21
     A                                      SPACEB(002)
     A                                     7
     A                                      'P.O.'
     A                                    17
     A                                      'Vendor'
     A                                     1
     A                                      SPACEB(001)
     A                                      'Type'
     A                                     7
     A                                      'Number'
     A                                    17
     A                                      'Number'
     A                                    29
     A                                      'Vendor Name'
     A                                   +66
     A                                      'Exception Message'
     A*
     A          R HEAD31
     A            PIHTYP         1A  O     3
     A                                      SPACEB(001)
     A            PIHPO     R        O     7
     A                                      REFFLD(PIHPO *LIBL/IMPFPOH2)
     A            PIHVEN    R        O    17
     A                                      REFFLD(PIHVEN *LIBL/IMPFPOH2)
     A                                      EDTCDE(3)
     A            PIHVNA    R        O    29
     A                                      REFFLD(PIHVNA *LIBL/IMPFPOH2)
510 AA            PIHBUY         3A  O    71
510 AA            PIHBYN        30A  O    75
     A            HEDMSG        20A  O   106
     A  81                               127
     A                                      '(CONT)'
     A                                      HIGHLIGHT
     A*
     A          R HEAD41
     A                                    64
     A                                      '----- Quantity -----'
     A                                      SPACEB(002)
     A                                    89
     A                                      'Vendor'
     A                                     4
     A                                      'Type'
     A                                      SPACEB(001)
     A                                    +3
     A                                      'Seq'
     A                                    16
     A                                      'Item'
     A                                    32
     A                                      'Description'
     A                                    64
     A                                      'Normal'
     A                                    72
     A                                      'Brk 1'
     A                                    +2
     A                                      'Brk 2'
     A                                    +5
     A                                      'Item'
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            PIDTYP         1A  O     6
     A            PIDSEQ         5S 0O    +2
     A                                      EDTCDE(L)
     A            PIDITM    R        O    16
     A                                      REFFLD(PIDITM *LIBL/IMPFPOD2)
     A            ITDESC        30A  O    32
     A            PIDQO1    R        O    65
     A                                      REFFLD(PIDREC/PIDQO1 *LIBL/IMPFPOD2)
     A                                      EDTCDE(M)
     A            PIDQO2    R        O    72
     A                                      REFFLD(PIDREC/PIDQO2 *LIBL/IMPFPOD2)
     A                                      EDTCDE(M)
     A            PIDQO3    R        O    +1
     A                                      REFFLD(PIDREC/PIDQO3 *LIBL/IMPFPOD2)
     A                                      EDTCDE(M)
     A            PIDVIT    R        O    89
     A                                      REFFLD(PIDREC/PIDVIT *LIBL/IMPFPOD2)
     A            DETMSG        20A  O   106
     A*
     A          R SUBTTL1
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    64
     A                                      '------'
     A                                    71
     A                                      '------'
     A                                    78
     A                                      '------'
     A            SUBQ11         7S 0O    63
     A                                      EDTCDE(3)
     A                                      SPACEB(001)
     A            SUBQ21         7S 0O    70
     A                                      EDTCDE(3)
     A            SUBQ31         7S 0O    77
     A                                      EDTCDE(3)
     A*
     A          R GRNDTTL1
     A                                      SPACEB(001)
     A                                     1
     A                                      SPACEB(001)
     A                                      'Grand Totals'
     A                                    26
     A                                      'Add'
     A                                      SPACEB(001)
     A                                    34
     A                                      'Change'
     A                                    +6
     A                                      'Delete'
     A                                    +4
     A                                      'Replace'
     A                                    +4
     A                                      'Invalid'
     A                                    +6
     A                                      'Total'
     A                                     4
     A                                      'Header records:'
     A                                      SPACEB(001)
     A            HDRADD         7S 0O    +1
     A                                      EDTCDE(J)
     A            HDRCHG         7S 0O    +1
     A                                      EDTCDE(J)
     A            HDRDEL         7S 0O    43
     A                                      EDTCDE(J)
     A            HDRRPL         7S 0O    54
     A                                      EDTCDE(J)
     A            HDROTH         7S 0O    65
     A                                      EDTCDE(J)
     A            HDRTTL         7S 0O    76
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Detail records:'
     A                                      SPACEB(001)
     A            DTLADD         7S 0O    +1
     A                                      EDTCDE(J)
     A            DTLCHG         7S 0O    +1
     A                                      EDTCDE(J)
     A            DTLDEL         7S 0O    43
     A                                      EDTCDE(J)
     A                                    +5
     A                                      'N/A'
     A            DTLOTH         7S 0O    65
     A                                      EDTCDE(J)
     A            DTLTTL         7S 0O    76
     A                                      EDTCDE(J)
     A                                     4
     A                                      'Exceptions    :'
     A                                      SPACEB(002)
     A            TTLEXC         7S 0O    20
     A                                      EDTCDE(J)
