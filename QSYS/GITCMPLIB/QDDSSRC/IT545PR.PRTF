     A*-------------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*-------------------------------------------------------------------
     A*   IT545PR  Outside Storage Listing - Report
     A*   02 September 2009
     A*   Ginger Andrews
     A*
     A*-------------------------------------------------------------------------
     A*  Revisions
     A*
640  A*  09/02/09  GJA  6.10
     A*    - Created.
610a A*  12/13/10  GJA  6.10a
     A*    - Fix:  Added message when item wasn't found in PIRITEM
     A*
     A*****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(002)
     A*
     A            RDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            WHSE          40A  O    40
     A                                   122
     A                                      DFT('Page')
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            RTIME          6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(001)
     A*
     A            OSSLOC        40A  O    40
     A                                   124
     A                                      DFT('IT545')
     A                                      SPACEA(001)
     A*%%***********************************************************************
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                      SPACEA(001)
     A*
     A                                     1
     A                                      SPACEB(001)
     A*
     A                                      DFT('Dept ')
     A                                      UNDERLINE
     A                                     8
     A                                      DFT('Item Number   ')
     A                                      UNDERLINE
     A                                    25
     A                                      DFT('Description                   -
     A                                      ')
     A                                      UNDERLINE
     A                                    59
     A                                      DFT('Qty')
     A                                      UNDERLINE
     A                                    68
     A                                      DFT('Slot')
     A                                      UNDERLINE
     A                                    85
     A                                      DFT('Entered')
     A                                      UNDERLINE
     A                                    95
     A                                      DFT('Expired')
     A                                      UNDERLINE
     A*%%***********************************************************************
     A* Print detail line
     A*
     A          R DETAIL1
     A*
     A                                      SPACEA(001)
     A            ITWHDP    R        O     1
     A                                      REFFLD(ITWHDP PIRITEM)
     A            ITITEM    R        O     8
     A                                      REFFLD(ITITEM PIRITEM)
     A            ITDESC    R        O    25
     A                                      REFFLD(ITDESC PIRITEM)
     A            SLSTK1    R        O    56REFFLD(SLSTK1 SLOT)
     A                                      EDTCDE(J)
     A            SLDISP    R        O    68REFFLD(SLDISP SLOT)
     A            ENTD           6  0O    85EDTCDE(Y)
     A            EXPD           6  0O    95EDTCDE(Y)
640aAA  20                               110'Item not found'
     A*
     A* Print detail line
     A*
     A          R DETAIL2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1'No Items Found for Transfer...'
     A*%%***********************************************************************
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT(' ')
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     1
     A                                      DFT('IT545: End of Report')
     A*
