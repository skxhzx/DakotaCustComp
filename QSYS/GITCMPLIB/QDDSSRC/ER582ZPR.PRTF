     A****************************************************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A* ER582ZPR  Crew Activity report by zone
     A* 23 May, 2011
     A* Ginger Andrews
     A*
     A*
     A* Revisions:
     A*
     A* 05/23/11 GJA 6.40
     A*   - Created.
     A*
     A****************************************************************
     A*
     A* Print report headings
     A*
     A          R HEAD1
     A                                      TEXT('REPORT HEADING')
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*
     A            UDATE          6S 0O     3
     A                                      EDTCDE(Y)
     A            RPTTTL        60        37
     A*
     A                                   122
     A                                      'Page'
     A            PAGE           4S 0O   126
     A                                      EDTCDE(Z)
     A*
     A            TIME           6S 0O     3
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEB(002)
     A            DTERNG        22A  O   +45
     A                                   125
     A                                      'ER582Z'
     A*
     A* Print report headings
     A*
     A          R HEAD2
     A                                      TEXT('REPORT HEADING')
     A            TMERNG        22A  O    56
     A                                      SPACEA(003)
     A  50                                43'** Pieces Do Not Include +
     A                                      Merge Pickup Pieces **'
     A* Print employee number and name
     A*
     A          R CRHEAD
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     1'Crew:'
     A            WKCREW    R             +1REFFLD(PTCREW ER581)
     A            WKDESC        40A       +1
     A  92                                75
     A                                      '(Continued)'
     A*
     A* Print column headings
     A*
     A          R COLUMNS
     A                                    44'Trans'
     A                                    53'Items'
     A                                    62'Cases'
     A                                    78'Total'
     A                                    92'Net'
     A                                   102'Direct'
     A                                   115'Cases'
     A                                   122'Trans'
     A                                   132'Items'
     A                                      SPACEA(001)
     A*
     A                                     1'Employee         '
     A                                      UNDERLINE
     A                                    43'Selected'
     A                                      UNDERLINE
     A                                    52'Selected'
     A                                      UNDERLINE
     A                                    61'Selected'
     A                                      UNDERLINE
     A                                    74'Qty Shorted'
     A                                      UNDERLINE
     A                                    88'Qty Selected'
     A                                      UNDERLINE
     A                                   104'Hrs'
     A                                      UNDERLINE
     A                                   115'Per/Hr'
     A                                      UNDERLINE
     A                                   122'Per/Hr'
     A                                      UNDERLINE
     A                                   132'Per/Hr'
     A                                      UNDERLINE
     A                                      SPACEA(002)
     A* Print detail
     A*
     A          R DETAIL1
     A                                      SPACEB(001)
     A            W1EMP#         5S 0      1
     A                                      EDTCDE(Z)
     A            W1NAME        25A        8
     A            W1ZONE         5A       35
     A            D#TRTOT        7S 0     42EDTCDE(1)
     A            D#RECK         7S 0     51EDTCDE(1)
     A            D#PTOT         7S 0     60EDTCDE(1)
     A            D#STOT         7S 0     74EDTCDE(1)
     A            D#NTOT         7S 0     88EDTCDE(1)
     A            D#DTOT         7S 2     99EDTCDE(1)
     A            D#CSPH         7S 0    112EDTCDE(1)
     A            D#TRPH         7S 0    119EDTCDE(1)
     A            D#REPH         7S 0    129EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Print totals
     A*
     A          R TOTAL1
     A                                      SPACEB(001)
     A            T#TRTOT        7S 0     42EDTCDE(1)
     A            T#RECK         7S 0     51EDTCDE(1)
     A            T#PTOT         7S 0     60EDTCDE(1)
     A            T#STOT         7S 0     74EDTCDE(1)
     A            T#NTOT         7S 0     88EDTCDE(1)
     A            T#DTOT         7S 2     99EDTCDE(1)
     A            T#CSPH         7S 0    112EDTCDE(1)
     A            T#TRPH         7S 0    119EDTCDE(1)
     A            T#REPH         7S 0    129EDTCDE(1)
     A                                      SPACEA(001)
     A* Print grand totals
     A*
     A          R GTOTAL
     A                                      SPACEB(001)
     A            G#TRTOT        7S 0     44EDTCDE(1)
     A            G#RECK         7S 0     53EDTCDE(1)
     A            G#PTOT         7S 0     61EDTCDE(1)
     A            G#STOT         7S 0     74EDTCDE(1)
     A            G#NTOT         7S 0     88EDTCDE(1)
     A            G#DTOT         7S 2     99EDTCDE(1)
     A            G#CSPH         7S 0    112EDTCDE(1)
     A            G#TRPH         7S 0    119EDTCDE(1)
     A            G#REPH         7S 0    129EDTCDE(1)
     A                                      SPACEA(001)
     A*
     A* Skip 1 Line
     A*
     A          R SKIP1
     A                                      TEXT('SKIP ONE LINE')
     A                                      SPACEA(001)
     A                                     1
     A                                      ' '
     A*
     A* End of report message
     A*
     A          R FOOT1
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER582Z: End of Report '
     A*
     A* No records found
     A*
     A          R FOOT2
     A                                      SPACEA(001)
     A                                     1
     A                                      'ER582Z: No records found'
     A*
