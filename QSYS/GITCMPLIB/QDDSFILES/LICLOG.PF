     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICLOG   (PF)
     A*  Description ... License log file (replaced LICHIST file)
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
600 AA*    10/15/08  DAS  600
     A*      - Created
     A*
640aAA*    05/28/11  DAS  6.40a
     A*      - Added fields lltstat, llostat, llexpts, llexpuc,
     A*        llexpjusr, llexpjob, llexpnbr, llexpflag
     A*
640bAA*    06/06/11  DAS  6.40b
     A*      - Added fields llltrnseq, llfltrn, llfltrnseq.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*   - License transaction number (LRLTRN) is used just as a
     A*     sequence number for the file.
     A*
     A*   - License number (LRLCNS) is the visible license number on
     A*     an item.
     A*
     A*   - Original license number (LRORLC). This differs from the
     A*     "From" license number in that it should contain the license
     A*     number that is associated with the original information for
     A*     the item not just the license number that was split. The
     A*     first time a record is created for a license number, the
     A*     the original license number will be the same as the license
     A*     number (LRLCNS). After that, the original license number
     A*     number never changes.
     A*
     A*   - From area (LRFARA) is used in conjunction with the "From"
     A*     Code (LRFMCD) values A thru D.
     A*     The From slot fields(LRFWHD,LRFASL,LRFLOC,LRFLVL,LRFHND)
     A*     are used when LRFMCD is 'S'lot.
     A*
     A*   - To area (LRTARA) is used in conjunction with the "To" Code
     A*     (LRTOCD) values A thru D.
     A*     The To slot fields(LRTWHD,LRTASL,LRTLOC,LRTLVL,LRTHND) are
     A*     used when LRTOCD is 'S'lot or 'F'loor.
     A*
     A*   - To order number (LRTORD) is used when To Code (LRTOCD) is
     A*     'P'icked. This identifies the customer that ordered the
     A*     item.
     A*
     A*   - LRVRFY identifies if the license was verified (Y)
     A*     by the user.
     A*
     A*   - Item numbers should always be "Slot" item numbers. The
     A*     only exception is the From item of a Receiving code. In
     A*     that case the item should be the actual item received. The
     A*     corresponding To item for the Dock code will be the "Slot"
     A*     item.
     A*
     A*   - LRTCHG identifies if the item number of the license
     A*     changed (i.e. REF item to a FRZ item).
     A*
640aAA*   - LLOSTAT is only used for STS (status change) action.
     A*
640bAA*   - LLLTRNSEQ should be a seq# within the LLLTRN number, which
     A*     is a unique number for a group of records written at the
     A*     same time, such as the 2 records of a TFR that remove the
     A*     qty from the normal slot and put the qty into the USR slot.
     A*
640bAA*   - LLFLTRN and LLFLTRNSEQ should provide a link back to the
     A*     Normal slot information. For example, for a TFR, the normal
     A*     information is the first record that is created taking the
     A*     qty out of the Normal (non-BFC) slot. This information is
     A*     passed to the USR record, then from the USR record to the
     A*     final TFR record into another normal slot. Now, the final
     A*     TFR record has a direct link to the first TFR record, even
     A*     if the transfer bombs in the middle and has to be finished
     A*     with a manual transfer from the USR slot into the normal
     A*     slot.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    From Code  (LRFMCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'I'  - Inventory.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
     A*    To Code  (LRTOCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
640aAA*    Export Flag (LLEXPFLAG)
     A*
     A*       ' '  - Not exported.
     A*       '1'  - Exported.
     A*       '2'  - Skipped.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Special License Numbers
     A*
     A*    Starting
     A*    Characters
     A*
     A*      '*SU'  -  Start Up
     A*                These license plates are used when a client
     A*                starts using license plate tracking after using
     A*                Dakota FS-WMS for a while. A start up program
     A*                will assign an '*SU' license plate to slots
     A*                that contain an item to be tracked.
     A*
     A*      '*NF'  -  Not Found
     A*                These license plates are created on-the-fly when
     A*                a license plate does not exist for a slot/item.
     A*
     A*      '*ML'  -  Missing License
     A*                These license plates are created when a user is
     A*                asked to scan a particular license plate when
     A*                taking an item out of a slot, but the user
     A*                cannot find the license plate to scan. The user
     A*                would press F10 to take an exception and let
     A*                the system know the requested license plate is
     A*                missing.
     A*
     A*      '*NA'  -  Not Applied
     A*                These license plates are created when a user
     A*                is asked to apply a new license plate to a
     A*                partial quantity being put into a slot, but the
     A*                user doesn't do it and presses F10 instead.
     A*
     A*      '*NT'  -  Not Tracked
     A*                These license plates are created for items in
     A*                which the License Tracking option is set to No.
     A*
     A*      '*PS'  -  Pick Slot
     A*                These license plates are created for items in
     A*                which a partial quantity was put into the Pick
     A*                slot and either we are in Locator mode or we
     A*                do not ask selectors to scan a license plate.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R LLREC
     A*
     A            LLWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            LLLTRN    R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
640bAA            LLLTRNSEQ      2S 0       TEXT('Lic Tran Seq #')
640bAA                                      COLHDG('Lic Tran' 'Seq #')
     A            LLACTION       3A         TEXT('Action')
     A                                      COLHDG('Action')
     A            LLTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            LLETRN#   R               TEXT('Employee Transaction Number')
     A                                      COLHDG('Employee' 'Transaction' -
     A                                      'Number')
     A                                      REFFLD(TRAN#)
     A            LLLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LLLCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            LLOLCNS       15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            LLVRFY         1A         TEXT('Lic Verified')
     A                                      COLHDG('Lic' 'Verified')
     A            LLFLCNS       15A         TEXT('"From" License Number')
     A                                      COLHDG('"From"' 'License' 'Number')
     A*
     A            LLAQTY1        5P 0       TEXT('Action Qty1')
     A                                      COLHDG('Action' 'Qty1')
     A            LLAQTY2        5P 0       TEXT('Action Qty2')
     A                                      COLHDG('Action' 'Qty2')
     A            LLAQTY3        5P 0       TEXT('Action Qty3')
     A                                      COLHDG('Action' 'Qty3')
     A*
     A            LLRQTY1        5P 0       TEXT('Result Qty1')
     A                                      COLHDG('Result' 'Qty1')
     A            LLRQTY2        5P 0       TEXT('Result Qty2')
     A                                      COLHDG('Result' 'Qty2')
     A            LLRQTY3        5P 0       TEXT('Result Qty3')
     A*
     A            LLSQTY1        5P 0       TEXT('Sent Actn Qty1')
     A                                      COLHDG('Sent' 'Actn Qty1')
     A            LLSQTY2        5P 0       TEXT('Sent Actn Qty2')
     A                                      COLHDG('Sent' 'Actn Qty2')
     A            LLSQTY3        5P 0       TEXT('Sent Actn Qty3')
     A                                      COLHDG('Sent' 'Actn Qty3')
     A*
     A* "To" Location information
     A*
     A            LLCODE         1A         TEXT('To Code')
     A                                      COLHDG('To' 'Code')
     A            LLAREA        15A         TEXT('To Area')
     A                                      COLHDG('To' 'Area')
     A            LLWHDP    R               TEXT('To Department')
     A                                      COLHDG('To' 'Department')
     A                                      REFFLD(WDCODE)
     A            LLDISP        12A         TEXT('To Display')
     A                                      COLHDG('To' 'Display')
     A            LLAISL    R               TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            LLLOC     R               TEXT('To Location')
     A                                      COLHDG('To' 'Location')
     A                                      REFFLD(SLLOC)
     A            LLRLVL    R               TEXT('To Rack Level')
     A                                      COLHDG('To' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LLHAND    R               TEXT('To Handstack')
     A                                      COLHDG('To' 'Handstack')
     A                                      REFFLD(HSCODE)
640aAA            LLTSTAT   R               TEXT('To Status')
640aAA                                      COLHDG('To' 'Status')
640aAA                                      REFFLD(SSCODE)
640aAA            LLOSTAT   R               TEXT('Orig Status')
640aAA                                      COLHDG('Orig' 'Status')
640aAA                                      REFFLD(SSCODE)
     A            LLBASE        12A         TEXT('To Base Slot')
     A                                      COLHDG('To' 'Base Slot')
     A            LLPOS          3P 0       TEXT('To Pallet Position')
     A                                      COLHDG('To' 'Pallet' 'Position')
     A            LLITEM    R               TEXT('To Item')
     A                                      COLHDG('To' 'Item')
     A                                      REFFLD(ITITEM)
     A            LLITYPE        1A         TEXT('To Item Type')
     A                                      COLHDG('To' 'Item Type')
     A            LLICHG         1A         TEXT('Item Changed')
     A                                      COLHDG('Item' 'Changed')
     A            LLSITEM   R               TEXT('To Slot Item')
     A                                      COLHDG('To' 'Slot Item')
     A                                      REFFLD(ITITEM)
     A            LLSITYPE       1A         TEXT('Slot Item Type')
     A                                      COLHDG('Slot' 'Item Type')
     A            LLPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            LLENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            LLEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A*
     A* To Customer information
     A*
     A            LLORD     R     P         TEXT('To Order Number')
     A                                      COLHDG('To' 'Order' 'Number')
     A                                      REFFLD(OHORD)
     A            LLSEQ     R     P         TEXT('To Order Sequence Number')
     A                                      COLHDG('To Order' 'Sequence Nbr')
     A                                      REFFLD(SEQ#)
     A*
     A            LLEXCD         8A         TEXT('Exception Code')
     A                                      COLHDG('Exception' 'Code')
640bAA*
640bAA* Link to Normal From information (see notes)
640bAA*
640bAA            LLFLTRN   R               TEXT('From Lic Tran')
640bAA                                      COLHDG('From' 'Lic Tran')
640bAA                                      REFFLD(TRAN#)
640bAA            LLFLTRNSEQ     2S 0       TEXT('From Lic Tran Seq')
640bAA                                      COLHDG('From' 'Lic Tran Seq')
     A*
     A* Timestamp / User tracking information.
     A*
     A            LLADDTS   R               REFFLD(ADDTS)
     A            LLADDUC   R               REFFLD(ADDUC)
     A            LLADDCPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Calling Pgm')
     A                                      TEXT('Calling Pgm')
     A            LLADDJPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Job Pgm')
     A                                      TEXT('Job Pgm')
     A            LLADDEMP  R               REFFLD(ADDEMP)
     A            LLADDEUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Employee' 'User')
     A                                      TEXT('Employee User')
     A            LLADDCUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            LLADDJUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            LLADDJOB  R               REFFLD(ADDJOB)
     A            LLADDNBR  R               REFFLD(ADDNBR)
     A*
640aAA            LLEXPFLAG      1A         TEXT('Export Flag')
640aAA                                      COLHDG('Export' 'Flag')
640aAA            LLEXPTS   R               REFFLD(ADDTS)
640aAA                                      COLHDG('Export Stamp')
640aAA                                      TEXT('Export Stamp')
640aAA            LLEXPUC   R               REFFLD(ADDUC)
640aAA                                      COLHDG('Export UC Stamp')
640aAA                                      TEXT('Export UC Stamp')
640aAA            LLEXPJUSR R               REFFLD(ADDUSR)
640aAA                                      TEXT('Export Job User')
640aAA                                      COLHDG('Export' 'Job User')
640aAA            LLEXPJOB  R               REFFLD(ADDJOB)
640aAA                                      TEXT('Export Job')
640aAA                                      COLHDG('Export' 'Job')
640aAA            LLEXPNBR  R               REFFLD(ADDNBR)
640aAA                                      TEXT('Export Job Nbr')
640aAA                                      COLHDG('Export' 'Job Nbr')
     A*
     A            LLDRISLOT     10A         TEXT('DRISLOT Cmd')
     A                                      COLHDG('DRISLOT Cmd')
     A            LLDRILIC      10A         TEXT('DRILICENSE Cmd')
     A                                      COLHDG('DRILICENSE' 'Cmd')
     A*
     A          K LLWHSE
     A          K LLLCNS
     A          K LLADDUC
