     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PRDLBHIST
     A*  Description ... Production Label History File
     A*  Creator ....... KDE
     A*
     A*
     A*  Revisions
     A*
650 AA*    Ver 6.50  10/31/14  KDE
     A*      - Created
700 AA*    Ver 7.00  04/09/15  KDE
     A*      - Added fields to track UCI and slot for Box Breaker
710 AA*    VER 7.10  07/11/17  KDE
     A*      - Added field to track mark non-unique uci case scan
     A*        as unique with a sequencial number.  This is needed
     A*        when user is breaking down non-unique uci cases
     A*        to keep breakdown records separated by individual case
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R LHHST
     A*
     A            LHCTS           Z         TEXT('Current Time Stamp')
     A                                      COLHDG('Current Time Stamp')
     A            LHDTETIM      14          TEXT('CCYYMMDDHHMMSS')
     A                                      COLHDG('Date/Time of Trans')
     A            LHWHSE         3  0       TEXT('Whse')
     A                                      COLHDG('Whse')
     A            LHSTA          2          TEXT('Station ID')
     A                                      COLHDG('Station ID')
     A            LHPORT         5  0       TEXT('IP Port')
     A                                      COLHDG('TCPIP Port1')
     A            LHUSER        10          TEXT('MRC User')
     A                                      COLHDG('MRC User')
     A            LHLITM        15          TEXT('Last Scanned Item')
     A                                      COLHDG('Last Scanned Item')
     A            LHDISP        12          TEXT('SLOT PLACED')
     A                                      COLHDG('SLOT PLACED')
     A            LHLCNS        15          TEXT('License')
     A                                      COLHDG('License')
     A            LHCUST        11  0       TEXT('Last Scanned Cust#')
     A                                      COLHDG('Last Scanned Cust#')
     A            LHORDNBR       7  0       TEXT('Last CUST ORD#')
     A                                      COLHDG('Last Scanned Ord#')
     A            LHPRTN        10          TEXT('5250 Printer')
     A                                      COLHDG('Printer ID')
     A            LHQTY          5  0       TEXT('Qty on Label')
     A                                      COLHDG('Qty on Label')
     A            LHWEGHT        9  3       TEXT('Last Weight')
     A                                      COLHDG('Last Weight')
     A            LHTARE         9  3       TEXT('Last Tare')
     A                                      COLHDG('Last Tare')
     A            LHWUOM         2          TEXT('Last Weight UOM')
     A                                      COLHDG('Last Weight UOM')
     A            LHTYPE         1          TEXT('AUTOMATIC LABELS OR NOT')
     A                                      COLHDG('AUTO LABEL')
     A            LHDEST         1          TEXT('CUSTOMER OR PAR STOCK')
     A                                      COLHDG('TYPE STOCK')
     A            LHLBLC         5  0       TEXT('NUMBER LABELS PRINTED')
     A                                      COLHDG('LABEL CNT')
     A            LHLSTS        20          TEXT('LAST SERIAL NUMBER ASSIGNED')
     A                                      COLHDG('LAST SERIAL')
     A            LHGS1         50          TEXT('GS1 DATA')
     A                                      COLHDG('GS1 DATA')
     A            LHWO          13          TEXT('WORK ORDER NUMBER')
     A                                      COLHDG('WO NUMBER')
     A            LHUCI        100          TEXT('Box Breaker UCI')
     A                                      COLHDG('BB UCI')
     A            LHDSPBB       12          TEXT('Box Breaker Slot')
     A                                      COLHDG('BB Slot')
710 AA            LHBXBSQ        5  0       TEXT('BB Case Sequence')
710 AA                                      COLHDG('BB SEQ#')
     A            LHTRAN         7  0       TEXT('Transaction Number')
     A                                      COLHDG('Trans#')
     A            LHSTAT         1          TEXT('LABEL STATUS')
     A                                      COLHDG('LAB STAT')
     A*
     A          K LHCTS
     A          K LHSTA
