     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMADJ1 (LF)
     A*  Description ... Item adjustments by route.
     A*  Date .......... June 5, 2001
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*   06/05/01  DAS  4.15
     A*     - Created
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This logical file extracts a route id and label # out of
     A*   the adjustment memo field. Because of this, all fields need
     A*   to be defined in order for a program to use them.
     A*
     A*   The main purpose for this logical file is to print a list of
     A*   exception adjustments that may have been created when closing
     A*   the transactions for a route.
     A*
     A*----------------------------------------------------------------
     A          R IAREC                     PFILE(ITEMADJ)
     A*
     A            IAWHSE
     A            IAITEM
     A            IADATE
     A            IATIME
     A            IABY
     A            IAWHDP
     A            IADISP
     A            IACWTA
     A            IAQTY1
     A            IAQTY2
     A            IAQTY3
     A            IAUDTE
     A            IAUTIM
     A            IACODE
     A            IAMEMO
     A*
     A            IARTID             I      SST(IAMEMO 14 7)
     A*                      This includes the 'R:' identifier.
     A            IALBL#             I      SST(IAMEMO 22 9)
     A*                      This includes the 'L:' identifier.
     A*
     A          K IAWHSE
     A          K IARTID
     A          K IAITEM
     A          K IAWHDP
     A          K IADISP
