     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPITEM (PF)
     A*  Description ... FTP Item Update Import File
     A*  Date .......... May 19, 1999
     A*  Creator ....... RH/DAS
     A*
     A*  This file should be identical to IMPITEM except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFITEM.FILE')
     A*
     A*  Revisions
     A*
414 AA*    10/27/00  DAS  4.14
     A*      - Removed UNIQUE keyword.
     A*      - This does not change the record id. Therefore, no
     A*        programs need to be recompiled.
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Update type (IUUTYP)
     A*      'A'  -  Add
     A*      'C'  -  Change
     A*      'D'  -  Delete
     A*
     A*    Item type (IUTYPE)
     A*
     A*      ' '  -  Normal or Base
     A*
     A*      'A'  -  Alias item
     A*
     A*        This type is used to sell an item using a different
     A*        item number.  It is just a name, no inventory is
     A*        attached to an Alias item.  All product is picked from
     A*        its' corresponding base item.
     A*
     A*      'B'  -  Broken case
     A*
     A*        This type is used when the breakdown quantity of an item
     A*        is sold under a separate item number and is picked from
     A*        a different slot.  During replenishment, 1 full case
     A*        item is replenished to the broken case item.
     A*        The ITEMLNK file provides the connection.
     A*
     A*      'C'  -  Contract item
     A*
     A*        This type is used to sell an item using a different
     A*        item number.  A contract item has an inventory quantity
     A*        attached to it which is allocated from the Base item.
     A*        An order for the base item can not take product reserved
     A*        for the contract item.
     A*
     A*      'S'  -  Same Slot Breakdown item
     A*
     A*        This type is used when the breakdown quantity of an item
     A*        is sold under a separate item number from the full case
     A*        and is picked from a different slot.  The inventory is
     A*        tracked uisng the breakdown quantities for the base item
     A*        The ITEMLNK file provides the connection.
     A*
     A*      'K'  -  Kit  (Future release)
     A*
     A*        This type is used when an item is created from a number
     A*        of other items.  Bags of mixed fruit might be an example
     A*        of this type.
     A*
     A*      'R'  -  Repackaged  (Future release)
     A*
     A*        This type is used when an item is repackaged to create
     A*        breakdown quantities which are sold under a separate
     A*        item number.  During replenishment 1 full case item is
     A*        repackaged into broken case items. For example, a 50 lb
     A*        full case item of oranges might be repackaged into 5 lb
     A*        and 10 lb broken case items.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
414 DA**                                    UNIQUE
     A          R IUREC
     A*
     A            IUUTYP         1          TEXT('Update type')
     A                                      COLHDG('Update' 'Type')
     A            IUWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IUITEM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            IUTYPE         1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            IUDESC        80          TEXT('Description')
     A                                      COLHDG('Description')
     A            IUWHDP         5          TEXT('Warehouse Dept')
     A                                      COLHDG('Warehouse' 'Dept')
     A            IUSTYP         1          TEXT('Section Type')
     A                                      COLHDG('Section' 'Type')
     A            IUDESG         5          TEXT('Designation')
     A                                      COLHDG('Designation')
     A            IUACTV         2          TEXT('Activity')
     A                                      COLHDG('Activity')
     A            IUBLD          2          TEXT('Build Code')
     A                                      COLHDG('Build' 'Code')
     A            IUVEN#        10S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
     A            IUVIT#        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            IUPDSC        15          TEXT('Pack Description')
     A                                      COLHDG('Pack' 'Description')
     A            IUUM1          2          TEXT('Normal UM')
     A                                      COLHDG('Normal' 'UM')
     A            IUUM2          2          TEXT('Breakdown 1 UM')
     A                                      COLHDG('Break 1' 'UM')
     A            IUUMQ2         3S 0       TEXT('Breakdown 1 UM Qty')
     A                                      COLHDG('Breakdown 1' 'UM Qty')
     A            IUFLG1         1          TEXT('Allow Brk1 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk1 Sale')
     A            IUUM3          2          TEXT('Breakdown 2 UM')
     A                                      COLHDG('Break 2' 'UM')
     A            IUUMQ3         3S 0       TEXT('Breakdown 2 UM Qty')
     A                                      COLHDG('Breakdown 2' 'UM Qty')
     A            IUFLG2         1          TEXT('Allow Brk2 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk2 Sale')
     A*
     A            IUULEN         5S 2       TEXT('Unit Length')
     A                                      COLHDG('Unit' 'Length')
     A            IUUWDT         5S 2       TEXT('Unit Width')
     A                                      COLHDG('Unit' 'Width')
     A            IUUHGT         5S 2       TEXT('Unit Height')
     A                                      COLHDG('Unit' 'Height')
     A            IUCUBE         7S 3       TEXT('Cube/Unit')
     A                                      COLHDG('Cube/Unit')
     A            IUSWGT         7S 2       TEXT('Shipping Weight')
     A                                      COLHDG('Shipping' 'Weight')
     A            IUCWGT         1          TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            IUREPL         5S 0       TEXT('Replenish Level')
     A                                      COLHDG('Replenish' 'Level')
     A            IUBLNK         5S 0       TEXT('Blank')
     A            IUFLGD         1          TEXT('Date Sensitive (Y/N)')
     A                                      COLHDG('Date' 'Sensitive')
     A            IUFLGB         1          TEXT('Allow Bulk Pick (Y/N)')
     A                                      COLHDG('Allow' 'Bulk Pick')
     A            IUSLIF         3S 0       TEXT('Shelf Life Days')
     A                                      COLHDG('Shelf Life' 'Days')
     A            IUNDAY         3S 0       TEXT('Notice Days')
     A                                      COLHDG('Notice' 'Days')
     A            IUTIE          4S 0       TEXT('Mfr Tie')
     A                                      COLHDG('Mfr' 'Tie')
     A            IUHIGH         2S 0       TEXT('Mfr High')
     A                                      COLHDG('Mfr' 'High')
     A            IUMFLG         1          TEXT('Manufacturing date (Y,N)')
     A                                      COLHDG('Manufacture' 'date flag')
     A            IUDDAY         3S 0       TEXT('Distributor days')
     A                                      COLHDG('Distributor' 'Days')
     A            IUUDAY         3S 0       TEXT('End user days')
     A                                      COLHDG('End User' 'Days')
     A            IUMDAY         3S 0       TEXT('Mfg Shelf Life Days')
     A                                      COLHDG('Mfg Shelf' 'Life Days')
     A            IUMITM        15          TEXT('Master Item Number')
     A                                      COLHDG('Master' 'Item' 'Number')
     A            IUCITM        15          TEXT('Contract Base Item Number')
     A                                      COLHDG('Contact' 'Base' 'Item #')
     A*
     A          K IUWHSE
     A          K IUITEM
