     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PLTMISC  (PF)
     A*  Description ... Pallet misc file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
510 AA*    09/05/07  DAS  5.10
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Code (PMCODE)
     A*
     A*      HARBORLDSD  - Harbor's LD/SD indicator.
     A*
     A*    Value (PMVALUE)
     A*
     A*      This field will contain both character and numeric data.
     A*      For numeric data, program CVTNUM can be used to convert
     A*      a number to a string and visa versa.
     A*
     A*      If this field contains multiple values, then they should
     A*      be defined in QCOPYSRC/C#PLTMISC.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A*                                     UNIQUE
     A          R PMREC
     A*
     A            PMWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            PMRTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A            PMPLID    R               TEXT('Pallet Id')
     A                                      COLHDG('Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            PMCODE        10          TEXT('Code')
     A                                      COLHDG('Code')
     A            PMSEQ     R               REFFLD(SEQ#)
     A                                      COLHDG('Code' 'Seq#')
     A            PMVALUE       75A         TEXT('Value')
     A                                      COLHDG('Value')
     A*
     A* Timestamp / User tracking information.
     A*
     A            PMADDTS   R               REFFLD(ADDTS)
     A            PMADDUSR  R               REFFLD(ADDUSR)
     A            PMADDJOB  R               REFFLD(ADDJOB)
     A            PMADDNBR  R               REFFLD(ADDNBR)
     A*
     A            PMCHGTS   R               REFFLD(CHGTS)
     A            PMCHGUSR  R               REFFLD(CHGUSR)
     A            PMCHGJOB  R               REFFLD(CHGJOB)
     A            PMCHGNBR  R               REFFLD(CHGNBR)
     A*
     A          K PMWHSE
     A          K PMRTID
     A          K PMPLID
     A          K PMCODE
     A          K PMSEQ
