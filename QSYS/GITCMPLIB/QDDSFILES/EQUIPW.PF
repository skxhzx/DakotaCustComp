     A*----------------------------------------------------------------***
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... EQUIPW (PF)
     A*  Description ... Equipment work order
     A*  Creator ....... RH
     A*
     A*  Revisions
     A*
417 AA*    03/12/03  RH   4.17
     A*      - Created.
417 AA*    07/01/03  RH   4.17
     A*      - Change to sort in Decending order.
     A*
     A*----------------------------------------------------------------***
     A* Notes:
     A*
     A*   - An equip work order is created when an exception is
     A*     created during check out or in. One record per transaction.
     A*
     A*----------------------------------------------------------------***
     A* Field Notes:
     A*
     A*   EWPDTE    0  - Work order not yet printed.
     A*
     A*
     A*----------------------------------------------------------------***
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R EWREC
     A*
     A            EWWHSE    R               REFFLD(WHCODE PIRDICT)
     A            EWWRK#    R               TEXT('Work Order #')
     A                                      COLHDG('Work' 'Order#')
     A                                      REFFLD(TRAN# PIRDICT)
     A            EWGDTE          Z         TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A            EWETRN    R               TEXT('Equip Tran #')
     A                                      COLHDG('Equip' 'Tran#')
     A                                      REFFLD(TRAN# PIRDICT)
     A            EWIO           1          TEXT('In/Out')
     A                                      COLHDG('Check' 'In/out')
     A            EWPDTE    R               TEXT('Printed Date')
     A                                      COLHDG('Printed' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            EWEMP#         5P 0       TEXT('Completed by Employee')
     A                                      COLHDG('Comp. by' 'Emp. #')
     A            EWCDTE    R               TEXT('Completed Date')
     A                                      COLHDG('Completed' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A*
     A          K EWWHSE
     A          K EWWRK#                    DESCEND
