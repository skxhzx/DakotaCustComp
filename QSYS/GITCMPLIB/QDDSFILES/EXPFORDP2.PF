     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFORDP  (PF)
     A*  Description ... FTP Order detail piece export file
     A*  Date .......... June 19, 2007
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file should be identical to EXPORDP except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFCADEC.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
510 AA*    06/19/07  DAS  5.10
     A*      - Created.
     A*
510aAA*    09/13/07  DAS  5.10a
     A*      - Added field ORPCNT, ORPTTL and ORPSCN.
     A*
520 AA*    04/10/08  RBD  5.20
     A*      - Fix: Revised field definitions for ORPCNT, ORPTTL,
     A*        and ORPSCN from packed to signed numeric.  Replaced
     A*        for readability.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OEPREC
     A*
     A            OEPWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OEPORD        30A         TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEPSQ#         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OEPITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OEPUP#         5S 0       TEXT('Unique Piece #')
     A                                      COLHDG('Unique' 'Piece #')
     A            OEPLBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            OEPTYP         1A         TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            OEPDSP        12A         TEXT('Slot Display')
     A                                      COLHDG('Slot' 'Display')
     A            OEPPAL         7S 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            OEPTRN         7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            OEPUMT         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            OEPSEQ         3S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            OEPWGT         7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
520 MA            OEPCNT         5S 0       TEXT('Count')
510aAA                                      COLHDG('Count')
520 MA            OEPTTL         5S 0       TEXT('Total ORDP Recs')
510aAA                                      COLHDG('Total' 'ORDP Recs')
520 MA            OEPSCN         5S 0       TEXT('Scan Qty')
510aAA                                      COLHDG('Scan' 'Qty')
     A*
     A          K OEPWHS
     A          K OEPORD
     A          K OEPSQ#
     A          K OEPITM
     A          K OEPUP#
