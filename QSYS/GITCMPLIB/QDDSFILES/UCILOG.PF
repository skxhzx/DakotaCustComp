     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... UCILOG   (PF)
     A*  Description ... UCI log file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
720 AA*    07/15/16  DAS  7.20
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ULREC
     A*
     A            ULWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            ULUCI        100A         TEXT('UCI')
     A                                      COLHDG('UCI')
     A            ULACTION      10A         TEXT('Action')
     A                                      COLHDG('Action')
     A            ULUCILCNS     15A         TEXT('UCI License')
     A                                      COLHDG('UCI' 'License')
     A            ULLCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            ULSEQ          2S 0       TEXT('License Sequence')
     A                                      COLHDG('License' 'Seq')
     A            ULOLCNS       15A         TEXT('Original License')
     A                                      COLHDG('Original' 'License')
     A            ULOSEQ         2S 0       TEXT('Orig Lcns Seq')
     A                                      COLHDG('Orig Lcns' 'Seq')
     A            ULUTRN    R               TEXT('UCI Tran Number')
     A                                      COLHDG('UCI Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            ULUTRNSEQ      2S 0       TEXT('UCI Tran Seq #')
     A                                      COLHDG('UCI Tran' 'Seq #')
     A            ULTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            ULETRN#   R               TEXT('Employee Transaction Number')
     A                                      COLHDG('Employee' 'Transaction' -
     A                                      'Number')
     A                                      REFFLD(TRAN#)
     A            ULLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            ULSCAN         1A         TEXT('UCI Scanned')
     A                                      COLHDG('UCI' 'Scanned')
     A*
     A* "From" information
     A*
     A            ULUCICHG       1A         TEXT('UCI Changed')
     A                                      COLHDG('UCI' 'Changed')
     A            ULFUCI       100A         TEXT('From UCI')
     A                                      COLHDG('From UCI')
     A            ULITEMCHG      1A         TEXT('Item Changed')
     A                                      COLHDG('Item' 'Changed')
     A            ULFITEM   R               TEXT('From Item')
     A                                      COLHDG('From' 'Item')
     A                                      REFFLD(ITITEM)
     A            ULFITYPE       1A         TEXT('From Item Type')
     A                                      COLHDG('From' 'Item Type')
     A            ULFUTYP        1A         TEXT('From UOM Type (N,1,2)')
     A                                      COLHDG('From UOM Type' '(N,1,2)')
     A            ULFSITEM  R               TEXT('From Slot Item')
     A                                      COLHDG('From' 'Slot Item')
     A                                      REFFLD(ITITEM)
     A            ULFSITYPE      1A         TEXT('From Slot Item Type')
     A                                      COLHDG('From Slot' 'Item Type')
     A            ULFSUTYP       1A         TEXT('From Slot UOM Type (N,1,2)')
     A                                      COLHDG('From Slot' 'UOM Type (N12)')
     A            ULLCNSCHG      1A         TEXT('License Changed')
     A                                      COLHDG('License' 'Changed')
     A            ULFLCNS       15A         TEXT('From License Number')
     A                                      COLHDG('From License' 'Number')
     A            ULFSEQ         2S 0       TEXT('From License Sequence')
     A                                      COLHDG('From License' 'Seq')
     A            ULENTCHG       1A         TEXT('Entered Date Changed')
     A                                      COLHDG('Entered' 'Date Changed')
     A            ULFENTD   R               TEXT('From Date Entered')
     A                                      COLHDG('From Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            ULEXPCHG       1A         TEXT('Exipiration Date Changed')
     A                                      COLHDG('Expiration' 'Date Changed')
     A            ULFEXPD   R               TEXT('From Expiration Date')
     A                                      COLHDG('From Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A*
     A* Selection Customer information
     A*
     A            ULSELTRN#      7P 0       TEXT('Selection Tran#')
     A                                      COLHDG('Selection' 'Tran#')
     A            ULSELLBL#      7P 0       TEXT('Selection Lbl#')
     A                                      COLHDG('Selection' 'Lbl#')
     A            ULSELORD  R               REFFLD(OHORD)
     A            ULSELSEQ  R               REFFLD(SEQ#)
     A            ULSELCSEQ      3P 0       TEXT('ORDC Seq#')
     A                                      COLHDG('ORDC' 'Seq#')
     A            ULSELTRAX     10          TEXT('Trax#')
     A                                      COLHDG('Trax#')
     A*
     A* Return information
     A*
     A            ULRTNTRN#      7P 0       TEXT('Return Tran#')
     A                                      COLHDG('Return' 'Tran#')
     A*
     A* Exception information
     A*
     A            ULEXCCODE      8A         TEXT('Exception Code')
     A                                      COLHDG('Exception' 'Code')
     A            ULEXCSTAT      2A         TEXT('Status')
     A                                      COLHDG('Status')
     A            ULEXCUSER     10A         TEXT('Corrected by user')
     A                                      COLHDG('Corrected' 'by user')
     A            ULEXCTS         Z         TEXT('Corrected Date Stamp')
     A                                      COLHDG('Corrected' 'Date Stamp')
     A*
     A* Export information
     A*
     A            ULEXPFLAG      1A         TEXT('Export Flag')
     A                                      COLHDG('Export' 'Flag')
     A            ULEXPTS   R               REFFLD(ADDTS)
     A                                      COLHDG('Export Stamp')
     A                                      TEXT('Export Stamp')
     A            ULEXPUC   R               REFFLD(ADDUC)
     A                                      COLHDG('Export UC Stamp')
     A                                      TEXT('Export UC Stamp')
     A            ULEXPJUSR R               REFFLD(ADDUSR)
     A                                      TEXT('Export Job User')
     A                                      COLHDG('Export' 'Job User')
     A            ULEXPJOB  R               REFFLD(ADDJOB)
     A                                      TEXT('Export Job')
     A                                      COLHDG('Export' 'Job')
     A            ULEXPNBR  R               REFFLD(ADDNBR)
     A                                      TEXT('Export Job Nbr')
     A                                      COLHDG('Export' 'Job Nbr')
     A*
     A* Timestamp / User tracking information.
     A*
     A            ULADDTS   R               REFFLD(ADDTS)
     A            ULADDUC   R               REFFLD(ADDUC)
     A            ULADDCPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Calling Pgm')
     A                                      TEXT('Calling Pgm')
     A            ULADDJPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Job Pgm')
     A                                      TEXT('Job Pgm')
     A            ULADDEMP  R               REFFLD(ADDEMP)
     A            ULADDEUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Employee' 'User')
     A                                      TEXT('Employee User')
     A            ULADDCUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            ULADDJUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            ULADDJOB  R               REFFLD(ADDJOB)
     A            ULADDNBR  R               REFFLD(ADDNBR)
     A*
     A          K ULWHSE
     A          K ULUCI
     A          K ULADDUC
