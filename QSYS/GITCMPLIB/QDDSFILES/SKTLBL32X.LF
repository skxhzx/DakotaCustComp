     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SKTLBL32X (LF)
     A*  Description ... Special join file for printing CLD pick lbls
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
510 AA*    08/11/06 DAS
     A*      - Created
     A*
520 AA*    02/27/09  MLB  5.20
     A*      - Enh: LF recompiled. Field SLWDISP added.
     A*
530 AA*    02/04/10  MLB  5.30
     A*      - Enh: LF recompiled. Field PSTRN# added to correct
     A*        problem with duplicate labels when multi trans checked out.
     A*
700 AA*    03/10/16  MLB  7.00
     A*      - Enh: Revised pgm to add LBLVL2 before LBAISL. Needed
     A*        for Greenleaf mods.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file is identical to SKTLBL32 except that fields
     A*  SLWWHDP and SLWSTYP have been removed from the key.
     A*  Now the first key field after LBTRN# is LBAISL.
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R SLWREC32X                 JFILE(SKTLBL               +
     A                                            LABEL PLTSUM PIRITEM +
     A                                            ORDH ORDD UNMESR)
     A*
     A          J                           JOIN(SKTLBL  LABEL)
     A                                      JFLD(SLWLBL# LBLBL#)
     A*
     A          J                           JOIN(LABEL   PLTSUM)
     A                                      JFLD(LBWHSE  PSWHSE)
     A                                      JFLD(LBRTE   PSRTID)
     A                                      JFLD(LBPAL#  PSPLID)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
     A*  Fields from SKTLBL file.
     A*
     A            SLWEMP#
     A            SLWWHSE
     A            SLWWHDP
     A            SLWITEM
     A            SLWAISL
     A            SLWLOC
     A            SLWRLVL
     A            SLWHAND
520 AA            SLWDISP
     A            SLWPSEQ
     A            SLWSTYP
     A            SLWLVL2
     A            SLWRTE
     A            SLWSTOP
     A            SLWTYPE
     A            SLWLBL#
     A            SLWTRN#
     A            SLWSTAMP
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
214 AA            LBPO
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
214 AA            LBGRP1
214 AA            LBGRP2
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPAL#
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
530 AA            PSTRN#
     A*
     A*  Fields from PIRITEM file.
     A*
213 AA            ITTYPE
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
214 AA            ITFLG1
     A            ITUM3
     A            ITUMQ3
214 AA            ITFLG2
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
214 AA            ITREOR
214 AA            ITREPL
214 AA            ITRCVD
214 AA            ITFLGD
214 AA            ITFLGB
214 AA            ITSLIF
214 AA            ITNDAY
     A*
     A*  Fields from ORDH file.
     A*
214 AA            OHORIG
     A            OHTYPE
     A            OHCUST
214 AA            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
214 AA            OHPBAT
214 AA            OHSTAT
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Key fields
     A*
     A          K SLWEMP#
     A          K SLWTYPE
510 DA**        K SLWWHDP
510 DA**        K SLWSTYP
700 AA          K SLWLVL2
     A          K SLWAISL
     A          K SLWPSEQ
     A          K SLWRLVL
     A          K SLWHAND
213 AA          K SLWSTOP                   DESCEND
