     A*----------------------------------------------------------------
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... DFPICKDTL (PF)
     A*  Description ... DFPICK detail file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
730  A*    05/01/19  DAS
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R DFDREC
     A*
     A            DFDID          7  0       TEXT('Unique Id')
     A                                      COLHDG('Unique' 'Id')
     A            DFDWHSE   R               REFFLD(WHCODE)
     A            DFDCODE       10A         TEXT('Template Code')
     A                                      COLHDG('Template' 'Code')
     A            DFDDESC       50          TEXT('Description')
     A                                      COLHDG('Description')
     A            DFDLVL2        2  0       TEXT('Pick Order')
     A                                      COLHDG('Pick' 'Order')
     A            DFDCMPPRTY     3  0       TEXT('Compare Priority')
     A                                      COLHDG('Compare' 'Priority')
     A*
     A* Slot fields
     A*
     A            DFDWHDP   R               REFFLD(WDCODE)
     A            DFDSTYP   R               REFFLD(SLSTYP)
     A            DFDAISL1  R               REFFLD(ASCODE)
     A                                      TEXT('From aisle')
     A                                      COLHDG('From' 'aisle')
     A            DFDAISL2  R               REFFLD(ASCODE)
     A                                      TEXT('To aisle')
     A                                      COLHDG('To' 'aisle')
     A            DFDLOC1   R               REFFLD(SLLOC)
     A                                      TEXT('From location')
     A                                      COLHDG('From' 'Location')
     A            DFDLOC2   R               REFFLD(SLLOC)
     A                                      TEXT('To location')
     A                                      COLHDG('To' 'Location')
     A            DFDRLVL1  R               REFFLD(SLRLVL)
     A                                      TEXT('From level')
     A                                      COLHDG('From' 'Level')
     A            DFDRLVL2  R               REFFLD(SLRLVL)
     A                                      TEXT('To level')
     A                                      COLHDG('To' 'Level')
     A            DFDHND1   R               REFFLD(SLHAND SLOT)
     A            DFDHND2   R               REFFLD(SLHAND SLOT)
     A            DFDUTYP   R               REFFLD(LBUTYP LABEL)
     A            DFDSIDE        1          TEXT('Side (O,E)')
     A                                      COLHDG('Side' '(O,E)')
     A*
     A* Item fields
     A*
     A            DFDITEM1  R               REFFLD(ITITEM PIRITEM)
     A                                      TEXT('From Item')
     A                                      COLHDG('From' 'Item')
     A            DFDITEM2  R               REFFLD(ITITEM PIRITEM)
     A                                      TEXT('To Item')
     A                                      COLHDG('To' 'Item')
     A            DFDDESG   R               REFFLD(ITDESG PIRITEM)
     A            DFDCAT    R               REFFLD(ICCODE ITEMCAT)
     A            DFDCLCDE      10          TEXT('Class Code')
     A                                      COLHDG('Class' 'Code')
     A            DFDACTV   R               REFFLD(ITACTV PIRITEM)
     A            DFDBLD    R               REFFLD(ITBLD PIRITEM)
     A*
     A* Label fields
     A*
     A            DFDSZON   R               REFFLD(LWSZON WORKLBL)
     A            DFDUWGT1  R               REFFLD(LBUWGT LABEL)
     A                                      TEXT('From Item Wgt')
     A                                      COLHDG('From' 'Item Wgt')
     A            DFDUWGT2  R               REFFLD(LBUWGT LABEL)
     A                                      TEXT('To Item Wgt')
     A                                      COLHDG('To' 'Item Wgt')
     A            DFDSWGT1  R               REFFLD(LBSWGT LABEL)
     A                                      TEXT('From Label Wgt')
     A                                      COLHDG('From' 'Label Wgt')
     A            DFDSWGT2  R               REFFLD(LBSWGT LABEL)
     A                                      TEXT('From Label Wgt')
     A                                      COLHDG('From' 'Label Wgt')
     A            DFDLTYPE       1          TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A*
     A          K DFDID

