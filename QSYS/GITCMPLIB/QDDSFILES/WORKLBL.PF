730aAA*********************************
730AAA**** DO NOT USE IN PROGRAMS  ****
730AAA**** USE WORKLBLW INSTEAD    ****
730AAA*********************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... WORKLBL  (PF)
     A*  Description ... Label work file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    06/12/97  DAS  4.00
     A*      - Created. Based on Label file.
     A*
417 AA*    08/08/04  DAS  4.17
     A*      - OR620 is moving *SPLIT into LWPO when a child record is
     A*        split onto multiple truck pallets.
     A*      - Record format did not change.
     A*
730aAA*    10/04/19  DAS  7.30a
     A*      - Removed UNIQUE. Moved to LF WORKLBLW that has Whse.
     A*      - This file should no longer be used in programs. Instead
     A*        use WORKLBLW.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    See label file.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    See Label file
     A*
     A*----------------------------------------------------------------
730aDA**                                    UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R LWREC
     A*
     A*  General information.
     A*
     A            LWWHSE    R               REFFLD(WHCODE)
     A            LWTRUK    R               REFFLD(TMTRUK)
     A            LWCOMP    R               REFFLD(TPCODE)
     A            LWTZON    R               REFFLD(ZHZONE)
     A                                      TEXT('Truck Zone')
     A                                      COLHDG('Truck' 'Zone')
     A            LWPZON    R               REFFLD(ZHZONE)
     A                                      TEXT('Parent Zone')
     A                                      COLHDG('Parent' 'Zone')
     A            LWSZON    R               REFFLD(ZHZONE)
     A                                      TEXT('Slot Zone')
     A                                      COLHDG('Slot' 'Zone')
     A            LWGEN          1  0       TEXT('Generation')
     A                                      COLHDG('Generation')
     A            LWWHDP    R               REFFLD(WDCODE)
     A            LWSEQ     R               REFFLD(SEQ#)
     A            LWITEM    R               REFFLD(ITITEM)
     A            LWPBAT    R               REFFLD(BATCH#)
     A*
     A*  "From" slot.
     A*
     A            LWAISL    R               REFFLD(ASCODE)
     A            LWLOC     R               REFFLD(SLLOC )
     A            LWRLVL    R               REFFLD(SLRLVL)
     A            LWHAND    R               REFFLD(HSCODE)
     A            LWPSEQ         5P 0       TEXT('Picking Sequence')
     A                                      COLHDG('Picking' 'Sequence')
     A            LWSTYP    R               REFFLD(SLSTYP)
     A            LWQRYF         1P 0       TEXT('Query Factor')
     A                                      COLHDG('Query' 'Factor')
     A            LWDISP        12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
213 AA            LWQAVL         5P 0       TEXT('Quantity Available')
213 AA                                      COLHDG('Quantity' 'Available')
     A*
     A*  "To" slot (for transfer).
     A*
     A            LWWHS2    R               TEXT('Transfer Warehouse')
     A                                      COLHDG('Transfer' 'Warehouse')
     A                                      REFFLD(WHCODE)
     A            LWWHD2    R               TEXT('Transfer Department')
     A                                      COLHDG('Transfer' 'Department')
     A                                      REFFLD(WDCODE)
     A            LWASL2    R               TEXT('Transfer Aisle')
     A                                      COLHDG('Transfer' 'Aisle')
     A                                      REFFLD(ASCODE)
     A            LWLOC2    R               TEXT('Transfer Location')
     A                                      COLHDG('Transfer' 'Location')
     A                                      REFFLD(SLLOC )
     A            LWLVL2    R               TEXT('Transfer Level')
     A                                      COLHDG('Transfer' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LWHND2    R               TEXT('Transfer Hand')
     A                                      COLHDG('Transfer' 'Hand')
     A                                      REFFLD(HSCODE)
     A            LWDSP2        12A         TEXT('Transfer Display')
     A                                      COLHDG('Transfer' 'Display')
     A*
     A*  Order information.
     A*
     A            LWRTE     R               REFFLD(ROUTE)
     A            LWSTOP    R               REFFLD(STOP)
     A            LWORD     R               REFFLD(OHORD)
     A            LWSPCL    R               TEXT('Special Status Code')
     A                                      COLHDG('Special' 'Status Code')
     A                                      REFFLD(SSCODE)
     A*
     A*  Put away information.
     A*
     A            LWPO      R               REFFLD(PONUM)
     A*
     A*  Item information.
     A*
     A            LWUTYP         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            LWUCOD    R               TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A                                      REFFLD(UMCODE)
     A            LWUCUB         7P 3       TEXT('Cube/Unit')
     A                                      COLHDG('Cube/Unit')
     A            LWCUBE         9P 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            LWUWGT         7P 2       TEXT('Shipping Weight')
     A                                      COLHDG('Shipping' 'Weight')
     A            LWSWGT         9P 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            LWQALC         5P 0       TEXT('Quantity Allocated')
     A                                      COLHDG('Quantity' 'Allocated')
     A            LWQRMN         5P 0       TEXT('Quantity Remaining')
     A                                      COLHDG('Quantity' 'Remaining')
     A            LWQPCK         5P 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            LWTIE          4S 0       TEXT('Tie')
     A                                      COLHDG('Tie')
     A            LWHIGH         2S 0       TEXT('High')
     A                                      COLHDG('High')
     A*
     A*  Label information.
     A*
213 AA            LWGRP1         1A         TEXT('Group 1')
213 AA                                      COLHDG('Group 1')
213 AA            LWGRP2         1A         TEXT('Group 2')
213 AA                                      COLHDG('Group 2')
     A            LWTYPE         1A         TEXT('Type')
     A                                      COLHDG('Type')
     A            LWLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LWPAL#    R               TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            LWPALI    R               TEXT('Internal Pallet Number')
     A                                      COLHDG('Internal' 'Pallet Number')
     A                                      REFFLD(TRAN#)
     A            LWTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            LWCTR#    R               TEXT('Corresponding Tran Number')
     A                                      COLHDG('Corresp. Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            LWSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            LWPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            LWSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            LWSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
213 AA            LWRDTE    R               TEXT('Report Date')
213 AA                                      COLHDG('Report' 'Date')
213 AA                                      REFFLD(DTECMD)
     A            LWVRFY         1A         TEXT('Verify the slot')
     A                                      COLHDG('Verify' 'the slot')
     A*
     A          K LWLBL#
