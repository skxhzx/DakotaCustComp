     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL5 (LF)
     A*  Description ... Label file for route check.
     A*  Date .......... October 06, 1994
     A*  Creator ....... DAS
     A*
     A*  Revision:
     A*    10/21/94 HNK
     A*      - Sequence number field L5SEQ added.
     A*
     A*    Rev 2.01  11/08/94  DAS
     A*      - Changed key to include P.O. number.
     A*
     A*    02/29/96  DAS  2.02
     A*      - Revised to work with new PIRTRAN file for EPM.
     A*        Change P5TYPE to P5TASK.
     A*
213 AA*    05/30/96  DAS  2.03
     A*      - Added field L5GRP2 to file and key.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*    This logical file was created in order to see if all the
     A*    transactions for a route have been completed. This file
     A*    OMITS records with a status of 'C'. Therefore, if a
     A*    record exists in this file for a particular route then the
     A*    route is not closed.
     A*
     A*    This layout uses a unique record name and field names so
     A*    it can be used in a program that uses the label file.
     A*
     A*    *** If fields are removed from the physical file LABEL they
     A*    must also be removed from this file. If a new field is added
     A*    to the file and it is needed by a program using this file,
     A*    you must also add the field to this file.
     A*
     A*    In order to use the JDFTVAL keyword the DYNSLT keywork
     A*    would also have to be used. Because DYNSLT might slow down
     A*    I/O we have decided to leave the JDFTVAL keyword out. This
     A*    means that unclosed labels without a corresponding
     A*    transaction record would be ignored.
     A*
     A*----------------------------------------------------------------
     A          R L5REC                     JFILE(LABEL PIRTRAN)
     A*
     A          J                           JOIN(LABEL   PIRTRAN)
     A                                      JFLD(LBTRN#  PTTRN#)
     A*
     A*  Fields from LABEL file.
     A*
     A            L5WHSE                    RENAME(LBWHSE)
     A            L5WHDP                    RENAME(LBWHDP)
     A            L5SEQ                     RENAME(LBSEQ)
     A            L5ITEM                    RENAME(LBITEM)
     A            L5PBAT                    RENAME(LBPBAT)
     A            L5AISL                    RENAME(LBAISL)
     A            L5LOC                     RENAME(LBLOC)
     A            L5RLVL                    RENAME(LBRLVL)
     A            L5HAND                    RENAME(LBHAND)
     A            L5PSEQ                    RENAME(LBPSEQ)
     A            L5STYP                    RENAME(LBSTYP)
     A            L5RTE                     RENAME(LBRTE)
     A            L5STOP                    RENAME(LBSTOP)
     A            L5ORD                     RENAME(LBORD)
     A            L5PO                      RENAME(LBPO)
     A            L5UTYP                    RENAME(LBUTYP)
     A            L5UCOD                    RENAME(LBUCOD)
     A            L5UCUB                    RENAME(LBUCUB)
     A            L5CUBE                    RENAME(LBCUBE)
     A            L5UWGT                    RENAME(LBUWGT)
     A            L5SWGT                    RENAME(LBSWGT)
     A            L5QALC                    RENAME(LBQALC)
     A            L5QRMN                    RENAME(LBQRMN)
     A            L5QPCK                    RENAME(LBQPCK)
213 AA            L5GRP2                    RENAME(LBGRP2)
     A            L5TYPE                    RENAME(LBTYPE)
     A            L5LBL#                    RENAME(LBLBL#)
     A            L5PAL#                    RENAME(LBPAL#)
     A            L5TRN#                    RENAME(LBTRN#)
     A            L5CTR#                    RENAME(LBCTR#)
     A            L5STAT                    RENAME(LBSTAT)
     A            L5PRTY                    RENAME(LBPRTY)
     A            L5SDTE                    RENAME(LBSDTE)
     A            L5STIM                    RENAME(LBSTIM)
     A*
     A*  Fields from PIRTRAN file.
     A*
101eDA*           P5TYPE                    RENAME(PTTYPE)
101eMA            P5TASK                    RENAME(PTTASK)
     A            P5CUBE                    RENAME(PTCUBE)
     A            P5SWGT                    RENAME(PTSWGT)
     A            P5PCS                     RENAME(PTPCS)
     A            P5SDTE                    RENAME(PTSDTE)
     A            P5STIM                    RENAME(PTSTIM)
     A            P5EDTE                    RENAME(PTEDTE)
     A            P5ETIM                    RENAME(PTETIM)
101eDA*           P5RTE                     RENAME(PTRTE)
101eDA*           P5PO                      RENAME(PTPO)
101eMA            P5RTPO                    RENAME(PTRTPO)
     A*
     A*  Key fields
     A*
     A          K L5WHSE
213 AA          K L5GRP2
     A          K L5RTE
     A          K L5PO
     A          K L5PBAT
     A          K L5TRN#
     A          O L5STAT                    COMP(EQ 'C')
