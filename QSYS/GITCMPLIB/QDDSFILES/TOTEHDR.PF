     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TOTEHDR (PF)
     A*  Description ... Tote header file
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    02/09/08  DAS  5.20  P#00011
     A*      - Created.
520aAA*    10/06/08  RBD  5.20a P#00011
     A*      - Fix:  Change field TOHNUM to TOHCNT, as TOHNUM is
     A*        already used in file TORDH.  Change field size to
     A*        3,0.  Change field size of TOHPCS to 4,0.
     A*
CWD AA*    04/28/15  MLB  CWD   P#01349
     A*      - Enh: Revised file key to add Order ID to make key unique.
     A*        Adding missing order id records for tote was causing
     A*        duplicate key errors.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R TOHREC
     A*
     A            TOHWHS    R               REFFLD(WHCODE)
     A            TOHRID    R               TEXT('Route Id')
     A                                      COLHDG('Route Id')
     A                                      REFFLD(ROUTE)
     A            TOHPPI    R               TEXT('Physical Pallet Id')
     A                                      COLHDG('Physical' 'Pallet Id')
     A                                      REFFLD(TRAN#)
520aDA**          TOHNUM         2S 0       TEXT('Tote Number')
520aMA            TOHCNT         3S 0       TEXT('Tote Number')
     A                                      COLHDG('Tote' 'Number')
     A            TOHORD    R               REFFLD(OHORD)
     A*
     A*  Position on pallet
     A*
     A            TOHLAY         2S 0       TEXT('Layer')
     A                                      COLHDG('Layer')
     A            TOHSEC         1          TEXT('Section')
     A                                      COLHDG('Section')
     A*
     A*  Physical properties
     A*
     A            TOHECB         5P 2       TEXT('External Tote Cube')
     A                                      COLHDG('External' 'Tote Cube')
     A            TOHICB         5P 2       TEXT('Internal Tote Cube')
     A                                      COLHDG('Internal' 'Tote Cube')
     A            TOHMXP         3P 0       TEXT('Maximum Tote Pieces')
     A                                      COLHDG('Maximum' 'Tote Pieces')
     A*
     A*  Content totals
     A*
     A            TOHCUB         5P 2       TEXT('Cube')
     A                                      COLHDG('Cube')
520aDA**          TOHPCS         3P 0       TEXT('Pieces')
520aMA            TOHPCS         4P 0       TEXT('Pieces')
     A                                      COLHDG('Pieces')
     A            TOHWGT         5P 2       TEXT('Weight')
     A                                      COLHDG('Weight')
     A*
     A*  Physical pick information (pallet id is above)
     A*
     A            TOHLBL    R               TEXT('Physical Label Number')
     A                                      COLHDG('Physical Lbl Number')
     A                                      REFFLD(TRAN#)
     A            TOHTR#    R               TEXT('Physical Tran Number')
     A                                      COLHDG('Physcal' 'Tran Number')
     A                                      REFFLD(TRAN#)
     A*
     A*  Merge pick information
     A*
     A            TOHMPI    R               TEXT('Merge Pallet Id')
     A                                      COLHDG('Merge' 'Pallet Id')
     A                                      REFFLD(TRAN#)
     A            TOHML#    R               TEXT('Merge Label Number')
     A                                      COLHDG('Merge Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            TOHMT#    R               TEXT('Merge Tran Number')
     A                                      COLHDG('Merge Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A*
     A          K TOHWHS
     A          K TOHRID
     A          K TOHPPI
520aDA**        K TOHNUM
520aMA          K TOHCNT
700 AA          K TOHORD
