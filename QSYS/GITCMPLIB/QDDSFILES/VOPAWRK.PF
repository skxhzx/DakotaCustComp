     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... VOPAWRK  (PF)
     A*  Description ... Voice putaway work file
     A*  Date .......... April 25, 2003
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
417 AA*    04/25/03  DAS
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Number type codes (WKPTYP)
     A*
     A*       'P'  -  License plate scanned.
     A*       'L'  -  Label scanned.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R WKPREC
     A*
     A            WKPEMP         5S 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            WKPSKP         3S 0       TEXT('Skip Count')
     A                                      COLHDG('Skip' 'Count')
     A            WKPUSR    R               REFFLD(USCODE *LIBL/PIRUSER)
     A            WKPNBR        15A         TEXT('Scanned Number')
     A                                      COLHDG('Scanned' 'Nbr')
     A            WKPTYP         1A         TEXT('Number Type')
     A                                      COLHDG('Number' 'Type')
     A            WKPTRN    R     P         REFFLD(TRAN#)
     A            WKPLB#    R               REFFLD(TRAN#)
     A                                      TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            WKPSTS         2A         TEXT('Status Code')
     A                                      COLHDG('Status' 'Code')
     A            WKPWHS    R               REFFLD(WHCODE)
     A            WKPWHD    R               REFFLD(WDCODE)
     A                                      TEXT('Warehouse Dept')
     A                                      COLHDG('Warehouse' 'Dept')
     A            WKPASL    R               REFFLD(ASCODE)
     A                                      TEXT('Aisle')
     A                                      COLHDG('Aisle')
     A            WKPLOC    R               REFFLD(SLLOC)
     A                                      TEXT('Slot Location')
     A                                      COLHDG('Slot' 'Location')
     A            WKPLVL    R               REFFLD(SLRLVL)
     A                                      TEXT('Slot Level')
     A                                      COLHDG('Slot' 'Level')
     A            WKPHND    R               REFFLD(HSCODE)
     A                                      TEXT('Handstack')
     A                                      COLHDG('Handstack')
     A            WKPDSP    R               REFFLD(SLDISP *LIBL/SLOT)
     A                                      TEXT('Slot Display')
     A                                      COLHDG('Slot' 'Display')
     A            WKPCHK         2S 0       TEXT('Check Digits')
     A                                      COLHDG('Check' 'Digits')
     A*
     A          K WKPEMP
     A          K WKPNBR
     A          K WKPTYP
