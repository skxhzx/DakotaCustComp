     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... UCIINFO  (PF)
     A*  Description ... UCI Information file
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    09/06/12  DAS  6.40
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*  UIUCILCNS - Internal UCI License
     A*
     A*    This is an internal license that can be used to record
     A*    UCI actions in the LICLOG file.
     A*
     A*    The license should start with 'U:' followed by a
     A*    13-digit number.
     A*
     A*----------------------------------------------------------------
     A*  UICRTBY - Record created by
     A*
     A*   1 = Receiving
     A*   2 = Returns
     A*   3 = Selection
     A*   4 = Internal GS1-99 creation
     A*   5 = Drilicense - Production Module
     A*   6 = CW122 - Catch weight entry mutiples
     A*   7 = PO232 - Catch weight entry single
     A*   8 = RU310 - Runner Selection
     A*   9 = LT107 - Production Replenishment
     A*   A = M17300 - Production Box Breaker
     A*   B = UCI tracking Inventory adjustment
     A*   C = UPC Selection
     A*   D = Canopy Production Import
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R UIREC
     A*
     A            UIWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            UIITEM    R               REFFLD(ITITEM)
     A            UIUCI        100A         TEXT('UCI')
     A                                      COLHDG('UCI')
     A            UIUCILCNS     15A         TEXT('UCI License')
     A                                      COLHDG('UCI' 'License')
     A            UILCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            UISEQ          2S 0       TEXT('License Sequence')
     A                                      COLHDG('License' 'Seq')
     A            UISERIAL      30          TEXT('Serial No')
     A                                      COLHDG('Serial No')
     A            UIWGTLBS       9P 2       TEXT('Weight Lbs')
     A                                      COLHDG('Weight' 'Lbs')
     A            UIWGTKGS       9P 2       TEXT('Weight Kgs')
     A                                      COLHDG('Weight' 'Kgs')
     A            UISELTRN#      7P 0       TEXT('Selection Tran#')
     A                                      COLHDG('Selection' 'Tran#')
     A            UISELLBL#      7P 0       TEXT('Selection Lbl#')
     A                                      COLHDG('Selection' 'Lbl#')
     A            UISELORD  R               REFFLD(OHORD)
     A            UISELSEQ  R               REFFLD(SEQ#)
     A            UISELCSEQ      3P 0       TEXT('ORDC Seq#')
     A                                      COLHDG('ORDC' 'Seq#')
     A            UISELTRAX     10          TEXT('Trax#')
     A                                      COLHDG('Trax#')
     A            UICRTBY        1          TEXT('Created By')
     A                                      COLHDG('Created' 'By')
     A            UIRTNTRN#      7P 0       TEXT('Return Tran#')
     A                                      COLHDG('Return' 'Tran#')
     A*
     A* Timestamp / User tracking information.
     A*
     A            UIADDTS   R               REFFLD(ADDTS)
     A            UIADDUC   R               REFFLD(ADDUC)
     A            UIADDPGM  R               REFFLD(ADDPGM)
     A            UIADDEMP  R               REFFLD(ADDEMP)
     A            UIADDUSR  R               REFFLD(ADDUSR)
     A            UIADDJOB  R               REFFLD(ADDJOB)
     A            UIADDNBR  R               REFFLD(ADDNBR)
     A*
     A            UICHGTS   R               REFFLD(CHGTS)
     A            UICHGUC   R               REFFLD(CHGUC)
     A            UICHGPGM  R               REFFLD(CHGPGM)
     A            UICHGEMP  R               REFFLD(CHGEMP)
     A            UICHGUSR  R               REFFLD(CHGUSR)
     A            UICHGJOB  R               REFFLD(CHGJOB)
     A            UICHGNBR  R               REFFLD(CHGNBR)
     A*
     A*
     A          K UIWHSE
     A          K UIUCI
