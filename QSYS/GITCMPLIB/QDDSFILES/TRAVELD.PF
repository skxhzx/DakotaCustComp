     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TRAVELD  (PF)
     A*  Description ... Travel Detail File
     A*  Date .......... December 18, 2008
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
520 AA*    12/18/08  DAS  5.20
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    This file identifies the detail on how to get from the
     A*    From Place to the To Place. It does this by recording the
     A*    TRAVELH id's for each step.
     A*
     A*    For example, the steps to get from slot DA-101-1 to
     A*    DB-110-1, may be as follows:
     A*
     A*      Slot DA-101-1 to Back of aisle DA
     A*      Back of aisle DA to Back of aisle DB
     A*      Back of aisle Db to Slot DB-110-1
     A*
     A*    Each of the above steps relates directly to a TRAVELH
     A*    record. Therefore, the detail for the TRAVELH record going
     A*    from DA-101-1 to DB-101-1 would consist of 3 records with
     A*    each pointing to a TRAVELH id for one of the steps above.
     A*
     A*    Note, if the TRAVELH record has a calculation type of 'H'
     A*    or 'S', it would only have a single detail record that
     A*    points back to itself. The reason for this is so we can
     A*    always use the detail file to list out the travel steps
     A*    without needing extra logic to handle 'H' and 'S' records.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R TRDREC
     A*
     A            TRDTYPE        1          TEXT('Travel Type')
     A                                      COLHDG('Travel' 'Type')
     A            TRDID          9  0       TEXT('Header Id')
     A                                      COLHDG('Header Id')
     A            TRDSEQ         3  0       TEXT('Seq#')
     A                                      COLHDG('Seq#')
     A            TRDID2         9  0       TEXT('Step Header Id')
     A                                      COLHDG('Step' 'Header Id')
     A*
     A* Timestamp / User tracking information.
     A*
     A            TRDCHGTS  R               REFFLD(CHGTS)
     A            TRDCHGUC  R               REFFLD(CHGUC)
     A            TRDCHGPGM R               REFFLD(CHGPGM)
     A            TRDCHGCUSRR               REFFLD(CHGUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            TRDCHGJUSRR               REFFLD(CHGUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            TRDCHGJOB R               REFFLD(CHGJOB)
     A            TRDCHGNBR R               REFFLD(CHGNBR)
     A*
     A          K TRDTYPE
     A          K TRDID
     A          K TRDSEQ
