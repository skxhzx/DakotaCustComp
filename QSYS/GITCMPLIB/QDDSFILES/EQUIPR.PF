     A*----------------------------------------------------------------***
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... EQUIPR    (PF)
     A*  Description ... Equipment work order repair
     A*  Creator ....... RH
     A*
     A*  Revisions
     A*
417 AA*    07/02/03  RH   4.17
     A*      - Created.
     A*
     A*----------------------------------------------------------------***
     A* Notes:
     A*
     A*
     A*----------------------------------------------------------------***
     A* Field Notes:
     A*
     A*   ERSTAT   '1' - Open
     A*            '4' - Closed.
     A*
     A*
     A*----------------------------------------------------------------***
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R ERREC
     A*
     A            ERWHSE    R               REFFLD(WHCODE PIRDICT)
     A            ERTRN#    R               TEXT('Equip Tran #')
     A                                      COLHDG('Equip' 'Tran #')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ERIO           1          TEXT('In/out')
     A                                      COLHDG('Check' 'In/out')
     A            ERSEQ#    R               TEXT('Question Seq#')
     A                                      COLHDG('Question' 'Seq#')
     A                                      REFFLD(SEQ# PIRDICT)
     A            EREMP#         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            ERWRK#    R               TEXT('Work Order #')
     A                                      COLHDG('Work' 'Order#')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ERMEMO        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            ERCMPL         1          TEXT('Complete')
     A                                      COLHDG('Complete')
     A            ERSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            ERSDTE          Z         TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A          K ERWHSE
     A          K ERTRN#
     A          K ERIO
     A          K ERSEQ#
     A          K EREMP#
     A          K ERSDTE
