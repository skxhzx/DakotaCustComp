     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... OPTIONHST (PF)
     A*  Description ... Options history file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    02/20/16  DAS  7.00
     A*      - Created
     A*
700a A*    03/09/16  DAS  7.00a
     A*      - Added OPHADDJUSR
     A*      - Renamed OPHADDJOB to OPHADDJJOB
     A*      - Renamed OPHADDNBR to OPHADDJNBR
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*    This history file is to be used with the following files:
     A*
     A*      File       OPHFILE
     A*      -------    -------
     A*      OPTIONC       C
     A*      OPTIOND       D
     A*      OPTIONE       E
     A*      OPTIONS       S
     A*      OPTIONZ       Z
     A*
     A*    Any fields that are not part of the option record being
     A*    saved should be left blank or zero.
     A*
     A*    A record should only be written when a change is made.
     A*
     A*    OPHADDUSR will contain the user we believe made the change.
     A*    For a QZDASOINIT job, we will try and track down the actual
     A*    user.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OPHREC
     A*
     A            OPHCODE        8          TEXT('Option Code')
     A                                      COLHDG('Option' 'Code')
     A            OPHFILE        1          TEXT('File')
     A                                      COLHDG('File')
     A            OPHWHSE   R               REFFLD(WHCODE)
     A            OPHWHDP   R               REFFLD(WDCODE)
     A            OPHCUST   R               REFFLD(CUSNUM)
     A            OPHTRUK   R               REFFLD(TMTRUK)
     A            OPHZONE   R               REFFLD(ZHZONE)
     A            OPHBFRDTA    117          TEXT('Before Data')
     A                                      COLHDG('Before' 'Data')
     A            OPHAFTDTA    117          TEXT('After Data')
     A                                      COLHDG('After' 'Data')
     A*
     A            OPHADDTS  R               REFFLD(ADDTS PIRDICT)
     A            OPHADDUC  R               REFFLD(ADDUC PIRDICT)
     A            OPHADDUSR R               REFFLD(ADDUSR PIRDICT)
700aAA            OPHADDJUSRR               REFFLD(ADDUSR PIRDICT)
700aMA            OPHADDJJOBR               REFFLD(ADDJOB PIRDICT)
700aMA            OPHADDJNBRR               REFFLD(ADDNBR PIRDICT)
     A*
     A          K OPHCODE
     A          K OPHFILE
     A          K OPHWHSE
     A          K OPHWHDP
     A          K OPHCUST
     A          K OPHTRUK
     A          K OPHZONE
     A          K OPHADDTS
