     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTRTN (PF)
     A*  Description ... Tracker returns file
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
530 AA*    01/28/10  RBD  5.30
     A*      - Created.
     A*
640 AA*    09/20/11  RBD  6.40
     A*      - Fix: Changed name of TTRTE field to TTROUT, was causing
     A*        conflict when used in pgm where file TTRTE is also used.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*  The following fields may or may not be returned by Tracker:
     A*
     A*     TTORD
     A*
     A*     TTUCI
     A*
     A*     TTMEMO
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes (TTSTAT)
     A*
     A*       '1'  -  Open
     A*       '5'  -  Closed
     A*       '9'  -  Deleted
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R TTREC
     A*
     A            TTWHSE    R               REFFLD(WHCODE)
     A            TTRTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
640 DA**          TTRTE     R               REFFLD(ROUTE)
640 MA            TTROUT    R               REFFLD(ROUTE)
     A            TTUSR         10          TEXT('Driver ID')
     A                                      COLHDG('Driver' 'ID')
     A            TTSTOP    R               REFFLD(STOP)
     A            TTORD     R               REFFLD(OHORD)
     A            TTITEM    R               REFFLD(ITITEM)
     A            TTBFLG         1          TEXT('BreakDown Flag')
     A                                      COLHDG('BrkDwn' 'Flag')
     A            TTOQTY         5P 0       TEXT('Orig Qty Returned')
     A                                      COLHDG('Orig Qty' 'Returned')
     A            TTUOM     R               TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A                                      REFFLD(UMCODE)
     A            TTPQTY         5P 0       TEXT('Qty Processed')
     A                                      COLHDG('Qty' 'Processed')
     A            TTCWGT         9P 2       TEXT('Catchweight')
     A                                      COLHDG('Catchweight')
     A            TTOLBL    R               TEXT('Original Label')
     A                                      COLHDG('Original' 'Label#' )
     A                                      REFFLD(TRAN#)
     A            TTUCI          5P 0       TEXT('Unique Piece #')
     A                                      COLHDG('Unique' 'Piece #')
     A            TTRCOD    R               REFFLD(RNCODE)
     A            TTMEMO        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            TTUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            TTUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TTSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            TTSTSD    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            TTSTST    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TTCNAM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer Name')
     A*
     A          K TTWHSE
     A          K TTSTAT
640 DA**        K TTRTE
640 MA          K TTROUT
     A          K TTRTID
     A          K TTOLBL
     A          K TTITEM
     A          K TTUCI
