     A*----------------------------------------------------------------
     A*   Copyright (C) 2021 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSDVOPH  (PF)
     A*  Description ... DockVue Export - Open PO Header v7.50
     A*
     A*  Revisions
     A*
750  A*    02/21/21  RTR  7.50
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A          R DPHREC
     A*
     A            DPHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            DPHBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            DPHPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            DPHSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            DPHSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            DPHSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            DPHTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            DPHVEN        11S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
     A            DPHVNM        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            DPHAD1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            DPHAD2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            DPHCTY        20          TEXT('City')
     A                                      COLHDG('City')
     A            DPHST          2          TEXT('State')
     A                                      COLHDG('State')
     A            DPHZP1        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            DPHSTS         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            DPHDATE        6S 0       TEXT('Deliv Dt')
     A                                      COLHDG('Deliv Dt')
     A            DPHNOTE      200A         TEXT('Note')
     A                                      COLHDG('Note')
     A*
     A*  Add stamps
     A*
     A            DPHADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            DPHADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K DPHWHS
     A          K DPHBATID
     A          K DPHPO
