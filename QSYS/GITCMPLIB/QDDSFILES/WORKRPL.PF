     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... WORKRPL (PF)
     A*  Description ... Workfile for Pushback Priority Replenishments.
     A*  Date .......... August 6, 2003
     A*  Creator ....... MLB
     A*
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    12/08/03  MLB  4.17
     A*      - Created.
     A*
600aAA*    01/29/09  DAS  6.00a  P#00045
     A*      - Added fields for license tracking.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Number type codes (WRTYPE)
     A*
     A*       'P'  -  License plate scanned.
     A*       'L'  -  Label scanned.
     A*
     A*    Replenishment status  (WRSTAT)
     A*
     A*       '1'  -  Replenishment pulled.
     A*       '2'  -  Replenishment complete - Putaway balance
     A*       '3'  -  Replenishment complete - Zero bal. remaining.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R WRREC
     A*
     A            WRUSER    R               REFFLD(USCODE *LIBL/PIRUSER)
     A            WRNBR         15A         TEXT('Scanned Number')
     A                                      COLHDG('Scanned' 'Nbr')
     A            WRTYPE         1A         TEXT('Number Type')
     A                                      COLHDG('Number' 'Type')
     A            WRTRN#    R     P         REFFLD(TRAN#)
     A            WRLBL#    R     P         TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            WRSTAT         2A         TEXT('Status Code')
     A                                      COLHDG('Status' 'Code')
     A            WRWHSE    R               REFFLD(WHCODE)
     A*
     A            WRFWHD    R               REFFLD(WDCODE)
     A                                      TEXT('From Dept')
     A                                      COLHDG('From' 'Dept')
     A            WRFASL    R               REFFLD(ASCODE)
     A                                      TEXT('From Aisle')
     A                                      COLHDG('From' 'Aisle')
     A            WRFLOC    R               REFFLD(SLLOC)
     A                                      TEXT('From Slot Location')
     A                                      COLHDG('From Slot' 'Location')
     A            WRFLVL    R               REFFLD(SLRLVL)
     A                                      TEXT('From Slot Level')
     A                                      COLHDG('From Slot' 'Level')
     A            WRFHND    R               REFFLD(HSCODE)
     A                                      TEXT('From Handstack')
     A                                      COLHDG('From' 'Handstack')
     A            WRFSEQ         5P 0       TEXT('From Pick Seq')
     A                                      COLHDG('From Pick' 'Seq')
     A            WRFSID         1S 0       TEXT('From Slot Side')
     A                                      COLHDG('From Slot' 'Side')
     A            WRFPOS         3P 0       TEXT('From Pallet Position')
     A                                      COLHDG('From Pallet' 'Position')
     A            WRFDSP    R               REFFLD(SLDISP *LIBL/SLOT)
     A                                      TEXT('From Slot Display')
     A                                      COLHDG('From Slot' 'Display')
600aAA            WRFITEM   R               REFFLD(SLITEM *LIBL/SLOT)
600aAA                                      TEXT('From Item')
600aAA                                      COLHDG('From Item')
600aAA            WRFCOD         1A         TEXT('From Code')
600aAA                                      COLHDG('From' 'Code')
600aAA            WRFARA        15A         TEXT('From Area')
600aAA                                      COLHDG('From Area')
600aAA            WRFLCNS       15A         TEXT('From License')
600aAA                                      COLHDG('From License')
600aAA            WRFOLCNS      15A         TEXT('From Orig Lcns')
600aAA                                      COLHDG('From Orig Lcns')
     A*
     A            WRTASL    R               REFFLD(ASCODE)
     A                                      TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A            WRTLOC    R               REFFLD(SLLOC)
     A                                      TEXT('To Slot Location')
     A                                      COLHDG('To Slot' 'Location')
     A            WRTLVL    R               REFFLD(SLRLVL)
     A                                      TEXT('To Slot Level')
     A                                      COLHDG('To Slot' 'Level')
     A            WRTHND    R               REFFLD(HSCODE)
     A                                      TEXT('To Handstack')
     A                                      COLHDG('To' 'Handstack')
     A            WRTSEQ         5P 0       TEXT('To Pick Seq')
     A                                      COLHDG('To Pick' 'Seq')
     A            WRTSID         1S 0       TEXT('To Slot Side')
     A                                      COLHDG('To Slot' 'Side')
     A            WRTPOS         3P 0       TEXT('To Pallet Position')
     A                                      COLHDG('To Pallet' 'Position')
     A            WRTDSP    R               REFFLD(SLDISP *LIBL/SLOT)
     A                                      TEXT('To Slot Display')
     A                                      COLHDG('To Slot' 'Display')
600aAA            WRTITEM   R               REFFLD(SLITEM *LIBL/SLOT)
600aAA                                      TEXT('To Item')
600aAA                                      COLHDG('To Item')
600aAA            WRTCOD         1A         TEXT('To Code')
600aAA                                      COLHDG('To' 'Code')
600aAA            WRTARA        15A         TEXT('To Area')
600aAA                                      COLHDG('To Area')
     A*
600aAA            WRUWHD    R               REFFLD(WDCODE)
600aAA                                      TEXT('USR Warehouse Dept')
600aAA                                      COLHDG('USR' 'Dept')
600aAA            WRUDSP    R               REFFLD(SLDISP *LIBL/SLOT)
600aAA                                      TEXT('USR Slot Display')
600aAA                                      COLHDG('USR' 'Slot')
600aAA            WRULCNS       15A         TEXT('USR License')
600aAA                                      COLHDG('USR License')
     A*
     A            WRRTN          1A         TEXT('Return to Home Slot Y/N')
     A                                      COLHDG('Rtn' 'Home Slot')
     A            WRVRFY         1A         TEXT('Verify Zero Slot Y/N')
     A                                      COLHDG('Verify' 'Zero Slot')
     A            WRBALQ         7P 0       TEXT('Balance qty')
     A                                      COLHDG('Balance' 'Qty')
     A            WRUTYP         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A*
     A          K WRWHSE
     A          K WRUSER
     A          K WRNBR
     A          K WRTYPE
