     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMQTY (PF)
     A*  Description ... Item warehouse quantity file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 2.00   07/22/94  DAS
     A*      - Redefined usage. Combined original fields into SLOT
     A*        file.  Now contains warehouse quantities.
     A*
     A*    Ver 2.01   09/16/94  DAS
     A*      - Added "hold" qtys for qtys in non-active slots.
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Quantity notes:
     A*
     A*    Each quantity type (ie. Stock qty) has three parts. Qty1
     A*    is the normal quantity. Qty2 is the breakdown 1 quantity and
     A*    Qty3 is the breakdown 2 quantity. The unit of measure for
     A*    each of these quantities is defined in the item file.
     A*
     A*    Type            Description
     A*    -------------   -------------------
     A*    Stock           Physical quantity in slot.
     A*
     A*    Allocated       Quantity selected for picking and
     A*                    replenishment.
     A*
     A*    Hold            Quantity in non-active slots.
     A*
     A*    Transfer In     Quantity being transfered in from another
     A*                    warehouse.
     A*
     A*    Transfer Out    Quantity being transfered to another
     A*                    warehouse.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R IQREC
     A*
     A            IQWHSE    R               REFFLD(WHCODE)
     A            IQITEM    R               REFFLD(ITITEM)
     A*
     A            IQSTK1         7P 0       TEXT('Stock Qty1')
     A                                      COLHDG('Stock' 'Qty1')
     A            IQSTK2         5P 0       TEXT('Stock Qty2')
     A                                      COLHDG('Stock' 'Qty2')
     A            IQSTK3         5P 0       TEXT('Stock Qty3')
     A                                      COLHDG('Stock' 'Qty3')
     A*
     A            IQALC1         7P 0       TEXT('Allocated Qty1')
     A                                      COLHDG('Allocated' 'Qty1')
     A            IQALC2         5P 0       TEXT('Allocated Qty2')
     A                                      COLHDG('Allocated' 'Qty2')
     A            IQALC3         5P 0       TEXT('Allocated Qty3')
     A                                      COLHDG('Allocated' 'Qty3')
     A*
     A            IQHLD1         7P 0       TEXT('Hold Qty1')
     A                                      COLHDG('Hold' 'Qty1')
     A            IQHLD2         5P 0       TEXT('Hold Qty2')
     A                                      COLHDG('Hold' 'Qty2')
     A            IQHLD3         5P 0       TEXT('Hold Qty3')
     A                                      COLHDG('Hold' 'Qty3')
     A*
     A            IQTRI1         7P 0       TEXT('Transfer In Qty1')
     A                                      COLHDG('Transfer In' 'Qty1')
     A            IQTRI2         5P 0       TEXT('Transfer In Qty2')
     A                                      COLHDG('Transfer In' 'Qty2')
     A            IQTRI3         5P 0       TEXT('Transfer In Qty3')
     A                                      COLHDG('Transfer In' 'Qty3')
     A*
     A            IQTRO1         7P 0       TEXT('Transfer Out Qty1')
     A                                      COLHDG('Transfer Out' 'Qty1')
     A            IQTRO2         5P 0       TEXT('Transfer Out Qty2')
     A                                      COLHDG('Transfer Out' 'Qty2')
     A            IQTRO3         5P 0       TEXT('Transfer Out Qty3')
     A                                      COLHDG('Transfer Out' 'Qty3')
     A*
     A            IQORD1         7P 0       TEXT('On Order Qty1')
     A                                      COLHDG('On Order' 'Qty1')
     A*
     A          K IQWHSE
     A          K IQITEM
