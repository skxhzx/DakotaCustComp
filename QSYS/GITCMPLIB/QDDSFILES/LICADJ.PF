     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICADJ   (PF)
     A*  Description ... License Adjustment file
     A*  Creator ....... RTR
     A*
     A*  Revisions
     A*
700  A*    03/11/15  RTR  7.00
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ADJREC
     A*
     A            ADJWHS    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            ADJNBR        11A         TEXT('Adj Number')
     A                                      COLHDG('Adjust' 'Number')
     A            ADJPARENT     11A         TEXT('Adj Parent')
     A                                      COLHDG('Adj' 'Parent')
     A*
     A            ADJCODE        2A         TEXT('Adj Code')
     A                                      COLHDG('Adj' 'Code')
     A            ADJCSYS        1A         TEXT('Upd Sys')
     A                                      COLHDG('Upd' 'Sys')
     A            ADJITEM   R               TEXT('To Item')
     A                                      COLHDG('To' 'Item')
     A                                      REFFLD(ITITEM)
     A*
     A* Adjustment Quantities
     A*
     A            ADJAQTY1       5P 0       TEXT('Adjust Qty1')
     A                                      COLHDG('Adjust' 'Qty1')
     A            ADJAQTY2       5P 0       TEXT('Adjust Qty2')
     A                                      COLHDG('Adjust' 'Qty2')
     A            ADJAQTY3       5P 0       TEXT('Adjust Qty3')
     A                                      COLHDG('Adjust' 'Qty3')
     A            ADJCQTA        9P 2       TEXT('Catchwgt Adj')
     A                                      COLHDG('Catchwgt' 'Adjust')
     A*
     A* Adjustment Location Information
     A*
     A            ADJWHDP   R               TEXT('Department')
     A                                      COLHDG('Dept')
     A                                      REFFLD(WDCODE)
     A            ADJDISP       12A         TEXT('Adj Display')
     A                                      COLHDG('Adj' 'Display')
     A            ADJAISL   R               TEXT('Adj Aisle')
     A                                      COLHDG('Adj' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            ADJLOC    R               TEXT('Adj Location')
     A                                      COLHDG('Adj' 'Location')
     A                                      REFFLD(SLLOC)
     A            ADJRLVL   R               TEXT('Adj Rack Level')
     A                                      COLHDG('Adj' 'Level')
     A                                      REFFLD(SLRLVL)
     A            ADJHAND   R               TEXT('Adj Handstack')
     A                                      COLHDG('Adj' 'Handstack')
     A                                      REFFLD(HSCODE)
     A            ADJSTAT   R               TEXT('Adj Status')
     A                                      COLHDG('Adj' 'Status')
     A                                      REFFLD(SSCODE)
     A*
     A* Adjustment License Log information
     A*
     A            ADJMEMO       30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            ADJLLTRN  R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            ADJLLTRSQ      2S 0       TEXT('Lic Tran Seq #')
     A                                      COLHDG('Lic Tran' 'Seq #')
     A            ADJLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A*
     A* Timestamp / User Tracking Information
     A*
     A            ADJADDTS  R               REFFLD(ADDTS)
     A            ADJADDUC  R               REFFLD(ADDUC)
     A            ADJADDCPGMR               REFFLD(ADDPGM)
     A                                      COLHDG('Calling Pgm')
     A                                      TEXT('Calling Pgm')
     A            ADJADDJPGMR               REFFLD(ADDPGM)
     A                                      COLHDG('Job Pgm')
     A                                      TEXT('Job Pgm')
     A            ADJADDEMP R               REFFLD(ADDEMP)
     A            ADJADDCUSRR               REFFLD(ADDUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            ADJADDJUSRR               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            ADJADDJOB R               REFFLD(ADDJOB)
     A            ADJADDNBR R               REFFLD(ADDNBR)
     A*
     A            ADJEXPFLAG     1A         TEXT('Export Flag')
     A                                      COLHDG('Export' 'Flag')
     A            ADJEXPTS  R               REFFLD(ADDTS)
     A                                      COLHDG('Export Stamp')
     A                                      TEXT('Export Stamp')
     A            ADJEXPUC  R               REFFLD(ADDUC)
     A                                      COLHDG('Export UC Stamp')
     A                                      TEXT('Export UC Stamp')
     A            ADJEXPJUSRR               REFFLD(ADDUSR)
     A                                      TEXT('Export Job User')
     A                                      COLHDG('Export' 'Job User')
     A            ADJEXPJOB R               REFFLD(ADDJOB)
     A                                      TEXT('Export Job')
     A                                      COLHDG('Export' 'Job')
     A            ADJEXPNBR R               REFFLD(ADDNBR)
     A                                      TEXT('Export Job Nbr')
     A                                      COLHDG('Export' 'Job Nbr')
     A*
     A            ADJDRISLOT    10A         TEXT('DRISLOT Cmd')
     A                                      COLHDG('DRISLOT Cmd')
     A            ADJDRILIC     10A         TEXT('DRILICENSE Cmd')
     A                                      COLHDG('DRILICENSE' 'Cmd')
     A*
     A          K ADJWHS
     A          K ADJNBR
