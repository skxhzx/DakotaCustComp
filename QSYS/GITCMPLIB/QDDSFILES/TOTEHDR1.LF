     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   P.O. Box 1957
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TOTEHDR1 (LF)
     A*  Description ... Tote header file by physical tran#
     A*  Date .......... February 22, 2008
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    10/06/08  RBD  5.20  P#00011
     A*      - Fix:  Change field TOHNUM to TOHCNT, as TOHNUM is
     A*        already used in file TORDH.
     A*
CWD AA*    04/28/15  MLB  CWD   P#01349
     A*      - Enh: Re-compiled file. Physical file had order id added
     A*        to make file key unique.
     A*
     A*----------------------------------------------------------------
     A*
     A                                      JDFTVAL
     A          R TOHRECJ                   JFILE(TOTEHDR ORDH ORDHM)
     A*
     A          J                           JOIN(TOTEHDR ORDH)
     A                                      JFLD(TOHWHS  OHWHSE)
     A                                      JFLD(TOHORD  OHORD)
     A*
     A          J                           JOIN(TOTEHDR ORDHM)
     A                                      JFLD(TOHWHS  HMWHSE)
     A                                      JFLD(TOHORD  HMORD)
     A*
     A*  Fields from TOTEHDR file.
     A*
     A            TOHWHS
     A            TOHRID
     A            TOHPPI
520 DA**          TOHNUM
520 MA            TOHCNT
     A            TOHORD
     A            TOHLAY
     A            TOHSEC
     A            TOHECB
     A            TOHICB
     A            TOHMXP
     A            TOHCUB
     A            TOHPCS
     A            TOHWGT
     A            TOHLBL
     A            TOHTR#
     A            TOHMPI
     A            TOHML#
     A            TOHMT#
     A*
     A*  Fields from ORDH file.
     A*
     A            OHORD
     A            OHORIG
     A            OHTYPE
     A            OHCUST
     A            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A            OHPBAT
     A            OHSTAT
     A*
     A*  Fields from ORDHM file.
     A*
     A            HMMSC1
     A            HMMSC2
     A            HMMSC3
     A            HMMSC4
     A            HMMSC5
     A*
     A*  Key fields
     A*
     A          K TOHTR#
520 DA**        K TOHNUM
520 MA          K TOHCNT
