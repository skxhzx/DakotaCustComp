     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... BARDEFH (PF)
     A*  Description ... User-Defined barcode def - Header
     A*  Creator ....... DAS
     A*  Date .......... January 17, 2020
     A*
     A*  Revisions:
     A*
740 AA*    01/17/20  DAS  7.40
     A*      - Created.
     A*
740aAA*    04/06/20  DAS  7.40a
     A*      - Added fields BDHMINLEN and BDHAIFLAGS
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   Mfr# will be required until we can start using item overrides
     A*   but before this can happen all programs that call CVTBAR will
     A*   need to be modified to send the item.
     A*
     A*   The Length and Id fields are optional.
     A*
740aAA*   The minimum length is the minimum length barcode required
740aAA*   by the definition and is calculated during update.
     A*
     A*----------------------------------------------------------------

740aAA*----------------------------------------------------------------
740aAA* AI Flags
     A*
     A*   0 = Not used, 1 = Used
     A*   Set in update program
     A*
     A*   Char Pos    Flag Description
     A*   --------    ----------------
     A*      1        GTIN
     A*      2        Weight - Pounds
     A*      3        Weight - Kilograms
     A*      4        Production Date
     A*      5        Sell By Date
     A*      6        Expiration Date
     A*      7        Lot Code
     A*      8        Serial Number
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R BDHREC
     A*
     A            BDHWHSE        3S 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            BDHDEF#        5  0       TEXT('Definition Number')
     A                                      COLHDG('Def#')
     A            BDHMFR#       11S 0       TEXT('Mfr Number')
     A                                      COLHDG('Mfr' 'Number')
     A            BDHDESC       40          TEXT('Description')
     A                                      COLHDG('Description')
     A            BDHDFLT        1          TEXT('Default (Y,N)')
     A                                      COLHDG('Default' '(Y,N)')
     A            BDHLEN         3  0       TEXT('Length')
     A                                      COLHDG('Length')
     A            BDHIDSTRT      3  0       TEXT('Id Start Pos')
     A                                      COLHDG('Id Start')
     A            BDHIDVAL      10          TEXT('Id Value')
     A                                      COLHDG('Id Value')
740aAA            BDHMINLEN      3  0       TEXT('Min Length')
740aAA                                      COLHDG('Min Length')
740aAA            BDHAIFLAGS    30          TEXT('AI Flags')
740aAA                                      COLHDG('AI Flags')
     A            BDHEXAMPLE   100          TEXT('Example')
     A                                      COLHDG('Example')
     A          K BDHWHSE
     A          K BDHDEF#
