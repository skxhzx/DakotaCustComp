     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TRAVELH  (PF)
     A*  Description ... Travel Header File
     A*  Date .......... December 18, 2008
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
520 AA*    12/18/08  DAS  5.20
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    Travel Type
     A*
     A*       The Travel Type field has been included in this file under
     A*       the premise that the selection travel path may not be the
     A*       same as the putaway travel path. This would probably only
     A*       be true if the client does not permit bi-directional
     A*       traffic for selectors.
     A*
     A*       During look up a program will first look for a specific
     A*       entry (i.e. 'S'). If not found, it will look for the
     A*       catch-all ' ' entry.
     A*
     A*       ' '  -  Catch-all
     A*       'S'  -  Selection
     A*       'P'  -  Putaway
     A*       'R'  -  Replenishment
     A*       'T'  -  Transfer
     A*
     A*    From/To Place
     A*
     A*       Place is synonymous with a warehouse location. The
     A*       reason for using the word 'Place' is so it would not
     A*       be confused with the slot location.
     A*
     A*       See the following Type information to see the values
     A*       that are put into th Place fields.
     A*
     A*    From/To Type (Place Type)
     A*
     A*       'A'  -  Area
     A*               (Place should be the code for the area)
     A*
     A*       'B'  -  Back of aisle
     A*               (Place should be 'Back-' + %trim(aisle))
     A*
     A*       'D'  -  Department
     A*               (Place should be 'Dept-' + %trim(department))
     A*
     A*       'F'  -  Front of aisle
     A*               (Place should be 'Front-' + %trim(aisle))
     A*
     A*       'S'  -  Slot
     A*               (Place should be slot display)
     A*
     A*       'T'  -  Tunnel
     A*               (Place should be tunnel slot display)
     A*
     A*    Calculation type
     A*
     A*       'H'  -  Hard coded (manually entered)
     A*
     A*       'S'  -  Calculated from slot definitions.
     A*               Used when calculating the distance between 2
     A*               slots within the same aisle using the lengths
     A*               found in the slot definition file.
     A*
     A*       'D'  -  Calculated from detail file.
     A*               Used when calculated based on the information
     A*               from the TRAVELD file.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R TRHREC
     A*
     A            TRHTYPE        1          TEXT('Travel Type')
     A                                      COLHDG('Travel' 'Type')
     A            TRHID          9  0       TEXT('Header Id')
     A                                      COLHDG('Header Id')
     A            TRHWHSE   R               REFFLD(WHCODE)
     A*
     A            TRHFTYPE       1          TEXT('From Type')
     A                                      COLHDG('From' 'Type')
     A            TRHFWHDP  R               REFFLD(WDCODE)
     A                                      TEXT('From Dept')
     A                                      COLHDG('From' 'Dept')
     A            TRHFPLACE     12          TEXT('From Place')
     A                                      COLHDG('From' 'Place')
     A            TRHFAISL  R               REFFLD(ASCODE)
     A                                      TEXT('From Aisle')
     A                                      COLHDG('From' 'Aisle')
     A            TRHFLOC   R               REFFLD(SLLOC)
     A                                      TEXT('From Loc')
     A                                      COLHDG('From' 'Loc')
     A            TRHFRLVL  R               REFFLD(SLRLVL)
     A                                      TEXT('From Level')
     A                                      COLHDG('From' 'Level')
     A            TRHFHAND  R               REFFLD(HSCODE)
     A                                      TEXT('From Hand')
     A                                      COLHDG('From' 'Hand')
     A*
     A            TRHTTYPE       1          TEXT('To Type')
     A                                      COLHDG('To' 'Type')
     A            TRHTWHDP  R               REFFLD(WDCODE)
     A                                      TEXT('To Dept')
     A                                      COLHDG('To' 'Dept')
     A            TRHTPLACE     12          TEXT('To Place')
     A                                      COLHDG('To' 'Place')
     A            TRHTAISL  R               REFFLD(ASCODE)
     A                                      TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A            TRHTLOC   R               REFFLD(SLLOC)
     A                                      TEXT('To Loc')
     A                                      COLHDG('To' 'Loc')
     A            TRHTRLVL  R               REFFLD(SLRLVL)
     A                                      TEXT('To Level')
     A                                      COLHDG('To' 'Level')
     A            TRHTHAND  R               REFFLD(HSCODE)
     A                                      TEXT('To Hand')
     A                                      COLHDG('To' 'Hand')
     A*
     A            TRHHCTYPE      1          TEXT('H. Calc Type')
     A                                      COLHDG('H. Calc' 'Type')
     A            TRHHDIST       7P 2       TEXT('H. Distance (Feet)')
     A                                      COLHDG('H. Distance' '(Feet)')
     A*
     A            TRHVCTYPE      1          TEXT('V. Calc Type')
     A                                      COLHDG('V. Calc' 'Type')
     A            TRHVDIST       7P 2       TEXT('V. Distance (Feet)')
     A                                      COLHDG('V. Distance' '(Feet)')
     A*
     A* Timestamp / User tracking information.
     A*
     A            TRHCHGTS  R               REFFLD(CHGTS)
     A            TRHCHGUC  R               REFFLD(CHGUC)
     A            TRHCHGPGM R               REFFLD(CHGPGM)
     A            TRHCHGCUSRR               REFFLD(CHGUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            TRHCHGJUSRR               REFFLD(CHGUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            TRHCHGJOB R               REFFLD(CHGJOB)
     A            TRHCHGNBR R               REFFLD(CHGNBR)
     A*
     A          K TRHTYPE
     A          K TRHID
