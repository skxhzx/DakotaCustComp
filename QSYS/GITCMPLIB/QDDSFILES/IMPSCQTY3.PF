     A*----------------------------------------------------------------
     A*   Copyright (C) 2023 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSCQTY3 (PF)
     A*  Description ... Staging Import - Host Qty for Compare V3
     A*
     A*  Revisions
     A*
770  A*    07/15/23  DAS  7.70
     A*      - Created based on IMPFCQTY2. Includes Host Lot codes
     A*        and weight.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*  This file has been created to work with a variety of
     A*  situations.
     A*
     A*  Host Lot code is optional. Originally, it would be used with
     A*  Host Systems FreshByte and NetSute.
     A*
     A*  License has not been included as a separate field. Instead,
     A*  it should be put in the Host Lot field.
     A*
     A*
     A*  Each qty is defined as a decimal. This is to accomodate
     A*  Host systems like FreshByte, where they will just send
     A*  a single qty for their master uom.
     A*
     A*  UOM is optional. It would be used by FreshByte to identify
     A*  the master UOM they are sending.
     A*
     A*  Weight is optional and is for possible future use.
     A*
     A*  Brand came from the other files. I do not know why a
     A*  comparison would be based on brand.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R IC3REC
     A*
     A            IC3WHSE        3S 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            IC3BATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            IC3STATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IC3STSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IC3SSTUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IC3TEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            IC3ITEM       15          TEXT('Item')
     A                                      COLHDG('Item')
     A            IC3HSTLOT     30A         TEXT('Host Lot')
     A                                      COLHDG('Host Lot')
     A            IC3UM1         2          TEXT('UOM 1')
     A                                      COLHDG('UOM 1')
     A            IC3QTY1       15S 5       TEXT('Qty1 on hand')
     A                                      COLHDG('Qty1' 'On hand')
     A            IC3WGT1       15S 5       TEXT('Wgt1 on hand')
     A                                      COLHDG('Wgt1' 'On hand')
     A            IC3UM2         2          TEXT('UOM 2')
     A                                      COLHDG('UOM 2')
     A            IC3QTY2       15S 5       TEXT('Qty2 on hand')
     A                                      COLHDG('Qty2' 'On hand')
     A            IC3WGT2       15S 5       TEXT('Wgt2 on hand')
     A                                      COLHDG('Wgt2' 'On hand')
     A            IC3UM3         2          TEXT('UOM 3')
     A                                      COLHDG('UOM 3')
     A            IC3QTY3       15S 5       TEXT('Qty3 on hand')
     A                                      COLHDG('Qty3' 'On hand')
     A            IC3WGT3       15S 5       TEXT('Wgt3 on hand')
     A                                      COLHDG('Wgt3' 'On hand')
     A            IC3BRND       10A         TEXT('Brand')
     A                                      COLHDG('Brand')
     A*
     A*  Add stamps
     A*
     A            IC3ADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IC3ADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IC3WHSE
     A          K IC3BATID
     A          K IC3ITEM
     A          K IC3HSTLOT
