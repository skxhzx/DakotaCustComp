     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... UCIINFOEXA  (PF)
     A*  Description ... UCI Information Extra fields
     A*  Creator ....... LMC
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
720 AA*    11/07/18  LMC  7.20
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*  UIUCILCNS - Internal UCI License
     A*
     A*    This is an internal license that can be used to record
     A*    UCI actions in the LICLOG file.
     A*
     A*    The license should start with 'U:' followed by a
     A*    13-digit number.
     A*
     A*----------------------------------------------------------------
     A*  UICRTBY - Record created by
     A*
     A*   1 = Receiving
     A*   2 = Returns
     A*   3 = Selection
     A*   4 = Internal GS1-99 creation
     A*   5 = Drilicense - Production Module
     A*   6 = CW122 - Catch weight entry mutiples
     A*   7 = PO232 - Catch weight entry single
     A*   8 = RU310 - Runner Selection
     A*   9 = LT107 - Prodcution Replenishment
     A*   A = M17300 - Production Box Breaker
     A*   B = Inventory adjustment
     A*   C = Canopy Import
     A*
     A*----------------------------------------------------------------
700aAA*  UIOLCNS/UIOSEQ - Original License/Seq
     A*
     A*    These fields are used to get original receiving information
     A*    for the UCI from the LICINFO file.
     A*
     A*----------------------------------------------------------------
700aAA*  UIENTD/UIEXPD - Entered/Expiration Dates
     A*
     A*    These fields are included in this file because they may be
     A*    manually changed. For exmaple, an item is changed from a
     A*    Cooler item to a Freezer item. Yes, LICACTIVE also has this
     A*    information, but a license could contain UCI's with
     A*    different dates.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R UIAREC
     A*
     A            UIALCNS       15A         TEXT('License')
     A                                      COLHDG('License'           )
     A            UIAOLCNS      15A         TEXT('Original License')
     A                                      COLHDG('Original' 'License')
     A            UIAOSEQ        2S 0       TEXT('Orig Lcns Seq')
     A                                      COLHDG('Orig Lcns' 'Seq')
     A            UIAUTYP        1A         TEXT('UOM Type (N,1,2)')
     A                                      COLHDG('UOM Type' '(N,1,2)')
     A            UIASITEM  R               TEXT('Slot Item')
     A                                      COLHDG('Slot Item')
     A                                      REFFLD(ITITEM)
     A            UIASUTYP       1A         TEXT('Slot UOM Type (N,1,2)')
     A                                      COLHDG('Slot UOM' 'Type (N,1,2)')
     A            UIAGTIN       14S 0       TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            UIAENTD   R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            UIAEXPD   R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A            UIALOT        30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            UIAPARTLTS      Z         TEXT('Partial Move TS')
     A                                      COLHDG('Partial Move' +
     A                                             'Timestamp')
     A            UIAUCILCNS    15A         TEXT('UCI License')
     A                                      COLHDG('UCI' 'License')
     A*
     A* Timestamp / User tracking information.
     A*
     A            UIAADDTS  R               REFFLD(ADDTS)
     A*
     A*
     A*
     A          K UIAADDTS
     A          K UIAUCILCNS
     A          K UIALCNS
