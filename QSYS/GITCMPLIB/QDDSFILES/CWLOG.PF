     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... CWLOG   (PF)
     A*  Description ... Catch Weight Log file
     A*  Creator ....... LMC
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650  A*    03/07/13  LMC  6.50
     A*      - Created
650a A*    09/30/14  LMC  6.50a
     A*      - Added some documentation - no file change
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R CWREC
     A*
     A            CWWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            CWSELORD  R               REFFLD(OHORD)
     A            CWSELSEQ  R               REFFLD(SEQ#)
     A            CWITEM    R               REFFLD(ITITEM)
     A            CWSELLBL#      7P 0       TEXT('Selection Lbl#')
     A                                      COLHDG('Selection' 'Lbl#')
     A            CWSELTRN#      7P 0       TEXT('Selection Tran#')
     A                                      COLHDG('Selection' 'Tran#')
     A            CWSELCSEQ      3P 0       TEXT('ORDC Seq#')
     A                                      COLHDG('ORDC' 'Seq#')
     A            CWSCAN       100A         TEXT('Scanned Value')
     A                                      COLHDG('Scanned')
     A            CWENTFLG       3S 0       TEXT('Entered Flag')
     A                                      COLHDG('Entered' 'Type')
      * 1 = Talkman scanned weight
      * 2 = Non scanned weight - voice
      * 3 = Manual Entry
      * 4 = Bulk load by license - UCI selection
      * 5 = Scanned using Runner program
     A            CWWGTLBS       9P 2       TEXT('Weight Lbs')
     A                                      COLHDG('Weight' 'Lbs')
     A* CWSTAT = ' ', UNLESS ORDC RECORD WAS DELETED
     A            CWSTAT         1A         TEXT('Status')
     A*
     A* Timestamp / User tracking information.
     A*
     A            CWADDTS   R               REFFLD(ADDTS)
     A            CWADDUC   R               REFFLD(ADDUC)
     A            CWADDPGM  R               REFFLD(ADDPGM)
     A            CWADDEMP  R               REFFLD(ADDEMP)
     A            CWADDUSR  R               REFFLD(ADDUSR)
     A            CWADDJOB  R               REFFLD(ADDJOB)
     A            CWADDNBR  R               REFFLD(ADDNBR)
     A*
     A            CWCHGTS   R               REFFLD(CHGTS)
     A            CWCHGUC   R               REFFLD(CHGUC)
     A            CWCHGPGM  R               REFFLD(CHGPGM)
     A            CWCHGEMP  R               REFFLD(CHGEMP)
     A            CWCHGUSR  R               REFFLD(CHGUSR)
     A            CWCHGJOB  R               REFFLD(CHGJOB)
     A            CWCHGNBR  R               REFFLD(CHGNBR)
     A*
     A*
     A          K CWWHSE
     A          K CWSELORD
     A          K CWSELSEQ
     A          K CWITEM
     A          K CWSELLBL#
     A          K CWSELCSEQ
     A          K CWADDUC                   DESCEND
