     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFADJ  (PF)
     A*  Description ... FTP Item quantity adjustment export file.
     A*  Date .......... March 3, 1999
     A*  Creator ....... DAS
     A*
     A*  This file should be identical to EXPADJ except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFADJ.FILE')
     A*
     A*  Revisions:
     A*
213 AA*    Ver 2.01  05/13/96  DAS
213 AA*      - Added fields IAWHDP, IADISP and IACWTA.
     A*
413 AA*    Ver 4.13  02/02/00  DAS
     A*      - Made key non-unique like ITEMADJ.
700a A*    Ver 7.00  03/27/15  RTR 7.00a
     A*      - Added slot status IESTAT.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
413 DA*                                     UNIQUE
     A          R IEREC
     A*
     A            IEWHSE    R               REFFLD(WHCODE)
     A            IEITEM    R               REFFLD(ITITEM)
     A            IEDATE    R               TEXT('Adjustment Date')
     A                                      COLHDG('Adjusted' 'Date')
     A                                      REFFLD(DTECMD)
     A            IETIME    R               TEXT('Adjustment Time')
     A                                      COLHDG('Adjusted' 'Time')
     A                                      REFFLD(TIMHMS)
     A            IEBY          10          TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
213 AA            IEWHDP    R               REFFLD(WDCODE)
213 AA            IEDISP        12A         TEXT('Slot# Display')
213 AA                                      COLHDG('Slot#' 'Display')
213 AA            IECWTA         9S 2       TEXT('Catchweight Adjustment')
213 AA                                      COLHDG('Catchweight' 'Adjustment')
     A            IEQTY1         5S 0       TEXT('Quantity 1')
     A                                      COLHDG('Quantity 1')
     A            IEQTY2         5S 0       TEXT('Quantity 2')
     A                                      COLHDG('Quantity 2')
     A            IEQTY3         5S 0       TEXT('Quantity 3')
     A                                      COLHDG('Quantity 3')
     A            IECODE    R               REFFLD(ADCODE)
     A            IEMEMO        30          TEXT('Memo')
     A                                      COLHDG('Memo')
700aAA            IESTAT         2A         TEXT('Slot Status')
700aAA                                      COLHDG('Slot Status')
     A*
     A          K IEWHSE
     A          K IEITEM
     A          K IEDATE
     A          K IETIME
