     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ZONED2 (LF)
     A*  Description ... Zone detail join file for search
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    06/11/97 DAS  4.00
     A*      - Created.
     A*
416 AA*    09/09/02 DAS  4.16
     A*      - Created.
     A*      - Programs do NOT need to be recompiled because the
     A*        Record Format Level Identified did not change.
     A*
520aAA*    02/27/09  DAS  5.20a  P#00150
     A*      - Added new fields.
     A*      - Changed key by moving zone from bottom to after
     A*        dept.
     A*
720aAA*    10/14/18  DAS  7.20a
     A*      - Added new fields
     A*
720bAA*    10/25/18  DAS  7.20b
     A*      - Added new field
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R ZJREC                     JFILE(ZONED ZONEH)
     A*
     A          J                           JOIN(ZONED ZONEH)
     A                                      JFLD(ZDWHSE  ZHWHSE)
416 AA                                      JFLD(ZDTRUK  ZHTRUK)
     A                                      JFLD(ZDZONE  ZHZONE)
     A*
     A*  Fields from ZONED file.
     A*
     A            ZDWHSE
     A            ZDTRUK
     A            ZDZONE
     A            ZDWHDP
     A            ZDSTYP
     A            ZDAISL
     A            ZDRLVL
     A            ZDLOC1
     A            ZDLOC2
520aAA            ZDDESG
520aAA            ZDCAT
520aAA            ZDHND1
520aAA            ZDHND2
520aAA            ZDUTYP
520aAA            ZDSIDE
720aAA            ZDACTV
720aAA            ZDAISL2
720aAA            ZDRLVL2
720aAA            ZDCLCDE
720bAA            ZDPCKOVF
720bAA            ZDCSTCOD
     A*
     A*  Fields from ZONEH file.
     A*
     A            ZHDESC
     A            ZHCOMP
     A            ZHPZON
     A            ZHGEN
     A            ZHDROP
     A*
     A*  Key
     A*
     A          K ZDWHSE
     A          K ZDTRUK
     A          K ZDWHDP
520aAA          K ZDZONE
     A          K ZDSTYP
     A          K ZDAISL
     A          K ZDRLVL
     A          K ZDLOC2
     A          K ZDLOC1
520aDA**        K ZDZONE
