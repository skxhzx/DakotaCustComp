     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFQTY2 (PF)
     A*  Description ... FTP Item quantity export file Ver 2
     A*  Date .......... December 11, 2009
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  This file should be identical to EXPQTY except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFQTY.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
530 AA*   12/11/09  MLB  5.30
     A*     - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*    QERFLG  - Reduced flag. Only used for Base items that have
     A*              corresponding Contract items.
     A*
     A*                 'Y' = Base item stock qty's were reduced by
     A*                       its corresponding Contract item stock
     A*                       qty's.
     A*                 'N' = Base item stock qty's were not reduced
     A*                       by its corresponding Contract item
     A*                       stock qty's.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R QEREC
     A*
     A            QEWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            QEWHDP    R               REFFLD(WDCODE)
     A            QEITEM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            QETYPE         1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            QEBITM        15          TEXT('Base Item')
     A                                      COLHDG('Base Item')
     A            QECITM        15          TEXT('Contract Base Item')
     A                                      COLHDG('Contract' 'Base Item')
     A            QERFLG         1          TEXT('Reduce Flag')
     A                                      COLHDG('Reduce' 'Flag')
     A            QESTK1         8A         TEXT('Normal Stock Qty')
     A                                      COLHDG('Normal' 'Stock Qty')
     A            QESTK2         6A         TEXT('Brk1 Stock Qty')
     A                                      COLHDG('Brk1' 'Stock Qty')
     A            QESTK3         6A         TEXT('Brk2 Stock Qty')
     A                                      COLHDG('Brk2' 'Stock Qty')
     A            QEDSP1        12          TEXT('First Pick Slot')
     A                                      COLHDG('First' 'Pick Slot')
     A            QEDSP2        12          TEXT('Second Pick Slot')
     A                                      COLHDG('Second' 'Pick Slot')
     A            QEDSP3        12          TEXT('Third Pick Slot')
     A                                      COLHDG('Third' 'Pick Slot')
     A            QEDATE         8S 0       TEXT('Date (YYYYMMDD)')
     A                                      COLHDG('Date')
     A            QETIME         6S 0       TEXT('Time (HHMMSS)')
     A                                      COLHDG('Time')
     A            QECUBE         7S 3       TEXT('Unit Cube')
     A                                      COLHDG('Unit Cube')
     A            QESDF1         2          TEXT('First Slot Def')
     A                                      COLHDG('First' 'Slot Def')
     A            QEPAL1         6          TEXT('First Pallet Code')
     A                                      COLHDG('First' 'Pallet Code')
     A            QEPOS1         3S 1       TEXT('First Positions')
     A                                      COLHDG('First' 'Positions')
     A            QETI1          4S 0       TEXT('First Tie')
     A                                      COLHDG('First' 'Tie')
     A            QEHI1          2S 0       TEXT('First High')
     A                                      COLHDG('First' 'High')
     A            QESDF2         2          TEXT('Second Slot Def')
     A                                      COLHDG('Second' 'Slot Def')
     A            QEPAL2         6          TEXT('Second Pallet Code')
     A                                      COLHDG('Second' 'Pallet Code')
     A            QEPOS2         3S 1       TEXT('Second Positions')
     A                                      COLHDG('Second' 'Positions')
     A            QETI2          4S 0       TEXT('Second Tie')
     A                                      COLHDG('Second' 'Tie')
     A            QEHI2          2S 0       TEXT('Second High')
     A                                      COLHDG('Second' 'High')
     A            QESDF3         2          TEXT('Third Slot Def')
     A                                      COLHDG('Third' 'Slot Def')
     A            QEPAL3         6          TEXT('Third Pallet Code')
     A                                      COLHDG('Third' 'Pallet Code')
     A            QEPOS3         3S 1       TEXT('Third Positions')
     A                                      COLHDG('Third' 'Positions')
     A            QETI3          4S 0       TEXT('Third Tie')
     A                                      COLHDG('Third' 'Tie')
     A            QEHI3          2S 0       TEXT('Third High')
     A                                      COLHDG('Third' 'High')
     A*
     A          K QEWHSE
     A          K QEITEM
