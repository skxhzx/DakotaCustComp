     A*----------------------------------------------------------------
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... WORKSOS (PF)
     A*  Description ... Workfile for Stageing Single Order Selection
     A*  Date .......... July 8, 2005
     A*  Creator ....... RH
     A*
     A*
     A*  Revisions
     A*
500a A*    07/08/05  RH
     A*      - Created.
500b A*    08/23/05  RH
     A*      - Add pallet license WKSPLI to file and key
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Number type codes (WKSTYP)
     A*
     A*       'P'  -  License plate scanned.
     A*       'L'  -  Label scanned.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R WKSREC
     A*
     A            WKSWHS    R               REFFLD(WHCODE)
     A            WKSUSR    R               REFFLD(USCODE *LIBL/PIRUSER)
     A            WKSLBL    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            WKSTYP         1A         TEXT('Number Type')
     A                                      COLHDG('Number' 'Type')
     A            WKSTRN    R     P         REFFLD(TRAN#)
     A            WKSWHD    R               REFFLD(WDCODE)
     A                                      TEXT('Warehouse Dept')
     A                                      COLHDG('Warehouse' 'Dept')
     A            WKSASL    R               REFFLD(ASCODE)
     A                                      TEXT('Aisle')
     A                                      COLHDG('Aisle')
     A            WKSLOC    R               REFFLD(SLLOC)
     A                                      TEXT('Slot Location')
     A                                      COLHDG('Slot' 'Location')
     A            WKSLVL    R               REFFLD(SLRLVL)
     A                                      TEXT('Slot Level')
     A                                      COLHDG('Slot' 'Level')
     A            WKSHND    R               REFFLD(HSCODE)
     A                                      TEXT('Handstack')
     A                                      COLHDG('Handstack')
     A            WKSDSP    R               REFFLD(SLDISP *LIBL/SLOT)
     A                                      TEXT('Slot Display')
     A                                      COLHDG('Slot' 'Display')
     A            WKSPCK         5P 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
500b A            WKSPLI    R               TEXT('Pallet License')
500b A                                      COLHDG('Pallet' 'License')
500b A                                      REFFLD(TRAN#)
     A*
     A          K WKSWHS
     A          K WKSUSR
     A          K WKSPLI
     A          K WKSLBL
     A          K WKSTYP
