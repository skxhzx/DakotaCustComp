     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSORDLT (PF)
     A*  Description ... Staging Export - Order Host Lots
     A*
     A*  Revisions
     A*
730 AA*    03/04/19  DAS  7.30
     A*      - Created
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file contains Host Lot information that corresponds to
     A*   the EXPSORDD file. There may be 1 or more records in this
     A*   file associated with an EXPSORDD record.
     A*
     A*   The total of the quantities of the associated records
     A*   in this file should equal the quantities in the corresponding
     A*   EXPSORDD record.
     A*
     A*   Typically, the Host Lot field will contain either the Dakota
     A*   License or the GS1 Serial Number from the Dakota UCI, the
     A*   latter being used with Unique UCI tracked items.
     A*
     A*   The following fields would only be used when working with
     A*   a UCI tracked item:
     A*
     A*     OELTUCI
     A*     OELTUCISEQ
     A*     OELTGTIN
     A*     OELTDATE
     A*     OELTDATECD
     A*     OELTDATENM
     A*     OELTLOT
     A*     OELTSERIAL
     A*
     A*   OELTLOT and OELTSERIAL are defined as 30 to match other files.
     A*
     A*   If a License lost its connection with its original Receiving
     A*   information, the Original License will be blank.
     A*
     A*   The PO information is associated with the Original License.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Date AI Codes
     A*
     A*    Code  Name  Description
     A*    ----  ----  -----------
     A*     11   PROD  Production date (YYMMDD)
     A*     12   DUE   Due date (YYMMDD)
     A*     13   PACK  Packaging date (YYMMDD)
     A*     15   BEST  Best before date (YYMMDD)
     A*     16   SELL  Sell by date (YYMMDD)
     A*     17   EXP   Expiration date (YYMMDD)
     A*
     A*----------------------------------------------------------------
     A          R OELTREC
     A*
     A            OELTWHS        3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OELTBATID     11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            OELTRTE        5          TEXT('Route')
     A                                      COLHDG('Route')
     A            OELTORD        7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OELTSEQ        5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OELTITM       15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OELTLBL        7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            OELTSTATUS     1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            OELTSTSTS       Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELTSTSUC       Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELTTEXT     200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            OELTHSTLOT    30          TEXT('Host Lot Code')
     A                                      COLHDG('Host Lot Code')
     A            OELTQTY1       5S 0       TEXT('Normal Qty')
     A                                      COLHDG('Normal' 'Qty')
     A            OELTQTY2       5S 0       TEXT('Brkdn1 Qty')
     A                                      COLHDG('Brkdn1' 'Qty')
     A            OELTQTY3       5S 0       TEXT('Brkdn2 Qty')
     A                                      COLHDG('Brkdn2' 'Qty')
     A            OELTRTEID      5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            OELTORDID      7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            OELTLCNS      15          TEXT('License')
     A                                      COLHDG('License')
     A            OELTOLCNS     15          TEXT('Original License')
     A                                      COLHDG('Original License')
     A            OELTPO         9          TEXT('PO')
     A                                      COLHDG('PO')
     A            OELTPOSEQ      5S 0       TEXT('PO Seq')
     A                                      COLHDG('PO Seq')
     A            OELTUCI      100          TEXT('UCI')
     A                                      COLHDG('UCI')
     A            OELTUCISEQ     5S 0       TEXT('UCI Sequence Number')
     A                                      COLHDG('UCI Sequence' 'Number')
     A            OELTGTIN      14          TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            OELTDATE       6S 0       TEXT('Date YYMMDD')
     A                                      COLHDG('Date YYMMDD')
     A            OELTDATECD     2          TEXT('Date AI Code')
     A                                      COLHDG('Date' 'AI Code')
     A            OELTDATENM     4          TEXT('Date AI Name')
     A                                      COLHDG('Date' 'AI Name')
     A            OELTLOT       30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            OELTSERIAL    30          TEXT('Serial Nunber')
     A                                      COLHDG('Serial Number')
     A            OELTWGT        7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            OELTTRAX      10          TEXT('Trax Number')
     A                                      COLHDG('Trax' 'Number')
     A            OELTTRN        7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A*
     A*  Add stamps
     A*
     A            OELTADDTS       Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELTADDUC       Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K OELTWHS
     A          K OELTBATID
     A          K OELTRTEID
     A          K OELTORD
     A          K OELTSEQ
     A          K OELTITM
     A          K OELTLBL
     A          K OELTHSTLOT

