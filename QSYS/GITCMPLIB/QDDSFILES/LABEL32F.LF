     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL32F (LF)
     A*  Description ... Special join file for printing CTW pick lbls
     A*  Date .......... August 21, 2008
     A*  Creator ....... JCJ
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file is identical to LABEL32 except that it used LBLVL2
     A*  in its key. LBLVL2 is used to control the special picking
     A*  sequence used by City Wholesale.... Program OR605.
     A*  calls program DFPICK to set the LBLVL2 field for the proper
     A*  picking sequence.
     A*  This view has stop number in Ascending sequence.
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R LJREC32F                  JFILE(LABEL PLTSUM PIRITEM +
     A                                            ORDH ORDD UNMESR)
     A*
     A          J                           JOIN(LABEL   PLTSUM)
     A                                      JFLD(LBWHSE  PSWHSE)
     A                                      JFLD(LBRTE   PSRTID)
     A                                      JFLD(LBPAL#  PSPLID)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
214 AA            LBPO
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
214 AA            LBGRP1
214 AA            LBGRP2
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPAL#
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
     A*
     A*  Fields from PIRITEM file.
     A*
213 AA            ITTYPE
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
214 AA            ITFLG1
     A            ITUM3
     A            ITUMQ3
214 AA            ITFLG2
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
214 AA            ITREOR
214 AA            ITREPL
214 AA            ITRCVD
214 AA            ITFLGD
214 AA            ITFLGB
214 AA            ITSLIF
214 AA            ITNDAY
     A*
     A*  Fields from ORDH file.
     A*
214 AA            OHORIG
     A            OHTYPE
     A            OHCUST
214 AA            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
214 AA            OHPBAT
214 AA            OHSTAT
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Key fields
     A*
     A          K LBWHSE
     A          K LBRTE
     A          K LBTYPE
     A          K LBTRN#
520 AA          K LBSTOP
     A          K LBWHDP
     A          K LBSTYP
     A          K LBLVL2
     A          K LBAISL
     A          K LBPSEQ
     A          K LBRLVL
     A          K LBHAND
