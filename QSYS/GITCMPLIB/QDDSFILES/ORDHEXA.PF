     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDHEXA (PF)
     A*  Description ... ORDH extra fields
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    10/19/18  DAS  7.20
     A*      - Created
     A*
730aAA*    01/22/19  DAS  7.30a
     A*      - Added OHAAUDTS.
     A*
730bAA*    03/24/19  DAS  7.30b
     A*      - Added OHAORDA, OHACUSTA.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file should only be touched using SQL so new fields
     A*   can be added without needing to recompile existing programs.
     A*
     A*   Ideally, RPG programs should only access/update this
     A*   file using program EXORDH.
     A*
     A*   SQL Insert and Update statements should work with
     A*   individual fields to prevent a level check error.
     A*
     A*   MRC programs should not be affected by new fields because
     A*   they use SQL.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Field Notes
     A*
     A*   OHAAUDFLG - Audit Flag (Y,N)
     A*      Originally created for City Line because they wanted to
     A*      audit the orders for specific customers.
     A*
     A*   OHAAUDID  - Audit Id
     A*      This is a random number unique within route
     A*
     A*   OHAAUDCMP - Audit Completion Flag (Y,N)
     A*
730bAA*   OHAORDA - Alpha order number
     A*      - All new programs should use this field for displaying
     A*        the order number. If blank, use OHCORD.
     A*      - Old reports/screens should be converted when requested.
     A*
730bAA*   OHACUSTA - Alpha customer number
     A*      - All new programs should use this field for displaying
     A*        the customer number. If blank, use OHCUST.
     A*      - Old reports/screens should be converted when requested.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R OHAREC
     A*
     A            OHAWHSE   R               REFFLD(WHCODE)
     A            OHAORD    R               REFFLD(OHORD)
     A            OHAAUDFLG      1          TEXT('Audit Flag')
     A                                      COLHDG('Audit' 'Flag')
     A            OHAAUDID       5  0       TEXT('Audit Id')
     A                                      COLHDG('Audit' 'Id')
     A            OHAAUDCMP      1          TEXT('Audit Completed Flag')
     A                                      COLHDG('Audit' 'Completed Flag')
     A            OHAAUDBY       5  0       TEXT('Audit Completed By Emp#')
     A                                      COLHDG('Audit' 'Completed By Emp#')
730aAA            OHAAUDTS        Z         TEXT('Audit Completed Stamp')
730aAA                                      COLHDG('Audit' 'Completed Stamp')
730bAA            OHAORDA       15          TEXT('Alpha Order Number')
730bAA                                      COLHDG('Alpha Order' 'Number')
730bAA            OHACUSTA      30          TEXT('Alpha Cust Number')
730bAA                                      COLHDG('Alpha Cust' 'Number')
     A*
     A          K OHAWHSE
     A          K OHAORD
