     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMADJ  (PF)
     A*  Description ... Item quantity adjustment file.
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    Ver 2.00  08/30/94  DAS
     A*      - Added warehouse code and split quantity.
     A*
213 AA*    Ver 2.01  05/13/96  DAS
213 AA*      - Added fields IAWHDP, IADISP and IACWTA.
411 AA*    4.11  05/13/99  DAS
     A*      - Change key to not unique.
     A*        Was causing error in exception labels.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
411 DA*                                     UNIQUE
     A          R IAREC
     A*
     A            IAWHSE    R               REFFLD(WHCODE)
     A            IAITEM    R               REFFLD(ITITEM)
     A            IADATE    R               TEXT('Adjustment Date')
     A                                      COLHDG('Adjusted' 'Date')
     A                                      REFFLD(DTECMD)
     A            IATIME    R               TEXT('Adjustment Time')
     A                                      COLHDG('Adjusted' 'Time')
     A                                      REFFLD(TIMHMS)
     A            IABY          10          TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
213 AA            IAWHDP    R               REFFLD(WDCODE)
213 AA            IADISP        12A         TEXT('Slot# Display')
213 AA                                      COLHDG('Slot#' 'Display')
213 AA            IACWTA         9P 2       TEXT('Catchweight Adjustment')
213 AA                                      COLHDG('Catchweight' 'Adjustment')
     A            IAQTY1         5P 0       TEXT('Quantity 1')
     A                                      COLHDG('Quantity 1')
     A            IAQTY2         5P 0       TEXT('Quantity 2')
     A                                      COLHDG('Quantity 2')
     A            IAQTY3         5P 0       TEXT('Quantity 3')
     A                                      COLHDG('Quantity 3')
     A            IAUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            IAUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
     A            IACODE    R               REFFLD(ADCODE)
     A            IAMEMO        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A*
     A          K IAWHSE
     A          K IAITEM
     A          K IADATE
     A          K IATIME
