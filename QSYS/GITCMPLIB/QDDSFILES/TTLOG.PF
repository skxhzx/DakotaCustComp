     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTLOG  (PF)
     A*  Description ... TRAX log file
     A*  Creator ....... RBD
     A*
     A*  Revisions
     A*
530 AA*    06/04/10  RBD  5.30
     A*      - Created.
     A*
640 AA*    02/02/12  RBD  6.40
     A*      - Added status 8, did not recompile.
     A*        When a route has been downloaded or uploaded and
     A*        it needs to be downloaded again, the status will
     A*        be changed from 3 or 4 to 8.
     A*
650 AA*    08/26/13  RBD  6.50
     A*      - Added serial# of handheld which prompted the log record.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Status Codes
     A*
     A*    1 = Start file build
     A*    2 = End file build
     A*    3 = Downloaded
     A*    4 = Uploaded
640 AA*    8 = Downloaded or uploaded status changed to 8 so
640 AA*        it can be downloaded again.
     A*    9 = Error
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A*                                     UNIQUE
     A          R TTLREC
     A*
     A            TTLWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            TTLRTID   R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            TTLTRY         2S 0       TEXT('Try')
     A                                      COLHDG('Try')
     A            TTLRTE    R               REFFLD(ROUTE)
     A            TTLSTATUS      1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            TTLFILESND    30A         TEXT('Send File Name')
     A                                      COLHDG('Send File Name')
     A            TTLERRCODE    10A         TEXT('Error Code')
     A                                      COLHDG('Error Code')
     A            TTLERRMSG     60A         TEXT('Error Message')
     A                                      COLHDG('Error Message')
     A*
     A* Timestamp / User tracking information.
     A*
     A            TTLTS1    R               REFFLD(ADDTS)
     A                                      COLHDG('File Start TS')
     A            TTLTS2    R               REFFLD(ADDTS)
     A                                      COLHDG('File End TS')
     A            TTLPGM1   R               REFFLD(ADDPGM)
     A                                      COLHDG('File Start' 'Actual Pgm')
     A            TTLUSER1  R               REFFLD(ADDUSR)
     A                                      COLHDG('File Start' 'User')
     A            TTLJOB1   R               REFFLD(ADDJOB)
     A                                      COLHDG('File Start' 'Job')
     A            TTLNBR1   R               REFFLD(ADDNBR)
     A                                      COLHDG('File Start' 'Job#')
     A*
     A            TTLTS3    R               REFFLD(ADDTS)
     A                                      COLHDG('Download TS')
     A            TTLPGM3   R               REFFLD(ADDPGM)
     A                                      COLHDG('Download' 'Actual Pgm')
     A            TTLUSER3  R               REFFLD(ADDUSR)
     A                                      COLHDG('Download' 'User')
     A            TTLJOB3   R               REFFLD(ADDJOB)
     A                                      COLHDG('Download' 'Job')
     A            TTLNBR3   R               REFFLD(ADDNBR)
     A                                      COLHDG('Download' 'Job#')
     A*
     A            TTLTS9    R               REFFLD(ADDTS)
     A                                      COLHDG('Stat Upd TS')
650 AA            TTLSER        30          TEXT('Serial Number')
650 AA                                      COLHDG('Serial' 'Number')
     A*
     A          K TTLWHSE
     A          K TTLRTID
     A          K TTLTRY
