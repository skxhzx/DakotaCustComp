     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... RTEUNDO (PF)
     A*  Description ... Route undo history
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
416 AA*   09/11/01  DAS  4.16
     A*     - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Field notes
     A*
     A*    Label/Tran number (RUNUM)
     A*
     A*      Can contain label # or tran # depending on value in
     A*      field RUFILE.
     A*
     A*    File (RUFILE) codes
     A*
     A*      'L'  -  LABEL file   (RUNUM contains label #)
     A*      'T'  -  PIRTRAN file (RUNUM contains tran #)
     A*      'W'  -  WORKLBL file (RUNUM contains label #)
     A*
     A*    Status (RUSTAT,RUSDTE,RUSTIM)
     A*
     A*      Contains the status fields of the label or tran file
     A*      record before being updated by the undo program.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A          R RUREC
     A*
     A            RUWHSE    R               REFFLD(WHCODE)
     A            RURTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            RUNEWR    R               REFFLD(ROUTE)
     A                                      TEXT('New Route ID')
     A                                      COLHDG('New' 'Route Id')
     A            RUDATE    R               TEXT('Date')
     A                                      COLHDG('Date')
     A                                      REFFLD(DTECMD)
     A            RUTIME    R               TEXT('Time')
     A                                      COLHDG('Time')
     A                                      REFFLD(TIMHMS)
     A            RUFILE         1A         TEXT('File')
     A                                      COLHDG('File')
     A            RUNUM     R               TEXT('Label/Tran Number')
     A                                      COLHDG('Label/Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            RUQPCK         5P 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            RUSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            RUSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            RUSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K RUWHSE
     A          K RURTID
     A          K RUDATE
     A          K RUTIME
