     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSPOH (PF)
     A*  Description ... Staging Export - P.O. Header
     A*
     A*  Revisions
     A*
640 AA*    02/14/11  DAS  6.40
     A*      - Created
     A*      - Base on EXPFPOH
640aAA*    10/10/12  MLB  6.40a
     A*      - Enh: Revised file to add Misc. fields 1 - 5.
     A*
     A*----------------------------------------------------------------
     A          R PEHREC
     A*
     A            PEHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PEHBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            PEHPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            PEHSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            PEHSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PEHSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PEHTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            PEHVEN        11S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
     A            PEHVNM        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            PEHAD1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            PEHAD2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            PEHCTY        20          TEXT('City')
     A                                      COLHDG('City')
     A            PEHST          2          TEXT('State')
     A                                      COLHDG('State')
     A            PEHZP1        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            PEHDTE         6S 0       TEXT('Delivery Date')
     A                                      COLHDG('Delivery' 'Date')
     A            PEHTIM         4S 0       TEXT('Delivery Time')
     A                                      COLHDG('Delivery' 'Time')
     A            PEHAMP         1          TEXT('Delivery Am/Pm')
     A                                      COLHDG('Delivery' 'Am/Pm')
     A*
     A*  Add stamps
     A*
     A            PEHADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            PEHADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
640aAA*
640aAA            PEHMM1        40A         TEXT('Host Misc Field 1')
640aAA                                      COLHDG('Host' 'Misc' 'Field 1')
640aAA            PEHMM2        40A         TEXT('Host Misc Field 2')
640aAA                                      COLHDG('Host' 'Misc' 'Field 2')
640aAA            PEHMM3        40A         TEXT('Host Misc Field 3')
640aAA                                      COLHDG('Host' 'Misc' 'Field 3')
640aAA            PEHMM4        40A         TEXT('Host Misc Field 4')
640aAA                                      COLHDG('Host' 'Misc' 'Field 4')
640aAA            PEHMM5        40A         TEXT('Host Misc Field 5')
640aAA                                      COLHDG('Host' 'Misc' 'Field 5')
     A*
     A          K PEHWHS
     A          K PEHBATID
     A          K PEHPO
