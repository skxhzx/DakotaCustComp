     A*----------------------------------------------------------------
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... DLYQTYT (PF)
     A*  Description ... Daily quantity time file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
415 AA*    06/22/01  DAS  41.5
     A*      - Created.
     A*
     A*----------------------------------------------------------------***
     A* Notes
     A*
     A*   Run Date/Time    -  Run date/time when record created.
     A*                       Corresponds to Run Date/Time in DLYQTY.
     A*   Start Date/Time  -  Date/Time when calculations started.
     A*                       Usually day after Run date.
     A*   End Date/Time    -  Date/Time when calculation finished.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R DTREC
     A*
     A* Run Date/Time
     A*
     A            DTRDTE    R               TEXT('Run Date')
     A                                      COLHDG('Run' 'Date')
     A                                      REFFLD(DTECMD)
     A            DTRTIM    R               TEXT('Run Time')
     A                                      COLHDG('Run' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A* Start Date/Time
     A*
     A            DTSDTE    R               TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A                                      REFFLD(DTECMD)
     A            DTSTIM    R               TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A* End Date/Time
     A*
     A            DTEDTE    R               TEXT('End Date')
     A                                      COLHDG('End' 'Date')
     A                                      REFFLD(DTECMD)
     A            DTETIM    R               TEXT('End Time')
     A                                      COLHDG('End' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A* Elapsed Minutes
     A*
     A            DTMIN          5P 2       TEXT('Elapsed Minutes')
     A                                      COLHDG('Elapsed' 'Minutes')
     A*
     A* Record Counts
     A*
     A*   - Only includes closed records.
     A*      - Otherwise, record could be counted twice if it was
     A*        open when count was done.
     A*      - Open records will skew a daily average because they
     A*        will be credited to the count of the day they were
     A*        closed instead of the day they were created.
     A*   - Replenishment count includes replenishments and letdowns.
     A*
     A            DTRECA         5P 0       TEXT('Adj Recs')
     A                                      COLHDG('Adj' 'Recs')
     A            DTRECR         5P 0       TEXT('Rcv Recs')
     A                                      COLHDG('Rcv' 'Recs')
     A            DTRECN         5P 0       TEXT('Rtn Recs')
     A                                      COLHDG('Rtn' 'Recs')
     A            DTRECP         5P 0       TEXT('Pck Recs')
     A                                      COLHDG('Pck' 'Recs')
     A            DTRECT         5P 0       TEXT('Tfr Recs')
     A                                      COLHDG('Tfr' 'Recs')
     A            DTRECL         5P 0       TEXT('Let Recs')
     A                                      COLHDG('Let' 'Recs')
     A            DTRECI         5P 0       TEXT('Inv Recs')
     A                                      COLHDG('Inv' 'Recs')
     A            DTREC@         5P 0       TEXT('Other Recs')
     A                                      COLHDG('Other' 'Recs')
     A            DTTTLR         7P 0       TEXT('Total Recs')
     A                                      COLHDG('Total' 'Recs')
     A*
     A          K DTRDTE
     A          K DTRTIM
