     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDLBL5 (LF)
     A*  Description ... Order Label by whse/rte/huow
     A*  Date .......... November 21, 2011
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*
     A          R ALREC                     PFILE(ORDLBL)
     A*
     A            ALWHSE
     A            ALRTE
     A            ALORD          7P 0       TEXT('Order Number')
     A            ALSEQ          5P 0       TEXT('Order Sequence Number')
     A            ALITEM        15          TEXT('Item')
     A            ALHTR#         7P 0       TEXT('Host Transaction Number')
     A            ALTRN#         7P 0       TEXT('Dakota Transaction Number')
     A            ALLBL#         7P 0       TEXT('Dakota Label Number')
     A            ALLSQ#         9P 0       TEXT('Label Sequence Number')
     A            ALPSQ#         5P 0       TEXT('Picking Sequence Number')
     A            ALPAL#         7P 0       TEXT('Pallet Number')
     A            ALDISP        12          TEXT('Slot# Display')
     A            ALAISL         3          TEXT('Aisle')
     A            ALLOC          3S 0       TEXT('Location')
     A            ALRLVL         2S 0       TEXT('Rack Level')
     A            ALHAND         2          TEXT('Handstack')
     A            ALQPK1         5P 0       TEXT('Normal Qty Picked')
     A            ALQPK2         5P 0       TEXT('BrkDown 1 Qty Picked')
     A            ALQPK3         5P 0       TEXT('BrkDown 2 Qty Picked')
     A            ALCOMP         1          TEXT('Truck Compartment')
     A            ALROW          2S 0       TEXT('Truck Row Position')
     A            ALCOL          1S 0       TEXT('Truck Column Position')
     A            ALLSEQ         2S 0       TEXT('Truck Load/Unload Sequence')
     A            ALHTRN        30          TEXT('Host Tracking Number')
     A*
     A            ALHUOW         9A  I      SST(ALHTRN 1 9)
     A                                      TEXT('Host Unit of Work Nbr')
     A            ALHCID         9A  I      SST(ALHTRN 10 9)
     A                                      TEXT('Host Carton ID Nbr')
     A            ALHLBL         3A  I      SST(ALHTRN 19 3)
     A                                      TEXT('Host Label Nbr')
     A            ALHBO          3A  I      SST(ALHTRN 22 3)
     A                                      TEXT('Host Back Order Nbr')
     A            ALHFL1        30          TEXT('Host Field 1')
     A            ALHFL2        30          TEXT('Host Field 2')
     A            ALHFL3        30          TEXT('Host Field 3')
     A            ALHFL4        30          TEXT('Host Field 4')
     A            ALHFL5        30          TEXT('Host Field 5')
     A*
     A*  Key fields
     A*
     A          K ALWHSE
     A          K ALHUOW
     A          K ALRTE
