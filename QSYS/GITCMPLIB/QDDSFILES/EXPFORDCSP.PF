     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFORDCSP  (PF)
     A*  Description ... FTP Cust Order CWgt export file for Stanpack.
     A*  Date .......... November 1, 2007
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file should be identical to EXPFORDC except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFORDCSP.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
510 AA*    11/01/07  MLB  5.10
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*  This file was created for use with exports to clients using
     A*  Stanpack software. Stanpack software requires files to be of
     A*  the follwing lengths: 50, 84, 94, 108, 126, 151, 189, 252. We
     A*  have added filler at the end of the record to pad the file
     A*  to the required length. iSeries FTP adds OA hex at the end
     A*  of each line so the rule will be to pad the file to the
     A*  required length minus 1.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
413  A*                                     UNIQUE
     A          R OECREC
     A*
     A            OECWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OECORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OECSQ#         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OECITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            OECSEQ         3S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            OECWGT         7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
409 AA            OECTRN         7S 0
409 AA                                      TEXT('Transaction Number')
409 AA                                      COLHDG('Transaction' 'Number')
     A            OECF01         2A         TEXT('Filler Field 1')
     A                                      COLHDG('Filler' 'Field 1')
     A                                      DFT('..')
     A*
     A          K OECWHS
     A          K OECORD
     A          K OECSQ#
     A          K OECITM
409 AA          K OECTRN
     A          K OECSEQ
