     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSORDU (PF)
     A*  Description ... Staging Export - Order UCIs
     A*
     A*  Revisions
     A*
700 AA*    05/11/16  DAS  7.00
     A*      - Created
     A*
720a A*    08/31/18  NLK  7.20a
     A*      - Added Transaction, Selector ID, and Selector Name
     A*        Originally for CBI 2018134
720b A*    02/12/19  NLK  7.20b
     A*      - Change Transaction field name to oeutr and rmv #.
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Date AI Codes
     A*
     A*    Code  Name  Description
     A*    ----  ----  -----------
     A*     11   PROD  Production date (YYMMDD)
     A*     12   DUE   Due date (YYMMDD)
     A*     13   PACK  Packaging date (YYMMDD)
     A*     15   BEST  Best before date (YYMMDD)
     A*     16   SELL  Sell by date (YYMMDD)
     A*     17   EXP   Expiration date (YYMMDD)
     A*
     A*----------------------------------------------------------------
     A          R OEUREC
     A*
     A            OEUWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OEUBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            OEURTE         5          TEXT('Route')
     A                                      COLHDG('Route')
730A  *           OEUORD         7S 0       TEXT('Order Number')
730A A            OEUORD        15          TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEUOSQ         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OEUITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OEULBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            OEUUCISEQ      5S 0       TEXT('UCI Sequence Number')
     A                                      COLHDG('UCI Sequence' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            OEUSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            OEUSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OEUSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OEUTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            OEURTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            OEUORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            OEUGTIN       14          TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            OEUDATE        6S 0       TEXT('Date YYMMDD')
     A                                      COLHDG('Date YYMMDD')
     A            OEUDATECOD     2          TEXT('Date AI Code')
     A                                      COLHDG('Date' 'AI Code')
     A            OEUDATENAM     4          TEXT('Date AI Name')
     A                                      COLHDG('Date' 'AI Name')
     A            OEULOT        30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            OEUSERIAL     30          TEXT('Serial Nunber')
     A                                      COLHDG('Serial Number')
     A            OEUTRAX       10          TEXT('Trax Number')
     A                                      COLHDG('Trax' 'Number')
     A            OEULCNS       30          TEXT('License')
     A                                      COLHDG('License')
     A            OEUUCI       100          TEXT('UCI')
     A                                      COLHDG('UCI')
     A*
     A*  Add stamps
     A*
     A            OEUADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OEUADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
720aD *           OEUTR#         7S 0       TEXT('Transaction Number')
720bMA            OEUTR          7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
720a A            OEUEMPL        5S 0       TEXT('Selector #')
     A                                      COLHDG('Selector #')
720a A            OEUNAME       25A         TEXT('Selector Name')
     A                                      COLHDG('Selector' 'Name')
     A*
     A          K OEUWHS
     A          K OEUBATID
     A          K OEURTEID
     A          K OEUORD
     A          K OEUOSQ
     A          K OEUITM
     A          K OEULBL
     A          K OEUUCISEQ
