     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SUM004EXA (PF)
     A*  Description ... SUM004 extra fields
     A*  Creator ....... RBD
     A*
     A*  Revisions
     A*
     A*    02/07/20  RBD  7.40
     A*      - Created
     A*
740aAA*    05/22/20  RBD  7.40a
     A*      - Added label number. Had already created sum004exa to
     A*        hold other new fields but found that because sum004
     A*        didn't have unique records mrc R16700 which joined sum004
     A*        and sum004exa was causing records to be duplicated in
     A*        the report. So now adding label to sum004 and sum004exa
     A*        to use in join. Discussed with Dave and he agreed.
     A*      - Added date type entered from licinfo.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file should only be touched using SQL so new fields
     A*   can be added without needing to recompile existing programs.
     A*
     A*   Ideally, RPG programs should only access/update this
     A*   file using program EXSUM004.
     A*
     A*   SQL Insert and Update statements should work with
     A*   individual fields to prevent a level check error.
     A*
     A*   MRC programs should not be affected by new fields because
     A*   they use SQL.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Field Notes
     A*
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R SMAREC
     A*
     A            SMAWHSE   R               REFFLD(WHCODE)
     A            SMARPTID       7S 0       TEXT('Report Id')
     A                                      COLHDG('Report' 'Id')
     A            SMALCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            SMADLVDAT      8S 0       TEXT('Delivery Date')
     A                                      COLHDG('Delivery' 'Date')
     A            SMATRAN   R               REFFLD(TRAN#)
740aAA            SMALBL    R               TEXT('Label Number')
740aAA                                      COLHDG('Label' 'Number')
740aAA                                      REFFLD(TRAN#)
740aAA            SMADTETYP      8          TEXT('Date Type Entered')
740aAA                                      COLHDG('Date Type' 'Entered')
     A*
     A          K SMAWHSE
     A          K SMARPTID
     A          K SMALCNS
