     A*----------------------------------------------------------------
     A*  Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*  BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMADJL (PF)
     A*  Description ... Inventory Adjustment License file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
650 AA*    03/04/13  DAS  6.50
     A*      - Created.
650aAA*    04/20/13  RTR  6.50a
     A*      - Added PO number/sequence
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*  - An ITEMADJ record does not contain a unique key field.
     A*    Therefore, we use a number of fields from ITEMADJ the should
     A*    end up being unique.
     A*
     A*  - The records in this file are created after-the-fact by looking
     A*    for LICLOG records that match the ITEMADJ information. The
     A*    reason for this is because LICLOG records are created as the
     A*    license is being touched (i.e. when a qty goes into a USR slot),
     A*    where ITEMADJ records are created at the end of a process.
     A*
     A*  - When an item has a corresponding SSB item, any breakdown 1
     A*    adjustment to the parent item will write an ITEMADJ record
     A*    for the SSB item. However, the LICLOG record will be written
     A*    with the parent item. This record would have the SSB item.
     A*
     A*  - A single adjustment can touch multiple license plates, so
     A*    this file could have multiple records that point to a single
     A*    ITEMADJ record. The quantities in this file, should be the
     A*    quantity the license was adjusted. The total quantity of
     A*    the records should be the ITEMADJ quantity.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R IALREC
     A*
     A            IALWHSE   R               REFFLD(WHCODE)
     A            IALITEM   R               REFFLD(ITITEM)
     A            IALDATE   R               TEXT('Adjustment Date (YYYYMMDD)')
     A                                      COLHDG('Adjustment' 'Date')
     A                                      REFFLD(DTECMD)
     A            IALTIME   R               TEXT('Adjustment Time')
     A                                      COLHDG('Adjustment' 'Time')
     A                                      REFFLD(TIMHMS)
     A            IALBY     R               TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
     A                                      REFFLD(USER)
     A            IALWHDP   R               REFFLD(WDCODE)
     A            IALDISP       12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            IALLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            IALOLCNS      15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            IALQTY1        5P 0       TEXT('Quantity 1')
     A                                      COLHDG('Quantity 1')
     A            IALQTY2        5P 0       TEXT('Quantity 2')
     A                                      COLHDG('Quantity 2')
     A            IALQTY3        5P 0       TEXT('Quantity 3')
     A                                      COLHDG('Quantity 3')
     A            IALUDTE   R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            IALUTIM   R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
650aAA            IALPO     R               REFFLD(PONUM)
650aAA            IALSEQ         5S 0       TEXT('Sequence number')
650aAA                                      COLHDG('Sequence' 'Number')
     A*
     A          K IALWHSE
     A          K IALITEM
     A          K IALDATE
     A          K IALTIME
     A          K IALBY
     A          K IALWHDP
     A          K IALDISP
     A          K IALLCNS
