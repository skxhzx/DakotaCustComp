     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... RUNCPY (PF)
     A*  Description ... RF Shorts for Runners
     A*  Date .......... July 15, 2004
     A*  Creator ....... JGD
     A*
     A*  Note: This file is a mirror image of 'Runner' file. The
     A*        reason for this is to break down 'datestamp' field
     A*        'RUODTS' into 2 fields 'date' and 'time' to be used
     A*        by 'RU370CL' (Cleared Flags report).
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*
     A*  Client Custom Revisions
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes (RUSTAT)
     A*
     A*       'O'  -  Open
     A*       'P'  -  Printed
     A*       'C'  -  Closed
     A*       'D'  -  Deleted
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R RUREC
     A*
     A            RUWHSE    R               REFFLD(WHCODE PIRDICT)
     A            RUTYPE         1A         TEXT('Runner Flag Type')
     A                                      COLHDG('Runner' 'Flag Type')
     A            RUWHDP    R               TEXT('Warehouse Department')
     A                                      COLHDG('Department')
     A                                      REFFLD(LBWHDP LABEL)
     A            RULBL#    R     P         TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            RUOTRN    R     P         TEXT('Original Transaction Number')
     A                                      COLHDG('Original' 'Transaction' 'Nu-
     A                                      mber')
     A                                      REFFLD(TRAN#)
     A            RUTRN#    R     P         TEXT('Runner Transaction Number')
     A                                      COLHDG('Runner' 'Transaction' 'Numb-
     A                                      er')
     A                                      REFFLD(TRAN#)
     A            RUDISP    R               TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A                                      REFFLD(LBDISP LABEL)
     A            RUQYSO    R     P         TEXT('Quantity Short/Out')
     A                                      COLHDG('Qty' 'Short/Out')
     A                                      REFFLD(LBQPCK LABEL)
     A            RUQYFD    R     P         TEXT('Quantity Found')
     A                                      COLHDG('Qty' 'Found')
     A                                      REFFLD(LBQPCK LABEL)
     A            RUSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
      * RUODTS broken into next 3 fields
      *
     A            RUODTD         8A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUODTT         6A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUODTX        12A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUPDTS          Z         TEXT('Printed Status Date Stamp')
     A                                      COLHDG('Print Status' 'Date Stamp')
     A            RUCDTS          Z         TEXT('Closed Status Date Stamp')
     A                                      COLHDG('Closed Status' 'Date Stamp')
     A*
     A          K RULBL#
