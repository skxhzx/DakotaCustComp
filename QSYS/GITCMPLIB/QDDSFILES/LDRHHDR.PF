     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LDRHHDR  (PF)
     A*  Description ... Holds truck loading header history info.
     A*  Creator ....... JGD
     A*                                                               *
     A*
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
700 AA*  11/21/15  MLB  7.00
     A*    - Added header in Notes section. No compilation needed.
     A*
     F*----------------------------------------------------------------
     F*  Client Custom Revisions
     F*
     F*    ##/##/##  XXX-XXX
     F*
     A*                                                               *
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
700 AA*        LHHSTA (Status)
     A*
     A*       '1'  -  Pending (Open)
     A*       '2'  -  Started (Active)
     A*       '3'  -  Ended   (Finished)
     A*       '4'  -  Closed  (Completed)
     A*       '5'  -  Unload  (Changed trailer)
     A*       '6'  -  Deleted
     A*       '7'  -  No Loaded thru loading system
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R LHHREC
     A*
     A            LHHWHS    R               REFFLD(WHCODE)
     A            LHHRTI    R               REFFLD(ROUTE)
     A                                      TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            LHHRTE    R               REFFLD(ROUTE)
     A            LHHTRL         8          TEXT('Trailer Id')
     A                                      COLHDG('Trailer' 'Id')
     A            LHHSTD    R               TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A                                      REFFLD(DTECMD)
     A            LHHSTT    R               TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
     A                                      REFFLD(TIMHMS)
     A            LHHEMP         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            LHHEND    R               TEXT('End Date')
     A                                      COLHDG('End Date')
     A                                      REFFLD(DTECMD)
     A            LHHENT    R               TEXT('End Time')
     A                                      COLHDG('End Time')
     A                                      REFFLD(TIMHMS)
     A            LHHSTA         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            LHHCLD    R               TEXT('Close Date')
     A                                      COLHDG('Close Date')
     A                                      REFFLD(DTECMD)
     A            LHHCLT    R               TEXT('Close Time')
     A                                      COLHDG('Close Time')
     A                                      REFFLD(TIMHMS)
     A            LHHSUP    R               TEXT('Supervisor Empl #')
     A                                      COLHDG('Supervisor' 'Empl #')
     A                                      REFFLD(USEMP# PIRUSER)
     A            LHHDOR         3          TEXT('Loading Door #')
     A                                      COLHDG('Loading' 'Door #')
     A*
     A          K LHHWHS
     A          K LHHRTE
     A          K LHHRTI
     A          K LHHSTD
     A          K LHHSTT
