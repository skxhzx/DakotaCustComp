     A*----------------------------------------------------------------
     A*   Copyright (C) 2017 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSORDCNS  (PF)
     A*  Description ... Staging Export - NetSuite Order C/W
     A*
     A*  Revisions
     A*
710  A*    01/16/15  RTR  7.10
     A*      - Created based on EXPSORDCNS.
     A*
     A*----------------------------------------------------------------
     A          R NECREC
     A*
     A            NECWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            NECBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            NECRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            NECORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            NECOSQ         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            NECITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            NECSEQ         3S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            NECSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            NECSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            NECSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            NECTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            NECRTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            NECORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            NECWGT         7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            NECLBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            NECTRN         7S 0
     A                                      TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A*
     A*  Add stamps
     A*
     A            NECADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            NECADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K NECWHS
     A          K NECBATID
     A          K NECRTEID
     A          K NECORD
     A          K NECSEQ
     A          K NECITM
     A          K NECLBL
