     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... BFCEXCP  (PF)
     A*  Description ... BFC Exception Tracking File
     A*  Creator ....... RTR
     A*
     A*  Revisions
     A*
     A*    01/15/15  RTR  6.50
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*  This file is to be used as a central exception logging method.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R EXCPREC
     A*
     A            EXCWHSE        3S 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            EXCUNIQ       11P 0       TEXT('Unique Key')
     A                                      COLHDG('Unique Key')
     A            EXCAPPL       15A         TEXT('Application')
     A                                      COLHDG('Application')
     A            EXCCAT        15A         TEXT('Category')
     A                                      COLHDG('Category')
     A            EXCSEV        10A         TEXT('Severity')
     A                                      COLHDG('Severity')
     A            EXCSTAT        2A         TEXT('Status')
     A                                      COLHDG('Status')
     A            EXCMSG1      100A         TEXT('Message 1')
     A                                      COLHDG('Message 1')
     A            EXCMSG2      100A         TEXT('Message 2')
     A                                      COLHDG('Message 2')
     A            EXCDATA1      40A         TEXT('Data 1')
     A                                      COLHDG('Data 1')
     A            EXCDATA2      40A         TEXT('Data 2')
     A                                      COLHDG('Data 2')
     A            EXCDATA3      40A         TEXT('Data 3')
     A                                      COLHDG('Data 3')
     A            EXCVAL1       10S 5       TEXT('Value 1')
     A                                      COLHDG('Value 1')
     A            EXCVAL2       10S 5       TEXT('Value 2')
     A                                      COLHDG('Value 2')
     A            EXCVAL3       10S 5       TEXT('Value 3')
     A                                      COLHDG('Value 3')
     A*
     A            EXCGENTS  R               REFFLD(ADDTS)
     A                                      TEXT('Generated TS')
     A                                      COLHDG('Generated TS')
     A            EXCGENUSR R               REFFLD(ADDUSR)
     A                                      TEXT('Generated User')
     A                                      COLHDG('Generated' 'User')
     A            EXCGENPGM R               REFFLD(ADDPGM)
     A                                      TEXT('Generated Pgm')
     A                                      COLHDG('Generated' 'Pgm')
     A*
     A            EXCCORTS  R               REFFLD(ADDTS)
     A                                      TEXT('Corrected TS')
     A                                      COLHDG('Corrected TS')
     A            EXCCORUSR R               REFFLD(ADDUSR)
     A                                      TEXT('Corrected User')
     A                                      COLHDG('Corrected' 'User')
     A            EXCCORPGM R               REFFLD(ADDPGM)
     A                                      TEXT('Corrected Pgm')
     A                                      COLHDG('Corrected' 'Pgm')
     A*
     A            EXCNOTTS  R               REFFLD(ADDTS)
     A                                      TEXT('Notified TS')
     A                                      COLHDG('Notified TS')
     A            EXCNOTTYP     10A
     A                                      TEXT('Notify Type')
     A                                      COLHDG('Notify' 'Type')
     A            EXCNOTKEY     11P 0
     A                                      TEXT('Notify Key')
     A                                      COLHDG('Notify' 'Key')
     A*
     A          K EXCWHSE
     A          K EXCUNIQ
     A*
