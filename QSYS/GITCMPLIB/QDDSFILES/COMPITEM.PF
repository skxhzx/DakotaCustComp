     A*----------------------------------------------------------------
     A*   Copyright (C) 2024 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... COMPITEM   (PF)
     A*  Description ... Compare - Item info
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
780 AI*    02/24/24  DAS  7.80
     I*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*   - This file was created to do snapshot comparisons of the
     A*     Dakota item data
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R CPIREC
     A*
     A            CPIWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            CPIBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch Id')
     A*
     A* LICACTIVE fields
     A*
     A            CPIITEM   R               REFFLD(ITITEM)
     A            CPITYPE        1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            CPIDESC       30          TEXT('Description')
     A                                      COLHDG('Description')
     A            CPIWHDP   R               REFFLD(WDCODE)
     A            CPIUM1    R               TEXT('Normal UM')
     A                                      COLHDG('Normal' 'UM')
     A                                      REFFLD(UMCODE)
     A            CPIUM2    R               TEXT('Breakdown 1 UM')
     A                                      COLHDG('Break 1' 'UM')
     A                                      REFFLD(UMCODE)
     A            CPIUMQ2        3P 0       TEXT('Breakdown 1 UM Qty')
     A                                      COLHDG('Breakdown 1' 'UM Qty')
     A            CPIFLG1        1          TEXT('Allow Brk1 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk1 Sale')
     A            CPIUM3    R               TEXT('Breakdown 2 UM')
     A                                      COLHDG('Break 2' 'UM')
     A                                      REFFLD(UMCODE)
     A            CPIUMQ3        3P 0       TEXT('Breakdown 2 UM Qty')
     A                                      COLHDG('Breakdown 2' 'UM Qty')
     A            CPIFLG2        1          TEXT('Allow Brk2 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk2 Sale')
     A            CPISITEM  R               TEXT('Slot Item')
     A                                      COLHDG('Slot Item')
     A                                      REFFLD(ITITEM)
     A            CPISITYPE      1A         TEXT('Slot Item Type')
     A                                      COLHDG('Slot' 'Item Type')
     A*
     A*  Add stamps
     A*
     A            CPIADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            CPIADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A            CPIADDPGM R               REFFLD(ADDPGM)
     A            CPIADDUSR R               REFFLD(ADDUSR)
     A            CPIADDJOB R               REFFLD(ADDJOB)
     A            CPIADDNBR R               REFFLD(ADDNBR)
     A*
     A* Key
     A*
     A          K CPIWHSE
     A          K CPIBATID
     A          K CPIITEM
