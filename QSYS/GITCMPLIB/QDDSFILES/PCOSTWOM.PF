     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PCOSTWOM
     A*  Description ... PRODUCTION COST SHEET - MATERIAL
     A*  Creator ....... KDE
     A*
     A*  --------------------------------------------------------------
     A*   SPECIAL NOTES:
     A*
     A*     FIELDS WHERE TEXT BEGINS WITH FACTOR ARE IN RECORD TO ASIST
     A*     IN CALCULATING ASSIGNED COST.
     A*
     A*     MATERIAL COST CONSIST OF BOTH USAGE AND WASTE.  THERE ARE
     A*     THREE POTENTIAL COSTS A CUSTOMER CAN POPULATE.  THESE COSTS
     A*     LAST PO, AVERAGE COST AND STANDARD COST.  NOTE THAT THIS
     A*     RECORD HAS UNITS COSTS FOR EACH ASSUMING THE CUSTOMER IS
     A*     POPULATING.  EACH COST WILL BE USED RESPECTIVELY TO CALCULATE
     A*     A COST USED.  WASTE IS ONLY DETERMINED AT END OF DAY IN TOTAL
     A*     FOR GIVEN RAW MATERIAL.  WE WILL DETERMINE WHAT PERCENTAGE
     A*     OF WASTE TO APPLY TO THIS WORK ORDER BY FIRST DIVIDING THE
     A*     TOTAL USAGE FOR THIS WORK ORDER BY THE TOTAL USAGE FOR THE
     A*     RAW MATERIAL FOR THE FULL DAY.  THAT FACTOR WILL BE MULTIPLIED
     A*     BY THE TOTAL WASTE FOR THE DAY AND THEN THAT DERIVED QUANTITY
     A*     WILL BE MULTIPLIED BY EACH OF THE COST FACTORS TO DETERMINE
     A*     EACH WASTE COST FOR THE DAY.  BASED ON A PRODUCTION SETTING
     A*     THE COST ASSIGNED FILED WILL BE POPULATED WITH THE ADDITION
     A*     OF THE RESPECTIVE USAGE AND WASTE COSTS THAT RELATE TO EITHER
     A*     LAST PO, AVERAGE OR STANDARD COST PER THE SETTING. THIS FACTOR
     A*     AT THAT POINT IN TIME CAN BE SEEN IN FIELD CSFACUSED.
     A*
     A*  Revisions
     A*
710  A*    Ver 7.10  04/20/17  KDE
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A          R CSMREC
     A            CSMWHSE        3P 0       COLHDG('WAREHOUSE')
     A            CSMPRDDATE     8P 0       COLHDG('PROD DATE')
     A            CSMLINE        3P 0       COLHDG('MFG LINE')
     A            CSMWO         13          TEXT('WORK ORDER')
     A                                      COLHDG('WORK ORDER')
     A            CSMTYPE        1          TEXT('COST TYPE - M')
     A                                      COLHDG('CST TYPE')
     A            CSMFGITM      15          TEXT('FG ITEM')
     A                                      COLHDG('FG ITEM')
     A            CSMRMITM      15          TEXT('RM ITEM')
     A                                      COLHDG('RM ITEM')
     A            CSMFGQTY       5P 0       TEXT('FG QTY PRODUCED')
     A                                      COLHDG('FG QTY PROD')
     A            CSMPRJNQ       5P 0       TEXT('PRJ CASE QTY')
     A                                      COLHDG('PRJ CASE QTY')
     A            CSMPRJB1       5P 0       TEXT('PRJ BD1 QTY')
     A                                      COLHDG('PRJ BD1 QTY')
     A            CSMPRJB2       5P 0       TEXT('PRJ BD2 QTY')
     A                                      COLHDG('PRJ BD2 QTY')
     A            CSMPRJPQ       9P 4       TEXT('PRJ PRD QTY')
     A                                      COLHDG('PRJ PRD QTY')
     A            CSMSCRF        5P 4       TEXT('PRJ SCRAP FACTOR')
     A                                      COLHDG('PRJ SCRAP FACTOR')
     A            CSMACTNQ       5P 0       TEXT('ACT CASE QTY')
     A                                      COLHDG('ACT CASE QTY')
     A            CSMACTB1       5P 0       TEXT('ACT BD1 QTY')
     A                                      COLHDG('ACT BD1 QTY')
     A            CSMACTB2       5P 0       TEXT('ACT BD2 QTY')
     A                                      COLHDG('ACT BD2 QTY')
     A            CSMACTPQ       9P 4       TEXT('ACT PRD QTY')
     A                                      COLHDG('ACT PRD QTY')
     A            CSMSCNWGT      9P 3       TEXT('SCANNED WEIGHT')
     A                                      COLHDG('SCN WGHT')
     A            CSMTARE        9P 3       TEXT('TARE WEIGHT')
     A                                      COLHDG('TARE')
     A            CSMBCKF        1          TEXT('BACK FLUSHED')
     A                                      COLHDG('BACK FLUSH')
     A            CSMPQUF        1          TEXT('USE PRD BRKD QTY')
     A                                      COLHDG('USE PRD QTY')
     A            CSMUSEP        1          TEXT('USED IN PROD')
     A                                      COLHDG('USED IN PRD')
     A            CSMTUSNQ       5P 0       TEXT('TOTAL RM CASE USED PRODLOG')
     A                                      COLHDG('RM CASE USED PRODLOG')
     A            CSMTUSB1       5P 0       TEXT('TOTAL RM BD1 USED PRODLOG')
     A                                      COLHDG('RM BD1 USED PRODLOG')
     A            CSMTUSB2       5P 0       TEXT('TOTAL RM BD2 USED PRODLOG')
     A                                      COLHDG('RM BD2 USED PRODLOG')
     A            CSMTWSNQ       5P 0       TEXT('TOTAL RM CASE WST PRODLOG')
     A                                      COLHDG('RM CASE WST PRODLOG')
     A            CSMTWSB1       5P 0       TEXT('TOTAL RM BD1 WST PRODLOG')
     A                                      COLHDG('RM BD1 WST PRODLOG')
     A            CSMTWSB2       5P 0       TEXT('TOTAL RM BD2 WST PRODLOG')
     A                                      COLHDG('RM BD2 WST PRODLOG')
     A            CSMPERUSE     20P16       TEXT('PCT TO CALC USAGE')
     A                                      COLHDG('PCT TO CALC USAGE')
     A            CSMPERWST     20P16       TEXT('PCT TO CALC WASTE')
     A                                      COLHDG('PCT TO CALC WASTE')
     A            CSMFACUSED     1          TEXT('P-LASTPO  A-AVG  S-STD')
     A                                      COLHDG('P-LSTPO A-AVG S-STD')
     A            CSMPOCOST      9P 4       TEXT('LAST PO UNIT CASE COST')
     A                                      COLHDG('LST PO UNT CST')
     A            CSMAVGCOST     9P 4       TEXT('AVG UNIT CASE COST')
     A                                      COLHDG('AVG UNT CST')
     A            CSMSTDCOST     9P 4       TEXT('STD UNIT CASE COST')
     A                                      COLHDG('STD UNT CST')
     A            CSMWSTCPO      9P 4       TEXT('WASTE COST LAST PO')
     A                                      COLHDG('WST CST PO')
     A            CSMUSECPO      9P 4       TEXT('USE COST LAST PO')
     A                                      COLHDG('USE CST PO')
     A            CSMWSTCA       9P 4       TEXT('WASTE COST AVG COST')
     A                                      COLHDG('WST CST AVGCST')
     A            CSMUSECA       9P 4       TEXT('USE COST AVG COST')
     A                                      COLHDG('USE CST AVGCST')
     A            CSMWSTCS       9P 4       TEXT('WASTE COST STD COST')
     A                                      COLHDG('WST CST STDCST')
     A            CSMUSECS       9P 4       TEXT('USE COST STD COST')
     A                                      COLHDG('USE CST STDCST')
     A            CSMCSTASGN     9P 4       TEXT('COST ASSIGNED')
     A                                      COLHDG('CST ASSGNED')
     A            CSMSYSTS        Z         TEXT('SYS TIMESTAMP')
     A                                      COLHDG('SYS TIMESTAMP')
     A            CSMLOCTS        Z         TEXT('LOC TIMESTAMP')
     A                                      COLHDG('LOC TIMESTAMP')
     A            CSMUCTS         Z         TEXT('UC TIMESTAMP')
     A                                      COLHDG('UC TIMESTAMP')
     A            CSMJOB        10          TEXT('JOB')
     A                                      COLHDG('JOB')
     A            CSMUSER       10          TEXT('USER')
     A                                      COLHDG('USER')
     A            CSMJOBNBR      6          TEXT('JOB NUMBER')
     A                                      COLHDG('JOB NUMBER')
     A          K CSMWHSE
     A          K CSMPRDDATE
     A          K CSMLINE
     A          K CSMWO
     A          K CSMTYPE
     A          K CSMFGITM
     A          K CSMRMITM
