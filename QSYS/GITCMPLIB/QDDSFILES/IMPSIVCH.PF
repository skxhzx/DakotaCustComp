     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSIVCH (PF)
     A*  Description ... Staging Import - Invoice Order Header
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740 AA*    11/09/20  MLB  7.40
     A*      - Created
     A*      - Base on IMPFORDH
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R IVHREC
     A*
     A            IVHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IVHBATID      11          TEXT('Import Batch Id')
     A                                      COLHDG('Import' 'Batch Id')
     A            IVHRTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            IVHORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            IVHSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IVHSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVHSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVHTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            IVHORDA       15          TEXT('Alpha Order Number')
     A                                      COLHDG('Alpha Order' 'Number')
     A            IVHINVOICE    15          TEXT('Alpha Invoice Number')
     A                                      COLHDG('Alpha Invoice' 'Number')
     A            IVHTYP         1          TEXT('Order Type')
     A                                      COLHDG('Order' 'Type')
     A            IVHCUSA       30          TEXT('Alpha Customer Number')
     A                                      COLHDG('Alpha Customer' 'Number')
     A            IVHCPO        15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            IVHCNM        40          TEXT('Bill To Customer Name')
     A                                      COLHDG('Bill To' 'Customer Name')
     A            IVHCA1        40          TEXT('Bill To Address 1')
     A                                      COLHDG('Bill To' 'Address 1')
     A            IVHCA2        40          TEXT('Bill To Address 2')
     A                                      COLHDG('Bill To' 'Address 2')
     A            IVHCCT        20          TEXT('Bill To City')
     A                                      COLHDG('Bill To' 'City')
     A            IVHCST         2          TEXT('Bill To State')
     A                                      COLHDG('Bill To' 'State')
     A            IVHCZP        10          TEXT('Bill To Zip')
     A                                      COLHDG('Bill To' 'Zip')
     A            IVHRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            IVHEST         5S 0       TEXT('Extended Stop Nbr')
     A                                      COLHDG('Extended Stop' 'Nbr')
     A            IVHTOTINV     14S 5       TEXT('Total Invoice')
     A                                      COLHDG('Total' 'Invoice')
     A            IVHTOTTAX     11S 5       TEXT('Total Tax')
     A                                      COLHDG('Total' 'Tax')
     A            IVHTOTFEE     11S 5       TEXT('Total Surcharges/Fees')
     A                                      COLHDG('Total' 'Surcharges/Fees')
     A            IVHPMODE       1          TEXT('Invoice Print Mode (D/S/E)')
     A                                      COLHDG('Invoice' 'Print Mode')
     A            IVHTERMS      30          TEXT('Invoice Terms')
     A                                      COLHDG('Invoice' 'Terms')
     A            IVHACTMGR     30          TEXT('Account Manager')
     A                                      COLHDG('Account' 'Manager')
     A            IVHPWGTS       1          TEXT('Print C/Wgts Y/N')
     A                                      COLHDG('Print' 'C/Wgts Y/N')
     A            IVHMSC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A            IVHMS1        40A         TEXT('Host Misc Field 1')
     A                                      COLHDG('Host' 'Misc' 'Field 1')
     A            IVHMS2        40A         TEXT('Host Misc Field 2')
     A                                      COLHDG('Host' 'Misc' 'Field 2')
     A            IVHMS3        40A         TEXT('Host Misc Field 3')
     A                                      COLHDG('Host' 'Misc' 'Field 3')
     A            IVHMS4        40A         TEXT('Host Misc Field 4')
     A                                      COLHDG('Host' 'Misc' 'Field 4')
     A            IVHMS5        40A         TEXT('Host Misc Field 5')
     A                                      COLHDG('Host' 'Misc' 'Field 5')
     A*
     A*  Add stamps
     A*
     A            IVHADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVHADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IVHWHS
     A          K IVHBATID
     A          K IVHRTE
     A          K IVHINVOICE
     A          K IVHORDA
