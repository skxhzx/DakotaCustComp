     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICACTIVE  (PF)
     A*  Description ... Active license file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
600 AI*    10/15/08  DAS  600
     I*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*   - License transaction number (LRLTRN) is used just as a
     A*     sequence number for the file.
     A*
500bAA*   - Control number (LRCTL#) and sequence number (LRCTLSEQ#) are
500bAA*     used to group multiple license plates together that belong
500bAA*     to the same slot.
     A*
     A*   - License number (LRLCNS) is the visible license number on
     A*     an item.
     A*
     A*   - From license number (LRFMLC). In the case of of split,
     A*     this would identify the license plate the split came from.
     A*
     A*   - Original license number (LRORLC). This differs from the
     A*     "From" license number in that it should contain the license
     A*     number that is associated with the original information for
     A*     the item not just the license number that was split. The
     A*     first time a record is created for a license number, the
     A*     the original license number will be the same as the license
     A*     number (LRLCNS). After that, the original license number
     A*     number never changes.
     A*
     A*   - From area (LRFARA) is used in conjunction with the "From"
     A*     Code (LRFMCD) values A thru D.
     A*     The From slot fields(LRFWHD,LRFASL,LRFLOC,LRFLVL,LRFHND)
     A*     are used when LRFMCD is 'S'lot.
     A*
     A*   - To area (LRTARA) is used in conjunction with the "To" Code
     A*     (LRTOCD) values A thru D.
     A*     The To slot fields(LRTWHD,LRTASL,LRTLOC,LRTLVL,LRTHND) are
     A*     used when LRTOCD is 'S'lot or 'F'loor.
     A*
     A*   - To order number (LRTORD) is used when To Code (LRTOCD) is
     A*     'P'icked. This identifies the customer that ordered the
     A*     item.
     A*
417fAA*   - LRFVFY identifies if the "From" license was verified (Y)
417fAA*     by the user.
     A*
500aAA*   - Item numbers should always be "Slot" item numbers. The
     A*     only exception is the From item of a Receiving code. In
     A*     that case the item should be the actual item received. The
     A*     corresponding To item for the Dock code will be the "Slot"
     A*     item.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    From Code  (LRFMCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'I'  - Inventory.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
     A*    To Code  (LRTOCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Special License Numbers
     A*
     A*    Starting
     A*    Characters
     A*
     A*      '*SU'  -  Start Up
     A*                These license plates are used when a client
     A*                starts using license plate tracking after using
     A*                Dakota FS-WMS for a while. A start up program
     A*                will assign an '*SU' license plate to slots
     A*                that contain an item to be tracked.
     A*
     A*      '*NF'  -  Not Found
     A*                These license plates are created on-the-fly when
     A*                a license plate does not exist for a slot/item.
     A*
     A*      '*ML'  -  Missing License
     A*                These license plates are created when a user is
     A*                asked to scan a particular license plate when
     A*                taking an item out of a slot, but the user
     A*                cannot find the license plate to scan. The user
     A*                would press F10 to take an exception and let
     A*                the system know the requested license plate is
     A*                missing.
     A*
     A*      '*NA'  -  Not Applied
     A*                These license plates are created when a user
     A*                is asked to apply a new license plate to a
     A*                partial quantity being put into a slot, but the
     A*                user doesn't do it and presses F10 instead.
     A*
     A*      '*NT'  -  Not Tracked
     A*                These license plates are created for items in
     A*                which the License Tracking option is set to No.
     A*                user doesn't do it and presses F10 instead.
     A*
     A*      '*OR'  -  Customer Order
     A*                These license plates are created for items
     A*                picked for customer orders.
     A*
     A*      '*PS'  -  Pick Slot
     A*                These license plates are created for items in
     A*                which a partial quantity was put into the Pick
     A*                slot and either we are in Locator mode or we
     A*                do not ask selectors to scan a license plate.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R LAREC
     A*
     A            LAWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            LALCNS        15A         TEXT('License Number')
     A                                      COLHDG('License Number')
     A            LAOLCNS       15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A*
     A            LAQTY1         5P 0       TEXT('Lic Qty1')
     A                                      COLHDG('Lic Qty1')
     A            LAQTY2         5P 0       TEXT('Lic Qty2')
     A                                      COLHDG('Lic Qty2')
     A            LAQTY3         5P 0       TEXT('Lic Qty3')
     A                                      COLHDG('Lic Qty3')
     A*
     A* Location information
     A*
     A            LACODE         1A         TEXT('Code')
     A                                      COLHDG('Code')
     A            LAAREA        15A         TEXT('Area')
     A                                      COLHDG('Area')
     A            LAWHDP    R               TEXT('Department')
     A                                      COLHDG('Department')
     A                                      REFFLD(WDCODE)
     A            LADISP        12A         TEXT('Display')
     A                                      COLHDG('Display')
     A            LAAISL    R               TEXT('Aisle')
     A                                      COLHDG('Aisle')
     A                                      REFFLD(SLAISL)
     A            LALOC     R               TEXT('Location')
     A                                      COLHDG('Location')
     A                                      REFFLD(SLLOC)
     A            LARLVL    R               TEXT('Rack Level')
     A                                      COLHDG('Level')
     A                                      REFFLD(SLRLVL)
     A            LAHAND    R               TEXT('Handstack')
     A                                      COLHDG('Handstack')
     A                                      REFFLD(HSCODE)
     A            LABASE        12A         TEXT('Base Slot')
     A                                      COLHDG('Base Slot')
     A            LAPOS          3P 0       TEXT('Pallet Position')
     A                                      COLHDG('Pallet' 'Position')
     A            LAITEM    R               TEXT('Item')
     A                                      COLHDG('Item')
     A                                      REFFLD(ITITEM)
     A            LAITYPE        1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            LASITEM   R               TEXT('Slot Item')
     A                                      COLHDG('Slot Item')
     A                                      REFFLD(ITITEM)
     A            LASITYPE       1A         TEXT('Slot Item Type')
     A                                      COLHDG('Slot' 'Item Type')
     A            LAPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            LAENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            LAEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A*
     A* To Customer information
     A*
     A            LAORD     R     P         TEXT('To Order Number')
     A                                      COLHDG('To' 'Order' 'Number')
     A                                      REFFLD(OHORD)
     A            LASEQ     R     P         TEXT('To Order Sequence Number')
     A                                      COLHDG('To Order' 'Sequence Nbr')
     A                                      REFFLD(SEQ#)
     A*
     A* Timestamp / User tracking information.
     A*
     A            LACHGTS   R               REFFLD(CHGTS)
     A            LACHGUC   R               REFFLD(CHGUC)
     A            LACHGPGM  R               REFFLD(CHGPGM)
     A            LACHGEMP  R               REFFLD(CHGEMP)
     A            LACHGEUSR R               REFFLD(CHGUSR)
     A                                      COLHDG('Employee' 'User')
     A                                      TEXT('Employee User')
     A            LACHGCUSR R               REFFLD(CHGUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            LACHGJUSR R               REFFLD(CHGUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            LACHGJOB  R               REFFLD(CHGJOB)
     A            LACHGNBR  R               REFFLD(CHGNBR)
     A*
     A          K LAWHSE
     A          K LALCNS
