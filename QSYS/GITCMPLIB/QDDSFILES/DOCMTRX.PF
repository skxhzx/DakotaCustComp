     A*----------------------------------------------------------------***
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51                         *
     A*   West Chicago, IL  60185                                        *
     A*   (630) 562-0375                                                 *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... DOCMTRX   (PF)
     A*  Description ... Dock door matrix file
     A*  Date .......... November 6, 2001
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------***
     A*  Revisions
     A*
416 AA*    11/06/01  DAS  4.16
     A*      - Created
     A*
520 AA*    09/22/09  MLB  5.20
     A*      - Enh: Added Assignment Type to file key to keep
     A*        automatic assigned doors separate from manually
     A*        assigned doors. This was done so that the next
     A*        dock door to be assigned in the automatic sequence
     A*        will always be correct even when a route is moved
     A*        from one door to another.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status (DMSTAT)
     A*
     A*       '1'  -  Open
     A*       '2'  -  Assigned
     A*       '3'  -  Skipped
     A*       '9'  -  Inactive
     A*
     A*    Overflow (DMOVRF)
     A*
     A*       'Y'  -  Overflow door for route.
     A*       'N'  -  Main door for route.
     A*
     A*    Skip reason (DMSKPR)
     A*
     A*       '1'  -  Pass mismatch ("Odd" pass, even door, visa versa)
     A*       '2'  -  Used by overflow in previous pass.
     A*       '3'  -  Cube doesn't fit and next door not available
     A*               for overflow.
     A*       '4'  -  New pass was created manually.
     A*       '5'  -  Route manually removed.
520 AA*       '6'  -  Route forced. No doors available during auto assign.
520 AA*       '7'  -  Route moved manually by user.
     A*
     A*    Assign type (DMATYP)
     A*
     A*       'A'  -  Automatic
     A*       'M'  -  Manual
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R DMREC
     A*
     A            DMWHSE    R               REFFLD(WHCODE)
     A            DMHDTE    R               TEXT('History Date')
     A                                      COLHDG('History' 'Date')
     A                                      REFFLD(DTECMD)
     A            DMHTIM    R               TEXT('History Time')
     A                                      COLHDG('History' 'Time')
     A                                      REFFLD(TIMHMS)
     A            DMPASS         3  0       TEXT('Pass')
     A                                      COLHDG('Pass')
     A            DMDOOR         3  0       TEXT('Door Number')
     A                                      COLHDG('Door' 'Number')
     A            DMWHDP    R               REFFLD(WDCODE)
     A            DMMAXC         5  0       TEXT('Max Cube')
     A                                      COLHDG('Max' 'Cube')
     A            DMRTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            DMRTEC         9  3       TEXT('Route Cube')
     A                                      COLHDG('Route' 'Cube')
     A            DMSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            DMOVRF         1          TEXT('Over Flow')
     A                                      COLHDG('Over' 'Flow')
     A            DMSKPR         1          TEXT('Skip Reason')
     A                                      COLHDG('Skip' 'Reason')
     A            DMATYP         1          TEXT('Assign Type')
     A                                      COLHDG('Assign' 'Type')
     A            DMABY         10          TEXT('Assigned By')
     A                                      COLHDG('Assigned' 'By')
     A            DMADTE    R               TEXT('Assigned Date')
     A                                      COLHDG('Assigned' 'Date')
     A                                      REFFLD(DTECMD)
     A            DMATIM    R               TEXT('Assigned Time')
     A                                      COLHDG('Assigned' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K DMWHSE
     A          K DMHDTE
     A          K DMHTIM
520 AA          K DMATYP
     A          K DMPASS
     A          K DMDOOR
     A          K DMWHDP
