     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PIRITEM (PF)
     A*  Description ... Item file
     A*  Date .......... December 18, 1992
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 2.00   07/07/94  DAS
     A*      - Renamed from ITEM.
     A*      - Added warehouse and other fields.
     A*
     A*    Ver 2.01   07/12/94  HNK
     A*      - ITBLD field revised.
     A*
     A*    Ver 2.02   08/30/94  DAS
     A*      - Added unit of measure quantities.
     A*
213 AA*    Ver 2.03   05/13/96  DAS
213 AA*      - Added new fields:
213 AA*           ITTYPE, ITFLG1, ITFLG2, ITFLGD, ITFLGB, ITSLIF, ITNDAY
     A*
414 AA*    Ver 4.14  07/03/00  KDE
     A*      - Change usage for ITVIT#
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
213 AA*    Item type (ITTYPE)
     A*
     A*      ' '  -  Normal
     A*
     A*      'B'  -  Broken case
     A*
     A*        This type is used when the breakdown quantity of an item
     A*        is sold under a separate item number.  During replenishment
     A*        1 full case item is replenished to the broken case item.
     A*        The ITEMLNK file provides the connection.
     A*
     A*      'K'  -  Kit  (Future release)
     A*
     A*        This type is used when an item is created from a number
     A*        of other items.  Bags of mixed fruit might be an example
     A*        of this type.
     A*
     A*      'R'  -  Repackaged  (Future release)
     A*
     A*        This type is used when an item is repackaged to create
     A*        breakdown quantities which are sold under a separate
     A*        item number.  During replenishment 1 full case item is
     A*        repackaged into broken case items. For example, a 50 lb
     A*        full case item of oranges might be repackaged into 5 lb
     A*        and 10 lb broken case items.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R ITREC
     A*
     A            ITWHSE    R               REFFLD(WHCODE)
     A            ITITEM    R               REFFLD(ITITEM)
213 AA            ITTYPE         1          TEXT('Item Type')
213 AA                                      COLHDG('Item' 'Type')
     A            ITDESC        30          TEXT('Description')
     A                                      COLHDG('Description')
     A            ITSTYP    R               REFFLD(SLSTYP)
     A            ITWHDP    R               REFFLD(WDCODE)
     A            ITDESG    R               REFFLD(DSCODE)
     A            ITACTV    R               REFFLD(ACCODE)
     A            ITUPC         15          TEXT('UPC code')
     A                                      COLHDG('UPC Code')
     A            ITBLD     R               REFFLD(BCCODE)
     A            ITVEN#        10S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
414 AA* Usage for ITVIT# has been changed to Mfg. Code...
     A            ITVIT#        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            ITPDSC        15          TEXT('Pack Description')
     A                                      COLHDG('Pack' 'Description')
     A            ITUM1     R               TEXT('Normal UM')
     A                                      COLHDG('Normal' 'UM')
     A                                      REFFLD(UMCODE)
     A            ITUM2     R               TEXT('Breakdown 1 UM')
     A                                      COLHDG('Break 1' 'UM')
     A                                      REFFLD(UMCODE)
     A            ITUMQ2         3P 0       TEXT('Breakdown 1 UM Qty')
     A                                      COLHDG('Breakdown 1' 'UM Qty')
213 AA            ITFLG1         1          TEXT('Allow Brk1 Sale (Y/N)')
213 AA                                      COLHDG('Allow' 'Brk1 Sale')
     A            ITUM3     R               TEXT('Breakdown 2 UM')
     A                                      COLHDG('Break 2' 'UM')
     A                                      REFFLD(UMCODE)
     A            ITUMQ3         3P 0       TEXT('Breakdown 2 UM Qty')
     A                                      COLHDG('Breakdown 2' 'UM Qty')
213 AA            ITFLG2         1          TEXT('Allow Brk2 Sale (Y/N)')
213 AA                                      COLHDG('Allow' 'Brk2 Sale')
     A*
     A            ITULEN         5P 2       TEXT('Unit Length')
     A                                      COLHDG('Unit' 'Length')
     A            ITUWDT         5P 2       TEXT('Unit Width')
     A                                      COLHDG('Unit' 'Width')
     A            ITUHGT         5P 2       TEXT('Unit Height')
     A                                      COLHDG('Unit' 'Height')
     A            ITCUBE         7P 3       TEXT('Cube/Unit')
     A                                      COLHDG('Cube/Unit')
     A            ITSWGT         7P 2       TEXT('Shipping Weight')
     A                                      COLHDG('Shipping' 'Weight')
     A            ITCWGT         1          TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            ITREOR         5P 0       TEXT('Reorder Level')
     A                                      COLHDG('Reorder' 'Level')
     A            ITREPL         5P 0       TEXT('Replenish Level')
     A                                      COLHDG('Replenish' 'Level')
     A            ITRCVD    R               TEXT('Last Received Date')
     A                                      COLHDG('Last Rcv' 'Date')
     A                                      REFFLD(DTECMD)
213 AA            ITFLGD         1          TEXT('Date Sensitive (Y/N)')
213 AA                                      COLHDG('Date' 'Sensitive')
213 AA            ITFLGB         1          TEXT('Allow Bulk Pick (Y/N)')
213 AA                                      COLHDG('Allow' 'Bulk Pick')
213 AA            ITSLIF         3P 0       TEXT('Shelf Life Days')
213 AA                                      COLHDG('Shelf Life' 'Days')
213 AA            ITNDAY         3P 0       TEXT('Notice Days')
213 AA                                      COLHDG('Notice' 'Days')
     A*
     A          K ITWHSE
     A          K ITITEM
