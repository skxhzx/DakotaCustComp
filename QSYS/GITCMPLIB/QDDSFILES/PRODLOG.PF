     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PRODLOG
     A*  Description ... Production Log
     A*  Creator ....... KDE
     A*
     A*
     A*  Revisions
     A*
650 AA*    Ver 6.50  10/31/14  KDE
     A*      - Created
710 AA*    Ver 7.10  02/13/17  KDE
     A*      - Add field for raw material UCI for Dry Aged products
     A*
     A*----------------------------------------------------------------
     A*
     A*   SOURCE FOR ACTION CODES   PLACTION
     A*     PRR - Production Replenishment
     A*     PBL - Production Pallet Build for PAR Stock
     A*     PBF - Raw Material BackFlush from Scale Operation
     A*     PSC - Finished Good in and out from Scale Operation
     A*     PIU - End of Day Adjustments usage
     A*     PIW - End of Day Adjustments waste
     A*
     A*----------------------------------------------------------------
     A          R PLREC
     A*
     A            PLWHSE         3  0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse'         )
     A            PLITEM        15          TEXT('Item Transacted')
     A                                      COLHDG('Item Transacted')
     A            PLDTETIM      14          TEXT('CCYYMMDDHHMMSS')
     A                                      COLHDG('Date/Time of Trans')
     A            PLACTION       3          TEXT('Action')
     A                                      COLHDG('Action')
     A            PLFLCNS       15          TEXT('From License')
     A                                      COLHDG('From License')
     A            PLTLCNS       15          TEXT('To License')
     A                                      COLHDG('To License')
     A            PLLINE         3  0       TEXT('MFG Line#')
     A                                      COLHDG('MFG Line#')
     A            PLDISP        12          TEXT('SLOT PLACED')
     A                                      COLHDG('SLOT PLACED')
     A            PLGS1         50          TEXT('GS1 DATA')
     A                                      COLHDG('GS1 DATA')
     A            PLQTYN         5  0       TEXT('Qty Normal')
     A                                      COLHDG('Qty Normal')
     A            PLQTY1         5  0       TEXT('Qty BD 1')
     A                                      COLHDG('Qty BD 1')
     A            PLQTY2         5  0       TEXT('Qty BD 2')
     A                                      COLHDG('Qty BD 2')
     A            PLWEGHT        9  3       TEXT('Gross Weight')
     A                                      COLHDG('Gross Weight')
     A            PLTARE         9  3       TEXT('Tare')
     A                                      COLHDG('Tare')
     A            PLPGM         10          TEXT('Program')
     A                                      COLHDG('Program')
     A            PLUSER        10          TEXT('User')
     A                                      COLHDG('User')
     A            PLJOB         10          TEXT('JOB')
     A                                      COLHDG('JOB')
     A            PLLSTS        20          TEXT('LAST SERIAL NUMBER ASSIGNED')
     A                                      COLHDG('LAST SERIAL')
     A            PLCTS           Z         TEXT('Current Time Stamp')
     A                                      COLHDG('Current Time Stamp')
     A            PLBFCWO       13          TEXT('BFC Work Order Number')
     A                                      COLHDG('BFC WO')
     A            PLCUSTWO      20          TEXT('Customer Work Order')
     A                                      COLHDG('CUST WO')
     A            PLTRN#         7  0       TEXT('Transaction NUMBER')
     A                                      COLHDG('Trans Number')
     A            PLLBL#         7  0       TEXT('Label Number')
     A                                      COLHDG('Lab Num')
     A            PLFGITM       15          TEXT('FG Item')
     A                                      COLHDG('FG Item')
     A            PLPRDDTE       8  0       TEXT('PRD DATE')
     A                                      COLHDG('PRD DATE')
710 AA            PLRMUCI       50          TEXT('RM UCI for Dry Age')
     A                                      COLHDG('RM UCI DRYAGE')
     A*
     A          K PLWHSE
     A          K PLITEM
     A          K PLCTS
     A          K PLACTION
     A          K PLPRDDTE
