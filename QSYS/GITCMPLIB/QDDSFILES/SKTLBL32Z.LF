     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SKTLBL32Z (LF)
     A*  Description ... Special join file for printing ELB pick lbls
     A*  Creator ....... DAS/MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    02/29/09  MLB  5.20
     A*      - Created
     A*
530 AA*    02/04/10  MLB  5.30
     A*      - Enh: LF recompiled. Field PSTRN# added to correct
     A*        problem with duplicate labels when multi trans checked out.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file is identical to SKTLBL32D except that it used SLWSTOP
     A*  was moved after LBTRN# and all slot fields were set to
     A*  descending sequence so that they would be picked from cart
     A*  in reverse sequence.
     A*  This was originally created for Ellenbee-Leggett
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R SLWREC32Z                 JFILE(SKTLBL               +
     A                                            LABEL PLTSUM PIRITEM +
     A                                            ORDH ORDD UNMESR)
     A*
     A          J                           JOIN(SKTLBL  LABEL)
     A                                      JFLD(SLWLBL# LBLBL#)
     A*
     A          J                           JOIN(LABEL   PLTSUM)
     A                                      JFLD(LBWHSE  PSWHSE)
     A                                      JFLD(LBRTE   PSRTID)
     A                                      JFLD(LBPAL#  PSPLID)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
     A*  Fields from SKTLBL file.
     A*
     A            SLWEMP#
     A            SLWWHSE
     A            SLWLVL2
     A            SLWWHDP
     A            SLWITEM
     A            SLWAISL
     A            SLWLOC
     A            SLWRLVL
     A            SLWHAND
     A            SLWDISP
     A            SLWPSEQ
     A            SLWSTYP
     A            SLWRTE
     A            SLWSTOP
     A            SLWTYPE
     A            SLWLBL#
     A            SLWTRN#
     A            SLWSTAMP
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
     A            LBPO
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
     A            LBGRP1
     A            LBGRP2
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPAL#
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
530 AA            PSTRN#
     A*
     A*  Fields from PIRITEM file.
     A*
     A            ITTYPE
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
     A            ITFLG1
     A            ITUM3
     A            ITUMQ3
     A            ITFLG2
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
     A            ITREOR
     A            ITREPL
     A            ITRCVD
     A            ITFLGD
     A            ITFLGB
     A            ITSLIF
     A            ITNDAY
     A*
     A*  Fields from ORDH file.
     A*
     A            OHORIG
     A            OHTYPE
     A            OHCUST
     A            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A            OHPBAT
     A            OHSTAT
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Key fields
     A*
     A          K SLWEMP#
     A          K SLWTYPE
     A          K SLWLVL2
     A          K SLWSTOP                   DESCEND
     A          K SLWDISP                   DESCEND
