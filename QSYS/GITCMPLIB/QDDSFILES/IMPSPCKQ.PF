     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSPCKQ   (PF)
     A*  Description ... Staging Import - Host Item Picked Quantities
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    03/08/11  DAS  6.40
     A*      - Created
     A*      - Base on IMPFPCKQ
     A*
640aAA*    03/10/11  DAS  6.40a
     A*      - Removed UNIQUE keyword.
     A*
700a A*    09/30/16  RTR  7.00a
     A*      - Added IPQLCNS for License.
720a A*    02/02/18  RTR  7.20a
     A*      - Added IPQLCNS to key.
750a A*    03/10/21  RTR  7.50a
     A*      - Added IPQUOM, optional for those who need it.
     A*
     A*----------------------------------------------------------------
640aDA**                                    UNIQUE
     A          R IPQREC
     A*
     A            IPQWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IPQBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A            IPQITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            IPQSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IPQSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IPQSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IPQTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            IPQWHD         5A         TEXT('Warehouse Department')
     A                                      COLHDG('Warehouse' 'Department')
     A            IPQDSP        12A         TEXT('Slot to Adjust')
     A                                      COLHDG('Slot to' 'Adjust')
     A            IPQQP1         7S 0       TEXT('Total Qty1 Picked')
     A                                      COLHDG('Tota Qty1' 'Picked')
     A            IPQQP2         7S 0       TEXT('Total Qty2 Picked')
     A                                      COLHDG('Tota Qty2' 'Picked')
     A            IPQQP3         7S 0       TEXT('Total Qty3 Picked')
     A                                      COLHDG('Tota Qty3' 'Picked')
     A            IPQADJ         2A         TEXT('Adjustment Code')
     A                                      COLHDG('Adj' 'Code')
     A            IPQMEM        30A         TEXT('Memo Field')
     A                                      COLHDG('Memo' 'Field')
     A            IPQSOF         1A         TEXT('Search for Overflow Slot Y/N')
     A                                      COLHDG('Search for' 'Overflow Slot')
700aAA            IPQLCNS       15A         TEXT('License')
700aAA                                      COLHDG('License')
750aAA            IPQUOM        10A         TEXT('UOM')
750aAA                                      COLHDG('UOM')
     A*
     A*  Add stamps
     A*
     A            IPQADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IPQADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IPQWHS
     A          K IPQBATID
     A          K IPQITM
     A          K IPQADDTS
720aAA          K IPQLCNS
