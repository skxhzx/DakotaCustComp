     A*----------------------------------------------------------------
     A*   Copyright (C) 2022 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PIRBUYER (PF)
     A*  Description ... Buyer file
     A*
     A*  Revisions
     A*
760 AA*    10/02/22  DAS  7.60
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A* Notes
     A*
     A*  This file, along with the PIRVENDOR file, were initially
     A*  created to work with the AFS Host system, which use alpha
     A*  Buyer and Vendor numbers. But is to become the standard
     A*  for both alpha an numeric Buyer/Vendor numbers.
     A*
     A*  At the time, it was decided that the alpha number would be
     A*  sent at the beginning of the Name field in the import file
     A*  followed by a colon, and the Buyer/Vendor number fields
     A*  would be left zero.
     A*
     A*  For an alpha number, we will get an Id number that would be
     A*  put into the item Buyer/Vendor number. Programs that need to
     A*  display the Buyer/Vendor number, should use the value in the
     A*  item file to get the Host Buyer/Vendor number from this
     A*  file and the PIRVENDOR file.
     A*
     A*  New programs GETBUYER and GETVENDOR are called by the import
     A*  programs. These programs will assign an Id to alpha numbers.
     A*
     A*  For clients using numeric Buyer/Vendor numbers, the Id will be
     A*  the Buyer/Vendor numeric, the Host value will be the number
     A*  with leading zeros, the Display value will be the number
     A*  without leading zeros.
     A*
     A*  For clients using alph Buyer/Vendor numbers, the Id will be
     A*  assigned, the Host and display values will be the alpha value.
     A*
     A*----------------------------------------------------------------
     A*
     A* Fields
     A*
     A*  BYBUYERID - Dakota Buyer Number
     A*
     A*  BYHSTBUYER - Host Buyer Number
     A*
     A*  BYBUYERDSP - Buyer Number Display value
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R BYREC
     A*
     A            BYWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            BYBUYERID      3S 0       TEXT('Dakota Buyer Id')
     A                                      COLHDG('Dakota' 'Buyer Id')
     A            BYHSTBUYER    20          TEXT('Host Buyer Number')
     A                                      COLHDG('Host' 'Buyer Number')
     A            BYBUYERDSP    20          TEXT('Buyer Display')
     A                                      COLHDG('Buyer' 'Display')
     A            BYNAME        40          TEXT('Buyer Name')
     A                                      COLHDG('Buyer' 'Name')
     A*
     A*  Add/Change information
     A*
     A            BYADDTS   R               REFFLD(ADDTS PIRDICT)
     A            BYADDUC   R               REFFLD(ADDUC PIRDICT)
     A            BYADDUSR  R               REFFLD(ADDUSR PIRDICT)
     A            BYADDJOB  R               REFFLD(ADDJOB PIRDICT)
     A            BYADDNBR  R               REFFLD(ADDNBR PIRDICT)
     A*
     A            BYCHGTS   R               REFFLD(CHGTS PIRDICT)
     A            BYCHGUC   R               REFFLD(CHGUC PIRDICT)
     A            BYCHGUSR  R               REFFLD(CHGUSR PIRDICT)
     A            BYCHGJOB  R               REFFLD(CHGJOB PIRDICT)
     A            BYCHGNBR  R               REFFLD(CHGNBR PIRDICT)
     A*
     A          K BYWHSE
     A          K BYBUYERID
