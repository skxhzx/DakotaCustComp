     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTSTPEXA (PF)
     A*  Description ... TTSTP extra fields
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740aAA*    04/16/20  RBD  7.40a
     A*      - Created.
     A*
760aAA*    05/09/22  RBD  7.60a
     A*      - Reserved.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file should only be touched using SQL so new fields
     A*   can be added without needing to recompile existing programs.
     A*
     A*   Ideally, RPG programs should only access/update this
     A*   file using program EXTTSTP.
     A*
     A*   SQL Insert and Update statements should work with
     A*   individual fields to prevent a level check error.
     A*
     A*   MRC programs should not be affected by new fields because
     A*   they use SQL.
     A*
     A*----------------------------------------------------------------

     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R TTPAREC
     A*
     A            TTPAWHS   R               REFFLD(WHCODE)
     A            TTPARTI   R               REFFLD(ROUTE)
     A                                      TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            TTPASTP   R               REFFLD(STOP)
     A            TTPAPSS        3P 0       TEXT('PALLETSCAN scanned count')
     A                                      COLHDG('PALLETSCAN' 'Scanned Count')
     A            TTPAPSC        3P 0       TEXT('PALLETSCAN clicked count')
     A                                      COLHDG('PALLETSCAN' 'Clicked Count')
     A            TTPAPAL        3P 0       TEXT('Total pallet count')
     A                                      COLHDG('Total' 'Pallet Count')
     A            TTPAISS        3P 0       TEXT('INSTORESCAN scanned count')
     A                                      COLHDG('INSTORESCAN' 'Scanned Cnt')
     A            TTPAISC        3P 0       TEXT('INSTORESCAN clicked count')
     A                                      COLHDG('INSTORESCAN' 'Clicked Cnt')
     A            TTPAIST        3P 0       TEXT('INSTORESCAN total count')
     A                                      COLHDG('INSTORESCAN' 'Total Cnt')
     A            TTPARETSH      4P 0       TEXT('Return code short count')
     A                                      COLHDG('Return Code' 'Short Cnt')
     A            TTPARET        4P 0       TEXT('Total qty items returned')
     A                                      COLHDG('Total Qty' 'Items Returned')

     A          K TTPAWHS
     A          K TTPARTI
     A          K TTPASTP
