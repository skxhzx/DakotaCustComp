     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL76 (LF)
     A*  Description ... Label join file by transaction, pallet,
     A*                  and printing sequence.
     A*  Date .......... Nov 4, 2010
     A*  Creator ....... RBD
     A*
     A*  Revisions
     A*
610 AA*    11/03/10 RBD  6.10
     A*      - Created.
     A*
610aAA*    11/29/10 DAS  6.10a
     A*      - Added fields from PIRITEM
     A*
650aAA*    12/26/13 RBD  6.50a
     A*      - Fix: Added field ITDESC so it will print correctly
     A*        on Transaction Audit Report.
     A*
730 AA*    08/30/19 KDE  7.30
     A*      - FIX: ADD TRANSACTION IN LABEL TO PLTSUM JOIN TO
     A*        PREVENT DUPLICATING LABEL RECORDS
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R L76REC                    JFILE(LABEL PLTSUM ORDH +
610aMA                                            RTEHED PIRITEM)
     A*
     A          J                           JOIN(LABEL   PLTSUM)
     A                                      JFLD(LBWHSE  PSWHSE)
     A                                      JFLD(LBRTE   PSRTID)
     A                                      JFLD(LBPAL#  PSPLID)
730 AA                                      JFLD(LBTRN#  PSTRN#)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   RTEHED)
     A                                      JFLD(LBWHSE  RHWHSE)
     A                                      JFLD(LBRTE   RHRTID)
610aAA*
610aAA          J                           JOIN(LABEL   PIRITEM)
610aAA                                      JFLD(LBWHSE  ITWHSE)
610aAA                                      JFLD(LBITEM  ITITEM)
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPLID
     A            PSPAL#
     A            PSUNLD
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
     A*
     A*  Fields from ORDH file.
     A*
     A            OHTYPE
     A            OHORD
     A            OHCORD
     A            OHCUST
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A*
     A*  Fields from RTEHED file.
     A*
     A            RHWHSE
     A            RHRTID
     A            RHRTE
610aAA*
610aAA*  Fields from PIRITEM file.
610aAA*
610aAA            ITTYPE
610aAA            ITSTYP
610aAA            ITWHDP
650aAA            ITDESC
     A*
     A*  Key fields
     A*
     A          K LBWHSE
     A          K LBRTE
     A          K LBTRN#
     A          K LBPAL#
     A          K LBTYPE
     A          K LBWHDP
     A          K LBSTYP
     A          K LBAISL
     A          K LBPSEQ
     A          K LBRLVL
     A          K LBHAND
     A          K LBSTOP                    DESCEND
