     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ZONEH (PF)
     A*  Description ... Zone header file.
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    06/10/97  DAS
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*   - A zone is either picked to go into a truck compartment or
     A*     to be merged into another zone's pallet (Compartment = ' ')
     A*
     A*   - A 'Parent zone' identifies which zone's pallet a product
     A*     should be merged into.  This must be blank if the
     A*     'Truck compartment' is not blank.
     A*
     A*   - Generation is the geneological generation of this child. If
     A*     the zone does not have a parent (Truck zone pallet) then it
     A*     is the first generation.  If it has a parent (Merge zone
     A*     pallet) and the parent does not have a parent, then it is a
     A*     second generation.  If the parent does have a parent then it
     A*     is a third generation. Etc . . .
     A*
     A*   - Generation is calculated, not entered (or seen?)  by the user.
     A*
     A*   - Pick by generation and then zone.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R ZHREC
     A*
     A            ZHWHSE    R               REFFLD(WHCODE)
     A            ZHTRUK    R               REFFLD(TMTRUK)
     A            ZHZONE    R               REFFLD(ZHZONE)
     A            ZHDESC        30          TEXT('Description')
     A                                      COLHDG('Description')
     A            ZHCOMP    R               REFFLD(TPCODE)
     A            ZHPZON    R               REFFLD(ZHZONE)
     A                                      TEXT('Parent Zone')
     A                                      COLHDG('Parent' 'Zone')
     A            ZHGEN          1  0       TEXT('Generation')
     A                                      COLHDG('Generation')
     A            ZHDROP         6          TEXT('Dropoff Location')
     A                                      COLHDG('Dropoff' 'Location')
     A*
     A          K ZHWHSE
     A          K ZHTRUK
     A          K ZHZONE
