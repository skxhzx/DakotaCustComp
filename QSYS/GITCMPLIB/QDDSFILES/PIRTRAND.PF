     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PIRTRAND (PF)
     A*  Description ... Transaction detail file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
630 AA*    06/15/10  DAS
     A*      - Created.
     A*
640aAA*    09/23/11  DAS  6.40a
     A*      - Changed PTDTRAN# to PTDTRAN
     A*      - Changed PTDEMP# to PTDEMPNUM
     A*
640bAA*    01/17/12  DAS  6.40b
     A*      - Changed meaning of voice id/flag to be 'at least one
     A*        label was touched by voice'.
     A*      - Added flag ptdnrmnv to indicate that at least one
     A*        normal selection label was not touched by voice.
     A*      - Added flag ptdmrgnv to indicate that at least one
     A*        merge selection label was not touched by voice.
     A*
640cAA*    02/01/12  DAS  6.40c
     A*      - Added fields ptdgrsblk, ptdblkpcs, ptdblkzns, ptdblklbls
     A*        to contain merge values related to a bulk pick. Old
     A*        merge fields will contain values related to non-bulk
     A*        picks.
     A*      - Added field ptdmrgzns to hold the number of merge
     A*        zones in a transaction (similar to number of slots).
     A*
640dAA*    02/14/12  DAS  6.40d
     A*      - Created ptdrpltf/ptdrpltfi for REPLTF task.
     A*      - Created ptdpick flag to identify if a putaway touched
     A*        a pick slot.
     A*
640eAA*    05/17/12  DAS  6.40e
     A*      - Added field ptdidlesec, ptdlabrsec and ptdcortran.
     A*        See notes about Corresponding Idle/Labor Time.
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   - This file could have more than one record per PIRTRAN rec.
     A*     For example, a PUTEMP transaction will have one record to
     A*     identify when it was received and one when it was put away
     A*     or deleted. Another example would be a selection transaction
     A*     where more than one person picked it.
     A*
     A*   - The program that updates this file (WRTTRAND) will always
     A*     add new records, regardless of their status. However, most
     A*     flags and other information cannot be recorded until after
     A*     the transaction has been ended and/or closed. Therefore,
     A*     we include the current status in this file so WRTTRAND
     A*     can later update these records when the status changes to
     A*     ended or closed.
     A*
     A*----------------------------------------------------------------
     A* Flags vs Id's
     A*
     A*    The 'Flag' field is used for transaction counts. If there
     A*    are multiple records for and employee/tran#, the 'Flag'
     A*    field will only be set for the first one.
     A*
     A*    The 'Id' field will stay on for every record and therfore
     A*    can be used for summaries such as total time for Selection.
     A*
     A*----------------------------------------------------------------
640eAA* Corresponding Idle/Labor Time Notes
     A*
     A*   The PIRTRAND file now associates the time for an IDLE tran
     A*   to the Direct transaction that preceeds it. The IDLE tran
     A*   time is added to the Direct tran ptdidlesec field (Note,
     A*   there may be more than one IDLE tran associated with a
     A*   Direct tran). The Direct tran# will be written into the
     A*   ptdcortran field of the IDLE record.
     A*
     A*   This will also be true for future Labor records, but will
     A*   add the seconds to the ptdlabrsec field.
     A*
     A*----------------------------------------------------------------
     A* Field Notes
     A*
     A*   PTDENDDAY
     A*     The WRTTRAND program will calculate the total seconds from
     A*     the first transaction it finds for the user that occurs
     A*     within 12 hours of the ENDDAY transaction.
     A*
     A*   PTDRCV
     A*     At the time this file was created, no time was being
     A*     tracked for a Receiver.
     A*
     A*   PTDRTN
     A*     At the time this file was created, no time was being
     A*     tracked for a Returner.
     A*
     A*   PTDSHFTDAY
     A*     The shift day is based on the corresponding BEGDAY tran,
     A*     which the WRTTRAND program will create if not found.
     A*
     A*    PTDEADJFLG
     A*     The WRTTRAND program checks the elapsed time between the
     A*     transaction start date/time and end/date time. If the
     A*     elapsed time is greater than a certain amount of time
     A*     (such as 8 hours), then the PTDETS field will be set to
     A*     be 1 hour after the PTDSTS and this flag will be set to 'Y'.
     A*     The PIRTRAN end date/time will be left alone. This
     A*     situation will normally occur if ENDDAY wasn't recorded so
     A*     and IDLE transaction would be open until the user starts a
     A*     new transaction the next day.
     A*
     A*----------------------------------------------------------------
     A          R PTDREC
     A*
     A*  PIRTRAN/PIRTRANH connection fields
     A*
     A            PTDTRAN   R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            PTDGDTE   R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PTDGTIM   R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A*
     A*  Misc fields
     A*
     A*    PTDDETL - Identifies if label detail has been created.
     A*
     A            PTDDETL        1          TEXT('Detail Created')
     A                                      COLHDG('Detail Created')
     A*
     A*    PTDSPLIT - Identifies that transaction was split into
     A*               multiple PIRTRAND records. For example,
     A*               tran was picked by multiple users, or an
     A*               Indirect tasked overlapped Direct Tran.
     A*
     A            PTDSPLIT       1S 0       TEXT('Split')
     A                                      COLHDG('Split')
     A*
     A*    PTDSIMTRAN - Number of simultaneous transaction prcessed
     A*                 at the same time. For example, if a user
     A*                 voice picked 3 trans together, this field
     A*                 would contain 3. Otherwise, this field
     A*                 should always contain 1.
     A*
     A            PTDSIMTRAN     3P 0       TEXT('Sim Trans')
     A                                      COLHDG('Sim Trans')
     A*
     A*    PTDWHSE - Included to allow selection without need of PIRTRAN.
     A*
     A            PTDWHSE   R               REFFLD(WHCODE PIRDICT)
     A*
     A*    PTDSTAT - PIRTRAN status. Used to identify if an updated is needed.
     A*
     A            PTDSTAT        1          TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A*    PTDTASK - This is duplicated in this file so it could be used
     A*              for record selection without needing to join to PIRTRAN.
     A*
     A            PTDTASK   R               REFFLD(TKCODE PIRDICT)
     A*
     A*    PTDEMP# - This is necessary because multiple people can pick a
     A*              single transaction. Each person will end up with their
     A*              own PIRTRAND record.
     A*
     A            PTDEMPNUM      5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A*
     A*    PTDSHFT - This is necessary because multiple people can pick a
     A*              single transaction and each may be in a different shift.
     A*
     A            PTDSHFT   R               REFFLD(SHCODE PIRDICT)
     A*
     A*    PTDSHFTDAY - This is the date the shift started on.
     A*                 (See above notes)
     A*
     A            PTDSHFTDAY     6S 0       TEXT('Shift Day Date')
     A                                      COLHDG('Shift Day Date')
     A*
     A*    PTDSHFTDOW - This is the day of week the shift started on.
     A*                 (1 = Sunday)
     A*
     A            PTDSHFTDOW     1S 0       TEXT('Shift Day of Week')
     A                                      COLHDG('Shift Day' 'of Week')
     A*
     A*    PTDSHFTWK - This is the week the shift started on.
     A*
     A            PTDSHFTWK      2S 0       TEXT('Shift Week')
     A                                      COLHDG('Shift Week')
     A*
     A*    PTDSHFTSUN - This is the Sunday date of the week the shift
     A*                 started on. To be used for selection purposes.
     A*
     A            PTDSHFTSUN     6S 0       TEXT('Shift Sunday of Week')
     A                                      COLHDG('Shift Sunday' 'of Week')
     A*
     A*    PTDCREW - This is necessary because multiple people can pick a
     A*              single transaction and each may be in a different crew.
     A*
     A            PTDCREW   R               REFFLD(CRCODE PIRDICT)
     A*
     A*    PTDZONE - This is only used for selection transactions.
     A*
     A            PTDZONE        6          TEXT('Zone')
     A                                      COLHDG('Zone')
     A*
     A*    PTDGRSPCS - These are the number of pcs the sent to be picked.
     A*
     A            PTDGRSPCS      5P 0       TEXT('Gross Pieces')
     A                                      COLHDG('Gross' 'Pieces')
     A*
640cMA*    PTDGRSMRG - These are the non-bulk merge pcs sent to be picked.
     A*
     A            PTDGRSMRG      5P 0       TEXT('Gross Merge Pieces')
     A                                      COLHDG('Gross Merge' 'Pieces')
640cAA*
640cAA*    PTDGRSBLK - These are the number of bulk merge pcs sent to be picked.
640cAA*
640cAA            PTDGRSBLK      5P 0       TEXT('Gross Bulk Mrg Pieces')
640cAA                                      COLHDG('Gross Bulk Mrg' 'Pieces')
     A*
     A*    PTDPCS - These are the non-merge pcs actually picked.
     A*
     A            PTDPCS         5P 0       TEXT('Net Normal Pieces')
     A                                      COLHDG('Net Normal' 'Pieces')
640bAA*
640bAA*    PTDNRMNV - Flag to indicate that at least one Normal label
640bAA*               was not entered by voice.
640bAA*
640bAA            PTDNRMNV       1S 0       TEXT('Normal Not Voiced Flag')
640bAA                                      COLHDG('Normal' 'Not Voiced Flag')
     A*
640cMA*    PTDMRGPCS - These are the non-bulk merge pcs actually picked.
     A*
     A            PTDMRGPCS      5P 0       TEXT('Net Merge Pieces')
     A                                      COLHDG('Net Merge' 'Pieces')
640cAA*
640cAA*    PTDBLKPCS - These are bulk merge pcs actually picked.
640cAA*
640cAA            PTDBLKPCS      5P 0       TEXT('Net Bulk Mrg Pieces')
640cAA                                      COLHDG('Net Bulk Mrg' 'Pieces')
640bAA*
640bAA*    PTDMRGNV - Flag to indicate that at least one Merge label
640bAA*               was not entered by voice.
640bAA*
640bAA            PTDMRGNV       1S 0       TEXT('Merge Not Voiced Flag')
640bAA                                      COLHDG('Merge' 'Not Voiced Flag')
     A*
     A*    PTDLABELS - These are non-merge labels touched by the selector.
     A*
     A            PTDLABELS      5P 0       TEXT('Labels Touched')
     A                                      COLHDG('Labels' 'Touched')
     A*
640cMA*    PTDMRGLBLS - These are non-bulk merge labels touched by the selector.
     A*
     A            PTDMRGLBLS     5P 0       TEXT('Merge Labels Touched')
     A                                      COLHDG('Merge Labels' 'Touched')
640cAA*
640cAA*    PTDBLKLBLS - These are bulk merge labels touched by the selector.
640cAA*
640cAA            PTDBLKLBLS     5P 0       TEXT('Bulk Mrg Lbls Touched')
640cAA                                      COLHDG('Bulk Mrg Lbls' 'Touched')
     A*
     A*    PTDSLOTS - These are the slots touched by the selector.
     A*
     A            PTDSLOTS       5P 0       TEXT('Slots Touched')
     A                                      COLHDG('Slots' 'Touched')
640cAA*
640cAA*    PTDMRGZNS  - These are non-bulk merge zones touched by the selector.
640cAA*
640cAA            PTDMRGZNS      5P 0       TEXT('Merge Zones Touched')
640cAA                                      COLHDG('Merge Zones' 'Touched')
640cAA*
640cAA*    PTDMRGZNS  - These are bulk merge zones touched by the selector.
640cAA*
640cAA            PTDBLKZNS      5P 0       TEXT('Bulk Mrg Zones Touched')
640cAA                                      COLHDG('Bulk Mrg Zones' 'Touched')
     A*
     A*    PTDSEC - These are the total seconds from Start to End.
     A*
     A            PTDSEC         7P 0       TEXT('Total Seconds')
     A                                      COLHDG('Total' 'Seconds')
640eAA*
640eAA*    PTDIDLESEC - These are total seconds from corresponding Idle trans.
640eAA*                 Only populate for Direct transaction records
640eAA*
640eAA            PTDIDLESEC     7P 0       TEXT('Corresponding Idle Seconds')
640eAA                                      COLHDG('Corres. Idle' 'Seconds')
640eAA*
640eAA*    PTDLABRSEC - These are total seconds from corresponding Labor trans.
640eAA*                 Only populate for Direct transaction records
640eAA*
640eAA            PTDLABRSEC     7P 0       TEXT('Corresponding Labor Seconds')
640eAA                                      COLHDG('Corres. Labor' 'Seconds')
640eAA*
640eAA*    PTDCORTRAN - This is the corresponding Direct trans that an
640eAA*                 Idle/Labor record was applied to.
640eAA*                 Only populate for Idle/Labor records
640eAA*
640eAA            PTDCORTRANR               TEXT('Corresponding Tran')
640eAA                                      COLHDG('Corres.' 'Tran')
640eAA                                      REFFLD(TRAN# PIRDICT)
     A*
     A*  Flags
     A*
     A*    PTDDIR - Direct transaction
     A*             (Doesn't include trans like IDLE, BEG/ENDDAY)
     A*
     A            PTDDIRI        1S 0       TEXT('Direct Id')
     A                                      COLHDG('Direct' 'Id')
     A            PTDDIR         1S 0       TEXT('Direct Flag')
     A                                      COLHDG('Direct' 'Flag')
     A*
     A*    PTDIND - Indirect transaction
     A*             (Doesn't include trans like IDLE, BEG/ENDDAY)
     A*
     A            PTDINDI        1S 0       TEXT('Indirect Id')
     A                                      COLHDG('Indirect' 'Id')
     A            PTDIND         1S 0       TEXT('Indirect Flag')
     A                                      COLHDG('Indirect' 'Flag')
     A*
     A*    PTDIDLE - Idle transaction
     A*
     A            PTDIDLEI       1S 0       TEXT('Idle Id')
     A                                      COLHDG('Idle' 'Id')
     A            PTDIDLE        1S 0       TEXT('Idle Flag')
     A                                      COLHDG('Idle' 'Flag')
     A*
     A*    PTDVOICE - Transaction touched by voice
640bAA*               Note: This does not mean every label was touched.
     A*
     A            PTDVOICEI      1S 0       TEXT('Voice Id')
     A                                      COLHDG('Voice' 'Id')
     A            PTDVOICE       1S 0       TEXT('Voice Flag')
     A                                      COLHDG('Voice' 'Flag')
     A*
     A*    PTDBEGDAY - Begin day transaction
     A*
     A            PTDBEGDAY      1S 0       TEXT('Begin Day Flag')
     A                                      COLHDG('Begin Day' 'Flag')
     A*
     A*    PTDENDDAY - End day transaction
     A*                See notes above.
     A*
     A            PTDENDDAY      1S 0       TEXT('End Day Flag')
     A                                      COLHDG('End Day' 'Flag')
640dAA*
640dAA*    PTDPICK - This is to record if a putaway tran touches a pick slot.
640dAA*
640dAA            PTDPICK        1S 0       TEXT('Pick Slot Flag')
640dAA                                      COLHDG('Pick Slot' 'Flag')
     A*
     A*  Direct/Indirect Sub Flags
     A*    These flags identify the type of a transaction.
     A*    In some cases multiple flags are tied together.
     A*
     A*    The 'Flag' field is used for transaction counts. If there
     A*    are multiple records for and employee/tran#, the 'Flag'
     A*    field will only be set for the first one.
     A*
     A*    The 'Id' field will stay on for every record and therfore
     A*    can be used for summaries such as total time for Selection.
     A*
     A*    PTDRCV - Identifies the Receiver of a putaway transaction
     A*             that is either open or has been put away.
     A*             (see notes above)
     A*
     A            PTDRCVI        1S 0       TEXT('Receive Id')
     A                                      COLHDG('Recieve' 'Id')
     A            PTDRCV         1S 0       TEXT('Receive Flag')
     A                                      COLHDG('Recieve' 'Flag')
     A*
     A*    PTDRCVDLT - Identifies the Receiver of a putaway transaction
     A*                that was deleted.
     A*
     A            PTDRCVDLTI     1S 0       TEXT('Receive Deleted Id')
     A                                      COLHDG('Recieve Deleted' 'Id')
     A            PTDRCVDLT      1S 0       TEXT('Receive Deleted Flag')
     A                                      COLHDG('Recieve Deleted' 'Flag')
     A*
     A*    PTDRCVPUT - Identifies a putaway transaction.
     A*
     A            PTDRCVPUTI     1S 0       TEXT('Receive Putaway Id')
     A                                      COLHDG('Receive Putaway' 'Id')
     A            PTDRCVPUT      1S 0       TEXT('Receive Putaway Flag')
     A                                      COLHDG('Receive Putaway' 'Flag')
     A*
     A*    PTDSTAGE - Identifies a staging transaction.
     A*
     A            PTDSTAGEI      1S 0       TEXT('Stage Id')
     A                                      COLHDG('Stage' 'Id')
     A            PTDSTAGE       1S 0       TEXT('Stage Flag')
     A                                      COLHDG('Stage' 'Flag')
     A*
640dMA*    PTDRPL - Identifies a replenishment tran (REPL).
     A*
     A            PTDRPLI        1S 0       TEXT('Replenish Id')
     A                                      COLHDG('Replenish' 'Id')
     A            PTDRPL         1S 0       TEXT('Replenish Flag')
640dAA*
640dAA*    PTDRPLRF - Identifies a replenishment Tfr tran (REPLTF)
640dAA*
640dAA            PTDRPLTFI      1S 0       TEXT('Repl Tfr Id')
640dAA                                      COLHDG('Repl Tfr' 'Id')
640dAA            PTDRPLTF       1S 0       TEXT('Repl Tfr Flag')
     A                                      COLHDG('Repl Tfr' 'Flag')
     A*
     A*    PTDTFR - Identifies a transfer transaction.
     A*
     A            PTDTFRI        1S 0       TEXT('Transfer Id')
     A                                      COLHDG('Transfer' 'Id')
     A            PTDTFR         1S 0       TEXT('Transfer Flag')
     A                                      COLHDG('Transfer' 'Flag')
     A*
     A*    PTDVERIFY - Identifies a zero verify transaction.
     A*
     A            PTDVFYI        1S 0       TEXT('Verify Id')
     A                                      COLHDG('Verify' 'Id')
     A            PTDVFY         1S 0       TEXT('Verify Flag')
     A                                      COLHDG('Verify' 'Flag')
     A*
     A*    PTDSEL - Identifies a selection transaction that touched
     A*             at least one label.
     A*             PTDFIX, PTDPIR or PTDBULK will also be flagged.
     A*
     A            PTDSELI        1S 0       TEXT('Selection Id')
     A                                      COLHDG('Selection' 'Id')
     A            PTDSEL         1S 0       TEXT('Selection Flag')
     A                                      COLHDG('Selection' 'Flag')
     A*
     A*    PTDSELZERO - Identifies a selection transaction that
     A*                 did not touch any label.
     A*             PTDFIX, PTDPIR or PTDBULK will also be flagged.
     A*
     A            PTDSELZROI     1S 0       TEXT('Zero Sel Id')
     A                                      COLHDG('Zero Sel' 'Id')
     A            PTDSELZERO     1S 0       TEXT('Zero Sel Flag')
     A                                      COLHDG('Zero Sel' 'Flag')
     A*
     A*    PTDFIX - Identifies a FIXED task transaction.
     A*
     A            PTDFIXI        1S 0       TEXT('Fixed Id')
     A                                      COLHDG('Fixed' 'Id')
     A            PTDFIX         1S 0       TEXT('Fixed Flag')
     A                                      COLHDG('Fixed' 'Flag')
     A*
     A*    PTDPIR - Identifies a PIR task transaction.
     A*
     A            PTDPIRI        1S 0       TEXT('PIR Id')
     A                                      COLHDG('PIR' 'Id')
     A            PTDPIR         1S 0       TEXT('PIR Flag')
     A                                      COLHDG('PIR' 'Flag')
     A*
     A*    PTDBULK - Identifies a task that starts with BULK.
     A*
     A            PTDBULKI       1S 0       TEXT('Bulk Id')
     A                                      COLHDG('Bulk' 'Id')
     A            PTDBULK        1S 0       TEXT('Bulk Flag')
     A                                      COLHDG('Bulk' 'Flag')
     A*
     A*    PTDRTN - Identifies the Returner of a return putaway transaction
     A*             that is either open or has been put away.
     A*             (see notes above)
     A*
     A            PTDRTNI        1S 0       TEXT('Return Id')
     A                                      COLHDG('Return' 'Id')
     A            PTDRTN         1S 0       TEXT('Return Flag')
     A                                      COLHDG('Return' 'Flag')
     A*
     A*    PTDRTNPUT - Identifies a return putaway transaction.
     A*
     A            PTDRTNPUTI     1S 0       TEXT('Return Putaway Id')
     A                                      COLHDG('Return Putaway' 'Id')
     A            PTDRTNPUT      1S 0       TEXT('Return Putaway Flag')
     A                                      COLHDG('Return Putaway' 'Flag')
     A*
     A*    PTDEQUIP - Identifies an equipment in/out transaction.
     A*
     A            PTDEQUIPI      1S 0       TEXT('Equip Id')
     A                                      COLHDG('Equip' 'Id')
     A            PTDEQUIP       1S 0       TEXT('Equip Flag')
     A                                      COLHDG('Equip' 'Flag')
     A*
     A*    PTDRUN - Identifies a runner transaction.
     A*
     A            PTDRUNI        1S 0       TEXT('Runner Id')
     A                                      COLHDG('Runner' 'Id')
     A            PTDRUN         1S 0       TEXT('Runner Flag')
     A                                      COLHDG('Runner' 'Flag')
     A*
     A*    PTDLOAD - Identifies a loader transaction.
     A*
     A            PTDLOADI       1S 0       TEXT('Load Id')
     A                                      COLHDG('Load' 'Id')
     A            PTDLOAD        1S 0       TEXT('Load Flag')
     A                                      COLHDG('Load' 'Flag')
     A*
     A*    PTDOTHER - Identifies a transactions that do not fit
     A*               into the other flags.
     A*
     A            PTDOTHERI      1S 0       TEXT('Other Id')
     A                                      COLHDG('Other' 'Id')
     A            PTDOTHER       1S 0       TEXT('Other Flag')
     A                                      COLHDG('Other' 'Flag')
     A*
     A*  Transaction date/time timestamps
     A*
     A*    PTDGTS - Generated date/time converted into a timestamp.
     A*
     A            PTDGTS          Z         TEXT('Generated Timestamp')
     A                                      COLHDG('Generated' 'Timestamp')
     A            PTDGUC          Z         TEXT('Generated UC Timestamp')
     A                                      COLHDG('Generated' 'UC Timestamp')
     A*
     A*    PTDSTS - Started date/time converted into a timestamp.
     A*
     A            PTDSTS          Z         TEXT('Start Timestamp')
     A                                      COLHDG('Start' 'Timestamp')
     A            PTDSUC          Z         TEXT('Start UC Timestamp')
     A                                      COLHDG('Start' 'UC Timestamp')
     A*
     A*    PTDETS - Ended date/time converted into a timestamp.
     A*    PTDEADJFLG - See notes above.
     A*
     A            PTDETS          Z         TEXT('End Timestamp')
     A                                      COLHDG('End' 'Timestamp')
     A            PTDEUC          Z         TEXT('End UC Timestamp')
     A                                      COLHDG('End' 'UC Timestamp')
     A            PTDEADJFLG     1          TEXT('Adjusted Flag')
     A                                      COLHDG('Adjusted' 'Flag')
     A*
     A*    PTDCTS - Closed date/time converted into a timestamp.
     A*
     A            PTDCTS          Z         TEXT('Close Timestamp')
     A                                      COLHDG('Close' 'Timestamp')
     A            PTDCUC          Z         TEXT('Close UC Timestamp')
     A                                      COLHDG('Close' 'UC Timestamp')
     A*
     A*  Add/Change information
     A*
     A            PTDADDTS  R               REFFLD(ADDTS PIRDICT)
     A            PTDADDUC  R               REFFLD(ADDUC PIRDICT)
     A            PTDADDUSR R               REFFLD(ADDUSR PIRDICT)
     A            PTDADDJOB R               REFFLD(ADDJOB PIRDICT)
     A            PTDADDNBR R               REFFLD(ADDNBR PIRDICT)
     A*
     A            PTDCHGTS  R               REFFLD(CHGTS PIRDICT)
     A            PTDCHGUC  R               REFFLD(CHGUC PIRDICT)
     A            PTDCHGUSR R               REFFLD(CHGUSR PIRDICT)
     A            PTDCHGJOB R               REFFLD(CHGJOB PIRDICT)
     A            PTDCHGNBR R               REFFLD(CHGNBR PIRDICT)
     A*
     A          K PTDTRAN
     A          K PTDGDTE
     A          K PTDGTIM
