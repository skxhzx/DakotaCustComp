     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PODLOGP  (PF)
     A*  Description ... POD pallet(tote) log file
     A*  Creator ....... RBD
     A*
     A*  Revisions
     A*
520 AA*    10/20/08  RBD  5.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Status Codes
     A*
     A*    1 = Start file build
     A*    2 = End file build
     A*    3 = Start send
     A*    4 = End send
     A*    9 = Error
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A*                                     UNIQUE
     A          R PODPREC
     A*
     A            PDPWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            PDPRTID   R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            PDPPLID   R               TEXT('Pallet Id')
     A                                      COLHDG('Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            PDPUP#         5S 0       TEXT('Unique piece#')
     A                                      COLHDG('Unique piece#')
     A            PDPSTOP   R               REFFLD(STOP)
     A            PDPORD    R               TEXT('Order Id')
     A                                      COLHDG('Order Id')
     A                                      REFFLD(OHORD)
     A            PDPTRY         2S 0       TEXT('Try')
     A                                      COLHDG('Try')
     A            PDPRTE    R               REFFLD(ROUTE)
     A            PDPCORD   R               TEXT('Customer Order Number')
     A                                      COLHDG('Customer' 'Order Number')
     A                                      REFFLD(OHORD)
     A            PDPSTATUS      1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            PDPFILESND    30A         TEXT('Send File Name')
     A                                      COLHDG('Send File Name')
     A            PDPFILERCV    30A         TEXT('Recv File Name')
     A                                      COLHDG('Recv File Name')
     A            PDPFILETRY    33A         TEXT('Recv File Name w/try')
     A                                      COLHDG('Recv File Name w/try')
     A            PDPRCVXML      1A         TEXT('Recv is XML (Y,N)')
     A                                      COLHDG('Recv is' 'XML (Y,N)')
     A            PDPERRCODE    10A         TEXT('Error Code')
     A                                      COLHDG('Error Code')
     A            PDPERRMSG     60A         TEXT('Error Message')
     A                                      COLHDG('Error Message')
     A*
     A* Timestamp / User tracking information.
     A*   PGM2 is the actual program that created the record.
     A*   PGM is the secondary program that called the actual program.
     A*
     A            PDPTS1    R               REFFLD(ADDTS)
     A                                      COLHDG('File Start TS')
     A            PDPUC1    R               REFFLD(ADDUC)
     A                                      COLHDG('File Start UC')
     A            PDPTS2    R               REFFLD(ADDTS)
     A                                      COLHDG('File End TS')
     A            PDPUC2    R               REFFLD(ADDUC)
     A                                      COLHDG('File End UC')
     A            PDPCALL1  R               REFFLD(ADDPGM)
     A                                      COLHDG('File Start' 'Call Pgm')
     A            PDPPGM1   R               REFFLD(ADDPGM)
     A                                      COLHDG('File Start' 'Actual Pgm')
     A            PDPUSER1  R               REFFLD(ADDUSR)
     A                                      COLHDG('File Start' 'User')
     A            PDPJOB1   R               REFFLD(ADDJOB)
     A                                      COLHDG('File Start' 'Job')
     A            PDPNBR1   R               REFFLD(ADDNBR)
     A                                      COLHDG('File Start' 'Job#')
     A*
     A            PDPTS3    R               REFFLD(ADDTS)
     A                                      COLHDG('Send Start TS')
     A            PDPUC3    R               REFFLD(ADDUC)
     A                                      COLHDG('Send Start UC')
     A            PDPTS4    R               REFFLD(ADDTS)
     A                                      COLHDG('Send End TS')
     A            PDPUC4    R               REFFLD(ADDUC)
     A                                      COLHDG('Send End UC')
     A            PDPCALL3  R               REFFLD(ADDPGM)
     A                                      COLHDG('Send Start' 'Call Pgm')
     A            PDPPGM3   R               REFFLD(ADDPGM)
     A                                      COLHDG('Send Start' 'Actual Pgm')
     A            PDPUSER3  R               REFFLD(ADDUSR)
     A                                      COLHDG('Send Start' 'User')
     A            PDPJOB3   R               REFFLD(ADDJOB)
     A                                      COLHDG('Send Start' 'Job')
     A            PDPNBR3   R               REFFLD(ADDNBR)
     A                                      COLHDG('Send Start' 'Job#')
     A*
     A            PDPTS9    R               REFFLD(ADDTS)
     A                                      COLHDG('Stat Upd TS')
     A            PDPUC9    R               REFFLD(ADDUC)
     A                                      COLHDG('Stat Upd UC')
     A*
     A          K PDPWHSE
     A          K PDPRTID
     A          K PDPPLID
     A          K PDPUP#
     A          K PDPTRY
