     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICPHRASE (PF)
     A*  Description ... License tracking phrase file
     A*  Creator ....... DAS
     A*  Date .......... July 9, 2008
     A*
     A*  Revisions:
     A*
600 AA*    07/09/08  DAS  6.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*    This file contains the phrase detail (history) for license
     A*    tracking. Since the file only contains a phrase id a
     A*    program is necessary to display the phrases.
     A*
     A*----------------------------------------------------------------
     A*  Variables (PHLVARS)
     A*
     A*    Information in this field should be defined as follows:
     A*
     A*       llddddllddddddllddddd
     A*
     A*    where 'd' is the variable data and the 'll' that preceds
     A*    is identifies the data's length.
     A*
     A*    Example:
     A*
     A*       0250070283437
     A*       llddllddddddd
     A*
     A*    Translates to
     A*
     A*       Var 1 = 50 (preceded by length of 02)
     A*       Var 2 = 0283437 (preceded by length of 07)
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R PHLREC
     A*
     A            PHLRECID      11P 0       TEXT('Record Id')
     A                                      COLHDG('Record Id')
     A            PHLID          5P 0       TEXT('Phrase Id')
     A                                      COLHDG('Phrase' 'Id')
     A*
     A            PHLWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            PHLEMP#        5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            PHLUSER       10          TEXT('User')
     A                                      COLHDG('User')
     A            PHLPGM        10          TEXT('Program')
     A                                      COLHDG('Program')
     A            PHLITEM   R               REFFLD(ITITEM)
     A            PHLWHDP   R               REFFLD(WDCODE)
     A            PHLLOC        15A         TEXT('Location')
     A                                      COLHDG('Location')
     A            PHLTRN#   R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            PHLLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            PHLLTRN   R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A*
     A            PHLVARS      100A         TEXT('Variables')
     A                                      COLHDG('Variables')
     A*
     A            PHLADDTS  R               REFFLD(ADDTS)
     A            PHLADDUC  R               REFFLD(ADDUC)
     A            PHLADDPGM R               REFFLD(ADDPGM)
     A            PHLADDUSR R               REFFLD(ADDUSR)
     A            PHLADDJOB R               REFFLD(ADDJOB)
     A            PHLADDNBR R               REFFLD(ADDNBR)
