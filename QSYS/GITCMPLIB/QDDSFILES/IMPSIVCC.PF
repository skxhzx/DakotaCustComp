     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSIVCC (PF)
     A*  Description ... Staging Import - Invoice Catch Weights
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740 AA*    10/30/20  MLB  7.40
     A*      - Created
     A*      - Base on EXPFORDC
     A*
     A*----------------------------------------------------------------
     A          R IVCREC
     A*
     A            IVCWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IVCBATID      11          TEXT('Import Batch Id')
     A                                      COLHDG('Import' 'Batch Id')
     A            IVCRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            IVCORDA       15          TEXT('Alpha Order Number')
     A                                      COLHDG('Alpha Order' 'Number')
     A            IVCINVOICE    15          TEXT('Alpha Invoice Number')
     A                                      COLHDG('Alpha Invoice' 'Number')
     A            IVCOSQ         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            IVCITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            IVCSEQ         5S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            IVCSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IVCSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVCSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVCTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being Imported
     A*
     A            IVCRTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            IVCORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            IVCWGT         7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A*
     A*           IVCLBL         7S 0       TEXT('Label Number')
     A*                                     COLHDG('Label' 'Number')
     A*           IVCTRN         7S 0
     A*                                     TEXT('Transaction Number')
     A*                                     COLHDG('Transaction' 'Number')
     A*
     A*  Add stamps
     A*
     A            IVCADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IVCADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IVCWHS
     A          K IVCBATID
     A          K IVCINVOICE
     A          K IVCORDA
     A          K IVCOSQ
     A          K IVCSEQ
     A          K IVCITM
