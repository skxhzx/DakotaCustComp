     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PWRKORDR2   (LF)
     A*  Description ... Production work orders by rm item, fg item,
     A*                  joined to header work orders, exclude status D
     A*  Creator ....... RBD
     A*  Date .......... Nov 30, 2015
     A*
     A*  Revision:
     A*
700  A*    11/30/15  RBD  7.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*    In order to use the JDFTVAL keyword the DYNSLT keywork
     A*    would also have to be used. Because DYNSLT might slow down
     A*    I/O we have decided to leave the JDFTVAL keyword out. This
     A*    means that non-deleted work orders without a corresponding
     A*    work order header record would be ignored.
     A*
     A*----------------------------------------------------------------
     A          R WRREC2                    JFILE(PWRKORDR PWRKORD)
     A*
     A          J                           JOIN(PWRKORDR PWRKORD)
     A                                      JFLD(WRWHSE  WOWHSE)
     A                                      JFLD(WRWO#   WOWO#)
     A*
     A*  Fields from PWRKORDR file.
     A*
     A            WRWHSE
     A            WRWO#
     A            WRITEM
     A            WRFGI
     A            WRQTYN
     A            WRQTY1
     A            WRQTY2
     A            WRSTAT
     A*
     A*  Fields from PWRKORD file.
     A*
     A            WOWHSE
     A            WOWO#
     A            WOMFGNO
     A            WOCUST
     A            WOCUSTWO
     A            WODTETIM
     A*
     A          K WRWHSE
     A          K WRITEM
     A          K WRFGI
     A          S WRSTAT                    COMP(NE 'D')
