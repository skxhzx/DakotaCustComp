     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... WORKLB31 (LF)
     A*  Description ... WorkLabel join file for printing Blk and Repl.
     A*  Date .......... November 19, 2003
     A*  Creator ....... RH
     A*
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R LJREC31                   JFILE(WORKLBL PLTSUM PIRITEM +
     A                                            ORDH ORDD UNMESR)
     A*
     A          J                           JOIN(WORKLBL PLTSUM)
     A                                      JFLD(LWWHSE  PSWHSE)
     A                                      JFLD(LWRTE   PSRTID)
     A                                      JFLD(LWPAL#  PSPLID)
     A*
     A          J                           JOIN(WORKLBL PIRITEM)
     A                                      JFLD(LWWHSE  ITWHSE)
     A                                      JFLD(LWITEM  ITITEM)
     A*
     A          J                           JOIN(WORKLBL ORDH)
     A                                      JFLD(LWWHSE  OHWHSE)
     A                                      JFLD(LWORD   OHORD)
     A*
     A          J                           JOIN(WORKLBL ORDD)
     A                                      JFLD(LWWHSE  ODWHSE)
     A                                      JFLD(LWORD   ODORD)
     A                                      JFLD(LWSEQ   ODSEQ)
     A                                      JFLD(LWITEM  ODITEM)
     A*
     A          J                           JOIN(WORKLBL UNMESR)
     A                                      JFLD(LWUCOD  UMCODE)
     A*
     A*  Fields from WORKLBL file.
     A*
     A            LWWHSE
     A            LWWHDP
     A            LWSEQ
     A            LWITEM
     A            LWPBAT
     A            LWAISL
     A            LWLOC
     A            LWRLVL
     A            LWHAND
     A            LWPSEQ
     A            LWSTYP
     A            LWQRYF
     A            LWDISP
     A            LWQAVL
     A            LWWHS2
     A            LWWHD2
     A            LWASL2
     A            LWLOC2
     A            LWLVL2
     A            LWHND2
     A            LWDSP2
     A            LWRTE
     A            LWSTOP
     A            LWORD
     A            LWSPCL
     A            LWPO
     A            LWUTYP
     A            LWUCOD
     A            LWUCUB
     A            LWCUBE
     A            LWUWGT
     A            LWSWGT
     A            LWQALC
     A            LWQRMN
     A            LWQPCK
     A            LWTIE
     A            LWHIGH
     A            LWGRP1
     A            LWGRP2
     A            LWTYPE
     A            LWLBL#
     A            LWPAL#
     A            LWTRN#
     A            LWCTR#
     A            LWSTAT
     A            LWPRTY
     A            LWSDTE
     A            LWSTIM
     A            LWVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPAL#
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
     A*
     A*  Fields from PIRITEM file.
     A*
213 AA            ITTYPE
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
214 AA            ITFLG1
     A            ITUM3
     A            ITUMQ3
214 AA            ITFLG2
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
214 AA            ITREOR
214 AA            ITREPL
214 AA            ITRCVD
214 AA            ITFLGD
214 AA            ITFLGB
214 AA            ITSLIF
214 AA            ITNDAY
     A*
     A*  Fields from ORDH file.
     A*
214 AA            OHORIG
     A            OHTYPE
     A            OHCUST
214 AA            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
214 AA            OHPBAT
214 AA            OHSTAT
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Key fields
     A*
     A          K LWWHSE
     A          K LWRTE
     A          K LWTYPE
     A          K LWPAL#
