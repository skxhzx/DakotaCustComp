     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSRTN (PF)
     A*  Description ... Staging Export - Customer Returns
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    11/30/12  MLB  6.40
     A*      - Created
     A*      - Based on EXPFRTN
     A*
720aAA*    12/13/18  DAS  7.30
     A*      - Added comments from EXPFRTN source. File did not change.
     A*
     A*----------------------------------------------------------------
     A*
720aAA*  Notes
     A*
     A*    The original transaction number (ORTRN#) ties a return
     A*    back to a specific employee when EPM is being used.
     A*
     A*    The corresponding item number (OECITM) was added to help
     A*    track which item was actually mispicked.
     A*
     A*----------------------------------------------------------------
     A*
720aAA*  Codes
     A*
     A*    Return types (OETYPE)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OEREC
     A*
     A            OEWHSE    R               REFFLD(WHCODE)
     A            OEBATID       11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            OESTATUS       1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            OESTSTS         Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OESTSUC         Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OETEXT       200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            OEORD          7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEITEM    R               REFFLD(ITITEM)
     A            OEQTY1         5S 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            OEQTY2         5S 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            OEQTY3         5S 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
     A            OECWTR         9S 2       TEXT('Catchweight Returned')
     A                                      COLHDG('Catchweight' 'Returned')
     A            OERDTE    R               TEXT('Return Date (YYYYMMDD)')
     A                                      COLHDG('Return' 'Date')
     A                                      REFFLD(DTECMD)
     A            OERTIM    R               TEXT('Return Time')
     A                                      COLHDG('Return' 'Time')
     A                                      REFFLD(TIMHMS)
     A            OERUSR        10A         TEXT('Returned By')
     A                                      COLHDG('Returned' 'By')
     A            OETYPE         1A         TEXT('Return Type')
     A                                      COLHDG('Return' 'Type')
     A            OETRN#         7S 0       TEXT('Original Tran#')
     A                                      COLHDG('Original' 'Tran#')
     A            OECITM    R               TEXT('Corresponding Item')
     A                                      COLHDG('Corresponding' 'Item')
     A                                      REFFLD(ITITEM)
     A            OERCOD    R               REFFLD(RNCODE)
     A            OEDCOD    R               REFFLD(RDCODE)
     A            OEIINV         1A         TEXT('Impact Inventory')
     A                                      COLHDG('Impact' 'Inv.')
     A            OEOLB#         7S 0       TEXT('Original Label')
     A                                      COLHDG('Orig.' 'Label#' )
     A            OEINV#        12A         TEXT('Orig. Invoice#')
     A                                      COLHDG('Orig.' 'Invoice#')
     A            OERMEM        30A         TEXT('Memo')
     A                                      COLHDG('Memo')
     A*
     A*  Add stamps
     A*
     A            OEADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OEADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K OEWHSE
     A          K OEBATID
     A          K OEORD
     A          K OEITEM
     A          K OERDTE
     A          K OERTIM
