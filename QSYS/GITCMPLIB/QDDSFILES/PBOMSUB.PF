     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PBOMSUB   (PF)
     A*  Description ... PRODUCTION SUBSTITUTE BOM FILE
     A*  Creator ....... TAB
     A*
     A*
     A*  Revisions
     A*
     A*      - KDE - 10/25/2016
     A*        Created
710 AA*      - KDE - 02/02/2017
     A*           Added field to identify raw material(s) that are used for production
     A*           of Finished Good Product
     A*
     A*----------------------------------------------------------------
     A*
     A*    Substitute Bill of Materials can use Dakota normal quantities
     A*    SBQTYN, SBQTY1, and SBQTY2 relate Case Qty, ITUMQ1, ITUMQ2
     A*    The User can also chose to use the Production Breakdown
     A*    Quantity.  Field SBPRDQ is the quantity of this breakdown
     A*    that this finished good requires.  The Substitute BOM
     A*    also has a flag SBPQUF which is populated with a "Y"
     A*    if the SBPRDQ is used and a "N" if the normal Dakota
     A*    quantities are used.
     A*
     A*    SBQTYN - Defines how many normal case quantity req
     A*    SBQTY1 - Defines how many Dakota BD1 quantity req
     A*    SBQTY2 - Defines how many Dakota BD2 quantity req
     A*    SBPRDQ - Defines how many Prod Qty BD req
     A*    SBPQUF - "Y" means Prod Qty used, "N" Dakota Quantities
     A*
     A*    The item definitions for the Production specific
     A*    definitions can be found in file ITEMPRD
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R SBREC
     A*
     A            SBWHSE         3P 0       TEXT('Whse')
     A                                      COLHDG('Whse')
     A            SBFGI         15          TEXT('Finished Good Item')
     A                                      COLHDG('FG Item#')
     A            SBRMI         15          TEXT('Raw Material Item')
     A                                      COLHDG('RM Item#')
     A            SBAMI         15          TEXT('Alt Raw Material Item')
     A                                      COLHDG('Alt RM Item#')
     A            SBSEQ          4P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq Number')
     A            SBQTYN         5P 0       TEXT('Qty Normal')
     A                                      COLHDG('RM Qty Nrm')
     A            SBQTY1         3P 0       TEXT('Qty BrkDwn 1')
     A                                      COLHDG('RM Qty BrkDwn1')
     A            SBQTY2         3P 0       TEXT('Qty BrkDwn 2')
     A                                      COLHDG('RM Qty BrkDwn2')
     A            SBPRDQ         9P 4       TEXT('PRD Qty Breakdown')
     A                                      COLHDG('PRD BrkDwn')
     A            SBSCRF         5P 4       TEXT('Raw Material Scrap')
     A                                      COLHDG('RM SCRP Factor')
     A            SBTYPE         1          TEXT('Raw Material Issue Type')
     A                                      COLHDG('RM Type Issue')
     A            SBBCKF         1          TEXT('Raw Material Back Flush')
     A                                      COLHDG('RM Back Flush')
     A            SBPQUF         1          TEXT('Prod Qty Use Flag')
     A                                      COLHDG('PRD Qty Use Flag')
     A            SBEFFD         8  0       TEXT('Effective Date')
     A                                      COLHDG('Eff. Date')
     A            SBEXPD         8  0       TEXT('Expiration Date')
     A                                      COLHDG('Exp. Date')
     A            SBWO#         13          TEXT('Work Order#')
     A                                      COLHDG('Work Order#')
710 AA            SBUSEP         1          TEXT('Used in FG Item')
710 AA                                      COLHDG('Used in FG ITM')
     A*
     A          K SBWHSE
     A          K SBFGI
     A          K SBRMI
     A          K SBAMI
