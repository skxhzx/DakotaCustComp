     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSPLBL
     A*  Description ... Staging Export - Pick Labels
     A*  Creator ....... RTR
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFPLBL.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
650  A*   12/10/13  RTR  6.50
     A*      - Created.
710a A*   12/10/13  RTR  6.50
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A          R LBEREC
     A*
     A* Main key fields
     A*
     A            LBEWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A*
     A*    Status info
     A*
     A            LBEBATID      11          TEXT('Import Batch Id')
     A                                      COLHDG('Import' 'Batch Id')
     A            LBESTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            LBESTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            LBESTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            LBETEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A            LBERID         5          TEXT('FS-WMS Route Id')
     A                                      COLHDG('FS-WMS' 'Route Id')
     A            LBESEQ         7S 0       TEXT('Print Seq#')
     A                                      COLHDG('Print' 'Seq#')
     A            LBEZON         6          TEXT('Pick Zone')
     A                                      COLHDG('Pick' 'Zone')
     A            LBETYP         1          TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            LBETR#         7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            LBEWHD         5          TEXT('Warehouse Department')
     A                                      COLHDG('Warehouse' 'Department')
     A            LBESLT        12          TEXT('Slot')
     A                                      COLHDG('Slot')
     A*
     A* Item information fields
     A*
     A            LBEITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            LBEITP         1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            LBEDSC        30          TEXT('Item Description')
     A                                      COLHDG('Item' 'Description')
     A            LBEPAK        15          TEXT('Pack Description')
     A                                      COLHDG('Pack' 'Description')
     A            LBEUTP         1          TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            LBEUCD         2          TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A            LBECUB         9S 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            LBEWGT         9S 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            LBEQTY         5S 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            LBECWF         1          TEXT('Catchweight Flag')
     A                                      COLHDG('Catchweight' 'Flag')
     A*
     A* Order information fields
     A*
     A            LBERTE         5          TEXT('Route')
     A                                      COLHDG('Route')
710aDA***         LBEORD         7S 0       TEXT('Order Number')
710aMA            LBEORD        30A         TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
710aDA***         LBEDSQ         3S 0       TEXT('Order Detail Seq#')
710aMA            LBEDSQ        20A         TEXT('Order Detail Seq#')
     A                                      COLHDG('Order Detail' 'Seq#')
     A            LBECUS        11S 0       TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            LBESTP         5S 0       TEXT('Extended Stop')
     A                                      COLHDG('Ext' 'Stop')
     A*
     A* Misc information fields
     A*
     A            LBEZTP         5          TEXT('Pick Zone Type')
     A                                      COLHDG('Pick Zone' 'Type')
     A            LBEMZN         6          TEXT('Merge Zone')
     A                                      COLHDG('Merge' 'Zone')
     A            LBEOUT         3          TEXT('Out Label Flag')
     A                                      COLHDG('Out Label' 'Flag')
     A            LBECOM         1          TEXT('Truck Compartment')
     A                                      COLHDG('Truck' 'Compartment')
     A            LBEPL#         7S 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            LBEPLID        7S 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            LBELB#         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            LBEDTE         8S 0       TEXT('Date (YYYYMMDD)')
     A                                      COLHDG('Date')
     A            LBETIM         6S 0       TEXT('Time (HHMMSS)')
     A                                      COLHDG('Time')
     A            LBEMS1        40A         TEXT('Miscellaneous 1')
     A                                      COLHDG('Miscellaneous 1')
     A            LBEMS2        40A         TEXT('Miscellaneous 2')
     A                                      COLHDG('Miscellaneous 2')
     A*
     A          K LBEWHS
     A          K LBEBATID
     A          K LBERID
     A          K LBESEQ
