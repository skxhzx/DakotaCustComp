     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMPRD (PF)
     A*  Description ... PRODUCTION PIRITEM FIELDS
     A*  Date .......... October 29, 2014
     A*  Creator ....... KDE
     A*
     A*  Revisions
710 AA*      - KDE - 02/02/2017
     A*           Added field to identify raw material(s) that are used for production
     A*           of Finished Good Product
710AAA*      - KDE - 02/15/2017
     A*           Added field to capture Mfg Shelf Days when tagged Date Sensitive in PIRITEM
     A*           Added field to capture whether item requires MultiDays to Produce
710bAA*      - MR  - 11/20/2017 7.10b
     A*           Add Min,Max and Lot Qty fields.
     A*----------------------------------------------------------------
     A*
     A*    IPPRDQ  -  Production Quantity Breakdown allows the system
     A*               to define a breakdown per a case that is used
     A*               for production and not seen in other BFC modules
     A*               Pounds would be a common breakdown for meat.
     A*               When product is moved or used the system will
     A*               ultimately get to the lowest unit of measure
     A*               per inventory breakdowns and either issue or
     A*               consume based on that factor.  It is in the
     A*               customers best interest to have more than a
     A*               case breakdown to allow for less movement when
     A*               less than a case is moved or consumed.
     A*
     A*    IPPRIM  -  This will be set to a "Y" if this Raw Material
     A*               is considered a Primal cut.  This is valid only
     A*               for Raw Materials and a Primal MUST have at least
     A*               one Sub-Primal defined.  A Primal for a Cow would
     A*               be the Short Loin, and a sub-primal would be the
     A*               Tenderloin.  These sub primal definitions are
     A*               stored in table PSUBPRIM where typical
     A*               percentages will be defined for each sub-primal
     A*               which adds up to 100%
     A*
     A*    IPSUBP  -  Sub definition of Primal Cut of meat.  Refer to
     A*               text above for field IPPRIM for better
     A*               understanding.
     A*
     A*    IPBRKP  -  If Y(Yes) replenishments for this item will
     A*               create a separate record in PRPLFIL for case
     A*               and breakdown quantity.  It will only generate
     A*               a replenishment at the breakdown one level.
     A*
710 AA*    IPUSEP  -  This indicator says whether the actual raw material
710 AA*               is used to generate the specific Finished good.
710 AA*               A N(No) indicates the raw material is something like
710 AA*               packing material or a tray or something that is not
710 AA*               actually part of the finished good itself
     A*
710aAA*    IPMDAY  -  Mfg Shelf Days required when Date Sensitive item
     A*
710aAA*    IPMULTI -  Identifies Item as one that requires Multiple days to produce
     A*
710bAA*    IPMINQT -  Minimum Quantity                                 s to produce
     A*
710bAA*    IPMAXQT -  Maximum Quantity                                 s to produce
     A*
710bAA*    IPLOTQT -  Lot Quantity                                     s to produce
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R IPREC
     A*
     A            IPWHSE    R               REFFLD(WHCODE)
     A            IPITEM    R               REFFLD(ITITEM)
     A            IPPRDFLG       1          TEXT('Prod flag')
     A                                      COLHDG('Prod' 'Flag')
     A            IPMFGNO        3  0       TEXT('MFG Line')
     A                                      COLHDG('MFG Line')
     A            IPMORP         1          TEXT('Meat or Produce')
     A                                      COLHDG('Item' 'Type' 'Flag')
     A            IPPUOM         2          TEXT('Produdction UOM')
     A                                      COLHDG('Prd UOM')
     A            IPPRDQ         9  4       TEXT('PRD Qty BrkDwn')
     A                                      COLHDG('Prd BrkQ')
     A            IPPRIM         1          TEXT('RM Primal Item')
     A                                      COLHDG('Primal')
     A            IPSUBP         2  0       TEXT('Sub Primal')
     A                                      COLHDG('Sub-Primal')
     A            IPBRKR         1          TEXT('BrkDwn Replen')
     A                                      COLHDG('BrkDwn Replen')
710 AA            IPUSEP         1          TEXT('Used in FG Item')
710 AA                                      COLHDG('Used in FG ITM')
710aAA            IPMDAY         3  0       TEXT('Mfg Shelf Days')
710aAA                                      COLHDG('Mfg Shelf Days')
710aAA            IPMULTI        1          TEXT('Multi Day Line Req.')
710aAA                                      COLHDG('Multi Line Req')
710bAA            IPMINQT        5  0       TEXT('Minimum Qty')
710bAA                                      COLHDG('Minimum Qty')
710bAA            IPMAXQT        5  0       TEXT('Maximum Qty')
710bAA                                      COLHDG('Maximum Qty')
710bAA            IPLOTQT        5  0       TEXT('Lot Qty')
710bAA                                      COLHDG('Lot Qty')
     A*
     A          K IPWHSE
     A          K IPITEM
