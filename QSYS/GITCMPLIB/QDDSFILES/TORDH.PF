     A*----------------------------------------------------------------
     A*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TORDH  (PF)
     A*  Description ... Transfer Order Header file
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
500 AA*    07/21/05  MLB  5.00
     A*      - Fix: Revised DDS to reference field TFCODE instead of
     A*        field TRTCOD.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    Status codes
     A*
     A*       '1'  -  Open
     A*       '2'  -  Released
     A*       '3'  -  Authorized
     A*       '4'  -  Processing
     A*       '5'  -  Picked
     A*       '6'  -  Received
     A*       '9'  -  Closed
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R TOHREC
     A*
     A            TOHNUM    R               REFFLD(TOHNUM)
     A            TOHFWH    R               REFFLD(WHCODE)
     A                                      TEXT('From Warehouse')
     A                                      COLHDG('From' 'Warehouse')
     A            TOHTWH    R               REFFLD(WHCODE)
     A                                      TEXT('To Warehouse')
     A                                      COLHDG('To' 'Warehouse')
500 DA*           TOHRSN    R               REFFLD(TRTCOD)
500 MA            TOHRSN    R               REFFLD(TFCODE)
     A            TOHRQB        10          TEXT('Requested By')
     A                                      COLHDG('Requested' 'By')
     A            TOHRQD    R               TEXT('Requested Date')
     A                                      COLHDG('Requested' 'Date')
     A                                      REFFLD(DTECMD)
     A            TOHRQT    R               TEXT('Requested Time')
     A                                      COLHDG('Requested' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TOHNDD    R               TEXT('Need by Date')
     A                                      COLHDG('Need by' 'Date')
     A                                      REFFLD(DTECMD)
     A            TOHAUB        10          TEXT('Authorized By')
     A                                      COLHDG('Authorized' 'By')
     A            TOHAUD    R               TEXT('Authorized Date')
     A                                      COLHDG('Authorized' 'Date')
     A                                      REFFLD(DTECMD)
     A            TOHAUT    R               TEXT('Authorized Time')
     A                                      COLHDG('Authorized' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TOHCO#    R               REFFLD(OHORD)
     A                                      TEXT('Customer Order Number')
     A                                      COLHDG('Customer' 'Order Number')
     A            TOHCC#    R               REFFLD(CUSNUM)
     A            TOHCPO        15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            TOHCNM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A            TOHCA1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            TOHCA2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            TOHCCT        20          TEXT('City')
     A                                      COLHDG('City')
     A            TOHCST         2          TEXT('State')
     A                                      COLHDG('State')
     A            TOHCZP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            TOHRTE    R               REFFLD(ROUTE)
     A            TOHSTP    R               REFFLD(STOP)
     A            TOHTRK        10          TEXT('Truck')
     A                                      COLHDG('Truck')
     A            TOHMSC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A            TOHSTS         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            TOHSDT    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            TOHSTM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K TOHNUM
