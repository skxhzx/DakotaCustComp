     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LAYSLOT  (PF)
     A*  Description ... Slot file
     A*  Creator ....... DAS/HNK
     A*
     A*  Revisions
     A*
     A*    Ver 2.00   07/22/94  DAS
     A*      - Major change. Combined ITEMQTY and SLOT files. Added
     A*        new fields. Split qty into 3 fields.
     A*
     A*    Ver 2.01   09/29/94  DAS
     A*      - Added transfer quantity.
     A*
     A*    Ver n/a    10/28/94  DAS
     A*      - Changed meaning of transfer qty from "not printed on
     A*        labels" to being printed on labels.
     A*
213 AA*    Ver 2.02   05/13/96  DAS
213 AA*      - Added new fields SLSIDE and SLSLFD.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status code (SLSTAT)
     A*
     A*      'A'  -  Active slot. Item occupies slot.
     A*      'I'  -  Inactive slot. Do not use slot.  .
     A*      'V'  -  Verified slot.  Ready to be put to use.
     A*      'Z'  -  Zero slot.  Slot quantity went to zero. Must be
     A*              verified before it can be reused.
     A*
     A*      Other status codes will be treated as inactive.
     A*
     A*    Side code (SLSIDE)
     A*
     A*      0  -  Even
     A*      1  -  Odd
     A*
     A*----------------------------------------------------------------
     A*
     A*  Fixed slot notes:
     A*
     A*    A fixed slot can be reserved for one item by entering an
     A*    item number for that slot through the slot maintenance
     A*    program.
     A*
     A*    If a fixed slot is not reserved for an item it is considered
     A*    a floating slot and is treated like a PIR slot.
     A*
     A*----------------------------------------------------------------
     A*
     A*  PIR slot notes:
     A*
     A*    PIR slots are floating slots and are never reserved for
     A*    a particular item number.  When the quantity of a PIR slot
     A*    goes to zero its status is changed to 'Z'.  The slot must
     A*    then be manually verified that it is empty before its status
     A*    is changed to 'V' and it can be reused.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Quantity notes:
     A*
     A*    Each quantity type (ie. Stock qty) has three parts. Qty1
     A*    is the normal quantity. Qty2 is the breakdown 1 quantity and
     A*    Qty3 is the breakdown 2 quantity. The unit of measure for
     A*    each of these quantities is defined in the item file.
     A*
     A*    Type            Description
     A*    -------------   -------------------
     A*    Stock           Physical quantity in slot.
     A*
     A*    Allocated       Quantity selected for picking but not yet
     A*                    printed on labels.
     A*
     A*    Transfer        Quantity printed on labels for transfer to
     A*                    another slot.
     A*
     A*    Picking         Quantity printed on labels for picking.
     A*
     A*    Receiving       Quantity printed on labels to be received
     A*                    into this slot.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R LLREC
     A*
     A            LLLNBR    R               REFFLD(WHCODE)
     A            LLWHDP    R               REFFLD(WDCODE)
     A*
     A            LLAISL    R               REFFLD(ASCODE)
     A            LLLOC     R               REFFLD(SLLOC)
     A            LLRLVL    R               REFFLD(SLRLVL)
     A            LLHAND    R               REFFLD(HSCODE)
213 AA            LLSIDE         1S 0       TEXT('Side')
213 AA                                      COLHDG('Side')
     A*
     A            LLSTYP    R               REFFLD(SLSTYP)
     A*
     A            LLDISP        12          TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            LLSTAT    R               REFFLD(SSCODE)
     A            LLSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            LLSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            LLSDEF    R               REFFLD(SLSDEF)
     A            LLDESG    R               REFFLD(DSCODE)
     A            LLACTV    R               REFFLD(ACCODE)
     A            LLBLD     R               REFFLD(BCCODE)
     A            LLPICK         1          TEXT('Pick Slot')
     A                                      COLHDG('Pick Slot')
     A            LLPSEQ         5P 0       TEXT('Picking Sequence')
     A                                      COLHDG('Picking' 'Sequence')
     A            LLPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            LLENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            LLEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
213 AA            LLSLFD    R               TEXT('Shelf Life Date')
213 AA                                      COLHDG('Shelf Life' 'Date')
213 AA                                      REFFLD(DTECMD)
     A            LLRSRV         1          TEXT('Reserved')
     A                                      COLHDG('Reserved')
     A            LLITEM    R               REFFLD(ITITEM)
     A*
     A            LLSTK1         5P 0       TEXT('Stock Qty1')
     A                                      COLHDG('Stock' 'Qty1')
     A            LLSTK2         3P 0       TEXT('Stock Qty2')
     A                                      COLHDG('Stock' 'Qty2')
     A            LLSTK3         3P 0       TEXT('Stock Qty3')
     A                                      COLHDG('Stock' 'Qty3')
     A*
     A            LLALC1         5P 0       TEXT('Allocated Qty1')
     A                                      COLHDG('Allocated' 'Qty1')
     A            LLALC2         3P 0       TEXT('Allocated Qty2')
     A                                      COLHDG('Allocated' 'Qty2')
     A            LLALC3         3P 0       TEXT('Allocated Qty3')
     A                                      COLHDG('Allocated' 'Qty3')
     A*
     A            LLTFR1         5P 0       TEXT('Transfer Qty1')
     A                                      COLHDG('Transfer' 'Qty1')
     A            LLTFR2         3P 0       TEXT('Transfer Qty2')
     A                                      COLHDG('Transfer' 'Qty2')
     A            LLTFR3         3P 0       TEXT('Transfer Qty3')
     A                                      COLHDG('Transfer' 'Qty3')
     A*
     A            LLPCK1         5P 0       TEXT('Picking Qty1')
     A                                      COLHDG('Picking' 'Qty1')
     A            LLPCK2         3P 0       TEXT('Picking Qty2')
     A                                      COLHDG('Picking' 'Qty2')
     A            LLPCK3         3P 0       TEXT('Picking Qty3')
     A                                      COLHDG('Picking' 'Qty3')
     A*
     A            LLRCV1         5P 0       TEXT('Receive Qty1')
     A                                      COLHDG('Receive' 'Qty 1')
     A            LLRCV2         3P 0       TEXT('Receive Qty2')
     A                                      COLHDG('Receive' 'Qty 2')
     A            LLRCV3         3P 0       TEXT('Receive Qty3')
     A                                      COLHDG('Receive' 'Qty 3')
     A*
     A          K LLWHDP
     A          K LLAISL
     A          K LLLOC
     A          K LLRLVL
     A          K LLHAND
