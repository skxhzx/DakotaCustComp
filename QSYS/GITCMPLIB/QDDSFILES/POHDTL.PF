     A*----------------------------------------------------------------
     A*   Copyright (C) 1992 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... POHDTL (PF)
     A*  Description ... Purchase order history detail file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 2.18  01/13/97 RH
     A*      - Created using PODTL
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R PHDREC
     A*
     A            PHDWHS    R               REFFLD(WHCODE)
     A            PHDPO     R               REFFLD(PONUM)
     A            PHDSEQ    R               REFFLD(SEQ#)
     A            PHDITM    R               REFFLD(ITITEM)
     A            PHDVIT        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            PHDSPC         2          TEXT('Special Order Code')
     A                                      COLHDG('Special' 'Order Code')
     A            PHDQO1         5S 0       TEXT('Normal Qty Ordered')
     A                                      COLHDG('Normal Qty' 'Ordered')
     A            PHDQO2         5S 0       TEXT('Breakdown 1 Qty Ordered')
     A                                      COLHDG('Breakdown 1' 'Qty Ordered')
     A            PHDQO3         5S 0       TEXT('Breakdown 2 Qty Ordered')
     A                                      COLHDG('Breakdown 2' 'Qty Ordered')
     A            PHDQR1         5S 0       TEXT('Normal Qty Received')
     A                                      COLHDG('Normal Qty' 'Received')
     A            PHDQR2         5S 0       TEXT('Breakdown 1 Qty Received')
     A                                      COLHDG('Breakdown 1' 'Qty Received')
     A            PHDQR3         5S 0       TEXT('Breakdown 2 Qty Received')
     A                                      COLHDG('Breakdown 2' 'Qty Received')
217 AA            PHDWGT         9P 2       TEXT('Weight Received')
217 AA                                      COLHDG('Weight' 'Received')
     A            PHDEDT    R               TEXT('Export Date')
     A                                      COLHDG('Export' 'Date')
     A                                      REFFLD(DTECMD)
     A            PHDETI    R               TEXT('Export Time')
     A                                      COLHDG('Export' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K PHDWHS
     A          K PHDPO
     A          K PHDSEQ
     A          K PHDITM
     A          K PHDEDT
     A          K PHDETI
