     A*----------------------------------------------------------------
     A*   Copyright (C) 2005 BFC Software, Inc. - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... RUNNER (PF)
     A*  Description ... RF Shorts for Runners
     A*  Date .......... May 06, 2004
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    05/06/04  MLB  4.17
     A*      - Created.
     A*
500 AA*    03/18/05  RLP  5.00
     A*      - Add new codes 'R'=Replenished & 'U'=Out
500aAA*    07/08/05  MLB  5.00a
     A*      - Add new fields: RUTYPE, RUOQYS. Add RUTYPE to
     A*        file key.
     A*      - Add table showing valid status codes for each flag type.
500bAA*    10/26/05  MLB  5.00b
     A*      - Add new field: RURDTS - Replenished date/time stamp.
     A*
710aAA*    07/20/17  TAB  7.10a
     A*      - Add new codes 'B'=Box Breaker
     A*
720aAA*    04/20/18  RBD  7.20a
     A*      - Added new status 'S' (Staged) currently only used by
     A*        Cheney. If flag type N or M is cleared in RU310 with
     A*        quantity found the status will be changed to S rather
     A*        than C.  Status S will be updated to C when either
     A*        the route is closed to the Loader Mod or the flag
     A*        is cleared again in RU310.  File NOT recompiled,
     A*        rev 720a is documentation only.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
500aAA*
500aAA*    Flag type (RUTYPE)
500aAA*
500aAA*       'N'  -  Normal
500aAA*       'U'  -  Out
500aAA*       'M'  -  Merge
710aAA*       'B'  -  Box Breaker
     A*
     A*    Status codes (RUSTAT)
     A*
     A*       'O'  -  Open
500 AA*       'R'  -  Replenished
     A*       'P'  -  Printed
     A*       'C'  -  Closed
     A*       'D'  -  Deleted
720aAA*       'S'  -  Staged
     A*
500aAA*----------------------------------------------------------------
500aAA*
500aAA*  Table
500aAA*
500aAA*    Flag type (N=Normal) will have records with these statuses:
500aAA*
500aAA*       'O'  -  Open
500aAA*       'R'  -  Replenished
500aAA*       'P'  -  Printed
500aAA*       'C'  -  Closed
500aAA*       'D'  -  Deleted
500aAA*
500aAA*    Flag type (U=Out) will have records with these statuses:
500aAA*
500aAA*       'O'  -  Open
500aAA*       'P'  -  Printed
500aAA*       'C'  -  Closed
500aAA*       'D'  -  Deleted
500aAA*
500aAA*    Flag type (M=Merge) will have records with these statuses:
500aAA*
500aAA*       'O'  -  Open
500aAA*       'P'  -  Printed
500aAA*       'C'  -  Closed
500aAA*       'D'  -  Deleted
500aAA*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R RUREC
     A*
     A            RUWHSE    R               REFFLD(WHCODE PIRDICT)
500aAA            RUTYPE         1A         TEXT('Runner Flag Type')
500aAA                                      COLHDG('Runner' 'Flag Type')
     A            RUWHDP    R               TEXT('Warehouse Department')
     A                                      COLHDG('Department')
     A                                      REFFLD(LBWHDP LABEL)
     A            RULBL#    R     P         TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            RUOTRN    R     P         TEXT('Original Transaction Number')
     A                                      COLHDG('Original' 'Transaction' 'Nu-
     A                                      mber')
     A                                      REFFLD(TRAN#)
     A            RUTRN#    R     P         TEXT('Runner Transaction Number')
     A                                      COLHDG('Runner' 'Transaction' 'Numb-
     A                                      er')
     A                                      REFFLD(TRAN#)
     A            RUDISP    R               TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A                                      REFFLD(LBDISP LABEL)
500aAA            RUOQYS    R     P         TEXT('Orignal Qty Short/Out')
500aAA                                      COLHDG('Original' 'Qty' 'Short/Out')
500aAA                                      REFFLD(LBQPCK LABEL)
     A            RUQYSO    R     P         TEXT('Quantity Short/Out')
     A                                      COLHDG('Qty' 'Short/Out')
     A                                      REFFLD(LBQPCK LABEL)
     A            RUQYFD    R     P         TEXT('Quantity Found')
     A                                      COLHDG('Qty' 'Found')
     A                                      REFFLD(LBQPCK LABEL)
     A            RUSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
500bAA            RURDTS          Z         TEXT('Replenished Date Stamp')
     A                                      COLHDG('Replenished' 'Date Stamp')
     A            RUODTS          Z         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUPDTS          Z         TEXT('Printed Status Date Stamp')
     A                                      COLHDG('Print Status' 'Date Stamp')
     A            RUCDTS          Z         TEXT('Closed Status Date Stamp')
     A                                      COLHDG('Closed Status' 'Date Stamp')
     A*
500aAA          K RUWHSE
500aAA          K RUTYPE
     A          K RULBL#
