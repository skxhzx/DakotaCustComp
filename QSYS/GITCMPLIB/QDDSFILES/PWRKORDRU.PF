     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PWRKORDRU (PF)
     A*  Description ... Production Word Order RM Used
     A*  Creator ....... KDE
     A*
     A*  Revisions
     A*
     A*    Ver 6.50  02/09/15 KDE
     A*      - Created
730 AA*    Ver 7.30  02/25/15 KDE
     A*      - Add new Make Quantity Breakdown fields for Components along
     A*        with uom fields from item master at time of addition in case of changes
     A*        while work order is active
     A*
     A*----------------------------------------------------------------
     A*
     A*    This file will be written to everytime a Production Label
     A*    is produced.  It will generate a record for each raw
     A*    material on the BOM and either extend the quantity produced
     A*    by the BOM quantity required and generate a record.  It will
     A*    create this record with a "C" closed status if the BOM was
     A*    defined to back flush since it would go ahead and reduce the
     A*    slots inventory.  If however the item is setup not to
     A*    backflush then it will be setup with a status of "O" open.
     A*    The End of Day process will then display inventory in a
     A*    Mfg Lines slots reflecting the beginning inventory, and
     A*    Used inventory from this file.  The Prj quantity fields
     A*    will be written at Production creation time extending
     A*    qty produced by BOM quantity.  The Act quantity fields
     A*    will be written when End of Day is told to consume a
     A*    value of open inventory for a given Mfg Line/RM Item.  If
     A*    the value is greater than records written it will create
     A*    a record with only the actual qty fields populated with
     A*    whatever rounding value remains to round out the usage.  If
     A*    a user requests to reverse any quantity there must be "C"
     A*    closed records in this file for the given Work Order that
     A*    total to the negative adjustment or higher, otherwise they
     A*    will be given an error stating "not enought inventory for
     A*    "desired transaction".
     A*
     A*    URSTAT - This status field will have either an "O"
     A*             for open or "C" for closed.  A "C" will be
     A*             written when production is produced if the
     A*             item is back flushed, otherwise it will be
     A*             written as an "O" open and not closed until
     A*             user performs closing at End of Day.
     A*
     A*    URPRJxx - These fields are written as the Projected
     A*              usage per the Raw Material definition.
     A*              Simply extends the BOM requirement by the
     A*              Production Quantity
     A*
     A*    URACTxx - These fields are populated as the user
     A*              consumes inventory at End of Day, or when
     A*              production occurs for an item marked as
     A*              back flush.  If additional quantity are
     A*              are required an additional record will be
     A*              written and only the rounding portion will
     A*              be written to the ACTxx fields leaving the
     A*              PRJxx fields at zero.
     A*
     A*     The user will NOT be allowed to close a Work Order
     A*     until they close any remaining "O" open records for
     A*     that Work Order in this file.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R URREC
     A            URWHSE         3  0       COLHDG('WAREHOUSE')
     A            URWO#         13          COLHDG('WORK ORDER#')
     A            URRMI         15          COLHDG('Raw Material Number')
     A            URSER         20          COLHDG('SERIAL NUMBER')
     A            URFGI         15          COLHDG('FG Item Number')
730 DA*           URFGQTY        5P 0       COLHDG('FG QUANTITY')
730 MA            URFGQTY        6P 0       COLHDG('FG QUANTITY CASE')
730 AA            URFGQTY1       6P 0       COLHDG('FG QUANTITY BRKDWN1')
730 AA            URFGQTY2       6P 0       COLHDG('FG QUANTITY BRKDWN2')
     A            URPRJNQ        5P 0       COLHDG('Prjcted Nrm Qty')
     A            URPRJB1        5P 0       COLHDG('Prjcted BK2 Qty')
     A            URPRJB2        5P 0       COLHDG('Prjcted BK2 Qty')
     A            URPRJPQ        9P 4       COLHDG('Prjcted Prd Qty')
     A            URACTNQ        5P 0       COLHDG('Actual Nrm Qty')
     A            URACTB1        5P 0       COLHDG('Actual BK2 Qty')
     A            URACTB2        5P 0       COLHDG('Actual BK2 Qty')
     A            URACTPQ        9P 4       COLHDG('Actual Prd Qty')
     A            URSCNWGT       9P 3       COLHDG('Scanned Weight')
     A            URTARE         9P 3       COLHDG('Scanned Tare')
     A            URPRDDTE       8  0       COLHDG('PRD DATE')
     A            URSTAT         1          COLHDG('Status')

     A          K URWHSE
     A          K URWO#
     A          K URRMI
     A          K URSER
     A          K URSTAT
     A          K URPRDDTE
