     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SCALE     (PF)
     A*  Description ... Scale Master File Information
     A*  Creator ....... KDE
     A*
     A*
     A*  Revisions
     A*
650 AA*    Ver 6.50  10/10/13  KDE
     A*      - Created
700 AA*    Ver 7.00  04/09/15  KDE
     A*      - Added fields UCI and Disp to Scale file so I can track
     A*        data for given Box Breaker transaction
710 AA*    VER 7.10  07/11/17  KDE
     A*      - Added field to track mark non-unique uci case scan
     A*        as unique with a sequencial number.  This is needed
     A*        when user is breaking down non-unique uci cases
     A*        to keep breakdown records separated by individual case
     A*
     A*   SCSTAT - Scale Status of Operation
     A*            blank - Scale/Thin Client not started for first time
     A*                I - Scale and socket service being initialized
     A*                C - Weight dedected without item
     A*                W - Box Weighed with item present
     A*                X - Scale closed or not communicating
     A*   SCTYPE - Either automatic or manual weight labels
     A*                A - Weight generated from Toledo Scale
     A*                M - Weight entered by user along with qty labels
     A*   SCDEST - Either Customer or PAR Stock destination
     A*                C - Stock being produced for specific customer
     A*                P - Stock being produced for PAR Stock
     A*   SCDEMO - Identifies this port as a Demo port.  This means
     A*            a server instance will be started as well as the
     A*            TCPIP client instance.  In this case no scale exists
     A*            for the demo so the server instance will act as the
     A*            scale and the MRC GUI will allow the person doing
     A*            the demo to click buttons to simulate weight
     A*            activities
     A*   SCDEMOWGT - This weight will be maintained by a user clicking
     A*               on the demo button weights.  This will be read
     A*               by the Demo server and returned to the client.
     A*               The client will therefore not realize a scale
     A*               does not exist
     A*   SCDEMOUOM - This UOM is what the demo server will return
     A*               with weight inquiries from the scale
     A*
     A*   SCERROR   - THE SOCKET PROCESS WILL UPDATE THIS VARIABLE
     A*               WITH ANY OF THE DRI ERRORS SO IT CAN COMMUNICATE
     A*               BACK TO USER PROMPTLY WITH FEEDBACK
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R SCREC
     A*
     A            SCWHSE         3  0       TEXT('Whse')
     A                                      COLHDG('Whse')
     A            SCSTA          2          TEXT('Station ID')
     A                                      COLHDG('Station ID')
     A            SCID          15          TEXT('Scale ID - ID4')
     A                                      COLHDG('Scale Id ID4')
     A            SCPORT         5  0       TEXT('IP Port')
     A                                      COLHDG('TCPIP Port1')
     A            SCUSER        10          TEXT('MRC USER')
     A                                      COLHDG('MRC USER')
     A            SCLITM        15          TEXT('Last Scanned Item')
     A                                      COLHDG('Last Scanned Item')
     A            SCCUST        11  0       TEXT('Last Scanned Customer#')
     A                                      COLHDG('Last Scanned Cust#')
     A            SCORDNBR       7  0       TEXT('Last Scanned Order#')
     A                                      COLHDG('Last Scanned Ord#')
     A            SCWEGHT        9  3       TEXT('Last Weight')
     A                                      COLHDG('Last Weight')
     A            SCTARE         9  3       TEXT('Last Tare')
     A                                      COLHDG('Last Tare')
     A            SCWUOM         2          TEXT('Last Weight UOM')
     A                                      COLHDG('Last Weight UOM')
     A            SCSTAT         1          TEXT('Oper Status')
     A                                      COLHDG('Oper Status')
     A            SCPRTN        10          TEXT('5250 Printer')
     A                                      COLHDG('Printer ID')
     A            SCTYPE         1          TEXT('Automatic Labels or not')
     A                                      COLHDG('Auto Label')
     A            SCDEST         1          TEXT('Customer or par stock')
     A                                      COLHDG('Type Stock')
     A            SCLBLC         5  0       TEXT('Number labels printed')
     A                                      COLHDG('Label Cnt')
     A            SCLSTS        20          TEXT('Last Serial Number Assigned')
     A                                      COLHDG('Last Serial')
     A            SCPTS           Z         TEXT('SCLSTAT Time Stamp')
     A                                      COLHDG('SCLSTAT Time Stamp')
     A            SCSKTTS         Z         TEXT('SKTSCL Time Stamp')
     A                                      COLHDG('SKTSCL Time Stamp')
     A            SCMFGNO        3  0       TEXT('MFG LINE NUMBER')
     A                                      COLHDG('MFG LNE NO.')
     A            SCWO          13          TEXT('WORK ORDER NUMBER')
     A                                      COLHDG('WO NUMBER')
     A            SCUCI        100          TEXT('CASE UCI for Box Breaker')
     A                                      COLHDG('UCI BB')
     A            SCDISP        12          TEXT('Slot for Box Breaker')
     A                                      COLHDG('Slot BB')
     A            SCBBLCNS      15          TEXT('License for Box Breaker')
     A                                      COLHDG('License BB')
     A            SCBBSEQ        2  0       TEXT('Lcns Seq Box Breaker')
     A                                      COLHDG('Lcns Seq BB')
     A            SCBXBSQ        5  0       TEXT('BB CASE SEQ#')
     A                                      COLHDG('BB CASE#')
     A            SCTRAN         7  0       TEXT('Scale Trans Number'  )
     A                                      COLHDG('Trans#')
     A            SCDEMO         1          TEXT('DEMO Scale (Y/N)')
     A                                      COLHDG('DEMO (Y/N)')
     A            SCDEMOWGT      9  3       TEXT('DEMO Weight')
     A                                      COLHDG('DEMO Weight')
     A            SCDEMOUOM      2          TEXT('DEMO UOM')
     A                                      COLHDG('DEMO UOM')
     A            SCERROR       60          TEXT('DRI ERROR')
     A                                      COLHDG('DRI ERROR')
     A*
     A          K SCWHSE
     A          K SCSTA
