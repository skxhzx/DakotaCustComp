     A*****************************************************************
     A* RPLFILE  Potential replenishment file.                        *
     A* Hemant Kapadia                                                *
     A* 10 May, 1996                                                  *
     A*                                                               *
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
213 AA*    Item type (RLTYPE)
     A*
     A*      ' '  -  Normal
     A*
     A*      'B'  -  Broken case
     A*****************************************************************
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R RLREC
     A            RLWHSE    R               REFFLD(SLWHSE SLOT)
     A            RLWHDP    R               REFFLD(SLWHDP SLOT)
     A            RLFAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('From' 'Aisle')
     A            RLTAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('To' 'Aisle')
     A            RLFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            RLTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            RLITEM    R               REFFLD(SLITEM SLOT)
     A            RLSEQ#         5P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq' 'Number')
     A            RLQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            RLSDEF    R               REFFLD(SLSDEF SLOT)
     A                                      COLHDG('Replenishment' 'Slot Type')
     A            RLFLAG         1A         TEXT('Select Flag')
     A                                      COLHDG('Select' 'Flag')
     A            RLPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            RLFREE         1A         TEXT('Free slot Y,N')
     A                                      COLHDG('Free Slot' 'Y,N')
     A            RLSTAT    R               REFFLD(SLSTAT SLOT)
     A                                      COLHDG('Status of' 'Home Slot')
     A            RLTYPE         1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            RLADTE    R               TEXT('Add Date')
     A                                      COLHDG('Add' 'Date')
     A                                      REFFLD(DTECMD)
     A            RLATIM    R               TEXT('Add Time')
     A                                      COLHDG('Add' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A*  File key.
     A*
     A          K RLWHSE
     A          K RLWHDP
     A          K RLFAIL
     A          K RLTAIL
     A*
