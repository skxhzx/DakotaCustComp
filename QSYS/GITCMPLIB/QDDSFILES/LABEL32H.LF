     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL32H (LF)
     A*  Description ... Special join file for voice selection for AVL.
     A*  Date .......... December 07, 2011
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
640 AA*    12/07/11  MLB  6.40
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file is identical to LABEL32 except that it used LBPSEQ
     A*  in its key. LBPSEQ is used to control the special voice picking
     A*  sequence used by Avalon with Retalix Pwr Whse.
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R LJREC32H                  JFILE(LABEL PLTSUM PIRITEM +
     A                                            ORDH ORDD UNMESR)
     A*
     A          J                           JOIN(LABEL   PLTSUM)
     A                                      JFLD(LBWHSE  PSWHSE)
     A                                      JFLD(LBRTE   PSRTID)
     A                                      JFLD(LBPAL#  PSPLID)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
     A            LBPO
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
     A            LBGRP1
     A            LBGRP2
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PLTSUM file.
     A*
     A            PSCOMP
     A            PSTZON
     A            PSPZON
     A            PSSZON
     A            PSTRUK
     A            PSROW
     A            PSCOL
     A            PSMAXC
     A            PSPAL#
     A            PSSTP1
     A            PSSTP2
     A            PSPCS
     A            PSCUBE
     A            PSSWGT
     A            PSTYPE
     A            PSBCNT
     A            PSFLAG
     A            PSTRN#
     A*
     A*  Fields from PIRITEM file.
     A*
     A            ITTYPE
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
     A            ITFLG1
     A            ITUM3
     A            ITUMQ3
     A            ITFLG2
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
     A            ITREOR
     A            ITREPL
     A            ITRCVD
     A            ITFLGD
     A            ITFLGB
     A            ITSLIF
     A            ITNDAY
     A*
     A*  Fields from ORDH file.
     A*
     A            OHORIG
     A            OHTYPE
     A            OHCUST
     A            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A            OHPBAT
     A            OHSTAT
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Key fields
     A*
     A          K LBWHSE
     A          K LBRTE
     A          K LBTYPE
     A          K LBTRN#
     A          K LBPSEQ
     A          K LBWHDP
     A          K LBSTYP
     A          K LBAISL
     A          K LBLOC
     A          K LBRLVL
     A          K LBHAND
     A          K LBSTOP                    DESCEND
