     A*----------------------------------------------------------------
     A*   Copyright (C) 2022 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PIRVENDOR (PF)
     A*  Description ... Vendor file
     A*
     A*  Revisions
     A*
760 AA*    10/02/22  DAS  7.60
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A* Notes
     A*
     A*  This file, along with the PIRBUYER file, were initially
     A*  created to work with the AFS Host system, which use alpha
     A*  Buyer and Vendor numbers. But is to become the standard
     A*  for both alpha an numeric Buyer/Vendor numbers.
     A*
     A*  At the time, it was decided that the alpha number would be
     A*  sent at the beginning of the Name field in the import file
     A*  followed by a colon, and the Buyer/Vendor number fields
     A*  would be left zero.
     A*
     A*  For an alpha number, we will get an Id number that would be
     A*  put into the item Buyer/Vendor number. Programs that need to
     A*  display the Buyer/Vendor number, should use the value in the
     A*  item file to get the Host Buyer/Vendor number from this
     A*  file and the PIRBUYER file.
     A*
     A*  New programs GETBUYER and GETVENDOR are called by the import
     A*  programs. These programs will assign an Id to alpha numbers.
     A*
     A*  For clients using numeric Buyer/Vendor numbers, the Id will be
     A*  the Buyer/Vendor numeric, the Host value will be the number
     A*  with leading zeros, the Display value will be the number
     A*  without leading zeros.
     A*
     A*  For clients using alph Buyer/Vendor numbers, the Id will be
     A*  assigned, the Host and display values will be the alpha value.
     A*
     A*----------------------------------------------------------------
     A*
     A* Fields
     A*
     A*  VNVENID - Dakota Vendor Number
     A*
     A*  VNHSTVEN - Host Vendor Number
     A*
     A*  VNVENDSP - Vendor Number Display value
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R VNREC
     A*
     A            VNWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            VNVENID       11S 0       TEXT('Dakota Vendor Id')
     A                                      COLHDG('Dakota' 'Vendor Id')
     A            VNHSTVEN      20          TEXT('Host Vendor Number')
     A                                      COLHDG('Host' 'Vendor Number')
     A            VNVENDSP      20          TEXT('Vendor Display')
     A                                      COLHDG('Vendor' 'Display')
     A            VNNAME        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            VNADR1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            VNADR2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            VNCITY        20          TEXT('City')
     A                                      COLHDG('City')
     A            VNSTATE        2          TEXT('State')
     A                                      COLHDG('State')
     A            VNZIP         10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A*
     A*  Add/Change information
     A*
     A            VNADDTS   R               REFFLD(ADDTS PIRDICT)
     A            VNADDUC   R               REFFLD(ADDUC PIRDICT)
     A            VNADDUSR  R               REFFLD(ADDUSR PIRDICT)
     A            VNADDJOB  R               REFFLD(ADDJOB PIRDICT)
     A            VNADDNBR  R               REFFLD(ADDNBR PIRDICT)
     A*
     A            VNCHGTS   R               REFFLD(CHGTS PIRDICT)
     A            VNCHGUC   R               REFFLD(CHGUC PIRDICT)
     A            VNCHGUSR  R               REFFLD(CHGUSR PIRDICT)
     A            VNCHGJOB  R               REFFLD(CHGJOB PIRDICT)
     A            VNCHGNBR  R               REFFLD(CHGNBR PIRDICT)
     A*
     A          K VNWHSE
     A          K VNVENID
