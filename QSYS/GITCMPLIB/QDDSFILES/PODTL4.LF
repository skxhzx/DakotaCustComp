     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PODTL4  (LF)
     A*  Description ... P.O. Detail join file by Whse/Item
     A*  Date .......... March 24, 2004
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*  03/24/04  MLB  4.17
     A*    - Created
     A*
     A*----------------------------------------------------------------
     A* Notes:
     A*
     A*   - This join file does NOT use the JDFTVAL keyword because we
     A*     do not want to include any detail records that do not have
     A*     a corresponding record in the POSPO file. Therefore, it is
     A*     easy to verify if a detail record is attached to a Super
     A*     P/O  - If a record exists it is attached to one or more
     A*     Super P/O's.
     A*
     A*----------------------------------------------------------------
     A          R PDJREC                    JFILE(PODTL POHDR POSPO)
     A*
     A          J                           JOIN(PODTL POHDR)
     A                                      JFLD(PDWHSE  PHWHSE)
     A                                      JFLD(PDPO    PHPO)
     A*
     A          J                           JOIN(POHDR   POSPO)
     A                                      JFLD(PHWHSE  PUWHSE)
     A                                      JFLD(PHPO    PUPO)
     A*
     A*  Fields from PODTL file.
     A*
     A            PDWHSE
     A            PDPO
     A            PDSEQ
     A            PDITEM
     A            PDVITM
     A            PDSPCL
     A            PDQOR1
     A            PDQOR2
     A            PDQOR3
     A            PDQRC1
     A            PDQRC2
     A            PDQRC3
     A            PDDWGT
     A*
     A*  Fields from POHDR file.
     A*
     A            PHWHSE
     A            PHPO
     A            PHVEN
     A            PHVNAM
     A            PHVAD1
     A            PHVAD2
     A            PHVCTY
     A            PHVST
     A            PHVZP1
     A            PHDATE
     A*  PHTIME will be changed to house military time versus
     A*  12 hour time.
     A            PHTIME
     A*  AM/PM Will no longer be used.  PHTIME will now have
     A*  Military time.
     A            PHAMPM
     A            PHUDTE
     A                                      COLHDG('Upload' 'Date')
     A            PHUTIM                    TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A            PHSTAT                    TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A*  Fields from POSPO file.
     A*
     A            PUWHSE
     A            PUSPO
     A            PUPO
     A            PUEDTE
     A            PUETIM
     A*
     A*  Key fields
     A*
     A          K PDWHSE
     A          K PDITEM
     A          K PDPO
     A          K PDSEQ
