     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSADJ2 (PF)
     A*  Description ... Staging Export - Adjustments V2
     A*
     A*  Revisions
     A*
720  A*    10/12/18  RTR  7.20
     A*      - Created from EXPSADJ.
     A*
     A*----------------------------------------------------------------
     A          R IEREC
     A*
     A            IEWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IEBATID       11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            IEITEM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            IESTATUS       1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            IESTSTS         Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IESTSUC         Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IETEXT       200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            IEDATE         8S 0       TEXT('Adjustment Date')
     A                                      COLHDG('Adjusted' 'Date')
     A            IETIME         6S 0       TEXT('Adjustment Time')
     A                                      COLHDG('Adjusted' 'Time')
     A            IEBY          10          TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
     A            IEWHDP         5A         TEXT('Department')
     A                                      COLHDG('Department')
     A            IEDISP        12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            IECWTA         9S 2       TEXT('Catchweight Adjustment')
     A                                      COLHDG('Catchweight' 'Adjustment')
     A            IEQTY1         7S 2       TEXT('Quantity 1')
     A                                      COLHDG('Quantity 1')
     A            IEQTY2         7S 2       TEXT('Quantity 2')
     A                                      COLHDG('Quantity 2')
     A            IEQTY3         7S 2       TEXT('Quantity 3')
     A                                      COLHDG('Quantity 3')
     A            IECODE         2          TEXT('Adjustment Code')
     A                                      COLHDG('Adj' 'Code')
     A            IEMEMO        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A*
     A*  Add stamps
     A*
     A            IEADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IEADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IEWHSE
     A          K IEBATID
     A          K IEITEM
     A          K IEDATE
     A          K IETIME
