     A*----------------------------------------------------------------
     A*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... UPDFILES (PF)
     A*  Description ... Files to updated
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*     This file has been created to hopefully make updating files
     A*     much easier.  Program UPDATECL and UPDATE are always called
     A*     to update the files.  In the past the UPDATECL program had
     A*     to be revised for each update.  Now, that program does not
     A*     have to change.  Instead, all of the files that have been
     A*     revised or created are put into this file.  Different types
     A*     of updates can be performed (see below) and conversion
     A*     programs can be identified.
     A*
     A*    The only logical files that should be included are new ones
     A*    with the UFTYPE set to 'N'.
     A*
     A*    All the existing logicals will be deleted and recreated
     A*    automatically for the physical files in this file.
     A*
     A*    Then new reports file NREPORTS should not be in this file.
     A*    The program to add the new reports from NREPORTS into the
     A*    REPORTS file is always executed.
     A*
     A*    Programs that need to be run can either be included in the
     A*    record of a physical file or can be a seperate record.
     A*    The programs can not have any parameters.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Update type (UFTYPE)
     A*
     A*      'U'  -  Update
     A*              Full service update with copy from current file.
     A*
     A*      'N'  -  New
     A*              Only use if there is no current file.
     A*
     A*      'R'  -  Replace
     A*              Replace current file if one exists (ie. PIRDICT).
     A*
     A*      'P'  -  Program
     A*              Only run conversion program (ie. REPORTS).
     A*
     A*    File type (UFFTYP)
     A*
     A*      'P'  -  Physical
     A*      'L'  -  Logical
     A*
     A*    Before/After (UFBA)
     A*
     A*      'B'  -  Execute program before any other updates.
     A*      'A'  -  Execute program after all other updates.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R UFREC
     A*
     A            UFTYPE         1A         TEXT('Update Type')
     A                                      COLHDG('Update' 'Type')
     A            UFNEWF        10A         TEXT('New File')
     A                                      COLHDG('New File')
     A            UFOLDF        10A         TEXT('Old File')
     A                                      COLHDG('Old File')
     A            UFFTYP         1A         TEXT('File Type')
     A                                      COLHDG('File' 'Type')
     A            UFPGM         10A         TEXT('Conversion program')
     A                                      COLHDG('Conversion' 'Program')
     A            UFBA           1A         TEXT('Before/After')
     A                                      COLHDG('Before/After')
     A            UFCPY          1A         TEXT('Copied')
     A                                      COLHDG('Copied')
     A            UFDLTL         1A         TEXT('Logicals Deleted')
     A                                      COLHDG('Logicals' 'Deleted')
     A            UFDLTP         1A         TEXT('Physical Deleted')
     A                                      COLHDG('Physical' 'Deleted')
     A            UFRNM          1A         TEXT('Renamed')
     A                                      COLHDG('Renamed')
     A            UFCRTL         1A         TEXT('Logicals Created')
     A                                      COLHDG('Logicals' 'Created')
     A            UFCVT          1A         TEXT('Converted')
     A                                      COLHDG('Converted')
     A*
     A          K UFNEWF
