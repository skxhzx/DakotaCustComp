     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTPICS (PF)
     A*  Description ... Trax pictures
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
720 AA*    02/02/18  DAS  7.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    Not all fields are filled in for all picture types. The
     A*    fields that are used are noted below with the type defs.
     A*    Warehouse, route id and route are filled in for all types.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A*
     A*  Picture Types (TTPCTYPE)
     A*
     A*    RETURNS  - Returns
     A*               Link Fields: TTPCSTP, TTPCORD, TTPCOSEQ,
     A*                            TTPCITEM, TTPCLBL, TTPCTRX
     A*
     A*    SIG      - Signature
     A*               Link fields: TTPCSTP
     A*
     A*    STOP     - Stop Note
     A*               Link fields: TTPCSTP
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R TTPCREC
     A*
     A            TTPCWHS   R               REFFLD(WHCODE)
     A            TTPCRTID  R               REFFLD(ROUTE)
     A                                      TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            TTPCRTE   R               REFFLD(ROUTE)
     A            TTPCTYPE      10          TEXT('Picture Type')
     A                                      COLHDG('Picture' 'Type')
     A            TTPCSTP   R               REFFLD(STOP)
     A            TTPCORD   R               REFFLD(OHORD)
     A            TTPCOSEQ  R               REFFLD(SEQ#)
     A                                      TEXT('Order Seq#')
     A                                      COLHDG('Order' 'Seq#')
     A            TTPCITM   R               REFFLD(ITITEM)
     A            TTPCLBL   R               REFFLD(TRAN#)
     A                                      TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            TTPCTRX        5S 0       TEXT('Trax #')
     A                                      COLHDG('Trax #')
     A            TTPCFNAM      60          TEXT('File Name')
     A                                      COLHDG('File Name')
     A            TTPCFTS   R               REFFLD(ADDTS)
     A                                      TEXT('File Stamp')
     A                                      COLHDG('File Stamp')
     A*
     A*
     A          K TTPCWHS
     A          K TTPCRTID
     A          K TTPCTYPE
     A          K TTPCSTP
     A          K TTPCITM
     A          K TTPCTRX
     A          K TTPCFNAM
