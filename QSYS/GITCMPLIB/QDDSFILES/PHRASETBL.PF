     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... BFCPHRASE (PF)
     A*  Description ... BFC phrase table
     A*  Creator ....... DAS
     A*  Date .......... July 9, 2008
     A*
     A*  Revisions:
     A*
600 AA*    07/09/08  DAS  6.00
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*    This file contains pharases used in detailed tracking.
     A*    This will help to reduced the size of the actual tracking
     A*    file because the tracking file will only contain a phrase
     A*    id insted of the actual phrase.
     A*
     A*    Special variables will be used for information such as
     A*    license plate number, quantity, etc ... Therefore, the only
     A*    way to view the detailed tracking will be through a program
     A*    that inserts the variables into the phrase. The variables
     A*    are listed below.
     A*
     A*    The phrases will be shared by multiple tracking files, such
     A*    as license tracking and employee tracking.
     A*
     A*----------------------------------------------------------------
     A*  Phrase Id
     A*
     A*    A unique number identifying a phrase.
     A*
     A*    Each language version of the same phrase should have the
     A*    same id.
     A*
     A*----------------------------------------------------------------
     A*  Language Types
     A*
     A*    EN = English
     A*    SP = Spanish
     A*
     A*----------------------------------------------------------------
     A*  Group
     A*
     A*    LICHST  -  License History
     A*    SELECT  -  Selection
     A*    PUTAWY  -  Putaway
     A*    RECV    -  Receiving
     A*    TFR     -  Transfers
     A*    REPLEN  -  Replenishments
     A*
     A*----------------------------------------------------------------
     A*  Level
     A*
     A*    The level is used to control what is displayed and for
     A*    clean up.
     A*
     A*    1 is the highest level and 99 is the lowest.
     A*
     A*    Levels are a work in progress. The intent is that most of
     A*    the time a manager only wants to see a high level summary
     A*    and then see more detail periodically. Likewise, we will
     A*    probably want to keep a higher summary levels for a longer
     A*    time than more detailed lower levels.
     A*
     A*    Entry into menu programs (i.e., Putaway) should start at
     A*    level 10, while recording what was entered on a screen
     A*    could be level 20. Actual function keys could be at level
     A*    90 and possibly controled with an option on whether they
     A*    get recorded at all.
     A*
     A*    Level 1 would be for Start/End Day.
     A*
     A*----------------------------------------------------------------
     A*  Variables
     A*
     A*   Varibles are defined as &1, &2, &3, etc ...
     A*
     A*   The program that creates the display phrase determines what
     A*   each variable represnets within a phrase.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R PHREC
     A*
     A            PHID           5P 0       TEXT('Phrase Id')
     A                                      COLHDG('Phrase' 'Id')
     A            PHLANG         2          TEXT('Language')
     A                                      COLHDG('Language')
     A            PHGROUP        6          TEXT('Group')
     A                                      COLHDG('Group')
     A            PHLEVEL        2S 0       TEXT('Level')
     A                                      COLHDG('Level')
     A            PHPHRASE      50          TEXT('Phrase')
     A                                      COLHDG('Phrase')
     A            PHXML        100          TEXT('XML Phrase')
     A                                      COLHDG('XML Phrase')
     A          K PHID
     A          K PHLANG
