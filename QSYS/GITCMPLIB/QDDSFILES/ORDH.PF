     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDH  (PF)
     A*  Description ... Order Header file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    07/29/94  DAS  Rev 2.00
     A*      - Added warehouse and order type fields.
     A*      - Added Date/Time stamps for status and upload.
     A*      - Made zip code 10 characters and removed Zip+4.
     A*      - Removed client batch number.
     A*
     A*    Ver 2.01  08/17/94  DAS
     A*      - Changed route and stop to REFFLDs.
     A*      - Changed OHORD to packed.
     A*      - Increased customer number from 10 digits to 11.
     A*      - Added misc. info field.
     A*
213 AA*    Ver 2.02  05/15/96  DAS
213 AA*      - Added fields OHCORD and OHORIG.
213 AA*      - Redefined use of field OHORD (See notes below).
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    The use of OHORD has been redefined to contain a unique
     A*    sequence number generated by the PIR system.  The new field
     A*    OHCORD will contain the customer order number.  When importing
     A*    orders the import order number sent from the host will go
     A*    into OHCORD. Because of this change, the order number sent
     A*    from the host system will no longer have to be unique.  It
     A*    will also allow us to use this file for both customer orders
     A*    and transfer orders created with the PIR transfer programs.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Origination types (OHORIG)
     A*
     A*       ' '  -  From host system
     A*       'T'  -  From PIR transfer programs
     A*
     A*    Order types (OHTYPE)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*    Status codes  (OHSTAT)
     A*
     A*       'O'  -  Open
     A*       'P'  -  Processing
     A*       'C'  -  Closed
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R OHREC
     A*
     A            OHWHSE    R               REFFLD(WHCODE)
     A            OHORD     R               REFFLD(OHORD)
213 AA            OHORIG         1          TEXT('Origination')
213 AA                                      COLHDG('Origination')
     A            OHTYPE         1          TEXT('Order Type')
     A                                      COLHDG('Order' 'Type')
     A            OHCUST    R               REFFLD(CUSNUM)
213 AA            OHCORD    R               TEXT('Customer Order Number')
213 AA                                      COLHDG('Customer' 'Order Number')
213 AA                                      REFFLD(OHORD)
     A            OHCPO         15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            OHCNAM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A            OHCAD1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            OHCAD2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            OHCCTY        20          TEXT('City')
     A                                      COLHDG('City')
     A            OHCST          2          TEXT('State')
     A                                      COLHDG('State')
     A            OHCZIP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            OHRTE     R               REFFLD(ROUTE)
     A            OHSTOP    R               REFFLD(STOP)
     A            OHTRUK        10          TEXT('Truck')
     A                                      COLHDG('Truck')
     A            OHMISC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A            OHPBAT    R               REFFLD(BATCH#)
     A            OHSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            OHSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            OHSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            OHUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            OHUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K OHWHSE
     A          K OHORD
