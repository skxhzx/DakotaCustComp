     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDHH (PF)
     A*  Description ... Order Header history file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
520 AA*    02/22/08  DAS  5.20  P#00023
     A*      - Created - Copied from ORDH in pirb
720aAA*    01/25/18  LMC  7.20a
     A*      - removed Unique atrribute.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    The use of OHORD has been redefined to contain a unique
     A*    sequence number generated by the PIR system.  The new field
     A*    OHCORD will contain the customer order number.  When importing
     A*    orders the import order number sent from the host will go
     A*    into OHCORD. Because of this change, the order number sent
     A*    from the host system will no longer have to be unique.  It
     A*    will also allow us to use this file for both customer orders
     A*    and transfer orders created with the PIR transfer programs.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Origination types (OHORIG)
     A*
     A*       ' '  -  From host system
     A*       'T'  -  From PIR transfer programs
     A*
     A*    Order types (OHTYPE)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*    Status codes  (OHSTAT)
     A*
     A*       'O'  -  Open
     A*       'P'  -  Processing
     A*       'C'  -  Closed
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
720aDA*                                     UNIQUE
     A          R OHREC
     A*
     A            OHWHSE    R               REFFLD(WHCODE)
     A            OHORD     R               REFFLD(OHORD)
213 AA            OHORIG         1          TEXT('Origination')
213 AA                                      COLHDG('Origination')
     A            OHTYPE         1          TEXT('Order Type')
     A                                      COLHDG('Order' 'Type')
     A            OHCUST    R               REFFLD(CUSNUM)
213 AA            OHCORD    R               TEXT('Customer Order Number')
213 AA                                      COLHDG('Customer' 'Order Number')
213 AA                                      REFFLD(OHORD)
     A            OHCPO         15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            OHCNAM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A            OHCAD1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            OHCAD2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            OHCCTY        20          TEXT('City')
     A                                      COLHDG('City')
     A            OHCST          2          TEXT('State')
     A                                      COLHDG('State')
     A            OHCZIP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            OHRTE     R               REFFLD(ROUTE)
     A            OHSTOP    R               REFFLD(STOP)
     A            OHTRUK        10          TEXT('Truck')
     A                                      COLHDG('Truck')
     A            OHMISC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A            OHPBAT    R               REFFLD(BATCH#)
     A            OHSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            OHSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            OHSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            OHUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            OHUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K OHWHSE
     A          K OHORD
