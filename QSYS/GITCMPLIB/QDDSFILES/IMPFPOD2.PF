     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPFPOD2 (PF)
     A*  Description ... FTP Purchase order detail import file
     A*  Creator ....... KDE
     A*
     A*  This file should be identical to IMPFPOD except that
     A*  field PIDTYP has been added.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFPOD.FILE')
     A*
     A*  Revisions
     A*
     A*    Rev 4.14  07/29/00  KDE
     A*      - Created
     A*
414aAA*    Rev 4.14a 11/02/00  DAS
     A*      - Removed UNIQUE keyword.
     A*
414bAA*    Rev 4.14b 11/03/00  DAS
     A*      - Added PIDCDT/PIDCTM fields. These fields have been added
     A*        for clients who write a record to a work file every time
     A*        a P.O. changes.
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Type (PIDTYP)
     A*
     A*      'A'  -  Add       Adds P.O. detail line.
     A*                        If detail exists (based on seq/item),
     A*                          treat as type 'C'.
     A*
     A*      'C'  -  Change    Changes P.O. detail line.
     A*                        If detail does not exist (based on
     A*                          seq/item), treat as type 'A'.
     A*                        If any quantity has been received for
     A*                          the detail line, the line will be
     A*                          ignored and nothing will be changed.
     A*
     A*      'D'  -  Delete    Deletes P.O. header and detail.
     A*                        If any quantity has been received for
     A*                          the detail line, the line will be
     A*                          ignored and the detail line will NOT
     A*                          be deleted.
     A*
     A*----------------------------------------------------------------***
414aDA**                                    UNIQUE
     A          R PIDREC
     A*
     A            PIDWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PIDPO          9A         TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
414bAA            PIDCDT         8S 0       TEXT('Create/Chg Date')
414bAA                                      COLHDG('Create/Chg' 'Date')
414bAA            PIDCTM         6S 0       TEXT('Create/Chg Time')
414bAA                                      COLHDG('Create/Chg' 'Time')
     A            PIDSEQ         5S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            PIDITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            PIDVIT        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            PIDSPC         2          TEXT('Special Order Code')
     A                                      COLHDG('Special' 'Order Code')
     A            PIDQO1         5S 0       TEXT('Normal Qty Ordered')
     A                                      COLHDG('Normal Qty' 'Ordered')
     A            PIDQO2         5S 0       TEXT('Breakdown 1 Qty Ordered')
     A                                      COLHDG('Breakdown 1' 'Qty Ordered')
     A            PIDQO3         5S 0       TEXT('Breakdown 2 Qty Ordered')
     A                                      COLHDG('Breakdown 2' 'Qty Ordered')
     A            PIDTYP         1          TEXT('Type')
     A                                      COLHDG('Type')
     A*
     A          K PIDWHS
     A          K PIDPO
     A          K PIDCDT
     A          K PIDCTM
     A          K PIDSEQ
     A          K PIDITM
