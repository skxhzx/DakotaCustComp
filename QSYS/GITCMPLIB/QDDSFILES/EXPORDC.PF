     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPORDC  (PF)
     A*  Description ... Order detail catch weight export file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    Rev 2.01  10/21/94  HNK
     A*      - Sequence number field added. (OECSQ#)
     A*      - Key changed.
409  A*    04/21/98 RH   4.09
     A*      - Transaction number field added. (OCTRAN)
     A*      - Key changed.
412  A*    08/24/99 RH   4.12
     A*      - Make key not unique.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
412 DA*                                     UNIQUE
     A          R OECREC
     A*
     A            OECWHS         3P 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OECORD         7P 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OECSQ#         5P 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OECITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            OECSEQ         3P 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            OECWGT         7P 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
409 AA            OECTRN    R               REFFLD(TRAN#)
409 AA                                      TEXT('Transaction Number')
409 AA                                      COLHDG('Transaction' 'Number')
     A*
     A          K OECWHS
     A          K OECORD
     A          K OECSQ#
     A          K OECITM
409 AA          K OECTRN
     A          K OECSEQ
