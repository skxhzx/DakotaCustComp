     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (360) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PBOMLVLITM(JF)
     A*  Description ... PBOMLVL JOINED TO PBOM TO ITEMPRD
     A*  Date .......... January 23, 2019
     A*  Creator ....... KDE
     A*
     A*  Revisions
     A*
     A*    01/23/19  KDE  7.30a
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A          R BOMJOIN                   JFILE(PBOMLVL PBOM ITEMPRD)
     A*
     A          J                           JOIN(PBOMLVL PBOM)
     A                                      JFLD(BMLWHS BMWHSE)
     A                                      JFLD(BMLFG BMFGI)
     A                                      JFLD(BMLLVL1 BMRMI)
     A          J                           JOIN(PBOMLVL ITEMPRD)
     A                                      JFLD(BMLWHS IPWHSE)
     A                                      JFLD(BMLLVL1 IPITEM)
     A*
     A*  Fields from PBOMLVL file.
     A*
     A            BMLWHS         3P 0
     A            BMLFG
     A            BMLLVL1
     A*
     A*  Fields from PBOM file.
     A*
     A            BMWHSE         3P 0N
     A            BMQTYN
     A            BMQTY1
     A            BMQTY2
     A            BMPRDQ
     A            BMSCRF
     A*
     A*  Fields from ITEMPRD file.
     A*
     A            IPWHSE         3P 0N
     A            IPMFGNO
     A            IPPRDFLG
     A*
     A*  Key fields
     A*
     A          K BMLWHS
     A          K BMLFG
     A          K BMLLVL1
     A          S IPPRDFLG                  CMP(EQ 'C')
