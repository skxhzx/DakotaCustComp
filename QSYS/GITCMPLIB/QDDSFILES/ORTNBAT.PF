     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORTNBAT (PF)
     A*  Description ... Batch Customer returns file
     A*  Creator ....... DAS
     A*
     A*  Revisions
416aAA*    Ver 4.16a 10/19/01  RH
     A*      - Added fields ORDCOD, ORIINV, OROLB#, ORINV#
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    The original transaction number (ORTRN#) ties a return
     A*    back to a specific employee when EPM is being used.
     A*
     A*    The corresponding item number (ORCITM) was added to help
     A*    track which item was actually mispicked.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Return types (OBRTYP)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*    Status  (OBRSTS)
     A*
     A*       '*INVALID' - Invalid item number.
     A*       '*NOOPT  ' - No receiving options defined.
     A*       '*NODEF  ' - No Tie/High defs defined for item.
     A*       '*NOSLOT ' - No slots available for item.
     A*       '*NOTENGH' - Not enough slots available for item.
     A*       '*OK     ' - Completed successfully.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OBRREC
     A*
     A            OBRWHS    R               REFFLD(WHCODE)
     A            OBRORD    R               REFFLD(OHORD)
     A            OBRITM    R               REFFLD(ITITEM)
     A            OBRQT1         5P 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            OBRQT2         5P 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            OBRQT3         5P 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
     A            OBRCWT         9P 2       TEXT('Catchweight Returned')
     A                                      COLHDG('Catchweight' 'Returned')
     A            OBRTYP         1          TEXT('Return Type')
     A                                      COLHDG('Return' 'Type')
     A            OBRTRN    R               TEXT('Original Tran#')
     A                                      COLHDG('Original' 'Tran#')
     A                                      REFFLD(TRAN#)
     A            OBRCIT    R               TEXT('Corresponding Item')
     A                                      COLHDG('Corresponding' 'Item')
     A                                      REFFLD(ITITEM)
     A            OBRRCD    R               REFFLD(RNCODE)
416aAA            OBDCOD    R               REFFLD(RDCODE)
416aAA            OBIINV         1          TEXT('Impact Inventory')
416aAA                                      COLHDG('Impact' 'Inv.')
416aAA            OBOLB#    R               TEXT('Original Label')
416aAA                                      COLHDG('Orig.' 'Label#' )
416aAA                                      REFFLD(TRAN#)
416aAA            OBINV#        12A         TEXT('Orig. Invoice#')
416aAA                                      COLHDG('Orig.' 'Invoice#')
     A            OBRMEM        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            OBRPUT         1          TEXT('Put away')
     A                                      COLHDG('Put away')
     A            OBREDT    R               TEXT('Expire Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            OBRSLT        12          TEXT('Manual Slot')
     A                                      COLHDG('Manual' 'Slot')
     A            OBRUDT    R               TEXT('Created Date')
     A                                      COLHDG('Created' 'Date')
     A                                      REFFLD(DTECMD)
     A            OBRUTI    R               TEXT('Created Time')
     A                                      COLHDG('Created' 'Time')
     A                                      REFFLD(TIMHMS)
     A            OBRBAT    R               REFFLD(BATCH#)
     A            OBRPAL         3P 0       TEXT('Pallets')
     A                                      COLHDG('# of' 'Pallets')
     A            OBRSTS         8          TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A          K OBRWHS
     A          K OBRORD
     A          K OBRITM
     A          K OBRUDT
     A          K OBRUTI
