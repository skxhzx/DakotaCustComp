     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... DDTASKH  (PF)
     A*  Description ... DragDrop 2.0 Task file History
     A*  Creator ....... KDE
     A*
     A*  Revisions
     A*
    ÂˆA*    ANY CHANGES MADE TO THIS FILE SHOULD BE REPLICATED TO DDTASK
     A*
740 AA*    07/20/20  KDE  7.40
     A*      - Created. New history file.  Same as DDTASK with
     A*        same record format and field names
     A*
     A*----------------------------------------------------------------
     A*
     A* Notes
     A*
     A*   This file contains the tasks for interacting with the GUI
     A*   front end. It includes the records retrieved to create the
     A*   initial screen. It records drop requests coming from the
     A*   screen. It records the changes made to the DDDETAIL file. It
     A*   records the JSON strings for updating the screen with the
     A*   drop changes.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*  DDTTYPE / DDTSUBTYPE
     A*
     A*    TRUCK - DD Tasks to build initial truck
     A*      - START
     A*      - END
     A*      - ADD
     A*
     A*    COLORS - DD Tasks to add colors
     A*
     A*    DROPREQ - Records request from DD
     A*      - REQUEST
     A*      - DRAGPHRASE
     A*      - DROPPHRASE
     A*
     A*    DDDCHANGES - Records changes to DDDETAIL file
     A*      - ADD
     A*      - CHANGE
     A*      - DELETE
     A*
     A*    DDTASKS - DD Tasks to perform DROPREQ
     A*      - START
     A*      - END
     A*      - ADD
     A*      - DELETE
     A*      - CHGBEFORE
     A*      - CHGAFTER
     A*
     A*
     A*  DDTTASKGRP
     A*    - Zero for INIT. New number for each Drop Request.
     A*    - All corresponding DROPREQ, DDDCHANGES and DDTASKS
     A*      records should have same number.
     A*
     A*  DDTSEQ
     A*    - Sequence records were added within a DDJREQNUM.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R DDTREC
     A*
     A            DDTWHSE   R               REFFLD(WHCODE)
     A            DDTRTID   R               TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A                                      REFFLD(ROUTE)
     A            DDTTYPE       10          TEXT('Type')
     A                                      COLHDG('Type')
     A            DDTSUBTYPE    10          TEXT('Sub Type')
     A                                      COLHDG('Sub Type')
     A            DDTTASKGRP     5  0       TEXT('Task Group')
     A                                      COLHDG('Task' 'Group')
     A            DDTSEQ         7  0       TEXT('Seq')
     A                                      COLHDG('Seq')
     A            DDTID         20          TEXT('Id')
     A                                      COLHDG('Id')
     A*
     A* DDDETAIL key fields
     A*
     A            DDTSNAP        2  0       TEXT('Shapshot')
     A                                      COLHDG('Snapshot')
     A            DDTGROUP       1          TEXT('Group')
     A                                      COLHDG('Group')
     A            DDTLEVEL       2  0       TEXT('Level')
     A                                      COLHDG('Level')
     A            DDTROW         3  0       TEXT('Row')
     A                                      COLHDG('Row')
     A            DDTCOL         2  0       TEXT('Column')
     A                                      COLHDG('Column')
     A            DDTPLID   R               TEXT('Pallet Id')
     A                                      COLHDG('Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            DDTPLTSCT      1          TEXT('Pallet Section')
     A                                      COLHDG('Pallet' 'Section')
     A            DDTSTOP   R               TEXT('Stop Id')
     A                                      COLHDG('Stop Id')
     A                                      REFFLD(STOP)
     A            DDTITMTYP      1          TEXT('Item Type')
     A                                      COLHDG('Item Type')
     A            DDTLBLTOT R               TEXT('Tote Label Num')
     A                                      COLHDG('Tote' 'Label Num')
     A                                      REFFLD(TRAN#)
     A            DDTLBLITM R               TEXT('Item Label Num')
     A                                      COLHDG('Item' 'Label Num')
     A                                      REFFLD(TRAN#)
     A*
     A*  Add information
     A*
     A            DDTADDPGM R               REFFLD(ADDPGM)
     A            DDTADDTS  R               REFFLD(ADDTS PIRDICT)
     A            DDTADDUC  R               REFFLD(ADDUC PIRDICT)
     A            DDTADDUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('User')
     A                                      TEXT('User')
     A            DDTADDUSRJR               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            DDTADDJOB R               REFFLD(ADDJOB PIRDICT)
     A            DDTADDNBR R               REFFLD(ADDNBR PIRDICT)
     A            DDTSESSID     40          VARLEN
     A                                      TEXT('Session Id')
     A                                      COLHDG('Session Id')
     A*
     A*  String
     A*
     A            DDTSTR      3072          VARLEN
     A                                      TEXT('JSON String')
     A                                      COLHDG('JSON String')
     A*
     A*  Key
     A*
     A          K DDTWHSE
     A          K DDTRTID
     A          K DDTTYPE
     A          K DDTTASKGRP
     A          K DDTSEQ

