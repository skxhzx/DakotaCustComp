     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... RTEHEDEXA (PF)
     A*  Description ... RTEHED extra fields
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    09/05/18  DAS  7.20
     A*      - Created
     A*
     A*    11/24/20  DAS  7.40
     A*      - Add field RHARDYPP
     A*
750 AA*    09/15/21  KDE  7.50
     A*      - Add field RHAAGN
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file should only be touched using SQL so new fields
     A*   can be added without needing to recompile existing programs.
     A*
     A*   Ideally, RPG programs should only access/update this
     A*   file using program EXRTEHED.
     A*
     A*   SQL Insert and Update statements should work with
     A*   individual fields to prevent a level check error.
     A*
     A*   MRC programs should not be affected by new fields because
     A*   they use SQL.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Field Notes
     A*
     A*   RHATBTYPE - TruckBuilder type (S=Standard, E=Enhanced)
     A*
740 AA*   RHRDYPP   - Identifies whether route is ready to be advanced
     A*               to Pick Print from Built routes.  This is for
     A*               information only and not used by system.  Coastal
     A*               wanted flag since separate person advances routes
     A*               to Pick and Print than actual Truck Builder
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R RHAREC
     A*
     A            RHAWHSE   R               REFFLD(WHCODE)
     A            RHARTID   R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            RHATBTYPE      1          TEXT('TruckBuilder Type')
     A                                      COLHDG('TruckBuilder' 'Type')
740 AA            RHARDYPP       1          TEXT('Ready Pick/Print')
740 AA                                      COLHDG('Ready' 'Pick/Print')
750 AA            RHAASN         1          TEXT('On-Demand Assignment')
750 AA                                      COLHDG('On-Demand' 'Assignment')
     A*
     A          K RHAWHSE
     A          K RHARTID
