     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSORDC (PF)
     A*  Description ... Staging Export - Order Catch Weights
     A*
     A*  Revisions
     A*
640 AA*    01/15/11  DAS  6.40
     A*      - Created
     A*      - Base on EXPFORDC
     A*
640aAA*    02/10/11  DAS  6.40a
     A*      - Renamed OECSQ# to OECOSQ.
     A*
     A*----------------------------------------------------------------
     A          R OECREC
     A*
     A            OECWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OECBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            OECRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
730A  *           OECORD         7S 0       TEXT('Order Number')
730A A            OECORD        15          TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
640aMA            OECOSQ         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OECITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OECSEQ         3S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            OECSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            OECSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OECSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OECTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            OECRTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            OECORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            OECWGT         7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            OECLBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            OECTRN         7S 0
     A                                      TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A*
     A*  Add stamps
     A*
     A            OECADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OECADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K OECWHS
     A          K OECBATID
     A          K OECRTEID
     A          K OECORD
     A          K OECSEQ
     A          K OECITM
     A          K OECLBL
