     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDAD  (PF)
     A*  Description ... Order Detail Walgreens SSCC-18 Format
     A*  Date .......... Jan 11, 2008
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    01/11/08  RBD  5.20  P#00003
     A*      - Created.
     A*
520aAA*    02/14/08  RBD  5.20a  P#00011
     A*      - Added type 'T' definition to ORDLTY, for totes.
     A*      - Did not recompile.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*    - The ORDUP# is unique, and once assigned, it must not be
     A*      reused within the next three years, per Walgreens.
     A*      Because ORDUP# is 9 digits, that should never be a worry.
     A*
     A*    - The ORDLTY label type may be : O = Normal
     A*                                     B = Bulk
     A*                                     M = Merge
520aAA*                                     T = Tote
     A*
     A*    - The ORDUMT uom type may be : N = Normal
     A*                                   1 = Breakdown 1
     A*                                   2 = Breakdown 2
     A*
     A*    - The ORDCNT and ORDTTL create a "1 of 2", "2 of 2"
     A*      combination.
     A*
     A*    - The ORDTTL corresponds to the number of labels printed
     A*      for the label#.
     A*
     A*    - The ORDSCN field will be 1 when 1 label is printed for
     A*      each piece.  Otherwise, it will contain the picked qty.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R ORDREC
     A*
     A            ORDWHS         3P 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            ORDORD         7P 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            ORDSQ#         5P 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            ORDITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            ORDUP#         9P 0       TEXT('Unique Piece #')
     A                                      COLHDG('Unique' 'Piece #')
     A            ORDLBL         7P 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            ORDLTY         1A         TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            ORDPAL         7P 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            ORDTRN         7P 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            ORDUMT         1A         TEXT('UOM Type')
     A                                      COLHDG('UOM' 'Type')
     A            ORDCNT         5P 0       TEXT('Count')
     A                                      COLHDG('Count')
     A            ORDTTL         5P 0       TEXT('Total ORDAD Recs')
     A                                      COLHDG('Total' 'ORDAD Recs')
     A            ORDSCN         5P 0       TEXT('Scan Qty')
     A                                      COLHDG('Scan' 'Qty')
     A*
     A          K ORDWHS
     A          K ORDORD
     A          K ORDUP#
