     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SLOT  (PF)
     A*  Description ... Slot file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 2.00   07/22/94  DAS
     A*      - Major change. Combined ITEMQTY and SLOT files. Added
     A*        new fields. Split qty into 3 fields.
     A*
     A*    Ver 2.01   09/29/94  DAS
     A*      - Added transfer quantity.
     A*
     A*    Ver n/a    10/28/94  DAS
     A*      - Changed meaning of transfer qty from "not printed on
     A*        labels" to being printed on labels.
     A*
213 AA*    Ver 2.02   05/13/96  DAS
213 AA*      - Added new fields SLSIDE and SLSLFD.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status code (SLSTAT)
     A*
     A*      'A'  -  Active slot. Item occupies slot.
     A*      'I'  -  Inactive slot. Do not use slot.  .
     A*      'V'  -  Verified slot.  Ready to be put to use.
     A*      'Z'  -  Zero slot.  Slot quantity went to zero. Must be
     A*              verified before it can be reused.
     A*
     A*      Other status codes will be treated as inactive.
     A*
     A*    Side code (SLSIDE)
     A*
     A*      0  -  Even
     A*      1  -  Odd
     A*
     A*----------------------------------------------------------------
     A*
     A*  Fixed slot notes:
     A*
     A*    A fixed slot can be reserved for one item by entering an
     A*    item number for that slot through the slot maintenance
     A*    program.
     A*
     A*    If a fixed slot is not reserved for an item it is considered
     A*    a floating slot and is treated like a PIR slot.
     A*
     A*----------------------------------------------------------------
     A*
     A*  PIR slot notes:
     A*
     A*    PIR slots are floating slots and are never reserved for
     A*    a particular item number.  When the quantity of a PIR slot
     A*    goes to zero its status is changed to 'Z'.  The slot must
     A*    then be manually verified that it is empty before its status
     A*    is changed to 'V' and it can be reused.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Quantity notes:
     A*
     A*    Each quantity type (ie. Stock qty) has three parts. Qty1
     A*    is the normal quantity. Qty2 is the breakdown 1 quantity and
     A*    Qty3 is the breakdown 2 quantity. The unit of measure for
     A*    each of these quantities is defined in the item file.
     A*
     A*    Type            Description
     A*    -------------   -------------------
     A*    Stock           Physical quantity in slot.
     A*
     A*    Allocated       Quantity selected for picking but not yet
     A*                    printed on labels.
     A*
     A*    Transfer        Quantity printed on labels for transfer to
     A*                    another slot.
     A*
     A*    Picking         Quantity printed on labels for picking.
     A*
     A*    Receiving       Quantity printed on labels to be received
     A*                    into this slot.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R SLREC
     A*
     A            SLWHSE    R               REFFLD(WHCODE)
     A            SLWHDP    R               REFFLD(WDCODE)
     A*
     A            SLAISL    R               REFFLD(ASCODE)
     A            SLLOC     R               REFFLD(SLLOC)
     A            SLRLVL    R               REFFLD(SLRLVL)
     A            SLHAND    R               REFFLD(HSCODE)
213 AA            SLSIDE         1S 0       TEXT('Side')
213 AA                                      COLHDG('Side')
     A*
     A            SLSTYP    R               REFFLD(SLSTYP)
     A*
     A            SLDISP        12          TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            SLSTAT    R               REFFLD(SSCODE)
     A            SLSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            SLSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            SLSDEF    R               REFFLD(SLSDEF)
     A            SLDESG    R               REFFLD(DSCODE)
     A            SLACTV    R               REFFLD(ACCODE)
     A            SLBLD     R               REFFLD(BCCODE)
     A            SLPICK         1          TEXT('Pick Slot')
     A                                      COLHDG('Pick Slot')
     A            SLPSEQ         5P 0       TEXT('Picking Sequence')
     A                                      COLHDG('Picking' 'Sequence')
     A            SLPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            SLENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            SLEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
213 AA            SLSLFD    R               TEXT('Shelf Life Date')
213 AA                                      COLHDG('Shelf Life' 'Date')
213 AA                                      REFFLD(DTECMD)
     A            SLRSRV         1          TEXT('Reserved')
     A                                      COLHDG('Reserved')
     A            SLITEM    R               REFFLD(ITITEM)
     A*
     A            SLSTK1         5P 0       TEXT('Stock Qty1')
     A                                      COLHDG('Stock' 'Qty1')
     A            SLSTK2         3P 0       TEXT('Stock Qty2')
     A                                      COLHDG('Stock' 'Qty2')
     A            SLSTK3         3P 0       TEXT('Stock Qty3')
     A                                      COLHDG('Stock' 'Qty3')
     A*
     A            SLALC1         5P 0       TEXT('Allocated Qty1')
     A                                      COLHDG('Allocated' 'Qty1')
     A            SLALC2         3P 0       TEXT('Allocated Qty2')
     A                                      COLHDG('Allocated' 'Qty2')
     A            SLALC3         3P 0       TEXT('Allocated Qty3')
     A                                      COLHDG('Allocated' 'Qty3')
     A*
     A            SLTFR1         5P 0       TEXT('Transfer Qty1')
     A                                      COLHDG('Transfer' 'Qty1')
     A            SLTFR2         3P 0       TEXT('Transfer Qty2')
     A                                      COLHDG('Transfer' 'Qty2')
     A            SLTFR3         3P 0       TEXT('Transfer Qty3')
     A                                      COLHDG('Transfer' 'Qty3')
     A*
     A            SLPCK1         5P 0       TEXT('Picking Qty1')
     A                                      COLHDG('Picking' 'Qty1')
     A            SLPCK2         3P 0       TEXT('Picking Qty2')
     A                                      COLHDG('Picking' 'Qty2')
     A            SLPCK3         3P 0       TEXT('Picking Qty3')
     A                                      COLHDG('Picking' 'Qty3')
     A*
     A            SLRCV1         5P 0       TEXT('Receive Qty1')
     A                                      COLHDG('Receive' 'Qty 1')
     A            SLRCV2         3P 0       TEXT('Receive Qty2')
     A                                      COLHDG('Receive' 'Qty 2')
     A            SLRCV3         3P 0       TEXT('Receive Qty3')
     A                                      COLHDG('Receive' 'Qty 3')
