     A*----------------------------------------------------------------
     A*   Copyright (C) 2009 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 15
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPFORDH4  (PF)
     A*  Description ... FTP Customer order import file Ver 4 - Header
     A*  Creator ....... MLB
     A*
     A*  This file should be identical to IMPORDH except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFORDH4.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
520 AA*    01/21/09  MLB  5.20
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Order types (OIHTYP)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
     A*                                     UNIQUE
     A          R OIHREC
     A*
     A            OIHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OIHORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OIHTYP         1          TEXT('Order Type')
     A                                      COLHDG('Order' 'Type')
     A            OIHCUS        11S 0       TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            OIHCPO        15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            OIHCNM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A            OIHCA1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            OIHCA2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            OIHCCT        20          TEXT('City')
     A                                      COLHDG('City')
     A            OIHCST         2          TEXT('State')
     A                                      COLHDG('State')
     A            OIHCZP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            OIHRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            OIHEST         5S 0       TEXT('Extended Stop Nbr')
     A                                      COLHDG('Extended Stop' 'Nbr')
     A            OIHTRK        10          TEXT('Truck')
     A                                      COLHDG('Truck')
     A            OIHMSC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A*
     A          K OIHWHS
     A          K OIHRTE
     A          K OIHORD
