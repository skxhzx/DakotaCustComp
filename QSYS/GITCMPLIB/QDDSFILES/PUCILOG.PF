      *----------------------------------------------------------------
      *   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved
      *   BFC Software, Inc.
      *   245 W. Roosevelt Rd.  BLDG 8, SUITE 51
      *   West Chicago, IL 60185
      *   (630) 562-0375
      *----------------------------------------------------------------
      *
      *  File name ..... PUCILOG  (PF)
      *  Description ... UCI log file. - OLD USED BY PACKERS
      *
      *  Revisions
      *
510 A *    09/27/06  JJ  5.10
      *      - Created
      *----------------------------------------------------------------***
      *
      *  Audit Codes
      *
      *    UCI Log Type (ULTYPE)
      *
      *      'A'  -  Add
      *      'C'  -  Change
      *      'D'  -  Deleted
      *
      *    UCI Program Type (ULPTYP)
      *
      *      'Adjustment'
      *      'Receiving '
      *      'Selection '
      *      'Return    '
      *      'Count     '
      *
      *    UCI Maint Type Flag (ULTFLG)
      *
      *      'M=Manual'
      *      'A=Auto  '
      *----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ULREC
     A*
     A            ULWHSE    R               REFFLD(WHCODE)
     A            ULINBR        10          TEXT('UCI Number')
     A                                      COLHDG('UCI' 'Number')
     A            ULIITM    R               REFFLD(ITITEM)
     A            ULCWGT         7P 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            ULADTE    R               REFFLD(DTECMD)
     A                                      TEXT('Audit Date')
     A                                      COLHDG('Audit' 'Date')
     A            ULATIM    R               REFFLD(TIMHMS)
     A                                      TEXT('Audit Time')
     A                                      COLHDG('Audit' 'Time')
     A            ULUSER    R               REFFLD(USER)
     A                                      TEXT('Audit User')
     A                                      COLHDG('Audit' 'User')
     A            ULJOB     R               REFFLD(JOB)
     A                                      TEXT('JOB')
     A                                      COLHDG('JOB')
     A            ULJNBR    R               REFFLD(JOBNBR)
     A                                      TEXT('Job Number')
     A                                      COLHDG('Job' 'Number')
     A            ULTYPE    R               REFFLD(JOBNBR)
     A                                      TEXT('Audit Type')
     A                                      COLHDG('Audit' 'Type')
     A            ULPTYP    R               REFFLD(PROGRAM)
     A                                      TEXT('Program Type')
     A                                      COLHDG('Program' 'Type')
     A            ULCPGM    R               REFFLD(PROGRAM)
     A                                      TEXT('Calling Program')
     A                                      COLHDG('Calling' 'Program')
     A            ULUPGM    R               REFFLD(PROGRAM)
     A                                      TEXT('Update Program')
     A                                      COLHDG('Update' 'Program')
     A            ULPO      R               REFFLD(PONUM)
     A                                      TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A            ULSEQ#    R               REFFLD(SEQ#)
     A                                      TEXT('P.O. Line#')
     A                                      COLHDG('P.O.' 'Line#')
     A            ULPTRN    R               REFFLD(TRAN#)
     A                                      TEXT('Putaway Tran Number')
     A                                      COLHDG('Putaway' 'Tran' 'Number')
     A            ULLCN#        15
     A                                      TEXT('Putaway License#')
     A                                      COLHDG('Putaway' 'License#')
     A            ULORD     R               REFFLD(OHORD)
     A                                      TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            ULOSEQ    R               REFFLD(SEQ#)
     A                                      TEXT('Select Order Line#')
     A                                      COLHDG('Selection' 'Order' 'Line#')
     A            ULSTRN    R               REFFLD(TRAN#)
     A                                      TEXT('Select Tran Number')
     A                                      COLHDG('Select' 'Tran' 'Number')
     A            ULLBL#    R               REFFLD(TRAN#)
     A                                      TEXT('Select Label Number')
     A                                      COLHDG('Select' 'Label' 'Number')
     A            ULCODE    R               REFFLD(ACCODE)
     A                                      TEXT('Adjustment Code')
     A                                      COLHDG('Adjustment' 'Code')
     A            ULTFLG    R               REFFLD(JOBNBR)
     A                                      TEXT('Maint Type Flag')
     A                                      COLHDG('Maint' 'Type' 'Flag')
