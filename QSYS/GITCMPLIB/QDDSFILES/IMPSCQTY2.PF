     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSCQTY2 (PF)
     A*  Description ... Staging Import - Host Qty for Compare V2
     A*
     A*  Revisions
     A*
720  A*    12/12/18  RTR  7.20
     A*      - Created based on IMPFCQTY. Needed for hosts with decimal
     A*        on-hand qtys. Only qty fields changed.
730a A*    09/09/19  RTR  7.30a
     A*      - Changed decimal fields from 9s2 to 9s3.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R ICREC
     A*
     A            ICWHSE         3S 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            ICBATID       11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            ICSTATUS       1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            ICSTSTS         Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            ICSTSUC         Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            ICTEXT       200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            ICITEM        15          TEXT('Item')
     A                                      COLHDG('Item')
730aMA            ICTQH1         9S 3       TEXT('Total Qty1 on hand')
     A                                      COLHDG('Total' 'Qty1' 'On hand')
730aMA            ICTQH2         9S 3       TEXT('Total Qty2 Onhand')
     A                                      COLHDG('Total' 'Qty2' 'Onhand')
730aMA            ICTQH3         9S 3       TEXT('Total Qty3 Onhand')
     A                                      COLHDG('Total' 'Qty3' 'Onhand')
     A            ICBRND        10A         TEXT('Brand')
     A                                      COLHDG('Brand')
     A*
     A*  Add stamps
     A*
     A            ICADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            ICADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A            ICLCNS        15A         TEXT('License')
     A                                      COLHDG('License')
     A*
     A          K ICWHSE
     A          K ICBATID
     A          K ICITEM
     A          K ICBRND
     A          K ICLCNS
