     A*****************************************************************
     A*                                                               *
     A* OR550    OPNQRYF to summarize routes for loading sheet        *
     A* 09 October 1994                                               *
     A* Dave Sommerville                                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(PIRDICT)
     A          R OQREC
     A*
     A* Fields from label file.
     A*
     A            LBPBAT    R               REFFLD(BATCH#)
     A            LBWHSE    R               REFFLD(WHCODE)
     A            LBRTE     R               REFFLD(ROUTE)
     A            LBSTOP    R               REFFLD(STOP)
     A            LBWHDP    R               REFFLD(WDCODE)
     A            LBORD     R               REFFLD(OHORD)
     A*
     A* Fields from order header file.
     A*
     A            OHCUST    R               REFFLD(CUSNUM)
     A            OHCNAM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A*
     A* Fields calculated by OPNQRYF.
     A*
     A            OQFPCS         5P 0       TEXT('Fixed Pieces')
     A                                      COLHDG('Fixed Pieces')
     A            OQFCUB         9P 3       TEXT('Fixed Cube')
     A                                      COLHDG('Fixed Cube')
     A            OQFWGT         9P 2       TEXT('Fixed Weight')
     A                                      COLHDG('Fixed Weight')
     A            OQPPCS         5P 0       TEXT('PIR Pieces')
     A                                      COLHDG('PIR Pieces')
     A            OQPCUB         9P 3       TEXT('PIR Cube')
     A                                      COLHDG('PIR Cube')
     A            OQPWGT         9P 2       TEXT('PIR Weight')
     A                                      COLHDG('PIR Weight')
     A*
     A*  File key.
     A*
     A          K LBPBAT
     A          K LBWHSE
     A          K LBRTE
     A          K LBSTOP
     A          K LBWHDP
     A*
