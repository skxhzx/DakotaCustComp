     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSPOLT  (PF)
     A*  Description ... Staging Export - P.O. Host Lots
     A*
     A*  Revisions
     A*
720 AA*    12/13/18  DAS  7.20
     A*      - Created
     A*
730 AA*    07/28/19  DAS  7.30
     A*      - Added PELTHSTLOT to end of key.
     A*
730aAA*    08/21/19  DAS  7.30a
     A*      - Added PELTWGT field.
     A*
740a A*    09/28/20  RTR  7.40a
     A*      - Added PELTDISP field.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file contains Host Lot information that corresponds to
     A*   the EXPSPOD file. There may be 1 or more records in this
     A*   file associated with an EXPSPOD record.
     A*
     A*   The total of the quantities of the associated records
     A*   in this file should equal the quantities in the corresponding
     A*   EXPSPOD record.
     A*
     A*   Typically, the Host Lot field will contain either the Dakota
     A*   License or the GS1 Serial Number from the Dakota UCI, the
     A*   latter being used with Unique UCI tracked items.
     A*
     A*   The following fields would only be used when working with
     A*   a UCI tracked item:
     A*
     A*     PELTUCI
     A*     PELTUCISEQ
     A*     PELTGTIN
     A*     PELTDATE
     A*     PELTDATECD
     A*     PELTDATENM
     A*     PELTLOT
     A*     PELTSERIAL
     A*
     A*   PELTLOT and PELTSERIAL are defined as 30 to match other files.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Date AI Codes
     A*
     A*    Code  Name  Description
     A*    ----  ----  -----------
     A*     11   PROD  Production date (YYMMDD)
     A*     12   DUE   Due date (YYMMDD)
     A*     13   PACK  Packaging date (YYMMDD)
     A*     15   BEST  Best before date (YYMMDD)
     A*     16   SELL  Sell by date (YYMMDD)
     A*     17   EXP   Expiration date (YYMMDD)
     A*
     A*----------------------------------------------------------------
     A          R PELTREC
     A*
     A            PELTWHS        3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PELTBATID     11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            PELTPO         9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A            PELTSEQ        5S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            PELTITM       15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            PELTLBL        7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            PELTSTATUS     1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            PELTSTSTS       Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PELTSTSUC       Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PELTTEXT     200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            PELTHSTLOT    30          TEXT('Host Lot Code')
     A                                      COLHDG('Host Lot Code')
     A            PELTQTY1       5S 0       TEXT('Normal Qty')
     A                                      COLHDG('Normal' 'Qty')
     A            PELTQTY2       5S 0       TEXT('Brkdn1 Qty')
     A                                      COLHDG('Brkdn1' 'Qty')
     A            PELTQTY3       5S 0       TEXT('Brkdn2 Qty')
     A                                      COLHDG('Brkdn2' 'Qty')
730aAA            PELTWGT        7S 2       TEXT('Catch Weight')
730aAA                                      COLHDG('Catch' 'Weight')
     A            PELTLCNS      15          TEXT('License')
     A                                      COLHDG('License')
     A            PELTUCI      100          TEXT('UCI')
     A                                      COLHDG('UCI')
     A            PELTUCISEQ     5S 0       TEXT('UCI Sequence Number')
     A                                      COLHDG('UCI Sequence' 'Number')
     A            PELTGTIN      14          TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            PELTDATE       6S 0       TEXT('Date YYMMDD')
     A                                      COLHDG('Date YYMMDD')
     A            PELTDATECD     2          TEXT('Date AI Code')
     A                                      COLHDG('Date' 'AI Code')
     A            PELTDATENM     4          TEXT('Date AI Name')
     A                                      COLHDG('Date' 'AI Name')
     A            PELTLOT       30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            PELTSERIAL    30          TEXT('Serial Nunber')
     A                                      COLHDG('Serial Number')
     A            PELTTRN        7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            PELBRD        10A         TEXT('Mfr Brand')
     A                                      COLHDG('Mfr Brand')
     A            PELOCO        15A         TEXT('Origin Country')
     A                                      COLHDG('Origin Country')
     A            PELOST        15A         TEXT('Origin State')
     A                                      COLHDG('Origin State')
     A            PELOCY        15A         TEXT('Origin County')
     A                                      COLHDG('Origin County')
     A            PELRBY        10A         TEXT('Origin State')
     A                                      COLHDG('Origin State')
     A            PELEMP         5S 0       TEXT('Origin County')
     A                                      COLHDG('Origin County')
     A*
     A*  Add stamps
     A*
     A            PELTADDTS       Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            PELTADDUC       Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
740aAA            PELTDISP      12A         TEXT('Slot Display')
740aAA                                      COLHDG('Slot' 'Display')
     A*
     A          K PELTWHS
     A          K PELTBATID
     A          K PELTPO
     A          K PELTSEQ
     A          K PELTITM
     A          K PELTLBL
     A          K PELTHSTLOT

