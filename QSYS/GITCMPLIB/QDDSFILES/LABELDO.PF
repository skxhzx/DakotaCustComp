     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABELDO  (PF)
     A*  Description ... Label detail file - O/M/B recs
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions
520 AI*    06/01/08  DAS  5.20  P#00059
     I*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*  - LBOREF# should be used to group records together, such as
     A*    VSENT/VSKIP records that are part of a single PICKS command.
     A*    This field should contain YYMMDDHHMMSS.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Action code  (LBOACT)
     A*
     A*       'VPICK ' - Voice Picked
     A*          (Runner pick can be retrieved from RUNNER file)
     A*       'VSENT ' - Sent to voice unit
     A*       'VSKIP ' - Not sent to voice unit
     A*       'RETURN' - Return
     A*       'TBADJ ' - Truck Builder adjustment
     A*       'TBCLS ' - Truck Builder closed
     A*
     A*    Program Type (LBOPTYP)
     A*
     A*       'SELECT' - Selection
     A*       'RUNNER' - Runner
     A*       'TRKBLD' - Truck Builder
     A*       'RETURN' - Returns
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R LBOREC
     A*
     A            LBOWHSE   R               TEXT('Warehouse')
     A                                      COLHDG('Whs')
     A                                      REFFLD(WHCODE)
     A            LBOACT         6A         TEXT('Action')
     A                                      COLHDG('Action')
     A            LBOLBL#   R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBOITEM   R               REFFLD(ITITEM)
     A            LBOTRN#   R               TEXT('Tran Number')
     A                                      COLHDG('Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBOTRN2#  R               TEXT('Tran2 Number')
     A                                      COLHDG('Tran2' 'Number')
     A                                      REFFLD(TRAN2#)
     A            LBOQTY         5P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            LBOQTYS        5P 0       TEXT('Short Qty')
     A                                      COLHDG('Short' 'Qty')
     A            LBOPGM        10A         TEXT('Program')
     A                                      COLHDG('Program')
     A            LBOPTYPE       6A         TEXT('Program  Type')
     A                                      COLHDG('Program' 'Type')
     A            LBOIP         15A         TEXT('IP Addr')
     A                                      COLHDG('IP Addr')
     A            LBOUID        10A         TEXT('Unit Id')
     A                                      COLHDG('Unit Id')
     A            LBOREF#       13P 0       TEXT('Ref#')
     A                                      COLHDG('Ref#')
     A            LBOLTYPE       1          TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            LBOEMP#   R               TEXT('Empl#')
     A                                      COLHDG('Empl#')
     A                                      REFFLD(EMPLOYEE#)
     A            LBOZONE   R               TEXT('Zone')
     A                                      COLHDG('Zone')
     A                                      REFFLD(ZHZONE)
     A            LBOSHIFT  R               TEXT('Shift')
     A                                      COLHDG('Shift')
     A                                      REFFLD(SHCODE)
     A            LBOCREW   R               TEXT('Crew')
     A                                      COLHDG('Crew')
     A                                      REFFLD(CRCODE)
     A            LBOWEEK        2S 0       TEXT('Week')
     A                                      COLHDG('Week')
     A            LBODOW         1S 0       TEXT('Day of Week')
     A                                      COLHDG('Day' 'of Week')
     A            LBOSOW         6S 0       TEXT('Sunday of Week')
     A                                      COLHDG('Sunday' 'of Week')
     A*
     A* Timestamp / User tracking information.
     A*
     A            LBOADDTS  R               REFFLD(ADDTS)
     A            LBOADDUC  R               REFFLD(ADDUC)
     A            LBOADDPGM R               REFFLD(ADDPGM)
     A            LBOADDUSR R               REFFLD(ADDUSR)
     A            LBOADDJOB R               REFFLD(ADDJOB)
     A            LBOADDNBR R               REFFLD(ADDNBR)
     A*
     A          K LBOLBL#
     A          K LBOTRN#
     A          K LBOADDUC
