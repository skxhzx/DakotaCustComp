     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTINV (PF)
     A*  Description ... Trax invoice file
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740aAA*    11/09/20  RBD  7.40a
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    - TTVEXCP: Exception = Y if tax or total invoice differs
     A*      between values sent in the invoice import file and values
     A*      sent back from the Trax unit in this file.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R TTVREC
     A*
     A            TTVWHS    R               REFFLD(WHCODE)
     A            TTVRTI    R               REFFLD(ROUTE)
     A                                      COLHDG('Route' 'Id')
     A            TTVSTP    R               REFFLD(STOP)
     A            TTVTYP         1          COLHDG('Document' 'Type')
     A            TTVEMP         5P 0       COLHDG('Driver' 'Id')
     A            TTVINV        15          COLHDG('Invoice' 'Number')
     A            TTVCORD      200          VARLEN
     A                                      COLHDG('Cust Order' 'Number')
     A            TTVORDID     200          VARLEN
     A                                      COLHDG('Order' 'Id')
     A            TTVIQTY        9P 2       COLHDG('Invoice Tot' 'Quantity')
     A            TTVIBTX        9P 2       COLHDG('Invoice Tot' 'Before Tax')
     A            TTVITTX        9P 2       COLHDG('Invoice' 'Tot Tax')
     A            TTVITOT        9P 2       COLHDG('Invoice' 'Total')
     A            TTVCUR         1          COLHDG('Currency')
     A            TTVPRT          Z         COLHDG('Invoice' 'Printed TS')
     A            TTVEXCP        1          COLHDG('Exception' 'Y/N')
     A            TTVRTN         1          COLHDG('Invoice Has' 'Returns')
     A            TTVRQTY        9P 2       COLHDG('Returns Tot' 'Quantity')
     A            TTVRBTX        9P 2       COLHDG('Returns Tot' 'Before Tax')
     A            TTVRTTX        9P 2       COLHDG('Returns' 'Tot Tax')
     A            TTVRTOT        9P 2       COLHDG('Returns' 'Total')
     A            TTVADD          Z         COLHDG('Added TS')
     A*
     A          K TTVWHS
     A          K TTVRTI
     A          K TTVINV
     A          K TTVCORD
