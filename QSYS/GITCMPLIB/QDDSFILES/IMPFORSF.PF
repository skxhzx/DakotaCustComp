     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPFORSF  (PF)
     A*  Description ... FTP Stop Flags import file
     A*  Creator ....... RBD
     A*
     A*  This file should be identical to IMPORSF except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFORSF.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    01/12/11  RBD  6.40
     A*      - Created.
     A*
650 AA*    01/16/13  RBD  6.50
     A*      - Removed some fields, added some fields.  This file has
     A*        not yet been used anywhere so to keep it neat and clean
     A*        I did not add mod marks.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    Stop Flags display time
     A*
     A*       ' '  -
     A*       '1'  - After download route
     A*       '2'  - Before scan stop
     A*       '3'  - After scan stop
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R OISREC
     A*
     A            OISWHS    R               REFFLD(WHCODE)
     A            OISRTE    R               REFFLD(ROUTE)
     A            OISORD         7S 0
     A                                      TEXT('Customer Order Number')
     A                                      COLHDG('Customer' 'Order Number')
     A            OISCHR         1          TEXT('Stop Flag Char')
     A                                      COLHDG('Stop Flag' 'Char')
     A            OISALE       100          TEXT('Stop Flag Alert')
     A                                      COLHDG('Stop Flag Alert')
     A            OISUID        12          TEXT('Unique ID')
     A                                      COLHDG('Unique ID')
     A            OISDSP         1          TEXT('Stop Flags Display Time')
     A                                      COLHDG('Stop Flags' 'Display Time')
     A*
     A          K OISWHS
     A          K OISRTE
     A          K OISORD
     A          K OISCHR
