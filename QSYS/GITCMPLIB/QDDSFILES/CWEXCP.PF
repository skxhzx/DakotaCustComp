     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... CWEXCP (PF)
     A*  Description ... Catchweight exception file
     A*  Date .......... August 18, 2004
     A*  Creator ....... DAS
     A*
     A*  Revisions
     F*
417 AF*    08/18/04  DAS  4.17
     F*      - Created
     F*
     A*----------------------------------------------------------------***
     A*
     A*  Codes
     A*
     A*      Status Codes (CXESTA)
     A*        1 = Open
     A*        4 = Closed
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Task/Error codes
     A*
     A*  Error    Description
     A*  -------  ---------------------------------------------
     A*  OVRTKN   Override taken
     A*
     A*----------------------------------------------------------------***
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R CXREC
     A            CXWHSE    R               REFFLD(WHCODE)
     A            CXTRN#    R               REFFLD(TRAN#)
     A            CXLBL#    R               REFFLD(TRAN#)
     A            CXITEM    R               REFFLD(ITITEM)
     A            CXWGT          7P 2       TEXT('Weight Entered')
     A                                      COLHDG('Weight' 'Entered')
     A            CXRNGL         7P 2       TEXT('Low Range')
     A                                      COLHDG('Low' 'Range')
     A            CXRNGH         7P 2       TEXT('High Range')
     A                                      COLHDG('High' 'Range')
     A            CXGDTE    R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD)
     A            CXGTIM    R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS)
     A            CXGEMP         5P 0       TEXT('Generated by Emp#')
     A                                      COLHDG('Generated' 'by Emp#')
     A            CXGBY         10A         TEXT('Generated by User')
     A                                      COLHDG('Generated by' 'User')
     A            CXERID         7A         TEXT('Error Id')
     A                                      COLHDG('Error' 'Id')
     A            CXESTA         2A         TEXT('Status')
     A                                      COLHDG('Status')
     A            CXUSER        10A         TEXT('Workstn User id')
     A                                      COLHDG('Workstn' 'User Id')
     A            CXCDTE    R               TEXT('Date corrected')
     A                                      COLHDG('Date' 'Corrected')
     A                                      REFFLD(DTECMD)
     A            CXCTIM    R               TEXT('Time Corrected')
     A                                      COLHDG('Time' 'Corrected')
     A                                      REFFLD(TIMHMS)
     A*
     A*  File key.
     A*
     A          K CXWHSE
     A          K CXTRN#
     A          K CXITEM
     A          K CXLBL#
     A          K CXGDTE
     A          K CXGTIM
     A*
