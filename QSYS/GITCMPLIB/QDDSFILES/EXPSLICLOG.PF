     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSLICLOG (PF)
     A*  Description ... Staging Export - LICLOG
     A*
     A*  Revisions
     A*
640 AA*    05/28/11  DAS  6.40
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R LEREC
     A*
     A            LEWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            LEBATID       11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            LESTATUS       1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            LESTSTS         Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            LESTSUC         Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            LETEXT       200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            LELTRN    R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            LEACTION       3A         TEXT('Action')
     A                                      COLHDG('Action')
     A            LETRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            LEETRN#   R               TEXT('Employee Transaction Number')
     A                                      COLHDG('Employee' 'Transaction' -
     A                                      'Number')
     A                                      REFFLD(TRAN#)
     A            LELBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LELCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            LEOLCNS       15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            LEVRFY         1A         TEXT('Lic Verified')
     A                                      COLHDG('Lic' 'Verified')
     A            LEFLCNS       15A         TEXT('"From" License Number')
     A                                      COLHDG('"From"' 'License' 'Number')
     A*
     A            LEAQTY1        5P 0       TEXT('Action Qty1')
     A                                      COLHDG('Action' 'Qty1')
     A            LEAQTY2        5P 0       TEXT('Action Qty2')
     A                                      COLHDG('Action' 'Qty2')
     A            LEAQTY3        5P 0       TEXT('Action Qty3')
     A                                      COLHDG('Action' 'Qty3')
     A*
     A            LERQTY1        5P 0       TEXT('Result Qty1')
     A                                      COLHDG('Result' 'Qty1')
     A            LERQTY2        5P 0       TEXT('Result Qty2')
     A                                      COLHDG('Result' 'Qty2')
     A            LERQTY3        5P 0       TEXT('Result Qty3')
     A*
     A            LESQTY1        5P 0       TEXT('Sent Actn Qty1')
     A                                      COLHDG('Sent' 'Actn Qty1')
     A            LESQTY2        5P 0       TEXT('Sent Actn Qty2')
     A                                      COLHDG('Sent' 'Actn Qty2')
     A            LESQTY3        5P 0       TEXT('Sent Actn Qty3')
     A                                      COLHDG('Sent' 'Actn Qty3')
     A*
     A* "To" Location information
     A*
     A            LECODE         1A         TEXT('To Code')
     A                                      COLHDG('To' 'Code')
     A            LEAREA        15A         TEXT('To Area')
     A                                      COLHDG('To' 'Area')
     A            LEWHDP    R               TEXT('To Department')
     A                                      COLHDG('To' 'Department')
     A                                      REFFLD(WDCODE)
     A            LEDISP        12A         TEXT('To Display')
     A                                      COLHDG('To' 'Display')
     A            LEAISL    R               TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            LELOC     R               TEXT('To Location')
     A                                      COLHDG('To' 'Location')
     A                                      REFFLD(SLLOC)
     A            LERLVL    R               TEXT('To Rack Level')
     A                                      COLHDG('To' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LEHAND    R               TEXT('To Handstack')
     A                                      COLHDG('To' 'Handstack')
     A                                      REFFLD(HSCODE)
     A            LETSTAT   R               TEXT('To Status')
     A                                      COLHDG('To' 'Status')
     A                                      REFFLD(SSCODE)
     A            LEOSTAT   R               TEXT('Orig Status')
     A                                      COLHDG('Orig' 'Status')
     A                                      REFFLD(SSCODE)
     A            LEBASE        12A         TEXT('To Base Slot')
     A                                      COLHDG('To' 'Base Slot')
     A            LEPOS          3P 0       TEXT('To Pallet Position')
     A                                      COLHDG('To' 'Pallet' 'Position')
     A            LEITEM    R               TEXT('To Item')
     A                                      COLHDG('To' 'Item')
     A                                      REFFLD(ITITEM)
     A            LEITYPE        1A         TEXT('To Item Type')
     A                                      COLHDG('To' 'Item Type')
     A            LEICHG         1A         TEXT('Item Changed')
     A                                      COLHDG('Item' 'Changed')
     A            LESITEM   R               TEXT('To Slot Item')
     A                                      COLHDG('To' 'Slot Item')
     A                                      REFFLD(ITITEM)
     A            LESITYPE       1A         TEXT('Slot Item Type')
     A                                      COLHDG('Slot' 'Item Type')
     A            LEPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            LEENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            LEEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A            LETADDTS  R               REFFLD(ADDTS)
     A                                      TEXT('To Add Timestamp')
     A                                      COLHDG('To Add Timestamp')
     A            LETADDUC  R               REFFLD(ADDUC)
     A                                      TEXT('To Add UC Stamp')
     A                                      COLHDG('To Add UC Stamp')
     A*
     A* To Customer information
     A*
     A            LEORD     R     P         TEXT('To Order Number')
     A                                      COLHDG('To' 'Order' 'Number')
     A                                      REFFLD(OHORD)
     A            LESEQ     R     P         TEXT('To Order Sequence Number')
     A                                      COLHDG('To Order' 'Sequence Nbr')
     A                                      REFFLD(SEQ#)
     A*
     A            LEEXCD         8A         TEXT('Exception Code')
     A                                      COLHDG('Exception' 'Code')
     A*
     A* Client Custom Fields
     A*
     A            LECSTM1       15          TEXT('Custom 1')
     A                                      COLHDG('Custom 1')
     A            LECSTM2       15          TEXT('Custom 2')
     A                                      COLHDG('Custom 2')
     A            LECSTM3       15          TEXT('Custom 3')
     A                                      COLHDG('Custom 3')
     A*
     A*  Add stamps
     A*
     A            LEADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            LEADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K LEWHSE
     A          K LEBATID
     A          K LEADDUC
