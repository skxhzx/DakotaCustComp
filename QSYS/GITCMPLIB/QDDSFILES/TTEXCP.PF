     A*----------------------------------------------------------------
     A*   Copyright (C) 2017 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TTEXCP (PF)
     A*  Description ... Trax scan exceptions
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
710 AA*    01/13/17  RBD  7.10
     A*      - Created.
     A*
710aAA*    07/19/17  RBD  7.10a
     A*      - Added field for intended stop.
     A*
710bAA*    08/25/17  RBD  7.10b
     A*      - Added transaction number.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*   The Trax handheld writes records to this file.  All fields are
     A*   defined as alpha and some are defined as variable length because
     A*   the driver may scan a wrong barcode and this file will accept
     A*   whatever they have scanned.
710aAA*   New field intended stop is numeric, updated by TR846.
710bAA*   New field transaction is numeric, updated by TR846.
     A*
     A*----------------------------------------------------------------
     A*
     A          R TTEREC
     A*
     A            TTEWHS         3          COLHDG('Warehouse')
     A            TTERID         5          COLHDG('Route Id')
     A            TTEDRV       100          VARLEN(10)
     A                                      COLHDG('Driver')
     A            TTESTP       100          VARLEN(5)
     A                                      COLHDG('Stop')
     A            TTEITM       100          VARLEN(15)
     A                                      COLHDG('Item')
     A            TTESCN       100          VARLEN(18)
     A                                      COLHDG('Scan Id')
     A            TTEGTI       100          VARLEN(18)
     A                                      COLHDG('Gtin#')
     A            TTEOPF         1          COLHDG('Oper Flag')
     A            TTEFGR        12          COLHDG('From Grid')
     A            TTETGR        12          COLHDG('To Grid')
     A            TTETS         26          COLHDG('Timestamp')
710aAA            TTESTI         2S 0       COLHDG('Intended Stop')
710bAA            TTETRN         7S 0       COLHDG('Transaction')
     A*
     A          K TTEWHS
     A          K TTERID
