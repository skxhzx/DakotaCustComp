     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   Batavia, IL  60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... DDDETAILH (PF)
     A*  Description ... DragDrop 2.0 detail file by Id HISTORY
     A*  Creator ....... KDE
     A*
     A*  Revisions
     A*
     A*   ÂˆANY CHANGES MADE TO THIS FILE SHOULD BE REPLICATED to DDDETAIL
     A*
740 AA*    07/20/20  KDE  7.40
     A*      - Created. COPY OF DDDETAIL.  Record format and field names
     A*        same as live files.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*  DDDSNAP - Snapshot number
     A*
     A*    0 = Current
     A*    1 = Original
     A*    2+ = Possible future use
     A*
     A*  DDDID - Unique Id
     A*
     A*    This id should be unique across all records for the route.
     A*    Id is used in HTML.
     A*    Used to tie records to corressponding DDJSON records.
     A*
     A*  DDDPID - Parent Id
     A*
     A*  DDDGROUP - Group
     A*
     A*    T = Truck
     A*    O = Overflow
     A*    2 = Merge
     A*    3 = Merge
     A*
     A*  DDDLEVEL - Level (unique id)
     A*
     A*    1 = Location (gen/row/col)
     A*    2 = Pallet (pallet id)
     A*    3 = Pallet Section (pallet id / section)
     A*    4 = Stop/Drop (pallet id / section / stop / drop)
     A*    5 = Item/Tote (label #)
     A*    6 = Tote Items (label #)
     A*
     A*  DDDSIDE - Side of truck
     A*
     A*    L = Left/Driver
     A*    R = Right/Passenger
     A*    M = Middle
     A*
     A*  DDDAXLE - Axle key
     A*
     A*    Corresponds to TRAILERAX/TLADISTFR field.
     A*
     A*  DDDPCS/CUBE/SWGT = Pieces/Cube/Weight for level
     A*
     A*    Corresponds to the level
     A*
     A*  DDDCUBWRN - Cube Warning for level
     A*
     A*    0 = No warning
     A*    1 = Overcube Warning
     A*    2 = Overcube Error
     A*    3 = Undercube Warning
     A*
     A*  DDDWGTWRN - Weight Warning for level
     A*
     A*    0 = No warning
     A*    1 = Overweight Warning
     A*    2 = Overweight Error
     A*    3 = Underweight Warning
     A*
     A*  DDDSTPSPL- Stop is split between pallets
     A*
     A*  DDDSWFLAG - SW Flag (USF Only)
     A*
     A*  DDDMLTORD - Multiple Orders for Stop flag
     A*
     A*  DDDITMSPL - Item is split between pallets
     A*
     A*  DDDJSON - Corresponding JSON string.
     A*
     A*    Only populated for Snapshot zero.
     A*    Used for building GUI DD screen.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R DDDREC
     A*
     A            DDDWHSE   R               REFFLD(WHCODE)
     A            DDDRTID   R               TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A                                      REFFLD(ROUTE)
     A            DDDSNAP        2  0       TEXT('Shapshot')
     A                                      COLHDG('Snapshot')
     A            DDDID         20          TEXT('Id')
     A                                      COLHDG('Id')
     A            DDDPID        20          TEXT('Paret Id')
     A                                      COLHDG('Parent' 'Id')
     A*
     A            DDDGROUP       1          TEXT('Group')
     A                                      COLHDG('Group')
     A            DDDLEVEL       2  0       TEXT('Level')
     A                                      COLHDG('Level')
     A            DDDROW         3  0       TEXT('Row')
     A                                      COLHDG('Row')
     A            DDDCOL         2  0       TEXT('Column')
     A                                      COLHDG('Column')
     A            DDDPLID   R               TEXT('Pallet Id')
     A                                      COLHDG('Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            DDDPLTSCT      1          TEXT('Pallet Section')
     A                                      COLHDG('Pallet' 'Section')
     A            DDDSTOP   R               TEXT('Stop Id')
     A                                      COLHDG('Stop Id')
     A                                      REFFLD(STOP)
     A            DDDITMTYP      1          TEXT('Item Type')
     A                                      COLHDG('Item Type')
     A            DDDLBLTOT R               TEXT('Tote Label Num')
     A                                      COLHDG('Tote' 'Label Num')
     A                                      REFFLD(TRAN#)
     A            DDDLBLITM R               TEXT('Item Label Num')
     A                                      COLHDG('Item' 'Label Num')
     A                                      REFFLD(TRAN#)
     A*
     A*  Common Attributes
     A*
     A            DDDPCS         5  0       TEXT('Pieces')
     A                                      COLHDG('Pieces')
     A            DDDCUBE        7  3       TEXT('Cube')
     A                                      COLHDG('Cube')
     A            DDDMAXC        7  3       TEXT('Max cube')
     A                                      COLHDG('Max cube')
     A            DDDCUBWRN      1  0       TEXT('Cube Warning')
     A                                      COLHDG('Cube' 'Warning')
     A            DDDWGT         9  2       TEXT('Weight')
     A                                      COLHDG('Weight')
     A            DDDMAXW        9  2       TEXT('Max Weight')
     A                                      COLHDG('Max Weight')
     A            DDDWGTWRN      1  0       TEXT('Wgt Warning')
     A                                      COLHDG('Wgt' 'Warning')
     A*
     A*  Level 1 - Location Attributes
     A*
     A            DDDSIDE        1          TEXT('Side')
     A                                      COLHDG('Side')
     A            DDDAXLE        5  2       TEXT('Axle')
     A                                      COLHDG('Axle')
     A            DDDLSEQ        3  0       TEXT('Load Seq')
     A                                      COLHDG('Load' 'Seq')
     A            DDDUSEQ        3  0       TEXT('Unload Seq')
     A                                      COLHDG('Unload' 'Seq')
     A            DDDCOMP   R               REFFLD(TPCODE)
     A*
     A*  Level 2 - Pallet Attributes
     A*
     A            DDDPALNUM R               TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            DDDGEN         1  0       TEXT('Generation')
     A                                      COLHDG('Generation')
     A            DDDTZON   R               TEXT('Truck Zone')
     A                                      COLHDG('Truck' 'Zone')
     A                                      REFFLD(ZHZONE)
     A            DDDPZON   R               TEXT('Parent Zone')
     A                                      COLHDG('Parent' 'Zone')
     A                                      REFFLD(ZHZONE)
     A            DDDSZON   R               TEXT('Slot Zone')
     A                                      COLHDG('Slot' 'Zone')
     A                                      REFFLD(ZHZONE)
     A            DDDSTP1        5  0       TEXT('First Stop')
     A                                      COLHDG('First' 'Stop')
     A            DDDSTP2        5  0       TEXT('Last Stop')
     A                                      COLHDG('Last' 'Stop')
     A            DDDSPFLAG      1  0       TEXT('SP Flag')
     A                                      COLHDG('SP Flag')
     A            DDDSWFLAG      1  0       TEXT('SW Flag')
     A                                      COLHDG('SW Flag')
     A            DDDAMPFLG      1  0       TEXT('AMP Flag')
     A                                      COLHDG('AMP Flag')
     A            DDDBPFLAG      1  0       TEXT('Bulk Flag')
     A                                      COLHDG('Bulk Flag')
     A            DDDPREFLG      1  0       TEXT('Pre-Pick Flag')
     A                                      COLHDG('Pre-Pick' 'Flag')
     A*
     A*  Level 3 - Pallet Section Attributes
     A*
     A*
     A*  Level 4 - Stop/Drop Attributes
     A*
     A            DDDHSTSTP      5  0       TEXT('Host Stop')
     A                                      COLHDG('Host' 'Stop')
     A            DDDDROP        3          TEXT('Drop')
     A                                      COLHDG('Drop')
     A            DDDSTPDRP      9          TEXT('Stop/Drop Display')
     A                                      COLHDG('Stop/Drop' 'Display')
     A            DDDORD    R               REFFLD(OHORD)
     A            DDDMLTORD      1  0       TEXT('Multi-Order Flag')
     A                                      COLHDG('Multi-Order' 'Flag')
     A            DDDSTPSPLT     1  0       TEXT('Stop Split Flag')
     A                                      COLHDG('Stop Split' 'Flag')
     A*
     A*  Level 5/6 - Item/Tote Attributes
     A*
     A            DDDLBLTYP      1          TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            DDDUTYP        1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            DDDUCOD   R               TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A                                      REFFLD(UMCODE)
     A            DDDITMSPLT     1  0       TEXT('Item Split Flag')
     A                                      COLHDG('Item Split' 'Flag')
     A            DDDORGCMP R               TEXT('Orig Comp')
     A                                      COLHDG('Orig' 'Comp')
     A                                      REFFLD(TPCODE)
     A            DDDORGZON R               TEXT('Orig Zone')
     A                                      COLHDG('Orig' 'Zone')
     A                                      REFFLD(ZHZONE)
     A*
     A*  Add information
     A*
     A            DDDADDPGM R               REFFLD(ADDPGM)
     A            DDDADDTS  R               REFFLD(ADDTS PIRDICT)
     A            DDDADDUC  R               REFFLD(ADDUC PIRDICT)
     A            DDDADDUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('User')
     A                                      TEXT('User')
     A            DDDADDUSRJR               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            DDDADDJOB R               REFFLD(ADDJOB PIRDICT)
     A            DDDADDNBR R               REFFLD(ADDNBR PIRDICT)
     A            DDDSESSID     40          VARLEN
     A                                      TEXT('Session Id')
     A                                      COLHDG('Session Id')
     A*
     A*  JSON string
     A*
     A            DDDJSON     3072          VARLEN
     A                                      TEXT('JSON String')
     A                                      COLHDG('JSON String')
     A*
     A*  Key
     A*
     A          K DDDWHSE
     A          K DDDRTID
     A          K DDDSNAP
     A          K DDDID

