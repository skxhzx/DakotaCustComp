     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL7 (LF)
     A*  Description ... Label join file by transaction.
     A*  Date .......... October 12, 1994
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*    Ver 2.01  11/10/94  DAS
     A*      - POD file changed. Qty split into 3 fields.
     A*
211 AA*    Ver 2.02  02/29/96  DAS
211 AA*      - Took out join to PIRTRAN. Nobody uses it.
     A*
213 AA*    Ver 2.03   05/13/96  DAS
213 AA*      - Added new label field LBQAVL.
     A*
218 AA*    02/06/97  RH  2.18
218 AA*      - Changed from POH & POD files to POHDR & PODTL
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
211 DA*         R L7REC                     JFILE(LABEL PIRTRAN +
211 MA          R L7REC                     JFILE(LABEL +
     A                                            PIRITEM ORDH ORDD +
218 MA                                            UNMESR POHDR PODTL +
218 DA*                                           UNMESR POH POD +
     A                                            WAREHS WHDEPT)
     A*
211 DA*         J                           JOIN(LABEL PIRTRAN)
211 DA*                                     JFLD(LBTRN#  PTTRN#)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
218 DA*         J                           JOIN(LABEL   POH)
218 MA          J                           JOIN(LABEL   POHDR)
     A                                      JFLD(LBWHSE  PHWHSE)
     A                                      JFLD(LBPO    PHPO)
     A*
218 DA*         J                           JOIN(LABEL   POD)
218 MA          J                           JOIN(LABEL   PODTL)
     A                                      JFLD(LBWHSE  PDWHSE)
     A                                      JFLD(LBPO    PDPO)
     A                                      JFLD(LBSEQ   PDSEQ)
     A                                      JFLD(LBITEM  PDITEM)
     A*
     A          J                           JOIN(LABEL   WAREHS)
     A                                      JFLD(LBWHSE  WHCODE)
     A*
     A          J                           JOIN(LABEL   WHDEPT)
     A                                      JFLD(LBWHSE  WDWHSE)
     A                                      JFLD(LBWHDP  WDCODE)
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
213 AA            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
211 DA*
211 DA*  Fields from PIRTRAN file.
211 DA*
211 DA*           PTTYPE
211 DA*           PTCUBE
211 DA*           PTSWGT
211 DA*           PTPCS
211 DA*           PTSDTE
211 DA*           PTSTIM
211 DA*           PTEDTE
211 DA*           PTETIM
211 DA*           PTRTE
211 DA*           PTPO
     A*
     A*  Fields from PIRITEM file.
     A*
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
     A            ITUM3
     A            ITUMQ3
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
     A*
     A*  Fields from ORDH file.
     A*
     A            OHTYPE
     A            OHCUST
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
218 DA*  Fields from POH file.
218 MA*  Fields from POHDR file.
     A*
     A            PHVEN
     A            PHVNAM
     A            PHVAD1
     A            PHVAD2
     A            PHVCTY
     A            PHVST
     A            PHVZP1
     A            PHDATE
     A            PHTIME
     A            PHAMPM
     A*
218 DA*  Fields from POD file.
218 MA*  Fields from PODTL file.
     A*
     A            PDVITM
     A            PDSPCL
     A            PDQOR1
     A            PDQOR2
     A            PDQOR3
     A            PDQRC1
     A            PDQRC2
     A            PDQRC3
     A*
     A*  Fields from WAREHS file.
     A*
     A            WHDESC
     A*
     A*  Fields from WHDEPT file.
     A*
     A            WDDESC
     A*
     A*  Key fields
     A*
     A          K LBTRN#
     A          K LBITEM
     A          K LBSEQ
