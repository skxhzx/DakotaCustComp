     A*----------------------------------------------------------------
     A*   Copyright (C) 1993 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... POHDR (PF)
     A*  Description ... Purchase order header file
     A*  Creator ....... DAS
     A*
     A*  Status
     A*       0 =  Open P.O.
     A*       1 =  Partial receipt
     A*       2 =  Received in full
401 AA*       3 =  Ready for export
     A*
     A*  Revisions
     A*
     A*    Ver 2.00  10/11/94 DAS
     A*      - Changed PO# to alpha.
     A*
     A*    Ver 2.01  11/10/94 DAS
     A*      - Added upload date/time.
     A*
     A*    Ver 2.17  10/08/96 HNK
     A*      - STATUS FIELD ADDED.
     A*
401 AA*    Ver 4.01  07/11/97 HNK
     A*      - Meaning of status 3 changed. No need to compile.
     A*
414 AA*    Ver 4.14  07/30/00 KDE
     A*      - Definition of Time field changed.  No need to compile.
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R PHREC
     A*
     A            PHWHSE    R               REFFLD(WHCODE)
     A            PHPO      R               REFFLD(PONUM)
     A            PHVEN     R               REFFLD(VENNUM)
     A            PHVNAM        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            PHVAD1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            PHVAD2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            PHVCTY        20          TEXT('City')
     A                                      COLHDG('City')
     A            PHVST          2          TEXT('State')
     A                                      COLHDG('State')
     A            PHVZP1        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            PHDATE    R               TEXT('Delivery Date')
     A                                      COLHDG('Delivery' 'Date')
     A                                      REFFLD(DTEYMD)
414  A*  PHTIME will be changed to house military time versus
414  A*  12 hour time.
     A            PHTIME         4S 0       TEXT('Delivery Time')
     A                                      COLHDG('Delivery' 'Time')
414 AA*  AM/PM Will no longer be used.  PHTIME will now have
414 AA*  Military time.
     A            PHAMPM         1          TEXT('Delivery Am/Pm')
     A                                      COLHDG('Delivery' 'Am/Pm')
     A            PHUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            PHUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
     A            PHSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A          K PHWHSE
     A          K PHPO
