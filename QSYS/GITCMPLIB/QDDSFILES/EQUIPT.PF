     A*----------------------------------------------------------------***
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... EQUIPT (PF)
     A*  Description ... Equipment transaction table
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
417 AA*    12/19/02  DAS  4.17
     A*      - Created.
417bAA*   04/28/04  RH   4.17b
     A*     - Change ETCODE length from 6 to 8.
     A*
     A*----------------------------------------------------------------***
     A* Notes:
     A*
     A*   - An equipment code should only have one active record.
     A*
     A*   - An active record should only be tied to an 'Out' record.
     A*
     A*   - An 'Out' record is one in which only the 'Out' fields are
     A*     filled in. The 'In' fields should be empty.
     A*
     A*   - An unactive record that only has 'Out' fields filled in
     A*     indicates that no corresponding 'In' transaction was
     A*     recorded by the employee for the equipment.
     A*
     A*   - An unactive record that only has 'In' fields filled in
     A*     indicates that no corresponding 'Out' transaction was
     A*     recorded by the employee for the equipment.
     A*
     A*----------------------------------------------------------------***
     A* Field Notes:
     A*
     A*   ETACTV   '0' - Unactive history record.
     A*            '1' - Current active record.
     A*
     A*   ETOQFL   '0' - No questions were asked.
     A*            '1' - Questions were asked.
     A*
     A*   ETIQFL   '0' - No questions were asked.
     A*            '1' - Questions were asked.
     A*
     A*   ETOXFL   '0' - No negative responses were recorded.
     A*            '1' - At least on negative response was recorded.
     A*
     A*   ETIXFL   '0' - No negative responses were recorded.
     A*            '1' - At least on negative response was recorded.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/BTSDICT)
     A          R ETREC
     A*
     A            ETWHSE    R               REFFLD(WHCODE PIRDICT)
     A            ETTRN#    R               TEXT('Equip Tran #')
     A                                      COLHDG('Equip' 'Tran #')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ETCODE    R               REFFLD(EQCODE PIRDICT)
     A            ETEMP#         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            ETACTV         1          TEXT('Active Flag')
     A                                      COLHDG('Active' 'Flag')
     A*
     A            ETOTRN    R               TEXT('Out Tran #')
     A                                      COLHDG('Out' 'Tran #')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ETOQFL         1          TEXT('Out Question Flag')
     A                                      COLHDG('Out Question' 'Flag')
     A            ETOXFL         1          TEXT('Out Exception Flag')
     A                                      COLHDG('Out Exception' 'Flag')
     A            ETODTS          Z         TEXT('Out Date Stamp')
     A                                      COLHDG('Out' 'Date Stamp')
     A            ETOUSR        10A         TEXT('Out User')
     A                                      COLHDG('Out User')
     A            ETOJOB        10A         TEXT('Out Job')
     A                                      COLHDG('Out Job')
     A            ETONBR         6A         TEXT('Out Job Number')
     A                                      COLHDG('Out' 'Job Number')
     A*
     A            ETITRN    R               TEXT('In Tran #')
     A                                      COLHDG('In' 'Tran #')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ETIQFL         1          TEXT('In Question Flag')
     A                                      COLHDG('In Question' 'Flag')
     A            ETIXFL         1          TEXT('In Exception Flag')
     A                                      COLHDG('In Exception' 'Fla g')
     A            ETIDTS          Z         TEXT('In Date Stamp')
     A                                      COLHDG('In' 'Date Stamp')
     A            ETIUSR        10A         TEXT('In User')
     A                                      COLHDG('In User')
     A            ETIJOB        10A         TEXT('In Job')
     A                                      COLHDG('In Job')
     A            ETINBR         6A         TEXT('In Job Number')
     A                                      COLHDG('In' 'Job Number')
     A*
     A            ETMIN          7  2       TEXT('Minutes')
     A                                      COLHDG('Minutes')
