     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (360) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDHEXA2 (LF)
     A*  Description ... Order header(S) JOIN with Alpha Order#
     A*  Date .......... May 06, 2011
     A*  Creator ....... NLK
     A*
     A*  Revisions
     A*
     A*    05/06/21  NLK  7.50a
     A*      - Created.  -- RH240CA needs subfile sorted by Alpha Ord#
     A*
     A*----------------------------------------------------------------
     A          R HXJREC                    JFILE(ORDHEXA ORDH)
     A*
     A          J                           JOIN(ORDHEXA ORDH)
     A                                      JFLD(OHAWHSE  OHWHSE)
     A                                      JFLD(OHAORD   OHORD)
     A*
     A*  Fields from ORDHEXA file.
     A*
     A            OHAWHSE
     A            OHAORD
     A            OHAORDA
     A            OHACUSTA
     A*
     A*  Fields from ORDH file.
     A*
     A            OHWHSE
     A            OHORD
     A            OHORIG
     A            OHTYPE
     A            OHCUST
     A            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A            OHPBAT
     A            OHSTAT
     A*
     A*  Key fields
     A*
     A          K OHAWHSE
     A          K OHAORDA
     A          S OHTYPE                    CMP(EQ 'W')
     A            OHSTAT                    CMP(EQ 'P')
