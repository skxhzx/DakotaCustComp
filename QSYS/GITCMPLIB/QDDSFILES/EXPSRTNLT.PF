     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSRTNLT (PF)
     A*  Description ... Staging Export - Returns Licenses
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740  A*    02/07/20  RTR  7.40
     A*      - Created based on EXPSRTN.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    This file contains Host Lot information that corresponds to
     A*    the ORRTN file. There may be 1 or more records in this
     A*    file associated with an ORRTN record if more than one license
     A*    was returned.
     A*
     A*    The total of the quantities of the associated records
     A*    in this file should equal the quantities in the corresponding
     A*    ORRTN record.
     A*
     A*    Typically, the Host Lot field will contain either the Dakota
     A*    License or the GS1 Serial Number from the Dakota UCI, the
     A*    latter being used with Unique UCI tracked items.
     A*
     A*    The following fields would only be used when working with
     A*    a UCI tracked item:
     A*
     A*     ORLTUCI
     A*     ORLTUCISEQ
     A*     ORLTGTIN
     A*     ORLTDATE
     A*     ORLTDATECD
     A*     ORLTDATENM
     A*     ORLTLOT
     A*     ORLTSERIAL
     A*
     A*   ORLTLOT and ORLTSERIAL are defined as 30 to match other files.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ORLTREC
     A*
     A            ORLTWHSE  R               REFFLD(WHCODE)
     A            ORLTBATID     11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            ORLTSTATUS     1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            ORLTSTSTS       Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            ORLTSTSUC       Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            ORLTTEXT     200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            ORLTORD        7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            ORLTITEM  R               REFFLD(ITITEM)
     A                                      TEXT('Returned Item')
     A                                      COLHDG('Return' 'Item')
     A            ORLTQTY1R      5S 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            ORLTQTY2R      5S 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            ORLTQTY3R      5S 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
     A            ORLTCWTR       9S 2       TEXT('Catchweight Returned')
     A                                      COLHDG('Catchweight' 'Returned')
     A            ORLTRDTE  R               TEXT('Return Date (YYYYMMDD)')
     A                                      COLHDG('Return' 'Date')
     A                                      REFFLD(DTECMD)
     A            ORLTRTIM  R               TEXT('Return Time')
     A                                      COLHDG('Return' 'Time')
     A                                      REFFLD(TIMHMS)
     A            ORLTUSR       10A         TEXT('Returned By')
     A                                      COLHDG('Returned' 'By')
     A            ORLTTYPE       1A         TEXT('Return Type')
     A                                      COLHDG('Return' 'Type')
     A            ORLTTRN#       7S 0       TEXT('Original Tran#')
     A                                      COLHDG('Original' 'Tran#')
     A            ORLTCITM  R               TEXT('Corresponding Item')
     A                                      COLHDG('Corresponding' 'Item')
     A                                      REFFLD(ITITEM)
     A            ORLTRCOD  R               REFFLD(RNCODE)
     A            ORLTDCOD  R               REFFLD(RDCODE)
     A            ORLTIINV       1A         TEXT('Impact Inventory')
     A                                      COLHDG('Impact' 'Inv.')
     A            ORLTOLB#       7S 0       TEXT('Original Label')
     A                                      COLHDG('Orig.' 'Label#' )
     A            ORLTINV#      12A         TEXT('Orig. Invoice#')
     A                                      COLHDG('Orig.' 'Invoice#')
     A            ORLTRMEM      30A         TEXT('Memo')
     A                                      COLHDG('Memo')
     A            ORLTHSTLOT    30          TEXT('Host Lot Code')
     A                                      COLHDG('Host Lot Code')
     A            ORLTLITEM R               REFFLD(ITITEM)
     A                                      TEXT('License Item)')
     A                                      COLHDG('License' 'Item')
     A            ORLTQTY1       5S 0       TEXT('Normal Qty')
     A                                      COLHDG('Normal' 'Qty')
     A            ORLTQTY2       5S 0       TEXT('Brkdn1 Qty')
     A                                      COLHDG('Brkdn1' 'Qty')
     A            ORLTQTY3       5S 0       TEXT('Brkdn2 Qty')
     A                                      COLHDG('Brkdn2' 'Qty')
     A            ORLTLCNS      15          TEXT('License')
     A                                      COLHDG('License')
     A            ORLTOLCNS     15          TEXT('Original License')
     A                                      COLHDG('Original License')
     A            ORLTPO         9          TEXT('PO')
     A                                      COLHDG('PO')
     A            ORLTPOSEQ      5S 0       TEXT('PO Seq')
     A                                      COLHDG('PO Seq')
     A            ORLTUCI      100          TEXT('UCI')
     A                                      COLHDG('UCI')
     A            ORLTUCISEQ     5S 0       TEXT('UCI Sequence Number')
     A                                      COLHDG('UCI Sequence' 'Number')
     A            ORLTGTIN      14          TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            ORLTDATE       6S 0       TEXT('Date YYMMDD')
     A                                      COLHDG('Date YYMMDD')
     A            ORLTDATECD     2          TEXT('Date AI Code')
     A                                      COLHDG('Date' 'AI Code')
     A            ORLTDATENM     4          TEXT('Date AI Name')
     A                                      COLHDG('Date' 'AI Name')
     A            ORLTLOT       30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            ORLTSERIAL    30          TEXT('Serial Number')
     A                                      COLHDG('Serial Number')
     A            ORLTWGT        7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            ORLTTRAX      10          TEXT('Trax Number')
     A                                      COLHDG('Trax' 'Number')
     A            ORLTTRN        7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A*
     A*  Add stamps
     A*
     A            ORLTADDTS       Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            ORLTADDUC       Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K ORLTWHSE
     A          K ORLTBATID
     A          K ORLTORD
     A          K ORLTITEM
     A          K ORLTRDTE
     A          K ORLTRTIM
