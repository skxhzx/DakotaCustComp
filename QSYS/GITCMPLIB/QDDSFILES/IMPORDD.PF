     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPORDD (PF)
     A*  Description ... Customer order import file - Detail
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    Rev 2.00  07/29/94  DAS
     A*      - Added warehouse, corresponding warehouse and order#
     A*        and original item #.
     A*      - Split quantity into three parts.
     A*
     A*    Rev 2.01  08/17/94  DAS
     A*      - Changed numbers to packed.
     A*
     A*    Rev 2.02  10/08/94  DAS
     A*      - Added special status code field (ODSPCL).
     A*
     A*    Rev 2.03  10/21/94  HNK
     A*      - Added sequence number field (OIDSEQ).
     A*
     A*    Rev 4.04  08/27/97  HNK
     A*      - Added PO number field (OIDPO).
     A*
415 AA*    Rev 4.15  07/11/01  DAS
     A*      - Removed UNIQUE keyword.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    - If the item to be picked (ODDITM) is a substitue, the
     A*      original item should be identified in field ODDOIT.
     A*
     A*    - The corresponding warehouse and order # are used for
     A*      transfers.
     A*
     A*----------------------------------------------------------------
415 DA**                                    UNIQUE
     A          R OIDREC
     A*
     A            OIDWHS         3P 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OIDORD         7P 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OIDSEQ         5P 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            OIDITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            OIDQ1          5P 0       TEXT('Normal Qty Ordered')
     A                                      COLHDG('Normal Qty' 'Ordered')
     A            OIDQ2          5P 0       TEXT('Breakdown 1 Qty Ordered')
     A                                      COLHDG('Breakdown 1' 'Qty Ordered')
     A            OIDQ3          5P 0       TEXT('Breakdown 2 Qty Ordered')
     A                                      COLHDG('Breakdown 2' 'Qty Ordered')
     A            OIDWH2         3P 0       TEXT('Corresponding Warehouse')
     A                                      COLHDG('Corresponding' 'Warehouse')
     A            OIDOR2         7P 0       TEXT('Corresponding Order #')
     A                                      COLHDG('Corresponding' 'Order #')
     A            OIDOIT        15          TEXT('Original Item')
     A                                      COLHDG('Original' 'Item')
     A            OIDSPC         2          TEXT('Special Status Code')
     A                                      COLHDG('Special' 'Status Code')
404 AA            OIDPO          9          TEXT('P.O. Number')
404 AA                                      COLHDG('P.O.' 'Number')
     A*
     A          K OIDWHS
     A          K OIDORD
     A          K OIDSEQ
     A          K OIDITM
