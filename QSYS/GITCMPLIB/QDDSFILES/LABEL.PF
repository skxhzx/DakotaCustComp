730aAA*********************************
730AAA**** DO NOT USE IN PROGRAMS  ****
730AAA**** USE LABELW INSTEAD      ****
730AAA*********************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL (PF)
     A*  Description ... Label file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    10/21/94 HNK
     A*      - Sequence number field LBSEQ added.
     A*
213 AA*    Ver 2.01  05/13/96  DAS
213 AA*      - Added field LBQAVL for 'From' slot.
213 AA*      - Added fields LBGRP1, LBGRP2, LGRDTE
413 AA*    Ver 4.13  04/06/00  HNK
413 AA*      - Added new LBGRP1 & LBGRP2 codes.
     A*
417 AA*    Ver N/A   08/06/03  DAS
     A*      - Added note about LBVRFY usage.
     A*      - No recompile necessary.
     A*
417aAA*    Ver N/A   08/08/04  DAS
     A*      - Added note about LBPO usage.
     A*      - No recompile necessary.
     A*
730aAA*    10/04/19  DAS  7.30a
     A*      - Removed UNIQUE. Moved to LF LABELW that has Whse.
     A*      - This file should no longer be used in programs. Instead
     A*        use LABELW.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    This file contains one record for each label to be printed.
     A*    This file replaces the ORDPICK file.
     A*
     A*    Priorities are 0 (normal) thru 9 (high) and will be stored
     A*    as NEGATIVE numbers.  This allows us to use a normal
     A*    ascending index to read the file in the correct sequence
     A*    (ie. -9 comes before 0 and will be read first).
     A*
     A*    A special field was added (LBQRYF) to allow OPNQRYF to
     A*    create stop summary records that contain fixed and
     A*    PIR totals. Without this field OPNQRYF would only be
     A*    able to create seperate records for each type. This field
     A*    should contain a 0 for fixed slots and a 1 for PIR slots.
     A*
414aAA*    The field LBASL2 is being used to hold the value 'OUT' for
414aAA*    label records where LBTYPE = 'O'. The file has NOT been re-
414aAA*    compiled.  The field LBASL2 is being reused for order records.
     A*
414aAA*    The field LBHND2 is being used to hold the pallet section in-
414aAA*    dicator value left justified in the field.  The pallet section
414aAA*    indicator is a 1 character field.  This file has NOT been re-
414aAA*    compiled.  The field LBHND2 is being reused for order records.
     A*
417aAA*    OR620 is moving *SPLIT into the PO field when a child record
417aAA*    is split onto multiple truck pallets.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes (LBSTAT)
     A*
     A*       'A'  -  Allocated
     A*       'B'  -  Built
     A*       'P'  -  Printed
     A*       'C'  -  Closed
     A*
     A*    Group type 1 (LBGRP1)
     A*
     A*       ' '  -  Misc.
     A*       'L'  -  Letdown/Replenishment
     A*       'P'  -  Putaway/Receiving
     A*       'R'  -  Return
     A*       'S'  -  Selection
413 AA*       '1'  -  Trans. item, no change in item #
413 AA*       '2'  -  Trans. item, change item #
413 AA*       '3'  -  Change item # in slot.
     A*
     A*    Group type 2  (LBGRP2)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
413 AA*       'A'  -  LBDISP has "from" slot.
413 AA*       'B'  -  LBDISP has "to" slot.
     A*
     A*    Type codes  (LBTYPE)
     A*
413 AA*       'A'  -  Adjustment
     A*       'B'  -  Bulk pick
     A*       'O'  -  Order pick
     A*       'R'  -  Replenishment
     A*       'P'  -  Put away
     A*       'M'  -  Merge
414 AA*       'T'  -  Transfer
     A*
417 AA*    Verify codes (LBVRFY)
     A*
     A*       '0'  -  Nothing picked during Selection (OR630).
     A*       'U'  -  Record was 'Undone' by program RH134.
     A*       'T'  -  Label closed via voice (PT132PL).
     A*
     A*----------------------------------------------------------------
730aDA**                                    UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R LBREC
     A*
     A*  General information.
     A*
     A            LBWHSE    R               REFFLD(WHCODE)
     A            LBWHDP    R               REFFLD(WDCODE)
     A            LBSEQ     R               REFFLD(SEQ#)
     A            LBITEM    R               REFFLD(ITITEM)
     A            LBPBAT    R               REFFLD(BATCH#)
     A*
     A*  "From" slot.
     A*
     A            LBAISL    R               REFFLD(ASCODE)
     A            LBLOC     R               REFFLD(SLLOC )
     A            LBRLVL    R               REFFLD(SLRLVL)
     A            LBHAND    R               REFFLD(HSCODE)
     A            LBPSEQ         5P 0       TEXT('Picking Sequence')
     A                                      COLHDG('Picking' 'Sequence')
     A            LBSTYP    R               REFFLD(SLSTYP)
     A            LBQRYF         1P 0       TEXT('Query Factor')
     A                                      COLHDG('Query' 'Factor')
     A            LBDISP        12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
213 AA            LBQAVL         5P 0       TEXT('Quantity Available')
213 AA                                      COLHDG('Quantity' 'Available')
     A*
     A*  "To" slot (for transfer).
     A*
     A            LBWHS2    R               TEXT('Transfer Warehouse')
     A                                      COLHDG('Transfer' 'Warehouse')
     A                                      REFFLD(WHCODE)
     A            LBWHD2    R               TEXT('Transfer Department')
     A                                      COLHDG('Transfer' 'Department')
     A                                      REFFLD(WDCODE)
     A            LBASL2    R               TEXT('Transfer Aisle')
     A                                      COLHDG('Transfer' 'Aisle')
     A                                      REFFLD(ASCODE)
     A            LBLOC2    R               TEXT('Transfer Location')
     A                                      COLHDG('Transfer' 'Location')
     A                                      REFFLD(SLLOC )
     A            LBLVL2    R               TEXT('Transfer Level')
     A                                      COLHDG('Transfer' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LBHND2    R               TEXT('Transfer Hand')
     A                                      COLHDG('Transfer' 'Hand')
     A                                      REFFLD(HSCODE)
     A            LBDSP2        12A         TEXT('Transfer Display')
     A                                      COLHDG('Transfer' 'Display')
     A*
     A*  Order information.
     A*
     A            LBRTE     R               REFFLD(ROUTE)
     A            LBSTOP    R               REFFLD(STOP)
     A            LBORD     R               REFFLD(OHORD)
     A            LBSPCL    R               TEXT('Special Status Code')
     A                                      COLHDG('Special' 'Status Code')
     A                                      REFFLD(SSCODE)
     A*
     A*  Put away information.
     A*
     A            LBPO      R               REFFLD(PONUM)
     A*
     A*  Item information.
     A*
     A            LBUTYP         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            LBUCOD    R               TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A                                      REFFLD(UMCODE)
     A            LBUCUB         7P 3       TEXT('Cube/Unit')
     A                                      COLHDG('Cube/Unit')
     A            LBCUBE         9P 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            LBUWGT         7P 2       TEXT('Shipping Weight')
     A                                      COLHDG('Shipping' 'Weight')
     A            LBSWGT         9P 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            LBQALC         5P 0       TEXT('Quantity Allocated')
     A                                      COLHDG('Quantity' 'Allocated')
     A            LBQRMN         5P 0       TEXT('Quantity Remaining')
     A                                      COLHDG('Quantity' 'Remaining')
     A            LBQPCK         5P 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            LBTIE          4S 0       TEXT('Tie')
     A                                      COLHDG('Tie')
     A            LBHIGH         2S 0       TEXT('High')
     A                                      COLHDG('High')
     A*
     A*  Label information.
     A*
213 AA            LBGRP1         1A         TEXT('Group 1')
213 AA                                      COLHDG('Group 1')
213 AA            LBGRP2         1A         TEXT('Group 2')
213 AA                                      COLHDG('Group 2')
     A            LBTYPE         1A         TEXT('Type')
     A                                      COLHDG('Type')
     A            LBLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBPAL#    R               TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBCTR#    R               TEXT('Corresponding Tran Number')
     A                                      COLHDG('Corresp. Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            LBSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A            LBPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            LBSDTE    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            LBSTIM    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
213 AA            LBRDTE    R               TEXT('Report Date')
213 AA                                      COLHDG('Report' 'Date')
213 AA                                      REFFLD(DTECMD)
     A            LBVRFY         1A         TEXT('Verify the slot')
     A                                      COLHDG('Verify' 'the slot')
     A*
     A          K LBLBL#
