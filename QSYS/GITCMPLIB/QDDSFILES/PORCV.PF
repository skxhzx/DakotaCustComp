     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PORCV (PF)
     A*  Description ... Purchase receiving detail file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 4.14  09/17/00  DAS
     A*      - Created.
     A*
     A*    Ver 4.14  10/12/00  DAS
     A*      - Added field PORDEV.
     A*
415 AA*    Ver 4.15  01/24/01  DAS
     A*      - Added fields POREDT and PORETI.
416 AI*    05/23/02  RH   4.16
     I*      - Change length of PORLIC to 15, alphanumeric
     A*
     A*----------------------------------------------------------------***
     A* Action codes:
     A*
     A*   R  -  Received
     A*   D  -  Deleted
     A*   X  -  Checked
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A          R PORREC
     A*
     A            PORWHS    R               REFFLD(WHCODE)
     A            PORPO     R               REFFLD(PONUM)
     A            PORSEQ    R               REFFLD(SEQ#)
     A            PORDTE    R               TEXT('Receive Date')
     A                                      COLHDG('Receive' 'Date')
     A                                      REFFLD(DTECMD)
     A            PORTIM    R               TEXT('Receive Time')
     A                                      COLHDG('Receive' 'Time')
     A                                      REFFLD(TIMHMS)
     A            PORUSR        10          TEXT('AS/400 User Id')
     A                                      COLHDG('AS/400' 'User Id')
     A            PORDEV        10          TEXT('Device Id')
     A                                      COLHDG('Device Id')
     A            POREMP         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            PORTRN    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
416 DA*           PORLIC         7S         TEXT('License #')
416 MA            PORLIC        15A         TEXT('License #')
     A                                      COLHDG('License #')
     A            PORLBL    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            PORACT         1          TEXT('Action')
     A                                      COLHDG('Action')
415 AA            POREDT    R               TEXT('Export Date')
415 AA                                      COLHDG('Export' 'Date')
415 AA                                      REFFLD(DTECMD)
415 AA            PORETI    R               TEXT('Export Time')
415 AA                                      COLHDG('Export' 'Time')
415 AA                                      REFFLD(TIMHMS)
     A*
     A          K PORWHS
     A          K PORPO
     A          K PORSEQ
415 AA          K POREDT
415 AA          K PORETI
     A          K PORDTE
     A          K PORTIM
