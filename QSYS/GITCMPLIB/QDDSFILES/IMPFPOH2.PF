     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPFPOH2 (PF)
     A*  Description ... FTP Purchase order header import file.
     A*  Creator ....... KDE
     A*
     A*  This file should be identical to IMPFPOH except that
     A*  fields PIHDTE, PIHTIM, and PIHTYP have been added.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFPOH.FILE')
     A*
     A*  Revisions
     A*
     A*    Rev 4.14  07/29/00  KDE
     A*      - Created
     A*
414aAA*    Rev 4.14a 11/02/00  DAS
     A*      - Removed UNIQUE keyword.
     A*
414bAA*    Rev 4.14b 11/03/00  DAS
     A*      - Added PIHCDT/PIHCTM fields. These fields have been added
     A*        for clients who write a record to a work file every time
     A*        a P.O. changes.
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Type (PIHTYP)
     A*
     A*      'A'  -  Add       Adds P.O. header.
     A*                        If P.O. exists, treat as type 'R'.
     A*
     A*      'C'  -  Change    Changes P.O. header.
     A*                        If P.O. does not exist, treat as 'A'.
     A*
     A*      'D'  -  Delete    Deletes P.O. header and detail.
     A*                        If any quantity has been received for
     A*                          the P.O. (Based on status code), the
     A*                          P.O. will NOT be deleted.
     A*
     A*      'R'  -  Replace   Replaces existing P.O.
     A*                        If P.O. does not exist, treat as 'A'.
     A*                        If P.O. does exist, the header and
     A*                          detail are deleted. Then, P.O. is
     A*                          added.
     A*                        If any quantity has been received for
     A*                          the P.O. (Based on status code), the
     A*                          P.O. will be ignored and NO changes
     A*                          will take place.
     A*
     A*----------------------------------------------------------------
414aDA**                                    UNIQUE
     A          R PIHREC
     A*
     A            PIHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PIHPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
414bAA            PIHCDT         8S 0       TEXT('Create/Chg Date')
414bAA                                      COLHDG('Create/Chg' 'Date')
414bAA            PIHCTM         6S 0       TEXT('Create/Chg Time')
414bAA                                      COLHDG('Create/Chg' 'Time')
     A            PIHVEN        11S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
     A            PIHVNA        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            PIHVA1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            PIHVA2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            PIHVCT        20          TEXT('City')
     A                                      COLHDG('City')
     A            PIHVST         2          TEXT('State')
     A                                      COLHDG('State')
     A            PIHVZP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            PIHDTE         8S 0       TEXT('Delivery Date')
     A                                      COLHDG('Delivery' 'Date')
     A            PIHTIM         4S 0       TEXT('Delivery Time')
     A                                      COLHDG('Delivery' 'Time')
     A            PIHTYP         1          TEXT('Type')
     A                                      COLHDG('Type')
     A*
     A          K PIHWHS
     A          K PIHPO
     A          K PIHCDT
     A          K PIHCTM
