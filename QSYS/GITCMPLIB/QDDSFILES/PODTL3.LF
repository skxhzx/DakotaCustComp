     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PODTL3  (LF)
     A*  Description ... P.O. Detail joing file
     A*  Date .......... September 17, 2000
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    09/17/00  DAS  4.14
     A*      - Created
     A*
     A*    Ver 4.14  10/24/00  DAS
     A*      - Added field PORDEV.
     A*
415 AA*    Ver 4.15  01/29/01  DAS
     A*      - Added new fields from PORCV file: POREDT and PORETI.
     A*      - Added select based on new PORCV field POREDT. We only
     A*        want to include the records that do not have an export
     A*        date.
416 AI*    05/23/02  RH   4.16
     I*      - Change length of PORLIC to 15, alphanumeric
     A*
     A*----------------------------------------------------------------
     A* Notes:
     A*
     A*   - This join file does NOT use the JDFTVAL keyword because we
     A*     do not want to include and detail records that do not have
     A*     a corresponding record in the PORCV file. Therefore, it is
     A*     easy to verify if a detail record has been touched by the
     A*     receiver - If a record exists in this file then it has
     A*     been touched, otherwise it has not been touched.
     A*
     A*----------------------------------------------------------------
     A          R PDJREC                    JFILE(PODTL PORCV)
     A*
     A          J                           JOIN(PODTL PORCV)
     A                                      JFLD(PDWHSE  PORWHS)
     A                                      JFLD(PDPO    PORPO)
     A                                      JFLD(PDSEQ   PORSEQ)
     A*
     A*  Fields from PODTL file.
     A*
     A            PDWHSE
     A            PDPO
     A            PDSEQ
     A            PDITEM
     A            PDVITM
     A            PDSPCL
     A            PDQOR1
     A            PDQOR2
     A            PDQOR3
     A            PDQRC1
     A            PDQRC2
     A            PDQRC3
     A            PDDWGT
     A*
     A*  Fields from PORCV file.
     A*
     A            PORWHS
     A            PORPO
     A            PORSEQ
     A            PORDTE
     A            PORTIM
     A            PORUSR
     A            PORDEV
     A            POREMP
     A            PORTRN
     A            PORLIC
     A            PORLBL
     A            PORACT
415 AA            POREDT
415 AA            PORETI
     A*
     A*  Key fields
     A*
     A          K PDWHSE
     A          K PDPO
     A          K PDSEQ
415 AA          S POREDT                    COMP(EQ 0)
