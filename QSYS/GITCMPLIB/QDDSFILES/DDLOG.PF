      *----------------------------------------------------------------
      *   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
      *   (630) 562-0375
      *----------------------------------------------------------------
      *
      *  File name ..... GLORTELOG (PF)
      *  Description ... Global Route Log file.
      *  Creator ....... JCJ
      *  Date .......... March 21, 2013
      *
      *  Revisions:
      *
650aA *    03/21/13  JCJ  6.50a
      *      - Created.
      *
650bA *    03/28/13  DAS  6.50b
      *      - Revised the fields.
      *
      *----------------------------------------------------------------

      *----------------------------------------------------------------
      * DDLRECTYP - Record Type
      *
      *   1 - Move
      *   2 - Error
      *   3 - Warning
      *   4 - CHKIUSE
      *   5 - Info
      *
      *----------------------------------------------------------------

      *----------------------------------------------------------------
      * DDLMOVTYP - Move Type
      *
      *   1 - Pallet
      *   2 - Stop
      *   3 - Label
      *   4 - Pieces
      *
      *----------------------------------------------------------------

     A                                      REF(*LIBL/PIRDICT)
     A          R DDLREC
     A*
     A            DDLWHSE   R               REFFLD(WHCODE)
     A            DDLBRNCH       2          TEXT('Branch Id')
     A                                      COLHDG('Branch' 'Id')
     A            DDLRTID   R               REFFLD(ROUTE)
     A            DDLGRPID       7  0       TEXT('Group ID')
     A                                      COLHDG('Group' 'Id')
     A            DDLRECTYP      1          TEXT('Rec Type')
     A                                      COLHDG('Rec' 'Type')
     A            DDLMOVTYP      1          TEXT('Move Type')
     A                                      COLHDG('Move' 'Type')
     A            DDLDDUSED      1          TEXT('DD Used')
     A                                      COLHDG('DD' 'Used')
     A            DDLFPLID  R               TEXT('From Pallet Id')
     A                                      COLHDG('From' 'Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            DDLTPLID  R               TEXT('To Pallet Id')
     A                                      COLHDG('To' 'Pallet' 'Id')
     A                                      REFFLD(TRAN#)
     A            DDLSTOP   R               TEXT('Stop')
     A                                      COLHDG('Stop')
     A                                      REFFLD(STOP)
     A            DDLLBL#   R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            DDLNEWLBL#R               TEXT('New Label Number')
     A                                      COLHDG('New Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            DDLPCS         5P 0       TEXT('Pieces')
     A                                      COLHDG('Pieces')
     A            DDLRTN        10          TEXT('Return code')
     A                                      COLHDG('Return' 'Code')
     A            DDLMSG        80          TEXT('Return message')
     A                                      COLHDG('Return' 'Message')
     A            DDLPGM        10          TEXT('Program')
     A                                      COLHDG('Program')
     A            DDLADDTS  R               TEXT('Add Timestamp')
     A                                      COLHDG('Add' 'Timestamp')
     A                                      REFFLD(ADDTS)
     A            DDLADDUC  R               TEXT('Add UC Timestamp')
     A                                      COLHDG('Add' 'UC' 'Timestamp')
     A                                      REFFLD(ADDUC)
     A            DDLCURUSR     10          TEXT('Current User')
     A                                      COLHDG('Current' 'User')
     A            DDLJOBUSR     10          TEXT('Job User')
     A                                      COLHDG('Job' 'User')
     A            DDLJOB        10          TEXT('Job Name')
     A                                      COLHDG('Job' 'Name')
     A            DDLJOBNBR      6          TEXT('Job Number')
     A                                      COLHDG('Job' 'Number')
