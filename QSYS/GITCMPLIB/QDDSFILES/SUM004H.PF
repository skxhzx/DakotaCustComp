     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SUM004H (PF)
     A*  Description ... Summary 004 header for recall summary file
     A*  Creator ....... RBD
     A*
     A*  Revisions:
     A*
700 AA*    09/28/15  RBD  7.00
     A*      - Created.
     A*
740aAA*    02/19/20  RBD  7.40a
     A*      - Comment only: PO field SMHPO is being replaced by
     A*        var length field SUM004HEXA/SHAPO which will allow for
     A*        multiple PO's to be selected.
     A*
     A*----------------------------------------------------------------
     A*  Codes
     A*
     A*    Status codes (SMHSTATUS)
     A*
     A*       'C'  -  Complete
     A*       'P'  -  Processing
     A*
     A*
     A*    Production Flag (SMHPFLAG)
     A*
     A*       'Y'  -  Production involved
     A*       'N'  -  Production not involved
     A*       'U'  -  Undetermined
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R SMHREC
     A*
     A            SMHWHSE   R               REFFLD(WHCODE PIRDICT)
     A            SMHRPTID       7S 0       TEXT('Report Id')
     A                                      COLHDG('Report' 'Id')
     A            SMHDESC       40          TEXT('Report Description')
     A                                      COLHDG('Report' 'Description')
     A            SMHSTATUS      1A         TEXT('Status')
     A            SMHPFLAG       1A         TEXT('Production Flag')
     A                                      COLHDG('Production' 'Flag')
     A            SMHITEM   R               REFFLD(ITITEM)
     A            SMHLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            SMHLOT        30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            SMHPO     R               REFFLD(PONUM)
     A            SMHFRMFGDT     8S 0       TEXT('From Mfg Date')
     A                                      COLHDG('From Mfg' 'Date')
     A            SMHTOMFGDT     8S 0       TEXT('To Mfg Date')
     A                                      COLHDG('To Mfg' 'Date')
     A            SMHFRMFGEX     8S 0       TEXT('From Mfg Exp Date')
     A                                      COLHDG('From Mfg' 'Exp Date')
     A            SMHTOMFGEX     8S 0       TEXT('To Mfg Exp Date')
     A                                      COLHDG('To Mfg' 'Exp Date')
     A            SMHSERIAL     30          TEXT('Serial No')
     A                                      COLHDG('Serial No')
     A            SMHDYSBEFL     3S 0       TEXT('Days Before Lic in Pick Slot')
     A                                      COLHDG('Days Before' 'Lic in P/S')
     A            SMHDYSAFTL     3S 0       TEXT('Days After Lic in Pick Slot')
     A                                      COLHDG('Days After' 'Lic in P/S')
     A            SMHDYSBEFI     3S 0       TEXT('Days Before Item in Prod')
     A                                      COLHDG('Days Before' 'Item in Prod')
     A            SMHDYSAFTI     3S 0       TEXT('Days After Item in Prod')
     A                                      COLHDG('Days After' 'Item in Prod')
     A*
     A* Timestamp / User tracking information.
     A*
     A            SMHADDTS  R               REFFLD(ADDTS)
     A            SMHADDUC  R               REFFLD(ADDUC)
     A            SMHADDPGM R               REFFLD(ADDPGM)
     A            SMHADDUSR R               REFFLD(ADDUSR)
     A            SMHADDJOB R               REFFLD(ADDJOB)
     A            SMHADDNBR R               REFFLD(ADDNBR)
     A*
     A          K SMHWHSE
     A          K SMHRPTID
