     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PRPLFILH  (PF)
     A*  Description ... Priority replenishments History
     A*  Creator ....... KDE
     A*
     A*  Revisions:
     A*
     A*    Create 7.40  09/24/20  KDE
     A*      - Copy of PRPLFIL with date stamp for when added to history
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Select flag (PRFLAG)
     A*      'A'  -  Replenishment in process
     A*      'D'  -  Replenishment has been deleted.
     A*      'S'  -  Replenishment has been sequenced
     A*
     A*    Item type (PRTYPE)
     A*      ' '  -  Normal
     A*      'B'  -  Broken case
     A*
     A*    Replenishment type (PRRPLT)
     A*      ' '  -  Normal replenishment
     A*      'E'  -  Eaches replenishment
     A*      'P'  -  Pushback replenishment
     A*
     A*****************************************************************
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R PRREC
     A            PRWHSE    R               REFFLD(SLWHSE SLOT)
     A            PRWHDP    R               REFFLD(SLWHDP SLOT)
     A            PRFAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('From' 'Aisle')
     A            PRFLOC    R               REFFLD(SLLOC  SLOT)
     A                                      COLHDG('From' 'Location')
     A            PRFLVL    R               REFFLD(SLRLVL SLOT)
     A                                      COLHDG('From' 'Level')
     A            PRFHND    R               REFFLD(SLHAND SLOT)
     A                                      COLHDG('From' 'Handstack')
     A            PRFPOS         3P 0       TEXT('From Slot Position')
     A                                      COLHDG('From Slot' 'Position')
     A            PRTAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('To' 'Aisle')
     A            PRTLOC    R               REFFLD(SLLOC  SLOT)
     A                                      COLHDG('To' 'Location')
     A            PRTLVL    R               REFFLD(SLRLVL SLOT)
     A                                      COLHDG('To' 'Level')
     A            PRTHND    R               REFFLD(SLHAND SLOT)
     A                                      COLHDG('To' 'Handstack')
     A            PRTPOS         3P 0       TEXT('To Slot Position')
     A                                      COLHDG('To Slot' 'Position')
     A            PRFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            PRTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            PRITEM    R               REFFLD(SLITEM SLOT)
     A            PRSEQ#         5P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq' 'Number')
     A            PRQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            PRSDEF    R               REFFLD(SLSDEF SLOT)
     A                                      COLHDG('Replenishment' 'Slot Type')
     A            PRFLAG         1A         TEXT('Select Flag')
     A                                      COLHDG('Select' 'Flag')
     A            PRPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            PRFREE         1A         TEXT('Free slot Y,N')
     A                                      COLHDG('Free Slot' 'Y,N')
     A            PRSTAT    R               REFFLD(SLSTAT SLOT)
     A                                      COLHDG('Status of' 'Home Slot')
     A            PRTYPE         1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            PRRPLT         1A         TEXT('Replenishment Type')
     A                                      COLHDG('Replenishment' 'Type')
     A            PRADTE    R               TEXT('Add Date')
     A                                      COLHDG('Add' 'Date')
     A                                      REFFLD(DTECMD)
     A            PRATIM    R               TEXT('Add Time')
     A                                      COLHDG('Add' 'Time')
     A                                      REFFLD(TIMHMS)
     A            PRAQS          7P 2       TEXT('Average Quantity Sold')
     A                                      COLHDG('Avg Qty' 'Sold')
     A            PRADDTS         Z         TEXT('Timestamp Added')
     A                                      COLHDG('Timestamp' 'Added')
