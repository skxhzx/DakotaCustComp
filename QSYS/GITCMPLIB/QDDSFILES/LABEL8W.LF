     A*----------------------------------------------------------------
     A*   Copyright (C) 2019 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL8W (LF)
     A*  Description ... Label join file for open tran check. w/Whse
     A*  Date .......... October 04, 2019
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*    All programs using LABEL8 should be changed to use this file.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*    This logical file was created in order to see if all the
     A*    labels for a transaction have been completed. This file
     A*    OMITS records with a status of 'C'. Therefore, if a
     A*    record exists in this file then the transaction has not
     A*    been totally closed.
     A*
     A*    This layout uses a unique record name and field names so
     A*    it can be used in a program that uses the label file.
     A*
     A*    *** If fields are removed from the physical file LABEL they
     A*    must also be removed from this file. If a new field is added
     A*    to the file and it is needed by a program using this file,
     A*    you must also add the field to this file.
     A*
     A*    In order to use the JDFTVAL keyword the DYNSLT keywork
     A*    would also have to be used. Because DYNSLT might slow down
     A*    I/O we have decided to leave the JDFTVAL keyword out. This
     A*    means that unclosed labels without a corresponding
     A*    transaction record would be ignored.
     A*
     A*----------------------------------------------------------------
     A          R L8REC                     JFILE(LABEL PIRTRAN)
     A*
     A          J                           JOIN(LABEL   PIRTRAN)
     A                                      JFLD(LBWHSE  PTWHSE)
     A                                      JFLD(LBTRN#  PTTRN#)
     A*
     A*  Fields from LABEL file.
     A*
     A            L8WHSE                    RENAME(LBWHSE)
     A            L8WHDP                    RENAME(LBWHDP)
     A            L8SEQ                     RENAME(LBSEQ)
     A            L8ITEM                    RENAME(LBITEM)
     A            L8PBAT                    RENAME(LBPBAT)
     A            L8AISL                    RENAME(LBAISL)
     A            L8LOC                     RENAME(LBLOC)
     A            L8RLVL                    RENAME(LBRLVL)
     A            L8HAND                    RENAME(LBHAND)
     A            L8PSEQ                    RENAME(LBPSEQ)
     A            L8STYP                    RENAME(LBSTYP)
     A            L8RTE                     RENAME(LBRTE)
     A            L8STOP                    RENAME(LBSTOP)
     A            L8ORD                     RENAME(LBORD)
     A            L8PO                      RENAME(LBPO)
     A            L8UTYP                    RENAME(LBUTYP)
     A            L8UCOD                    RENAME(LBUCOD)
     A            L8UCUB                    RENAME(LBUCUB)
     A            L8CUBE                    RENAME(LBCUBE)
     A            L8UWGT                    RENAME(LBUWGT)
     A            L8SWGT                    RENAME(LBSWGT)
     A            L8QALC                    RENAME(LBQALC)
     A            L8QRMN                    RENAME(LBQRMN)
     A            L8QPCK                    RENAME(LBQPCK)
     A            L8TYPE                    RENAME(LBTYPE)
     A            L8LBL#                    RENAME(LBLBL#)
     A            L8PAL#                    RENAME(LBPAL#)
     A            L8TRN#                    RENAME(LBTRN#)
     A            L8CTR#                    RENAME(LBCTR#)
     A            L8STAT                    RENAME(LBSTAT)
     A            L8PRTY                    RENAME(LBPRTY)
     A            L8SDTE                    RENAME(LBSDTE)
     A            L8STIM                    RENAME(LBSTIM)
     A*
     A*  Fields from PIRTRAN file.
     A*
101eDA*           P8TYPE                    RENAME(PTTYPE)
101eMA            P8TASK                    RENAME(PTTASK)
     A            P8CUBE                    RENAME(PTCUBE)
     A            P8SWGT                    RENAME(PTSWGT)
     A            P8PCS                     RENAME(PTPCS)
     A            P8SDTE                    RENAME(PTSDTE)
     A            P8STIM                    RENAME(PTSTIM)
     A            P8EDTE                    RENAME(PTEDTE)
     A            P8ETIM                    RENAME(PTETIM)
101eDA*           P8RTE                     RENAME(PTRTE)
101eDA*           P8PO                      RENAME(PTPO)
     A*
     A*  Key fields
     A*
     A          K L8WHSE
     A          K L8RTE
     A          K L8PO
     A          K L8TRN#
     A          O L8STAT                    COMP(EQ 'C')
