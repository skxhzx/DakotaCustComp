     A*****************************************************************
     A* PR341    Work file priority replenishment report              *
     A* Lynn McMahon                                                  *
     A* 29 Oct, 2007                                                  *
     A*
     A*  Revisions
     A*                                                               *
     A*    Created  5.10
     A*                                                               *
     A*****************************************************************
     A                                      REF(PRPLFIL)
     A          R OQREC
     A            PRWHSE    R               REFFLD(SLWHSE SLOT)
     A            PRWHDP    R               REFFLD(SLWHDP SLOT)
     A            PRFAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('From' 'Aisle')
417 AA            PRFLOC    R               REFFLD(SLLOC  SLOT)
417 AA                                      COLHDG('From' 'Location')
417 AA            PRFLVL    R               REFFLD(SLRLVL SLOT)
417 AA                                      COLHDG('From' 'Level')
417 AA            PRFHND    R               REFFLD(SLHAND SLOT)
417 AA                                      COLHDG('From' 'Handstack')
417aAA            PRFPOS         3P 0       TEXT('From Slot Position')
417aAA                                      COLHDG('From Slot' 'Position')
     A            PRTAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('To' 'Aisle')
417 AA            PRTLOC    R               REFFLD(SLLOC  SLOT)
417 AA                                      COLHDG('To' 'Location')
417 AA            PRTLVL    R               REFFLD(SLRLVL SLOT)
417 AA                                      COLHDG('To' 'Level')
417 AA            PRTHND    R               REFFLD(SLHAND SLOT)
417 AA                                      COLHDG('To' 'Handstack')
417aAA            PRTPOS         3P 0       TEXT('To Slot Position')
417aAA                                      COLHDG('To Slot' 'Position')
     A            PRFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            PRTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            PRITEM    R               REFFLD(SLITEM SLOT)
     A            PRSEQ#         5P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq' 'Number')
     A            PRQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            PRSDEF    R               REFFLD(SLSDEF SLOT)
     A                                      COLHDG('Replenishment' 'Slot Type')
     A            PRFLAG         1A         TEXT('Select Flag')
     A                                      COLHDG('Select' 'Flag')
     A            PRPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            PRFREE         1A         TEXT('Free slot Y,N')
     A                                      COLHDG('Free Slot' 'Y,N')
     A            PRSTAT    R               REFFLD(SLSTAT SLOT)
     A                                      COLHDG('Status of' 'Home Slot')
     A            PRTYPE         1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
417aAA            PRRPLT         1A         TEXT('Replenishment Type')
417aAA                                      COLHDG('Replenishment' 'Type')
     A            PRADTE    R               TEXT('Add Date')
     A                                      COLHDG('Add' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PRATIM    R               TEXT('Add Time')
     A                                      COLHDG('Add' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            PRAQS          7P 2       TEXT('Average Quantity Sold')
     A                                      COLHDG('Avg Qty' 'Sold')
     A*  Key
     A          K PRWHSE
     A          K PRWHDP
     A          K PRTDIS
     A*
