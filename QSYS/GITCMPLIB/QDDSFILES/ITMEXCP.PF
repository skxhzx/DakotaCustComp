     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (630) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITMEXCP (PF)
     A*  Description ... Item exception file
     A*  Date .......... January 23, 1997
     A*  Creator ....... HK
     A*
     A*  Revisions
     F*
     F*    04/20/99  RH
     F*      - Created
     F*
414aAF*    06/29/00  MLB  4.14a
414aAF*      - Added new fields to file.
414aAF*         EXGBY  - Exception generated by
414aAF*         EXERID - Exception error id
414aAF*         EXESTA - Item exception status
414aAF*         EXUSER - Workstn User id
414aAF*         EXCDTE - Date corrected
414aAF*         EXCTIM - Time corrected
     A*
416 AF*    09/26/01  DAS  4.16
     F*      - Added comments about task and error codes.
     F*      - Not recompiled.
416aAF*    11/05/01  RH   4.16a
     F*      - Added Breakdown qty fields EXQTY2, EXQTY3.
     F*      - Added Corresponding item, EXCITM.
510aAF*    05/24/06  DAS  5.10d
     F*      - Added RUNNER task with OVRZRO id.
     F*      - No recompile necessary.
610aAF*    12/09/09  DAS  6.10a
     F*      - No recompile needed.
     F*      - Only comments added about new task LCNS.
     F*
     A*----------------------------------------------------------------***
     A*
     A*  NOTE:
     A*      EXFDIS AND EXTDIS FIELDS ARE USED TO STORE OTHER INFO.
     A*      DATA STRUCTURE FOR EXPIRATION DATE OVERRIDE IS:
     A*                1   9 DSPO
     A*               10  160DSLBL
     A*               17  240DSEXPD
     A*                1  12 DSFDIS  (EXFDIS)
     A*               13  24 DSTDIS  (EXTDIS)
     A*      DATA STRUCTURE FOR MULTIPLE UPC CODE IS:
     A*                1  20 DSUPC
     A*                1  12 DSFDIS  (EXFDIS)
     A*               13  24 DSTDIS  (EXTDIS)
     A*----------------------------------------------------------------***
     A*
     A*  Codes
     A*
     A*      Status Codes (EXESTA)
     A*        1 = Open
     A*        4 = Closed
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Task/Error codes
     A*
     A*  Task    Error    Description
     A*  ------  -------  ---------------------------------------------
     A*  REPL    WRGITM   Wrong item in slot
     A*          QTYEXC   Qty not correct in slot
     A*          VRTSLT   Virtual slot found
     A*          NOTEMTY  Empty = No
     A*
     A*  RCV     EXPOVR   Received exp date comes before overflow date
     A*                   Fields EXFDIS and EXTDIS are treated as one
     A*                   24-char field. The following are put into
     A*                   those positions:
     A*
     A*                      1 -  9  P.O. number
     A*                     10 - 16  Label number
     A*                     17 - 24  Overflow exp date (YYYYMMDD)
     A*  RCV     RDUPUPC  Duplicate UPC code exists in file.
     A*  PUTRTN           Returns exception.
     A*
510a A*  RUNNER  OVRZRO   Overrided warning when zero qty was entered
510a A*                   when Dakota showed available qty.
     A*
610a A*  LCNS    NOSCAN   License not scanned for verification in LT210.
610a A*                   Field EXCITM contains license.
610a A*                   Field EXTDIS contains calling program.
610a A*          LT210    License not sent into program LT210.
610a A*          LT210a   LT210 routine ZZUPD2A. This routine should
610a A*                   not be called, so this should not be written
610a A*                   to the file.
610a A*
     A*
     A*----------------------------------------------------------------
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R EXREC
     A            EXWHSE    R               REFFLD(SLWHSE SLOT)
     A            EXWHDP    R               REFFLD(SLWHDP SLOT)
     A            EXSTYP    R               REFFLD(SLSTYP SLOT)
     A            EXITEM    R               REFFLD(SLITEM SLOT)
     A            EXFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            EXTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            EXQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
416a A            EXQTY2         3P 0       TEXT('Brk Qty1')
416a A                                      COLHDG('Brk Qty1')
416a A            EXQTY3         3P 0       TEXT('Brk Qty2')
416a A                                      COLHDG('Brk Qty2')
416a A            EXCITM    R               REFFLD(SLITEM SLOT)
416a A                                      COLHDG('Correspond Item')
     A            EXTASK    R               REFFLD(TKCODE PIRDICT)
     A            EXGDTE    R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD)
     A            EXGTIM    R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS)
414aAA            EXGBY         10A         TEXT('Generated by User')
414aAA                                      COLHDG('Generated by' 'User')
414aAA            EXERID         7A         TEXT('Error Id')
414aAA                                      COLHDG('Error' 'Id')
414aAA            EXESTA         2A         TEXT('Status')
414aAA                                      COLHDG('Status')
414aAA            EXUSER        10A         TEXT('Workstn User id')
414aAA                                      COLHDG('Workstn' 'User Id')
414aAA            EXCDTE    R               TEXT('Date corrected')
414aAA                                      COLHDG('Date' 'Corrected')
414aAA                                      REFFLD(DTECMD)
414aAA            EXCTIM    R               TEXT('Time Corrected')
414aAA                                      COLHDG('Time' 'Corrected')
414aAA                                      REFFLD(TIMHMS)
     A*
     A*  File key.
     A*
     A          K EXWHSE
     A          K EXWHDP
     A          K EXSTYP
     A          K EXITEM
     A          K EXGDTE
     A          K EXGTIM
     A*
