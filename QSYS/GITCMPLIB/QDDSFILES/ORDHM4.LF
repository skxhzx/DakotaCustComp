     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (360) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDHM4(LF)
     A*  Description ... Order header, Ord Misc, joined with route header
     A*  Date .......... August 24, 2015
     A*  Creator ....... NLK
     A*
     A*  Revisions
     A*
700aAA*    08/24/15  NLK  7.00a
     A*      - created
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R HMJREC                    JFILE(ORDH ORDHM RTEHED)
     A*
     A          J                           JOIN(ORDH ORDHM)
     A                                      JFLD(OHWHSE  HMWHSE)
     A                                      JFLD(OHRTE    HMRTID)
     A*
     A          J                           JOIN(ORDH  RTEHED)
     A                                      JFLD(OHWHSE  RHWHSE)
     A                                      JFLD(OHRTE    RHRTID)
     A*  Fields from ORDH file.
     A*
     A            OHWHSE
     A            OHORD
     A            OHORIG
     A            OHTYPE
     A            OHCUST
     A            OHCORD
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A            OHPBAT
     A            OHSTAT
     A*
     A*  Fields from ORDHM file.
     A*
     A            HMWHSE
     A            HMORD
     A            HMRTID
     A            HMMSC1
     A            HMMSC2
     A            HMMSC3
     A            HMMSC4
     A            HMMSC5
     A*
     A*  Fields from RTEHED file.
     A*
     A            RHWHSE
     A            RHRTID
     A            RHRTE
     A            RHSTAT
     A*
     A*  Key fields
     A*
     A          K OHWHSE
     A          K OHCORD
