     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSITMO   (PF)
     A*  Description ... Staging Import - Total item qty ordered
     A*
     A*  Revisions
     A*
640 AA*    01/15/11  DAS  6.40
     A*      - Created
     A*      - Base on IMPFITMO
730 AA*    11/04/19  KDE  7.30
     A*      - Added field IJBD1 to allow Breakdown One Quantity
     A*        to be sent instead of Case Quantity
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R IJREC
     A*
     A            IJWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IJBATID       11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A            IJITEM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            IJSTATUS       1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IJSTSTS         Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IJSTSUC         Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IJTEXT       200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            IJTQO          7S 0       TEXT('Total Quantity Ordered')
     A                                      COLHDG('Total' 'Qty' 'Ordered')
     A            IJQYOH         7S 0       TEXT('Total Quantity Onhand')
     A                                      COLHDG('Total' 'Qty' 'Onhand')
     A*
     A*  Add stamps
     A*
     A            IJADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IJADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
730 AA            IJBD1          7S 0       TEXT('Breakdown 1 Qty Ordered')
730 AA                                      COLHDG('Breakdown' 'Qty1' 'Ordered')
     A*
     A          K IJWHSE
     A          K IJBATID
     A          K IJITEM
