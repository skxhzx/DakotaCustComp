     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSRTN   (PF)
     A*  Description ... Staging Import - Host Returns
     A*
     A*  Revisions
     A*
650 AA*    07/22/14  RTR  6.50
     A*      - Created
     A*      - Based on IMPFRTN
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R OIRREC
     A*
     A            OIRWHS         3S 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            OIRBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A*
     A*  Import information
     A*
     A*    Status info
     A*
     A            OIRSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            OIRSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OIRSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OIRTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            OIRORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OIRITEM       15          TEXT('Item')
     A                                      COLHDG('Item')
     A            OIRITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            OIRTYP         1          TEXT('Return Type')
     A                                      COLHDG('Return' 'Type')
     A            OIRQT1         5S 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            OIRQT2         5S 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            OIRQT3         5S 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
     A            OIRCWT         9S 2       TEXT('Catchweight Returned')
     A                                      COLHDG('Catchweight' 'Returned')
     A            OIRTRN         7S 0       TEXT('Original Tran#')
     A                                      COLHDG('Original' 'Tran#')
     A            OIRCIT        15          TEXT('Corresponding Item')
     A                                      COLHDG('Corresponding' 'Item')
     A            OIRRCD         2          TEXT('Return Code')
     A                                      COLHDG('Return' 'Code')
     A            OIRDCD         5          TEXT('Disposition Code')
     A                                      COLHDG('Disposition' 'Code')
     A            OIRMEM        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            OIRPUT         1          TEXT('Put away')
     A                                      COLHDG('Put away')
     A            OIREDT         6S 0       TEXT('Expire Date')
     A                                      COLHDG('Expiration' 'Date')
     A            OIRLBL         7S 0       TEXT('Label #')
     A                                      COLHDG('Label #')
     A            OIRLCN        15          TEXT('License')
     A                                      COLHDG('License')
     A            OIRMS1        40          TEXT('Miscelleanous Data 1')
     A                                      COLHDG('Misc. Data 1')
     A            OIRMS2        40          TEXT('Miscelleanous Data 2')
     A                                      COLHDG('Misc. Data 2')
     A            OIRSTS         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            OIRSDT          Z         TEXT('Status Date Stamp')
     A                                      COLHDG('Status' 'Date Stamp')
     A*
     A*  Add stamps
     A*
     A            OIRADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OIRADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K OIRWHS
     A          K OIRBATID
     A          K OIRADDUC
     A          K OIRITM
     A          K OIRORD
