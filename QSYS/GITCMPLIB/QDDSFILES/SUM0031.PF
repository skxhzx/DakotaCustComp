     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... SUM003 (PF)
     A*  Description ... Summary 003 - Route Estimated Completion Summary
     A*  Creator ....... RBD
     A*
     A*  Revisions:
     A*
700 AA*    03/30/15  RBD  7.00
     A*      - Created.
     A*
700aAA*    06/16/15  RBD  7.00a
     A*      - Added employee name. If multiple emps exist in PIRTRAND
     A*        for a route, write *MULTIPLE for employee name.  Removed
     A*        multiple employee flag.
     A*
700bAA*    07/10/15  RBD  7.00b
     A*      - Changed remaining hours from 5-0 to 3-2.
     A*
700cAA*    10/25/15  DAS  7.00c
     A*      - Added 'Add' stamp for demo purposes. Allows us to
     A*        manipulate the Departure Time and recalculate.
     A*
730aAA*    03/12/19  RBD  7.30a
     A*      - Changed remaining hours from 3-2 to 3-1. Was blowing
     A*        up at Merit when 3-2.
     A*
750 AA*    09/01/21  KDE  7.50
     A*      - Changed key to use a new 50 character field.  User can
     A*        now decide to sequence by Departure Date/Time, Priority
     A*        sequence using MRC Drag Drop screen to order routes, or
     A*        by default sort by earliest Printed Date/Time, and then
     A*        route for anything not printed yet.  These respective
     A*        values will be placed in this 50 character field by
     A*        UPDSUM003 based on setting in System General.
     A*
750AaA*    09/15/21  KDE  7.50a
     A*      - Add field to keep flag to show if route is auto assigned
     A*
     A*----------------------------------------------------------------
     A          R SMREC
     A*
     A            SMSESSID      40          TEXT('Session Id')
     A                                      COLHDG('Session Id')
     A            SMWHSE    R               REFFLD(WHCODE PIRDICT)
     A            SMRTE          5          TEXT('Route')
     A                                      COLHDG('Route')
     A            SMRTID         5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            SMRTESTAT      1          TEXT('Route Status')
     A                                      COLHDG('Route' 'Status')
     A            SMDDTE    R               TEXT('Departure Date')
     A                                      COLHDG('Departure' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            SMDTIM    R               TEXT('Departure Time')
     A                                      COLHDG('Departure' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            SMEDTE    R               TEXT('Est Completion Date')
     A                                      COLHDG('Est Completion' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            SMETIM    R               TEXT('Est Completion Time')
     A                                      COLHDG('Est Completion' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            SMMINFRDEP     5P 0       TEXT('Min From Departure Date')
     A                                      COLHDG('Min From' 'Departure Date')
     A            SMPCSPCK       5P 0       TEXT('Picked Pieces')
     A                                      COLHDG('Picked' 'Pieces')
     A            SMPCSREM       5P 0       TEXT('Remaining Pieces')
     A                                      COLHDG('Remaining' 'Pieces')
     A            SMPCSTTL       5P 0       TEXT('Total Pieces')
     A                                      COLHDG('Total' 'Pieces')
     A            SMEMP#         5P 0       TEXT('Employee')
     A                                      COLHDG('Employee')
700aDA**          SMEFLAG        1S 0       TEXT('Multiple Emps Flag')
700aDA**                                    COLHDG('Multiple' 'Employees Flag')
700aAA            SMEMPNAM      40          TEXT('Employee Name')
700aAA                                      COLHDG('Employee' 'Name')
     A            SMPPH          7P 0       TEXT('Pieces Per Hour')
     A                                      COLHDG('Pieces' 'Per Hour')
700bDA**          SMHRSREM       5P 0       TEXT('Remaining Hours')
730aDA**          SMHRSREM       3P 2       TEXT('Remaining Hours')
730aMA            SMHRSREM       3P 1       TEXT('Remaining Hours')
     A                                      COLHDG('Remaining' 'Hours')
     A            SMTRNOPN       5P 0       TEXT('Open Trans')
     A                                      COLHDG('Open' 'Trans')
     A            SMTRNSTR       5P 0       TEXT('Started Trans')
     A                                      COLHDG('Started' 'Trans')
     A            SMTRNEND       5P 0       TEXT('Ended Trans')
     A                                      COLHDG('Ended' 'Trans')
     A            SMTRNCLS       5P 0       TEXT('Closed Trans')
     A                                      COLHDG('Closed' 'Trans')
     A            SMTRNTTL       5P 0       TEXT('Total Trans')
     A                                      COLHDG('Total' 'Trans')
700cAA            SMADDTS   R               REFFLD(ADDTS PIRDICT)
750 AA            SMID           4P 0       TEXT('Priority Seq')
750 AA                                      COLHDG('Priority' 'Seq')
750 AA            SMPDTE         8P 0       TEXT('Printed Date')
750 AA                                      COLHDG('Printed' 'Date')
750 AA            SMPTIM         6P 0       TEXT('Printed Time')
750 AA                                      COLHDG('Printed' 'Time')
750 AA            SMKEYF        50          TEXT('Dynamic Key')
750 AA                                      COLHDG('Dynamic' 'Key')
750 AA            SMFLGSHRTS     1          TEXT('Shorts Flag')
750 AA                                      COLHDG('Shorts' 'Flag')
750 AA            SMFLGLDR       1          TEXT('Loader Flag')
750 AA                                      COLHDG('Loader' 'Flag')
750 AA            SMFLGAUD       1          TEXT('Audit Flag')
750 AA                                      COLHDG('Audit' 'Flag')
750aAA            SMAASN         1          TEXT('Auto Assign')
750aAA                                      COLHDG('Auto' 'Assign')
     A*
     A          K SMSESSID
750 DA*         K SMDDTE
750 DA*         K SMDTIM
750 DA*         K SMRTE
750 DA*         K SMRTID
750 AA          K SMKEYF
