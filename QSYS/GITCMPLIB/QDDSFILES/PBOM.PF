     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PBOM      (PF)
     A*  Description ... PRODUCTION BILL OF MATERIAL FILE
     A*  Creator ....... KDE
     A*
     A*
     A*  Revisions
     A*
     A*      - KDE - 07/31/2014
     A*        Created
710 AA*      - KDE - 02/02/2017
     A*        Added field to identify raw material(s) that are used for production
     A*        of Finished Good Product
     A*
     A*----------------------------------------------------------------
     A*
     A*  Bill of Materials can use Dakota normal quantities
     A*    BMQTYN, BMQTY1, and BMQTY2 relate Case Qty, ITUMQ1, ITUMQ2
     A*    The User can also chose to use the Production Breakdown
     A*    Quantity.  Field BMPRDQ is the quantity of this breakdown
     A*    that this finished good requires.  The Bill of Material
     A*    also has a flag BMPQUF which is populated with a "Y"
     A*    if the BMPRDQ is used and a "N" if the normal Dakota
     A*    quantities are used.
     A*
     A*    BMQTYN - Defines how many normal case quantity req
     A*    BMQTY1 - Defines how many Dakota BD1 quantity req
     A*    BMQTY2 - Defines how many Dakota BD2 quantity req
     A*    BMPRDQ - Defines how many Prod Qty BD req
     A*    BMPQUF - "Y" means Prod Qty used, "N" Dakota Quantities
     A*
     A*    The item definitions for the Production specific
     A*    definitions can be found in file ITEMPRD
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R BMREC
     A*
     A            BMWHSE         3P 0       TEXT('Whse')
     A                                      COLHDG('Whse')
     A            BMFGI         15          TEXT('Finished Good Item')
     A                                      COLHDG('FG Item#')
     A            BMSEQ          4P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq Number')
     A            BMRMI         15          TEXT('Raw Material Item')
     A                                      COLHDG('RM Item#')
     A            BMQTYN         5P 0       TEXT('Qty Normal')
     A                                      COLHDG('RM Qty Nrm')
     A            BMQTY1         3P 0       TEXT('Qty BrkDwn 1')
     A                                      COLHDG('RM Qty BrkDwn1')
     A            BMQTY2         3P 0       TEXT('Qty BrkDwn 2')
     A                                      COLHDG('RM Qty BrkDwn2')
     A            BMPRDQ         9P 4       TEXT('PRD Qty Breakdown')
     A                                      COLHDG('PRD BrkDwn')
     A            BMSCRF         5P 4       TEXT('Raw Material Scrap')
     A                                      COLHDG('RM SCRP Factor')
     A            BMTYPE         1          TEXT('Raw Material Issue Type')
     A                                      COLHDG('RM Type Issue')
     A            BMBCKF         1          TEXT('Raw Material Back Flush')
     A                                      COLHDG('RM Back Flush')
     A            BMPQUF         1          TEXT('Prod Qty Use Flag')
     A                                      COLHDG('PRD Qty Use Flag')
     A            BMEFFD         8  0       TEXT('Effective Date')
     A                                      COLHDG('Eff. Date')
     A            BMEXPD         8  0       TEXT('Expiration Date')
     A                                      COLHDG('Exp. Date')
710 AA            BMUSEP         1          TEXT('Used in FG Item')
710 AA                                      COLHDG('Used in FG ITM')
     A*
     A          K BMWHSE
     A          K BMFGI
     A          K BMSEQ
     A          K BMEFFD
     A          K BMEXPD
