     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   24 N. Washington Ave Suite 203
     A*   Batavia, IL 60510
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFLCNS (PF)
     A*  Description ... FTP Export Inventory License Info file
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    08/16/11  MLB  6.40  P#01049
     A*      - Created.
640aAA*    09/27/11  MLB  6.40a  P#01049
     A*      - Enh: Revised file to add 2 new fields. ELAENT - License
     A*        Entered Date and ELAEXP - License Expiration Date.
640bAA*    01/05/12  MLB  6.40b  P#
     A*      - Enh: Revised file to add 1 new fields. ELASTS - Slot
     A*        status code.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A          R ELAREC
     A*
     A            ELAWHS    R               REFFLD(WHCODE)
     A            ELAWHD    R               REFFLD(WDCODE)
     A            ELASTP    R               REFFLD(SLSTYP)
     A            ELAASL    R               REFFLD(ASCODE)
     A            ELALOC    R               REFFLD(SLLOC)
     A            ELALVL    R               REFFLD(SLRLVL)
     A            ELAHND    R               REFFLD(HSCODE)
     A*
     A            ELADSP        12          TEXT('Slot Display')
     A                                      COLHDG('Slot' 'Display')
     A            ELAITM    R               REFFLD(ITITEM)
     A*
     A            ELAQT1         6A         TEXT('License Qty1')
     A                                      COLHDG('License' 'Qty1')
     A            ELAQT2         6A         TEXT('License Qty2')
     A                                      COLHDG('License' 'Qty2')
     A            ELAQT3         6A         TEXT('License Qty3')
     A                                      COLHDG('License' 'Qty3')
     A*
     A            ELALCN        15A         TEXT('License Number')
     A                                      COLHDG('License Number')
     A            ELAOLC        15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            ELALOT        30A         TEXT('Lot Code')
     A                                      COLHDG('Lot' 'Code')
     A            ELAPO          9A         TEXT('P/O Number')
     A                                      COLHDG('P/O' 'Nbr')
     A            ELAPOS         5S 0       TEXT('P/O Seq Number')
     A                                      COLHDG('P/O' 'Seq Nbr')
     A*
     A            ELADSG    R               TEXT('Designation Code')
     A                                      COLHDG('Designation' 'Code')
     A                                      REFFLD(DSCODE)
     A            ELAPCK         1          TEXT('Pick Slot')
     A                                      COLHDG('Pick' 'Slot')
640aAA            ELAENT         8S 0       TEXT('License Entered DateYYYYMMDD')
640aAA                                      COLHDG('License' 'Entered Date')
640aAA            ELAEXP         8S 0       TEXT('License Exp. Date YYYYMMDD')
640aAA                                      COLHDG('License' 'Exp Date')
640bAA            ELASTS         2          TEXT('Slot Status')
640bAA                                      COLHDG('Slot' 'Status')
     A*
     A          K ELAWHS
     A          K ELAWHD
     A          K ELADSP
     A          K ELALCN
     A          K ELAITM
