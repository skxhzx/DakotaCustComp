     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... BFCTMPLST (PF)
     A*  Description ... Temporary list file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
700 AA*    Ver 7.00   12/26/15  DAS
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file is to be used for simple temporary lists. It was
     A*   originally designed for use in VOSELECT to keep a list of AMP
     A*   Staging locations that the TMan unit could request one at a
     A*   time at the end of the pick. The BTLLIST field would contain
     A*   the TMan number plus 'AMDSTAGE', BTLKEY would contain the
     A*   seq# and stage code, and BTLVALUE would contain the stage code.
     A*
     A*   The program responsible for creating the list should delete
     A*   the list when finished.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R BTLREC
     A*
     A            BTLWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            BTLLIST       30          TEXT('List Code')
     A                                      COLHDG('List Code')
     A            BTLKEY        30          TEXT('List Key')
     A                                      COLHDG('List Key')
     A            BTLVALUE      50          TEXT('List Value')
     A                                      COLHDG('List Value')
     A*
     A            BTLADDTS  R               REFFLD(ADDTS PIRDICT)
     A            BTLADDUC  R               REFFLD(ADDUC PIRDICT)
     A            BTLADDUSR R               REFFLD(ADDUSR PIRDICT)
     A            BTLADDJOB R               REFFLD(ADDJOB PIRDICT)
     A            BTLADDNBR R               REFFLD(ADDNBR PIRDICT)
     A*
     A          K BTLWHSE
     A          K BTLLIST
     A          K BTLKEY
