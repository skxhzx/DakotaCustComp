     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDD  (PF)
     A*  Description ... Order Detail file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    07/29/94  DAS  Rev 2.00
     A*      - Added warehouse, corresponding warehouse and order#
     A*        and original item #.
     A*      - Split quantity into three parts.
     A*
     A*    08/17/94  DAS  Rev 2.01
     A*      - Changed OHORD to packed.
     A*
     A*    09/30/94  DAS  Rev 2.02
     A*      - Added special status code field (ODSPCL).
     A*
     A*    10/21/94  HNK  Rev 2.03
     A*      - Added sequence number field (ODSEQ).
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    - If the item to be picked (ODDITM) is a substitue, the
     A*      original item should be identified in field ODDOIT.
     A*
     A*    - The corresponding warehouse and order # are used for
     A*      transfers.
     A*
     A*
     A*    - Status codes
     A*
     A*       'O'  -  Open
     A*       'P'  -  Processing
     A*       'C'  -  Closed
     A*
     A*
     A*    - Special order items need to be identified using the
     A*      ODSPCL field.  It should contain the status code of the
     A*      slot which contains the item. At picking time only slots
     A*      with a matching status code will be searched.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R ODREC
     A*
     A            ODWHSE    R               REFFLD(WHCODE)
     A            ODORD     R               REFFLD(OHORD)
     A            ODSEQ     R               REFFLD(SEQ#)
     A            ODITEM    R               REFFLD(ITITEM)
     A                                      TEXT('Item Number')
     A                                      COLHDG('Item' 'Number')
     A            ODQOR1         5S 0       TEXT('Normal Qty Ordered')
     A                                      COLHDG('Normal Qty' 'Ordered')
     A            ODQOR2         5S 0       TEXT('Breakdown 1 Qty Ordered')
     A                                      COLHDG('Breakdown 1' 'Qty Ordered')
     A            ODQOR3         5S 0       TEXT('Breakdown 2 Qty Ordered')
     A                                      COLHDG('Breakdown 2' 'Qty Ordered')
     A            ODWH2     R               TEXT('Corresponding Warehouse')
     A                                      COLHDG('Corresponding' 'Warehouse')
     A                                      REFFLD(WHCODE)
     A            ODOR2     R               TEXT('Corresponding Order #')
     A                                      COLHDG('Corresponding' 'Order #')
     A                                      REFFLD(OHORD)
     A            ODOITM    R               TEXT('Original Item')
     A                                      COLHDG('Original' 'Item')
     A                                      REFFLD(ITITEM)
     A            ODSPCL    R               TEXT('Special Status Code')
     A                                      COLHDG('Special' 'Status Code')
     A                                      REFFLD(SSCODE)
     A            ODQPK1         5S 0       TEXT('Normal Qty Picked')
     A                                      COLHDG('Normal Qty' 'Picked')
     A            ODQPK2         5S 0       TEXT('Breakdown 1 Qty Picked')
     A                                      COLHDG('Breakdown 1' 'Qty Picked')
     A            ODQPK3         5S 0       TEXT('Breakdown 2 Qty Picked')
     A                                      COLHDG('Breakdown 2' 'Qty Picked')
     A            ODSTAT         1A         TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A          K ODWHSE
     A          K ODORD
     A          K ODSEQ
     A          K ODITEM
