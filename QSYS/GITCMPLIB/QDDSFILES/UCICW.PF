     A*----------------------------------------------------------------
     A*   Copyright (C) 1996 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W Roosevelt Rd.  #8-51
     A*   West Chicago, IL 60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... UCICW   (PF)
     A*  Description ... UCI catch weight file
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
510 AF*    06/16/06  JJ  5.10
     A*      - Created
510aAF*    11/14/06  MLB 5.10a
     A*      - Enh: Added two new fields; UCISQI to store p/o line number
     A*        and UCISQO to store customer order line number.
     A*
510bAF*    04/04/07  JCJ 5.10b
     A*      - Enh: Added new fields: UCILBL.                          er
     A*
510cAF*    05/15/07  JCJ 5.10c
     A*      - Enh: Added new fields: UCIVAL.                          er
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*    **Important**
     A*      - Any time fields are added to this physical file,
     A*        the fields must be added to file UCICW3. UCICW3
     A*        utilizes the SST keyword and this requires all
     A*        fields to be listed in the LF dds.
     A*
510cAA*----------------------------------------------------------------***
510cAA*
510cAA*  Codes
510cAA*
510cAA*    Validation Code   (UCIVAL)
510cAA*
510cAA*      'Y'  -  Validated
510cAA*
510cAA*      'N'  -  Not Validated
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R UCIREC
     A*
     A            UCIWHS    R               REFFLD(WHCODE)
     A            UCINBR        10          TEXT('UCI Number')
     A                                      COLHDG('UCI' 'Number')
     A            UCISQ2         5P 0
     A                                      TEXT('Cwt Detail Seq Number')
     A                                      COLHDG('Cwt Detail Seq' 'Number')
     A            UCIITM    R               REFFLD(ITITEM)
     A            UCILCN        15          TEXT('License #')
     A                                      COLHDG('License #')
     A            UCIWGT         7P 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            UCILOT        30A         TEXT('Lot Code Value')
     A                                      COLHDG('Lic Info' 'Value')
     A            UCIINB    R               REFFLD(PONUM)
     A                                      TEXT('Inbound delivery number')
     A                                      COLHDG('Inbound' 'Delivery#')
510aAA            UCISQI    R               REFFLD(SEQ#)
510aAA                                      TEXT('Inbound Delivery Line Nbr')
510aAA                                      COLHDG('Inbound' 'Delivery' -
510aAA                                      'Line Number')
     A            UCIOUT    R               REFFLD(PONUM)
     A                                      TEXT('Outbound delivery number')
     A                                      COLHDG('Outbound' 'Delivery#')
510aAA            UCISQO    R               REFFLD(SEQ#)
510aAA                                      TEXT('Outbound Delivery Line Nbr')
510aAA                                      COLHDG('Outbound' 'Delivery' -
510aAA                                      'Line Number')
     A            UCIRDT    R               TEXT('Receive Date')
     A                                      COLHDG('Receive' 'Date')
     A                                      REFFLD(DTECMD)
     A            UCIRTM    R               TEXT('Receive Time')
     A                                      COLHDG('Receive' 'Time')
     A                                      REFFLD(TIMHMS)
     A            UCIEDT    R               REFFLD(DTECMD)
     A                                      TEXT('Expiration Date')
     A                                      COLHDG('Exp' 'Date')
     A            UCIQTY         5S 0       TEXT('Quanitity')
     A                                      COLHDG('Quantity')
     A            UCIPCK         1          TEXT('Pick Flag (Y/N)      ')
     A                                      COLHDG('Pick' 'Flag')
     A            UCIRTS         1          TEXT('Returns Flag (Y/N)   ')
     A                                      COLHDG('Returns' 'Flag')
     A            UCIRDE    R               REFFLD(DTECMD)
     A                                      TEXT('Returns Date')
     A                                      COLHDG('Returns' 'Date')
     A            UCIRTI    R               TEXT('Returns Time')
     A                                      COLHDG('Returns' 'Time')
     A                                      REFFLD(TIMHMS)
     A            UCIRCD         2          TEXT('Returns reason code')
     A                                      COLHDG('Returns' 'Reason' 'Code')
     A            UCISTS         1          TEXT('Stock Code')
     A                                      COLHDG('Stock' 'Code')
     A            UCIRCT    R               TEXT('Receiving Tran Number')
     A                                      COLHDG('Receiving' 'Tran' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            UCIPKT    R               TEXT('Pick Tran Number')
     A                                      COLHDG('Pick' 'Tran' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            UCIRTT    R               TEXT('Return Tran Number')
     A                                      COLHDG('Return' 'Tran' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            UCIPED    R               REFFLD(DTECMD)
     A                                      TEXT('PO Export Date')
     A                                      COLHDG('PO' 'Export' 'Date')
     A            UCIPET    R               REFFLD(TIMHMS)
     A                                      TEXT('PO Export Time')
     A                                      COLHDG('PO' 'Export' 'Time')
     A            UCIRED    R               REFFLD(DTECMD)
     A                                      TEXT('RTS Export Date')
     A                                      COLHDG('RTS' 'Export' 'Date')
     A            UCIRET    R               REFFLD(TIMHMS)
     A                                      TEXT('RTS Export Time')
     A                                      COLHDG('RTS' 'Export' 'Time')
     A            UCILBL    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            UCIVAL         1          TEXT('Validation Code')
     A                                      COLHDG('Validation' 'Code')
     A*
     A          K UCIWHS
     A          K UCINBR
