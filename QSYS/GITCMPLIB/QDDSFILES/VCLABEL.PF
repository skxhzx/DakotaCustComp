     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... VCLABEL (PF)
     A*  Description ... Selection file for Vocollect test
     A*  Creator ....... DAS/HNK
     A*
     A*  Revisions:
     A*
     A*----------------------------------------------------------------
     A*
     A*  Roxanne - DO not send this file with any updates.  This is for
     A*            VOCOLLECT test only ...
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes (VCSTAT)
     A*
     A*       'n'  -  Used internally by Talkman
     A*
     A*    Catch weight Flag
     A*
     A*       '0'  -  No Catch Weight
     A*       '1'  -  Catch weight required
     A*       '2'  -  Case number required
     A*
     A*    Inner Pack Flag
     A*
     A*       '0'  -  Pick inner packs (Eaches)
     A*       '1'  -  Pick cases
     A*
     A*    Number of check digits
     A*
     A*       2,3 only allowed.
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R VCREC
     A*
     A*  General information.
     A*
     A            VCEROR         5P 0       TEXT('Error Number')
     A                                      COLHDG('Error' 'Number')
     A            VCERRT        50A         TEXT('Error Text')
     A                                      COLHDG('Error' 'Text')
     A            VCTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            VCSTOP    R               REFFLD(STOP)
     A            VCAISL    R               REFFLD(ASCODE)
     A            VCLOC     R               REFFLD(SLLOC )
     A            VCDISP        12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            VCQPCK         5P 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            VCCNUM         2S 0       TEXT('Number of check digits')
     A                                      COLHDG('Num Chk Digit')
     A            VCSLID         2S 0       TEXT('Check digits')
     A                                      COLHDG('Check digit')
     A            VCDESC        30A         TEXT('Item Description')
     A                                      COLHDG('Item' 'Description')
     A            VCSIZE        15A         TEXT('Size')
     A                                      COLHDG('Size')
     A            VCPACK        15A         TEXT('Pack')
     A                                      COLHDG('Pack')
     A            VCITEM    R               REFFLD(ITITEM)
     A            VCMFGC        15A         TEXT('Mfg Code')
     A                                      COLHDG('Mfg Code')
     A            VCUPC         15          TEXT('UPC code')
     A                                      COLHDG('UPC Code')
     A            VCUWGT         9P 4       TEXT('Unit Weight')
     A                                      COLHDG('Unit' 'Weight')
     A            VCUFLG         1          TEXT('Inner Pack Flag')
     A                                      COLHDG('Inner Pck' 'Flag')
     A            VCCFLG         1          TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            VCCWGT         7P 2       TEXT('Catch Weight Base')
     A                                      COLHDG('Catch' 'Weight Base')
     A            VCCTOL         5P 2       TEXT('Catch Weight Tolerance')
     A                                      COLHDG('Catch' 'Weight Tol')
     A            VCLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            VCPAL#    R               TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            VCSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A*
     A          K VCLBL#
