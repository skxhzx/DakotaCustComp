     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL4 (LF)
     A*  Description ... Label file for printing summary info.
     A*  Date .......... October 06, 1994
     A*  Creator ....... DAS
     A*
     A*  Revision:
     A*  10/21/94 HNK
     A*  Sequence number field L4SEQ added.
     A*  Key changed.
     A*
     A*  Notes:
     A*
     A*    This logical file was created so I could have two copies
     A*    of the same file in one program. One file will be the main
     A*    file read for printing labels. This file will be used to
     A*    calculate the stop and pallet summaries.
     A*
     A*    *** If fields are removed from the physical file LABEL they
     A*    must also be removed from this file. If a new field is added
     A*    to the file and it is need by a program using this file you
     A*    must also add the field to this file.
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R L4REC                     JFILE(LABEL ORDH)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A*  Fields from LABEL file.
     A*
     A            L4WHSE                    RENAME(LBWHSE)
     A            L4WHDP                    RENAME(LBWHDP)
     A            L4SEQ                     RENAME(LBSEQ)
     A            L4ITEM                    RENAME(LBITEM)
     A            L4PBAT                    RENAME(LBPBAT)
     A            L4AISL                    RENAME(LBAISL)
     A            L4LOC                     RENAME(LBLOC)
     A            L4RLVL                    RENAME(LBRLVL)
     A            L4HAND                    RENAME(LBHAND)
     A            L4PSEQ                    RENAME(LBPSEQ)
     A            L4STYP                    RENAME(LBSTYP)
     A            L4RTE                     RENAME(LBRTE)
     A            L4STOP                    RENAME(LBSTOP)
     A            L4ORD                     RENAME(LBORD)
     A            L4SPCL                    RENAME(LBSPCL)
     A            L4UTYP                    RENAME(LBUTYP)
     A            L4UCOD                    RENAME(LBUCOD)
     A            L4UCUB                    RENAME(LBUCUB)
     A            L4CUBE                    RENAME(LBCUBE)
     A            L4UWGT                    RENAME(LBUWGT)
     A            L4SWGT                    RENAME(LBSWGT)
     A            L4QALC                    RENAME(LBQALC)
     A            L4QRMN                    RENAME(LBQRMN)
     A            L4QPCK                    RENAME(LBQPCK)
     A            L4TIE                     RENAME(LBTIE)
     A            L4HIGH                    RENAME(LBHIGH)
     A            L4TYPE                    RENAME(LBTYPE)
     A            L4LBL#                    RENAME(LBLBL#)
     A            L4PAL#                    RENAME(LBPAL#)
     A            L4TRN#                    RENAME(LBTRN#)
     A            L4CTR#                    RENAME(LBCTR#)
     A            L4STAT                    RENAME(LBSTAT)
     A            L4PRTY                    RENAME(LBPRTY)
     A            L4SDTE                    RENAME(LBSDTE)
     A            L4STIM                    RENAME(LBSTIM)
     A            L4VRFY                    RENAME(LBVRFY)
     A*
     A*  Fields from ORDH file.
     A*
     A            L4CNAM                    RENAME(OHCNAM)
     A*
     A*  Key fields
     A*
     A          K L4PBAT
     A          K L4WHSE
     A          K L4RTE
     A          K L4STYP
     A          K L4WHDP
     A          K L4TYPE
     A          K L4TRN#
     A          K L4STOP
     A          K L4SEQ
     A          K L4ITEM
