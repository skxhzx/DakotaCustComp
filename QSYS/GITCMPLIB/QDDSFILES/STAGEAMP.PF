     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... STAGEAMP
     A*  Created   ..... 7/17/2015
     A*  Description ... ADVANCED MERGE PICK STAGING AREAS
     A*  Creator ....... NANCY KOELZ
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
700aAA*    09/22/15  NLK  7.00a
     A*      - added extra check digits and zone fields
700bAA*    09/28/15 NLK  7.00b
     A*      - ADDED Drop Off Seq# to control the order a user is sent
     A*        to the staging area for dropping off the AMP items.
     A*        Orders can go to multi doors and multi orders in 1 door.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*    GUI maintenace program created for this file
     A*    Stage Area type (A-Area or D-Door)
     A*    Default and Verify Chk Digit (Y/N)
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A                                      REF(*LIBL/PIRDICT)
     A          R SAREC
     A*
     A*  General information.
     A*
     A            SAWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            SACODE         3  0       TEXT('Code')
     A                                      COLHDG('Code')
     A            SADESC        30A         TEXT('Description')
     A                                      COLHDG('Description')
     A            SAVDESC       30A         TEXT('Voice Description')
     A                                      COLHDG('Voice Description')
     A            SATYPE         1A         TEXT('Stage area type')
     A                                      COLHDG('Stage area type')
     A            SADFLT         1A         TEXT('Default')
     A                                      COLHDG('Default')
     A            SAVFYCD        1A         TEXT('Verify Chk Digit')
     A                                      COLHDG('Verify Chk Dgt')
     A            SACHKDGT1      3  0       TEXT('CheckDigit 1')
     A                                      COLHDG('CheckDigit 1')
700aAA            SACHKDGT2      3  0       TEXT('CheckDigit 2')
     A                                      COLHDG('CheckDigit 2')
700aAA            SACHKDGT3      3  0       TEXT('CheckDigit 3')
     A                                      COLHDG('CheckDigit 3')
     A            SAAISL    R               REFFLD(SLAISL)
     A                                      COLHDG('Aisle')
     A            SALOC     R               REFFLD(SLLOC)
     A                                      COLHDG('Location')
     A            SADDPT         5A         TEXT('Door Department')
     A                                      COLHDG('Door Dept')
     A            SADNUM         3  0       TEXT('Door Number')
     A                                      COLHDG('Door Number')
700aAA            SAZONEM   R               REFFLD(ZHZONE)
     A                                      COLHDG('Merge Zone')
700aAA            SAZONET   R               REFFLD(ZHZONE)
     A                                      COLHDG('Tote Zone')
700bAA            SADRPOFF       3  0       TEXT('Drop Off Seq#')
700bAA                                      COLHDG('Drop Off Seq#')
      *
     A          K SAWHSE
     A          K SACODE
