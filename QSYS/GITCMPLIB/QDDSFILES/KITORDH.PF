     A*----------------------------------------------------------------
     A*   Copyright (C) 2006 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... KITORDH   (PF)
     A*  Description ... Kit Work Order Header File
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
510 AA*   02/29/07  DAS  5.10
     A*     - Created
     A*
510aAA*   03/22/07  TJ   5.10a
     A*     - Added Date Needed field.
     A*----------------------------------------------------------------
     A*  Field Values
     A*
     A*  KHOBY  - Created By
     A*           'D' - Dakota Priority Replenishment
     A*           'H' - Host
     A*           'M' - Manually
     A*
     A*  KHOSTA - Status
     A*           '1' - Open
     A*           '2' - Started
     A*           '4' - Closed
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R KOHREC
     A*
     A            KOHWHS         3  0       TEXT('Whse')
     A                                      COLHDG('Whse')
     A            KOHORD         7  0       TEXT('Internal Work Order')
     A                                      COLHDG('Internal' 'Work Order')
     A            KOHBY          1          TEXT('Created By')
     A                                      COLHDG('Created' 'By')
     A            KOHREF        15          TEXT('Host Ref')
     A                                      COLHDG('Host Ref')
     A            KOHITM        15          TEXT('Kit Item Number')
     A                                      COLHDG('Kit Item')
     A            KOHQTY         5  0       TEXT('Qty Needed')
     A                                      COLHDG('Qty Needed')
     A            KOHSTA         1          TEXT('Sts - 1=Open 2=Start 4=Close')
     A                                      COLHDG('Sts-1,2,4')
     A            KOHODT         8  0       TEXT('Date Opened')
     A                                      COLHDG('Date Opened')
     A            KOHOTM         6  0       TEXT('Time Opened')
     A                                      COLHDG('Time Opened')
     A            KOHSDT         8  0       TEXT('Date Started')
     A                                      COLHDG('Date Started')
     A            KOHSTM         6  0       TEXT('Time Started')
     A                                      COLHDG('Time Started')
     A            KOHCDT         8  0       TEXT('Date Closed')
     A                                      COLHDG('Date Closed')
     A            KOHCTM         6  0       TEXT('Time Closed')
     A                                      COLHDG('Time Closed')
510aAA            KOHNDT         8  0       TEXT('Date Needed')
510aAA                                      COLHDG('Date Needed')
     A*
     A          K KOHWHS
     A          K KOHORD
     A          K KOHITM
