730aAA*********************************
730AAA**** DO NOT USE IN PROGRAMS  ****
730AAA**** USE PIRTRANW INSTEAD    ****
730AAA*********************************
     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PIRTRAN (PF)
     A*  Description ... Transaction file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
     A*    Ver 2.02  3/10/96  DAS
     A*      - Revised to contain fields to be used by EPM.
     A*
213 AA*    Ver 2.03  05/13/96  DAS
213 AA*      - Added fields PTGRP1, PTGRP2, PTRDTE
     A*
730aAA*    10/04/19  DAS  7.30a
     A*      - Removed UNIQUE. Moved to LF PIRTRANW that has Whse.
     A*      - This file should no longer be used in programs. Instead
     A*        use PIRTRANW.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes
     A*
     A*       '1'  -  Pending
     A*       '2'  -  Started (Active)
     A*       '3'  -  Ended   (Finished)
     A*       '4'  -  Closed  (Completed)
     A*       '8'  -  Interrupted
     A*       '9'  -  Canceled
     A*
     A*    End type codes
     A*
     A*       'C'  -  Ended by close program.
     A*       'M'  -  Ended by manual entry of time.
     A*       'N'  -  Ended by start of new transaction.
     A*
     A*    Group type 1 (PTGRP1)
     A*
     A*       ' '  -  Misc.
     A*       'L'  -  Letdown/Replenishment
     A*       'P'  -  Putaway/Receiving
     A*       'R'  -  Return
     A*       'S'  -  Selection
     A*
     A*    Group type 2  (PTGRP2)
     A*
     A*       ' '  -  Normal
     A*       'W'  -  Will Call
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
730aDA**                                    UNIQUE
     A          R PTREC
     A*
     A            PTTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            PTSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            PTTASK    R               REFFLD(TKCODE PIRDICT)
     A            PTCAT     R               REFFLD(TCCODE PIRDICT)
     A            PTSUB     R               REFFLD(TSCODE PIRDICT)
     A            PTWHSE    R               REFFLD(WHCODE PIRDICT)
     A            PTWHDP    R               REFFLD(WDCODE PIRDICT)
     A            PTSTYP    R               REFFLD(SLSTYP PIRDICT)
     A            PTSHFT    R               REFFLD(SHCODE PIRDICT)
     A            PTCREW    R               REFFLD(CRCODE PIRDICT)
     A            PTEMP#         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            PTCUBE         9P 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            PTSWGT         9P 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            PTPCS          5P 0       TEXT('Total Pieces')
     A                                      COLHDG('Total' 'Pieces')
     A            PTAISL         5P 0       TEXT('Total Aisles')
     A                                      COLHDG('Total' 'Aisles')
     A            PTQTY1         5P 0       TEXT('Total Qty1')
     A                                      COLHDG('Total' 'Qty1')
     A            PTQTY2         5P 0       TEXT('Total Qty2')
     A                                      COLHDG('Total' 'Qty2')
     A            PTQTY3         5P 0       TEXT('Total Qty3')
     A                                      COLHDG('Total' 'Qty3')
     A            PTGDTE    R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PTGTIM    R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            PTSDTE    R               TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PTSTIM    R               TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            PTEDTE    R               TEXT('End Date')
     A                                      COLHDG('End' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PTETIM    R               TEXT('End Time')
     A                                      COLHDG('End' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            PTETYP         1          TEXT('End Type')
     A                                      COLHDG('End' 'Type')
     A            PTCDTE    R               TEXT('Close Date')
     A                                      COLHDG('Close' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PTCTIM    R               TEXT('Close Time')
     A                                      COLHDG('Close' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
213 AA            PTRDTE    R               TEXT('Report Date')
213 AA                                      COLHDG('Report' 'Date')
213 AA                                      REFFLD(DTECMD PIRDICT)
     A            PTDMIN         5P 0       TEXT('Direct Minutes')
     A                                      COLHDG('Direct' 'Minutes')
     A            PTIMIN         5P 0       TEXT('Indirect Minutes')
     A                                      COLHDG('Indirect' 'Minutes')
     A            PTRTPO         9          TEXT('Route/PO')
     A                                      COLHDG('Route/PO')
213 AA            PTGRP1         1A         TEXT('Group 1')
213 AA                                      COLHDG('Group 1')
213 AA            PTGRP2         1A         TEXT('Group 2')
213 AA                                      COLHDG('Group 2')
     A*
     A          K PTTRN#
