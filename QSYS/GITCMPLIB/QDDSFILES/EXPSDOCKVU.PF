     A*----------------------------------------------------------------
     A*   Copyright (C) 2022 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSDOCKVU (PF)
     A*  Description ... Staging Export for DockVue (PO header info)
     A*
     A*  Revisions
     A*
760 AA*    06/03/22  TDC  7.60
     A*      - Created
     A*
770aAA*    07/27/22  TDC  7.70a
     A*      - Change the due date from 6 positions to 8.
     A*
770bAA*    10/28/22  TDC  7.70b
     A*      - Add field DVESDEL to reflect that a PO has been
     A*        unscheduled in DockVue.
     A*
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*   This file is used for transferring data from DockVue to the
     A*   Host system. Data is written to this file via a program that
     A*   Damco wrote to retrieve this information from DockVue.
     A*
     A*   The Damco program also updates file PODOCKVUE at the same
     A*   time.
     A*
     A*----------------------------------------------------------------
     A          R DVEREC
     A
     A            DVEWHSE        3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            DVEBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            DVEPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            DVESTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            DVESTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            DVESTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            DVETEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            DVEPOSTS       1A         TEXT('PO Status')
     A                                      COLHDG('PO Status')
     A            DVELDID       19S 0       TEXT('Load ID')
     A                                      COLHDG('Load ID')
     A                                      EDTCDE(J)
     A            DVELDSTS       1A         TEXT('Load Status')
     A                                      COLHDG('Load Status')
770aMA*           DVEDUEDT       6S 0       TEXT('Due Date')
770aAA            DVEDUEDT       8S 0       TEXT('Due Date')
     A                                      COLHDG('Due Date')
     A            DVEINITDT       Z         TEXT('Initial Scheduled Date')
     A                                      COLHDG('Initial Scheduled' +
     A                                      'Date' )
     A            DVESCHDT        Z         TEXT('Scheduled Date')
     A                                      COLHDG('Scheduled Date')
     A            DVELNDDT        Z         TEXT('Landed Date')
     A                                      COLHDG('Landed Date')
     A            DVELND         1          TEXT('Landed')
770bAA*
770bAA            DVESDEL        1          TEXT('Schedule Deleted')
770bAA                                      COLHDG('Schedule' 'Deleted')
     A            DVECHGTS        Z         TEXT('Last Change Timestamp')
     A                                      COLHDG('Last Change' +
     A                                      'Timestamp')
     A*
     A*  Add stamps
     A*
     A            DVEADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            DVEADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K DVEWHSE
     A          K DVEBATID
     A          K DVEPO
