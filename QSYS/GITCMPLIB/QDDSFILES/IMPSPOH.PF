     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSPOH  (PF)
     A*  Description ... Staging Import - P.O. Header
     A*
     A*  Revisions
     A*
640 AA*    02/12/11  DAS  6.40
     A*      - Created
     A*      - Based on IMPFPOH2 / IMPFOPHM
     A*      - Removed PIHCDT/PIHCTM fields. Instead, host is
     A*        responsible for writting records into this file in the
     A*        proper order, then the PIHADDTS field will keep them
     A*        in order.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Type (PIHTYP)
     A*
     A*      'A'  -  Add       Adds P.O. header.
     A*                        If P.O. exists, treat as type 'R'.
     A*
     A*      'C'  -  Change    Changes P.O. header.
     A*                        If P.O. does not exist, treat as 'A'.
     A*
     A*      'D'  -  Delete    Deletes P.O. header and detail.
     A*                        If any quantity has been received for
     A*                          the P.O. (Based on status code), the
     A*                          P.O. will NOT be deleted.
     A*
     A*      'R'  -  Replace   Replaces existing P.O.
     A*                        If P.O. does not exist, treat as 'A'.
     A*                        If P.O. does exist, the header and
     A*                          detail are deleted. Then, P.O. is
     A*                          added.
     A*                        If any quantity has been received for
     A*                          the P.O. (Based on status code), the
     A*                          P.O. will be ignored and NO changes
     A*                          will take place.
     A*
     A*----------------------------------------------------------------
     A          R PIHREC
     A*
     A            PIHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PIHBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A            PIHPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A*
     A*  Import information
     A*
     A            PIHTYP         1          TEXT('Update type')
     A                                      COLHDG('Update' 'Type')
     A*
     A*    Status info
     A*
     A            PIHSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            PIHSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIHSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIHTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            PIHVEN        11S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
     A            PIHVNA        40          TEXT('Vendor Name')
     A                                      COLHDG('Vendor' 'Name')
     A            PIHVA1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            PIHVA2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            PIHVCT        20          TEXT('City')
     A                                      COLHDG('City')
     A            PIHVST         2          TEXT('State')
     A                                      COLHDG('State')
     A            PIHVZP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            PIHDTE         8S 0       TEXT('Delivery Date')
     A                                      COLHDG('Delivery' 'Date')
     A            PIHTIM         4S 0       TEXT('Delivery Time')
     A                                      COLHDG('Delivery' 'Time')
     A*
     A            PIHMM1        40A         TEXT('Host Misc Field 1')
     A                                      COLHDG('Host' 'Misc' 'Field 1')
     A            PIHMM2        40A         TEXT('Host Misc Field 2')
     A                                      COLHDG('Host' 'Misc' 'Field 2')
     A            PIHMM3        40A         TEXT('Host Misc Field 3')
     A                                      COLHDG('Host' 'Misc' 'Field 3')
     A            PIHMM4        40A         TEXT('Host Misc Field 4')
     A                                      COLHDG('Host' 'Misc' 'Field 4')
     A            PIHMM5        40A         TEXT('Host Misc Field 5')
     A                                      COLHDG('Host' 'Misc' 'Field 5')
     A*
     A*  Add stamps
     A*
     A            PIHADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIHADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K PIHWHS
     A          K PIHBATID
     A          K PIHADDUC
     A          K PIHPO
