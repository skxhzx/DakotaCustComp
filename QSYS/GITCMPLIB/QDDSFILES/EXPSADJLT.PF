     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSADJLT (PF)
     A*  Description ... Staging Export - Adjustments Lots
     A*
     A*  Revisions
     A*
730 AA*    11/07/19  DAS  7.30
     A*      - Created
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file contains Host Lot information that corresponds to
     A*   the EXPSADJ file. There may be 1 or more records in this
     A*   file associated with an EXPSADJ record.
     A*
     A*   The total of the quantities of the associated records
     A*   in this file should equal the quantities in the corresponding
     A*   EXPSADJ record.
     A*
     A*   Typically, the Host Lot field will contain either the Dakota
     A*   License or the GS1 Serial Number from the Dakota UCI, the
     A*   latter being used with Unique UCI tracked items.
     A*
     A*   The following fields would only be used when working with
     A*   a UCI tracked item:
     A*
     A*     IELTUCI
     A*     IELTUCISEQ
     A*     IELTGTIN
     A*     IELTDATE
     A*     IELTDATECD
     A*     IELTDATENM
     A*     IELTLOT
     A*     IELTSERIAL
     A*
     A*   IELTLOT and IELTSERIAL are defined as 30 to match other files.
     A*
     A*   If a License lost its connection with its original Receiving
     A*   information, the Original License will be blank.
     A*
     A*   The PO information is associated with the Original License.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Date AI Codes
     A*
     A*    Code  Name  Description
     A*    ----  ----  -----------
     A*     11   PROD  Production date (YYMMDD)
     A*     12   DUE   Due date (YYMMDD)
     A*     13   PACK  Packaging date (YYMMDD)
     A*     15   BEST  Best before date (YYMMDD)
     A*     16   SELL  Sell by date (YYMMDD)
     A*     17   EXP   Expiration date (YYMMDD)
     A*
     A*----------------------------------------------------------------
     A          R IELTREC
     A*
     A            IELTWHS        3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IELTBATID     11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            IELTITEM      15          TEXT('Item')
     A                                      COLHDG('Item')
     A            IELTADATE      8S 0       TEXT('Adjustment Date (YYYYMMDD)')
     A                                      COLHDG('Adjustment' 'Date')
     A            IELTATIME      6S 0       TEXT('Adjustment Time')
     A                                      COLHDG('Adjustment' 'Time')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            IELTSTATUS     1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            IELTSTSTS       Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELTSTSUC       Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELTTEXT     200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            IELTHSTLOT    30          TEXT('Host Lot Code')
     A                                      COLHDG('Host Lot Code')
     A            IELTQTY1       5S 0       TEXT('Normal Qty')
     A                                      COLHDG('Normal' 'Qty')
     A            IELTQTY2       5S 0       TEXT('Brkdn1 Qty')
     A                                      COLHDG('Brkdn1' 'Qty')
     A            IELTQTY3       5S 0       TEXT('Brkdn2 Qty')
     A                                      COLHDG('Brkdn2' 'Qty')
     A            IELTWGT        7S 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            IELTBY        10          TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
     A            IELTADJCD      2          TEXT('Adjustment Code')
     A                                      COLHDG('Adjustment' 'Code')
     A            IELTMEMO      30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A            IELTWHDP       5          TEXT('Department')
     A                                      COLHDG('Department')
     A            IELTDISP      12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot' 'Display')
     A            IELTLCNS      15          TEXT('License')
     A                                      COLHDG('License')
     A            IELTOLCNS     15          TEXT('Original License')
     A                                      COLHDG('Original License')
     A            IELTPO         9          TEXT('PO')
     A                                      COLHDG('PO')
     A            IELTPOSEQ      5S 0       TEXT('PO Seq')
     A                                      COLHDG('PO Seq')
     A            IELTBRD       10A         TEXT('Brand')
     A                                      COLHDG('Brand')
     A            IELTSTAT       2A         TEXT('Slot Status')
     A                                      COLHDG('Slot' 'Status')
     A            IELTEXPD       8S 0       TEXT('Expiration Date')
     A                                      COLHDG('Expire' 'Date')
     A            IELTOCO       15A         TEXT('Origin Country')
     A                                      COLHDG('Origin Country')
     A            IELTOST       15A         TEXT('Origin State')
     A                                      COLHDG('Origin State')
     A            IELTUCI      100          TEXT('UCI')
     A                                      COLHDG('UCI')
     A            IELTUCISEQ     5S 0       TEXT('UCI Sequence Number')
     A                                      COLHDG('UCI Sequence' 'Number')
     A            IELTGTIN      14          TEXT('GTIN')
     A                                      COLHDG('GTIN')
     A            IELTDATE       6S 0       TEXT('Date YYMMDD')
     A                                      COLHDG('Date YYMMDD')
     A            IELTDATECD     2          TEXT('Date AI Code')
     A                                      COLHDG('Date' 'AI Code')
     A            IELTDATENM     4          TEXT('Date AI Name')
     A                                      COLHDG('Date' 'AI Name')
     A            IELTLOT       30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            IELTSERIAL    30          TEXT('Serial Nunber')
     A                                      COLHDG('Serial Number')
     A*
     A*  Add stamps
     A*
     A            IELTADDTS       Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELTADDUC       Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IELTWHS
     A          K IELTBATID
     A          K IELTITEM
     A          K IELTADATE
     A          K IELTATIME

