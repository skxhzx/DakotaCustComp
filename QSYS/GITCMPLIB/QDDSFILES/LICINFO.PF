     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICINFO  (PF)
     A*  Description ... License Information file
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*    11/22/02  MLB  4.17
     A*      - Created.
     A*
417aAA*    05/06/04  DAS  4.17a
     A*      - Added fields LNTYPE and LNCSEQ.
     A*      - Added ADD/CHG tracking fields.
     A*      - Added LNTYPE, LNCODE and LNCSEQ to key.
     A*      - Removed LNPO, LNSEQ and LNITEM. These will now be
     A*        stored as individual records.
     A*      - Removed LNVAL2 field.
     A*      - Renamed LNVAL1 to LNVALUE.
     A*
640 AA*    09/06/12  DAS  6.40
     A*      - MAJOR CHANGE
     A*      - Changed from a multi-records-per-license file into
     A*        a single-record-per-license file.
     A*
     A*----------------------------------------------------------------
     A*  LNSEQ - License Sequence
     A*
     A*   1 = Main information for license.
     A*       This would be the first information recorded for the license.
     A*       This is the record that DRILICINFO will use to get the
     A*       information associated with the license.
     A*
     A*   2+ = Secondary information for license.
     A*        This record would be created if a different piece of
     A*        information is received for the license, such as a
     A*        different Lot code or expiration date.
     A*        UCI and ASN Receiving will allow this situation.
     A*        This record will be benificial for recall lookups.
     A*
     A*----------------------------------------------------------------
     A*  LNRCVTYP  Receive Type
     A*
     A*   1 = Manual quantity (original method)
     A*   2 = Manual quantity with initial GS1 scan
     A*   3 = UCI using Mfg GS1 barcodes
     A*   4 = UCI using Internal GS1 barcodes (start with 99)
     A*   5 = ASN file
     A*
     A*----------------------------------------------------------------
     A*  LNSEQQTY  License/Seq# quantity
     A*
     A*   The LNRCVQTY is the quantity received for the license,
     A*   which is also the label quantity. Only a records with
     A*   LNSEQ = 1 will have this quantity. Records with LNSEQ = 2+
     A*   should have zero in this field.
     A*
     A*   The LNSEQQTY is the quantity attributed to that record.
     A*   In the case where there 2 Lot numbers were scanned to the
     A*   license, which would create a seq 1 and 2 record, this field
     A*   would contain the qty associated for the lot number in the rec.
     A*
     A*   Example: If Lot codes 123 and 124 were received on license 246
     A*   and there were 10 of lot 123 and 15 of lot 124, the seq 1
     A*   record would have LNRCVQTY = 25 and LNSEQQTY = 10, while
     A*   seq 2 record would have LNRCVQTY = 0 and LNSEQQTY = 15.
     A*
     A*----------------------------------------------------------------
     A*  LNCHG*    Change fields
     A*
     A*   When adding a record set the lnchg fields to the lnadd values.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R LNREC
     A*
     A            LNWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            LNLCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            LNSEQ          2S 0       TEXT('License Sequence')
     A                                      COLHDG('License' 'Seq')
     A            LNSEQQTY       5P 0       TEXT('License Seq Qty')
     A                                      COLHDG('License' 'Seq Qty')
     A            LNITEM    R               REFFLD(ITITEM)
     A            LNPO      R               REFFLD(PONUM)
     A            LNPOSEQ   R               REFFLD(SEQ#)
     A                                      TEXT('PO Seq')
     A                                      COLHDG('PO' 'Seq')
     A            LNRCVTYP       1          TEXT('Receive Type')
     A                                      COLHDG('Receive' 'Type')
     A            LNRCVTRN#      7P 0       TEXT('Receive Tran#')
     A                                      COLHDG('Receive' 'Tran#')
     A            LNRCVLBL#      7P 0       TEXT('Receive Lbl#')
     A                                      COLHDG('Receive' 'Lbl#')
     A            LNRCVQTY       5P 0       TEXT('Receive Lbl Qty')
     A                                      COLHDG('Receive' 'Lbl Qty')
     A            LNENTDATE      8S 0       TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A            LNENTTYPE      8          TEXT('Date Type Entered')
     A                                      COLHDG('Date Type' 'Entered')
     A            LNMFGDATE      8S 0       TEXT('Mfg Date')
     A                                      COLHDG('Mfg' 'Date')
     A            LNMFGEXP       8S 0       TEXT('Mfg Exp Date')
     A                                      COLHDG('Mfg Exp' 'Date')
     A            LNFIFODATE     8S 0       TEXT('Move-In FIFO Date')
     A                                      COLHDG('Move-In' 'FIFO Date')
     A            LNEXPDATE      8S 0       TEXT('Dakota Exp Date')
     A                                      COLHDG('Dakota' 'Exp Date')
     A            LNDATEFLAG     1          TEXT('Date Flag')
     A                                      COLHDG('Date' 'Flag')
     A            LNMFGFLAG      1          TEXT('Mfg Date Flag')
     A                                      COLHDG('Mfg Date' 'Flag')
     A            LNMFGDAYS      3P 0       TEXT('Mfg Days')
     A                                      COLHDG('Mfg' 'Days')
     A            LNUSRDAYS      3P 0       TEXT('User Days')
     A                                      COLHDG('User' 'Days')
     A            LNDSTDAYS      3P 0       TEXT('Dist Days')
     A                                      COLHDG('Dist' 'Days')
     A            LNMFGCODE     15          TEXT('Mfg Date Code')
     A                                      COLHDG('Mfg' 'Date Code')
     A            LNTTLLBS       9P 2       TEXT('Total Pounds')
     A                                      COLHDG('Total' 'Pounds')
     A            LNAVGLBS       9P 2       TEXT('Average Pounds')
     A                                      COLHDG('Average' 'Pounds')
     A            LNLOT         30          TEXT('Lot Code')
     A                                      COLHDG('Lot Code')
     A            LNLOTPTI2      2          TEXT('Lot PTI 2')
     A                                      COLHDG('PTI2')
     A            LNLOTPTI4      4          TEXT('Lot PTI 4')
     A                                      COLHDG('PTI4')
     A            LNHRVCERT     20          TEXT('Harvest Cert')
     A                                      COLHDG('Harvest' 'Cert')
     A            LNHRVDATE      8S 0       TEXT('Harvest Date')
     A                                      COLHDG('Harvest' 'Date')
     A            LNHRVLOC      20          TEXT('Harvest Location')
     A                                      COLHDG('Harvest' 'Location')
     A            LNBRAND       10          TEXT('Brand')
     A                                      COLHDG('Brand')
     A*
     A* Timestamp / User tracking information.
     A*
     A            LNADDTS   R               REFFLD(ADDTS)
     A            LNADDUC   R               REFFLD(ADDUC)
     A            LNADDPGM  R               REFFLD(ADDPGM)
     A            LNADDEMP  R               REFFLD(ADDEMP)
     A            LNADDUSR  R               REFFLD(ADDUSR)
     A            LNADDJOB  R               REFFLD(ADDJOB)
     A            LNADDNBR  R               REFFLD(ADDNBR)
     A*
     A            LNCHGTS   R               REFFLD(CHGTS)
     A            LNCHGUC   R               REFFLD(CHGUC)
     A            LNCHGPGM  R               REFFLD(CHGPGM)
     A            LNCHGEMP  R               REFFLD(CHGEMP)
     A            LNCHGUSR  R               REFFLD(CHGUSR)
     A            LNCHGJOB  R               REFFLD(CHGJOB)
     A            LNCHGNBR  R               REFFLD(CHGNBR)
     A*
     A*
     A          K LNWHSE
     A          K LNLCNS
     A          K LNSEQ
