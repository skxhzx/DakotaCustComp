     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSITMN (PF)
     A*  Description ... Staging Import - Items
     A*
     A*  Revisions
     A*
640 AA*    12/21/10  DAS  6.40
     A*      - Created
     A*      - Based on IMPFITM4 / IMPFITMM
     A*
640aAA*    02/10/11  DAS  6.40a
     A*      - Renamed IUVEN# to IUVEN
     A*      - Renamed IUVIT# to IUVITM
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Update type (IUUTYP)
     A*      'A'  -  Add
     A*      'C'  -  Change
     A*      'D'  -  Delete
     A*
     A*----------------------------------------------------------------
     A          R IUREC
     A*
     A            IUWHSE         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            IUBATID       11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A*
     A*  Import information
     A*
     A            IUUTYP         1          TEXT('Update type')
     A                                      COLHDG('Update' 'Type')
     A*
     A*    Status info
     A*
     A            IUSTATUS       1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            IUSTSTS         Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IUSTSUC         Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IUTEXT       200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            IUITEM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            IUTYPE         1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            IUDESC        80          TEXT('Description')
     A                                      COLHDG('Description')
     A            IUWHDP         5          TEXT('Warehouse Dept')
     A                                      COLHDG('Warehouse' 'Dept')
     A            IUSTYP         1          TEXT('Section Type')
     A                                      COLHDG('Section' 'Type')
     A            IUDESG         5          TEXT('Designation')
     A                                      COLHDG('Designation')
     A            IUACTV         2          TEXT('Activity')
     A                                      COLHDG('Activity')
     A            IUBLD          2          TEXT('Build Code')
     A                                      COLHDG('Build' 'Code')
640aMA            IUVEN         10S 0       TEXT('Vendor Number')
     A                                      COLHDG('Vendor' 'Number')
640aMA            IUVITM        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            IUPDSC        15          TEXT('Pack Description')
     A                                      COLHDG('Pack' 'Description')
     A            IUUM1          2          TEXT('Normal UM')
     A                                      COLHDG('Normal' 'UM')
     A            IUUM2          2          TEXT('Breakdown 1 UM')
     A                                      COLHDG('Break 1' 'UM')
     A            IUUMQ2         3S 0       TEXT('Breakdown 1 UM Qty')
     A                                      COLHDG('Breakdown 1' 'UM Qty')
     A            IUFLG1         1          TEXT('Allow Brk1 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk1 Sale')
     A            IUUM3          2          TEXT('Breakdown 2 UM')
     A                                      COLHDG('Break 2' 'UM')
     A            IUUMQ3         3S 0       TEXT('Breakdown 2 UM Qty')
     A                                      COLHDG('Breakdown 2' 'UM Qty')
     A            IUFLG2         1          TEXT('Allow Brk2 Sale (Y/N)')
     A                                      COLHDG('Allow' 'Brk2 Sale')
     A*
     A            IUULEN         5S 2       TEXT('Unit Length')
     A                                      COLHDG('Unit' 'Length')
     A            IUUWDT         5S 2       TEXT('Unit Width')
     A                                      COLHDG('Unit' 'Width')
     A            IUUHGT         5S 2       TEXT('Unit Height')
     A                                      COLHDG('Unit' 'Height')
     A            IUCUBE         7S 3       TEXT('Cube/Unit')
     A                                      COLHDG('Cube/Unit')
     A            IUSWGT         7S 2       TEXT('Shipping Weight')
     A                                      COLHDG('Shipping' 'Weight')
     A            IUCWGT         1          TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            IUREPL         5S 0       TEXT('Replenish Level')
     A                                      COLHDG('Replenish' 'Level')
     A            IUBLNK         5S 0       TEXT('Blank')
     A            IUFLGD         1          TEXT('Date Sensitive (Y/N)')
     A                                      COLHDG('Date' 'Sensitive')
     A            IUFLGB         1          TEXT('Allow Bulk Pick (Y/N)')
     A                                      COLHDG('Allow' 'Bulk Pick')
     A            IUSLIF         3S 0       TEXT('Shelf Life Days')
     A                                      COLHDG('Shelf Life' 'Days')
     A            IUNDAY         3S 0       TEXT('Notice Days')
     A                                      COLHDG('Notice' 'Days')
     A            IUTIE          4S 0       TEXT('Mfr Tie')
     A                                      COLHDG('Mfr' 'Tie')
     A            IUHIGH         2S 0       TEXT('Mfr High')
     A                                      COLHDG('Mfr' 'High')
     A            IUMFLG         1          TEXT('Manufacturing date (Y,N)')
     A                                      COLHDG('Manufacture' 'date flag')
     A            IUDDAY         3S 0       TEXT('Distributor days')
     A                                      COLHDG('Distributor' 'Days')
     A            IUUDAY         3S 0       TEXT('End user days')
     A                                      COLHDG('End User' 'Days')
     A            IUMDAY         3S 0       TEXT('Mfg Shelf Life Days')
     A                                      COLHDG('Mfg Shelf' 'Life Days')
     A            IUMITM        15          TEXT('Master Item Number')
     A                                      COLHDG('Master' 'Item' 'Number')
     A            IUCITM        15          TEXT('Contract Base Item Number')
     A                                      COLHDG('Contact' 'Base' 'Item #')
     A            IUNSLT         1A         TEXT('Non-slotted item (Y,N)')
     A                                      COLHDG('Non-Slotted' 'Item')
     A            IUXDCK         1A         TEXT('Crossdock item (Y,N)')
     A                                      COLHDG('Crossdock' 'Item')
     A            IUBNBR         3S 0       TEXT('Buyer number')
     A                                      COLHDG('Buyer' 'number')
     A            IUBNAM        40A         TEXT('Buyer name')
     A                                      COLHDG('Buyer name')
     A            IUVNAM        40A         TEXT('Vendor name')
     A                                      COLHDG('Vendor name')
     A            IUBRND        10A         TEXT('Brand')
     A                                      COLHDG('Brand')
     A            IULOT          1A         TEXT('Lot code item (Y/N)')
     A                                      COLHDG('Lot code' 'item')
     A            IULTRK         1A         TEXT('Track by License (Y/N)')
     A                                      COLHDG('Track by' 'license')
     A            IUODAY         3S 0       TEXT('# of Days Out')
     A                                      COLHDG('# of Day Out')
     A            IURG1C         3S 1       TEXT('Item CW low variance %')
     A                                      COLHDG('Item CW ' 'Low Var %')
     A            IURG2C         3S 1       TEXT('Item CW high variance%')
     A                                      COLHDG('Item CW ' 'High Var %')
     A            IUPQN1         3S 0       TEXT('Repack Parent Normal Qty')
     A                                      COLHDG('Repack Parent' 'Nrm Qty')
     A            IUPQN2         3S 0       TEXT('Repack Parent Brkdn1 Qty')
     A                                      COLHDG('Repack Parent' 'Bk1 Qty')
     A            IUPQN3         3S 0       TEXT('Repack Parent Brkdn2 Qty')
     A                                      COLHDG('Repack Parent' 'Bk2 Qty')
     A*
     A            IMPGRP         5A         TEXT('Material Group')
     A                                      COLHDG('Material' 'Group')
     A            IMPPNT         4A         TEXT('Plant')
     A                                      COLHDG(' ' ' ' 'Plant')
     A            IMPVAR         5S 2       TEXT('Receiving Qty Variance')
     A                                      COLHDG('Receiving' 'Qty' 'Variance')
     A            IMPCAT        10A         TEXT('Item Category')
     A                                      COLHDG('Item' 'Category')
     A            IMPMS1        40A         TEXT('Misc User Field 1')
     A                                      COLHDG('Misc User' 'Field 1')
     A            IMPMS2        40A         TEXT('Misc User Field 2')
     A                                      COLHDG('Misc User' 'Field 2')
     A            IMPMS3        40A         TEXT('Misc User Field 3')
     A                                      COLHDG('Misc User' 'Field 3')
     A            IMPMS4        40A         TEXT('Misc User Field 4')
     A                                      COLHDG('Misc User' 'Field 4')
     A            IMPMS5        40A         TEXT('Misc User Field 5')
     A                                      COLHDG('Misc User' 'Field 5')
     A*
     A*  Add stamps
     A*
     A            IUADDTS         Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IUADDUC         Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IUWHSE
     A          K IUBATID
     A          K IUADDUC
     A          K IUITEM
