     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EQUIPYS (PF)
     A*  Description ... Equipment Yard Status table
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
417 AA*   01/26/04  MLB  4.17
     A*     - Created.
417bAA*   04/29/04  RH   4.17b
     A*     - Equipment ID code increased from 6 to 8
     A*
     A*----------------------------------------------------------------
     A*  Codes
     A*
     A*    Status codes (ESSTAT)
     A*
     A*       '0'  -  In the yard
     A*       '2'  -  At the dock
     A*       '4'  -  Loaded
     A*       '6'  -  Out of the yard
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R ESREC
     A*
     A            ESWHSE    R               REFFLD(WHCODE PIRDICT)
     A            ESCODE    R               REFFLD(EQCODE PIRDICT)
     A            ESIO           1A         TEXT('In/out')
     A                                      COLHDG('In/Out')
     A            ESLOC          2A         TEXT('Equipment Location')
     A                                      COLHDG('Equipment' 'Location')
     A            ESSTAT         2A         TEXT('Yard Status')
     A                                      COLHDG('Yard' 'Status')
     A            ESDOOR    R     P         REFFLD(DDDOOR DOCDOOR)
     A            ESRTE     R               REFFLD(RHRTE  RTEHED)
     A            ESRTID    R               REFFLD(RHRTID RTEHED)
     A            ESEMP#    R     P         REFFLD(PTEMP# PIRTRAN)
     A            ESUSER    R               REFFLD(USCODE PIRUSER)
     A            ESSDTS          Z         TEXT('Status Date Stamp')
     A                                      COLHDG('Status' 'Date Stamp')
     A*
     A          K ESWHSE
     A          K ESCODE
