     A*----------------------------------------------------------------
     A*   Copyright (C) 2000 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFPLBL  (PF)
     A*  Description ... FTP Pick label export file
     A*  Date .......... August 28, 2000
     A*  Creator ....... DAS
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFPLBL.FILE')
     A*
     A*  Revisions
     A*
414 AA*    Rev 4.14  08/29/00  DAS
     A*      - Created
     A*
414aAA*    Rev 4.14a 09/13/00  DAS
     A*      - Added LBECWF field (catchweight flag).
     A*
720a A*    03/28/18  RTR  7.20a
     A*      - Added LBEPLS field (Pallet Section).
     A*
     A*----------------------------------------------------------------
     A          R LBEREC
     A*
     A* Main key fields
     A*
     A            LBEWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            LBERID         5          TEXT('FS-WMS Route Id')
     A                                      COLHDG('FS-WMS' 'Route Id')
     A            LBESEQ         7S 0       TEXT('Print Seq#')
     A                                      COLHDG('Print' 'Seq#')
     A*
     A* Secondary key fields
     A*
     A            LBEZON         6          TEXT('Pick Zone')
     A                                      COLHDG('Pick' 'Zone')
     A            LBETYP         1          TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            LBETR#         7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            LBEWHD         5          TEXT('Warehouse Department')
     A                                      COLHDG('Warehouse' 'Department')
     A            LBESLT        12          TEXT('Slot')
     A                                      COLHDG('Slot')
     A*
     A* Item information fields
     A*
     A            LBEITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A            LBEITP         1          TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            LBEDSC        30          TEXT('Item Description')
     A                                      COLHDG('Item' 'Description')
     A            LBEPAK        15          TEXT('Pack Description')
     A                                      COLHDG('Pack' 'Description')
     A            LBEUTP         1          TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            LBEUCD         2          TEXT('UM Code')
     A                                      COLHDG('UM' 'Code')
     A            LBECUB         9S 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            LBEWGT         9S 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            LBEQTY         5S 0       TEXT('Quantity Picked')
     A                                      COLHDG('Quantity' 'Picked')
     A            LBECWF         1          TEXT('Catchweight Flag')
     A                                      COLHDG('Catchweight' 'Flag')
     A*
     A* Order information fields
     A*
     A            LBERTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            LBEORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            LBEDSQ         3S 0       TEXT('Order Detail Seq#')
     A                                      COLHDG('Order Detail' 'Seq#')
     A            LBECUS        11S 0       TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            LBESTP         2S 0       TEXT('Stop')
     A                                      COLHDG('Stop')
     A*
     A* Misc information fields
     A*
     A            LBEZTP         5          TEXT('Pick Zone Type')
     A                                      COLHDG('Pick Zone' 'Type')
     A            LBEMZN         6          TEXT('Merge Zone')
     A                                      COLHDG('Merge' 'Zone')
     A            LBEOUT         3          TEXT('Out Label Flag')
     A                                      COLHDG('Out Label' 'Flag')
     A            LBECOM         1          TEXT('Truck Compartment Code')
     A                                      COLHDG('Truck' 'Compartment Code')
     A            LBEPL#         7S 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            LBELB#         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            LBEDTE         8S 0       TEXT('Date (YYYYMMDD)')
     A                                      COLHDG('Date')
     A            LBETIM         6S 0       TEXT('Time (HHMMSS)')
     A                                      COLHDG('Time')
720a A            LBEPLS         2A         TEXT('Pallet Section')
720a A                                      COLHDG('Sec')
     A*
     A          K LBEWHS
     A          K LBERID
     A          K LBESEQ
