     A*----------------------------------------------------------------***
     A*   Copyright (C) 2001 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (708) 790-8383                                                 *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... LOGPRTYRPL (PF)
     A*  Description ... Log file for DRIPRTYRPL
     A*  Date .......... 30 October 2001
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
416 AA*    10/30/01  DAS  4.16
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    File should only be used with LE programs because field
     A*    names are longer than 6-characters.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A          R LGPRREC
     A*
     A            LGPRWHSE  R               REFFLD(WHCODE)
     A            LGPRITEM  R               REFFLD(ITITEM)
     A            LGPRDATE  R               TEXT('Date')
     A                                      COLHDG('Date')
     A                                      REFFLD(DTECMD)
     A            LGPRTIME  R               TEXT('Time')
     A                                      COLHDG('Time')
     A                                      REFFLD(TIMHMS)
     A            LGPRSCMD      10          TEXT('Subcommand')
     A                                      COLHDG('Subcommand')
     A            LGPRPGM       10          TEXT('Calling Program')
     A                                      COLHDG('Calling' 'Program')
     A            LGPRCRT        1          TEXT('Created')
     A                                      COLHDG('Created')
     A            LGPRHCNT       3P 0       TEXT('Home Count')
     A                                      COLHDG('Home' 'Count')
     A            LGPRHWHD  R               REFFLD(WDCODE)
     A                                      TEXT('Home Dept')
     A                                      COLHDG('Home' 'Dept')
     A            LGPRHSLT      12          TEXT('Home Slot')
     A                                      COLHDG('Home' 'Slot')
     A            LGPRHAVL       7P 0       TEXT('Home Available')
     A                                      COLHDG('Home' 'Available')
     A            LGPROWHD  R               REFFLD(WDCODE)
     A                                      TEXT('Overflow Dept')
     A                                      COLHDG('Overflow' 'Dept')
     A            LGPROSLT      12          TEXT('Overflow Slot')
     A                                      COLHDG('Overflow' 'Slot')
     A            LGPROAVL       7P 0       TEXT('Overflow Avail')
     A                                      COLHDG('Overflow' 'Avail')
     A            LGPRQTYN       7P 0       TEXT('Qty Needed')
     A                                      COLHDG('Qty' 'Needed')
     A            LGPRQTYW       7P 0       TEXT('Qty Warehouse')
     A                                      COLHDG('Qty' 'Warehouse')
     A            LGPRFILL       1          TEXT('Mult Flag')
     A                                      COLHDG('Mult' 'Flag')
     A            LGPRRTN       10          TEXT('Return Code')
     A                                      COLHDG('Return' 'Code')
     A            LGPRMSG       60          TEXT('Message')
     A                                      COLHDG('Message')
     A            LGPRDATA      70          TEXT('Parm Data')
     A                                      COLHDG('Parm Data')
