     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFRTN(PF)
     A*  Description ... FTP Customer returns export file
     A*  Date .......... March 3, 1999
     A*  Creator ....... DAS
     A*
     A*  This file should be identical to EXPRTN except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPFRTN.FILE')
     A*
     A*  Revisions
     A*
213 AA*    Ver 2.01  05/16/96  DAS
213 AA*      - Added fields OECWTR, OETYPE, OETRN# and OECITM.
213 AA*      - Removed UNIQUE keyword.
416aAA*    Ver 4.16a 10/19/01  RH
     A*      - Added fields ORDCOD, ORIINV, OROLB#, ORINV#
416bAA*    Ver 4.16a 08/29/02  RH
     A*      - Change OROLB# to signed not packed
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    The original transaction number (ORTRN#) ties a return
     A*    back to a specific employee when EPM is being used.
     A*
     A*    The corresponding item number (OECITM) was added to help
     A*    track which item was actually mispicked.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Return types (OETYPE)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
213 DA*                                     UNIQUE
     A          R OEREC
     A*
     A            OEWHSE    R               REFFLD(WHCODE)
     A            OEORD          7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEITEM    R               REFFLD(ITITEM)
     A            OEQTY1         5S 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            OEQTY2         5S 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            OEQTY3         5S 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
213 AA            OECWTR         9S 2       TEXT('Catchweight Returned')
213 AA                                      COLHDG('Catchweight' 'Returned')
     A            OERDTE    R               TEXT('Return Date (YYYYMMDD)')
     A                                      COLHDG('Return' 'Date')
     A                                      REFFLD(DTECMD)
     A            OERTIM    R               TEXT('Return Time')
     A                                      COLHDG('Receive' 'Time')
     A                                      REFFLD(TIMHMS)
     A            OERUSR        10          TEXT('Returned By')
     A                                      COLHDG('Returned' 'By')
213 AA            OETYPE         1          TEXT('Return Type')
213 AA                                      COLHDG('Return' 'Type')
213 AA            OETRN#         7S 0       TEXT('Original Tran#')
213 AA                                      COLHDG('Original' 'Tran#')
213 AA            OECITM    R               TEXT('Corresponding Item')
213 AA                                      COLHDG('Corresponding' 'Item')
213 AA                                      REFFLD(ITITEM)
     A            OERCOD    R               REFFLD(RNCODE)
416aAA            OEDCOD    R               REFFLD(RDCODE)
416aAA            OEIINV         1          TEXT('Impact Inventory')
416aAA                                      COLHDG('Impact' 'Inv.')
416bMA            OEOLB#         7S 0       TEXT('Original Label')
416aAA                                      COLHDG('Orig.' 'Label#' )
416bDA**                                    REFFLD(TRAN#)
416aAA            OEINV#        12A         TEXT('Orig. Invoice#')
416aAA                                      COLHDG('Orig.' 'Invoice#')
     A            OERMEM        30          TEXT('Memo')
     A                                      COLHDG('Memo')
     A*
     A          K OEWHSE
     A          K OEORD
     A          K OEITEM
     A          K OERDTE
     A          K OERTIM
