     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ZONED (PF)
     A*  Description ... Zone detail file.
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    06/11/97  DAS
     A*      - Created.
     A*
520aAA*    02/27/09  DAS  P#00150
     A*      - Added Deignation, category, uom type, side
     A*        and handstack range.
     A*
720aAA*    10/14/18  DAS  7.20a
     A*      - Added Activity Zone, Aisle range, Level range
     A*        and Item Class Code
     A*
720bAA*    10/25/18  DAS  7.20b
     A*      - Added slot type (pick, overflow), Customer code
     A*
     A*----------------------------------------------------------------
     A*  Notes
     A*
     A*   - Department code is manditory.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ZDREC
     A*
     A            ZDWHSE    R               REFFLD(WHCODE)
     A            ZDTRUK    R               REFFLD(TMTRUK)
     A            ZDZONE    R               REFFLD(ZHZONE)
     A            ZDWHDP    R               REFFLD(WDCODE)
     A            ZDSTYP    R               REFFLD(SLSTYP)
     A            ZDAISL    R               REFFLD(ASCODE)
     A            ZDRLVL    R               REFFLD(SLRLVL)
     A            ZDLOC1    R               REFFLD(SLLOC)
     A                                      TEXT('From location')
     A                                      COLHDG('From' 'Location')
     A            ZDLOC2    R               REFFLD(SLLOC)
     A                                      TEXT('To location')
     A                                      COLHDG('To' 'Location')
520aAA            ZDDESG    R               REFFLD(SLDESG SLOT)
520aAA            ZDCAT     R               REFFLD(ICCODE ITEMCAT)
520aAA            ZDHND1    R               REFFLD(SLHAND SLOT)
520aAA            ZDHND2    R               REFFLD(SLHAND SLOT)
520aAA            ZDUTYP    R               REFFLD(LBUTYP LABEL)
520aAA            ZDSIDE         1A         TEXT('Side (O,E)')
520aAA                                      COLHDG('Side' '(O,E)')
720aAA            ZDACTV    R               REFFLD(SLACTV SLOT)
720aAA            ZDAISL2   R               REFFLD(ASCODE)
720aAA                                      TEXT('To aisle')
720aAA                                      COLHDG('To' 'aisle')
720aAA            ZDRLVL2   R               REFFLD(SLRLVL)
720aAA                                      TEXT('To level')
720aAA                                      COLHDG('To' 'Level')
720aAA            ZDCLCDE       10          TEXT('Class Code')
720aAA                                      COLHDG('Class' 'Code')
720bAA            ZDPCKOVF       1          TEXT('Slot Type (P,O)')
720bAA                                      COLHDG('Slot Type' '(P,O)')
720bAA            ZDCSTCOD      20          TEXT('Customer Code')
720bAA                                      COLHDG('Customer Code')
