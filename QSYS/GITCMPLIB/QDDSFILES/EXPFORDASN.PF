     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPFORDASN  (PF)
     A*  Description ... FTP Order detail ASN SSCC-18 export file
     A*  Date .......... Feb 18, 2008
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
520 AA*    02/18/08  RBD  5.20  P#00011
     A*      - Created.
520aAA*    10/06/08  RBD  5.20a P#00011
     A*      - Fix:  Change OEATOT field size to 3,0.
520bAA*    06/30/09  RBD  5.20b P#00218
     A*      - Output customer order# to OEAORD instead of BFC order#.
     A*      - Add host line seq#.
     A*      - Add qty shipped.
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*    - The OEAUS# is unique, and once assigned, it must not be
     A*      reused within the next three years, per Walgreens.
     A*
     A*    - The OEALTY label type may be : O = Normal
     A*                                     B = Bulk
     A*
     A*    - The OEAUMT uom type may be : N = Normal
     A*                                   1 = Breakdown 1
     A*                                   2 = Breakdown 2
     A*
520bAA*    - The OEAHLN host line seq# is supplied by Harbor
     A*      in ORDD/ODOR2.
     A*
520bAA*    - The OEAQTY qty shipped will be 1 for non-tote items
     A*      and qty picked for tote items.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OEAREC
     A*
     A            OEAWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OEAORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEASQ#         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OEAITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OEAUS#        18S 0       TEXT('Unique Piece #')
     A                                      COLHDG('Unique' 'Piece #')
     A            OEALBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            OEALTY         1A         TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            OEAUMT         1A         TEXT('UOM Type')
     A                                      COLHDG('UOM' 'Type')
     A            OEATRN         7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
520aDA**          OEATOT         2S 0       TEXT('Tote Number')
520aMA            OEATOT         3S 0       TEXT('Tote Number')
     A                                      COLHDG('Tote' 'Number')
520bAA            OEAHLN         5S 0       TEXT('Host Line Seq Number')
520bAA                                      COLHDG('Host Line' 'Seq Number')
520bAA            OEAQTY         5S 0       TEXT('Qty Shipped')
520bAA                                      COLHDG('Qty' 'Shipped')
     A*
     A          K OEAWHS
     A          K OEAORD
     A          K OEATOT
     A          K OEAUS#
     A          K OEASQ#
