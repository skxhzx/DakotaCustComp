     A*----------------------------------------------------------------
     A*   Copyright (C) 2015 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PCOSTWOL
     A*  Description ... PRODUCTION COST SHEET - LABOR
     A*  Creator ....... KDE
     A*
     A*  --------------------------------------------------------------
     A*   SPECIAL NOTES:
     A*
     A*     LABOR COST LINES ARE DEFINED IN FILE PLABCSTLNE.  FIELDS ARE
     A*     BROUGHT INTO THIS RECORD FOR CALCULATION PURPOSES AND TO
     A*     EXPLAIN HOW COST ASSIGNED VALUE WAS DERIVED.
     A*
     A*     WILL FIRST DETERMINE IF COST LINE APPLIES FOR DAY OF WEEK BEING
     A*     CLOSED.  SEVEN YES NO FLAGS EXIST IN FIELD CSLCSDYWK TO
     A*     DETERMINE IF WORK DAY.
     A*
     A*     TOTAL COST FOR DAY FOR THIS LABOR LINE IS CALCULATED BY EXTENDING
     A*     CSLEMPCNT * CSMSTDRATE * CSLHRDAY.
     A*
     A*     UPDATE CSLWOQTY (TOTAL QTY ALL WORK ORDERS FOR DAY FOR LINE)
     A*     WITH TOTAL WORK ORDER QUANTITY PRODUCED FOR DAY AND LINE.
     A*     CSFGQTY (QTY PRODUCED FOR THIS WORK ORDER) WILL BE UPDATED.
     A*
     A*     COST ASSIGNED (CSLCSTASGN) WILL BE CALCULATED BY
     A*     MULTIPLYING TOTAL COST FOR DAY  (CALCULATED SEVERAL LINES UP)
     A*     BY THE FACTOR OF (CSFGQTY / CSLWOQTY).  THIS WILL ASSIGN A
     A*     PORTION OF THE TOTAL LINE LABOR COST BASED ON QUANTITY
     A*     PRODUCED ON THIS WORK ORDER COMPARED TO TOTAL QUANTITY PRODUCED
     A*     FOR ALL WORK ORDERS THAT WERE PROCESSED USING THIS LINE ON
     A*     THIS DAY
     A*
     A*  Revisions
     A*
710  A*    Ver 7.10  04/20/17  KDE
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A          R CSLREC
     A            CSLWHSE        3P 0       COLHDG('WAREHOUSE')
     A            CSLPRDDATE     8P 0       COLHDG('PROD DATE')
     A            CSLLINE        3P 0       COLHDG('MFG LINE')
     A            CSLWO         13          TEXT('WORK ORDER')
     A                                      COLHDG('WORK ORDER')
     A            CSLTYPE        1          TEXT('COST TYPE - L')
     A                                      COLHDG('CST TYPE')
     A            CSLFGITM      15          TEXT('FG ITEM')
     A                                      COLHDG('FG ITEM')
     A* ---------------------------------------------------------------
     A* ---  CSLSEQ HAS EMPLOYEE NUMBER AS STARTING POINT.  IF AN
     A* ---  ADDITIONAL LINE IS REQUIRED THAT WOULD OTHERWISE CAUSE
     A* ---  A DUPLICATE KEY WILL WILL USE DECIMAL PLACES FROM 1-999
     A* ---  TO PREVENT THE DUPLICATE
     A* ---------------------------------------------------------------
     A            CSLSEQ         8P 3       TEXT('SEQ# COST LINE')
     A                                      COLHDG('SEQ# COST LINE')
     A            CSLDESC       40          TEXT('DESC. LABOR COST LINE')
     A                                      COLHDG('DESC. LAB CST LNE')
     A            CSLEMPCNT      4P 0       TEXT('NUM EMPLOYEES')
     A                                      COLHDG('NUM EMPLOYEES')
     A            CSLSTDRATE     7P 3       TEXT('STD LABOR RATE')
     A                                      COLHDG('STD LAB RATE')
     A            CSLSECS       11P 4       TEXT('NUM SECS')
     A                                      COLHDG('NUM SECS')
     A            CSLCSDTWK      7          TEXT('YES/NO WORK DAYS SUN-SAT')
     A                                      COLHDG('WORK DAYS')
     A            CSLTASK        6          TEXT('TASK CODE')
     A                                      COLHDG('TASK CODE')
     A            CSLFGQTY       5P 0       TEXT('WO QTY PROD')
     A                                      COLHDG('WO QTY PROD')
     A            CSLWOQTY       6P 0       TEXT('TOT QTY LINE/DAY')
     A                                      COLHDG('TOT QTY LINE/DAY')
     A            CSLCSTASGN     9P 4       TEXT('COST ASSIGNED')
     A                                      COLHDG('COST ASSIGNED')
     A            CSLSYSTS        Z         TEXT('SYS TIMESTAMP')
     A                                      COLHDG('SYS TIMESTAMP')
     A            CSLLOCTS        Z         TEXT('LOC TIMESTAMP')
     A                                      COLHDG('LOC TIMESTAMP')
     A            CSLUCTS         Z         TEXT('UC TIMESTAMP')
     A                                      COLHDG('UC TIMESTAMP')
     A            CSLJOB        10          TEXT('JOB')
     A                                      COLHDG('JOB')
     A            CSLUSER       10          TEXT('USER')
     A                                      COLHDG('USER')
     A            CSLJOBNBR      6          TEXT('JOB NUMBER')
     A                                      COLHDG('JOB NUMBER')
     A          K CSLWHSE
     A          K CSLPRDDATE
     A          K CSLLINE
     A          K CSLWO
     A          K CSLTYPE
     A          K CSLFGITM
     A          K CSLSEQ
