     A*----------------------------------------------------------------
     A*   Copyright (C) 1999 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPFORDH  (PF)
     A*  Description ... FTP Customer order import file - Header
     A*  Creator ....... DAS
     A*
     A*  This file should be identical to IMPORDH except that the
     A*  numeric fields should be defined as Signed, not Packed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/IMPFORDH.FILE')
     A*
     A*  Revisions
     A*
     A*    Rev 2.00  07/29/94  DAS
     A*      - Added warehouse and order type fields.
     A*      - Made zip code 10 characters and removed Zip+4.
     A*      - Removed client batch number.
     A*
     A*    Rev 2.01  08/17/94  DAS
     A*      - Changed numbers to packed.
     A*      - Added misc. info field.
     A*      - Increased customer number to 11 digits from 10.
     A*
213 AA*    Rev 2.02  05/15/96  DAS
213 AA*      - Customer order number no longer has to be unique.
213 AA*        OIHORD will be written to OHCORD in file ORDH.
213 AA*      - File layout did not change.
     A*
415 AA*    Rev 4.15  07/11/01  DAS
     A*      - Removed UNIQUE keyword.
     A*
     A*----------------------------------------------------------------
     A*
     A*  FTP Notes
     A*
     A*   - Incoming file must be in fixed field length format which
     A*     matches this layout exactly.
     A*   - Decimal point and sign must be excluded from numeric fields
     A*      (ie. '12345', not ' 123.45')
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Order types (OIHTYP)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
415 DA**                                    UNIQUE
     A          R OIHREC
     A*
     A            OIHWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OIHORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OIHTYP         1          TEXT('Order Type')
     A                                      COLHDG('Order' 'Type')
     A            OIHCUS        11S 0       TEXT('Customer Number')
     A                                      COLHDG('Customer' 'Number')
     A            OIHCPO        15          TEXT('Customer P.O. Number')
     A                                      COLHDG('Customer' 'P.O. Number')
     A            OIHCNM        40          TEXT('Customer Name')
     A                                      COLHDG('Customer' 'Name')
     A            OIHCA1        40          TEXT('Address 1')
     A                                      COLHDG('Address 1')
     A            OIHCA2        40          TEXT('Address 2')
     A                                      COLHDG('Address 2')
     A            OIHCCT        20          TEXT('City')
     A                                      COLHDG('City')
     A            OIHCST         2          TEXT('State')
     A                                      COLHDG('State')
     A            OIHCZP        10          TEXT('Zip')
     A                                      COLHDG('Zip')
     A            OIHRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            OIHSTP         2S 0       TEXT('Stop')
     A                                      COLHDG('Stop')
     A            OIHTRK        10          TEXT('Truck')
     A                                      COLHDG('Truck')
     A            OIHMSC        20          TEXT('Misc. Info')
     A                                      COLHDG('Misc. Info')
     A*
     A          K OIHWHS
     A          K OIHRTE
     A          K OIHORD
