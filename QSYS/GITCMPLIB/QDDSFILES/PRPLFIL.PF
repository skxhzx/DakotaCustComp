     A*----------------------------------------------------------------
     A*   Copyright (C) 2003 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PRPLFIL  (PF)
     A*  Description ... Priority replenishments.
     A*  Creator ....... MLB
     A*
     A*  Revisions:
     A*
     A*    Rev 4.13  01/11/00  MLB
     A*      - File created.
     A*
414 AA*    08/23/00  MLB  4.14
     A*      - Updated documentation for PRFLAG.  New code added.
     A*        No re-compiliation necessary.
     A*
417 AA*    07/22/03  MLB  4.17
     A*      - Enh: Added 6 new fields to file; PRTLOC, PRTLVL and
     A*        PRTHND, PRFLOC, PRFLVL and PRFHND. These new fields
     A*        will support (PPR) Pushback Priority Replenishments.
417aAA*    09/08/03  MLB  4.17a
     A*      - Enh: Added 1 new field to file; PRRPLT
417bAA*    10/09/03  MLB  4.17b
     A*      - Enh: Added 2 new fields to file; PRFPOS, PRTPOS
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Select flag (PRFLAG)
     A*      'A'  -  Replenishment in process
414 AA*      'D'  -  Replenishment has been deleted.
     A*      'S'  -  Replenishment has been sequenced
     A*
     A*    Item type (PRTYPE)
     A*      ' '  -  Normal
     A*      'B'  -  Broken case
417aAA*
417aAA*    Replenishment type (PRRPLT)
417aAA*      ' '  -  Normal replenishment
417aAA*      'E'  -  Eaches replenishment
417aAA*      'P'  -  Pushback replenishment
     A*
     A*****************************************************************
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R PRREC
     A            PRWHSE    R               REFFLD(SLWHSE SLOT)
     A            PRWHDP    R               REFFLD(SLWHDP SLOT)
     A            PRFAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('From' 'Aisle')
417 AA            PRFLOC    R               REFFLD(SLLOC  SLOT)
417 AA                                      COLHDG('From' 'Location')
417 AA            PRFLVL    R               REFFLD(SLRLVL SLOT)
417 AA                                      COLHDG('From' 'Level')
417 AA            PRFHND    R               REFFLD(SLHAND SLOT)
417 AA                                      COLHDG('From' 'Handstack')
417aAA            PRFPOS         3P 0       TEXT('From Slot Position')
417aAA                                      COLHDG('From Slot' 'Position')
     A            PRTAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('To' 'Aisle')
417 AA            PRTLOC    R               REFFLD(SLLOC  SLOT)
417 AA                                      COLHDG('To' 'Location')
417 AA            PRTLVL    R               REFFLD(SLRLVL SLOT)
417 AA                                      COLHDG('To' 'Level')
417 AA            PRTHND    R               REFFLD(SLHAND SLOT)
417 AA                                      COLHDG('To' 'Handstack')
417aAA            PRTPOS         3P 0       TEXT('To Slot Position')
417aAA                                      COLHDG('To Slot' 'Position')
     A            PRFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            PRTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            PRITEM    R               REFFLD(SLITEM SLOT)
     A            PRSEQ#         5P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq' 'Number')
     A            PRQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            PRSDEF    R               REFFLD(SLSDEF SLOT)
     A                                      COLHDG('Replenishment' 'Slot Type')
     A            PRFLAG         1A         TEXT('Select Flag')
     A                                      COLHDG('Select' 'Flag')
     A            PRPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            PRFREE         1A         TEXT('Free slot Y,N')
     A                                      COLHDG('Free Slot' 'Y,N')
     A            PRSTAT    R               REFFLD(SLSTAT SLOT)
     A                                      COLHDG('Status of' 'Home Slot')
     A            PRTYPE         1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
417aAA            PRRPLT         1A         TEXT('Replenishment Type')
417aAA                                      COLHDG('Replenishment' 'Type')
     A            PRADTE    R               TEXT('Add Date')
     A                                      COLHDG('Add' 'Date')
     A                                      REFFLD(DTECMD)
     A            PRATIM    R               TEXT('Add Time')
     A                                      COLHDG('Add' 'Time')
     A                                      REFFLD(TIMHMS)
     A            PRAQS          7P 2       TEXT('Average Quantity Sold')
     A                                      COLHDG('Avg Qty' 'Sold')
     A*
     A*  File key.
     A*
     A          K PRWHSE
     A          K PRWHDP
     A          K PRFAIL
     A          K PRTAIL
     A*
