     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSADJL (PF)
     A*  Description ... Staging Export - Adjustment Licenses
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
700  A*    09/30/16  RTR  7.00
     A*      - Created from EXPFADJL.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R IELREC
     A*
     A            IELWHSE   R               REFFLD(WHCODE)
     A            IELBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A*
     A*    Export information
     A*
     A            IELSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            IELSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            IELITEM   R               REFFLD(ITITEM)
     A            IELDATE   R               TEXT('Adjustment Date (YYYYMMDD)')
     A                                      COLHDG('Adjustment' 'Date')
     A                                      REFFLD(DTECMD)
     A            IELTIME   R               TEXT('Adjustment Time')
     A                                      COLHDG('Adjustment' 'Time')
     A                                      REFFLD(TIMHMS)
     A            IELBY     R               TEXT('Adjusted By')
     A                                      COLHDG('Adjusted' 'By')
     A                                      REFFLD(USER)
     A            IELWHDP   R               REFFLD(WDCODE)
     A            IELDISP       12A         TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            IELLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            IELOLCNS      15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            IELQTY1        5S 0       TEXT('Quantity 1')
     A                                      COLHDG('Quantity 1')
     A            IELQTY2        5S 0       TEXT('Quantity 2')
     A                                      COLHDG('Quantity 2')
     A            IELQTY3        5S 0       TEXT('Quantity 3')
     A                                      COLHDG('Quantity 3')
     A            IELCWTA        9S 2       TEXT('Catch Weight Adj')
     A                                      COLHDG('Catch' 'Wgt Adj')
     A            IELPO     R               REFFLD(PONUM)
     A            IELSEQ         5S 0       TEXT('Sequence number')
     A                                      COLHDG('Sequence' 'Number')
     A            IELBRD        10A         TEXT('Brand')
     A                                      COLHDG('Brand')
     A            IELSTAT        2A         TEXT('Slot Status')
     A                                      COLHDG('Slot' 'Status')
     A            IELEXPD        8S 0       TEXT('Expiration Date')
     A                                      COLHDG('Expire' 'Date')
     A            IELOCO        15A         TEXT('Origin Country')
     A                                      COLHDG('Origin Country')
     A            IELOST        15A         TEXT('Origin State')
     A                                      COLHDG('Origin State')
     A*
     A*  Add stamps
     A*
     A            IELADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            IELADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K IELWHSE
     A          K IELBATID
     A          K IELITEM
     A          K IELDATE
     A          K IELTIME
