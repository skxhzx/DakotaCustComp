     A*----------------------------------------------------------------
     A*   Copyright (C) 2011 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSORDL (PF)
     A*  Description ... Staging Export - Order Labels
     A*
     A*  Revisions
     A*
640 AA*    01/15/11  DAS  6.40
     A*      - Created
     A*      - Base on EXPFORDL, EXPFORDB
     A*
     A*----------------------------------------------------------------
     A          R OELREC
     A*
     A            OELWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OELBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A            OELRTE         5          TEXT('Route')
     A                                      COLHDG('Route')
     A            OELORD         7S 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OELSEQ         5S 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OELITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OELLBL         7S 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            OELSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            OELSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            OELRTEID       5          TEXT('Route Id')
     A                                      COLHDG('Route' 'Id')
     A            OELORDID       7S 0       TEXT('Order Id')
     A                                      COLHDG('Order' 'Id')
     A            OELQP1         5S 0       TEXT('Normal Qty Picked')
     A                                      COLHDG('Normal Qty' 'Picked')
     A            OELQP2         5S 0       TEXT('Breakdown 1 Qty Picked')
     A                                      COLHDG('Breakdown 1' 'Qty Picked')
     A            OELQP3         5S 0       TEXT('Breakdown 2 Qty Picked')
     A                                      COLHDG('Breakdown 2' 'Qty Picked')
     A            OELTYP         1A         TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
     A            OELDSP        12A         TEXT('Slot Display')
     A                                      COLHDG('Slot' 'Display')
     A            OELPAL         7S 0       TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A            OELTRN         7S 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            OELUMT         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            OELALC         5S 0       TEXT('Qty Allocated')
     A                                      COLHDG('Qty' 'Allocated')
     A            OELBOX         2S 0       TEXT('Box Number')
     A                                      COLHDG('Box' 'Number')
     A            OELEMP         5S 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A*
     A*  Add stamps
     A*
     A            OELADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            OELADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K OELWHS
     A          K OELBATID
     A          K OELRTE
     A          K OELORD
     A          K OELSEQ
     A          K OELITM
     A          K OELLBL
