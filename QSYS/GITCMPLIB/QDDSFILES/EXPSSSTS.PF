     A*----------------------------------------------------------------
     A*   Copyright (C) 2020 BFC Software, Inc. - All Rights Reserved
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPSSSTS  (PF)
     A*  Description ... Staging Export - Slot Status - Rev 7.40
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
740  A*    10/19/20  RTR  7.40
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    This file contains slot status changes.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R STSREC
     A*
     A            STSWHSE   R               REFFLD(WHCODE)
     A            STSBATID      11          TEXT('Export Batch Id')
     A                                      COLHDG('Export' 'Batch Id')
     A*
     A*  Export information
     A*
     A*    Status info
     A*
     A            STSSTATUS      1A         TEXT('Exp Status')
     A                                      COLHDG('Exp Status')
     A            STSSTSTS        Z         TEXT('Exp Status Timestamp')
     A                                      COLHDG('Exp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            STSSTSUC        Z         TEXT('Exp Status UC Timestamp')
     A                                      COLHDG('Exp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            STSTEXT      200          TEXT('Exp Text')
     A                                      COLHDG('Exp Text')
     A*
     A*    Fields being exported
     A*
     A            STSTRN         7S 0       TEXT('Trans Num')
     A                                      COLHDG('Trans' 'Num')
     A            STSLLTRN       7S 0       TEXT('Liclog Trans')
     A                                      COLHDG('Liclog' 'Trans')
     A            STSITEM       15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            STSACTION      3A         TEXT('Action')
     A                                      COLHDG('Action')
     A            STSFDISP      12A         TEXT('From Slot')
     A                                      COLHDG('From' 'Slot')
     A            STSFSTS        2A         TEXT('From Sts')
     A                                      COLHDG('From' 'Sts')
     A            STSTDISP      12A         TEXT('To Slot')
     A                                      COLHDG('To' 'Slot')
     A            STSTSTS        2A         TEXT('To Sts')
     A                                      COLHDG('To' 'Sts')
     A            STSFQTY1       5S 0       TEXT('From Qty1')
     A                                      COLHDG('From' 'Qty1')
     A            STSFQTY2       5S 0       TEXT('From Qty2')
     A                                      COLHDG('From' 'Qty2')
     A            STSFQTY3       5S 0       TEXT('From Qty3')
     A                                      COLHDG('From' 'Qty3')
     A            STSTQTY1       5S 0       TEXT('To Qty1')
     A                                      COLHDG('To' 'Qty1')
     A            STSTQTY2       5S 0       TEXT('To Qty2')
     A                                      COLHDG('To' 'Qty2')
     A            STSTQTY3       5S 0       TEXT('To Qty3')
     A                                      COLHDG('To' 'Qty3')
     A*
     A*  Add stamps
     A*
     A            STSADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            STSADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K STSWHSE
     A          K STSBATID
     A          K STSADDTS
     A*
