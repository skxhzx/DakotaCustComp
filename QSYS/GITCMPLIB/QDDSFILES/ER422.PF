     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ER422   (PF)
     A*  Description ... Work file ER422
     A*  Creator ....... DAS/HNK
     A*
     A*  Revisions:
     A*  10/02/00  HNK 4.14
     A*  - Created
     A*
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes
     A*
     A*       '1'  -  Pending
     A*       '2'  -  Started (Active)
     A*       '3'  -  Ended   (Finished)
     A*       '4'  -  Closed  (Completed)
     A*       '8'  -  Interrupted
     A*       '9'  -  Canceled
     A*
     A*    End type codes
     A*
     A*       'C'  -  Ended by close program.
     A*       'M'  -  Ended by manual entry of time.
     A*       'N'  -  Ended by start of new transaction.
     A*
     A*    Group type 1 (PTGRP1)
     A*
     A*       ' '  -  Misc.
     A*       'L'  -  Letdown/Replenishment
     A*       'P'  -  Putaway/Receiving
     A*       'R'  -  Return
     A*       'S'  -  Selection
     A*
     A*    Group type 2  (PTGRP2)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
     A*
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R ERREC
     A*
     A            ERTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN# PIRDICT)
     A            ERSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            ERTASK    R               REFFLD(TKCODE PIRDICT)
     A            ERCAT     R               REFFLD(TCCODE PIRDICT)
     A            ERSUB     R               REFFLD(TSCODE PIRDICT)
     A            ERWHSE    R               REFFLD(WHCODE PIRDICT)
     A            ERWHDP    R               REFFLD(WDCODE PIRDICT)
     A            ERSTYP    R               REFFLD(SLSTYP PIRDICT)
     A            ERSHFT    R               REFFLD(SHCODE PIRDICT)
     A            ERCREW    R               REFFLD(CRCODE PIRDICT)
     A            EREMP#         5P 0       TEXT('Employee Number')
     A                                      COLHDG('Employee' 'Number')
     A            ERCUBE         9P 3       TEXT('Total Cube')
     A                                      COLHDG('Total' 'Cube')
     A            ERSWGT         9P 2       TEXT('Total Weight')
     A                                      COLHDG('Total' 'Weight')
     A            ERPCS          5P 0       TEXT('Total Pieces')
     A                                      COLHDG('Total' 'Pieces')
     A            ERAISL         5P 0       TEXT('Total Aisles')
     A                                      COLHDG('Total' 'Aisles')
     A            ERQTY1         5P 0       TEXT('Total Qty1')
     A                                      COLHDG('Total' 'Qty1')
     A            ERQTY2         5P 0       TEXT('Total Qty2')
     A                                      COLHDG('Total' 'Qty2')
     A            ERQTY3         5P 0       TEXT('Total Qty3')
     A                                      COLHDG('Total' 'Qty3')
     A            ERGDTE    R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            ERGTIM    R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            ERSDTE    R               TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            ERSTIM    R               TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            EREDTE    R               TEXT('End Date')
     A                                      COLHDG('End' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            ERETIM    R               TEXT('End Time')
     A                                      COLHDG('End' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            ERETYP         1          TEXT('End Type')
     A                                      COLHDG('End' 'Type')
     A            ERCDTE    R               TEXT('Close Date')
     A                                      COLHDG('Close' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            ERCTIM    R               TEXT('Close Time')
     A                                      COLHDG('Close' 'Time')
     A                                      REFFLD(TIMHMS PIRDICT)
     A            ERRDTE    R               TEXT('Report Date')
     A                                      COLHDG('Report' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            ERDMIN         5P 0       TEXT('Direct Minutes')
     A                                      COLHDG('Direct' 'Minutes')
     A            ERIMIN         5P 0       TEXT('Indirect Minutes')
     A                                      COLHDG('Indirect' 'Minutes')
     A            ERRTPO         9          TEXT('Route/PO')
     A                                      COLHDG('Route/PO')
     A            ERGRP1         1A         TEXT('Group 1')
     A                                      COLHDG('Group 1')
     A            ERGRP2         1A         TEXT('Group 2')
     A                                      COLHDG('Group 2')
     A*
     A          K EREMP#
     A          K ERSDTE                    DESCEND
     A          K ERSTIM                    DESCEND
