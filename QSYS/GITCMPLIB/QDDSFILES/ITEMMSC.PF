     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ITEMMSC (PF)
     A*  Description ... Item Misc Info file
     A*  Date .......... May 18, 1998
     A*  Creator ....... DAS/HNK
     A*
     A*  Revisions
412 AI*    08/04/99  RH   4.12
     I*      - Added IMWRNC Item Perform catch wt. variance check.
     I*      - Added IMRG1C Item Catch weight low variance percentage.
     I*      - Added IMRG2C Item Catch weight high variance percentage.
     A*
412 AI*    08/20/99  MLB  4.12
     I*      - Added IMAPCK Allow automatic assignment of pick slots.
     I*      - Added IMNRPK Number of pick slots for item.
416 AI*    07/01/02  RH   4.16
     I*      - Added IMNSLT Non-slotted item (Y/N)
     I*      - Added IMXDCK Crossdock item (Y/N)
     I*      - Added IMBNBR Buyer number
     I*      - Added IMBNAM Buyer name
     I*      - Added IMVNAM Vendor name
     I*      - Added IMBRND Brand
     I*      - Added IMLOT  Lot code item (Y/N)
     I*      - Added IMLTRK Track by license plate (Y/N)
417aAI*    04/01/03  RH   4.17a
     I*      - Added IMODAY # of days out
510aAI*    02/06/07  RH   5.10a
     I*      - Fix: Change comments only, missed in V5.00 update.
     I*        Change values for IMLTRK to L=Low, M=Medium, H=High
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes: Some items like Poultry, has only manufacturing date.
     A*         Manufacturers do not put expiration date on it.  Hence
     A*         we need to come up with the expiration date from the
     A*         following info.
     A*
     A*
     A*    IMMFLG - Manufacturing date flag. Does the item have mfg
     A*             date?
     A*
     A*            'Y'  -  Yes
     A*
     A*            'N'  -  No
     A*
     A*    IMDDAY - Number of days the distributor needs to sell the
     A*             item.
     A*
     A*    IMUDAY - Number of days the end user needs to sell the
     A*             item. This field is for information only.
     A*
     A*    IMMDAY - Shelf life days, stipulated by manufacturer.
     A*
417a A*    IMODAY - # of days out, if expiration date is greater than
     A*             today plus IMODAY, display warning.
     A*
     I*    IMRG1C  -Item Catch weight low variance percentage and
     I*    IMRG2C  -Item Catch weight high variance percentage will
     A*             override the global *SYSTEM % values.
     I*    IMWRNC  -Not used at present time.
     I*    IMAPCK  -Allow pick slots to be automatically assigned for
     A*             fixed item.  This switch is only valid for item
     A*             with ITTYPE = ' ' or 'B'.  Valid values are 'Y', 'N'.
     A*             NOTE: Not used at present time.
     I*    IMNRPK  -Number of pick slots for item. If above field is 'N'
     A*             this field will always be 0. Valid values are 0-999.
416 AI*    IMNSLT  -Non-slotted item (Y/N)                             '
416 AI*    IMXDCK  -Non-slotted item (Y/N)                             '
416 AI*    IMBNBR  -Buyer number.  Buyer number from the host system.  '
416 AI*    IMBNAM  -Buyer name. Buyer name from the host system.       '
416 AI*    IMVNAM  -Vendor name. Vendor name from the host system.     '
416 AI*    IMBRND  -Brand. Brand from the host system.                 '
416 AI*    IMLOT   -Lot code item (Y/N)                                '
510aAI*    IMLTRK  -Track by license plate L=Low, M=Medium, H=High     '
510 DI*    IMLTRK  -Track by license plate (Y/N)                       '
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R IMREC
     A*
     A            IMWHSE    R               REFFLD(WHCODE)
     A            IMITEM    R               REFFLD(ITITEM)
     A            IMTIE          4S 0       TEXT('Tie')
     A                                      COLHDG('Tie')
     A            IMHIGH         2S 0       TEXT('High')
     A                                      COLHDG('High')
     A            IMMFLG         1          TEXT('Manufacturing date (Y,N)')
     A                                      COLHDG('Manufacture' 'date flag')
     A            IMDDAY         3P 0       TEXT('Distributor days')
     A                                      COLHDG('Distributor' 'Days')
     A            IMUDAY         3P 0       TEXT('End user days')
     A                                      COLHDG('End User' 'Days')
     A            IMMDAY         3P 0       TEXT('Mfg Shelf Life Days')
     A                                      COLHDG('Mfg Shelf' 'Life Days')
412 AA            IMWRNC         1          TEXT('Perform CW var. check (Y,N)')
     A                                      COLHDG('Perform CW ' 'var check')
412 AA            IMRG1C         3P 1       TEXT('Item CW low variance %')
     A                                      COLHDG('Item CW ' 'Low Var %')
412 AA            IMRG2C         3P 1       TEXT('Item CW high variance%')
     A                                      COLHDG('Item CW ' 'High Var %')
412 AA            IMAPCK         1A         TEXT('Automatically assign pick sl +
     A                                      ots')
     A                                      COLHDG('Auto Assign' 'Pick Slot')
412 AA            IMNRPK         3P 0       TEXT('Number of pick slots')
     A                                      COLHDG('Nbr' 'Pick Slots')
416 AA            IMNSLT         1A         TEXT('Non-slotted item (Y,N)')
     A                                      COLHDG('Non-Slotted' 'Item')
416 AA            IMXDCK         1A         TEXT('Crossdock item (Y,N)')
     A                                      COLHDG('Crossdock' 'Item')
416 AA            IMBNBR         3P 0       TEXT('Buyer number')
     A                                      COLHDG('Buyer' 'number')
416 AA            IMBNAM        40A         TEXT('Buyer name')
     A                                      COLHDG('Buyer name')
416 AA            IMVNAM        40A         TEXT('Vendor name')
     A                                      COLHDG('Vendor name')
416 AA            IMBRND        10A         TEXT('Brand')
     A                                      COLHDG('Brand')
416 AA            IMLOT          1A         TEXT('Lot code item (Y/N)')
     A                                      COLHDG('Lot code' 'item')
510aAA            IMLTRK         1A         TEXT('Track by License (L/M/H)')
     A                                      COLHDG('Track by' 'license')
417a A            IMODAY         3P 0       TEXT('# of Days Out')
     A                                      COLHDG('# of Day Out')
     A*
     A          K IMWHSE
     A          K IMITEM
