     A*----------------------------------------------------------------
     A*   Copyright (C) 2007 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... EXPORDP  (PF)
     A*  Description ... Order detail piece export file
     A*  Date .......... June 19, 2007
     A*  Creator ....... DAS
     A*
     A*----------------------------------------------------------------
     A*  Notes:
     A*
     A*  This file should be identical to EXPFORDP except that the
     A*  numeric fields should be defined as Packed, not Signed.
     A*
     A*  Compile with MBR(*NONE) MAXMBRS(*NOMAX) AUT(*ALL)
     A*
     A*  To verify authority:
     A*       WRKAUT OBJ('QSYS.LIB/library.LIB/EXPORDL.FILE')
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
510 AA*    06/19/07  DAS  5.10
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OEPREC
     A*
     A            OEPWHS         3P 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            OEPORD         7P 0       TEXT('Order Number')
     A                                      COLHDG('Order' 'Number')
     A            OEPSQ#         5P 0       TEXT('Order Sequence Number')
     A                                      COLHDG('Order Sequence' 'Number')
     A            OEPITM        15A         TEXT('Item')
     A                                      COLHDG('Item')
     A            OEPUP#         5P 0       TEXT('Unique Piece #')
     A                                      COLHDG('Unique' 'Piece #')
     A            OEPLBL         7P 0       TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A            OEPTYP         1A         TEXT('Label Type')
     A                                      COLHDG('Label' 'Type')
510aAA            OEPDSP        12A         TEXT('Slot Display')
510aAA                                      COLHDG('Slot' 'Display')
510aAA            OEPPAL         7S 0       TEXT('Pallet Number')
510aAA                                      COLHDG('Pallet' 'Number')
     A            OEPTRN         7P 0       TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            OEPUMT         1A         TEXT('UM Type')
     A                                      COLHDG('UM' 'Type')
     A            OEPSEQ         3P 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            OEPWGT         7P 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A*
     A          K OEPWHS
     A          K OEPORD
     A          K OEPSQ#
     A          K OEPITM
     A          K OEPUP#
