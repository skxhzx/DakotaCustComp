     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... VOEXCP (PF)
     A*  Description ... RF Selection exception file
     A*  Date .......... March 20, 2006
     A*  Creator ....... RH
     A*
     A*  Revisions
     F*
     F*    03/20/06  RH   5.00
     F*      - Created
     F*
     A*----------------------------------------------------------------***
     A*
     A*  Codes
     A*
     A*      Status Codes (SXESTA)
     A*        1 = Open
     A*        4 = Closed
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Task/Error codes
     A*
     A*  Error    Description
     A*  -------  ---------------------------------------------
     A*  SKPLCN   Skip License
     A*  SKPUPC   Skip UPC
     A*  SHORTS   Short taken Selection
     A*  SHORTM   Short taken Merge pick up
     A*
     A*----------------------------------------------------------------***
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R SXREC
     A            SXWHSE    R               REFFLD(WHCODE)
     A            SXTRN#    R               REFFLD(TRAN#)
     A            SXLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            SXITEM    R               REFFLD(ITITEM)
     A            SXGDTS          Z         TEXT('Generated Date Stamp')
     A                                      COLHDG('Generated' 'Date Stamp')
     A            SXGEMP         5P 0       TEXT('Generated by Emp#')
     A                                      COLHDG('Generated' 'by Emp#')
     A            SXGBY         10A         TEXT('Generated by User')
     A                                      COLHDG('Generated by' 'User')
     A            SXERID         7A         TEXT('Error Id')
     A                                      COLHDG('Error' 'Id')
     A            SXESTA         2A         TEXT('Status')
     A                                      COLHDG('Status')
     A            SXCUSR        10A         TEXT('Corrected by user')
     A                                      COLHDG('Corrected' 'by user')
     A            SXCDTS          Z         TEXT('Corrected Date Stamp')
     A                                      COLHDG('Corrected' 'Date Stamp')
     A*
     A*  File key.
     A*
     A          K SXWHSE
     A          K SXTRN#
     A          K SXITEM
     A          K SXLBL#
     A          K SXERID
     A*
