     A*****************************************************************
     A* WK381    Slot Replenishment - Work file.                      *
     A* Hemant Kapadia                                                *
     A* 10 May, 1996                                                  *
     A*                                                               *
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
213 AA*    Item type (WKTYPE)
     A*
     A*      ' '  -  Normal
     A*
     A*      'B'  -  Broken case
     A*****************************************************************
     A*
     A                                      REF(*LIBL/PIRDICT)
     A          R WKREC
     A            WKWHSE    R               REFFLD(SLWHSE SLOT)
     A            WKWHDP    R               REFFLD(SLWHDP SLOT)
     A            WKFAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('From' 'Aisle')
     A            WKTAIL    R               REFFLD(SLAISL SLOT)
     A                                      COLHDG('To' 'Aisle')
     A            WKFDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('From' 'Slot')
     A            WKTDIS    R               REFFLD(SLDISP SLOT)
     A                                      COLHDG('To' 'Slot')
     A            WKITEM    R               REFFLD(SLITEM SLOT)
     A            WKSEQ#         5P 0       TEXT('Seq Number')
     A                                      COLHDG('Seq' 'Number')
     A            WKQTY          3P 0       TEXT('Quantity')
     A                                      COLHDG('Quantity')
     A            WKSDEF    R               REFFLD(SLSDEF SLOT)
     A                                      COLHDG('Replenishment' 'Slot Type')
     A            WKFLAG         1A         TEXT('Select Flag')
     A                                      COLHDG('Select' 'Flag')
     A            WKPRTY         1P 0       TEXT('Priority')
     A                                      COLHDG('Priority')
     A            WKFREE         1A         TEXT('Free slot Y,N')
     A                                      COLHDG('Free Slot' 'Y,N')
     A            WKSTAT    R               REFFLD(SLSTAT SLOT)
     A                                      COLHDG('Status of' 'Home Slot')
     A            WKTYPE         1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            WKADTE    R               TEXT('Add Date')
     A                                      COLHDG('Add' 'Date')
     A                                      REFFLD(DTECMD)
     A            WKATIM    R               TEXT('Add Time')
     A                                      COLHDG('Add' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A*  File key.
     A*
     A          K WKWHSE
     A          K WKWHDP
     A          K WKFAIL
     A          K WKTAIL
     A*
