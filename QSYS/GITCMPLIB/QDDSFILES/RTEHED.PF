     A*----------------------------------------------------------------
     A*   Copyright (C) 1997 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... RTEHED (PF)
     A*  Description ... Route header file.
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    06/02/97  DAS
     A*      - Created.
     A*
416 AA*    09/13/01  DAS  4.16
     A*      - Added status code '8' for unallocated/unpicked route.
     A*        RHEXPT will contain new route id.
     A*      - Added status code '9' for delete.
     A*      - Format did not change.
     A*
650aAA*    04/09/13  DAS  6.50a
     A*      - No field changes. Just added comments for RHSHPD.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
413aAA*  The field RHSHPT is now being used as a flag field for IT501 -
     A*  FS-WMS End of Day processing program.  The field is being used
     A*  as-is and the file has NOT been recreated.
     A*
416 AA*  When a route is unallocated or unpicked the following happens:
     A*
     A*    - Original RTEHED rec is set to a status of '8'.
     A*    - New RTEHED rec is created with new route id.
     A*       - Note: The above was done to prevent unpredictable
     A*               results from happening if the route sent through
     A*               Truck Builder a second time.
     A*    - Order records are set to open and updated with new rte id.
     A*    - WORKLBL recs are set to status of 'X'.
     A*    - LABEL recs are set to status of 'C'.
     A*    - PIRTRAN recs are set to status of '4'.
     A*    - Slots are updated.
     A*    - ITEMQTY recs are updated.
     A*    - Corresponding letdowns are left alone.
     A*
     A*----------------------------------------------------------------
     A*  Codes
     A*
     A*    Origination types (RHORIG)
     A*
     A*       ' '  -  From host system
     A*       'T'  -  From PIR transfer programs
     A*
     A*    Route types (RHTYPE)
     A*
     A*       ' '  -  Normal
     A*       'T'  -  Transfer
     A*       'W'  -  Will Call
     A*
     A*    Status codes (RHSTAT)
     A*
     A*       '1'  -  Open
     A*       '2'  -  Allocated
     A*       '3'  -  Built
     A*       '4'  -  Printed
     A*       '5'  -  Closed
     A*       '6'  -  Exported
416 AA*       '8'  -  Unallocated/Unpicked
416 AA*       '9'  -  Deleted
     A*
650aAA*    Ship date positions 6, 8 (RHSHPD) - Used as In-Use flag (See CHKIUSE
     A*
     A*      Pos 8  Pos 6   Description
     A*      -----  -----   -------------------------------
     A*        1      0     Open Route - Allocate & Build
     A*        1      1     Open Route - Deleting
     A*        1      2     Open Route - Editing
     A*        1      3     Open Route - Truck Detail
     A*
     A*        2      0     Built Route - Pick & Print
     A*        2      1     Built Route - Unallocating
     A*        2      2     Built Route - Editing
     A*        2      3     Built Route - Truck Detail
     A*        2      4     Built Route - Drag Drop
     A*        2      5     Built Route - Rebuild
     A*
     A*        3      0     Printed Route - Closing Transactions
     A*        3      1     Printed Route - Unallocating
     A*        3      2     Printed Route - Exporting Catchweights (Ledyards)
     A*
     A*        4      0     Closed Route - Exporting Route
     A*
     A*        5      0     Open Route - Importing Route
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R RHREC
     A*
     A            RHWHSE    R               REFFLD(WHCODE)
     A            RHRTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            RHRTE     R               REFFLD(ROUTE)
     A            RHTRUK    R               REFFLD(TMTRUK)
     A            RHORIG         1          TEXT('Origination')
     A                                      COLHDG('Origination')
     A            RHTYPE         1          TEXT('Route Type')
     A                                      COLHDG('Route' 'Type')
     A            RHPBAT    R               REFFLD(BATCH#)
     A            RHRPTD    R               TEXT('Report Date')
     A                                      COLHDG('Report' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHIMPD    R               TEXT('Import Date')
     A                                      COLHDG('Import' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHIMPT    R               TEXT('Import Time')
     A                                      COLHDG('Import' 'Time')
     A                                      REFFLD(TIMHMS)
     A            RHSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            RHSTSD    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHSTST    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            RHBLDD    R               TEXT('Built Date')
     A                                      COLHDG('Built' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHBLDT    R               TEXT('Built Time')
     A                                      COLHDG('Built' 'Time')
     A                                      REFFLD(TIMHMS)
     A            RHPRTD    R               TEXT('Printed Date')
     A                                      COLHDG('Printed' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHPRTT    R               TEXT('Printed Time')
     A                                      COLHDG('Printed' 'Time')
     A                                      REFFLD(TIMHMS)
     A            RHEXPD    R               TEXT('Exported Date')
     A                                      COLHDG('Exported' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHEXPT    R               TEXT('Exported Time')
     A                                      COLHDG('Exported' 'Time')
     A                                      REFFLD(TIMHMS)
     A            RHSHPD    R               TEXT('Shipped Date')
     A                                      COLHDG('Shipped' 'Date')
     A                                      REFFLD(DTECMD)
     A            RHSHPT    R               TEXT('Shipped Time')
     A                                      COLHDG('Shipped' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K RHWHSE
     A          K RHRTID
