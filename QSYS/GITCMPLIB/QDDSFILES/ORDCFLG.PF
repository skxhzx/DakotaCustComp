     A*----------------------------------------------------------------
     A*   Copyright (C) 2014 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORDCFLG  (PF)
     A*  Description ... Order detail catch weight flag file
     A*  Creator ....... DAS
     A*
     A*  Revisions:
     A*
650  A*    04/18/14  DAS
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A* Record Types
     A*
     A*   T - Transaction
     A*   L - Label
     A*   W - Weight
     A*
     A*----------------------------------------------------------------
     A* Flag Code
     A*
     A*   01 - No weights entered
     A*   02 - Missing weights
     A*   03 - Too many weights
     A*   04 - Total weight out of range
     A*   05 - Individual weight out of range
     A*   06 - Individual weight cannot be equal to zero
     A*   07 - Individual weight cannot be less than zero
     A*   51 - Glazier - weight cannot be greater than 999999 for tfr order
     A*   99 - No errors or warnings
     A*
     A* Flags
     A*
     A*   1 - Error
     A*   2 - Warning, needs override
     A*   3 - Warning, override taken
     A*   4 - No error or warning
     A*
     A*----------------------------------------------------------------
     A* Notes
     A*
     A*
     A*
     A*
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R OCFREC
     A*
     A            OCFWHS    R               REFFLD(WHCODE)
     A            OCFTRN    R               REFFLD(TRAN#)
     A                                      TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A            OCFLBL    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            OCFORD    R               REFFLD(OHORD)
     A            OCFOSQ    R               REFFLD(SEQ#)
     A                                      TEXT('Order Sequence')
     A                                      COLHDG('Order' 'Seq')
     A            OCFITM    R               REFFLD(ITITEM)
     A                                      TEXT('Item Number')
     A                                      COLHDG('Item' 'Number')
     A            OCFWSQ         3P 0       TEXT('Weight Sequence')
     A                                      COLHDG('Weight' 'Seq')
     A            OCFTYP         1          TEXT('Record Type')
     A                                      COLHDG('Rec' 'Type')
     A            OCFCOD         2          TEXT('Flag Code')
     A                                      COLHDG('Flag' 'Code')
     A            OCFFLG         1          TEXT('Flag')
     A                                      COLHDG('Flag')
     A            OCFWGT        13P 2       TEXT('Catch Weight')
     A                                      COLHDG('Catch' 'Weight')
     A            OCFLRG        13P 2       TEXT('Low Range')
     A                                      COLHDG('Low' 'Range')
     A            OCFHRG        13P 2       TEXT('High Range')
     A                                      COLHDG('High' 'Range')
     A*
     A          K OCFWHS
     A          K OCFTRN
     A          K OCFLBL
     A          K OCFORD
     A          K OCFOSQ
     A          K OCFITM
     A          K OCFWSQ
