     A*----------------------------------------------------------------
     A*   Copyright (C) 2008 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICLOGH  (PF)
     A*  Description ... License log file clean up file
     A*  Creator ....... GJA
     A*
     A*  Revisions
     A*
     A*    10/15/10  GJA  6.40
     A*      - Created
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R LLHREC
     A*
     A            LLWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            LLLTRN    R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
     A            LLLTRNSEQ      2S 0       TEXT('Lic Tran Seq #')
     A                                      COLHDG('Lic Tran' 'Seq #')
     A            LLACTION       3A         TEXT('Action')
     A                                      COLHDG('Action')
     A            LLTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            LLETRN#   R               TEXT('Employee Transaction Number')
     A                                      COLHDG('Employee' 'Transaction' -
     A                                      'Number')
     A                                      REFFLD(TRAN#)
     A            LLLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LLLCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
     A            LLOLCNS       15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            LLVRFY         1A         TEXT('Lic Verified')
     A                                      COLHDG('Lic' 'Verified')
     A            LLFLCNS       15A         TEXT('"From" License Number')
     A                                      COLHDG('"From"' 'License' 'Number')
     A*
     A            LLAQTY1        5P 0       TEXT('Action Qty1')
     A                                      COLHDG('Action' 'Qty1')
     A            LLAQTY2        5P 0       TEXT('Action Qty2')
     A                                      COLHDG('Action' 'Qty2')
     A            LLAQTY3        5P 0       TEXT('Action Qty3')
     A                                      COLHDG('Action' 'Qty3')
     A*
     A            LLRQTY1        5P 0       TEXT('Result Qty1')
     A                                      COLHDG('Result' 'Qty1')
     A            LLRQTY2        5P 0       TEXT('Result Qty2')
     A                                      COLHDG('Result' 'Qty2')
     A            LLRQTY3        5P 0       TEXT('Result Qty3')
     A*
     A            LLSQTY1        5P 0       TEXT('Sent Actn Qty1')
     A                                      COLHDG('Sent' 'Actn Qty1')
     A            LLSQTY2        5P 0       TEXT('Sent Actn Qty2')
     A                                      COLHDG('Sent' 'Actn Qty2')
     A            LLSQTY3        5P 0       TEXT('Sent Actn Qty3')
     A                                      COLHDG('Sent' 'Actn Qty3')
     A*
     A* "To" Location information
     A*
     A            LLCODE         1A         TEXT('To Code')
     A                                      COLHDG('To' 'Code')
     A            LLAREA        15A         TEXT('To Area')
     A                                      COLHDG('To' 'Area')
     A            LLWHDP    R               TEXT('To Department')
     A                                      COLHDG('To' 'Department')
     A                                      REFFLD(WDCODE)
     A            LLDISP        12A         TEXT('To Display')
     A                                      COLHDG('To' 'Display')
     A            LLAISL    R               TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            LLLOC     R               TEXT('To Location')
     A                                      COLHDG('To' 'Location')
     A                                      REFFLD(SLLOC)
     A            LLRLVL    R               TEXT('To Rack Level')
     A                                      COLHDG('To' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LLHAND    R               TEXT('To Handstack')
     A                                      COLHDG('To' 'Handstack')
     A                                      REFFLD(HSCODE)
     A            LLTSTAT   R               TEXT('To Status')
     A                                      COLHDG('To' 'Status')
     A                                      REFFLD(SSCODE)
     A            LLOSTAT   R               TEXT('Orig Status')
     A                                      COLHDG('Orig' 'Status')
     A                                      REFFLD(SSCODE)
     A            LLBASE        12A         TEXT('To Base Slot')
     A                                      COLHDG('To' 'Base Slot')
     A            LLPOS          3P 0       TEXT('To Pallet Position')
     A                                      COLHDG('To' 'Pallet' 'Position')
     A            LLITEM    R               TEXT('To Item')
     A                                      COLHDG('To' 'Item')
     A                                      REFFLD(ITITEM)
     A            LLITYPE        1A         TEXT('To Item Type')
     A                                      COLHDG('To' 'Item Type')
     A            LLICHG         1A         TEXT('Item Changed')
     A                                      COLHDG('Item' 'Changed')
     A            LLSITEM   R               TEXT('To Slot Item')
     A                                      COLHDG('To' 'Slot Item')
     A                                      REFFLD(ITITEM)
     A            LLSITYPE       1A         TEXT('Slot Item Type')
     A                                      COLHDG('Slot' 'Item Type')
     A            LLPRTY         1S 0       TEXT('Picking Priority')
     A                                      COLHDG('Picking' 'Priority')
     A            LLENTD    R               TEXT('Date Entered')
     A                                      COLHDG('Date' 'Entered')
     A                                      REFFLD(DTECMD)
     A            LLEXPD    R               TEXT('Expiration Date')
     A                                      COLHDG('Expiration' 'Date')
     A                                      REFFLD(DTECMD)
     A*
     A* To Customer information
     A*
     A            LLORD     R     P         TEXT('To Order Number')
     A                                      COLHDG('To' 'Order' 'Number')
     A                                      REFFLD(OHORD)
     A            LLSEQ     R     P         TEXT('To Order Sequence Number')
     A                                      COLHDG('To Order' 'Sequence Nbr')
     A                                      REFFLD(SEQ#)
     A*
     A            LLEXCD         8A         TEXT('Exception Code')
     A                                      COLHDG('Exception' 'Code')
     A*
     A* Link to Normal From information (see notes)
     A*
     A            LLFLTRN   R               TEXT('From Lic Tran')
     A                                      COLHDG('From' 'Lic Tran')
     A                                      REFFLD(TRAN#)
     A            LLFLTRNSEQ     2S 0       TEXT('From Lic Tran Seq')
     A                                      COLHDG('From' 'Lic Tran Seq')
     A*
     A* Timestamp / User tracking information.
     A*
     A            LLADDTS   R               REFFLD(ADDTS)
     A            LLADDUC   R               REFFLD(ADDUC)
     A            LLADDCPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Calling Pgm')
     A                                      TEXT('Calling Pgm')
     A            LLADDJPGM R               REFFLD(ADDPGM)
     A                                      COLHDG('Job Pgm')
     A                                      TEXT('Job Pgm')
     A            LLADDEMP  R               REFFLD(ADDEMP)
     A            LLADDEUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Employee' 'User')
     A                                      TEXT('Employee User')
     A            LLADDCUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Current' 'User')
     A                                      TEXT('Current User')
     A            LLADDJUSR R               REFFLD(ADDUSR)
     A                                      COLHDG('Job User')
     A                                      TEXT('Job User')
     A            LLADDJOB  R               REFFLD(ADDJOB)
     A            LLADDNBR  R               REFFLD(ADDNBR)
     A*
     A            LLEXPFLAG      1A         TEXT('Export Flag')
     A                                      COLHDG('Export' 'Flag')
     A            LLEXPTS   R               REFFLD(ADDTS)
     A                                      COLHDG('Export Stamp')
     A                                      TEXT('Export Stamp')
     A            LLEXPUC   R               REFFLD(ADDUC)
     A                                      COLHDG('Export UC Stamp')
     A                                      TEXT('Export UC Stamp')
     A            LLEXPJUSR R               REFFLD(ADDUSR)
     A                                      TEXT('Export Job User')
     A                                      COLHDG('Export' 'Job User')
     A            LLEXPJOB  R               REFFLD(ADDJOB)
     A                                      TEXT('Export Job')
     A                                      COLHDG('Export' 'Job')
     A            LLEXPNBR  R               REFFLD(ADDNBR)
     A                                      TEXT('Export Job Nbr')
     A                                      COLHDG('Export' 'Job Nbr')
     A*
     A            LLDRISLOT     10A         TEXT('DRISLOT Cmd')
     A                                      COLHDG('DRISLOT Cmd')
     A            LLDRILIC      10A         TEXT('DRILICENSE Cmd')
     A                                      COLHDG('DRILICENSE' 'Cmd')
     A*
     A          K LLWHSE
     A          K LLLCNS
     A          K LLADDUC
