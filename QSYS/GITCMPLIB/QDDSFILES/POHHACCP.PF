     A*----------------------------------------------------------------***
     A*   Copyright (C) 2006 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108                           *
     A*   Glen Ellyn, IL  60137                                          *
     A*   (630) 790-8383                                                 *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... POHHACCP (PF)
     A*  Description ... HACCP History Information
     A*  Creator ....... RAH
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Revisions
     A*
     A*    11/20/06  RAH  5.10
     A*      - Created
510b A*    01/08/07  RH   5.10b
     A*      - Add export date and time to key
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R HAHREC
     A*
     A            HAHWHSE   R               REFFLD(WHCODE)
     A            HAHPO     R               REFFLD(PONUM)
     A            HAHSEQ    R               REFFLD(SEQ#)
     A            HAHITEM   R               REFFLD(ITITEM)
     A            HAHEST#        8A         TEXT('USDA Establishment Number')
     A                                      COLHDG('USDA' ' Est.' 'Number')
     A            HAHTKSL       15A         TEXT('Truck Seal Number')
     A                                      COLHDG('Truck' 'Seal' 'Number')
     A            HAHPRDA        5A         TEXT('Product Area')
     A                                      COLHDG('Product' 'Area')
     A            HAHPRDT        5A         TEXT('Product Type')
     A                                      COLHDG('Product' 'Type')
     A*
     A            HAHRSRQ        5P 2       TEXT('Refer Setting - Required')
     A                                      COLHDG('Refer' 'Setting' 'Required')
     A            HAHRSDL        5P 2       TEXT('Refer Setting - Delivery')
     A                                      COLHDG('Refer' 'Setting' 'Delivery')
     A*
     A            HAHTTRR        5P 2       TEXT('Trailer Temp - Rear')
     A                                      COLHDG('Trailer' 'Temp' 'Rear')
     A            HAHTTMD        5P 2       TEXT('Trailer Temp - Middle')
     A                                      COLHDG('Trailer' 'Temp' 'Middle')
     A            HAHTTNS        5P 2       TEXT('Trailer Temp - Nose')
     A                                      COLHDG('Trailer' 'Temp' 'Nose')
     A*
     A            HAHPTRT        5P 2       TEXT('Pallet Temp - Rear Top')
     A                                      COLHDG('Pallet' 'Temp' 'Rear Top')
     A            HAHPTRM        5P 2       TEXT('Pallet Temp - Rear Middle')
     A                                      COLHDG('Pallet' 'Temp' 'Rear Mid')
     A            HAHPTRB        5P 2       TEXT('Pallet Temp - Rear Bottom')
     A                                      COLHDG('Pallet' 'Temp' 'Rear Btm')
     A*
     A            HAHPTMT        5P 2       TEXT('Pallet Temp - Middle Top')
     A                                      COLHDG('Pallet' 'Temp' 'Mid Top')
     A            HAHPTMM        5P 2       TEXT('Pallet Temp - Mid Middle')
     A                                      COLHDG('Pallet' 'Temp' 'Mid Mid')
     A            HAHPTMB        5P 2       TEXT('Pallet Temp - Middle Bottom')
     A                                      COLHDG('Pallet' 'Temp' 'Mid Btm')
     A*
     A            HAHPTNT        5P 2       TEXT('Pallet Temp - Nose Top')
     A                                      COLHDG('Pallet' 'Temp' 'Nose Top')
     A            HAHPTNM        5P 2       TEXT('Pallet Temp - Nose Middle')
     A                                      COLHDG('Pallet' 'Temp' 'Nose Mid')
     A            HAHPTNB        5P 2       TEXT('Pallet Temp - Nose Bottom')
     A                                      COLHDG('Pallet' 'Temp' 'Nose Btm')
     A            HAHLDCN        2A         TEXT('Load Condition')
     A                                      COLHDG('Load' 'Condition')
     A            HAHINSP       30A         TEXT('Inspected by Agency')
     A                                      COLHDG('Inspected' 'By Agency')
     A            HAHEDT    R               TEXT('Export Date')
     A                                      COLHDG('Export' 'Date')
     A                                      REFFLD(DTECMD)
     A            HAHETI    R               TEXT('Export Time')
     A                                      COLHDG('Export' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K HAHWHSE
     A          K HAHPO
     A          K HAHSEQ
     A          K HAHITEM
510b A          K HAHEDT
510b A          K HAHETI
