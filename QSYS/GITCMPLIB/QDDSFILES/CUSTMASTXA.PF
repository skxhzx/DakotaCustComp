     A*----------------------------------------------------------------
     A*   Copyright (C) 2018 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... CUSTMASTXA (PF)
     A*  Description ... CUSTMAST extra fields
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    10/19/18  DAS  7.20
     A*      - Created
     A*
720aAA*    11/22/18  DAS  7.20a
     A*      - Added Audit Exp Date
     A*
740 AA*    05/05/20  KDE  7.40
     A*      - Added Single Pallet Flag
     A*
740aAA*    05/22/20  KDE  7.40a
     A*      - Added Customer Audit text area for Why Customer is on Audit
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Notes
     A*
     A*   This file should only be touched using SQL so new fields
     A*   can be added without needing to recompile existing programs.
     A*
     A*   Ideally, RPG programs should only access/update this
     A*   file using program EXCUSTMAST.
     A*
     A*   SQL Insert and Update statements should work with
     A*   individual fields to prevent a level check error.
     A*
     A*   MRC programs should not be affected by new fields because
     A*   they use SQL.
     A*
     A*----------------------------------------------------------------

     A*----------------------------------------------------------------
     A* Field Notes
     A*
     A*   CMAAUDFLG - Audit Flag (Y,N)
     A*      Originally created for City Line because they wanted to
     A*      audit the orders for specific customers.
     A*   CMSPFLG - Single Pallet Flag (Y,N)
     A*      Originally created for Holt Paper.  They were not able to
     A*      send in the order import file
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R CMAREC
     A*
     A            CMAWHSE   R               REFFLD(WHCODE)
     A            CMACUST   R               REFFLD(CUSNUM)
     A            CMAAUDFLG      1          TEXT('Audit Flag')
     A                                      COLHDG('Audit' 'Flag')
720aAA            CMAAUDEXPD     8  0       TEXT('Audit Exp Date YYYYMMDD')
720aAA                                      COLHDG('Audit' 'Exp Date')
740 AA            CMSPFLG        1          TEXT('Single Pallet Flag')
740 AA                                      COLHDG('Single' 'Pallet')
740aAA            CMAUDR       500          TEXT('Customer Audit Reason')
740aAA                                      COLHDG('Cust' 'Audit' 'Reason')
740aAA                                      VARLEN(25)
     A*
     A          K CMAWHSE
     A          K CMACUST
