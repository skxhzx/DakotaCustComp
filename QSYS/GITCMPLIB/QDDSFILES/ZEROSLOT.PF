     A*----------------------------------------------------------------
     A*
     A*  File name ..... ZEROSLOT  (PF)
     A*  Description ... Slot file holding verify/zeros slot info.
     A*  Creator ....... JGD
     A*
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*  Codes
     A*
     A*    Status code (SLSTAT)
     A*
     A*      'A'  -  Active slot. Item occupies slot.
     A*      'I'  -  Inactive slot. Do not use slot.  .
     A*      'V'  -  Verified slot.  Ready to be put to use.
     A*      'Z'  -  Zero slot.  Slot quantity went to zero. Must be
     A*              verified before it can be reused.
     A*
     A*----------------------------------------------------------------
     A*
     A*  PIR slot notes:
     A*
     A*    PIR slots are floating slots and are never reserved for
     A*    a particular item number.  When the quantity of a PIR slot
     A*    goes to zero its status is changed to 'Z'.  The slot must
     A*    then be manually verified that it is empty before its status
     A*    is changed to 'V' and it can be reused.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R ZSLREC
     A*
     A            SLWHSE    R               REFFLD(WHCODE)
     A            SLWHDP    R               REFFLD(WDCODE)
     A*
     A            SLHAND    R               REFFLD(HSCODE)
     A            SLDISP        12          TEXT('Slot# Display')
     A                                      COLHDG('Slot#' 'Display')
     A            SLSTAT    R               REFFLD(SSCODE)
     A            SLSDEF    R               REFFLD(SLSDEF)
     A            SLPICK         1          TEXT('Pick Slot')
     A                                      COLHDG('Pick Slot')
     A            SLRSRV         1          TEXT('Reserved')
     A                                      COLHDG('Reserved')
     A*
     A          K SLWHSE
     A          K SLWHDP
     A          K SLDISP
