     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... IMPSPOD  (PF)
     A*  Description ... Staging Import - P.O. Detail
     A*
     A*  Revisions
     A*
640 AA*    02/12/11  DAS  6.40
     A*      - Created
     A*      - Based on IMPFPOD2 / IMPFOPDM
     A*      - Removed PIDCDT/PIDCTM fields. Instead, host is
     A*        responsible for writting records into this file in the
     A*        proper order, then the PIHADDTS field will keep them
     A*        in order.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Type (PIDTYP)
     A*
     A*      'A'  -  Add       Adds P.O. detail line.
     A*                        If detail exists (based on seq/item),
     A*                          treat as type 'C'.
     A*
     A*      'C'  -  Change    Changes P.O. detail line.
     A*                        If detail does not exist (based on
     A*                          seq/item), treat as type 'A'.
     A*                        If any quantity has been received for
     A*                          the detail line, the line will be
     A*                          ignored and nothing will be changed.
     A*
     A*      'D'  -  Delete    Deletes P.O. header and detail.
     A*                        If any quantity has been received for
     A*                          the detail line, the line will be
     A*                          ignored and the detail line will NOT
     A*                          be deleted.
     A*
     A*----------------------------------------------------------------
     A          R PIDREC
     A*
     A            PIDWHS         3S 0       TEXT('Warehouse Number')
     A                                      COLHDG('Warehouse' 'Number')
     A            PIDBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch' 'Id')
     A            PIDPO          9          TEXT('P.O. Number')
     A                                      COLHDG('P.O.' 'Number')
     A            PIDSEQ         5S 0       TEXT('Sequence Number')
     A                                      COLHDG('Sequence' 'Number')
     A            PIDITM        15          TEXT('Item')
     A                                      COLHDG('Item')
     A*
     A*  Import information
     A*
     A            PIDTYP         1          TEXT('Update type')
     A                                      COLHDG('Update' 'Type')
     A*
     A*    Status info
     A*
     A            PIDSTATUS      1A         TEXT('Imp Status')
     A                                      COLHDG('Imp Status')
     A            PIDSTSTS        Z         TEXT('Imp Status Timestamp')
     A                                      COLHDG('Imp Status Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIDSTSUC        Z         TEXT('Imp Status UC Timestamp')
     A                                      COLHDG('Imp Status UC Stamp')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIDTEXT      200          TEXT('Imp Text')
     A                                      COLHDG('Imp Text')
     A*
     A*    Fields being imported
     A*
     A            PIDVIT        15          TEXT('Vendor Item #')
     A                                      COLHDG('Vendor' 'Item Number')
     A            PIDSPC         2          TEXT('Special Order Code')
     A                                      COLHDG('Special' 'Order Code')
     A            PIDQO1         5S 0       TEXT('Normal Qty Ordered')
     A                                      COLHDG('Normal Qty' 'Ordered')
     A            PIDQO2         5S 0       TEXT('Breakdown 1 Qty Ordered')
     A                                      COLHDG('Breakdown 1' 'Qty Ordered')
     A            PIDQO3         5S 0       TEXT('Breakdown 2 Qty Ordered')
     A                                      COLHDG('Breakdown 2' 'Qty Ordered')
     A*
520aAA            PIDODS        40A         TEXT('Override Item Description')
520aAA                                      COLHDG('Override' 'Item' 'Descripti-
520aAA                                      on')
     A            PIDPOM        40A         TEXT('Item P/O Message')
     A                                      COLHDG('Item' 'P/O Message')
     A            PIDMS1        40A         TEXT('Host Misc Field 1')
     A                                      COLHDG('Host' 'Misc' 'Field 1')
     A            PIDMS2        40A         TEXT('Host Misc Field 2')
     A                                      COLHDG('Host' 'Misc' 'Field 2')
     A            PIDMS3        40A         TEXT('Host Misc Field 3')
     A                                      COLHDG('Host' 'Misc' 'Field 3')
     A            PIDMS4        40A         TEXT('Host Misc Field 4')
     A                                      COLHDG('Host' 'Misc' 'Field 4')
     A            PIDMS5        40A         TEXT('Host Misc Field 5')
     A                                      COLHDG('Host' 'Misc' 'Field 5')
     A*
     A*  Add stamps
     A*
     A            PIDADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            PIDADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A*
     A          K PIDWHS
     A          K PIDBATID
     A          K PIDPO
     A          K PIDADDUC
     A          K PIDSEQ
     A          K PIDITM
