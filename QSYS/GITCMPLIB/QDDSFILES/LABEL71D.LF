     A*----------------------------------------------------------------
     A*   Copyright (C) 2013 BFC Software, Inc. - All Rights Reserved
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LABEL71D (LF)
     A*  Description ... Label join file by transaction.
     A*                  Keyed by stop/drop rather than label stop.
     A*  Date .......... June 3, 2013
     A*  Creator ....... RBD
     A*
     A*  Revisions
     A*
650 AA*    06/03/13  RBD  6.50
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A                                      JDFTVAL
     A          R L71DREC                   JFILE(LABEL +
     A                                            PIRITEM ORDH ORDD +
     A                                            UNMESR POHDR PODTL +
     A                                            WAREHS WHDEPT RTESTP)
     A*
     A          J                           JOIN(LABEL   PIRITEM)
     A                                      JFLD(LBWHSE  ITWHSE)
     A                                      JFLD(LBITEM  ITITEM)
     A*
     A          J                           JOIN(LABEL   ORDH)
     A                                      JFLD(LBWHSE  OHWHSE)
     A                                      JFLD(LBORD   OHORD)
     A*
     A          J                           JOIN(LABEL   ORDD)
     A                                      JFLD(LBWHSE  ODWHSE)
     A                                      JFLD(LBORD   ODORD)
     A                                      JFLD(LBSEQ   ODSEQ)
     A                                      JFLD(LBITEM  ODITEM)
     A*
     A          J                           JOIN(LABEL   UNMESR)
     A                                      JFLD(LBUCOD  UMCODE)
     A*
     A          J                           JOIN(LABEL   POHDR)
     A                                      JFLD(LBWHSE  PHWHSE)
     A                                      JFLD(LBPO    PHPO)
     A*
     A          J                           JOIN(LABEL   PODTL)
     A                                      JFLD(LBWHSE  PDWHSE)
     A                                      JFLD(LBPO    PDPO)
     A                                      JFLD(LBSEQ   PDSEQ)
     A                                      JFLD(LBITEM  PDITEM)
     A*
     A          J                           JOIN(LABEL   WAREHS)
     A                                      JFLD(LBWHSE  WHCODE)
     A*
     A          J                           JOIN(LABEL   WHDEPT)
     A                                      JFLD(LBWHSE  WDWHSE)
     A                                      JFLD(LBWHDP  WDCODE)
     A*
     A          J                           JOIN(LABEL   RTESTP)
     A                                      JFLD(LBWHSE  RTSWHS)
     A                                      JFLD(LBRTE   RTSRID)
     A                                      JFLD(LBSTOP  RTSSTP)
     A*
     A*  Fields from LABEL file.
     A*
     A            LBWHSE
     A            LBWHDP
     A            LBSEQ
     A            LBITEM
     A            LBPBAT
     A            LBAISL
     A            LBLOC
     A            LBRLVL
     A            LBHAND
     A            LBPSEQ
     A            LBSTYP
     A            LBQRYF
     A            LBDISP
     A            LBQAVL
     A            LBWHS2
     A            LBWHD2
     A            LBASL2
     A            LBLOC2
     A            LBLVL2
     A            LBHND2
     A            LBDSP2
     A            LBRTE
     A            LBSTOP
     A            LBORD
     A            LBSPCL
     A            LBUTYP
     A            LBUCOD
     A            LBUCUB
     A            LBCUBE
     A            LBUWGT
     A            LBSWGT
     A            LBQALC
     A            LBQRMN
     A            LBQPCK
     A            LBTIE
     A            LBHIGH
     A            LBTYPE
     A            LBLBL#
     A            LBPAL#
     A            LBTRN#
     A            LBCTR#
     A            LBSTAT
     A            LBPRTY
     A            LBSDTE
     A            LBSTIM
     A            LBVRFY
     A*
     A*  Fields from PIRITEM file.
     A*
     A            ITDESC
     A            ITSTYP
     A            ITWHDP
     A            ITDESG
     A            ITACTV
     A            ITUPC
     A            ITBLD
     A            ITVEN#
     A            ITVIT#
     A            ITPDSC
     A            ITUM1
     A            ITUM2
     A            ITUMQ2
     A            ITUM3
     A            ITUMQ3
     A            ITULEN
     A            ITUWDT
     A            ITUHGT
     A            ITCUBE
     A            ITSWGT
     A            ITCWGT
     A*
     A*  Fields from ORDH file.
     A*
     A            OHTYPE
     A            OHCUST
     A            OHCPO
     A            OHCNAM
     A            OHCAD1
     A            OHCAD2
     A            OHCCTY
     A            OHCST
     A            OHCZIP
     A            OHRTE
     A            OHSTOP
     A            OHTRUK
     A            OHMISC
     A*
     A*  Fields from ORDD file.
     A*
     A            ODQOR1
     A            ODQOR2
     A            ODQOR3
     A            ODWH2
     A            ODOR2
     A            ODOITM
     A            ODSPCL
     A            ODQPK1
     A            ODQPK2
     A            ODQPK3
     A*
     A*  Fields from UNMESR file.
     A*
     A            UMDESC
     A            UMSHRT
     A*
     A*  Fields from POHDR file.
     A*
     A            PHVEN
     A            PHVNAM
     A            PHVAD1
     A            PHVAD2
     A            PHVCTY
     A            PHVST
     A            PHVZP1
     A            PHDATE
     A            PHTIME
     A            PHAMPM
     A*
     A*  Fields from PODTL file.
     A*
     A            PDVITM
     A            PDSPCL
     A            PDQOR1
     A            PDQOR2
     A            PDQOR3
     A            PDQRC1
     A            PDQRC2
     A            PDQRC3
     A*
     A*  Fields from WAREHS file.
     A*
     A            WHDESC
     A*
     A*  Fields from WHDEPT file.
     A*
     A            WDDESC
     A*
     A*  Fields from RTESTP file.
     A*
     A            RTSWHS
     A            RTSRID
     A            RTSSTP
     A            RTSST5
     A            RTSSD
     A*
     A*  Key fields
     A*
     A          K LBTRN#
     A          K LBTYPE                    DESCEND
     A          K LBWHDP
     A          K LBSTYP
     A          K LBAISL
     A          K LBPSEQ
     A          K LBRLVL
     A          K LBHAND
     A          K LBSTOP                    DESCEND
