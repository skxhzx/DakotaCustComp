     A*****************************************************************
     A* RU371A   View/Print Clear Flag (by Route#)                    *
     A* Jose Duque                                                    *
     A* November 9, 2004                                              *
     A*                                                               *
     A*----------------------------------------------------------------
     A*  Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A*  Client Custom Revisions
     A*
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R RUREC
     A*
     A            RUWHSE    R               REFFLD(WHCODE PIRDICT)
     A            RUTYPE    R               REFFLD(RUTYPE RUNNER)
     A            RUWHDP    R               REFFLD(LBWHDP LABEL)
     A            RULBL#    R     P         REFFLD(TRAN#)
     A            RUOTRN    R     P         REFFLD(TRAN#)
     A            RUTRN#    R     P         REFFLD(TRAN#)
     A            RUDISP    R               REFFLD(LBDISP LABEL)
     A            RUQYSO    R     P         REFFLD(LBQPCK LABEL)
     A            RUQYFD    R     P         REFFLD(LBQPCK LABEL)
     A            RUSTAT         1A         TEXT('Status')
     A            RUODTD         8A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUODTT         6A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUODTX        12A         TEXT('Open Status Date Stamp')
     A                                      COLHDG('Opn Status' 'Date Stamp')
     A            RUPDTS        26A         TEXT('Printed Status Date Stamp')
     A            RUCDTS        26A         TEXT('Closed Status Date Stamp')
     A*
     A*  Fields from PIRITEM file.
     A*
     A            ITITEM    R               REFFLD(ITITEM PIRITEM)
     A            ITDESC    R               REFFLD(ITDESC PIRITEM)
     A*
     A*  Fields from RTEHED file.
     A*
     A            RHRTE     R               REFFLD(RHRTE  RTEHED)
     A            RHEXPD    R               REFFLD(RHEXPD RTEHED)
     A            RHEXPT    R               REFFLD(RHEXPT RTEHED)
     A            RHSHPT    R               REFFLD(RHSHPT RTEHED)
     A*
     A*  Fields from PIRTRAN file.
     A*
     A            PTEMP#    R               REFFLD(PTEMP# PIRTRAN)
     A*
     A*  File key.
     A*
     A          K RHSHPT                    DESCEND
     A*
