     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... BATCTL   (PF)
     A*  Description ... Batch control file.
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
     A*    Ver 2.00  08/23/94  DAS
     A*      - Added Warehouse, Route and Order number.
     A*      - Changed status codes.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Status codes (BTSTAT)
     A*
     A*    For type PICK (Pick customer orders)
     A*
     A*       '1'  -  Allocating items from slots.
     A*       '2'  -  Building pallets.
     A*       '3'  -  Printing labels.
     A*       '9'  -  Finished.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R BTREC
     A*
     A            BTBAT#    R               TEXT('PIR Batch Number')
     A                                      COLHDG('PIR Batch' 'Number')
     A                                      REFFLD(BATCH#)
     A            BTWHSE    R               REFFLD(WHCODE)
     A            BTORD     R               REFFLD(OHORD)
     A            BTRTE     R               REFFLD(ROUTE)
     A            BTTYPE         8A         TEXT('Batch Type')
     A                                      COLHDG('Batch' 'Type')
     A            BTSTAT         1A         TEXT('Batch Status')
     A                                      COLHDG('Batch' 'Status')
     A            BTSTRD    R               TEXT('Start Date')
     A                                      COLHDG('Start' 'Date')
     A                                      REFFLD(DTECMD)
     A            BTSTRT    R               TEXT('Start Time')
     A                                      COLHDG('Start' 'Time')
     A                                      REFFLD(TIMHMS)
     A            BTSTRB        10          TEXT('Started By')
     A                                      COLHDG('Started' 'By')
     A            BTENDD    R               TEXT('End Date')
     A                                      COLHDG('End' 'Date')
     A                                      REFFLD(DTECMD)
     A            BTENDT    R               TEXT('End Time')
     A                                      COLHDG('End' 'Time')
     A                                      REFFLD(TIMHMS)
     A*
     A          K BTBAT#
