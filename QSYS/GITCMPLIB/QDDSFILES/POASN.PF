     A*----------------------------------------------------------------
     A*   Copyright (C) 2012 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*----------------------------------------------------------------
     A*
     A*  File name ..... POASN    (PF)
     A*  Description ... Purchase Order ASN file
     A*  Creator ....... LMC
     A*
     A*----------------------------------------------------------------
     A*  Revisions
     A*
640 AA*    08/01/12  LMC & RBD 6.40
     A*      - Created.  Originally written for BiRite.
     A*
740 AA*    07/07/20  KDE
     A*      - Added UOM
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R PAREC
     A*
     A            PAWHSE    R               REFFLD(WHCODE)
     A            PAPO          22A         TEXT('PO Number')
     A            PALPN         48A         TEXT('Pallet LPN Number')
     A            PAITEM        20A         TEXT('Buyer SKU')
     A            PASEQ          5S 0       TEXT('Line Number')
     A                                      COLHDG('Line' 'Number')
     A            PAUPC         20A         TEXT('UPC')
     A            PALOT         30A         TEXT('Lot Number')
     A            PAQTY         10S 0       TEXT('Quantity Shipped')
     A                                      COLHDG('Qty' 'Shipped')
     A            PAWGT         10S 3       TEXT('Net Weight')
     A                                      COLHDG('Net' 'Weight')
     A            PAMFGDT   R               TEXT('Manufactured Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PAMFGEXP  R               TEXT('MFG Expiration Date')
     A                                      COLHDG('MFG' 'Exp' 'Date')
     A                                      REFFLD(DTECMD PIRDICT)
     A            PAORIGIN      80A         TEXT('ORIGIN')
     A            PASUB          1A         TEXT('SUBSTITUTE')
     A            PALCNS        15A         TEXT('License Number')
     A            PAADDTS   R               REFFLD(ADDTS)
     A            PAADDFN       25A         TEXT('Add File Name')
     A            PACHGTS   R               REFFLD(ADDTS)
     A            PACHGFN       25A         TEXT('Change File Name')
     A            PAUOM          2A         TEXT('RECEIPT UOM')
     A*
     A          K PAWHSE
     A          K PAPO
     A          K PALPN
     A          K PAITEM
     A          K PASEQ
     A          K PALOT
     A          K PAMFGDT
     A          K PAMFGEXP
