     A*----------------------------------------------------------------***
     A*   Copyright (C) 2002 BFC Software, Inc. - All Rights Reserved    *
     A*   BFC Software, Inc.                                             *
     A*----------------------------------------------------------------***
     A*
     A*  File name ..... EQUIPA    (PF)
     A*  Description ... Equipment answer table
     A*  Creator ....... RH
     A*
     A*  Revisions
     A*
417 AA*    01/06/03  RH   4.17
     A*      - Created.
417aAA*    06/05/03  RH   4.17a
     A*      - Add equip ID, status, status date and time,
     A*        original question and existing work order #.
417bAA*    02/18/04  RH   4.17b
     A*      - Add status 3 repeat, has existing work order #
417cAA*   04/28/04  RH   4.17c
     A*     - Change EACODE length from 6 to 8.
     A*
     A*----------------------------------------------------------------***
     A* Notes:
     A*
     A*
     A*----------------------------------------------------------------***
     A* Field Notes:
     A*
417a A*   EASTAT   '1' - Open
417b A*            '3' - Repeat.
417a A*            '4' - Closed.
     A*
     A*   EAEXCP   '0' - Normal answer entered.
     A*            '1' - Exception answer entered.
417a A*   EAWRK#   '0' - No work order existed, new work order created
417a A*            '1' - If not 0, contains the existing work order #
417a A*                  no new work order was created.
     A*
     A*----------------------------------------------------------------***
     A                                      REF(*LIBL/PIRDICT)
     A          R EAREC
     A*
     A            EAWHSE    R               REFFLD(WHCODE PIRDICT)
     A            EATRN#    R               TEXT('Equip Tran #')
     A                                      COLHDG('Equip' 'Tran #')
     A                                      REFFLD(TRAN# PIRDICT)
     A            EAIO           1          TEXT('In/out')
     A                                      COLHDG('Check' 'In/out')
     A            EASEQ#    R               TEXT('Question Seq#')
     A                                      COLHDG('Question' 'Seq#')
     A                                      REFFLD(SEQ# PIRDICT)
     A            EAANSR         1          TEXT('Answer')
     A                                      COLHDG('Answer')
     A            EAEXCP         1          TEXT('Exception')
     A                                      COLHDG('Exception')
417a A            EAQSTN        30          TEXT('Question')
417a A                                      COLHDG('Question')
417a A            EASTAT         1          TEXT('Status')
417a A                                      COLHDG('Status')
417a A            EASDTS          Z         TEXT('Status Date Stamp')
417a A                                      COLHDG('Status' 'Date Stamp')
417a A            EACODE    R               REFFLD(EQCODE PIRDICTEPM)
417a A            EAWRK#    R               TEXT('Existing Work Order')
417a A                                      COLHDG('Existing' 'Work Order')
417a A                                      REFFLD(TRAN# PIRDICT)
     A*
