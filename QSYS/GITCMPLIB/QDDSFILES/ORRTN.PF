     A*----------------------------------------------------------------
     A*   Copyright (C) 1994 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   799 Roosevelt Rd.  Bldg 6, Suite 108
     A*   Glen Ellyn, IL  60137
     A*   (708) 790-8383
     A*----------------------------------------------------------------
     A*
     A*  File name ..... ORRTN (PF)
     A*  Description ... Customer returns file
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
213 AA*    Ver 2.01  05/16/96  DAS
213 AA*      - Added fields ORCWTR, ORTYPE, ORTRN#, ORCITM, ORSLOT.
213 AA*      - Removed UNIQUE keyword.
     A*
416 AA*    Ver 4.16  10/12/01  MLB
     A*      - Added field ORPBAT
416aAA*    Ver 4.16a 10/19/01  RH
     A*      - Added fields ORDCOD, ORIINV, OROLB#, ORINV#
416bAA*    Ver 4.16b 07/03/02  RH
     A*      - Added fields OROOD and ORWHOL
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes
     A*
     A*    The original transaction number (ORTRN#) ties a return
     A*    back to a specific employee when EPM is being used.
     A*
     A*    The corresponding item number (ORCITM) was added to help
     A*    track which item was actually mispicked.
     A*
416 AA*    The System batch number assigned to the transaction(s) is saved
416 AA*    with the return record so that any changes to the quantity
416 AA*    returned during transaction close can be updated in the
416 AA*    return record.
     A*
416 AA*    A new Return type was added, type U indicating that a user
416 AA*    changed the quanity originally entered by the user.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Return types (ORTYPE)
     A*
     A*       ' '  -  Normal
     A*       'P'  -  POS
     A*       'T'  -  Transfer
416 AA*       'U'  -  Return qty updated
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
213 DA*                                     UNIQUE
     A          R ORREC
     A*
     A            ORWHSE    R               REFFLD(WHCODE)
     A            ORORD     R               REFFLD(OHORD)
     A            ORITEM    R               REFFLD(ITITEM)
     A            ORQTY1         5P 0       TEXT('Normal Qty Returned')
     A                                      COLHDG('Normal Qty' 'Returned')
     A            ORQTY2         5P 0       TEXT('Brk1 Qty Returned')
     A                                      COLHDG('Brk1 Qty' 'Returned')
     A            ORQTY3         5P 0       TEXT('Brk2 Qty Returned')
     A                                      COLHDG('Brk2 Qty' 'Returned')
213 AA            ORCWTR         9P 2       TEXT('Catchweight Returned')
213 AA                                      COLHDG('Catchweight' 'Returned')
     A            ORRDTE    R               TEXT('Return Date (YYYYMMDD)')
     A                                      COLHDG('Return' 'Date')
     A                                      REFFLD(DTECMD)
     A            ORRTIM    R               TEXT('Return Time')
     A                                      COLHDG('Receive' 'Time')
     A                                      REFFLD(TIMHMS)
     A            ORRUSR        10          TEXT('Returned By')
     A                                      COLHDG('Returned' 'By')
213 AA            ORTYPE         1          TEXT('Return Type')
213 AA                                      COLHDG('Return' 'Type')
416 AA            ORPBAT    R     P         TEXT('Batch Number')
416 AA                                      COLHDG('Batch' 'Number')
416 AA                                      REFFLD(BATCH#)
213 AA            ORTRN#    R               TEXT('Original Tran#')
213 AA                                      COLHDG('Original' 'Tran#')
213 AA                                      REFFLD(TRAN#)
213 AA            ORCITM    R               TEXT('Corresponding Item')
213 AA                                      COLHDG('Corresponding' 'Item')
213 AA                                      REFFLD(ITITEM)
     A            ORRCOD    R               REFFLD(RNCODE)
416aAA            ORDCOD    R               REFFLD(RDCODE)
416aAA            ORIINV         1          TEXT('Impact Inventory')
416aAA                                      COLHDG('Impact' 'Inv.')
416aAA            OROLB#    R               TEXT('Original Label')
416aAA                                      COLHDG('Orig.' 'Label#' )
416aAA                                      REFFLD(TRAN#)
416aAA            ORINV#        12A         TEXT('Orig. Invoice#')
416aAA                                      COLHDG('Orig.' 'Invoice#')
     A            ORRMEM        30          TEXT('Memo')
     A                                      COLHDG('Memo')
213 AA            ORSLOT        12          TEXT('Manual Slot')
213 AA                                      COLHDG('Manual' 'Slot')
     A            ORUDTE    R               TEXT('Upload Date')
     A                                      COLHDG('Upload' 'Date')
     A                                      REFFLD(DTECMD)
     A            ORUTIM    R               TEXT('Upload Time')
     A                                      COLHDG('Upload' 'Time')
     A                                      REFFLD(TIMHMS)
416b A            OROORD    R               TEXT('Original Order')
416b A                                      COLHDG('Original' 'Order')
416b A                                      REFFLD(OHORD)
416b A            ORWHOL         1          TEXT('Whole order Return')
416b A                                      COLHDG('Whole' 'Ord.'+
416b A                                            'Return')
     A*
     A          K ORWHSE
     A          K ORORD
     A          K ORITEM
     A          K ORRDTE
     A          K ORRTIM
