     A*----------------------------------------------------------------
     A*   Copyright (C) 2024 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... COMPLIC    (PF)
     A*  Description ... Compare - Acitive Licenses
     A*  Creator ....... DAS
     A*
     A*  Revisions
     A*
780 AI*    02/24/24  DAS  7.80
     I*      - Created
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*   - This file was created to do snapshot comparisons of the
     A*     LICACTIVE
     A*
     A*----------------------------------------------------------------
     A*
     A*  Level Flags:
     A*
     A*   CPLLEVEL  - Level
     A*
     A*     'L' - License
     A*     'H' - Host Lot
     A*     'S' - Slot Item
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R CPLREC
     A*
     A            CPLLEVEL       1A         TEXT('Level')
     A                                      COLHDG('Level')
     A            CPLWHSE   R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            CPLBATID      11          TEXT('Batch Id')
     A                                      COLHDG('Batch Id')
     A*
     A* LICACTIVE fields
     A*
     A            CPLITEM   R               REFFLD(ITITEM)
     A            CPLITYPE       1A         TEXT('Item Type')
     A                                      COLHDG('Item' 'Type')
     A            CPLLCNS       15A         TEXT('License Number')
     A                                      COLHDG('License Number')
     A            CPLOLCNS      15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License Number')
     A            CPLHSTLOT     30A         TEXT('Host Lot')
     A                                      COLHDG('Host Lot')
     A*
     A            CPLAQTY1       5P 0       TEXT('Active Qty1')
     A                                      COLHDG('Active' 'Qty1')
     A            CPLAQTY2       5P 0       TEXT('Active Qty2')
     A                                      COLHDG('Active' 'Qty2')
     A            CPLAQTY3       5P 0       TEXT('Active Qty3')
     A                                      COLHDG('Active' 'Qty3')
     A*
     A            CPLCQTY1       5P 0       TEXT('Converted Qty1')
     A                                      COLHDG('Converted' 'Qty1')
     A            CPLCQTY2       5P 0       TEXT('Converted Qty2')
     A                                      COLHDG('Converted' 'Qty2')
     A            CPLCQTY3       5P 0       TEXT('Converted Qty3')
     A                                      COLHDG('Converted' 'Qty3')
     A*
     A            CPLWHDP   R               TEXT('Department')
     A                                      COLHDG('Department')
     A                                      REFFLD(WDCODE)
     A            CPLDISP       12A         TEXT('Display')
     A                                      COLHDG('Display')
     A*
     A* Timestamp / User tracking information.
     A*
     A            CPLCHGTS  R               REFFLD(CHGTS)
     A            CPLCHGUC  R               REFFLD(CHGUC)
     A*
     A*  Add stamps
     A*
     A            CPLADDTS        Z         TEXT('Add Timestamp')
     A                                      COLHDG('Added')
     A                                      DFT('0001-01-01-00.00.00')
     A            CPLADDUC        Z         TEXT('Add UC Timestamp')
     A                                      COLHDG('Added UC')
     A                                      DFT('0001-01-01-00.00.00')
     A            CPLADDPGM R               REFFLD(ADDPGM)
     A            CPLADDUSR R               REFFLD(ADDUSR)
     A            CPLADDJOB R               REFFLD(ADDJOB)
     A            CPLADDNBR R               REFFLD(ADDNBR)
     A*
     A* Key
     A*
     A          K CPLWHSE
     A          K CPLBATID
     A          K CPLLEVEL
     A          K CPLITEM
     A          K CPLLCNS
     A          K CPLHSTLOT
