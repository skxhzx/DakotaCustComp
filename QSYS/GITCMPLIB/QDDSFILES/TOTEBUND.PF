     A*----------------------------------------------------------------
     A*   Copyright (C) 2010 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... TOTEBUND (PF)
     A*  Description ... Tote and Bundle Count file
     A*  Creator ....... RBD
     A*
     A*----------------------------------------------------------------
     A*  Revisions:
     A*
530 AA*    04/01/10  RBD  5.30
     A*      - Created.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    Status codes
     A*
     A*       '1'  -  Open
     A*       '4'  -  Closed  (Completed)
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A          R TBREC
     A*
     A            TBWHSE    R               REFFLD(WHCODE)
     A            TBRTID    R               REFFLD(ROUTE)
     A                                      TEXT('Route ID')
     A                                      COLHDG('Route' 'Id')
     A            TBRTE     R               REFFLD(ROUTE)
     A            TBCOMP    R               REFFLD(TPCODE)
     A            TBZONG    R               REFFLD(ZHZONE)
     A                                      TEXT('Zone Group')
     A                                      COLHDG('Zone' 'Group')
     A            TBSZON    R               REFFLD(ZHZONE)
     A                                      TEXT('Slot Zone')
     A                                      COLHDG('Slot' 'Zone')
     A            TBSTOP    R               REFFLD(STOP)
     A                                      TEXT('Stop')
     A            TBMPL#    R               TEXT('Merge Pallet Number')
     A                                      COLHDG('Merge Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            TBPAL#    R               TEXT('Pallet Number')
     A                                      COLHDG('Pallet' 'Number')
     A                                      REFFLD(TRAN#)
     A            TBTRN#    R               TEXT('Transaction Number')
     A                                      COLHDG('Transaction' 'Number')
     A                                      REFFLD(TRAN#)
     A            TBORD     R               REFFLD(OHORD)
     A            TBQTOT    R               TEXT('Tote Count')
     A                                      COLHDG('Tote' 'Count')
     A                                      REFFLD(SEQ#)
     A            TBQBUN    R               TEXT('Bundle Count')
     A                                      COLHDG('Bundle' 'Count')
     A                                      REFFLD(SEQ#)
     A            TBSTAT         1          TEXT('Status')
     A                                      COLHDG('Status')
     A            TBGDTE    R               TEXT('Generated Date')
     A                                      COLHDG('Generated' 'Date')
     A                                      REFFLD(DTECMD)
     A            TBGTIM    R               TEXT('Generated Time')
     A                                      COLHDG('Generated' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TBSTSD    R               TEXT('Status Date')
     A                                      COLHDG('Status' 'Date')
     A                                      REFFLD(DTECMD)
     A            TBSTST    R               TEXT('Status Time')
     A                                      COLHDG('Status' 'Time')
     A                                      REFFLD(TIMHMS)
     A            TBUSER        10          TEXT('AS/400 User Id')
     A                                      COLHDG('AS/400' 'User Id')
     A*
     A          K TBWHSE
     A          K TBRTID
     A          K TBMPL#
     A          K TBSZON
