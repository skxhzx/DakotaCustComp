     A*----------------------------------------------------------------
     A*   Copyright (C) 2016 BFC Associates - All Rights Reserved
     A*   BFC Associates
     A*   245 W.Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... PBOMVERS  (PF)
     A*  Description ... PRODUCTION DYNAMIC BOM FILE
     A*  Creator ....... TAB
     A*
     A*
     A*  Revisions
     A*
     A*      - KDE - 11/28/2016
     A*        Created
710 AA*      - KDE - 02/02/2017
     A*           Added field to identify raw material(s) that are used for production
     A*           of Finished Good Product
     A*
     A*----------------------------------------------------------------
     A*
     A*    Dynamic Bill of Materials can use Dakota normal quantities
     A*    PXQTYN, PXQTY1, and PXQTY2 relate Case Qty, ITUMQ1, ITUMQ2
     A*    The User can also chose to use the Production Breakdown
     A*    Quantity.  Field PXPRDQ is the quantity of this breakdown
     A*    that this finished good requires.  The Substitute BOM
     A*    also has a flag PXPQUF which is populated with a "Y"
     A*    if the SBPRDQ is used and a "N" if the normal Dakota
     A*    quantities are used.
     A*
     A*    PXQTYN - Defines how many normal case quantity req
     A*    PXQTY1 - Defines how many Dakota BD1 quantity req
     A*    PXQTY2 - Defines how many Dakota BD2 quantity req
     A*    PXPRDQ - Defines how many Prod Qty BD req
     A*    PXPQUF - "Y" means Prod Qty used, "N" Dakota Quantities
     A*
     A*    The item definitions for the Production specific
     A*    definitions can be found in file ITEMPRD
     A*----------------------------------------------------------------
     A                                      UNIQUE
     A          R PXREC
     A*
     A            PXWHSE         3P 0       TEXT('Warehouse')
     A                                      COLHDG('Warehouse')
     A            PXFGI         15          TEXT('Finished Good Item')
     A                                      COLHDG('FG Item#')
     A            PXASEQ         6P 0       TEXT('Alternate Seq. Number')
     A                                      COLHDG('Alt. Seq. Number')
     A            PXSEQ          4P 0       TEXT('Bill Seq Number')
     A                                      COLHDG('Bill Seq Number')
     A            PXRMI         15          TEXT('Raw Material Item')
     A                                      COLHDG('RM Item#')
     A            PXALTN        45          TEXT('Alternate Name')
     A                                      COLHDG('Alt. Name')
     A            PXQTYN         5P 0       TEXT('Qty Normal')
     A                                      COLHDG('RM Qty Nrm')
     A            PXQTY1         3P 0       TEXT('Qty BrkDwn 1')
     A                                      COLHDG('RM Qty BrkDwn1')
     A            PXQTY2         3P 0       TEXT('Qty BrkDwn 2')
     A                                      COLHDG('RM Qty BrkDwn2')
     A            PXPRDQ         9P 4       TEXT('PRD Qty Breakdown')
     A                                      COLHDG('PRD BrkDwn')
     A            PXSCRF         5P 4       TEXT('Raw Material Scrap')
     A                                      COLHDG('RM SCRP Factor')
     A            PXTYPE         1          TEXT('Raw Material Issue Type')
     A                                      COLHDG('RM Type Issue')
     A            PXBCKF         1          TEXT('Raw Material Back Flush')
     A                                      COLHDG('RM Back Flush')
     A            PXPQUF         1          TEXT('Prod Qty Use Flag')
     A                                      COLHDG('PRD Qty Use Flag')
     A            PXEFFD         8  0       TEXT('Effective Date')
     A                                      COLHDG('Eff. Date')
     A            PXEXPD         8  0       TEXT('Expiration Date')
     A                                      COLHDG('Exp. Date')
710 AA            PXUSEP         1          TEXT('Used in FG Item')
710 AA                                      COLHDG('Used in FG ITM')
     A*
     A          K PXWHSE
     A          K PXFGI
     A          K PXASEQ
     A          K PXALTN
     A          K PXSEQ
     A          K PXRMI
