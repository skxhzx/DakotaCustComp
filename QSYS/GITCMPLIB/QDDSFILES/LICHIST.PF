     A*----------------------------------------------------------------
     A*   Copyright (C) 2004 BFC Software, Inc. - All Rights Reserved
     A*   BFC Software, Inc.
     A*   245 W. Roosevelt Rd.  Bldg 8, Suite 51
     A*   West Chicago, IL  60185
     A*   (630) 562-0375
     A*----------------------------------------------------------------
     A*
     A*  File name ..... LICHIST  (PF)
     A*  Description ... License History file
     A*  Creator ....... MLB
     A*
     A*----------------------------------------------------------------***
     A*  Revisions
417 AI*    01/07/03  MLB  4.17
     I*      - Created.
417aAI*    08/11/03  MLB  4.17a
     I*      - Added two new fields, LRFSLP - From slot position and
     A*        LRTSLP - To slot position.
417bAI*    11/26/03  MLB  4.17b
     I*      - Enh: Addo new fields, LRFSLP - From slot position and
     A*
417cAI*    03/25/04  DAS  4.17c
     I*      - Enh: Added fields LRNODE,LRFDSP,LRTDISP,LRFITM,LRTITM.
     A*
417dAI*    04/06/04  DAS  4.17d
     I*      - Enh: Added fields LRAPGM,LRCPGM.
     A*
417eAI*    05/06/04  DAS  4.17e
     I*      - Enh: Revised Add/Change field names and change to
     I*        reference PIRDICT fields.
     A*
417fAI*    07/22/04  DAS  4.17f
     I*      - Enh: Fixed Notes so they fit on an 80-char screen.
     I*      - Enh: Added field LRFVFY.
     I*      - Enh: Renamed LRFMLC to LRFLIC, LRORLC to LROLIC,
     I*        LRFMCD to LRFCOD, LRTOCD to LRTCOD.
     A*
500 AI*    10/17/04  DAS  5.00
     I*      - Enh: Increase LRNODE to 50 characters, from 20.
     A*
500aAI*    01/13/05  DAS  5.00a
     I*      - Enh: Added fields LRFBASE, LRTBASE, LRFITMS, LRTITMS,
     I*        LRFITYP, LRTITYP.
     I*      - Enh: Renamed LRACFL to LRSTAT.
     A*
500bAI*    01/20/05  DAS  5.00b
     I*      - Enh: Added fields LRCTL# and LRCTLSEQ#.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Notes:
     A*
     A*   - License transaction number (LRLTRN) is used just as a
     A*     sequence number for the file.
     A*
500bAA*   - Control number (LRCTL#) and sequence number (LRCTLSEQ#) are
500bAA*     used to group multiple license plates together that belong
500bAA*     to the same slot.
     A*
     A*   - License number (LRLCNS) is the visible license number on
     A*     an item.
     A*
     A*   - From license number (LRFMLC). In the case of of split,
     A*     this would identify the license plate the split came from.
     A*
     A*   - Original license number (LRORLC). This differs from the
     A*     "From" license number in that it should contain the license
     A*     number that is associated with the original information for
     A*     the item not just the license number that was split. The
     A*     first time a record is created for a license number, the
     A*     the original license number will be the same as the license
     A*     number (LRLCNS). After that, the original license number
     A*     number never changes.
     A*
     A*   - From area (LRFARA) is used in conjunction with the "From"
     A*     Code (LRFMCD) values A thru D.
     A*     The From slot fields(LRFWHD,LRFASL,LRFLOC,LRFLVL,LRFHND)
     A*     are used when LRFMCD is 'S'lot.
     A*
     A*   - To area (LRTARA) is used in conjunction with the "To" Code
     A*     (LRTOCD) values A thru D.
     A*     The To slot fields(LRTWHD,LRTASL,LRTLOC,LRTLVL,LRTHND) are
     A*     used when LRTOCD is 'S'lot or 'F'loor.
     A*
     A*   - To order number (LRTORD) is used when To Code (LRTOCD) is
     A*     'P'icked. This identifies the customer that ordered the
     A*     item.
     A*
417fAA*   - LRFVFY identifies if the "From" license was verified (Y)
417fAA*     by the user.
     A*
500aAA*   - Item numbers should always be "Slot" item numbers. The
     A*     only exception is the From item of a Receiving code. In
     A*     that case the item should be the actual item received. The
     A*     corresponding To item for the Dock code will be the "Slot"
     A*     item.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Codes
     A*
     A*    From Code  (LRFMCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'I'  - Inventory.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
     A*    To Code  (LRTOCD)
     A*
     A*       'A'  - Area.
     A*       'D'  - Dock.
     A*       'F'  - Floor.
     A*       'P'  - Picked.
     A*       'R'  - Receiving.
     A*       'S'  - Slot.
     A*       '?'  - Unknown.
     A*
     A*----------------------------------------------------------------
     A*
     A*  Special License Numbers
     A*
     A*    Starting
     A*    Characters
     A*
     A*      '*SU'  -  Start Up
     A*                These license plates are used when a client
     A*                starts using license plate tracking after using
     A*                Dakota FS-WMS for a while. A start up program
     A*                will assign an '*SU' license plate to slots
     A*                that contain an item to be tracked.
     A*
     A*      '*NF'  -  Not Found
     A*                These license plates are created on-the-fly when
     A*                a license plate does not exist for a slot/item.
     A*
     A*      '*ML'  -  Missing License
     A*                These license plates are created when a user is
     A*                asked to scan a particular license plate when
     A*                taking an item out of a slot, but the user
     A*                cannot find the license plate to scan. The user
     A*                would press F10 to take an exception and let
     A*                the system know the requested license plate is
     A*                missing.
     A*
     A*      '*NA'  -  Not Applied
     A*                These license plates are created when a user
     A*                is asked to apply a new license plate to a
     A*                partial quantity being put into a slot, but the
     A*                user doesn't do it and presses F10 instead.
     A*
     A*      '*NT'  -  Not Tracked
     A*                These license plates are created for items in
     A*                which the License Tracking option is set to No.
     A*
     A*      '*PS'  -  Pick Slot
     A*                These license plates are created for items in
     A*                which a partial quantity was put into the Pick
     A*                slot and either we are in Locator mode or we
     A*                do not ask selectors to scan a license plate.
     A*
     A*----------------------------------------------------------------***
     A*
     A*  Threads (Nodes)
     A*
     A*  Threads provide a simple way to track a license from receiving
     A*  to its current/ending location. The thread field (LRNODE) is
500 MA*  50 characters long, alphanumeric.
     A*
     A*  When a license is created, the thread will be set to '1'. Any
     A*  action performed on that license (ie. putaway, tfr, adj ...)
     A*  will add another character to the field, up to the maximum of
500 MA*  50 characters. This would mean that a license can be touched
500 MA*  up to 50 times.
     A*
     A*  Example:
     A*
     A*  - When license 123 is recieved, its thread is set to '1'.
     A*  - When it is put away, the thread is set to '11' for the new
     A*    active record. If the item was split into a second slot,
     A*    the second active record would have '12'. Note: we are
     A*    making the assumption that a license will not be split more
     A*    than 9 times in a single action.
     A*  - If those slots are adjusted, the new active records would
     A*    have threads of '111' and '121'.
     A*  - When the slots are used for replenishment, the new active
     A*    records would have threads of '1111' and '1211'.
     A*  - Starting with the original license of 123, it is easy to
     A*    trace what happened to the item. Part of the received qty
     A*    would follow this thread: 1,11,111,1111. The other part
     A*    would follow thread: 1,12,121,1211.
     A*
     A*----------------------------------------------------------------
     A                                      REF(*LIBL/PIRDICT)
     A                                      UNIQUE
     A          R LRREC
     A*
     A            LRWHSE    R               REFFLD(WHCODE)
     A                                      COLHDG('Warehouse')
     A            LRLTRN    R               TEXT('Lic Tran Number')
     A                                      COLHDG('Lic Tran' 'Number')
     A                                      REFFLD(TRAN#)
500bAA            LRCTL#         7P 0       TEXT('Control Number')
500bAA                                      COLHDG('Control#')
500bAA            LRCTLSEQ#      3P 0       TEXT('Control Seq#')
500bAA                                      COLHDG('Seq#')
500aDA**          LRACFL         2A         TEXT('Active Flag')
500aDA**                                    COLHDG('Active' 'Flag')
500aMA            LRSTAT         2A         TEXT('Status')
500aMA                                      COLHDG('Status')
500 DA**          LRNODE        20A         TEXT('Thread')
500 MA            LRNODE        50A         TEXT('Thread')
417cAA                                      COLHDG('Thread')
     A            LRTRN#    R               TEXT('Employee Transaction Number')
     A                                      COLHDG('Employee' 'Transaction' -
     A                                      'Number')
     A                                      REFFLD(TRAN#)
     A            LRLBL#    R               TEXT('Label Number')
     A                                      COLHDG('Label' 'Number')
     A                                      REFFLD(TRAN#)
     A            LRLCNS        15A         TEXT('License Number')
     A                                      COLHDG('License' 'Number')
417fMA            LRFLIC        15A         TEXT('"From" License Number')
     A                                      COLHDG('"From"' 'License' 'Number')
417fAA            LRFVFY         1A         TEXT('"From" Lic Verified')
417fAA                                      COLHDG('"From" Lic' 'Verified')
417fMA            LROLIC        15A         TEXT('Original License Number')
     A                                      COLHDG('Original' 'License' 'Number-
     A                                      ')
     A            LRLQY1         7P 0       TEXT('Location Qty1')
     A                                      COLHDG('Location' 'Qty1')
     A            LRLQY2         7P 0       TEXT('Location Qty2')
     A                                      COLHDG('Location' 'Qty2')
     A            LRLQY3         7P 0       TEXT('Location Qty3')
     A                                      COLHDG('Location' 'Qty3')
     A            LRAQY1         7P 0       TEXT('Adjustment Qty1')
     A                                      COLHDG('Adjustment' 'Qty1')
     A            LRAQY2         7P 0       TEXT('Adjustment Qty2')
     A                                      COLHDG('Adjustment' 'Qty2')
     A            LRAQY3         7P 0       TEXT('Adjustment Qty3')
     A                                      COLHDG('Adjustment' 'Qty3')
     A*
     A* "From" Location information
     A*
417fMA            LRFCOD         1A         TEXT('From Code')
     A                                      COLHDG('From' 'Code')
     A            LRFARA        15A         TEXT('From Area')
     A                                      COLHDG('From' 'Area')
     A            LRFWHD    R               TEXT('From Department')
     A                                      COLHDG('From' 'Department')
     A                                      REFFLD(WDCODE)
417cAA            LRFDSP        12A         TEXT('From Display')
417cAA                                      COLHDG('From' 'Display')
     A            LRFASL    R               TEXT('From Aisle')
     A                                      COLHDG('From' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            LRFLOC    R               TEXT('From Location')
     A                                      COLHDG('From' 'Location')
     A                                      REFFLD(SLLOC)
     A            LRFLVL    R               TEXT('From Rack Level')
     A                                      COLHDG('From' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LRFHND    R               TEXT('From Handstack')
     A                                      COLHDG('From' 'Handstack')
     A                                      REFFLD(HSCODE)
500aAA            LRFBASE       12A         TEXT('From Base Slot')
500aAA                                      COLHDG('From' 'Base Slot')
417aAA            LRFSLP         3P 0       TEXT('From Pallet Position')
417aAA                                      COLHDG('From' 'Pallet' 'Position')
417cAA            LRFITM    R               TEXT('From Item')
417cAA                                      COLHDG('From' 'Item')
417cAA                                      REFFLD(ITITEM)
500aAA            LRFITYP        1A         TEXT('From Item Type')
500aAA                                      COLHDG('From' 'Item Type')
500aAA            LRFITMS   R               TEXT('From Slot Item')
500aAA                                      COLHDG('From' 'Slot Item')
500aAA                                      REFFLD(ITITEM)
     A*
     A* "To" Location information
     A*
417fMA            LRTCOD         1A         TEXT('To Code')
     A                                      COLHDG('To' 'Code')
     A            LRTARA        15A         TEXT('To Area')
     A                                      COLHDG('To' 'Area')
     A            LRTWHD    R               TEXT('To Department')
     A                                      COLHDG('To' 'Department')
     A                                      REFFLD(WDCODE)
417cAA            LRTDSP        12A         TEXT('To Display')
417cAA                                      COLHDG('To' 'Display')
     A            LRTASL    R               TEXT('To Aisle')
     A                                      COLHDG('To' 'Aisle')
     A                                      REFFLD(SLAISL)
     A            LRTLOC    R               TEXT('To Location')
     A                                      COLHDG('To' 'Location')
     A                                      REFFLD(SLLOC)
     A            LRTLVL    R               TEXT('To Rack Level')
     A                                      COLHDG('To' 'Level')
     A                                      REFFLD(SLRLVL)
     A            LRTHND    R               TEXT('To Handstack')
     A                                      COLHDG('To' 'Handstack')
     A                                      REFFLD(HSCODE)
500aAA            LRTBASE       12A         TEXT('To Base Slot')
500aAA                                      COLHDG('To' 'Base Slot')
417aAA            LRTSLP         3P 0       TEXT('To Pallet Position')
417aAA                                      COLHDG('To' 'Pallet' 'Position')
417cAA            LRTITM    R               TEXT('To Item')
417cAA                                      COLHDG('To' 'Item')
417cAA                                      REFFLD(ITITEM)
500aAA            LRTITYP        1A         TEXT('To Item Type')
500aAA                                      COLHDG('To' 'Item Type')
500aAA            LRTITMS   R               TEXT('To Slot Item')
500aAA                                      COLHDG('To' 'Slot Item')
500aAA                                      REFFLD(ITITEM)
     A*
     A* To Customer information
     A*
     A            LRTORD    R     P         TEXT('To Order Number')
     A                                      COLHDG('To' 'Order' 'Number')
     A                                      REFFLD(OHORD)
     A            LRTSEQ    R     P         TEXT('To Order Sequence Number')
     A                                      COLHDG('To Order' 'Sequence Nbr')
     A                                      REFFLD(SEQ#)
     A*
     A            LREXCD         8A         TEXT('Exception Code')
     A                                      COLHDG('Exception' 'Code')
     A*
     A* Timestamp / User tracking information.
     A*
417eMA            LRADDTS   R               REFFLD(ADDTS)
417eMA            LRADDUC   R               REFFLD(ADDUC)
417eMA            LRADDPGM  R               REFFLD(ADDPGM)
417eMA            LRADDEMP  R               REFFLD(ADDEMP)
417eMA            LRADDUSR  R               REFFLD(ADDUSR)
417eMA            LRADDJOB  R               REFFLD(ADDJOB)
417eMA            LRADDNBR  R               REFFLD(ADDNBR)
     A*
417eMA            LRCHGTS   R               REFFLD(CHGTS)
417eMA            LRCHGUC   R               REFFLD(CHGUC)
417eMA            LRCHGPGM  R               REFFLD(CHGPGM)
417eMA            LRCHGEMP  R               REFFLD(CHGEMP)
417eMA            LRCHGUSR  R               REFFLD(CHGUSR)
417eMA            LRCHGJOB  R               REFFLD(CHGJOB)
417eMA            LRCHGNBR  R               REFFLD(CHGNBR)
     A*
     A          K LRWHSE
     A          K LRLCNS
     A          K LRLTRN
